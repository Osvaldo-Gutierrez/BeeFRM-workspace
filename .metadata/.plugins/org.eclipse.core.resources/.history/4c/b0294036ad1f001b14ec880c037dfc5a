options {
//BUILD_PARSER           = false;
//BUILD_TOKEN_MANAGER    = false;
//CACHE_TOKENS           = true;
//CHOICE_AMBIGUITY_CHECK = 3;
//COMMON_TOKEN_ACTION    = true;

//DEBUG_LOOKAHEAD        = true;
//DEBUG_PARSER           = true;
//DEBUG_TOKEN_MANAGER    = true;

//ERROR_REPORTING        = false;
  FORCE_LA_CHECK         = true;
//IGNORE_CASE            = true;
//JAVA_UNICODE_ESCAPE    = true;
  LOOKAHEAD              = 2;
//OPTIMIZE_TOKEN_MANAGER = true;
//OTHER_AMBIGUITY_CHECK  = 2 ;
//OUTPUT_DIRECTORY       = "<current directory>";
//SANITY_CHECK           = false;
  STATIC                 = false;
//UNICODE_INPUT          = true;
//USER_CHAR_STREAM       = true;
//USER_TOKEN_MANAGER     = true;
}


PARSER_BEGIN(FLGParser)

package cl.bee.genfrmjava.flgparser;



import java.io.*;

import java.util.*;

import cl.bee.genfrmjava.flgparser.visitor.*;
import cl.bee.genfrmjava.flgparser.syntaxtree.*;

public class FLGParser {

    /******************************************************************************
     * main
     ******************************************************************************/
    /**
     *
     * @param args TODO_javadoc
     *
     */
    public static void main(String args[]) {}
}

PARSER_END(FLGParser)

< * > SKIP :
{
   " "
 | "\t"
 | "\n"
 | "\r"
 | "\f"
}

/****************************************************************************/
/* COMMENT_STATE                                                            */
/****************************************************************************/

<COMMENT_STATE> SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<COMMENT_STATE> MORE :
{
  < ~[] >
}

/****************************************************************************/
/* DEFAULT                                                                  */
/****************************************************************************/

MORE :
{
  "!" : COMMENT_STATE
}

TOKEN :
{
   <FORM                     : "FORM"                                                > : FORM_STATE
 | <TEXT                     : "TEXT"                                                > : TEXT_STATE
 | <ATTRIBUTE_DEFAULTS       : "ATTRIBUTE_DEFAULTS"                                  >
 | <FIELD                    : "FIELD"                                               > : FIELD_STATE
 | <ORDER                    : "ORDER"                                               > : ORDER_STATE
 | <NAMED_DATA               : "NAMED_DATA"                                          > : NAMED_DATA_STATE
 | <END_OF_FORM              : "END_OF_FORM"                                         >
 | <NAME                     : "NAME"                                                >

 | <EQUAL                    : "="                                                   >
 | <SEMICOLON                : ";"                                                   >

 | <STRING                   :  "'" ( ~["'","\n","\r"] | "'" "'" )* "'"              >
}

/****************************************************************************/
/* FORM_STATE                                                               */
/****************************************************************************/

<FORM_STATE> TOKEN :
{
   <AREA_TO_CLEAR            : "AREA_TO_CLEAR"                                       >
 | <BACKGROUND               : "BACKGROUND"                                          >
 | <CURRENT                  : "CURRENT"                                             >
 | <HIGHLIGHT                : "HIGHLIGHT"                                           >
 | <REVERSE_FORM             : "REVERSE"                                             >
 | <HELP_FORM                : "HELP_FORM"                                           >
 | <NAME_ST_FORM             : "NAME"                                                >
 | <PRE_HELP_ACTION_ROUTINE  : "PRE_HELP_ACTION_ROUTINE"                             >
 | <POST_HELP_ACTION_ROUTINE : "POST_HELP_ACTION_ROUTINE"                            >
 | <WIDTH                    : "WIDTH"                                               >

 | <COLON_ST_FORM            : ":"                                                   >
 | <EQUAL_ST_FORM            : "="                                                   >
 | <SEMICOLON_ST_FORM        : ";"                                                   > : DEFAULT

 | <INTEGER_ST_FORM          : ( ["0"-"9"] )+                                        >
 | <STRING_ST_FORM           : "'" ( ~["'","\n","\r"] | "'" "'" )* "'"               >
}

/****************************************************************************/
/* TEXT_STATE                                                               */
/****************************************************************************/

<TEXT_STATE> TOKEN :
{
   <BOLD_TEXT                : "BOLD"                                                >
 | <BLINK                    : "BLINK"                                               >

 | <AMPERSAND                : "&"                                                   >
 | <COMMA_ST_TEXT            : ","                                                   >
 | <LPAR_ST_TEXT             : "("                                                   >
 | <RPAR_ST_TEXT             : ")"                                                   >
 | <SEMICOLON_ST_TEXT        : ";"                                                   > : DEFAULT

 | <INTEGER_ST_TEXT          : ( ["0"-"9"] )+                                        >
 | <STRING_ST_TEXT           : "'" ( ~["'","\n","\r"] | "'" "'" )* "'"               >
}

/****************************************************************************/
/* FIELD_STATE                                                              */
/****************************************************************************/

<FIELD_STATE> TOKEN :
{
   <BLANK_FILL               : "BLANK_FILL"                                          >
 | <CLEAR_CHARACTER          : "CLEAR_CHARACTER"                                     >
 | <DEF                      : "DEFAULT"                                             >
 | <DISPLAY_ONLY             : "DISPLAY_ONLY"                                        >
 | <ECHO                     : "ECHO"                                                >
 | <FIXED_DECIMAL            : "FIXED_DECIMAL"                                       >
 | <HELP                     : "HELP"                                                >
 | <INDEX_ST_FIELD           : "INDEX"                                               >
 | <LEFT_JUSTIFIED           : "LEFT_JUSTIFIED"                                      >
 | <NAME_ST_FIELD            : "NAME"                                                >
 | <NOAUTOTAB                : "NOAUTOTAB"                                           >
 | <NOBLINKING               : "NOBLINKING"                                          >
 | <NOBOLD                   : "NOBOLD"                                              >
 | <NODISPLAY_ONLY           : "NODISPLAY_ONLY"                                      >
 | <NOECHO                   : "NOECHO"                                              >
 | <NOFIXED_DECIMAL          : "NOFIXED_DECIMAL"                                     >
 | <NOREVERSE                : "NOREVERSE"                                           >
 | <NOSUPERVISOR_ONLY        : "NOSUPERVISOR_ONLY"                                   >
 | <NOSUPPRESS               : "NOSUPPRESS"                                          >
 | <NOUNDERLINE              : "NOUNDERLINE"                                         >
 | <NOUPPERCASE              : "NOUPPERCASE"                                         >
 | <PICTURE                  : "PICTURE"                                             >
 | <RESPONSE_REQUIRED        : "RESPONSE_REQUIRED"                                   >
 | <REVERSE                  : "REVERSE"                                             >
 | <RIGHT_JUSTIFIED          : "RIGHT_JUSTIFIED"                                     >
 | <SUPPRESS                 : "SUPPRESS"                                            >
 | <ZERO_FILL                : "ZERO_FILL"                                           >
 | <BOLD_FIELD               : "BOLD"                                                >
 | <UNDERLINE                : "UNDERLINE"                                           >

 | <COLON_ST_FIELD           : ":"                                                   >
 | <COMMA_ST_FIELD           : ","                                                   >
 | <EQUAL_ST_FIELD           : "="                                                   >
 | <LPAR_ST_FIELD            : "("                                                   >
 | <RPAR_ST_FIELD            : ")"                                                   >
 | <SEMICOLON_ST_FIELD       : ";"                                                   > : DEFAULT

 | <INTEGER_ST_FIELD         : ( ["0"-"9"] )+                                        >
 | <STRING_ST_FIELD          : "'" ( ~["'","\n","\r"] | "'" "'" )* "'"               >

}

/****************************************************************************/
/* ORDER_STATE                                                              */
/****************************************************************************/

<ORDER_STATE> TOKEN :
{
   <BEGIN_WITH               : "BEGIN_WITH"                                          >
 | <NAME_ST_ORDER            : "NAME"                                                >

 | <EQUAL_ST_ORDER           : "="                                                   >
 | <SEMICOLON_ST_ORDER       : ";"                                                   > : DEFAULT

 | <INTEGER_ST_ORDER         : ( ["0"-"9"] )+                                        >
 | <STRING_ST_ORDER          : "'" ( ~["'","\n","\r"] | "'" "'" )* "'"               >
}

/****************************************************************************/
/* NAMED_DATA_STATE                                                         */
/****************************************************************************/

<NAMED_DATA_STATE> TOKEN :
{
   <INDEX_ST_NAMED_DATA      : "INDEX"                                               >
 | <DATA                     : "DATA"                                                >
 | <NAME_ST_NAMED_DATA       : "NAME"                                                >

 | <EQUAL_ST_NAMED_DATA      : "="                                                   >
 | <SEMICOLON_ST_NAMED_DATA  : ";"                                                   > : DEFAULT

 | <INTEGER_ST_NAMED_DATA    : ( ["0"-"9"] )+                                        >
 | <STRING_ST_NAMED_DATA     : "'" ( ~["'","\n","\r"] | "'" "'" )* "'"               >
}

/****************************************************************************/
/* specification()                                                          */
/****************************************************************************/

void specification() :
{}
{
  form()
  ( text() )+
  adef()
  (field() )+
  order()
  [ ( ndata() )+ ]
  eofrm() <EOF>
}

/****************************************************************************/
/* form()                                                                   */
/****************************************************************************/

void form() :
{}
{
  <FORM>
  ( <NAME_ST_FORM>             <EQUAL_ST_FORM> <STRING_ST_FORM>
  | <HELP_FORM>                <EQUAL_ST_FORM> <STRING_ST_FORM>
  | <AREA_TO_CLEAR>            <EQUAL_ST_FORM> <INTEGER_ST_FORM> <COLON_ST_FORM> <INTEGER_ST_FORM>
  | <WIDTH>                    <EQUAL_ST_FORM> ( <INTEGER_ST_FORM> | <CURRENT> )
  | <BACKGROUND>               <EQUAL_ST_FORM> <CURRENT>
  | <HIGHLIGHT>                <EQUAL_ST_FORM> <REVERSE_FORM>
  | <PRE_HELP_ACTION_ROUTINE>  <EQUAL_ST_FORM> <STRING_ST_FORM>
  | <POST_HELP_ACTION_ROUTINE> <EQUAL_ST_FORM> <STRING_ST_FORM> <COLON_ST_FORM> <STRING_ST_FORM>
  )+
  <SEMICOLON_ST_FORM>
}

/****************************************************************************/
/* text()                                                                   */
/****************************************************************************/

void text() :
{}
{
  <TEXT> <LPAR_ST_TEXT> <INTEGER_ST_TEXT> <COMMA_ST_TEXT> <INTEGER_ST_TEXT> <RPAR_ST_TEXT> [ <INTEGER_ST_TEXT> ] <STRING_ST_TEXT> [ <AMPERSAND> <STRING_ST_TEXT> ]
  ( <BOLD_TEXT> | <BLINK> )*
  <SEMICOLON_ST_TEXT>
}

/****************************************************************************/
/* adef()                                                                   */
/****************************************************************************/

void adef() :
{}
{
  <ATTRIBUTE_DEFAULTS> <FIELD>
  ( field_assign()  | field_attribute() )+
  <SEMICOLON_ST_FIELD>
}

/****************************************************************************/
/* field_assign()                                                           */
/****************************************************************************/

void field_assign() :
{}
{
    <CLEAR_CHARACTER> <EQUAL_ST_FIELD>                        <STRING_ST_FIELD>
  | <DEF>             <EQUAL_ST_FIELD>                        <STRING_ST_FIELD>
  | <HELP>            <EQUAL_ST_FIELD>                        <STRING_ST_FIELD>
  | <NAME_ST_FIELD>   <EQUAL_ST_FIELD>                        <STRING_ST_FIELD> <LPAR_ST_FIELD> <INTEGER_ST_FIELD> <COMMA_ST_FIELD> <INTEGER_ST_FIELD> <RPAR_ST_FIELD>
  | <PICTURE>         <EQUAL_ST_FIELD> [ <INTEGER_ST_FIELD> ] <STRING_ST_FIELD>
  | <INDEX_ST_FIELD>  <EQUAL_ST_FIELD> <LPAR_ST_FIELD> <INTEGER_ST_FIELD> <COMMA_ST_FIELD> <INTEGER_ST_FIELD> <RPAR_ST_FIELD> ( <COLON_ST_FIELD> <LPAR_ST_FIELD> <INTEGER_ST_FIELD> <COMMA_ST_FIELD> <INTEGER_ST_FIELD> <RPAR_ST_FIELD> )*
}

/****************************************************************************/
/* field_attribute()                                                        */
/****************************************************************************/

void field_attribute() :
{}
{
    <BLANK_FILL>
  | <DISPLAY_ONLY>
  | <ECHO>
  | <FIXED_DECIMAL>
  | <LEFT_JUSTIFIED>
  | <NOAUTOTAB>
  | <NOBLINKING>
  | <NOBOLD>
  | <NODISPLAY_ONLY>
  | <NOECHO>
  | <NOFIXED_DECIMAL>
  | <NOREVERSE>
  | <NOSUPERVISOR_ONLY>
  | <NOSUPPRESS>
  | <NOUNDERLINE>
  | <NOUPPERCASE>
  | <RESPONSE_REQUIRED>
  | <REVERSE>
  | <RIGHT_JUSTIFIED>
  | <SUPPRESS>
  | <ZERO_FILL>
  | <BOLD_FIELD>
  | <UNDERLINE>
}

/****************************************************************************/
/* field()                                                                  */
/****************************************************************************/

void field() :
{}
{
  <FIELD>
  ( field_assign()  | field_attribute() )+
  <SEMICOLON_ST_FIELD>
}

/****************************************************************************/
/* order()                                                                  */
/****************************************************************************/

void order() :
{}
{
  <ORDER> <BEGIN_WITH> <EQUAL_ST_ORDER> <INTEGER_ST_ORDER>
  ( <NAME_ST_ORDER> <EQUAL_ST_ORDER> <STRING_ST_ORDER>
  )+
  <SEMICOLON_ST_ORDER>
}

/****************************************************************************/
/* ndata()                                                                  */
/****************************************************************************/

void ndata() :
{}
{
  <NAMED_DATA>
  ( <INDEX_ST_NAMED_DATA> <EQUAL_ST_NAMED_DATA> <INTEGER_ST_NAMED_DATA>
  | <NAME_ST_NAMED_DATA>  <EQUAL_ST_NAMED_DATA> <STRING_ST_NAMED_DATA>
  | <DATA>                <EQUAL_ST_NAMED_DATA> <STRING_ST_NAMED_DATA>
  )+
  <SEMICOLON_ST_NAMED_DATA>
}

/****************************************************************************/
/* eofrm()                                                                  */
/****************************************************************************/

void eofrm() :
{}
{
  <END_OF_FORM> <NAME> <EQUAL> <STRING> <SEMICOLON>
}