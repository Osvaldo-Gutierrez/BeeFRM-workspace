       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.       LDCPGGFI.
       AUTHOR.           CONSIST(MHM).
       DATE-WRITTEN.     19-OCT-1995. 

      * PROGRAMA BATCH QUE CALCULA FECHA DE INICIO DE LA LINEA
      * Y LA GRABA EN LA LDC.

       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
      *====================
       FILE-CONTROL.
      *-------------

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY LDCBRLDC.

       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSBRMSC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSBRMSG.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY TABWGTVC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LDC-REQA==.

       01  WSS-VARI.
           03  WSS-NUM-RBAD          VALUE 0          PIC 9(02).
           03  WSS-NUM-LLDC          VALUE 0          PIC 9(08). 
           03  WSS-NUM-GLDC          VALUE 0          PIC 9(08).
           03  WSS-FEC-FINI
               05  WSS-NUM-SINI                       PIC 9(02). 
               05  WSS-NUM-AINI                       PIC 9(02). 
               05  WSS-NUM-MINI                       PIC 9(02). 
               05  WSS-NUM-DINI                       PIC 9(02). 
      *

       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
      *------------
       INI-MAIN.
      *
       COPY GNSBGEDB.
      *EFECTUA APERTURA DE ARCHIVOS
           PERFORM INICIO-GFI.

      *LEE  LA PRIMERA LDC
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM LDC-FIO-LDC.
           IF NOT FIO-STAT-OKS 
               DISPLAY 'ERROR: NO EXISTEN DATOS EN LDCLDC '
               DISPLAY 'PROCESO CANCELADO...'.

      *PROCESO PRINCIPAL
           PERFORM PROCESO-GFI UNTIL NOT FIO-STAT-OKS.

      *CIERRA ARCHIVOS
           PERFORM FINAL-GFI.

      *ESTADISTICA Y FIN DEL PGM.
           PERFORM ESTADISTICAS.

       FIN-MAIN.
           EXIT.

       INICIO-GFI SECTION.
      *---------------
       INI-INICIO-GFI.
           MOVE 'LDCPGGFI' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

      *APERTURA DE ARCHIVOS

      *LDCLDC
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM LDC-FIO-LDC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO LDCLDC '
               DISPLAY 'DE I/O   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               PERFORM ESTADISTICAS.

       FIN-INICIO-GFI.
           EXIT.

       PROCESO-GFI SECTION.
      *--------------------
       INI-PROCESO-GFI.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND
           PERFORM LDC-FIO-LDC
           IF NOT FIO-STAT-OKS 
              DISPLAY 'ERROR AL GET-KEY-UPD ARCHIVO LDCLDC '
              DISPLAY 'GET-KEY-UPD '    FIO-STAT
              MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
              PERFORM FINAL-GFI
              PERFORM ESTADISTICAS.

           ADD 1 TO WSS-NUM-LLDC IN WSS-VARI. 

           PERFORM CALCULA-FECHA-INICIO.
           MOVE WSS-FEC-FINI IN WSS-VARI TO LDC-FEC-FINI IN LDC.

           MOVE FIO-MOD TO FIO-CMND
           PERFORM LDC-FIO-LDC
           IF NOT FIO-STAT-OKS 
              DISPLAY 'ERROR AL MODIFICAR ARCHIVO LDCLDC '
              DISPLAY 'FIO-MOD   '    FIO-STAT
              MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
              PERFORM FINAL-GFI
              PERFORM ESTADISTICAS
           ELSE
              ADD 1 TO WSS-NUM-GLDC IN WSS-VARI. 

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM LDC-FIO-LDC.

       FIN-PROCESO-GFI.
           EXIT.

       CALCULA-FECHA-INICIO SECTION.
      *----------------------------
       INI-CALCULA-FECHA-INICIO.
           IF LDC-IND-PVIG IN LDC = 'A'
              MOVE FEC-RST-ANO  TO FEC-CMND
              MOVE LDC-NUM-PVIG IN LDC TO FEC-NANO
           ELSE
           IF LDC-IND-PVIG IN LDC = 'M'
              MOVE FEC-RST-MES  TO FEC-CMND
              MOVE LDC-NUM-PVIG IN LDC TO FEC-NMES
           ELSE
           IF LDC-IND-PVIG IN LDC = 'D'
              MOVE FEC-RST-DIA  TO FEC-CMND
              MOVE LDC-NUM-PVIG IN LDC TO FEC-NDIA.
           MOVE LDC-NUM-DVEN IN LDC TO FEC-ITM1.
           MOVE LDC-NUM-MVEN IN LDC TO FEC-ITM2.
           MOVE LDC-NUM-SVEN IN LDC TO FEC-ITM3.
           MOVE LDC-NUM-AVEN IN LDC TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           PERFORM CAL-FEC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL RESTAR A FECHA VENCIMIENTO'
              MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
              PERFORM FINAL-GFI
              PERFORM ESTADISTICAS.
           MOVE FEC-ITM1 TO WSS-NUM-DINI IN WSS-VARI.
           MOVE FEC-ITM2 TO WSS-NUM-MINI IN WSS-VARI.
           MOVE FEC-ITM3 TO WSS-NUM-SINI IN WSS-VARI.
           MOVE FEC-ITM4 TO WSS-NUM-AINI IN WSS-VARI.

       FIN-CALCULA-FECHA-INICIO.
           EXIT.

       FINAL-GFI SECTION.
       INI-FINAL-GFI.
      *--------------
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-LDC.

       FIN-FINAL-GFI.
           EXIT.
      *
       ESTADISTICAS SECTION.
      *---------------------
       INI-ESTADISTICAS.

           DISPLAY ' '.
           DISPLAY 'ESTADISTICAS  LDCPGGFI'.
           DISPLAY '----------------------'.
           DISPLAY 'PROCESADOS LDC  : ' 
                   WSS-NUM-LLDC IN WSS-VARI.
           DISPLAY 'GRABADOS   LDC  : ' 
                   WSS-NUM-GLDC IN WSS-VARI.
      *
           IF WSS-NUM-RBAD IN WSS-VARI > ZEROES
               PERFORM PRG-ABT
           ELSE
               PERFORM INI-GNS-PRO-END.
       FIN-ESTADISTICAS.
           EXIT.

       COPY LDCBFLDC.
       COPY GNSBFMSG.
       COPY GNSBFTAB.

       COPY GNSBBMSG.

       COPY GNSBGDTC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGFEC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGABT.
       COPY GNSBGEND.
