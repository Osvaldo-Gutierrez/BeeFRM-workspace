       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      LDCPLCER.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    16-Jan-95 05:49 PM.

      * Programa Listador de Reporte CER con Sort.


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTCER         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTCER         ASSIGN TO        UT-S-RPTCER.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTCER         ASSIGN TO        UT-S-RPTCER.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY LDCRRCER.
       FD  RPTCER LABEL RECORDS OMITTED
           REPORT IS RPT-CER
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CLF-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY LDCBRCLF.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY LDCRWCER.
      *<<<< RW
       COPY LDCRWCE1. 
      *>>>>
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
      *Entidades de GNS
       COPY GNSBRMSC.
       COPY GNSWGHOY.
       COPY GNSWGFEC.
       COPY GNSWGCPT.
      *Entidades de TAB
       COPY TABBROFI.
       COPY TABBRUSR.
      *Entidades de LDC
      *<<<< NOT PGM_DB2
       COPY LDCBRLDC.
       COPY LDCBRARC.
       COPY LDCBRARG.
       COPY LDCBRRDC.
       COPY SGCBRDBC.
      *>>>>

       01  WSS-VARI.
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-DFIN                      PIC 9(02).
               05  WSS-NUM-MFIN                      PIC 9(02).
               05  WSS-NUM-SFIN                      PIC 9(02).
               05  WSS-NUM-AFIN                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-DTER                      PIC 9(02).
           03  WSS-FEC-FPRI.
               05  WSS-NUM-SPRI                      PIC 9(02).
               05  WSS-NUM-APRI                      PIC 9(02).
               05  WSS-NUM-MPRI                      PIC 9(02).
               05  WSS-NUM-DPRI                      PIC 9(02).
           03  WSS-FEC-FPAS.
               05  WSS-NUM-SPAS                      PIC Z(02).
               05  WSS-NUM-APAS                      PIC Z(02).
               05  WSS-NUM-MPAS                      PIC Z(02).
               05  WSS-NUM-DPAS                      PIC Z(02).
           03  WSS-COD-OFIC                          PIC X(03).
           03  WSS-COD-EJEC                          PIC X(12).
           03  WSS-CLA-OPE.
               05  WSS-TIP-OPE                       PIC X(03).
               05  WSS-STP-OPE                       PIC X(03).
           03  ARC-KEY-IARC.
               05  ARC-KEY-ILDC.
                   07  ARC-CAI-ICLF               PIC X(04).      
                   07  ARC-IIC-ICLF               PIC 9(08).      
                   07  ARC-NUM-ILDC               PIC 9(02).
               05  ARC-NUM-IARC                   PIC 9(01).
           03  ARG-KEY-IARG.
               05  ARG-KEY-ILDC.
                   07  ARG-CAI-ICLF               PIC X(04).      
                   07  ARG-IIC-ICLF               PIC 9(08).      
                   07  ARG-NUM-ILDC               PIC 9(02).
               05  ARG-NUM-IARG                   PIC 9(01).
           03  WSS-IARC       VALUE ZEROES        PIC 9(02).
           03  WSS-IARG       VALUE ZEROES        PIC 9(02).
           03  WSS-IFLD       VALUE ZEROES        PIC 9(02).
           03  WSS-MOV        VALUE ZEROES        PIC 9(02).
           03  WSS-FIN        VALUE ZEROES        PIC 9(01).

           03  WSS-TAB-ARC   OCCURS 9 TIMES.
               05  WSS-COD-TOOC                   PIC X(05).
               05  WSS-COD-AOOC                   PIC X(08).
               05  WSS-COD-VCOC                   PIC X(05).

           03  WSS-TAB-ARG   OCCURS 9 TIMES.
      *DAD ini  16-SEP-1992
               05  WSS-COD-TGAR                   PIC X(03).
      *DAD fin  16-SEP-1992
               05  WSS-COD-TOGA                   PIC X(05).
               05  WSS-COD-AOGA                   PIC X(08).
               05  WSS-NUM-PORG                   PIC 9(03).

           03  WSS-STAT-RDC          VALUE ZEROES  PIC 9(01).
           03  WSS-IND-MCLI          VALUE SPACES  PIC X(01).
      *>>>>
      *<<<< WSS_DTC
      * Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
      * Entidades de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      * Entidades de SGC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      * Entidades de LDC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ARC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ARG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY LDCRFCER.
           .
       COPY LDCRTCER.
      *<<<< RT
       COPY LDCRTCE1. 
      *>>>>

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-CER-CLF-COD-OFIC SECTION.
           USE BEFORE REPORTING CF-CER-CLF-COD-OFIC.
       INI-CF-CER-CLF-COD-OFIC.
       FIN-CF-CER-CLF-COD-OFIC.
           EXIT.
       DEC-CF-CER-CLF-COD-EJEC SECTION.
           USE BEFORE REPORTING CF-CER-CLF-COD-EJEC.
       INI-CF-CER-CLF-COD-EJEC.
       FIN-CF-CER-CLF-COD-EJEC.
           EXIT.
       DEC-PH-CER SECTION.
           USE BEFORE REPORTING PH-CER.
       INI-PH-CER.
       FIN-PH-CER.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'LDCPLCER' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'LDC'        TO FIO-SIST.
           PERFORM LDC-FIO-LDC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM LDC-FIO-LDC.

           MOVE 'LDC'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM LDC-FIO-ARC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM LDC-FIO-ARC.

           MOVE 'LDC'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM LDC-FIO-ARG.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM LDC-FIO-ARG.

           MOVE 'LDC'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM LDC-FIO-RDC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM LDC-FIO-RDC.

           MOVE 'TAB'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE 'DEU'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE 'TAB'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM TAB-FIO-OFI.

           MOVE 'TAB'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM TAB-FIO-USR.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM TAB-FIO-USR.

           MOVE 'SGC'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

      *>>>>
      *<<<< INI
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA PARAMETROS'.
           DISPLAY ' '.
      * Captura codigo de oficina y fechas de parametro
           DISPLAY 'INGRESE FECHA INICIAL (ddmmssaa) : '.
           ACCEPT WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
     
           DISPLAY 'INGRESE FECHA FINAL   (ddmmssaa) : '.
           ACCEPT WSS-FEC-FFIN IN WSS-VARI.
           DISPLAY WSS-FEC-FFIN IN WSS-VARI.

           DISPLAY 'INGRESE COD. OFICINA  (XXX/*)    : '.
           ACCEPT WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.

           DISPLAY 'INGRESE COD. EJECUTIVO (X(12)/*) : '.
           ACCEPT WSS-COD-EJEC IN WSS-VARI.
           DISPLAY WSS-COD-EJEC IN WSS-VARI.

           DISPLAY 'FIN CAPTURA PARAMETROS'.
           DISPLAY ' '.
           DISPLAY 'INICIO VALIDACION PARAMETROS'.

      *VAL-FEC Valida fecha inicial
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-N               TO FEC-CHOY.  
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN VALIDACION F. INICIAL ' 
                        WSS-FEC-FINI IN WSS-VARI '  ' FEC-MENS
      *<<<< NOT PGM_ARG
              PERFORM GNS-PRO-ABT.
      *>>>>
 
      *VAL-FEC Valida fecha final
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-N               TO FEC-CHOY.  
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN VALIDACION F. FINAL ' 
                        WSS-FEC-FFIN IN WSS-VARI '  ' FEC-MENS
      *<<<< NOT PGM_ARG
              PERFORM GNS-PRO-ABT.
      *>>>>

      *CMP-FEC Compara fecha inicial con fecha final
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-CMP-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' FECHA INICIO > FECHA FINAL: ' 
                        WSS-FEC-FINI IN WSS-VARI ' > ' 
                        WSS-FEC-FFIN IN WSS-VARI 
      *<<<< NOT PGM_ARG
              PERFORM GNS-PRO-ABT.
      *>>>>

      *VAL-OFI Valida codigo de oficina
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
              MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
              PERFORM VAL-OFI
              IF MSG-GLS-MENS = 'COD    NEX'
                 DISPLAY ' '
                 DISPLAY ' PROCESO CANCELADO '
                 DISPLAY ' ERROR EN VALIDACION COD. OFICINA : ' 
                           WSS-COD-OFIC IN WSS-VARI
      *<<<< NOT PGM_ARG
                 PERFORM GNS-PRO-ABT.
      *>>>>

      *VAL-USR Valida codigo de ejecutivo
           IF WSS-COD-EJEC IN WSS-VARI > SPACES AND
              WSS-COD-EJEC IN WSS-VARI NOT = '*'
              MOVE WSS-COD-EJEC IN WSS-VARI TO USR-COD-USER IN USR
              PERFORM VAL-USR
              IF MSG-GLS-MENS = 'COD    NEX'
                 DISPLAY ' '
                 DISPLAY ' PROCESO CANCELADO '
                 DISPLAY ' ERROR EN VALIDACION COD. EJECUTIVO: ' 
                           WSS-COD-EJEC IN WSS-VARI
      *<<<< NOT PGM_ARG
                 PERFORM GNS-PRO-ABT.
      *>>>>

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DDES IN WSS-CER
                                            WSS-NUM-DPRI IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MDES IN WSS-CER
                                            WSS-NUM-MPRI IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SDES IN WSS-CER
                                            WSS-NUM-SPRI IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-ADES IN WSS-CER
                                            WSS-NUM-APRI IN WSS-VARI.
           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DHAS IN WSS-CER
                                            WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MHAS IN WSS-CER
                                            WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-SHAS IN WSS-CER
                                            WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-AHAS IN WSS-CER
                                            WSS-NUM-ATER IN WSS-VARI.
           DISPLAY ' '.
           DISPLAY 'FIN VALIDACION PARAMETROS'.
           DISPLAY ' '.
      *>>>>
           SORT GNSSRT
       COPY LDCRSCER.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
           IF CLF-FEC-FCIE IN CLF NOT < WSS-FEC-FPRI AND
              CLF-FEC-FCIE IN CLF NOT > WSS-FEC-FTER 
              NEXT SENTENCE
           ELSE
              GO TO NXT-INP-SRT. 

           IF CLF-MSC-STAT IN CLF NOT = 'C'
              GO TO NXT-INP-SRT.

           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
              IF WSS-COD-OFIC IN WSS-VARI NOT = CLF-COD-OFIC IN CLF
                 GO TO NXT-INP-SRT.

           IF WSS-COD-EJEC IN WSS-VARI > SPACES AND
              WSS-COD-EJEC IN WSS-VARI NOT = '*'
              IF WSS-COD-EJEC IN WSS-VARI NOT = CLF-COD-EJEC IN CLF
                 GO TO NXT-INP-SRT.
      *>>>>
       MOV-INP-SRT.
       COPY LDCRMCER.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-CLF.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTCER.
      *     MOVE 'LDCPLCER' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-CER.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *BUS-OFI busca oficina en tablas
           MOVE SPACES TO WSS-GLS-OFIC IN WSS-CER.
           IF CLF-COD-OFIC IN SRT > SPACES
               MOVE CLF-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIC IN WSS-CER.
      *BUS-USR busca usuario en tablas
           MOVE SPACES TO WSS-GLS-EJEC IN WSS-CER.
           IF CLF-COD-EJEC IN SRT > SPACES
               MOVE CLF-COD-EJEC IN SRT TO USR-COD-USER IN USR
               PERFORM BUS-USR
               MOVE USR-GLS-DESC IN USR TO WSS-GLS-EJEC IN WSS-CER.

      * JVM: SI EXISTE MAS DE UN CLIENTE DESPLIEGA "MULTIPLES CLIENTES"
      * EN EL NOMBRE Y BLANCOS EN RUT ; posteriormente cuando se modi-
      * fique layout del listado para mostrar todos los clientes debe
      * hacerse LOOP para obtener desde RDC todos los clientes de la CLF
      *
      * Obtiene Datos desde RDC para datos clientes 
           PERFORM GET-CUR-RDC.
           IF WSS-STAT-RDC IN WSS-VARI = 1
              PERFORM RST-DBC
           ELSE
              PERFORM GET-RDC-DBC
              PERFORM GET-NXT-RDC
              IF WSS-IND-MCLI IN WSS-VARI = 'M'
                 MOVE SPACES TO DBC-NUM-ICLI IN DBC
                                DBC-VRF-ICLI IN DBC
                                DBC-IND-ICLI IN DBC
                                DBC-GLS-ICLI IN DBC
                 MOVE 'MULTIPLES CLIENTES' TO DBC-GLS-NOMC IN DBC.

      *BUS-TAB busca glosa de codigo en tabla
           MOVE SPACES TO WSS-GLS-VCCF IN WSS-CER.
           IF CLF-COD-VCCF IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE CLF-COD-VCCF IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-VCCF IN WSS-CER.
      *BUS-TAB busca glosa de codigo en tabla
           MOVE SPACES TO WSS-GLS-TOCF IN WSS-CER.
           IF CLF-COD-TOCF IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'TIO'               TO TAB-COD-TTAB IN TAB
               MOVE CLF-COD-TOCF IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-TOCF IN WSS-CER.
      *BUS-TAB busca glosa de codigo en tabla
           MOVE SPACES TO WSS-GLS-RAZC IN WSS-CER.
           IF CLF-COD-RAZC IN SRT > SPACES
               MOVE 'DEU'               TO TAB-COD-SIST
               MOVE 'RZC'               TO TAB-COD-TTAB IN TAB
               MOVE CLF-COD-RAZC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-RAZC IN WSS-CER.

           MOVE CLF-FEC-FCIE IN SRT TO WSS-FEC-FPAS IN WSS-VARI.
           MOVE WSS-NUM-DPAS IN WSS-VARI TO WSS-NUM-DCIE IN WSS-CER.
           MOVE WSS-NUM-MPAS IN WSS-VARI TO WSS-NUM-MCIE IN WSS-CER.
           MOVE WSS-NUM-SPAS IN WSS-VARI TO WSS-NUM-SCIE IN WSS-CER.
           MOVE WSS-NUM-APAS IN WSS-VARI TO WSS-NUM-ACIE IN WSS-CER.

           MOVE CLF-FEC-FVEN IN SRT TO WSS-FEC-FPAS IN WSS-VARI.
           MOVE WSS-NUM-DPAS IN WSS-VARI TO WSS-NUM-DVEN IN WSS-CER.
           MOVE WSS-NUM-MPAS IN WSS-VARI TO WSS-NUM-MVEN IN WSS-CER.
           MOVE WSS-NUM-SPAS IN WSS-VARI TO WSS-NUM-SVEN IN WSS-CER.
           MOVE WSS-NUM-APAS IN WSS-VARI TO WSS-NUM-AVEN IN WSS-CER.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-CER.
      *<<<< FIN_GEN_OUT
           MOVE CLF-CAI-ICLF IN SRT TO LDC-CAI-ICLF IN LDC.
           MOVE CLF-IIC-ICLF IN SRT TO LDC-IIC-ICLF IN LDC.
           MOVE ZEROS               TO LDC-NUM-ILDC IN LDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM LDC-FIO-LDC.
           IF NOT ( FIO-STAT-OKS AND
                    CLF-CAI-ICLF IN SRT = LDC-CAI-ICLF IN LDC AND
                    CLF-IIC-ICLF IN SRT = LDC-IIC-ICLF IN LDC )
                    GO TO NXT-OUT-SRT.
           GENERATE HDR-CE1.
       GEN-CE1.
           PERFORM CLR-TAB VARYING WSS-IFLD FROM 1 BY 1 UNTIL 
                                                      WSS-IFLD > 9.
       CARGA-ARC.
           MOVE ZEROES TO WSS-IFLD WSS-IARC.
           MOVE LDC-KEY-ILDC IN LDC      TO ARC-KEY-ILDC IN WSS-VARI.
           MOVE ZEROES                   TO ARC-NUM-IARC IN WSS-VARI.
           MOVE ARC-KEY-IARC IN WSS-VARI TO ARC-KEY-IARC IN ARC.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LEE-ARC. 
           PERFORM LDC-FIO-ARC.
           IF NOT FIO-STAT-OKS OR
              ARC-KEY-ILDC IN WSS-VARI NOT = ARC-KEY-ILDC IN ARC
              GO TO CARGA-ARG.
           PERFORM PUT-TAB-ARC.

       CARGA-ARG.
           MOVE ZEROES TO WSS-IFLD WSS-IARG.
           MOVE LDC-KEY-ILDC IN LDC      TO ARG-KEY-ILDC IN WSS-VARI.
           MOVE ZEROES                   TO ARG-NUM-IARG IN WSS-VARI.
           MOVE ARG-KEY-IARG IN WSS-VARI TO ARG-KEY-IARG IN ARG.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LEE-ARG. 
           PERFORM LDC-FIO-ARG.
           IF NOT FIO-STAT-OKS OR
              ARG-KEY-ILDC IN WSS-VARI NOT = ARG-KEY-ILDC IN ARG
              GO TO GEN-DET-CE1.
           PERFORM PUT-TAB-ARG.

       GEN-DET-CE1.
           PERFORM GEN-DET-LDC.
           GENERATE TRL-CE1.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM LDC-FIO-LDC.
           IF FIO-STAT-OKS AND
              CLF-CAI-ICLF IN SRT = LDC-CAI-ICLF IN LDC AND
              CLF-IIC-ICLF IN SRT = LDC-IIC-ICLF IN LDC
              GO TO GEN-CE1.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-CER.
      *<<<< FIN_TMT
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-LDC.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-ARC.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-ARG.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-RDC.

           MOVE 'TAB' TO FIO-SIST.                    

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM TAB-FIO-OFI.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM TAB-FIO-USR.
      *>>>>
           CLOSE RPTCER.
       EXT-OUT-SRT.
           EXIT.
       COPY LDCBFCLF.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
       GEN-DET-LDC SECTION.
       INI-GEN-DET-LDC.
           MOVE LDC-NUM-ILDC IN LDC     TO WSS-NUM-ILDC IN WSS-CE1.
           MOVE LDC-GLS-DESC IN LDC     TO WSS-GLS-DESC IN WSS-CE1.
           MOVE LDC-IND-MGAR IN LDC     TO WSS-IND-MGAR IN WSS-CE1.
           MOVE LDC-VAL-CMAX IN LDC     TO WSS-VAL-CMLC IN WSS-CE1.
           MOVE WSS-GLS-VCCF IN WSS-CER TO WSS-GLS-VCAM IN WSS-CE1.
      *DAD ini  16-SEP-1992
      *     MOVE WSS-GLS-VCCF IN WSS-CER TO WSS-GLS-VCCR IN WSS-CE1.
      *DAD fin  16-SEP-1992
           MOVE LDC-NUM-DVEN IN LDC     TO WSS-NUM-DVEN IN WSS-CE1.
           MOVE '-'                     TO WSS-GLS-GUI1 IN WSS-CE1.
           MOVE LDC-NUM-MVEN IN LDC     TO WSS-NUM-MVEN IN WSS-CE1.
           MOVE '-'                     TO WSS-GLS-GUI2 IN WSS-CE1.
           MOVE LDC-NUM-SVEN IN LDC     TO WSS-NUM-SVEN IN WSS-CE1.
           MOVE LDC-NUM-AVEN IN LDC     TO WSS-NUM-AVEN IN WSS-CE1.
           MOVE 0 TO WSS-FIN.
           PERFORM CMP-DET-LDC VARYING WSS-IFLD FROM 1 BY 1 
                                 UNTIL WSS-FIN = 1 OR WSS-IFLD > 9.
       FIN-GEN-DET-LDC.
           EXIT.

       CMP-DET-LDC SECTION.
       INI-CMP-DET-LDC.
           IF WSS-IARC = ZEROS AND WSS-IARG = ZEROS
               MOVE 1 TO WSS-FIN
               MOVE SPACES TO WSS-GLS-VCCR IN WSS-CE1
               MOVE SPACES TO WSS-COD-TOOC IN WSS-CE1
               MOVE SPACES TO WSS-COD-AOOC IN WSS-CE1
      *DAD ini  16-SEP-1992
      *         MOVE SPACES TO WSS-GLS-VCAM IN WSS-CE1
               MOVE SPACES TO WSS-COD-TGAR IN WSS-CE1
      *DAD fin  16-SEP-1992
               MOVE SPACES TO WSS-COD-TOGA IN WSS-CE1
               MOVE SPACES TO WSS-COD-AOGA IN WSS-CE1
               MOVE ZEROES TO WSS-NUM-PORG IN WSS-CE1
               GO TO GEN-GEN-CE1.

           IF WSS-IFLD > WSS-IARC AND WSS-IFLD > WSS-IARG 
               MOVE 1 TO WSS-FIN
               GO TO FIN-CMP-DET-LDC.

           IF WSS-IFLD > 1
               MOVE ZEROES TO WSS-NUM-ILDC IN WSS-CE1
               MOVE SPACES TO WSS-GLS-DESC IN WSS-CE1
               MOVE SPACES TO WSS-IND-MGAR IN WSS-CE1
               MOVE ZEROES TO WSS-VAL-CMLC IN WSS-CE1
               MOVE SPACES TO WSS-GLS-VCCR IN WSS-CE1
               MOVE SPACES TO WSS-GLS-VCAM IN WSS-CE1
               MOVE SPACES TO WSS-GLS-GUI1 IN WSS-CE1
               MOVE SPACES TO WSS-GLS-GUI2 IN WSS-CE1
               MOVE SPACES TO WSS-NUM-DVEN IN WSS-CE1
               MOVE SPACES TO WSS-NUM-MVEN IN WSS-CE1
               MOVE SPACES TO WSS-NUM-SVEN IN WSS-CE1
               MOVE SPACES TO WSS-NUM-AVEN IN WSS-CE1.
 
           MOVE SPACES TO WSS-GLS-VCCR IN WSS-CE1.
           MOVE SPACES TO WSS-COD-TOOC IN WSS-CE1.
           MOVE SPACES TO WSS-COD-AOOC IN WSS-CE1.
      *DAD ini  16-SEP-1992
      *     MOVE SPACES TO WSS-GLS-VCAM IN WSS-CE1.
           MOVE SPACES TO WSS-COD-TGAR IN WSS-CE1.
      *DAD fin  16-SEP-1992
           MOVE SPACES TO WSS-COD-TOGA IN WSS-CE1.
           MOVE SPACES TO WSS-COD-AOGA IN WSS-CE1.
           MOVE ZEROES TO WSS-NUM-PORG IN WSS-CE1.

      *DAD ini  16-SEP-1992
      *     IF WSS-IARC = 1
      *         MOVE WSS-COD-VCOC IN WSS-VARI(1) TO 
      *                              WSS-GLS-VCCR IN WSS-CE1
      *                              WSS-GLS-VCAM IN WSS-CE1.
      *DAD fin  16-SEP-1992

           IF WSS-IARC > ZEROES AND WSS-IARC NOT < WSS-IFLD                
      *DAD ini  16-SEP-1992
               MOVE WSS-COD-VCOC IN WSS-VARI(WSS-IFLD) TO
                                    WSS-GLS-VCCR IN WSS-CE1
      *DAD fin  16-SEP-1992
               MOVE WSS-COD-TOOC IN WSS-VARI(WSS-IFLD) TO 
                                    WSS-COD-TOOC IN WSS-CE1
               MOVE WSS-COD-AOOC IN WSS-VARI(WSS-IFLD) TO 
                                    WSS-COD-AOOC IN WSS-CE1.

           IF WSS-IARG > ZEROES AND WSS-IARG NOT < WSS-IFLD
      *DAD ini  16-SEP-1992
               MOVE WSS-COD-TGAR IN WSS-VARI(WSS-IFLD) TO
                                    WSS-COD-TGAR IN WSS-CE1
      *DAD fin  16-SEP-1992
               MOVE WSS-COD-TOGA IN WSS-VARI(WSS-IFLD) TO 
                                    WSS-COD-TOGA IN WSS-CE1
               MOVE WSS-COD-AOGA IN WSS-VARI(WSS-IFLD) TO 
                                    WSS-COD-AOGA IN WSS-CE1
               MOVE WSS-NUM-PORG IN WSS-VARI(WSS-IFLD) TO 
                                    WSS-NUM-PORG IN WSS-CE1.
       GEN-GEN-CE1.
           GENERATE DL-CE1.
       FIN-CMP-DET-LDC.
           EXIT.

       CLR-TAB SECTION.
       INI-CLR-TAB.
           MOVE SPACES TO WSS-COD-TOOC IN WSS-VARI(WSS-IFLD).
           MOVE SPACES TO WSS-COD-AOOC IN WSS-VARI(WSS-IFLD).
           MOVE SPACES TO WSS-COD-VCOC IN WSS-VARI(WSS-IFLD).
      *DAD ini  16-SEP-1992
           MOVE SPACES TO WSS-COD-TGAR IN WSS-VARI(WSS-IFLD).
      *DAD fin  16-SEP-1992
           MOVE SPACES TO WSS-COD-TOGA IN WSS-VARI(WSS-IFLD).
           MOVE SPACES TO WSS-COD-AOGA IN WSS-VARI(WSS-IFLD).
           MOVE ZEROES TO WSS-NUM-PORG IN WSS-VARI(WSS-IFLD).
       FIN-CLR-TAB.
           EXIT.

       PUT-TAB-ARC SECTION.
       INI-PUT-TAB-ARC.
           ADD 1 TO WSS-IARC IN WSS-VARI
                    WSS-IFLD IN WSS-VARI.
           IF ARC-IIC-IOPC IN ARC > ZEROES
               MOVE ARC-CAI-IOPC IN ARC TO WSS-COD-TOOC
                                                IN WSS-VARI(WSS-IFLD)
               MOVE ARC-IIC-IOPC IN ARC TO WSS-COD-AOOC 
                                                IN WSS-VARI(WSS-IFLD)
      *DAD ini  16-SEP-1992
               IF ARC-COD-VCOC IN ARC > SPACES
                   MOVE 'TAB'               TO TAB-COD-SIST
                   MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
                   MOVE ARC-COD-VCOC IN ARC TO TAB-COD-CTAB IN TAB
                   PERFORM BUS-TAB
                   MOVE TAB-GLS-ABRV IN TAB TO WSS-COD-VCOC
                                                IN WSS-VARI(WSS-IFLD)
                   GO TO NXT-PUT-TAB-ARC
               ELSE
      *DAD fin  16-SEP-1992
                   GO TO NXT-PUT-TAB-ARC.

      *BUS-TAB Busca Glosa Moneda de Tabla
           IF ARC-COD-VCOC IN ARC > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE ARC-COD-VCOC IN ARC TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-COD-VCOC
                                                IN WSS-VARI(WSS-IFLD).
      *BUS-TAB Busca Glosa Tipo Operacion de Tabla
           IF ARC-COD-TOOC IN ARC > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'TIO'               TO TAB-COD-TTAB IN TAB
               MOVE ARC-COD-TOOC IN ARC TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-COD-TOOC
                                                IN WSS-VARI(WSS-IFLD).
      *BUS-TAB Busca Glosa Cod. Auxiliar de Tabla
           IF ARC-COD-AOOC IN ARC > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE SPACES              TO TAB-COD-CTAB IN TAB
               MOVE ARC-COD-TOOC IN ARC TO WSS-TIP-OPE 
               MOVE ARC-COD-AOOC IN ARC TO WSS-STP-OPE 
               MOVE WSS-CLA-OPE         TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-COD-AOOC
                                                IN WSS-VARI(WSS-IFLD).
       NXT-PUT-TAB-ARC.
           MOVE 'ARC-KEY-IARC' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM LDC-FIO-ARC.
           IF FIO-STAT-OKS AND
              ARC-KEY-ILDC IN WSS-VARI = ARC-KEY-ILDC IN ARC
              GO TO INI-PUT-TAB-ARC.
       FIN-PUT-TAB-ARC.
           EXIT.

       PUT-TAB-ARG SECTION.
       INI-PUT-TAB-ARG.
           ADD 1 TO WSS-IARG IN WSS-VARI
                    WSS-IFLD IN WSS-VARI.
           IF ARG-IIC-IGAR IN ARG > ZEROES
               MOVE ARG-CAI-IGAR IN ARG TO WSS-COD-TOGA
                                                IN WSS-VARI(WSS-IFLD)
               MOVE ARG-IIC-IGAR IN ARG TO WSS-COD-AOGA 
                                                IN WSS-VARI(WSS-IFLD)
               MOVE ARG-NUM-PORG IN ARG TO WSS-NUM-PORG 
                                                IN WSS-VARI(WSS-IFLD)
      *DAD ini  16-SEP-1992
               MOVE ARG-COD-TOGA IN ARG TO WSS-COD-TGAR
      *DAD fin  16-SEP-1992
                                                IN WSS-VARI(WSS-IFLD)
               GO TO NXT-PUT-TAB-ARG.

           MOVE ARG-NUM-PORG IN ARG TO WSS-NUM-PORG 
                                                 IN WSS-VARI(WSS-IFLD).
      *BUS-TAB Busca Glosa Tipo Operacion Garantia de Tabla
           IF ARG-COD-TOGA IN ARG > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'TIO'               TO TAB-COD-TTAB IN TAB
               MOVE ARG-COD-TOGA IN ARG TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-COD-TOGA
                                                IN WSS-VARI(WSS-IFLD).
      *BUS-TAB Busca Glosa Cod. Auxiliar de Tabla
           IF ARG-COD-AOGA IN ARG > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE SPACES              TO TAB-COD-CTAB IN TAB
               MOVE ARG-COD-TOGA IN ARG TO WSS-TIP-OPE 
               MOVE ARG-COD-AOGA IN ARG TO WSS-STP-OPE 
               MOVE WSS-CLA-OPE         TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-COD-AOGA
                                                IN WSS-VARI(WSS-IFLD).
       NXT-PUT-TAB-ARG.
           MOVE 'ARG-KEY-IARG' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM LDC-FIO-ARG.
           IF FIO-STAT-OKS AND
              ARG-KEY-ILDC IN WSS-VARI = ARG-KEY-ILDC IN ARG
              GO TO INI-PUT-TAB-ARG.
       FIN-PUT-TAB-ARG.
           EXIT.

       GET-CUR-RDC SECTION.                                             
       INI-GET-CUR-RDC.                                                 
           MOVE 0 TO WSS-STAT-RDC IN WSS-VARI.
           MOVE CLF-CAI-ICLF IN SRT TO RDC-CAI-ICLF IN RDC.             
           MOVE CLF-IIC-ICLF IN SRT TO RDC-IIC-ICLF IN RDC.             
           MOVE 'RDC-KEY-ICLF' TO FIO-AKEY.                             
           MOVE FIO-GET-NLS    TO FIO-CMND.                             
           PERFORM LDC-FIO-RDC.                                         
           IF NOT ( FIO-STAT-OKS AND                                          
                    CLF-CAI-ICLF IN SRT = RDC-CAI-ICLF IN RDC AND
                    CLF-IIC-ICLF IN SRT = RDC-IIC-ICLF IN RDC )
              MOVE 1 TO WSS-STAT-RDC IN WSS-VARI.
       FIN-GET-CUR-RDC.                                                 
           EXIT.                                                        
                                                                        
       GET-NXT-RDC SECTION.                                             
       INI-GET-NXT-RDC.                                                 
           MOVE SPACES TO MSG-COD-SIST.                                 
           MOVE SPACES TO MSG-COD-MENS.                                 
           MOVE 'RDC-KEY-ICLF' TO FIO-AKEY.                             
           MOVE FIO-GET-NXT TO FIO-CMND.                                
           PERFORM LDC-FIO-RDC.                                         
           IF FIO-STAT-OKS AND                                          
              CLF-CAI-ICLF IN SRT = RDC-CAI-ICLF IN RDC AND
              CLF-IIC-ICLF IN SRT = RDC-IIC-ICLF IN RDC                 
                MOVE 'M'    TO WSS-IND-MCLI IN WSS-VARI
           ELSE
                MOVE SPACES TO WSS-IND-MCLI IN WSS-VARI.
       FIN-GET-NXT-RDC.                                                 
           EXIT.                                                        

      * Lee con CIC CLIENTE archivo DBC
       GET-RDC-DBC SECTION.
       INI-GET-RDC-DBC.
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
              PERFORM RST-DBC
           ELSE
              MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
              PERFORM CPT-BLKS
              MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC. 
       FIN-GET-RDC-DBC.
           EXIT.

       RST-DBC SECTION.
       INI-RST-DBC.
           MOVE 'CLIENTE INEXISTENTE' TO DBC-GLS-NOMC IN DBC.
           MOVE SPACES TO DBC-NUM-ICLI IN DBC.
           MOVE SPACES TO DBC-VRF-ICLI IN DBC.
           MOVE SPACES TO DBC-IND-ICLI IN DBC.
           MOVE SPACES TO DBC-GLS-ICLI IN DBC.
       FIN-RST-DBC.
           EXIT.

      *Entidades de GNS
       COPY GNSBFMSC.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBBMSC.
       COPY GNSBGCPT.
      *Entidades de TAB
       COPY TABBFOFI.
       COPY TABBFUSR.
       COPY TABBVOFI.
       COPY TABBBOFI.
       COPY TABBBUSR.
       COPY TABBVUSR.
      *Entidades de LDC
      *<<<< NOT PGM_DB2
       COPY LDCBFLDC.
       COPY LDCBFARC.
       COPY LDCBFARG.
       COPY LDCBFRDC.
       COPY SGCBFDBC.
      *>>>>
      *>>>>
