       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.     GARPSIDP.
       AUTHOR.          CONSIST.

      * Subrutina Escribe Registros Asociados a un Deposito a Plazo de l
      *    propia institucion
      * ES INVOCADO DESDE GARPAGDG (VMS) O GARPUGDG (IBM)
      * Escribe registros GDO, GVT necesariamente
      *   utilizando el CIC de la Garantia
      *      GDG-NUM-SIS IN IDP-VARI
      *      GDG-COD-CRR IN IDP-VARI
      *   utilizando el CIC de la Garantia y el CIC del Deposito
      *      FRM-CAI-IOPD IN IDP-VARI
      *      FRM-IIC-IOPD IN IDP-VARI
      * Escribe registros GDD, opcionalmente, de acuerdo al parametro
      *  de llamada FRM-COD-DEUD ('S'= si lo hace, 'N' no lo hace )
      * Si hay un error devuelve MSG-COD-MENS con el codigo del mensaje
      * El error se maneja en programa llamador

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSBRTAB.
       COPY GNSBRMSC.
       COPY GNSBRMSG.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       COPY GNSWGPES.
       COPY GNSWGFRM.
       COPY GNSWVIDD.
       01  SCR-VARI.
           03 SCR-SIST             VALUE 'GAR' PIC X(03).
           03 SCR-QIDD                         PIC X(08).
           03 SCR-LIDD                    COMP PIC S9(04).
      * Para utilizar codigo estandar .SRC
           03 SCR-CCPP             VALUE 'ING' PIC X(03).

       COPY GARBRGDD.
       COPY GARBRGDO.
       COPY GARBRGVT.

       COPY DAPBROPD.
       COPY DAPBRRCC.

       COPY GARWGIDP.

       01  WSS-VARI.
           03 WSS-IMAL                  VALUE 0           PIC 9(01).
           03 CIV-SIV-EMI.
              05 GAR-CIV-EMI                              PIC X(03).
              05 GAR-SIV-EMI                              PIC X(03).
           03 GAR-VCB              VALUE SPACES           PIC X(06).

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCC-REQA==.

       LINKAGE SECTION.
      *---------------
       01  DFHCOMMAREA.
           02 FILLER                               PIC X(430).
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
      *------------
       INI-MAIN.
           PERFORM INI.

      * Graba GDD si corresponde
           IF FRM-COD-DEUD IN IDP-VARI NOT = 'S'
               GO TO ESC-GDO.

      * Busca Cliente(s) del OPD de DAP mediante RCC
           MOVE FRM-CAI-IOPD IN IDP-VARI TO RCC-CAI-IOPD IN RCC.
           MOVE FRM-IIC-IOPD IN IDP-VARI TO RCC-IIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF NOT (
                  FIO-STAT-OKS AND
                  FRM-CAI-IOPD IN IDP-VARI = RCC-CAI-IOPD IN RCC AND
                  FRM-IIC-IOPD IN IDP-VARI = RCC-IIC-IOPD IN RCC
                  )
               MOVE 'GAR'          TO MSG-COD-SIST
               MOVE 'RCC    NEX'   TO MSG-COD-MENS
               GO TO EXT-MAIN.

      * Graba GDD
           MOVE SPACES TO GDD-GLS-FLAG IN GDD.
           MOVE SPACES TO GDD-MSC-TACC IN GDD.
           MOVE SPACES TO GDD-TML-ACT  IN GDD.
           MOVE SPACES TO GDD-IDN-USU-ING IN GDD.
           MOVE SPACES TO GDD-IDN-USU-ACT IN GDD.
           MOVE 'I'    TO GDD-MSC-STAT IN GDD.

           PERFORM GET-FHOY.
           MOVE HOY-FHOY TO GDD-FEC-ULT-ACT.
           MOVE HOY-HHOY TO GDD-HOR-ACT.
           MOVE HOY-FHOY TO GDD-FEC-ING-SIS IN GDD.
           IF SCR-CCPP = 'ING'
               MOVE 'I'      TO GDD-MSC-TACC    IN GDD

           ELSE
               MOVE 'M'      TO GDD-MSC-TACC    IN GDD.
           PERFORM LUP-ESC-GDD UNTIL
              NOT (
                  FIO-STAT-OKS AND
                  FRM-CAI-IOPD IN IDP-VARI = RCC-CAI-IOPD IN RCC AND
                  FRM-IIC-IOPD IN IDP-VARI = RCC-IIC-IOPD IN RCC
                  ).

      * Graba GDO
       ESC-GDO.
           MOVE SPACES TO GDO-GLS-FLAG IN GDO.
           MOVE SPACES TO GDO-MSC-TACC IN GDO.
           MOVE SPACES TO GDO-TML-ACT  IN GDO.
           MOVE SPACES TO GDO-IDN-USU-ING IN GDO.
           MOVE SPACES TO GDO-IDN-USU-ACT IN GDO.
           MOVE 'I'    TO GDO-MSC-STAT IN GDO.

           PERFORM GET-FHOY.
           MOVE HOY-FHOY TO GDO-FEC-ULT-ACT.
           MOVE HOY-HHOY TO GDO-HOR-ACT.
           MOVE HOY-FHOY TO GDO-FEC-ING-SIS IN GDO.
           IF SCR-CCPP = 'ING'
               MOVE 'I'      TO GDO-MSC-TACC    IN GDO

           ELSE
               MOVE 'M'      TO GDO-MSC-TACC    IN GDO.
           MOVE FRM-CAI-IOPD IN IDP-VARI TO GAR-NUM-SIS IN GDO.
           MOVE FRM-IIC-IOPD IN IDP-VARI TO GAR-COD-CRR IN GDO.
           MOVE GAR-CLV-GDO-1 IN GDO     TO GAR-COD-DOC IN GDO.
           MOVE GAR-ID IN IDP-VARI       TO GAR-CLV-GDO-1 IN GDO.
           MOVE OPD-COD-COPD IN OPD      TO GAR-TIP-DOC IN GDO.
           MOVE OPD-FEC-FREA IN OPD      TO GAR-FEC-CAP IN GDO.

      *BUS-TAB Busca codigo del propio Banco
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'BCO' TO TAB-CIC-TTAB IN TAB.
           MOVE '016' TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = SPACES
               MOVE 'BCO'                   TO GAR-TIV-EMI IN GDO
               MOVE TAB-COD-CTAB IN TAB     TO CIV-SIV-EMI IN WSS-VARI
               MOVE GAR-CIV-EMI IN WSS-VARI TO GAR-CIV-EMI IN GDO
               MOVE GAR-SIV-EMI IN WSS-VARI TO GAR-SIV-EMI IN GDO
           ELSE
               MOVE 'GNS' TO MSG-COD-SIST
               GO TO EXT-MAIN.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GDO.
           IF NOT FIO-STAT-OKS
               MOVE 'GAR'        TO MSG-COD-SIST
               MOVE 'GDO    PUT' TO MSG-COD-MENS
               GO TO EXT-MAIN.

      * Graba GVT
       ESC-GVT.
           MOVE SPACES TO GVT-GLS-FLAG IN GVT.
           MOVE SPACES TO GVT-MSC-TACC IN GVT.
           MOVE SPACES TO GVT-TML-ACT  IN GVT.
           MOVE SPACES TO GVT-IDN-USU-ING IN GVT.
           MOVE SPACES TO GVT-IDN-USU-ACT IN GVT.
           MOVE 'I'    TO GVT-MSC-STAT IN GVT.

           PERFORM GET-FHOY.
           MOVE HOY-FHOY TO GVT-FEC-ULT-ACT.
           MOVE HOY-HHOY TO GVT-HOR-ACT.
           MOVE HOY-FHOY TO GVT-FEC-ING-SIS IN GVT.
           IF SCR-CCPP = 'ING'
               MOVE 'I'      TO GVT-MSC-TACC    IN GVT

           ELSE
               MOVE 'M'      TO GVT-MSC-TACC    IN GVT.
           MOVE GAR-NUM-SIS  IN IDP-VARI TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR  IN IDP-VARI TO GAR-COD-CRR IN GVT.
           MOVE +1                       TO GAR-COD-TSN IN GVT.
           MOVE OPD-FEC-FREA IN OPD      TO GAR-FEC-TSN IN GVT.

           MOVE OPD-VAL-SCAP IN OPD      TO GAR-VAL-CML-TSN IN GVT.
           MOVE OPD-VAL-SCAP IN OPD      TO GAR-VAL-LIQ-TSN IN GVT.

           MOVE GAR-PCT-PON  IN IDP-VARI TO GAR-PCT-PON     IN GVT.
      * Calcula Valor Ponderado
           COMPUTE GAR-VAL-PON IN GVT ROUNDED =
                   GAR-VAL-LIQ-TSN IN GVT *
                   GAR-PCT-PON IN IDP-VARI / 100.

           IF GAR-VCB IN IDP-VARI = GAR-VCB IN WSS-VARI
               MOVE GAR-VAL-PON  IN GVT TO
                    PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO
                    GAR-VAL-PON  IN GVT.

           MOVE GAR-PCT-AJT-SUP IN IDP-VARI TO GAR-PCT-AJT-SUP IN GVT.
      * Calcula Valor Ajustado SBIF
           COMPUTE   GAR-VAL-AJT-SUP IN GVT ROUNDED =
                     GAR-VAL-CML-TSN IN GVT -
                   ( GAR-VAL-CML-TSN IN GVT *
                     GAR-PCT-AJT-SUP IN IDP-VARI / 100 ).

           IF GAR-VCB IN IDP-VARI = GAR-VCB IN WSS-VARI
               MOVE GAR-VAL-AJT-SUP IN GVT TO
                    PES-SGV-DECI    IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE    IN PES-VARI TO
                    GAR-VAL-AJT-SUP IN GVT.

      * Se contabiliza por valor ajustado, luego debe ser > 0
      *    salvo en el ingreso con Tasaciones Parciales
           IF GAR-VAL-AJT-SUP IN GVT NOT > ZEROES
               MOVE 'GAR'      TO MSG-COD-SIST
               MOVE 'GVTAJT=0' TO MSG-COD-MENS
               GO TO EXT-MAIN.

      *    Obtiene Codigo Tasador = Ejecutivo Mediante CIC  '997'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'TAS'          TO TAB-CIC-TTAB IN TAB.
           MOVE '997'          TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'TAS997 NEX'   TO MSG-COD-MENS
               GO TO EXT-MAIN
           ELSE
               MOVE TAB-COD-CTAB IN TAB TO GAR-IIV-TSD  IN GVT
               MOVE SPACES              TO GAR-IIV-SVS  IN GVT.

           MOVE 'A'                     TO GVT-MSC-STAT IN GVT.
           MOVE SPACES                  TO GAR-TIP-CTR  IN GVT.
           MOVE ZEROES                  TO GAR-M2-TRR   IN GVT.
           MOVE SPACES                  TO GAR-COD-UMD  IN GVT.
           MOVE GAR-FEC-TSN IN GVT      TO GAR-FEC-RVL  IN GVT.
           MOVE ZEROES                  TO GAR-M2-CTR   IN GVT.
           MOVE SPACES                  TO GAR-TIP-MAQ-EQP IN GVT.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               MOVE 'GAR'          TO MSG-COD-SIST
               MOVE 'GVT    PUT'   TO MSG-COD-MENS
               GO TO EXT-MAIN.

       EXT-MAIN.
           PERFORM FIN.
       FIN-MAIN.
           EXIT.

       INI SECTION.
       INI-INI.
       COPY GNSBGEDB.
           MOVE DFHCOMMAREA          TO IDP-CMMA IN IDP-VARI.
           MOVE IDP-PROG             TO FIO-PROG.
      *Buscar Independencia de Datos
           MOVE IDP-QIDD TO SCR-QIDD.
           MOVE IDP-LIDD TO SCR-LIDD.
           PERFORM GNS-BUS-IDD.
      *Carga
           MOVE IDP-ROPD IN IDP-VARI TO OPD.
      * Limpia Variables de Retorno
           MOVE SPACES  TO MSG-COD-SIST
                           MSG-COD-MENS.

      *    Obtiene Codigo Valor de Cambio de Pesos Mediante CIC de Moned
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               GO TO EXT-MAIN
           ELSE
               MOVE TAB-COD-CTAB IN TAB TO GAR-VCB IN WSS-VARI.
       FIN-INI.
           EXIT.

       FIN SECTION.
       INI-FIN.
           MOVE MSG-COD-SIST TO IDP-COD-SIST IN IDP-VARI.
           MOVE MSG-COD-MENS TO IDP-COD-MENS IN IDP-VARI.
           MOVE IDP-CMMA IN IDP-VARI TO DFHCOMMAREA.
           MOVE +0 TO EIBRESP.

       COPY GNSBGGBK.
       FIN-FIN.
           EXIT.

       LUP-ESC-GDD SECTION.
       INI-LUP-ESC-GDD.
           MOVE GAR-NUM-SIS  IN IDP-VARI TO GAR-NUM-SIS       IN GDD.
           MOVE GAR-COD-CRR  IN IDP-VARI TO GAR-COD-CRR       IN GDD.
           MOVE RCC-CIC-ICLI IN RCC      TO GDD-CIC-ICLI      IN GDD.

           MOVE ZEROES                     TO GAR-VAL-LIM-DDR IN GDD.
           IF RCC-VAL-PRCO IN RCC NOT > ZEROES
               MOVE +100                   TO GAR-PCT-LIM-DDR IN GDD
           ELSE
               MOVE RCC-VAL-PRCO IN RCC    TO GAR-PCT-LIM-DDR IN GDD.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GDD.
           IF NOT FIO-STAT-OKS
               MOVE 'GAR'        TO MSG-COD-SIST
               MOVE 'GDD    PUT' TO MSG-COD-MENS
               GO TO FIN-LUP-ESC-GDD.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
       FIN-LUP-ESC-GDD.
           EXIT.

       COPY GNSBGSYS.
       COPY GNSBGHOY.
       COPY GNSBGPES.
       COPY GNSBBTAB.
       COPY GNSBBIDD.
       COPY GNSBGDTC.
       COPY GNSBGMSG.
       COPY GNSBIABT.

       COPY GNSBFTAB.
       COPY GNSBFMSC.
       COPY GNSBFMSG.

           COPY GARBFGDD.
           COPY GARBFGDO.
           COPY GARBFGVT.

           COPY DAPBFRCC.

       BCK-OUT SECTION.
       INI-BCK-OUT.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           PERFORM GAR-FIO-GDO.
           IF FIO-STAT-OKS
               MOVE 'GAR'    TO MSG-COD-SIST
               MOVE 'BCKOUT' TO MSG-COD-MENS
               MOVE '49'     TO FIO-STAT.
       FIN-BCK-OUT.
           EXIT.


