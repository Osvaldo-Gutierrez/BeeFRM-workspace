       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.  GARPKGDD.
       AUTHOR.      CONSIST.
      **************************************************************
      *  OBJETIVO :                                                *
      *           TRASPASAR REGISTRO GDD DE BASE ANTIGUA A NUEVA   *
      *                                                            *
      ********************* ARCHIVOS  USADOS************************
      *         INPUT : GDD FORMATO BCI IDEAL                      *
      *                                                            *
      *         OUT   : GDD FORMATO CONSIST                        *
      *                                                            *
      **************************************************************

       ENVIRONMENT DIVISION.
      *=======================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
             DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *------------------------

       77    WK-CONT-GDD-LEIDOS        PIC 9(06)             VALUE 0.
       77    WK-CONT-GDD-GRABADOS      PIC 9(06)             VALUE 0.

       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSBRERR.
       COPY GNSBRCIC.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ODD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.

       01  ODD.
      *
      *    Clave Primaria Registro
           03  GAR-CLV-GDD-1.
      *
      *        Cod. Int. Comp. Garantia
               05  GAR-ID.
      *
      *                    Cod. Int. Comp. GARANTIA
                           09  GAR-NUM-SIS              PIC X(04).
      *
      *                    Nro Correl. de la Garantia
                           09  GAR-COD-CRR              PIC X(08).
      *
               05 GAR-RUT-DDR                           PIC 9(08).
               05 GAR-IRT-DDR                           PIC X(01).
               05 GAR-GRT-DDR                           PIC X(03).
      *
           03 GAR-VRT-DDR                               PIC X(01).
      *
      *    Monto limite por deudor
           03  GAR-VAL-LIM-DDR          COMP-3          PIC S9(12)V9(3).

      *
      *    Porcentaje Limite Por Deudor
           03  GAR-PCT-LIM-DDR          COMP-3          PIC S9(3)V9(2).
      *
      *    Fecha de Ingreso Registro GDD
           03  GDD-FEC-ING-SIS.
                   04  GDD-FA-ING-SIS                   PIC 9(04).
                   04  GDD-FM-ING-SIS                   PIC 9(02).
                   04  GDD-FD-ING-SIS                   PIC 9(02).
      *
      *    Fecha Ult. Actualiz. Deudor
           03  GDD-FEC-ULT-ACT.
                   04  GDD-FA-ULT-ACT                   PIC 9(04).
                   04  GDD-FM-ULT-ACT                   PIC 9(02).
                   04  GDD-FD-ULT-ACT                   PIC 9(02).
      *
      *    Hora Ult. Actualiz. Deudor
           03  GDD-HOR-ACT                              PIC 9(06).
      *
      *    Terminal Ult. Actualiz. Deudor
           03  GDD-TML-ACT                              PIC X(04).
      *
      *    Usuario Ult. Actualiz. Deudor
           03  GDD-IDN-USU-ACT                          PIC X(12).
      *
      *    Usuario Que Ingreso Deudor
           03  GDD-IDN-USU-ING                          PIC X(12).
      *
           03  GDD-GLS-DISP                             PIC X(20).


       COPY GARBRGDD.
       COPY GARBRGDG.

       COPY SGCBRDBC.
       COPY SGCBRDCE.
       COPY SGCBRDCP.
       COPY SGCBRRCD.

      *  VARIABLES DE WORKING PARA RECUPERAR CIC DEL DIR. CLTES.        
       01  UIB.                                                         
           02 FILLER            PIC X(32)      VALUE 'INCCMSGC'.        
       01  REQ-AREA1.                                                   
           02 COD-CMND1         PIC X(5).                               
           02 NOM-TABL1         PIC X(3)       VALUE 'DBC'.             
           02 NOM-CLAV1         PIC X(5).                               
           02 RET-CODI1         PIC X(2)       VALUE SPACES.            
           02 RET-INTE1         PIC X.                                  
           02 NUM-BASE1         PIC S9(4) COMP VALUE +100.              
           02 NOM-REGI1         PIC X(7).                               
           02 FILLER            PIC X(25).                              
           02 MAX-CONT1         PIC S9(4) COMP.                         
           02 NOM-CONT1         PIC S9(4) COMP.                         
           02 FILLER            PIC X(22).                              
           02 VAL-CLAV1         PIC X(12).                              
      *                                                                 
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *                                                                 
       01  REQ-AREA.                                                    
           02 REQ-CMD              PIC X(5).                            
           02 REQ-TBLNM            PIC X(3).                            
           02 REQ-KEYNM            PIC X(5).                            
           02 REQ-RETURN-CODE      PIC X(2) VALUE SPACES.               
           02 REQ-INT-RTN-CD       PIC X(1).                            
           02 REQ-DBID             PIC S9(4) COMP VALUE +100.           
           02 REQ-TBLID            PIC X(7).                            
           02 FILLER               PIC X(25).                           
           02 REQ-COUNT-MAX        PIC S9(4) COMP.                      
           02 REQ-IO-COUNT         PIC S9(4) COMP.                      
           02 FILLER               PIC X(22).                           
           02 REQ-KEY-VALUE        PIC X(24).                           

                                                                 
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *                                                                 
       01  ELEM-LIST.                                                   
           03 ELEMEN-1       PIC X(05).                                 
           03 ELEM-SEC       PIC X(01).                                 
           03 FILLER         PIC X(05) VALUE SPACES.                    
                                                                        
       01  ELEM-LIST1.                                                  
           02 NOM-ELEM1         PIC X(11).                              
       01  REQ-AREA2.                                                   
           02 COD-CMND2         PIC X(5).                               
           02 NOM-TABL2         PIC X(3)       VALUE 'CIC'.             
           02 NOM-CLAV2         PIC X(5).                               
           02 RET-CODI2         PIC X(2)       VALUE SPACES.            
           02 RET-INTE2         PIC X.                                  
           02 NUM-BASE2         PIC S9(4) COMP VALUE +180.              
           02 NOM-REGI2         PIC X(7).                               
           02 FILLER            PIC X(25).                              
           02 MAX-CONT2         PIC S9(4) COMP.                         
           02 NOM-CONT2         PIC S9(4) COMP.                         
           02 FILLER            PIC X(22).                              
           02 VAL-CLAV2         PIC X(012).                             
       01  ELEM-LIST2.                                                  
           02 NOM-ELEM2         PIC X(11).                              

       01  WS-VARIOS.                                                   
           05  BLANCO           PIC X(12) VALUE SPACES.                 
           05  WS-FECHA.                                                
               10  WS-MES       PIC X(002).                             
               10  FILLER       PIC X(001).                             
               10  WS-DIA       PIC X(002).                             
               10  FILLER       PIC X(001).                             
               10  WS-ANO       PIC X(002).                             
           05  WS-PARAMETROS.                                           
               10  WS-GLOSA     PIC X(075).                             
               10  WS-CIC       PIC X(012).                             
               10  WS-CODRET    PIC X(001).                             
               10  WS-RUT       PIC X(008).                             
               10  WS-DIG       PIC X(001).                             
               10  WS-OFI       PIC X(003).                             
                                                                        
       01  REG-LISTADO.                                                 
          05  LIST-CIC-ICLI        PIC X(012).                          
          05  LIST-IDC-ICLI        PIC X(008).                          
          05  LIST-VRF-ICLI        PIC X(001).                          
          05  LIST-COD-OFIC        PIC X(003).                          
          05  LIST-FEC-TTRN        PIC X(008).                          
          05  LIST-GLS-NOMC        PIC X(075).                          
      *                                                                 

      *------------------------------------------------------------*
      *                                                            *
      *                   PROGRAMA PRINCIPAL                       *
      *                                                            *
      *------------------------------------------------------------*

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           PERFORM INI.
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN.


       LUP-MAIN.
           ADD 1 TO WK-CONT-GDD-LEIDOS.

           MOVE GAR-NUM-SIS     IN ODD TO GAR-NUM-SIS     IN GDG.
           MOVE GAR-COD-CRR     IN ODD TO GAR-COD-CRR     IN GDG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               MOVE 'GDG    NEX'         TO ERR-COD-MENS IN ERR
               MOVE GAR-CLV-GDG-1 IN GDG TO ERR-COD-CMPO IN ERR
               MOVE ADR-GDG-REQA         TO ADR-REQA
               PERFORM PRO-ERR
               GO TO NXT-MAIN.
           MOVE GAR-NUM-SIS     IN ODD TO GAR-NUM-SIS     IN GDD.
           MOVE GAR-COD-CRR     IN ODD TO GAR-COD-CRR     IN GDD.
           MOVE GAR-RUT-DDR     IN ODD TO DBC-NUM-ICLI    IN DBC.
           MOVE GAR-IRT-DDR     IN ODD TO DBC-IND-ICLI    IN DBC.
           MOVE GAR-GRT-DDR     IN ODD TO DBC-GLS-ICLI    IN DBC.
           MOVE 'DBC-IDC-ICLI'  TO FIO-AKEY.
           MOVE FIO-GET-KEY     TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
      *FLB CST  29-NOV-1994
               PERFORM CONSULTA-CREA-CIC.

           MOVE DBC-KEY-IDBC    IN DBC TO GDD-CIC-ICLI    IN GDD.
           MOVE GAR-VAL-LIM-DDR IN ODD TO GAR-VAL-LIM-DDR IN GDD.
           MOVE GAR-PCT-LIM-DDR IN ODD TO GAR-PCT-LIM-DDR IN GDD.
           MOVE GDD-FEC-ING-SIS IN ODD TO GDD-FEC-ING-SIS IN GDD.
           MOVE GDD-FEC-ULT-ACT IN ODD TO GDD-FEC-ULT-ACT IN GDD.
           MOVE GDD-HOR-ACT     IN ODD TO GDD-HOR-ACT     IN GDD.
           MOVE GDD-TML-ACT     IN ODD TO GDD-TML-ACT     IN GDD.
           MOVE GDD-IDN-USU-ACT IN ODD TO GDD-IDN-USU-ACT IN GDD.
           MOVE GDD-IDN-USU-ING IN ODD TO GDD-IDN-USU-ING IN GDD.
           MOVE SPACES                 TO GDD-GLS-DISP    IN GDD.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GDD.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR
               MOVE 'GDD    PUT'    TO ERR-COD-MENS IN ERR
               MOVE SPACES          TO ERR-COD-CMPO IN ERR
               MOVE ADR-GDD-REQA    TO ADR-REQA
               PERFORM PRO-ERR
               PERFORM PRG-ABT.

           ADD 1 TO WK-CONT-GDD-GRABADOS.
       NXT-MAIN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-ODD.
           IF FIO-STAT-OKS
               GO TO LUP-MAIN.

       FIN-MAIN.
           GO TO EXT-MAIN.
           COPY INCBMSGC.
       EXT-MAIN.
           PERFORM C-TERMINAR.
       COPY GNSBGGBK.

       INI SECTION.
       INI-INI.
           MOVE 'GARPKGDD' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           MOVE ERR-TERR-LGC IN ERR-VARI TO ERR-COD-TERR IN ERR.
           MOVE 'GARPKGDD'               TO ERR-COD-ATRN IN ERR.
           MOVE ERR-TTRN-BAT IN ERR-VARI TO ERR-MSC-TTRN IN ERR.
           MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR.
           MOVE 'M' TO ERR-MSC-TACC IN ERR.

      * POSICIONA GDD.

           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-ODD.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR
               MOVE 'ODD    NEX'    TO ERR-COD-MENS IN ERR
               MOVE SPACES          TO ERR-COD-CMPO IN ERR
               MOVE ADR-ODD-REQA    TO ADR-REQA
               PERFORM PRO-ERR
               PERFORM PRG-ABT
           ELSE
               MOVE ERR-SVRT-REC IN ERR-VARI TO ERR-IND-SVRT IN ERR.

       FIN-INI.
           EXIT.

       C-TERMINAR SECTION.
      *-----------
       INI-C-TERMINAR.
           DISPLAY '==========================================='.
           DISPLAY '= REGS. GDD LEIDOS .................'
                      WK-CONT-GDD-LEIDOS ' ='.
           DISPLAY '= REGS. GDD GRABADOS................'
                      WK-CONT-GDD-GRABADOS ' ='.
           DISPLAY ' '.
           DISPLAY '==========================================='.
       FIN-C-TERMINAR.
           EXIT.

       COPY GARBFODD.
       COPY GARBFGDD.
       COPY GARBFGDG.
       COPY SGCBFDBC.

       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBFCIC.
       COPY GNSBGERR REPLACING ==FIO-STAT IN FIO-VARI== BY ==FIO-STAT==.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.

