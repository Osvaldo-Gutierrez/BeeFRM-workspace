       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      CTBPBBCC.
       AUTHOR.          CONSIST.

      * BALANCE CUENTAS CONTABLES

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
      * INPUT-OUTPUT SECTION.
      *--------------------
      * FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY CTBBRSLD.
       COPY CTBBRBCC.
       01  WSS-VARI.
      *
      *    Cuenta Contable
           03  WSS-NUM-CCTB.
      *        Partida
               05  WSS-COD-PART.
      *            Gran Grupo
                   07  WSS-IND-GGRP                 PIC X(01).
      *            Grupo
                   07  WSS-IND-GRUP                 PIC X(01).
      *            Sub Grupo
                   07  WSS-IND-SGRP                 PIC X(01).
      *        Cuenta ( Nivel Mayor )
               05  WSS-COD-CMAY                     PIC X(02).
      *        Subcuenta ( Nivel Detalle )
               05  WSS-COD-SCTA                     PIC X(03).
      *


       COPY GNSWCFIO.
       COPY GNSWVFIO.

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           PERFORM INI.
           PERFORM PRO-BCC UNTIL NOT FIO-STAT-OKS.
           PERFORM FIN.
           STOP RUN.
       FIN-MAIN.
           EXIT.

       INI SECTION.
       INI-INI.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM CTB-FIO-BCC.
      *  OBTENER FST-SLD
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ARCHIVO SALDOS VACIO :  ABORTO'
               STOP RUN.
       FIN-INI.
           EXIT.

       PRO-BCC SECTION.
       INI-PRO-BCC.
           IF SLD-IND-TREG IN SLD NOT = '1'
               GO TO LAB-NXT-SLD.
           MOVE SLD-NUM-CTB1 TO WSS-NUM-CCTB.
           PERFORM PRO-NIV.
           MOVE ZEROES TO WSS-COD-SCTA.
           PERFORM PRO-NIV.
           MOVE ZEROES TO WSS-COD-CMAY.
           PERFORM PRO-NIV.
           MOVE ZEROES TO WSS-IND-SGRP.
           PERFORM PRO-NIV.
           MOVE ZEROES TO WSS-IND-GRUP.
           PERFORM PRO-NIV.
       LAB-NXT-SLD.
           PERFORM NXT-SLD.
       FIN-PRO-BCC.
            EXIT.

       PRO-NIV SECTION.
       INI-PRO-NIV.
           MOVE WSS-NUM-CCTB TO BCC-NUM-CCTB.
           MOVE ZEROES TO BCC-VAL-SDEU.
           MOVE ZEROES TO BCC-VAL-SACR.
           MOVE ZEROES TO BCC-VAL-ADEB.
           MOVE ZEROES TO BCC-VAL-AHAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM CTB-FIO-BCC.
           ADD SLD-VAL-ADEB TO BCC-VAL-ADEB.
           ADD SLD-VAL-AHAB TO BCC-VAL-AHAB.
           IF BCC-VAL-ADEB > BCC-VAL-AHAB
              MOVE ZEROES TO BCC-VAL-SACR
              COMPUTE BCC-VAL-SDEU = BCC-VAL-ADEB - BCC-VAL-AHAB
           ELSE
              MOVE ZEROES TO BCC-VAL-SDEU
              COMPUTE BCC-VAL-SACR = BCC-VAL-AHAB - BCC-VAL-ADEB.
           IF FIO-STAT-OKS
               MOVE FIO-MOD TO FIO-CMND
               PERFORM CTB-FIO-BCC
           ELSE
               MOVE FIO-PUT TO FIO-CMND
               PERFORM CTB-FIO-BCC.
       FIN-PRO-NIV.
           EXIT.

       NXT-SLD SECTION.
       INI-NXT-SLD.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
       FIN-NXT-SLD.
           EXIT.

       FIN SECTION.
       FIN-INI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM CTB-FIO-BCC.
       FIN-FIN.
           EXIT.

       COPY CTBBFSLD.
       COPY CTBBFBCC.
