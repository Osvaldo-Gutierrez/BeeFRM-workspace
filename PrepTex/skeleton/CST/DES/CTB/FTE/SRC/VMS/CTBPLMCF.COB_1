       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      CTBPLMCF.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    25-Sep-89 04:12 PM.

      * Programa Listador de Reporte MCF con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
           SELECT GNSSRT         ASSIGN TO 'SYS$DISK:'.
           SELECT RPTMCF      ASSIGN TO RPTMCF.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY CTBRRMCF.
       FD  RPTMCF LABEL RECORDS OMITTED
           REPORT IS RPT-MCF
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY CTBBRHMV.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY CTBRWMCF.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
       COPY CTBBRHCP.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       01  VARI.
           03 WSS-VAL-DDOH                            PIC 9(12).
           03 WSS-VAL-DDOS                            PIC 9(12).
           03 WSS-IND-FINA  VALUE 0                   PIC 9(01).
       01  WSS-PER.
           03 WSS-NUM-MPER                            PIC 9(02).
           03 WSS-NUM-SPER                            PIC 9(02).
           03 WSS-NUM-APER                            PIC 9(02).

       REPORT SECTION.
      *--------------
       COPY CTBRFMCF.
           .
       COPY CTBRTMCF.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-MCF-HMV-COD-FINA SECTION.
           USE BEFORE REPORTING CH-MCF-HMV-COD-FINA.
       INI-CH-MCF-HMV-COD-FINA.
       FIN-CH-MCF-HMV-COD-FINA.
           EXIT.
       DEC-CF-MCF-HMV-COD-FINA SECTION.
           USE BEFORE REPORTING CF-MCF-HMV-COD-FINA.
       INI-CF-MCF-HMV-COD-FINA.
      *    IF WSS-IND-FINA = 0
      *        MOVE 1 TO WSS-IND-FINA
      *        MOVE 1 TO PRINT-SWITCH
      *        GO TO FIN-CH-MCF-HMV-COD-FINA.
           IF SUM-VAL-TSDB > SUM-VAL-TSHB
               COMPUTE WSS-VAL-DDOH = SUM-VAL-TSDB - SUM-VAL-TSHB
               MOVE ZEROES TO WSS-VAL-DDHD IN SRT
               MOVE WSS-VAL-DDOH TO WSS-VAL-DDHH IN SRT
           ELSE
               COMPUTE WSS-VAL-DDOH = SUM-VAL-TSHB - SUM-VAL-TSDB
               MOVE ZEROES TO WSS-VAL-DDHH IN SRT
               MOVE WSS-VAL-DDOH TO WSS-VAL-DDHD IN SRT.
           MOVE SUM-VAL-TSDB TO WSS-VAL-TCFD IN SRT.
           ADD WSS-VAL-DDHD IN SRT TO WSS-VAL-TCFD IN SRT.
           MOVE SUM-VAL-TSHB TO WSS-VAL-TCFH IN SRT.
           ADD WSS-VAL-DDHH IN SRT TO WSS-VAL-TCFH IN SRT.
       FIN-CF-MCF-HMV-COD-FINA.
           EXIT.
       DEC-FF-MCF SECTION.
           USE BEFORE REPORTING FF-MCF.
       INI-FF-MCF.
           IF WSS-VAL-STGD IN WSS-MCF > WSS-VAL-STGH IN WSS-MCF
               COMPUTE WSS-VAL-DDOS = WSS-VAL-STGD IN WSS-MCF 
               - WSS-VAL-STGH IN WSS-MCF
               MOVE ZEROES TO WSS-VAL-DGND IN WSS-MCF
               MOVE WSS-VAL-DDOS TO WSS-VAL-DGNH IN WSS-MCF
           ELSE
               COMPUTE WSS-VAL-DDOS = WSS-VAL-STGH IN WSS-MCF 
               - WSS-VAL-STGD IN WSS-MCF
               MOVE ZEROES TO WSS-VAL-DGNH IN WSS-MCF
               MOVE WSS-VAL-DDOS TO WSS-VAL-DGND IN WSS-MCF.
           MOVE WSS-VAL-STGD IN WSS-MCF TO WSS-VAL-TGND IN WSS-MCF.
           ADD  WSS-VAL-DGND IN WSS-MCF TO WSS-VAL-TGND IN WSS-MCF.
           MOVE WSS-VAL-STGH IN WSS-MCF TO WSS-VAL-TGNH IN WSS-MCF.
           ADD  WSS-VAL-DGNH IN WSS-MCF TO WSS-VAL-TGNH IN WSS-MCF.
       FIN-FF-MCF.
           EXIT.
       DEC-DL-MCF SECTION.
           USE BEFORE REPORTING DL-MCF.
       INI-DL-MCF.
       FIN-DL-MCF.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
           MOVE 'CTBPLMCF' TO FIO-PROG.
           SORT GNSSRT
       COPY CTBRSMCF.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE ZEROES TO WSS-VAL-DDOH.
           MOVE ZEROES TO WSS-VAL-DDOS.
           MOVE ZEROES TO WSS-VAL-DEBE IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-HABE IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-DDHD IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-DDHH IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-TCFH IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-TCFD IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-STGD IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-STGH IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-DGNH IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-DGND IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-TGND IN WSS-MCF.
           MOVE ZEROES TO WSS-VAL-TGNH IN WSS-MCF.
           MOVE 'CTB' TO FIO-SIST.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO TAB: ABORTO'
               STOP RUN.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-HCP.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO HCP: ABORTO'
               STOP RUN.
           DISPLAY ' PERIODO DE PROCESO MMSSAA : '.
           ACCEPT WSS-PER.
           MOVE WSS-NUM-SPER TO WSS-NUM-SANO IN WSS-MCF.
           MOVE WSS-NUM-APER TO WSS-NUM-AANO IN WSS-MCF.
           MOVE 'MES' TO TAB-COD-TTAB.
           MOVE WSS-NUM-MPER TO TAB-COD-CTAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC TO WSS-GLS-MESS. 
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
           MOVE WSS-NUM-SPER TO HMV-NUM-SMOV IN HMV.
           MOVE WSS-NUM-APER TO HMV-NUM-AMOV IN HMV.
           MOVE WSS-NUM-MPER TO HMV-NUM-MMOV IN HMV.
           MOVE ZEROES       TO HMV-NUM-DMOV IN HMV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
           IF WSS-NUM-SPER < HMV-NUM-SMOV IN HMV OR
              WSS-NUM-APER < HMV-NUM-AMOV IN HMV OR
              WSS-NUM-MPER < HMV-NUM-MMOV IN HMV
               GO TO FIN-INP-SRT.
           MOVE HMV-FEC-FMOV IN HMV TO HCP-FEC-FCPB IN HCP.
           MOVE HMV-NUM-COMP IN HMV TO HCP-NUM-COMP IN HCP.          
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM CTB-FIO-HCP.
           IF FIO-STAT-OKS
               IF HCP-IND-TCOM IN HCP = 'F'
                   MOVE FIO-GET-NXT TO FIO-CMND
                   PERFORM CTB-FIO-HMV
                   GO TO LUP-INP-SRT
               ELSE
                   MOVE HCP-IND-TCOM IN HCP TO WSS-IND-TCOM IN WSS-MCF
           ELSE 
               MOVE SPACES TO WSS-IND-TCOM IN WSS-MCF.
           IF HMV-COD-FINA IN HMV NOT > SPACES
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM CTB-FIO-HMV
               GO TO LUP-INP-SRT
           ELSE
               MOVE 'FIN' TO TAB-COD-TTAB
               MOVE HMV-COD-FINA IN HMV TO TAB-COD-CTAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC TO WSS-GLS-FINA IN WSS-MCF.
           IF HMV-IND-TRAT = 'D' 
               MOVE HMV-VAL-MVTO IN HMV TO WSS-VAL-DEBE IN WSS-MCF
               ADD WSS-VAL-DEBE IN WSS-MCF TO WSS-VAL-STGD IN WSS-MCF
               MOVE ZEROES TO WSS-VAL-HABE IN WSS-MCF
           ELSE
               MOVE HMV-VAL-MVTO IN HMV TO WSS-VAL-HABE IN WSS-MCF
               ADD WSS-VAL-HABE IN WSS-MCF TO WSS-VAL-STGH IN WSS-MCF
               MOVE ZEROES TO WSS-VAL-DEBE IN WSS-MCF.
       MOV-INP-SRT.
       COPY CTBRMMCF.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM CTB-FIO-HMV.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTMCF.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-MCF.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
       GEN-OUT-SRT.
           GENERATE DL-MCF.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-MCF.
           CLOSE RPTMCF.
       EXT-OUT-SRT.
           EXIT.

       COPY CTBBFHMV.
       COPY GNSBBSYS.

       COPY CTBBFHCP.
       COPY GNSBSTAB.
       COPY GNSBSMSG.
       COPY GNSBFTAB.
       COPY GNSBFMSG.

       GNS-PRO-END SECTION.
       INI-GNS-PRO-END.
           STOP RUN.
       FIN-GNS-PRO-END.
           EXIT.
