       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      CTBPLANA.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    28-Sep-89 12:34 PM.

      * Programa Listador de Reporte ANA con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
           SELECT GNSSRT         ASSIGN TO 'SYS$DISK:'.
           SELECT RPTANA      ASSIGN TO RPTANA.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY CTBRRANA.
           03 HMV-NUM-MVTO               PIC 9(03).
       FD  RPTANA LABEL RECORDS OMITTED
           REPORT IS RPT-ANA
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY CTBBRHMV.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY CTBRWANA.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
       COPY CTBBRHCP.
       COPY CTBBRCTB.
       COPY CTBBRREF.
       01  WSS-VARI.
           03 WSS-FEC-FMES.
              05 WSS-NUM-SMES            PIC 9(02).
              05 WSS-NUM-AMES            PIC 9(02).
           03 WSS-NUM-REFE               PIC 9(06).
           03 WSS-NUM-REFD               PIC 9(12).
           03 WSS-NUM-REFH               PIC 9(12).

       REPORT SECTION.
      *--------------
       COPY CTBRFANA.
           .
       COPY CTBRTANA.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-ANA-CTB-COD-CMAY SECTION.
           USE BEFORE REPORTING CH-ANA-CTB-COD-CMAY.
       INI-CH-ANA-CTB-COD-CMAY.
      * BUSQUEDA CUENTA CONTABLE
           MOVE CTB-COD-PART IN SRT TO CTB-COD-PART IN CTB.
           MOVE CTB-COD-CMAY IN SRT TO CTB-COD-CMAY IN CTB.
           MOVE '000'               TO CTB-COD-SCTA IN CTB.
           MOVE SPACES              TO CTB-VRF-CCTB.
           MOVE SPACES              TO CTB-GLS-CCTB.
           MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM CTB-FIO-CTB.
           CALL 'CTBPFCTB' USING FIO-VARI CTB.
       FIN-CH-ANA-CTB-COD-CMAY.
           EXIT.
       DEC-CF-ANA-CTB-COD-CMAY SECTION.
           USE BEFORE REPORTING CF-ANA-CTB-COD-CMAY.
       INI-CF-ANA-CTB-COD-CMAY.
           IF SUM-VAL-CTAD > SUM-VAL-CTAA
               MOVE ZEROES TO WSS-VAL-CTAA
               COMPUTE WSS-VAL-CTAD = SUM-VAL-CTAD - SUM-VAL-CTAA
           ELSE
               MOVE ZEROES TO WSS-VAL-CTAD
               COMPUTE WSS-VAL-CTAA = SUM-VAL-CTAA - SUM-VAL-CTAD.
       FIN-CF-ANA-CTB-COD-CMAY.
           EXIT.
       DEC-CH-ANA-CTB-COD-SCTA SECTION.
           USE BEFORE REPORTING CH-ANA-CTB-COD-SCTA.
       INI-CH-ANA-CTB-COD-SCTA.
      * BUSQUEDA CUENTA CONTABLE
           MOVE CTB-COD-PART IN SRT TO CTB-COD-PART IN CTB.
           MOVE CTB-COD-CMAY IN SRT TO CTB-COD-CMAY IN CTB.
           MOVE CTB-COD-SCTA IN SRT TO CTB-COD-SCTA IN CTB.
           MOVE SPACES              TO CTB-VRF-CCTB.
           MOVE SPACES              TO CTB-GLS-CCTB.
           MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM CTB-FIO-CTB.
           CALL 'CTBPFCTB' USING FIO-VARI CTB.
       FIN-CH-ANA-CTB-COD-SCTA.
           EXIT.
       DEC-CF-ANA-CTB-COD-SCTA SECTION.
           USE BEFORE REPORTING CF-ANA-CTB-COD-SCTA.
       INI-CF-ANA-CTB-COD-SCTA.
           IF SUM-VAL-SUBD > SUM-VAL-SUBA
               MOVE ZEROES TO WSS-VAL-SUBA
               COMPUTE WSS-VAL-SUBD = SUM-VAL-SUBD - SUM-VAL-SUBA
           ELSE
               MOVE ZEROES TO WSS-VAL-SUBD
               COMPUTE WSS-VAL-SUBA = SUM-VAL-SUBA - SUM-VAL-SUBD.
       FIN-CF-ANA-CTB-COD-SCTA.
           EXIT.
       DEC-DL-ANA SECTION.
           USE BEFORE REPORTING DL-ANA.
       INI-DL-ANA.
       FIN-DL-ANA.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
           MOVE 'CTBPLANA' TO FIO-PROG.
           SORT GNSSRT
       COPY CTBRSANA.
              ON ASCENDING KEY HMV-NUM-MVTO IN SRT
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE ZEROES TO WSS-NUM-REFE IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-REFD IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-REFH IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-SUBD IN WSS-ANA.
           MOVE ZEROES TO WSS-VAL-SUBA IN WSS-ANA.
           MOVE ZEROES TO WSS-VAL-CTAD IN WSS-ANA.
           MOVE ZEROES TO WSS-VAL-CTAA IN WSS-ANA.
           MOVE ZEROES TO WSS-VAL-DEBE IN WSS-ANA.
           MOVE ZEROES TO WSS-VAL-HABE IN WSS-ANA.
           MOVE ZEROES TO WSS-VAL-DEUD IN WSS-ANA.
           MOVE ZEROES TO WSS-VAL-ACRE IN WSS-ANA.
           MOVE '000'  TO WSS-COD-SCTA.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-CTB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO CTB: ABORTO'
               STOP RUN.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-HCP.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO HCP: ABORTO'
               STOP RUN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM CTB-FIO-REF.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO REF: ABORTO'
               STOP RUN.
           DISPLAY ' ANO DE PROCESO SSAA : '.
           ACCEPT WSS-FEC-FMES.
           MOVE WSS-NUM-SMES TO WSS-NUM-SANA.
           MOVE WSS-NUM-AMES TO WSS-NUM-AANA.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
           MOVE WSS-NUM-SANA TO HMV-NUM-SMOV IN HMV.
           MOVE WSS-NUM-AANA TO HMV-NUM-AMOV IN HMV.
           MOVE ZEROES       TO HMV-NUM-MMOV IN HMV.
           MOVE ZEROES       TO HMV-NUM-DMOV IN HMV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
           IF HMV-NUM-SMOV IN HMV > WSS-NUM-SMES OR
              HMV-NUM-AMOV IN HMV > WSS-NUM-AMES
                GO TO FIN-INP-SRT.
           IF HMV-NUM-REFE IN HMV NOT > ZEROES
                MOVE FIO-GET-NXT TO FIO-CMND
                PERFORM CTB-FIO-HMV
                GO TO LUP-INP-SRT.
           MOVE HMV-NUM-CCTB IN HMV TO REF-NUM-CCTB.
           MOVE HMV-NUM-REFE IN HMV TO REF-NUM-REFE.
           PERFORM MOV-REF.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM CTB-FIO-REF.
           IF NOT FIO-STAT-OKS
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM CTB-FIO-REF
               PERFORM ADD-REF
               MOVE FIO-MOD TO FIO-CMND
               PERFORM CTB-FIO-REF.
       
           MOVE HMV-NUM-CCTB TO CTB-NUM-CCTB IN CTB.
           IF HMV-IND-TRAT IN HMV = 'D'
               MOVE HMV-VAL-MVTO IN HMV TO WSS-VAL-DEBE IN WSS-ANA
               MOVE HMV-VAL-MVTO IN HMV TO WSS-VAL-DEUD IN WSS-ANA
               MOVE ZEROES              TO WSS-VAL-HABE IN WSS-ANA
               MOVE ZEROES              TO WSS-VAL-ACRE IN WSS-ANA
           ELSE
               MOVE ZEROES              TO WSS-VAL-DEBE IN WSS-ANA
               MOVE ZEROES              TO WSS-VAL-DEUD IN WSS-ANA
               MOVE HMV-VAL-MVTO IN HMV TO WSS-VAL-HABE IN WSS-ANA
               MOVE HMV-VAL-MVTO IN HMV TO WSS-VAL-ACRE IN WSS-ANA.
       MOV-INP-SRT.
       COPY CTBRMANA.
           MOVE HMV-NUM-MVTO IN HMV TO HMV-NUM-MVTO IN SRT.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM CTB-FIO-HMV.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM CTB-FIO-HMV.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTANA.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-ANA.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
           MOVE HMV-NUM-SMOV IN SRT TO HCP-NUM-SCPB IN HCP.
           MOVE HMV-NUM-AMOV IN SRT TO HCP-NUM-ACPB IN HCP.
           MOVE HMV-NUM-MMOV IN SRT TO HCP-NUM-MCPB IN HCP.
           MOVE HMV-NUM-DMOV IN SRT TO HCP-NUM-DCPB IN HCP.
           MOVE HMV-NUM-COMP IN SRT TO HCP-NUM-COMP IN HCP.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM CTB-FIO-HCP.
           MOVE CTB-COD-PART IN SRT TO REF-COD-PART.
           MOVE CTB-COD-CMAY IN SRT TO REF-COD-CMAY.
           MOVE CTB-COD-SCTA IN SRT TO REF-COD-SCTA.
           MOVE HMV-NUM-REFE IN SRT TO REF-NUM-REFE.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM CTB-FIO-REF.
           IF NOT FIO-STAT-OKS
               DISPLAY 'INCONSISTENCIA EN ARCHIVO REF : ABORTO'
               STOP RUN.
           IF REF-VAL-DEBE = REF-VAL-HABE
               GO TO LUP-OUT-GEN.
           IF HMV-NUM-REFE IN SRT = WSS-NUM-REFE IN WSS-VARI
               ADD WSS-NUM-REFD IN WSS-VARI TO WSS-VAL-DEUD IN SRT
               ADD WSS-NUM-REFH IN WSS-VARI TO WSS-VAL-ACRE IN SRT
           ELSE
               MOVE HMV-NUM-REFE IN SRT TO WSS-NUM-REFE IN WSS-VARI.
           MOVE WSS-VAL-DEUD IN SRT TO WSS-NUM-REFD IN WSS-VARI.
           MOVE WSS-VAL-ACRE IN SRT TO WSS-NUM-REFH IN WSS-VARI.
       GEN-OUT-SRT.
           GENERATE DL-ANA.
       LUP-OUT-GEN.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-ANA.
           CLOSE RPTANA.
       EXT-OUT-SRT.
           EXIT.

       COPY CTBBFHMV.
       COPY GNSBBSYS.

       MOV-REF SECTION.
       INI-MOV-REF.
           IF HMV-IND-TRAT = 'D'
               MOVE HMV-VAL-MVTO IN HMV TO REF-VAL-DEBE
               MOVE ZEROES              TO REF-VAL-HABE
           ELSE
               MOVE ZEROES              TO REF-VAL-DEBE
               MOVE HMV-VAL-MVTO IN HMV TO REF-VAL-HABE.
       FIN-MOV-REF.
           EXIT.

       ADD-REF SECTION.
       INI-ADD-REF.
           IF HMV-IND-TRAT = 'D'
               ADD HMV-VAL-MVTO IN HMV TO REF-VAL-DEBE
           ELSE
               ADD HMV-VAL-MVTO IN HMV TO REF-VAL-HABE.
       FIN-ADD-REF.
           EXIT.

       COPY CTBBFHCP.
       COPY CTBBFCTB.
       COPY CTBBFREF.

       GNS-PRO-END SECTION.
       INI-GNS-PRO-END.
           STOP RUN.
       FIN-GNS-PRO-END.
           EXIT.
