       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      CTBPLBCC.
       AUTHOR.          COCSIST.

      * Programa Listador de Reporte BCC con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
           SELECT GNSSRT           ASSIGN TO 'SYS$DISK:'.
           SELECT RPTBCC         ASSIGN TO RPTBCC.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY CTBRRBCC.
           03 SLD-VAL-ADEB PIC 9(12).
           03 SLD-VAL-AHAB PIC 9(12).
       FD  RPTBCC LABEL RECORDS OMITTED
           REPORT IS RPT-BCC
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY CTBBRSLD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY CTBRWBCC.
       COPY GNSWGSYS.
       COPY CTBBRCTB.
       COPY CTBBRCDC.
       01  WSS-VARI.
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI             PIC 9(02).
              05 WSS-NUM-MINI             PIC 9(02).
              05 WSS-NUM-SINI             PIC 9(02).
              05 WSS-NUM-AINI             PIC 9(02).
           03 WSS-FEC-FFIN.
              05 WSS-NUM-DFIN             PIC 9(02).
              05 WSS-NUM-MFIN             PIC 9(02).
              05 WSS-NUM-SFIN             PIC 9(02).
              05 WSS-NUM-AFIN             PIC 9(02).
           03 WSS-IND-SCTA  VALUE 0       PIC 9(01).

       REPORT SECTION.
      *--------------
       COPY CTBRFBCC.
           .
       COPY CTBRTBCC.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-BCC-CDC-NUM-CCDC SECTION.
           USE BEFORE REPORTING CH-BCC-CDC-NUM-CCDC.
       INI-CH-BCC-CDC-NUM-CCDC.
           MOVE CDC-NUM-CCDC IN SRT TO CDC-NUM-CCDC IN CDC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           CALL 'CTBPFCDC' USING FIO-CMND CDC.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO CDC-VRF-CCDC
               MOVE 'CENTRO DE COSTOS INEXISTENTE' TO CDC-GLS-CCDC.
       FIN-CH-BCC-CDC-NUM-CCDC.
           EXIT.
       DEC-CF-BCC-CDC-NUM-CCDC SECTION.
           USE BEFORE REPORTING CF-BCC-CDC-NUM-CCDC.
       INI-CF-BCC-CDC-NUM-CCDC.
       FIN-CF-BCC-CDC-NUM-CCDC.
           EXIT.
       DEC-CF-BCC-CTB-COD-CMAY SECTION.
           USE BEFORE REPORTING CF-BCC-CTB-COD-CMAY.
       INI-CF-BCC-CTB-COD-CMAY.
           MOVE CTB-COD-PART IN SRT TO CTB-COD-PART IN CTB.
           MOVE CTB-COD-CMAY IN SRT TO CTB-COD-CMAY IN CTB.
           MOVE WSS-COD-SCTA        TO CTB-COD-SCTA IN CTB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           CALL 'CTBPFCTB' USING FIO-CMND CTB.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO CTB-VRF-CCTB
               MOVE 'CUENTA CONTABLE INEXISTENTE' TO CTB-GLS-CCTB.
       FIN-CF-BCC-CTB-COD-CMAY.
           EXIT.
       DEC-PH-BCC SECTION.
           USE BEFORE REPORTING PH-BCC.
       INI-PH-BCC.
       FIN-PH-BCC.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
           SORT GNSSRT
       COPY CTBRSBCC.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           STOP RUN.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE '000' TO WSS-COD-SCTA.
           MOVE ZEROES TO WSS-DEB-SCTA.
           MOVE ZEROES TO WSS-HAB-SCTA.

           DISPLAY 'INGRESE FECHA INICIO DE PROCESO "DDMMSSAA".'
           ACCEPT WSS-FEC-FINI.
           MOVE WSS-NUM-DINI TO WSS-NUM-DDES.
           MOVE WSS-NUM-MINI TO WSS-NUM-MDES.
           MOVE WSS-NUM-SINI TO WSS-NUM-SDES.
           MOVE WSS-NUM-AINI TO WSS-NUM-ADES.

           DISPLAY 'INGRESE FECHA TERMINO DE PROCESO "DDMMSSAA".'
           ACCEPT WSS-FEC-FFIN.
           MOVE WSS-NUM-DFIN TO WSS-NUM-DHAS.
           MOVE WSS-NUM-MFIN TO WSS-NUM-MHAS.
           MOVE WSS-NUM-SFIN TO WSS-NUM-SHAS.
           MOVE WSS-NUM-AFIN TO WSS-NUM-AHAS.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-CTB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO CTB: ABORTO'
               STOP RUN.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-CDC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO CDC: ABORTO'
               STOP RUN.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
           MOVE WSS-FEC-FINI TO SLD-FEC-FSLD.
           MOVE SPACES TO SLD-IND-TREG.
           MOVE SPACES TO SLD-COD-CCCT.
           MOVE SPACES TO SLD-COD-MONE.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
           IF SLD-FEC-FSLD > WSS-FEC-FFIN
               GO TO FIN-MAIN.
           IF SLD-IND-TREG NOT = '2' OR SLD-NUM-CDC2 IN SLD = '00000'
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM CTB-FIO-SLD
               GO TO LUP-INP-SRT.
           MOVE SLD-NUM-CDC2 TO CDC-NUM-CCDC IN CDC.
           MOVE SLD-NUM-CTB2 TO CTB-NUM-CCTB.
           MOVE SLD-VAL-ADEB IN SLD TO SLD-VAL-ADEB IN SRT.
           MOVE SLD-VAL-AHAB IN SLD TO SLD-VAL-AHAB IN SRT.
       MOV-INP-SRT.
       COPY CTBRMBCC.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM CTB-FIO-SLD.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM CTB-FIO-SLD.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           OPEN OUTPUT RPTBCC.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-BCC.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
           MOVE CTB-COD-PART IN SRT TO CTB-COD-PART IN CTB.
           MOVE CTB-COD-CMAY IN SRT TO CTB-COD-CMAY IN CTB.
           MOVE CTB-COD-SCTA IN SRT TO CTB-COD-SCTA IN CTB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM CTB-FIO-CTB.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO WSS-VRF-CCTB
               MOVE 'CUENTA CONTABLE INEXISTENTE' TO WSS-GLS-CCTB
           ELSE
               MOVE CTB-VRF-CCTB IN CTB TO WSS-VRF-CCTB
               MOVE CTB-GLS-CCTB IN CTB TO WSS-GLS-CCTB.
           MOVE SLD-VAL-ADEB IN SRT TO WSS-DEB-SCTA.
           MOVE SLD-VAL-AHAB IN SRT TO WSS-HAB-SCTA.
       GEN-OUT-SRT.
           GENERATE DL-BCC.
           IF WSS-IND-SCTA = 1
               MOVE ZEROES TO WSS-DEB-SCTA.
               MOVE ZEROES TO WSS-HAB-SCTA.
               MOVE ZEROES TO WSS-IND-SCTA.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-BCC.
           CLOSE RPTBCC.

       COPY CTBIOSLD.
       COPY GNSBBSYS.

       COPY CTBIOCTB.
       COPY CTBIOCDC.
