*%! Codigo adicional para DAPPAONP
*% PGM_PTC = TRUE
*% NO_HDR_CST = TRUE
*%! INCLUDE 'GNSSRC:MANEJO_VSI.SRC'
*% IF INI_FIO
      *<<<< INI_FIO
      * JVM: Mantencion 21/09/1992
            PERFORM BUS-OFI-CSSN.
       FIN-INI-FIO.
      * JVM: Fin
      *>>>> INI_FIO
*% END
*% IF WSS
      *<<<< WSS
      *Entidades de GNS
       COPY GNSWGPES.
      *Entidades de TAB
       COPY TABBRCAM.
      * JVM : Mantencion 29/09/1992
       COPY TABBRUSR.
      * JVM :
      *Entidades de DAP
       COPY DAPBROPD.
       COPY DAPBRLIC.
       COPY DAPBRLIQ.
       COPY DAPBRJNL.
       01  WSS-SCR-VARI.
      * ISP: VARIABLES CON NOMBRE SCR-..., PARA USAR RUTINA YA HECHAS
           02 SCR-SGV-VCAM              PIC 9(11)V9(04).
           02 SCR-CIC-VCAM.
              04 SCR-CIC-TVAL           PIC X(01).
              04 SCR-CIC-CVAL           PIC X(03).
              04 SCR-CIC-TCAM           PIC X(08).
           02 WSS-VAL-IPES              PIC 9(11)V9(04).
           02 WSS-IND-IONP              PIC X(01).
           02 SCR-COD-OFIC              PIC X(03).

       01  WSS-VARI.
           03 WSS-OPD-CAI-ISER                          PIC 9(04).
           03 PGM-STAT-LIQ              VALUE '23'      PIC X(02).
                 88 PGM-STAT-LIQ-OKS    VALUE '00'.
                 88 PGM-STAT-LIQ-NUL    VALUE '  '.
           03 PGM-STAT-LIC              VALUE '23'      PIC X(02).
                 88 PGM-STAT-LIC-OKS    VALUE '00'.
                 88 PGM-STAT-LIC-NUL    VALUE '  '.
           03 WSS-ICOR                  VALUE 0         PIC 9(01).
              88 NO-ASIGNE-CORRELATIVO  VALUE 0.
      *>>>> WSS
*% END
*% IF WSS_DTC
      *<<<< WSS_DTC
      *Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LIQ-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LIC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-JNL-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      * JVM : Mantencion 29/09/1992
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
      * JVM :
      *>>>>
*% END
*% IF INI_FIO
      *<<<< INI_FIO
*% IF PGM_PU
      * ISP-INI 31-07-1998 INICIALIZA CAMPOS DE REGISTROS
           PERFORM CLR-REG-ONP.
      * ISP-FIN 31-07-1998 INICIALIZA CAMPOS DE REGISTROS
      *>>>> END
*% END
*% END
*% IF FND_KEY
      *<<<< FND_KEY
      ****  FUERA DE HORARIO - INICIO
           IF NOT SCR-OFRM-SRV OR
                  SCR-CNAL = '150'
                 MOVE 'DAP'         TO FIO-SIST
                 MOVE 'VDS'         TO TAB-COD-TTAB IN TAB
                 MOVE 'PPTPG150045' TO TAB-COD-CTAB IN TAB
                 MOVE FIO-GET-KEY  TO FIO-CMND
                 PERFORM GNS-FIO-TAB
                 IF ( FIO-STAT-OKS AND
                      TAB-IND-VIGE IN TAB = 'N' )
                         MOVE 'S' TO SCR-IERR
                         MOVE 'DAP' TO MSG-COD-SIST
                         MOVE 'ONP3270NODIS'  TO MSG-COD-MENS
                         PERFORM GET-MSG
                         MOVE MSG-GLS-DESC TO FRM-MENS
                         MOVE FIO-STAT-NEX TO FIO-STAT
                         GO TO FIN-FND-KEY.
      ****  FUERA DE HORARIO - FIN
           IF ONP-CAI-IOPD IN ONP-FLD > SPACES OR
              ONP-IIC-IOPD IN ONP-FLD > ZEROES
               PERFORM GET-FRM-OPD
           ELSE
               PERFORM GET-SER-OPD.

           IF NOT PGM-STAT-OPD-OKS
               MOVE 'DAP' TO MSG-COD-SIST
               MOVE 'OPD    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO FRM-MENS
               MOVE '00' TO FIO-STAT
               MOVE SCR-STAT-CON TO SCR-STAT
               GO TO FIN-FND-KEY
           ELSE
               IF NOT ( ONP-CAI-IOPD IN ONP-FLD > SPACES OR
                        ONP-IIC-IOPD IN ONP-FLD > ZEROES )
                         MOVE OPD-CIC-IOPD IN OPD TO
                              ONP-CIC-IOPD IN ONP.
           IF SCR-CCPP = 'ING'
                PERFORM PUT-OPD-ONP.
      *LRH ini 8-sep-1994
           PERFORM BUS-COR-ONP.
      *LRH fin 8-sep-1994
      *>>>>
*% END
*% IF FIN_SCR_TRL
      *<<<< FIN_SCR_TRL
           IF SCR-CCPP = 'MOD'
               IF OPD-COD-IONP IN OPD = 'N'
      *            opd sin orden de no pago
                   MOVE 'DAP' TO MSG-COD-SIST
                   MOVE 'OPD    NOONP' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   MOVE MSG-GLS-DESC TO FRM-MENS
                   MOVE SCR-STAT-CON TO SCR-STAT.

           MOVE 'N' TO WSS-IND-IONP.
           IF OPD-COD-RENO IN OPD = 'N'
               IF OPD-COD-STAT IN OPD = 'G'
                   MOVE 'S' TO WSS-IND-IONP
               ELSE
                   MOVE OPD-CIC-ISER IN OPD TO
                        LIC-CIC-ISER IN LIC
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM DAP-FIO-LIC
                   IF FIO-STAT-OKS
                       IF LIC-COD-STAT IN LIC = 'S'
                           MOVE 'S' TO WSS-IND-IONP.

           IF SCR-CCPP = 'ING'
               IF OPD-COD-IONP IN OPD = 'S'
      *            opd con orden de no pago
                   MOVE 'DAP' TO MSG-COD-SIST
                   MOVE 'OPD    SIONP' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   MOVE MSG-GLS-DESC TO FRM-MENS
                   MOVE SCR-STAT-CON TO SCR-STAT
               ELSE
               IF OPD-COD-RENO IN OPD = 'S'
                   IF OPD-COD-STAT IN OPD NOT = 'G'
      *                opd no activada
                       MOVE 'DAP' TO MSG-COD-SIST
                       MOVE 'OPDRENNOIONP' TO MSG-COD-MENS
                       PERFORM GET-MSG
                       MOVE MSG-GLS-DESC TO FRM-MENS
                       MOVE SCR-STAT-CON TO SCR-STAT
                   ELSE
                       NEXT SENTENCE
               ELSE
               IF WSS-IND-IONP = 'N' AND
                  FRM-FFLD NOT = FRM-FFLD-PF12
      *               OPERACION FIJA NO DISPONIBLE PARA ONP
                      MOVE 'DAP' TO MSG-COD-SIST
                      MOVE 'OPDFIJNOIONP' TO MSG-COD-MENS
                      PERFORM GET-MSG
                      MOVE MSG-GLS-DESC TO FRM-MENS
                      MOVE SCR-STAT-CON TO SCR-STAT.
      *GET-HOY Obtiene Fecha y Hora actual

      *     IF SCR-CMND = 'ING'
      *         PERFORM GET-FHOY
      *         MOVE HOY-DHOY IN HOY-VARI TO ONP-NUM-DING IN ONP-FLD
      *         MOVE HOY-MHOY IN HOY-VARI TO ONP-NUM-MING IN ONP-FLD
      *         MOVE HOY-SHOY IN HOY-VARI TO ONP-NUM-SING IN ONP-FLD
      *         MOVE HOY-AHOY IN HOY-VARI TO ONP-NUM-AING IN ONP-FLD
      *         MOVE HOY-HHOY IN HOY-VARI TO ONP-HRA-HRIN IN ONP-FLD.
      
      *     IF SCR-CMND = 'ALZ'
      *         PERFORM GET-FHOY
      *         MOVE HOY-DHOY IN HOY-VARI TO ONP-NUM-DELI IN ONP-FLD
      *         MOVE HOY-MHOY IN HOY-VARI TO ONP-NUM-MELI IN ONP-FLD
      *         MOVE HOY-SHOY IN HOY-VARI TO ONP-NUM-SELI IN ONP-FLD
      *         MOVE HOY-AHOY IN HOY-VARI TO ONP-NUM-AELI IN ONP-FLD
      *         MOVE HOY-HHOY IN HOY-VARI TO ONP-HRA-HREL IN ONP-FLD.

      *>>>>
*% END
*% IF FIN_HBL_FLD
      *<<<< FIN_HBL_FLD
           MOVE FRM-CPIM-AEY TO OPD-COD-OFIC-ATR IN ONP-FLD.
           IF SCR-CMND = 'ING'
      * JVM: Mantencion 21/09/1992
               MOVE SCR-COD-OFIC TO ONP-COD-OFIC     IN ONP-FLD
               MOVE FRM-CPIM-AEY TO ONP-COD-OFIC-ATR IN ONP-FLD
      *BUS-OFI busca oficina en tablas
               MOVE SCR-COD-OFIC        TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO FRM-GLS-OFOC IN ONP-FLD
      * JVM:
               MOVE FRM-CPIM-AEY TO ONP-COD-OFEL-ATR IN ONP-FLD
           ELSE
           IF SCR-CMND = 'ALZ'
      * JVM: Mantencion 21/09/1992
               MOVE SCR-COD-OFIC TO ONP-COD-OFEL     IN ONP-FLD
               MOVE FRM-CPIM-AEY TO ONP-COD-OFEL-ATR IN ONP-FLD
      *BUS-OFI busca oficina en tablas
               MOVE SCR-COD-OFIC        TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO FRM-GLS-OFEL IN ONP-FLD
               PERFORM GET-FHOY
               MOVE HOY-DHOY IN HOY-VARI TO ONP-NUM-DELI IN ONP-FLD
               MOVE HOY-MHOY IN HOY-VARI TO ONP-NUM-MELI IN ONP-FLD
               MOVE HOY-SHOY IN HOY-VARI TO ONP-NUM-SELI IN ONP-FLD
               MOVE HOY-AHOY IN HOY-VARI TO ONP-NUM-AELI IN ONP-FLD
               MOVE HOY-HHOY IN HOY-VARI TO ONP-HRA-HREL IN ONP-FLD
      * JVM:
               MOVE FRM-CPIM-AEY TO ONP-COD-OFIC-ATR IN ONP-FLD
               MOVE FRM-CPIM-AEY TO ONP-COD-MONP-ATR IN ONP-FLD.
      *>>>> FIN_HBL_FLD
*% END
*% IF CMN_ING
      *<<<< CMN_ING
           PERFORM MOD-OPD.
           IF NOT PGM-STAT-OPD-OKS
               GO TO FIN-CMN-ING.

           PERFORM DEL-LIC.
           IF NOT PGM-STAT-LIC-OKS
               GO TO FIN-CMN-ING.

           PERFORM MOD-LIQ.
           IF NOT PGM-STAT-LIQ-OKS
               GO TO FIN-CMN-ING.

           PERFORM PRO-JNL.
           IF NOT FIO-STAT-OKS
               GO TO FIN-CMN-ING.
      *>>>>
*% END
*% IF FIN_CMN_ING
      *<<<< FIN_CMN_ING
           IF NOT FIO-STAT-OKS
               PERFORM BCK-OUT
      *        Error inconsistencia en el ingreso : informar
               MOVE FIO-MENS TO FRM-MENS
               GO TO FIN-CMN-ING.
      *>>>>
*% END
*% IF CMN_ELI
      *<<<< CMN_ELI
      *LRH ini 8-SEP-1994
           PERFORM GET-FHOY
           IF HOY-FHOY NOT = ONP-FEC-FING IN ONP
               MOVE 'DAP' TO MSG-COD-SIST
               MOVE 'ONPNOESDEHOY' TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO FRM-MENS
               MOVE FIO-STAT-NEX TO FIO-STAT
               GO TO FIN-CMN-ELI.
      *LRH fin 8-SEP-1994
           PERFORM MOD-OPD.
           IF NOT PGM-STAT-OPD-OKS
               GO TO FIN-CMN-ELI.
      *>>>>
*% END
*% IF FIN_CMN_ELI
      *<<<< FIN_CMN_ELI
           PERFORM PRO-JNL.
      *>>>>
*% END
*% IF FIN_CMN_MOD
      *<<<< FIN_CMN_MOD
           IF NOT FIO-STAT-OKS
      *        Error inconsistencia en el ingreso : informar
               MOVE FIO-MENS TO FRM-MENS
               GO TO FIN-CMN-MOD.

           PERFORM MOD-OPD.
           IF NOT PGM-STAT-OPD-OKS
               GO TO FIN-CMN-MOD.
           PERFORM PRO-JNL.
      *>>>>
*% END
*% IF CMN_ING OR CMN_MOD OR CMN_ACC
      *<<<< CMN_ING OR CMN_MOD OR CMN_ACC
           PERFORM RET-HDR.
      *>>>>
*% END
*% IF EOF
      *<<<< EOF
      *LRH ini 8-SEP-1994
       BUS-COR-ONP SECTION.
       INI-BUS-COR-ONP.
           MOVE 99 TO ONP-NUM-CORR IN ONP.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM DAP-FIO-ONP.
           IF NOT ( FIO-STAT-OKS AND
                    ONP-CAI-IOPD IN ONP-FLD = ONP-CAI-IOPD IN ONP AND
                    ONP-IIC-IOPD IN ONP-FLD = ONP-IIC-IOPD IN ONP )
               PERFORM KEY-ALL-ONP
               MOVE ZEROES TO ONP-NUM-CORR IN ONP.
           IF SCR-CCPP = 'ING' OR 'ACT'
               ADD 1 TO ONP-NUM-CORR IN ONP
               MOVE ONP-NUM-CORR IN ONP TO ONP-NUM-CORR IN ONP-FLD
           ELSE
               MOVE ONP-NUM-CORR IN ONP TO ONP-NUM-CORR IN ONP-FLD.
           MOVE 1 TO WSS-ICOR.
       FIN-BUS-COR-ONP.
           EXIT.
      *LRH fin 8-SEP-1994

      *Entidades de DAP
       COPY DAPBFOPD.
       COPY DAPBFLIC.
       COPY DAPBFLIQ.
       COPY DAPBFJNL.

       COPY TABBFCAM.
      * JVM : Mantencion 29/09/1992
       COPY TABBFUSR.
       COPY TABBBUSR.
       COPY TABBVUSR.
      * JVM :
       COPY GNSBGCAM.
       COPY GNSBGPES.

       DEL-LIC SECTION.
       INI-DEL-LIC.
           MOVE OPD-CIC-ISER IN OPD TO LIC-CIC-ISER IN LIC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-LIC.
           IF NOT FIO-STAT-OKS
               MOVE '00' TO PGM-STAT-LIC
           ELSE
           IF LIC-COD-STAT IN LIC NOT = 'S'
               MOVE 'DAP' TO MSG-COD-SIST
               MOVE 'ONPLIC NO=S' TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO FRM-MENS
               MOVE '23' TO PGM-STAT-LIC
           ELSE
               MOVE FIO-DEL TO FIO-CMND
               PERFORM DAP-FIO-LIC
               MOVE FIO-STAT TO PGM-STAT-LIC.
       FIN-DEL-LIC.
           EXIT.

       MOD-LIQ SECTION.
       INI-MOD-LIQ.
           MOVE '00' TO PGM-STAT-LIQ.                                   
           MOVE ONP-CIC-IOPD IN ONP TO LIQ-CIC-IOPD IN LIQ.             
           MOVE 999                 TO LIQ-NUM-ILIQ IN LIQ.             
           MOVE FIO-GET-LEQ         TO FIO-CMND.                        
           PERFORM DAP-FIO-LIQ.                                         
           IF NOT FIO-STAT-OKS                                          
              GO TO FIN-MOD-LIQ.                                        
           IF NOT                                                       
              ( ONP-CIC-IOPD IN ONP = LIQ-CIC-IOPD IN LIQ )             
              GO TO FIN-MOD-LIQ.                                        
                                                                        
           IF NOT                                                       
              ( LIQ-COD-LIQU IN LIQ = 'RC' OR                           
                LIQ-COD-LIQU IN LIQ = 'LT' )                            
                   GO TO FIN-MOD-LIQ.                                   
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           MOVE '00' TO PGM-STAT-LIQ.
           IF FIO-STAT-OKS
               MOVE 'T' TO LIQ-COD-STAT IN LIQ
               MOVE FIO-MOD TO FIO-CMND
               PERFORM DAP-FIO-LIQ
               MOVE FIO-STAT TO PGM-STAT-LIQ
               IF NOT PGM-STAT-LIQ-OKS
                   MOVE 'DAP'        TO MSG-COD-SIST
                   MOVE 'ONPLIQ ERR' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   MOVE MSG-GLS-DESC TO FRM-MENS.
       FIN-MOD-LIQ.
           EXIT.

       MOD-OPD SECTION.
       INI-MOD-OPD.
           MOVE ONP-CIC-IOPD IN ONP TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF FIO-STAT-OKS
               PERFORM SET-IONP
*% IF PGM_PU
      *RVM-ADA-INI 16-OCT-2000
               PERFORM GET-FHOY
               MOVE HOY-FHOY TO OPD-FEC-FTRN IN OPD
               MOVE HOY-HHOY TO OPD-HRA-HRTR IN OPD
      *RVM-ADA-FIN 16-OCT-2000
*% END
               MOVE FIO-MOD TO FIO-CMND
               PERFORM DAP-FIO-OPD.
           MOVE FIO-STAT TO PGM-STAT-OPD.
           IF NOT PGM-STAT-OPD-OKS
               MOVE 'DAP'        TO MSG-COD-SIST
               MOVE 'ONPOPD ERR' TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO FRM-MENS.
       FIN-MOD-OPD.
           EXIT.

       SET-IONP SECTION.
       INI-SET-IONP.
      * ISP: SETEA STATUS OPD
           IF SCR-CCPP = 'ING'
               MOVE 'S' TO OPD-COD-IONP IN OPD
           ELSE
               MOVE 'N' TO OPD-COD-IONP IN OPD.
       FIN-SET-IONP.
           EXIT.

       GET-SER-OPD SECTION.
       INI-GET-SER-OPD.
      * ISP: FND-KEY YA PUSO LAS LLAVES DEL SERIAL EN OPD
           MOVE FIO-GET-NLS TO FIO-CMND.
       CIC-GET-SER-OPD.
           MOVE 'OPD-CIC-ISER' TO FIO-AKEY.
           PERFORM DAP-FIO-OPD.
           MOVE '23' TO PGM-STAT-OPD.
           IF FIO-STAT-OKS
               IF OPD-CAI-ISER IN ONP-FLD =
                  OPD-CAI-ISER IN OPD        AND
                  OPD-IIC-ISER IN ONP-FLD =
                  OPD-IIC-ISER IN OPD
                    IF OPD-COD-STAT IN OPD = 'G'
                        MOVE '00' TO PGM-STAT-OPD
                        MOVE OPD-CAI-IOPD IN OPD TO
                             ONP-CAI-IOPD IN ONP-FLD
                        MOVE ONP-CAI-IOPD IN ONP-FLD TO
                             ONP-CAI-IOPD IN ONP
                        MOVE OPD-IIC-IOPD IN OPD TO
                             ONP-IIC-IOPD IN ONP-FLD
                        MOVE ONP-IIC-IOPD IN ONP-FLD TO
                             ONP-IIC-IOPD IN ONP
                    ELSE
                        MOVE FIO-GET-NXT TO FIO-CMND
                        GO TO CIC-GET-SER-OPD.
       FIN-GET-SER-OPD.
           EXIT.

       GET-FRM-OPD SECTION.
       INI-GET-FRM-OPD.
      * ISP: OBTIENE OPD DESDE LA PANTALLA
           MOVE ONP-CAI-IOPD IN ONP-FLD TO OPD-CAI-IOPD IN OPD.
           MOVE ONP-IIC-IOPD IN ONP-FLD TO OPD-IIC-IOPD IN OPD.
           PERFORM GET-OPD.
       FIN-GET-FRM-OPD.
           EXIT.

       GET-OPD SECTION.
       INI-GET-OPD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           MOVE FIO-STAT TO PGM-STAT-OPD.
       FIN-GET-OPD.
           EXIT.

      * JVM : Mantencion 29/09/1992
       BUS-OFI-CSSN SECTION.
       INI-BUS-OFI-CSSN.
      *     MOVE SCR-USER TO USR-COD-USER IN USR.
           IF SCR-USER NOT > SPACES
               MOVE 'RJUNCO' TO SCR-USER.

           MOVE SCR-USER TO USR-COD-USER IN USR.
           PERFORM VAL-USR.
           IF MSG-COD-MENS > SPACES
               MOVE 'USRCSSNNEX' TO MSG-COD-MENS
               PERFORM GOTO-CMN
           ELSE
               MOVE USR-COD-OFUS IN USR TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               IF MSG-COD-MENS > SPACES
                   MOVE 'OFICSSNNEX' TO MSG-COD-MENS
                   PERFORM GOTO-CMN
               ELSE
                   MOVE USR-COD-OFUS IN USR TO SCR-COD-OFIC.
       FIN-BUS-OFI-CSSN.
           EXIT.

       GOTO-CMN SECTION.
       INI-GOTO-CMN.
           MOVE 'DAP' TO MSG-COD-SIST.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC TO FRM-MENS.
           PERFORM SCR-MSSG.
           MOVE SCR-INST-HCM TO SCR-INST.
           PERFORM SCR-COM.
       FIN-GOTO-CMN.
           EXIT.
      * JVM : Fin
      *>>>>
*% END
*% IF EOF_PU
      *<<<< EOF_PU

       RET-HDR SECTION.
       INI-RET-HDR.
           PERFORM GET-FHOY.
      *    Fecha Transaccion
           MOVE HOY-FHOY TO ONP-FEC-FTRN IN ONP.
      *    Hora Transaccion
           MOVE HOY-HHOY TO ONP-HRA-HRTR IN ONP.
      *    Codigo Origen transaccion
           MOVE SCR-OTRN TO ONP-COD-OTRN IN ONP.
      *    Autor Transaccion
           MOVE SCR-USER TO ONP-COD-ATRN IN ONP.
           IF SCR-CCPP = 'ING'
               MOVE 'I' TO ONP-COD-TACC IN ONP
           ELSE
           IF SCR-CMND = 'ALZ'
               MOVE 'E' TO ONP-COD-TACC IN ONP.
       FIN-RET-HDR.
           EXIT.

      * proceso journal
       PRO-JNL SECTION.
       INI-PRO-JNL.
           MOVE SPACES TO JNL.
           MOVE ZEROES TO JNL-FEC-CTB
                          JNL-VAL-1
                          JNL-VAL-2
                          JNL-FEC-EMI
                          JNL-FEC-LIQ
                          JNL-TAS-INT
                          JNL-NUM-LIQ
                          JNL-FEC-VTO.
      
      * DATOS CONSTANTES

      *    TIPO DE REGISTRO (E/S)
           MOVE 'S' TO JNL-TIP-REG.

      *    CANT. DE REGISTROS POR REG. LOGICO
           MOVE '0' TO JNL-CTD-REG.

      *    CODIGO DE SECUENCIA DEL REG. (0)
           MOVE '0' TO JNL-COD-SEC-REG.

      *    FECHA DE PROCESO
           PERFORM GET-FHOY.
           MOVE HOY-FHOY TO JNL-FEC-PRC.

      *    HORA DE PROCESO
           MOVE HOY-HHOY TO JNL-HOR-PRC.

      *    NUMERO DE SEGUIMIENTO
      *     MOVE '1800' TO CIC-CAI-ICIC IN CIC.
      *     MOVE CIC-GET TO CIC-CMND.
      *     PERFORM PRO-CIC.
      *     IF CIC-STAT NOT = CIC-STAT-OKS
      *         MOVE CIC-MENS TO FRM-MENS
      *         MOVE FIO-STAT-NEX TO FIO-STAT
      *         GO TO FIN-PRO-JNL
      *     ELSE
      *        CENTRO DE ASIGNACION SEGUIM.
      *         MOVE CIC-CAI-ICIC TO JNL-CAI-SGM
      *        NUMERO SECUENCIAL DENTRO CEN. ASG.
      *         MOVE CIC-IIC-ICIC TO JNL-IIC-SGM.
           MOVE SPACES TO JNL-CAI-SGM.
           MOVE ZEROES TO JNL-IIC-SGM.

      *    CODIGO DE SISTEMA DE LA TRN
           MOVE 'DAP' TO JNL-COD-SIS-TRN.

      *    CODIGO DE TRANSACCION
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'EVT' TO TAB-COD-TTAB IN TAB.
           MOVE SCR-CMND TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-CIC-CTAB IN TAB TO JNL-COD-TRN.

      *    CODIGO DE TRANSACCION
           MOVE SPACES TO JNL-COD-AUX-TRN.

      *    AMBIENTE GENERACION (SAFE,CICS,ETC)
           MOVE 'I' TO JNL-COD-AMB.

      *    MODALIDAD (LINEA,FUERA DE LINEA)
           MOVE 'L' TO JNL-COD-MOD.

      *    IDENTIFICACION OPERADOR
      *    OBTENER DESDE USUARIOS Y PRIVILEGIOS, MIENTRAS TANTO : SPACES
           MOVE SPACES TO JNL-IDN-USU-OPR.

      *    CODIGO DE TERMINAL
      *    OBTENER DESDE USUARIOS Y PRIVILEGIOS, MIENTRAS TANTO : SPACES
           MOVE SPACES TO JNL-TML.

      *    TIPO DE USUARIO DEL AUTORIZADOR TRN
      *    OBTENER DESDE USUARIOS Y PRIVILEGIOS, MIENTRAS TANTO : SPACES
           MOVE SPACES TO JNL-COD-TUS-AUT.

      *    OBTENER DESDE USUARIOS Y PRIVILEGIOS, MIENTRAS TANTO : SPACES
           MOVE SPACES TO JNL-IDN-USU-AUT.

      *    CODIGO DE PRODUCTO
      *    OPD-COD-COPD -> TO JNL-COD-PDT
      
      *    CODIGO DE ESTADO DE PROCESAMIENTO
           MOVE 'P' TO JNL-COD-EST-PRC.

      *    TIPO ORIGEN PARA CUADRAT. (OFI/INT)
           MOVE SPACES TO JNL-TIP-ORN-CUA.

      *    CODIGO DE CUADRATURA
           MOVE SPACES TO JNL-COD-CUA.

      *    FECHA CONTABLE
           MOVE HOY-FHOY TO JNL-FEC-CTB.

      * DATOS VARIABLES
      
      *    CODIGO OFICINA ORIGEN = Oficina Emisora ONP
           MOVE ONP-COD-OFIC IN ONP TO JNL-COD-OFI-ORN.
      *    CODIGO OFICINA DESTINO = Oficina Elimina ONP
           MOVE ONP-COD-OFEL IN ONP TO JNL-COD-OFI-DST.

      *    EJECUTIVO DEL DAP (NOTA 4)
           MOVE ONP-STP-ITRN IN ONP TO JNL-STP-ITRN.

      *    CODIGO OPERACION
      *    MOVE OPD-CIC-IOPD IN OPD TO JNL-COP.
           MOVE OPD-CAI-IOPD IN OPD TO JNL-NUM-SIS.
           MOVE OPD-CAI-IOPD IN OPD TO JNL-COD-CRR.

      *    CLASE DE OPERACION
      *    MOVE OPD-COD-COPD IN OPD TO JNL-CLA-OPE.
           MOVE OPD-COD-TOPD IN OPD TO JNL-TIP-OPE.
           MOVE OPD-COD-AOPD IN OPD TO JNL-STP-OPE.

      *    CIC DEL CLIENTE
      *    RCC-CIC-ICLI -> JNL-CIC-CLI.

      *    VALOR DE CAMBIO OPERACION
           MOVE OPD-COD-VCAM IN OPD TO JNL-VCB-OPE.

      *    MONTO 1
           MOVE OPD-VAL-CAPT IN OPD TO JNL-VAL-1.

      *    MONTO 2
      *    LIQ-SGV-TPAG -> JNL-VAL-2

      *    EJECUTIVO QUE ATIENDE AL CLIENTE.
           MOVE OPD-COD-EJEC IN OPD TO JNL-COD-EJE.

      *    FECHA EMISION
           MOVE OPD-FEC-FREA IN OPD TO JNL-FEC-EMI.

      *    FECHA LIQUIDACION
      *    LIQ-FEC-FREA -> JNL-FEC-LIQ.

      *    TIPO TASA DE INTERES.
           MOVE OPD-COD-TINT IN OPD TO JNL-TIP-TAS-INT.

      *    TASA DE INTERES
           MOVE OPD-SGV-TINT IN OPD TO JNL-TAS-INT.

      *    NUMERO DE LIQUIDACION
      *    LIQ-NUM-ILIQ -> JNL-NUM-LIQ

      *    FECHA VENCIMIENTO
           MOVE OPD-FEC-FTER IN OPD TO JNL-FEC-VTO.

      *    CODIGO OPERACION ABONO.
           MOVE SPACES TO JNL-COP-ABO.

      *    EJECUTIVO LIQUIDACION.
      *    LIQ-COD-EJEC -> JNL-COD-EJE-LIQ

      *    SERIAL DOCUMENTO.
           MOVE OPD-CIC-ISER IN OPD TO JNL-SRL-DOC.

      *    MOTIVO ONP.
      *    ONP-COD-MONP -> JNL-COD-MOT-ONP
           MOVE ONP-COD-MONP IN ONP TO JNL-COD-MOT-ONP.

      *    INDICADOR MULTIPLES CLIENTES.
           MOVE SPACES TO JNL-IND-MUL-CLI.

       PUT-JNL.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM DAP-FIO-JNL.
           IF NOT FIO-STAT-OKS
      *        Error al grabar el journal
               MOVE 'DAP' TO MSG-COD-SIST
               MOVE 'JNL    ERR' TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO FRM-MENS
               PERFORM BCK-OUT.
       FIN-PRO-JNL.
           EXIT.

       BCK-OUT SECTION.
       INI-BCK-OUT.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           MOVE 'DAP' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
      *        Para informar que el backout se realizo con exito
               MOVE '49' TO FIO-STAT.
       FIN-BCK-OUT.
           EXIT.

      * ISP-INI 31-07-1998 INICIALIZA CAMPOS DE REGISTROS
       COPY DAPBCONP.
      * ISP-FIN 31-07-1998 INICIALIZA CAMPOS DE REGISTROS
      *>>>> EOF_PU
*% END
