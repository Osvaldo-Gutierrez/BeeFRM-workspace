*%! Codigo adicional para archivo ECC
*% IF WSS
      *<<<< WSS
      *Entidades de GNS
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *Entidades de TAB
       COPY TABBRCAM.
       COPY TABBROFI.
       COPY TABBRVLR.

       01  WSS-VARI.
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-DTER                      PIC 9(02).
           03  WSS-COD-TINT.
               05  WSS-COD-BCTI                      PIC X(01).
               05  WSS-COD-PBTI                      PIC X(01).
               05  WSS-COD-MCTI                      PIC X(02).
               05  WSS-COD-CVTI                      PIC X(03).
               05  WSS-GLS-REST                      PIC X(05).
           03  WSS-COD-OFIC                          PIC X(03).
           03  WSS-COD-VCAM                          PIC X(06).
      *JVM/BCI 01-06-93
           03  WSS-COD-UEFE                          PIC X(06).
           03  PDB-CIC-0998         VALUE '0998  '   PIC X(06).
      *JVM/BCI 
           03  WSS-FINM             VALUE ZEROES     PIC 9(02).
           03  WSS-FINT             VALUE ZEROES     PIC 9(02).
           03  WSS-IND-PRI          VALUE ZEROES     PIC 9(01).
           03  WSS-IND-CORT         VALUE ZEROES     PIC 9(01).
           03  WSS-IND-ERR1         VALUE ZEROES     PIC 9(01).
           03  WSS-IND-ERR2         VALUE ZEROES     PIC 9(01).
           03  WSS-I                VALUE ZEROES     PIC 9(02).
           03  WSS-J                VALUE ZEROES     PIC 9(02).
           03  WSS-SUM-OPER         VALUE ZEROES     PIC 9(07).
           03  WSS-SUM-CAPT         VALUE ZEROES     PIC 9(11)V9999.
           03  WSS-SUM-OPET         VALUE ZEROES     PIC 9(07).
           03  WSS-SUM-CAPI         VALUE ZEROES     PIC 9(11)V9999.
           03  WSS-TOT-CAPT         VALUE ZEROES     PIC 9(11)V9999.
           03  WSS-TOT-OPET         VALUE ZEROES     PIC 9(07).
           03  WSS-TOT-CAPI         VALUE ZEROES     PIC 9(11)V9999.
           03  WSS-TOT-OPER         VALUE ZEROES     PIC 9(07).
           03  WSS-ACU-OPER         VALUE ZEROES     PIC 9(05)V99.
           03  WSS-ACU-CAPT         VALUE ZEROES     PIC 9(05)V99.
           03  WSS-COD-CTAB.
               05  WSS-COD-CTA1                      PIC X(06).
               05  WSS-COD-CTA2                      PIC X(03).
               05  WSS-COD-CTA3                      PIC 9(03).
           03  WSS-GLS-TINI.
               05  WSS-NUM-TINI                      PIC 9(08)V9(02).
           03  WSS-GLS-TFIN.
               05  WSS-NUM-TFIN                      PIC 9(08)V9(02).
           03  WSS-GLS-TASI.
               05  WSS-SGV-TASI                      PIC 9(06)V9(04).
           03  WSS-GLS-TASF.
               05  WSS-SGV-TASF                      PIC 9(06)V9(04).
           03  WSS-SGV-TINI                          PIC 9(11)V9(04).
           03  WSS-SGV-TFIN                          PIC 9(11)V9(04).
           03  WSS-KEY-ANT.
               05  WSS-COD-OFIA                      PIC X(03).
               05  WSS-COD-VCAA                      PIC X(06).
           03  WSS-KEY-SRT.
               05  WSS-KEY-OFIC                      PIC X(03).
               05  WSS-KEY-VCAM                      PIC X(06).
      *    Tasa Interes Informada
           03  INT-TINT           VALUE ZEROES       PIC 9(03)V9(06).

           03  WSS-TBL-MONT.
               05  WSS-TBL-VMON  OCCURS 20 TIMES.
                   07  WSS-GLS-TRAM             PIC X(20).
                   07  WSS-NUM-VFIN             PIC 9(11)V9(04).
                   07  WSS-NUM-OPER             PIC 9(06).
                   07  WSS-POR-OPER             PIC 9(03)V9(02).
                   07  WSS-POT-OPER             PIC 9(03)V9(02).
                   07  WSS-VAL-CAPT             PIC 9(11)V9(04).
                   07  WSS-POR-CAPT             PIC 9(03)V9(02).
                   07  WSS-POT-CAPT             PIC 9(03)V9(02).

           03  WSS-TBL-TASA.
               05  WSS-TBL-VTAS  OCCURS 20 TIMES.
                   07  WSS-GLS-TRAT             PIC X(20).
                   07  WSS-SGV-VFIN             PIC 9(06)V9(04).
                   07  WSS-NUM-OPET             PIC 9(06).
                   07  WSS-VAL-CAPI             PIC 9(11)V9(04).
      *>>>>
*% END
*% IF WSS_DTC
      *<<<< WSS_DTC
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *>>>>
*% END
*% IF RW
      *<<<< RW
       COPY DAPRWECI.
      *>>>>
*% END
*% IF RT
      *<<<< RT
       COPY DAPRTECI.
      *>>>>
*% END
*% IF INI
      *<<<< INI
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA PARAMETROS '.
           DISPLAY ' '.
      * Captura parametros 
           DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA) : '.
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.

           DISPLAY 'INGRESE CODIGO OFICINA      (XXX/*) :  '.
           ACCEPT WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.

           DISPLAY 'INGRESE CODIGO MONEDA    (X(06)/*)  :  '.
           ACCEPT WSS-COD-VCAM IN WSS-VARI.
           DISPLAY WSS-COD-VCAM IN WSS-VARI.

           DISPLAY 'FIN CAPTURA PARAMETROS '.
           DISPLAY ' '.
           DISPLAY 'INICIO VALIDACION PARAMETROS '.

      *VAL-FEC Valida Fecha de Proceso
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-CHOY-N TO FEC-CHOY.
           MOVE FEC-VHBL-N TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR VALIDACION EN FECHA DE PROCESO: '
                         WSS-FEC-FPRO '  ' FEC-MENS
               PERFORM GNS-PRO-ABT.

      *VAL-OFI Valida Codigo de oficina
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
              DISPLAY WSS-COD-OFIC IN WSS-VARI
              MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
              PERFORM VAL-OFI
              IF MSG-GLS-MENS = 'COD    NEX'
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' ERROR VALIDACION OFICINA: ' 
                            WSS-COD-OFIC IN WSS-VARI
                  PERFORM GNS-PRO-ABT.

      *BUS-TAB Valida Codigo de moneda
           IF WSS-COD-VCAM IN WSS-VARI > SPACES AND
              WSS-COD-VCAM IN WSS-VARI NOT = '*'
              DISPLAY WSS-COD-VCAM IN WSS-VARI
              MOVE 'TAB' TO TAB-COD-SIST
              MOVE 'VLR' TO TAB-COD-TTAB IN TAB
              MOVE WSS-COD-VCAM IN WSS-VARI TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
              IF MSG-GLS-MENS = 'COD    NEX'
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' ERROR VALIDACION COD. MONEDA: ' WSS-COD-VCAM
                  PERFORM GNS-PRO-ABT.

      *JVM/BCI 01-06-93
      * Obtiene Codigo Valor de Cambio de UF Mediante CIC de la Moneda '
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-0998   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0998  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-UEFE IN WSS-VARI.
      *JVM/BCI 

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DPRO IN WSS-ECC
                                            WSS-NUM-DPRO IN WSS-ECI
                                            WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MPRO IN WSS-ECC
                                            WSS-NUM-MPRO IN WSS-ECI
                                            WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SPRO IN WSS-ECC
                                            WSS-NUM-SPRO IN WSS-ECI
                                            WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-APRO IN WSS-ECC
                                            WSS-NUM-APRO IN WSS-ECI
                                            WSS-NUM-ATER IN WSS-VARI.
           DISPLAY ' '.
           DISPLAY 'FIN VALIDACION PARAMETROS '.
           DISPLAY ' '.
      *>>>>
*% END
*% IF PH_ECC
      *<<<< IF PH_ECC
      *Recupera glosa de oficina
           MOVE WSS-COD-OFIA IN WSS-VARI TO WSS-COD-OFIC IN WSS-ECC.
           MOVE WSS-COD-OFIA IN WSS-VARI TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.

      *Recupera glosa de moneda
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'VLR' TO TAB-COD-TTAB IN TAB.
           MOVE WSS-COD-VCAA IN WSS-VARI TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-VCAM IN WSS-ECC.

      *>>>>
*% END
*% IF INI_FST_INP
      *<<<< INI_FST_INP
           MOVE ZEROES TO OPD-FEC-FCMP IN OPD.
           MOVE ZEROES TO WSS-FEC-FINI IN WSS-VARI.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.

           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR AL ACCESAR OPD POR OPD-FEC-FCMP : '
                                                     FIO-STAT
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           IF OPD-FEC-FCMP IN OPD > WSS-FEC-FTER IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS OPD EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI 
                       ' A    ' WSS-FEC-FPRO IN WSS-VARI
               GO TO FIN-INP-SRT
           ELSE
               GO TO LUP-INP-SRT.
      *>>>>
*% END
*% IF LUP_INP
      *<<<< LUP_INP
      *Controla ingreso de registros validos
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
              IF WSS-COD-OFIC IN WSS-VARI NOT = OPD-COD-OFIC IN OPD
                 GO TO NXT-INP-SRT.

           IF WSS-COD-VCAM IN WSS-VARI > SPACES AND
              WSS-COD-VCAM IN WSS-VARI NOT = '*'
              IF WSS-COD-VCAM IN WSS-VARI NOT = OPD-COD-VCAM IN OPD
                 GO TO NXT-INP-SRT.

           IF OPD-VAL-SCAP IN OPD > ZEROES AND
              OPD-COD-STAT IN OPD = 'G'
              NEXT SENTENCE
           ELSE
              GO TO NXT-INP-SRT.

           MOVE OPD-COD-TINT IN OPD TO WSS-COD-TINT IN WSS-VARI.
           PERFORM OBT-TASA-BAS.
           MOVE INT-TINT TO OPD-SGV-TINT IN OPD.

           IF OPD-COD-VCAM IN OPD NOT = WSS-COD-UEFE IN WSS-VARI
              DIVIDE OPD-VAL-SCAP IN OPD BY 1000 GIVING
                                         OPD-VAL-SCAP IN OPD.
      *>>>>
*% END
*% IF INI_NXT_INP
      *<<<< INI_NXT_INP
      *Ubica registro en OPD por fecha
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
      *>>>>
*% END
*% IF FIN_NXT_INP
      *<<<< FIN_NXT_INP
      *Controla ingreso de registros por fecha termino
           IF OPD-FEC-FCMP IN OPD > WSS-FEC-FTER IN WSS-VARI
              GO TO FIN-INP-SRT.
      *>>>>
*% END
*% IF FST_OUT
      *<<<< FST_OUT
      *Carga tabla para primer registro
           PERFORM CLR-TBL-MON VARYING WSS-I FROM 1 BY 1 UNTIL
                                                     WSS-I > 20. 
           PERFORM CLR-TBL-TAS VARYING WSS-J FROM 1 BY 1 UNTIL
                                                     WSS-J > 20. 
           PERFORM CLR-VAR-ACU.
           PERFORM GEN-TBL-MON1.
           PERFORM GEN-TBL-TAS1.
           MOVE WSS-I IN WSS-VARI TO WSS-FINM IN WSS-VARI.
           MOVE WSS-J IN WSS-VARI TO WSS-FINT IN WSS-VARI.
           MOVE OPD-COD-OFIC IN SRT TO WSS-COD-OFIA IN WSS-VARI.
           MOVE OPD-COD-VCAM IN SRT TO WSS-COD-VCAA IN WSS-VARI.
           MOVE 1 TO WSS-IND-PRI IN WSS-VARI.
      *>>>>
*% END
*% IF LUP_OUT
      *<<<< LUP_OUT
      *     DISPLAY 'SRT ' SRT.
      *Genera datos desde archivo SORT.
           MOVE OPD-COD-OFIC IN SRT TO WSS-KEY-OFIC IN WSS-VARI.
           MOVE OPD-COD-VCAM IN SRT TO WSS-KEY-VCAM IN WSS-VARI.
           IF NOT ( WSS-KEY-SRT IN WSS-VARI = WSS-KEY-ANT IN WSS-VARI )
              PERFORM ACU-LST-MON
              PERFORM ACU-LST-TAS
              MOVE OPD-COD-OFIC IN SRT TO WSS-COD-OFIA IN WSS-VARI
              MOVE OPD-COD-VCAM IN SRT TO WSS-COD-VCAA IN WSS-VARI
              PERFORM CLR-VAR-ACU
              PERFORM CLR-TBL-MON VARYING WSS-I FROM 1 BY 1 UNTIL
                                                        WSS-I > 20 
              PERFORM CLR-TBL-TAS VARYING WSS-J FROM 1 BY 1 UNTIL
                                                        WSS-J > 20 
              PERFORM GEN-TBL-MON1
              PERFORM GEN-TBL-TAS1
              MOVE WSS-I IN WSS-VARI TO WSS-FINM IN WSS-VARI
              MOVE WSS-J IN WSS-VARI TO WSS-FINT IN WSS-VARI
              PERFORM GEN-TBL-MON2 VARYING WSS-I FROM 1 BY 1 UNTIL
                                                   WSS-I > WSS-FINM            
              PERFORM GEN-TBL-TAS2 VARYING WSS-J FROM 1 BY 1 UNTIL
                                                   WSS-J > WSS-FINT
              MOVE 0 TO WSS-IND-PRI IN WSS-VARI
              MOVE 0 TO WSS-IND-CORT IN WSS-VARI
           ELSE
              MOVE 1 TO WSS-IND-CORT IN WSS-VARI
              PERFORM GEN-TBL-MON2 VARYING WSS-I FROM 1 BY 1 UNTIL
                                                   WSS-I > WSS-FINM            
              PERFORM GEN-TBL-TAS2 VARYING WSS-J FROM 1 BY 1 UNTIL
                                                   WSS-J > WSS-FINT.

           GO TO NXT-OUT-SRT.
      *>>>>
*% END
*% IF FIN_OUT
      *<<<< FIN_OUT
           IF WSS-IND-CORT IN WSS-VARI = 1 OR
              WSS-IND-PRI  IN WSS-VARI = 1
              PERFORM GEN-TBL-MON2 VARYING WSS-I FROM 1 BY 1 UNTIL
                                                   WSS-I > WSS-FINM            
              PERFORM GEN-TBL-TAS2 VARYING WSS-J FROM 1 BY 1 UNTIL
                                                   WSS-J > WSS-FINT            
              PERFORM ACU-LST-MON
              PERFORM ACU-LST-TAS.
      *>>>>
*% END
*% IF EOF
      *<<<< EOF
      *Limpia matriz
       CLR-TBL-MON SECTION.
       INI-CLR-TBL-MON.
           MOVE SPACES TO WSS-GLS-TRAM IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-NUM-VFIN IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-NUM-OPER IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-POR-OPER IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-POT-OPER IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-VAL-CAPT IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-POR-CAPT IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-POT-CAPT IN WSS-VARI(WSS-I).
       FIN-CLR-TBL-MON.
           EXIT.

       CLR-TBL-TAS SECTION.
       INI-CLR-TBL-TAS.
           MOVE SPACES TO WSS-GLS-TRAT IN WSS-VARI(WSS-J).
           MOVE ZEROES TO WSS-SGV-VFIN IN WSS-VARI(WSS-J).
           MOVE ZEROES TO WSS-NUM-OPET IN WSS-VARI(WSS-J).
           MOVE ZEROES TO WSS-VAL-CAPI IN WSS-VARI(WSS-J).
       FIN-CLR-TBL-TAS.
           EXIT.

       CLR-VAR-ACU SECTION.
       INI-CLR-VAR-ACU.
           MOVE ZEROES TO WSS-TOT-OPER IN WSS-VARI.
           MOVE ZEROES TO WSS-TOT-CAPT IN WSS-VARI.
           MOVE ZEROES TO WSS-ACU-CAPT IN WSS-VARI.
           MOVE ZEROES TO WSS-ACU-OPER IN WSS-VARI.
       FIN-CLR-VAR-ACU.
           EXIT.

       GEN-TBL-MON1 SECTION.
       INI-GEN-TBL-MON1.
           MOVE 0 TO WSS-IND-ERR1 IN WSS-VARI.
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'TMO' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-VCAM IN SRT TO WSS-COD-CTA1 IN WSS-VARI.
           MOVE SPACES              TO WSS-COD-CTA2 IN WSS-VARI.
           MOVE 001                 TO WSS-COD-CTA3 IN WSS-VARI.
           MOVE WSS-COD-CTAB IN WSS-VARI TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS IN MSG = 'COD    NEX'
              DISPLAY 'ERROR EN ACCESO A TAB-TMO ' TAB-COD-CTAB
              MOVE 1 TO WSS-IND-ERR1 IN WSS-VARI
              GO TO FIN-GEN-TBL-MON1.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI.
           MOVE ZEROS TO WSS-I IN WSS-VARI.

       LUP-MON.    
           IF NOT ( FIO-STAT-OKS AND
              OPD-COD-VCAM IN SRT = WSS-COD-CTA1 IN WSS-VARI )
              GO TO FIN-GEN-TBL-MON1.
           ADD 1 TO WSS-I IN WSS-VARI
                    WSS-FINM IN WSS-VARI.
           MOVE TAB-GLS-DESC IN TAB TO 
                                     WSS-GLS-TRAM IN WSS-VARI(WSS-I).
           MOVE TAB-GLS-DAT1 IN TAB TO 
                                     WSS-GLS-TFIN IN WSS-VARI.
           MOVE WSS-NUM-TFIN IN WSS-VARI TO 
                                     WSS-SGV-TFIN IN WSS-VARI.
           MOVE WSS-SGV-TFIN IN WSS-VARI TO 
                                     WSS-NUM-VFIN IN WSS-VARI(WSS-I).
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF TAB-COD-TTAB IN TAB NOT = 'TMO' 
              MOVE '23' TO FIO-STAT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI.
           GO TO LUP-MON.
       FIN-GEN-TBL-MON1.
           EXIT.

       GEN-TBL-MON2 SECTION.
       INI-GEN-TBL-MON2.
           IF OPD-VAL-SCAP IN SRT NOT > 
                                    WSS-NUM-VFIN IN WSS-VARI(WSS-I)
              ADD 1 TO WSS-NUM-OPER IN WSS-VARI(WSS-I) 
              ADD OPD-VAL-SCAP IN SRT TO 
                                    WSS-VAL-CAPT IN WSS-VARI(WSS-I) 
              MOVE 20 TO WSS-I.
       FIN-GEN-TBL-MON2.
           EXIT.

       GEN-TBL-TAS1 SECTION.
       INI-GEN-TBL-TAS1.
           MOVE 0 TO WSS-IND-ERR2 IN WSS-VARI.
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'TTA' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-VCAM IN SRT TO WSS-COD-CTA1 IN WSS-VARI.
           MOVE SPACES              TO WSS-COD-CTA2 IN WSS-VARI.
           MOVE 001                 TO WSS-COD-CTA3 IN WSS-VARI.
           MOVE WSS-COD-CTAB IN WSS-VARI TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS IN MSG = 'COD    NEX'
              DISPLAY 'ERROR EN ACCESO A TAB-TTA ' TAB-COD-CTAB
              MOVE 1 TO WSS-IND-ERR2 IN WSS-VARI
              GO TO FIN-GEN-TBL-TAS1.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI.
           MOVE ZEROES TO WSS-J IN WSS-VARI.

       LUP-TAS.    
           IF NOT ( FIO-STAT-OKS AND
              OPD-COD-VCAM IN SRT = WSS-COD-CTA1 IN WSS-VARI )
              GO TO FIN-GEN-TBL-TAS1.
           ADD 1 TO WSS-J IN WSS-VARI
                    WSS-FINT IN WSS-VARI.
           MOVE TAB-GLS-DESC IN TAB TO 
                                     WSS-GLS-TRAT IN WSS-VARI(WSS-J).
           MOVE TAB-GLS-DAT1 IN TAB TO 
                                     WSS-GLS-TASF IN WSS-VARI.
           MOVE WSS-SGV-TASF IN WSS-VARI TO 
                                     WSS-SGV-VFIN IN WSS-VARI(WSS-J).
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF TAB-COD-TTAB IN TAB NOT = 'TTA' 
              MOVE '23' TO FIO-STAT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI.
           GO TO LUP-TAS.
       FIN-GEN-TBL-TAS1.
           EXIT.

       GEN-TBL-TAS2 SECTION.
       INI-GEN-TBL-TAS2.
           IF OPD-SGV-TINT IN SRT NOT > 
                                    WSS-SGV-VFIN IN WSS-VARI(WSS-J)
              ADD 1 TO WSS-NUM-OPET IN WSS-VARI(WSS-J) 
              ADD OPD-VAL-SCAP IN SRT TO 
                                    WSS-VAL-CAPI IN WSS-VARI(WSS-J) 
              MOVE 20 TO WSS-J.
       FIN-GEN-TBL-TAS2.
           EXIT.

       ACU-LST-MON SECTION.
       INI-ACU-LST-MON.
      *Genera datos desde TABLA en WSS.
           PERFORM ACU-REG-MON VARYING WSS-I FROM 1 BY 1 UNTIL 
                                                 WSS-I > WSS-FINM.
           PERFORM CAL-REG-MON VARYING WSS-I FROM 1 BY 1 UNTIL 
                                                 WSS-I > WSS-FINM.
           PERFORM LST-REG-MON VARYING WSS-I FROM 1 BY 1 UNTIL 
                                                 WSS-I > WSS-FINM.
           PERFORM LST-TOT-MON.
       FIN-ACU-LST-MON.
           EXIT.

       ACU-REG-MON SECTION.
       INI-ACU-REG-MON.
           ADD WSS-NUM-OPER IN WSS-VARI(WSS-I)  TO
                                       WSS-TOT-OPER IN WSS-VARI.
           ADD WSS-VAL-CAPT IN WSS-VARI(WSS-I)  TO
                                       WSS-TOT-CAPT IN WSS-VARI.
       FIN-ACU-REG-MON.
           EXIT.

       CAL-REG-MON SECTION.
       INI-CAL-REG-MON.
           IF WSS-TOT-OPER IN WSS-VARI = ZEROS
              MOVE ZEROS TO WSS-POR-OPER IN WSS-VARI(WSS-I)
           ELSE
              COMPUTE WSS-POR-OPER IN WSS-VARI(WSS-I) ROUNDED = 
                  (WSS-NUM-OPER IN WSS-VARI(WSS-I) * 100) /
                                      WSS-TOT-OPER IN WSS-VARI.
      *Calcula porcentaje operacion  
           IF WSS-POR-OPER IN WSS-VARI(WSS-I) = ZEROES
              MOVE ZEROES  TO WSS-POT-OPER IN WSS-VARI(WSS-I)
           ELSE
              ADD WSS-POR-OPER IN WSS-VARI(WSS-I) TO 
                                       WSS-ACU-OPER IN WSS-VARI
              MOVE WSS-ACU-OPER IN WSS-VARI TO 
                                WSS-POT-OPER IN WSS-VARI(WSS-I).
      *Calcula porcentaje capital
           IF WSS-TOT-CAPT IN WSS-VARI = ZEROS
              MOVE ZEROS TO WSS-POR-CAPT IN WSS-VARI(WSS-I)
           ELSE
              COMPUTE WSS-POR-CAPT IN WSS-VARI(WSS-I) ROUNDED = 
                  (WSS-VAL-CAPT IN WSS-VARI(WSS-I) * 100) /
                                      WSS-TOT-CAPT IN WSS-VARI.
      *Acumula porcentaje capital
           IF WSS-POR-CAPT IN WSS-VARI(WSS-I) = ZEROES
              MOVE ZEROES  TO WSS-POT-CAPT IN WSS-VARI(WSS-I)
           ELSE
              ADD WSS-POR-CAPT IN WSS-VARI(WSS-I) TO 
                                       WSS-ACU-CAPT IN WSS-VARI
              MOVE WSS-ACU-CAPT IN WSS-VARI TO 
                                WSS-POT-CAPT IN WSS-VARI(WSS-I).
       FIN-CAL-REG-MON.
           EXIT.

       LST-REG-MON SECTION.
       INI-LST-REG-MON.
           MOVE WSS-GLS-TRAM IN WSS-VARI(WSS-I)  TO
                                       WSS-GLS-TRAM IN WSS-ECC.
           MOVE WSS-NUM-OPER IN WSS-VARI(WSS-I)  TO
                                       WSS-NUM-OPER IN WSS-ECC.
           MOVE WSS-POR-OPER IN WSS-VARI(WSS-I)  TO
                                       WSS-NUM-POTR IN WSS-ECC.
           MOVE WSS-POT-OPER IN WSS-VARI(WSS-I)  TO
                                       WSS-VAL-POTT IN WSS-ECC.
           MOVE WSS-VAL-CAPT IN WSS-VARI(WSS-I)  TO
                                       WSS-VAL-CAPT IN WSS-ECC.
           MOVE WSS-POR-CAPT IN WSS-VARI(WSS-I)  TO
                                       WSS-VAL-PCTR IN WSS-ECC.
           MOVE WSS-POT-CAPT IN WSS-VARI(WSS-I)  TO
                                       WSS-VAL-PCTT IN WSS-ECC.
           ADD WSS-NUM-OPER IN WSS-ECC TO
                                      WSS-SUM-OPER IN WSS-VARI.
           ADD WSS-VAL-CAPT IN WSS-ECC TO
                                      WSS-SUM-CAPT IN WSS-VARI.
           GENERATE DL-ECC.
       FIN-LST-REG-MON.
           EXIT.

       LST-TOT-MON SECTION.
       INI-LST-TOT-MON.
           MOVE WSS-SUM-OPER IN WSS-VARI TO
                                       WSS-NUM-TOPE IN WSS-ECC.
           MOVE WSS-SUM-CAPT IN WSS-VARI TO
                                       WSS-VAL-TCAP IN WSS-ECC.
           GENERATE DL-TECC.
           MOVE ZEROES TO WSS-SUM-OPER IN WSS-VARI.
           MOVE ZEROES TO WSS-SUM-CAPT IN WSS-VARI.
       FIN-LST-TOT-MON.
           EXIT.

       ACU-LST-TAS SECTION.
       INI-ACU-LST-TAS.
      *Genera datos desde TABLA en WSS.
           PERFORM ACU-REG-TAS VARYING WSS-J FROM 1 BY 1 UNTIL 
                                                 WSS-J > WSS-FINT.
           GENERATE DL-EECI.
           PERFORM LST-REG-TAS VARYING WSS-J FROM 1 BY 1 UNTIL 
                                                 WSS-J > WSS-FINT.
           PERFORM LST-TOT-TAS.
       FIN-ACU-LST-TAS.
           EXIT.

       ACU-REG-TAS SECTION.
       INI-ACU-REG-TAS.
           ADD WSS-NUM-OPET IN WSS-VARI(WSS-J)  TO
                                       WSS-TOT-OPET IN WSS-VARI.
           ADD WSS-VAL-CAPI IN WSS-VARI(WSS-J)  TO
                                       WSS-TOT-CAPI IN WSS-VARI.
       FIN-ACU-REG-TAS.
           EXIT.

       LST-REG-TAS SECTION.
       INI-LST-REG-TAS.
           MOVE WSS-GLS-TRAT IN WSS-VARI(WSS-J)  TO
                                       WSS-GLS-TRAM IN WSS-ECI.
           MOVE WSS-NUM-OPET IN WSS-VARI(WSS-J)  TO
                                       WSS-NUM-OPER IN WSS-ECI.
           MOVE WSS-VAL-CAPI IN WSS-VARI(WSS-J)  TO
                                       WSS-VAL-CAPT IN WSS-ECI.
           ADD WSS-NUM-OPER IN WSS-ECI TO
                                      WSS-SUM-OPET IN WSS-VARI.
           ADD WSS-VAL-CAPT IN WSS-ECI TO
                                      WSS-SUM-CAPI IN WSS-VARI.
           GENERATE DL-ECI.
       FIN-LST-REG-TAS.
           EXIT.

       LST-TOT-TAS SECTION.
       INI-LST-TOT-TAS.
           MOVE WSS-SUM-OPET IN WSS-VARI TO
                                       WSS-NUM-TOPE IN WSS-ECI.
           MOVE WSS-SUM-CAPI IN WSS-VARI TO
                                       WSS-VAL-TCAP IN WSS-ECI.
           GENERATE DL-TECI.
           MOVE '67' TO LINE-COUNTER.
           MOVE ZEROES TO WSS-SUM-OPET IN WSS-VARI.
           MOVE ZEROES TO WSS-SUM-CAPI IN WSS-VARI.
       FIN-LST-TOT-TAS.
           EXIT.

      *Entidades  de GNS
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.

      *Entidades  de TAB
       COPY TABBFCAM.
       COPY TABBFOFI.
       COPY TABBBOFI.
       COPY TABBVOFI.

      * Obtiene tasa base para operaciones con T. Variable
       OBT-TASA-BAS SECTION.
       INI-OBT-TASA-BAS.
           MOVE OPD-SGV-TINT IN OPD TO INT-TINT.
      *Si es tasa variable
           IF ( WSS-COD-CVTI IN WSS-VARI > SPACES )
                MOVE OPD-FEC-FREA IN OPD TO CAM-FEC-FCAM IN CAM(1)
                MOVE OPD-FEC-FREA IN OPD TO CAM-FEC-FCAM IN CAM(2)
                MOVE 'I'          TO CAM-MSC-TVAL IN CAM(1)
                MOVE WSS-COD-CVTI TO CAM-COD-CVAL IN CAM(1)
                MOVE WSS-COD-PBTI TO CAM-COD-TCAM IN CAM(1)
                MOVE OPD-COD-VCAM IN OPD TO CAM-COD-VCAM IN CAM(2)

                MOVE FIO-GET-KEY TO FIO-CMND
                PERFORM TAB-FIO-CAM
                IF ( NOT FIO-STAT-OKS ) OR
                   ( CAM-MSC-STAT IN CAM NOT = 'S' )
                    DISPLAY 'NO EXISTE VALOR DE CAMBIO TASA BASE'
                             CAM-KEY-IREG IN CAM
                ELSE
                    ADD CAM-SGV-VCAM IN CAM TO INT-TINT
                    DISPLAY 'TASA BASE ' CAM-SGV-VCAM IN CAM ' '
                         OPD-SGV-TINT IN OPD ' ' INT-TINT.
       FIN-OBT-TASA-BAS.
           EXIT.
      *>>>>
*% END
