//JVALDES  JOB ,,CLASS=A,MSGCLASS=X,USER=JVALDES,PASSWORD=JVLAGO,
//            REGION=4M
/*ROUTE PRINT MVS
//STEP1  EXEC PGM=AFOLIBR
//STEPLIB  DD DSN=ADR.LIB.V38.LOAD,DISP=SHR
//MASTER   DD DSN=DAPCL.FUENTES,DISP=SHR
//OSJOB    DD DUMMY
//SYSPRINT DD SYSOUT=X
//SYSIN    DD *
-ADD DAPPGEDM,PSWD=CONS,ARC
     DUMMY
-EMOD
-SEL DAPPGEDM,CONS
-REP ALL
       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPGEDM.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    10-Jul-91 12:53 PM.

      * Programa Generador Interfaz Estadisticas de Marketing sin Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY DAPBROPD.

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
      * COPY GNSWVIDD.
       COPY GNSBRTAB.
       COPY GNSBRMSG.

       COPY GNSWGSYS.
      *<<<< WSS
      *Entidades de GNS
       COPY GNSBRCIC.
       COPY GNSBRMSC.
       COPY GNSWGCIC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWCVRF.
       COPY GNSWVVRF.

      *Entidades de DAP
       COPY DAPBRVEN.
       COPY DAPBRLIQ.
       COPY DAPBRCAD.
       COPY DAPBRPTD.
       COPY DAPBREDM.
      * ISP: VARIABLES WSS-PTD
       COPY DAPWG004.

       01  WSS-VARI.
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-DFIN                      PIC 9(02).
               05  WSS-NUM-MFIN                      PIC 9(02).
               05  WSS-NUM-SFIN                      PIC 9(02).
               05  WSS-NUM-AFIN                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-DTER                      PIC 9(02).
           03  WSS-LEI-OPD            VALUE ZEROES   PIC 9(05).
           03  WSS-LEI-LIQ            VALUE ZEROES   PIC 9(05).
           03  WSS-LEI-VEN            VALUE ZEROES   PIC 9(05).
           03  WSS-LEI-CAD            VALUE ZEROES   PIC 9(05).
           03  WSS-GRA-EDM            VALUE ZEROES   PIC 9(05).
           03  WSS-STAT-RCC           VALUE ZEROES   PIC 9(01).
           03  WSS-IND-MCLI           VALUE SPACES   PIC X(01).
           03  WSS-GLS-COMU           VALUE SPACES   PIC X(20).
           03  WSS-COD-LUGA.
               05 WSS-IND-LUGA                       PIC X(01).
               05 WSS-COD-PAIS                       PIC X(03).
           03  WSS-I                VALUE 0          PIC 9(04).
           03  WSS-NUM-JUL          VALUE 0          PIC 9(06).
           03  WSS-NDIA                              PIC 9(04).
           03  WSS-TRM-DIA                           PIC 9(04).
           03  WSS-TRM-MTO                           PIC 9(04).
           03  WSS-IND-TDIA                          PIC X(01).
           03  WSS-IND-TMTO                          PIC X(01).
           03  WSS-IDX-INT                           PIC 9(04).
      *>>>>
      *<<<< WSS_DTC
      *Entidades de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CIC-REQA==.
      *Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LIQ-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-PTD-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPGEDM' TO FIO-PROG.
           PERFORM BUS-FSIS.
       INI-INP-SRT.
      *<<<< INI_INP
      * Captura parametros
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA PARAMETROS '.
           DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA) : '.
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N  TO FEC-FBLK.
           MOVE FEC-CHOY-N  TO FEC-CHOY.
           MOVE FEC-VHBL-N  TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'PROCESO CANCELADO ' FEC-STAT
               DISPLAY 'ERROR VALIDACION F. PROCESO ' FEC-MENS
               PERFORM GNS-PRO-ABT.

           PERFORM GET-FHOY.
           MOVE 01           TO WSS-NUM-DINI IN WSS-VARI.
           MOVE WSS-NUM-MPRO TO WSS-NUM-MINI IN WSS-VARI.
           MOVE HOY-SHOY     TO WSS-NUM-SINI IN WSS-VARI.
           MOVE HOY-AHOY     TO WSS-NUM-AINI IN WSS-VARI.

      *Calcular el ultimo dia del mes
           MOVE 01           TO WSS-NUM-DFIN IN WSS-VARI.
           MOVE WSS-NUM-MPRO TO WSS-NUM-MFIN IN WSS-VARI.
           MOVE HOY-SHOY     TO WSS-NUM-SFIN IN WSS-VARI.
           MOVE HOY-AHOY     TO WSS-NUM-AFIN IN WSS-VARI.
           ADD 1 TO WSS-NUM-MFIN IN WSS-VARI.
           IF WSS-NUM-MFIN = 13
              MOVE 01 TO WSS-NUM-MFIN IN WSS-VARI
              ADD 1   TO WSS-NUM-AFIN IN WSS-VARI.
           PERFORM BUS-ULT-HAB.
           IF MSG-COD-MENS > SPACES 
               DISPLAY 'PROCESO CANCELADO ' FEC-STAT
               DISPLAY 'ERROR EN RUTINA ULTIMO DIA DEL MES '
                                                 FIO-STAT
               DISPLAY MSG-COD-MENS
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.

           MOVE FIO-OUT TO FIO-CMND.
           PERFORM DAP-FIO-EDM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'PROCESO CANCELADO ' FEC-STAT
               DISPLAY 'ERROR FIO-UPD EDM, FIO-STAT : ' FIO-STAT
               PERFORM GNS-PRO-ABT.

       LUP-MAIN.
           IF NOT FIO-STAT-OKS
                GO TO FIN-MAIN.
           PERFORM PRO-OPD.
       FIN-MAIN.

           DISPLAY 'TOTALES DE CONTROL DAPPGEDM '.
           DISPLAY '--------------------------- '.
           DISPLAY '     '.
           DISPLAY 'TOTAL REGISTROS LEIDOS OPD   : ' WSS-LEI-OPD.
           DISPLAY 'TOTAL REGISTROS LEIDOS LIQ   : ' WSS-LEI-LIQ.
           DISPLAY 'TOTAL REGISTROS LEIDOS VEN   : ' WSS-LEI-VEN.
           DISPLAY 'TOTAL REGISTROS LEIDOS CAD   : ' WSS-LEI-CAD.
           DISPLAY 'TOTAL REGISTROS GRABADOS EDM : ' WSS-GRA-EDM.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-EDM.

           PERFORM GNS-PRO-END.

       COPY DAPBFOPD.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBGABT.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.

      *<<<< EOF
      *Entidades  de GNS
       COPY GNSBFCIC.
       COPY GNSBFMSC.
       COPY GNSBGCIC.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY GNSBVTAB.

      *Entidades  de DAP
       COPY DAPBFLIQ.
       COPY DAPBFCAD.
       COPY DAPBFVEN.
       COPY DAPBFPTD.
       COPY DAPBFEDM.
       COPY DAPBG004.

       PRO-OPD SECTION.
       INI-PRO-OPD.
      * RECORRE OPD POR FECHA COMPUTACIONAL
           MOVE WSS-FEC-FINI TO OPD-FEC-FCMP IN OPD.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY ' ERROR EN ACCESO A OPD: ' 
                         OPD-FEC-FCMP IN OPD ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
           IF OPD-FEC-FCMP IN OPD > WSS-FEC-FTER IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS OPD EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI ' A '
                                WSS-FEC-FFIN IN WSS-VARI
               GO TO FIN-PRO-OPD.

       LUP-PRO-OPD.
           ADD 1 TO WSS-LEI-OPD.
      *Seleccionar solo DAP vigentes (DAP fijos no vencidos y los
      *DAP renovables) 
           IF OPD-NUM-MPVC IN OPD NOT > WSS-NUM-MPRO IN WSS-VARI
              GO TO NXT-PRO-OPD.

           IF OPD-COD-RENO IN OPD NOT = 'S'
              GO TO NXT-PRO-OPD.

           PERFORM PRO-EST. 

       NXT-PRO-OPD.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS OR
              OPD-FEC-FCMP IN OPD > WSS-FEC-FTER 
               GO TO FIN-PRO-OPD
           ELSE
               GO TO LUP-PRO-OPD.
       FIN-PRO-OPD.
           EXIT.

       PRO-EST SECTION.
       INI-PRO-EST.
           MOVE OPD-CIC-IOPD IN OPD TO LIQ-CIC-IOPD IN LIQ.
           MOVE 1                   TO LIQ-NUM-ILIQ IN LIQ.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           IF NOT FIO-STAT-OKS
               DISPLAY 'LIQ INEXISTENTE PARA OPD : CIC=' 
                        LIQ-CIC-IOPD IN LIQ
              GO TO FIN-PRO-EST.

           ADD 1 TO WSS-LEI-LIQ IN WSS-VARI.

           MOVE OPD-CIC-IOPD IN OPD TO VEN-CIC-IOPD IN VEN.
           MOVE 1                   TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT FIO-STAT-OKS
               DISPLAY ' ' 
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' VEN INEXISTENTE PARA OPD : CIC=' 
                         VEN-CIC-IOPD IN VEN
               PERFORM GNS-PRO-ABT.
 
           ADD 1 TO WSS-LEI-VEN IN WSS-VARI.

           MOVE OPD-CAI-IOPD IN OPD TO CAD-CAI-IOPD IN CAD.
           MOVE OPD-IIC-IOPD IN OPD TO CAD-IIC-IOPD IN CAD.
           MOVE ZEROES              TO CAD-NUM-ILIQ IN CAD.
           MOVE ZEROES              TO CAD-NUM-ICAD IN CAD.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-CAD.
           IF NOT ( FIO-STAT-OKS AND
              OPD-CAI-IOPD IN OPD = CAD-CAI-IOPD IN CAD AND
              OPD-IIC-IOPD IN OPD = CAD-IIC-IOPD IN CAD )
              DISPLAY ' ' 
              DISPLAY 'CAD INEXISTENTE PARA OPD : CIC=' 
                        OPD-CIC-IOPD IN OPD
              GO TO FIN-PRO-EST.

           ADD 1 TO WSS-LEI-CAD IN WSS-VARI.

           PERFORM PRO-LOAD-EDM.

       FIN-PRO-EST.
           EXIT.

       PRO-LOAD-EDM SECTION.
       INI-PRO-LOAD-EDM.
      *    EDM Registro 
           MOVE SPACES TO EDM.
      *
      *    Oficina
           MOVE OPD-COD-OFIC IN OPD TO EDM-COD-OFIC IN EDM.
      *
      *    Valor de Cambio Operacion Captacion
           MOVE OPD-COD-VCAM IN OPD TO EDM-COD-VCAM IN EDM.
      *
      *    SERIAL operacion captacion
           MOVE OPD-CIC-ISER IN OPD TO EDM-CIC-ISER IN EDM.
      *
      *    Clase de Operacion
           MOVE OPD-COD-COPD IN OPD TO EDM-COD-COPD IN EDM.
      *
      *    Monto de la Captacion
           MOVE OPD-VAL-CAPT IN OPD TO EDM-VAL-CAPT IN EDM.
      *
      *    Monto Intereses Periodo
           SUBTRACT LIQ-SGV-CAPI IN LIQ FROM LIQ-SGV-TPAG IN LIQ
                    GIVING EDM-VAL-INTE IN EDM.
      *
      *    Monto Liquidado
           MOVE LIQ-SGV-TPAG IN LIQ TO EDM-VAL-MLIQ IN EDM.
      *
      *    Oficina de Liquidacion
           MOVE LIQ-COD-OFIC IN LIQ TO EDM-COD-OFLI IN EDM.
      *
      *    Fecha de liquidacion
           MOVE LIQ-FEC-FREA IN LIQ TO EDM-FEC-FLIQ IN EDM.
      *
      *    Fecha de Vencimiento
           MOVE VEN-FEC-FVEN IN VEN TO EDM-FEC-FVEN IN EDM.
      *
      *    Fecha de Captacion
           MOVE OPD-FEC-FREA IN OPD TO EDM-FEC-FCAP IN EDM.
      *
      *    Valor Tasa Referencial a 30 dias
           MOVE SPACES TO MSG-COD-MENS.
           PERFORM GET-TINT.
           PERFORM BUS-OPD-SGV-TINT.
           IF MSG-COD-MENS > SPACES
              DISPLAY ' ' 
              DISPLAY ' PROCESO CANCELADO ' 
              DISPLAY ' ERROR CALCULO TASA REF. ' MSG-COD-MENS
              MOVE ZEROES  TO EDM-SGV-TREF IN EDM
              PERFORM GNS-PRO-ABT.
      *
           IF OPD-COD-RENO IN OPD = 'S'
      *       Fecha de Captacion Ultima Renovacion
              MOVE OPD-FEC-FREA IN OPD TO EDM-FEC-FCUR IN EDM
      *
      *       Capital Ultima Renovacion
              MOVE OPD-VAL-CAPT IN OPD TO EDM-VAL-CAUR IN EDM.
      *
      *    Calcula Diferencia en Dias
           MOVE OPD-NUM-DREA IN OPD      TO FEC-DEC1 IN FEC-VARI.
           MOVE OPD-NUM-MREA IN OPD      TO FEC-MEC1 IN FEC-VARI.
           MOVE OPD-NUM-SREA IN OPD      TO FEC-SEC1 IN FEC-VARI.
           MOVE OPD-NUM-AREA IN OPD      TO FEC-AEC1 IN FEC-VARI.
           MOVE VEN-NUM-DVEN IN VEN      TO FEC-DEC2 IN FEC-VARI.
           MOVE VEN-NUM-MVEN IN VEN      TO FEC-MEC2 IN FEC-VARI.
           MOVE VEN-NUM-SVEN IN VEN      TO FEC-SEC2 IN FEC-VARI.
           MOVE VEN-NUM-AVEN IN VEN      TO FEC-AEC2 IN FEC-VARI.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF FEC-STAT-OKS
      *        Plazo en Dias
               MOVE FEC-NDIA TO EDM-NUM-PLZO IN EDM
           ELSE
               DISPLAY 'ERROR EN CAL-FEC PARA BUSQUEDA PLAZO'
               DISPLAY MSG-COD-MENS
               MOVE ZEROES TO EDM-NUM-PLZO IN EDM.
      *
      *    Terminal de la Captacion
      *PENDIENTE
           MOVE SPACES TO EDM-COD-TERM IN EDM.
      *
      *    Indicador ONP
           MOVE OPD-COD-IONP IN OPD TO EDM-IND-IONP IN EDM.
      *
      *    Indicador Garantia
           MOVE OPD-COD-GARA IN OPD TO EDM-IND-GARA IN EDM.
      *
      *BUS-MSC busca miscelaneo en tabla
           MOVE 'TAB'  TO MSC-COD-SIST.
           MOVE 'TCYA' TO MSC-COD-TMSC IN MSC.
           MOVE CAD-IND-TCAD IN CAD TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF MSC-CIC-MSCL IN MSC = 'TCYACAJA'
      *       Indicador Pago Efectivo
              MOVE 'S'              TO EDM-IND-EFEC IN EDM.
      *    Indicador Pago con Cheque BCI
           MOVE SPACES              TO EDM-IND-CBCI IN EDM.
      *
      *    Indicador Pago con Cheque otro Banco
           MOVE SPACES              TO EDM-IND-OBCO IN EDM.
      *
           PERFORM EXHIBIT-EDM.
           PERFORM PUT-EDM.
      *
       FIN-PRO-LOAD-EDM.
           EXIT.

       PUT-EDM SECTION.
       INI-PUT-EDM.
           MOVE FIO-PUT TO FIO-CMND.
      *  Interfaz Estadisticas de Marketing
           PERFORM DAP-FIO-EDM.
           IF NOT FIO-STAT-OKS
              DISPLAY ' ' 
              DISPLAY ' PROCESO CANCELADO ' 
              DISPLAY ' ERROR FIO-PUT EDM, FIO-STAT : ' FIO-STAT
              PERFORM GNS-PRO-ABT
           ELSE
              ADD 1 TO WSS-GRA-EDM.
       FIN-PUT-EDM.
           EXIT.

       EXHIBIT-EDM SECTION.
       INI-EXHIBIT-EDM.
           DISPLAY '     '.
           DISPLAY '     '.
           EXHIBIT NAMED EDM-COD-OFIC.
           EXHIBIT NAMED EDM-COD-VCAM.
           EXHIBIT NAMED EDM-CIC-ISER.
           EXHIBIT NAMED EDM-COD-COPD.
           EXHIBIT NAMED EDM-VAL-CAPT.
           EXHIBIT NAMED EDM-VAL-INTE.
           EXHIBIT NAMED EDM-VAL-MLIQ.
           EXHIBIT NAMED EDM-COD-OFLI.
           EXHIBIT NAMED EDM-FEC-FLIQ.
           EXHIBIT NAMED EDM-FEC-FVEN.
           EXHIBIT NAMED EDM-FEC-FCAP.
           EXHIBIT NAMED EDM-SGV-TREF.
           EXHIBIT NAMED EDM-FEC-FCUR.
           EXHIBIT NAMED EDM-VAL-CAUR.
           EXHIBIT NAMED EDM-NUM-PLZO.
           EXHIBIT NAMED EDM-IND-IONP.
           EXHIBIT NAMED EDM-IND-GARA.
           EXHIBIT NAMED EDM-IND-EFEC.
           EXHIBIT NAMED EDM-IND-CBCI.
           EXHIBIT NAMED EDM-IND-OBCO.
       FIN-EXHIBIT-EDM.
           EXIT.

       BUS-ULT-HAB SECTION.
       INI-BUS-ULT-HAB.
      * BUS-ULTIMO-DIA-HABIL
           MOVE WSS-FEC-FFIN TO FEC-FECH.
       LUP-BUS-ULT-HAB.
      *     DISPLAY 'FECHA LUP ' WSS-FEC-FFIN.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM CAL-FEC.
      *VAL-FEC Validar Fecha
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
      *     IF NOT FEC-STAT-HBL
      *        GO TO LUP-BUS-ULT-HAB.

       NOT-BUS-ULT-HAB.
           MOVE FEC-FECH TO WSS-FEC-FFIN.
           DISPLAY 'ULT. FECHA MES ' WSS-FEC-FFIN.
       FIN-BUS-ULT-HAB.
           EXIT.

       BUS-OPD-SGV-TINT SECTION.
       INI-BUS-OPD-SGV-TINT.
      *VAL-FEC diferencia de fechas para calcular valor final
           MOVE OPD-NUM-DREA IN OPD  TO FEC-DEC1 IN FEC-VARI.
           MOVE OPD-NUM-MREA IN OPD  TO FEC-MEC1 IN FEC-VARI.
           MOVE OPD-NUM-SREA IN OPD  TO FEC-SEC1 IN FEC-VARI.
           MOVE OPD-NUM-AREA IN OPD  TO FEC-AEC1 IN FEC-VARI.
           MOVE OPD-NUM-DPVC IN OPD  TO FEC-DEC2 IN FEC-VARI.
           MOVE OPD-NUM-MPVC IN OPD  TO FEC-MEC2 IN FEC-VARI.
           MOVE OPD-NUM-SPVC IN OPD  TO FEC-SEC2 IN FEC-VARI.
           MOVE OPD-NUM-APVC IN OPD  TO FEC-AEC2 IN FEC-VARI.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA TO FEC-CMND.
           PERFORM CAL-FEC.
           IF FEC-STAT-OKS
               MOVE FEC-NDIA TO WSS-NDIA IN WSS-VARI
           ELSE
               DISPLAY 'ERROR EN CAL-FEC PARA BUSQUEDA DE TASA'
               DISPLAY MSG-COD-MENS
               GO TO FIN-BUS-OPD-SGV-TINT.

           MOVE 10 TO WSS-TRM-DIA.
           MOVE 5  TO WSS-TRM-MTO.

           MOVE 'N' TO WSS-IND-TDIA.
           PERFORM BUS-TRM-DIA VARYING WSS-I FROM 1 BY 1 UNTIL
                              WSS-I > 10 OR WSS-IND-TDIA = 'S'.
           IF WSS-IND-TDIA = 'N'
               MOVE 'TDIA NOESPEC' TO MSG-COD-MENS
               GO TO FIN-BUS-OPD-SGV-TINT.

           MOVE 'N' TO WSS-IND-TMTO.
           PERFORM BUS-TRM-MTO VARYING WSS-I FROM 5 BY -1 UNTIL
                   WSS-I < 1 OR WSS-IND-TMTO = 'S'.

           IF WSS-IND-TMTO = 'N'
                MOVE 1 TO WSS-TRM-MTO.

           MULTIPLY WSS-TRM-DIA BY 5 GIVING WSS-IDX-INT.
           SUBTRACT 5 FROM WSS-IDX-INT.
           ADD WSS-TRM-MTO TO WSS-IDX-INT.

           MOVE PTD-SGV-TMIN IN PTD(WSS-IDX-INT) TO
                                    EDM-SGV-TREF IN EDM.
       FIN-BUS-OPD-SGV-TINT.
           EXIT.

       BUS-TRM-DIA SECTION.
       INI-BUS-TRM-DIA.
           IF WSS-NDIA IN WSS-VARI NOT > 
              PTD-NUM-DIAS IN PTD(WSS-I)
               MOVE WSS-I TO WSS-TRM-DIA
               MOVE 'S' TO WSS-IND-TDIA.
       FIN-BUS-TRM-DIA.
           EXIT.

       BUS-TRM-MTO SECTION.
       INI-BUS-TRM-MTO.
           IF PTD-VAL-MNTO IN PTD(WSS-I) > ZEROES
               IF OPD-VAL-CAPT IN OPD NOT <
                   PTD-VAL-MNTO IN PTD(WSS-I)
                   MOVE WSS-I TO WSS-TRM-MTO
                   MOVE 'S' TO WSS-IND-TMTO.
       FIN-BUS-TRM-MTO.
           EXIT.

       GET-TINT SECTION.
       INI-GET-TINT.
      *    obtener ptd mas actual y vigente
      *    acceso a ptd con VCAM del SRT
           MOVE OPD-COD-VCAM IN OPD     TO PTD-COD-VCAM IN PTD.
           MOVE SPACES                  TO PTD-COD-OFIC IN PTD.
           MOVE SPACES                  TO PTD-COD-TOPD IN PTD.
           MOVE SPACES                  TO PTD-COD-AOPD IN PTD.
           PERFORM GET-FHOY.
           MOVE OPD-NUM-DREA IN OPD TO PTD-NUM-DPTD IN PTD.
           MOVE OPD-NUM-MREA IN OPD TO PTD-NUM-MPTD IN PTD.
           MOVE OPD-NUM-SREA IN OPD TO PTD-NUM-SPTD IN PTD.
           MOVE OPD-NUM-AREA IN OPD TO PTD-NUM-APTD IN PTD.

           IF OPD-FEC-FREA IN OPD = OPD-FEC-FCMP IN OPD
                 MOVE HOY-HHOY TO PTD-HRA-HRPT IN PTD
           ELSE
                 MOVE '235959' TO PTD-HRA-HRPT IN PTD.
      * ISP: DAPBG004
           PERFORM CMP-FEC-PTD.

           MOVE FIO-GET-NLS TO FIO-CMND.

       LUP-GET-TINT.
           PERFORM DAP-FIO-PTD.
           IF NOT ( FIO-STAT-OKS AND
                    OPD-COD-VCAM IN OPD = PTD-COD-VCAM IN PTD )
               MOVE 'PTD    NEX' TO MSG-COD-MENS
               DISPLAY 'PTD NO EXISTE '
               GO TO FIN-GET-TINT.

           IF NOT ( ( PTD-COD-STAT IN PTD = 'S' )
                    AND
                    ( PTD-COD-TOPD IN PTD = '*' OR
                      PTD-COD-TOPD IN PTD = OPD-COD-TOPD IN OPD )
                    AND
                    ( PTD-COD-AOPD IN PTD = '*' OR
                      PTD-COD-AOPD IN PTD = OPD-COD-AOPD IN OPD )
                    AND
                    ( PTD-COD-OFIC IN PTD = '*' OR
                      PTD-COD-OFIC IN PTD = OPD-COD-OFIC IN OPD ) )
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-GET-TINT.
       FIN-GET-TINT.
           EXIT.
      *>>>>
-END
/*
//STEP1  EXEC COBUCL,PARM.COB='APOST,LIB,BUF=15000,FLOW=20,STATE'
//COB.SYSPRINT DD SYSOUT=*
//COB.SYSLIB   DD DSN=ADR.DB75.SOURCE.DESA,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES.V31,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES.V30,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=DEUCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DEUCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=COLCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=COLCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=SUPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SUPCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=SBTCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SBTCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//COB.SYSIN    DD DSN=DAPCL.FUENTES(DAPPGEDM),
//             DISP=SHR,SUBSYS=(LAM,'PSWD=CONS')
//LKED.SYSLIN  DD DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD DDNAME=SYSIN
//LKED.SYSLMOD DD DSN=DAPCM.LINEA(DAPPGEDM),
//             DISP=SHR
//LKED.SYSLIB  DD DSN=CICS.V211.LOADLIB,DISP=SHR
//             DD DSN=GNSCM.RUTINAS.CONSIST,DISP=SHR
//             DD DSN=DAPCM.LINEA,DISP=SHR
//             DD DSN=COB.VSCLLIB,DISP=SHR
//LKED.OBJLIB  DD DSN=TABST.OBJECT,DISP=SHR
//             DD DSN=DAPCT.OBJECT,DISP=SHR
//             DD DSN=ADR.DB75.OBJECT.DESA,DISP=SHR
//LKED.SYSIN   DD *
 INCLUDE OBJLIB(DAPCGEDM)
 ENTRY BEGIN
 NAME DAPPGEDM(R)
/*
//
