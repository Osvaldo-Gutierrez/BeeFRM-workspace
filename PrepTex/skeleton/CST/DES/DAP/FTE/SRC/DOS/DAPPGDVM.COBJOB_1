//ISILVA   JOB ,,CLASS=A,MSGCLASS=X,USER=ISILVA,PASSWORD=SLV93MAY,
//            REGION=4M
/*ROUTE PRINT MVS
//STEP1  EXEC PGM=AFOLIBR
//STEPLIB  DD DSN=ADR.LIB.V38.LOAD,DISP=SHR
//MASTER   DD DSN=DAPCL.FUENTES,DISP=SHR
//OSJOB    DD DUMMY
//SYSPRINT DD SYSOUT=X
//SYSIN    DD *
-ADD DAPPGDVM,PSWD=CONS,ARC
     DUMMY
-EMOD
-SEL DAPPGDVM,CONS
-REP ALL
       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPGDVM.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.     15-APR-1993 12:00:55 

      * Programa Generador Interfaz Documentos Vencidos del Mes DVM con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       01  SRT.
           03 SRT-COD-PROD PIC 9(04).
           03 SRT-COD-REGI PIC 9(02).
           03 SRT-COD-CVAL PIC X(03).
           03 SRT-COD-COMU PIC 9(04).
           03 SRT-VAL-MONT PIC 9(11)V99.
           03 SRT-CIC-IOPD PIC X(12).

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY DAPBRVEN.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *Entidades de GNS
       COPY GNSBRMSC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
      *CST
       COPY GNSWGPES.
      * COPY GNSWGPDB.

      *Entidades de TAB
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY TABBRCOM.

      *Entidades de DAP
       COPY DAPBROPD.
      * COPY DAPBRLIQ.
       COPY DAPBRDVM.

       01  WSS-VARI.
           03  WSS-VAL-EDIT              PIC Z.ZZZ.ZZZ.ZZZ.ZZZ.
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-DFIN                      PIC 9(02).
               05  WSS-NUM-MFIN                      PIC 9(02).
               05  WSS-NUM-SFIN                      PIC 9(02).
               05  WSS-NUM-AFIN                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-DTER                      PIC 9(02).
           03  WSS-FEC-FHOY.
               05  WSS-NUM-SHOY                      PIC 9(02).
               05  WSS-NUM-AHOY                      PIC 9(02).
               05  WSS-NUM-MHOY                      PIC 9(02).
               05  WSS-NUM-DHOY                      PIC 9(02).
           03  WSS-GLS-COMU.
               05  WSS-NUM-COMU                      PIC 9(03).
               05  WSS-RES-COMU                      PIC X(01).
           03  WSS-LEI-VEN           VALUE ZEROES    PIC 9(07).
      *     03  WSS-LEI-LIQ           VALUE ZEROES    PIC 9(07).
           03  WSS-LEI-OPD           VALUE ZEROES    PIC 9(07).
           03  WSS-GRA-SRT           VALUE ZEROES    PIC 9(07).
           03  WSS-GRA-DVM           VALUE ZEROES    PIC 9(07).
      *     03  WSS-STAT-DVM          VALUE ZEROES    PIC 9(01).
           03  WSS-ACU-DVME          VALUE ZEROES    PIC 9(08).
           03  WSS-ACU-MONT          VALUE ZEROES    PIC 9(11)V99.
           03  WSS-KEY-ANT.
               05  WSS-PROD-ANT                      PIC 9(04).
               05  WSS-REGI-ANT                      PIC 9(02).
               05  WSS-CVAL-ANT                      PIC X(03).
           03  WSS-KEY-DVM.
               05  WSS-PROD-DVM                      PIC 9(04).
               05  WSS-REGI-DVM                      PIC 9(02).
               05  WSS-CVAL-DVM                      PIC X(03).
           03  WSS-COD-0999                          PIC X(06).
           03  WSS-COD-0998                          PIC X(06).
      * VARIABLE PARA COMPARAR
           03  WSS-COD-1013-CMP                      PIC X(06).
      * VARIABLE PARA VALOR DE CAMBIO
           03  WSS-COD-1013.
               05  WSS-COD-1013-CVAL                 PIC X(04).
               05  WSS-COD-1013-TVAL                 PIC X(02).
           03  WSS-COD-0013                          PIC X(06).
           03  WSS-SGV-0998                          PIC S9(11)V9999.
           03  WSS-SGV-1013                          PIC S9(11)V9999.
           03  WSS-SGV-0013                          PIC S9(11)V9999.
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-COM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *Entidades de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      *Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
      * COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LIQ-REQA==.

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPGDVM' TO FIO-PROG.
           PERFORM BUS-FSIS.
      * Captura parametros
           DISPLAY ' '. 
           DISPLAY 'INICIO CAPTURA PARAMETROS '.
           DISPLAY ' '. 
           DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA) : '.
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N  TO FEC-FBLK.
           MOVE FEC-CHOY-N  TO FEC-CHOY.
           MOVE FEC-VHBL-N  TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO ' FEC-STAT
               DISPLAY ' ERROR VALIDACION EN F. PROCESO '
                                   WSS-FEC-FPRO IN WSS-VARI
               PERFORM GNS-PRO-ABT.

           PERFORM GET-FHOY.
           MOVE FEC-DHOY TO WSS-NUM-DHOY IN WSS-VARI.
           MOVE FEC-MHOY TO WSS-NUM-MHOY IN WSS-VARI.
           MOVE FEC-SHOY TO WSS-NUM-SHOY IN WSS-VARI.
           MOVE FEC-AHOY TO WSS-NUM-AHOY IN WSS-VARI.

           MOVE 01           TO WSS-NUM-DINI IN WSS-VARI.
           MOVE WSS-NUM-MPRO TO WSS-NUM-MINI IN WSS-VARI.
           MOVE HOY-SHOY     TO WSS-NUM-SINI IN WSS-VARI.
           MOVE HOY-AHOY     TO WSS-NUM-AINI IN WSS-VARI.
           DISPLAY 'FECHA INICIO  SELECCION ' WSS-NUM-DINI IN WSS-VARI
                                              WSS-NUM-MINI IN WSS-VARI
                                              WSS-NUM-SINI IN WSS-VARI
                                              WSS-NUM-AINI IN WSS-VARI.

      *Calcular el ultimo dia habil del mes y ult. dia mes
           MOVE 01           TO WSS-NUM-DFIN IN WSS-VARI.
           MOVE WSS-NUM-MPRO TO WSS-NUM-MFIN IN WSS-VARI.
           MOVE HOY-SHOY     TO WSS-NUM-SFIN IN WSS-VARI.
           MOVE HOY-AHOY     TO WSS-NUM-AFIN IN WSS-VARI.
           ADD 1 TO WSS-NUM-MFIN IN WSS-VARI.
           IF WSS-NUM-MFIN = 13
              MOVE 01 TO WSS-NUM-MFIN IN WSS-VARI.
           PERFORM BUS-ULT-HAB.
           IF MSG-COD-MENS > SPACES 
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO ' 
              DISPLAY ' ERROR EN RUTINA ULTIMO DIA DEL MES '
                                              MSG-COD-MENS
              PERFORM GNS-PRO-ABT.

      *Se genera solo ultimo dia del mes en wss-fec-fter
           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.

           DISPLAY 'FECHA TERMINO SELECCION ' WSS-NUM-DTER IN WSS-VARI
                                              WSS-NUM-MTER IN WSS-VARI
                                              WSS-NUM-STER IN WSS-VARI
                                              WSS-NUM-ATER IN WSS-VARI.

      *Calcular valor de cambio de U.F., US$, US$REAJ a fecha de termino.

      * Obtiene Codigo Valor de Cambio de Pesos Mediante CIC de la Moneda '0999'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-0999   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-0999 IN WSS-VARI.

      * Obtiene Codigo Valor de Cambio de UF Mediante CIC de la Moneda '0998'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-0998   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0998  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-0998 IN WSS-VARI.

      * Obtiene Codigo Valor de Cambio de Pesos Mediante CIC de la Moneda '0013'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-0013   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0013  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-0013 IN WSS-VARI.

      * Obtiene Codigo Valor de Cambio de US$ Mediante CIC de la Moneda '1013'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-1013   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC1013  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.

           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-1013      IN WSS-VARI.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-1013-CMP  IN WSS-VARI.
           MOVE PDB-COD-TVAL-DOLO   TO WSS-COD-1013-TVAL IN WSS-VARI.
      *     DISPLAY 'DOLL ' WSS-COD-1013 IN WSS-VARI.

      * Obtiene Valor de Cambio de la UF a Fecha ultimo dia habil mes
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-0998 IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-0999 IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' FIO-STAT
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA LA UF.'
               PERFORM GNS-PRO-ABT.

           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' FIO-STAT
               DISPLAY 'INDICADOR DE VIGENCIA NO VALIDO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA LA UF.'
               PERFORM GNS-PRO-ABT.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-0998 IN WSS-VARI.
      *     DISPLAY 'VCAM UF ' WSS-SGV-0998 IN WSS-VARI.

      * Obtiene Valor de Cambio de la US$ a Fecha Ultimo dia Habil Mes
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-1013 IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-0999 IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' FIO-STAT
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA US$.'
               PERFORM GNS-PRO-ABT.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' FIO-STAT
               DISPLAY 'INDICADOR DE VIGENCIA NO VALIDO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA US$.'
               PERFORM GNS-PRO-ABT.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-1013 IN WSS-VARI.
      *     DISPLAY 'VCAM US$ ' WSS-SGV-1013 IN WSS-VARI.

      * Obtiene Valor de Cambio de US$REAJ a Fecha Ultimo dia Habil Mes
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-0013 IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-0999 IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' FIO-STAT
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                       WSS-FEC-FTER IN WSS-VARI ' PARA US$REAJ.'
               PERFORM GNS-PRO-ABT.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' FIO-STAT
               DISPLAY 'INDICADOR DE VIGENCIA NO VALIDO A ESTA FECHA : '
                       WSS-FEC-FTER IN WSS-VARI ' PARA US$REAJ.'
               PERFORM GNS-PRO-ABT.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-0013 IN WSS-VARI.
      *     DISPLAY 'VCAM US$REAJ ' WSS-SGV-0013 IN WSS-VARI.

           DISPLAY 'FIN VALIDACION PARAMETROS '.
           SORT GNSSRT
              ON ASCENDING KEY SRT-COD-PROD IN SRT
              ON ASCENDING KEY SRT-COD-REGI IN SRT
              ON ASCENDING KEY SRT-COD-CVAL IN SRT
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *     MOVE FIO-INP TO FIO-CMND.
      *     PERFORM DAP-FIO-VEN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-DVM.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'ERROR APERTURA ARCHIVO DVM' 
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
       FST-INP-SRT.
           MOVE WSS-NUM-DINI IN WSS-VARI TO VEN-NUM-DVEN IN VEN.
           MOVE WSS-NUM-MINI IN WSS-VARI TO VEN-NUM-MVEN IN VEN.
           MOVE WSS-NUM-SINI IN WSS-VARI TO VEN-NUM-SVEN IN VEN.
           MOVE WSS-NUM-AINI IN WSS-VARI TO VEN-NUM-AVEN IN VEN.
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.

           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'ERROR EN ACCESO A VEN ' 
                        VEN-FEC-FVEN IN VEN ' FS: ' FIO-STAT
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTER IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS VEN EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI ' A '
                                WSS-FEC-FTER IN WSS-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
           ADD 1 TO WSS-LEI-VEN IN WSS-VARI.
      *     DISPLAY 'VENCIMIENTO CANDIDATO ' VEN-CAI-IOPD IN VEN '-'
      *                                     VEN-IIC-IOPD IN VEN
      *             ' FECHA VENC. ' VEN-NUM-DVEN IN VEN
      *                             VEN-NUM-MVEN IN VEN
      *                             VEN-NUM-SVEN IN VEN
      *                             VEN-NUM-AVEN IN VEN.
           PERFORM GET-OPD-VEN.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR NO EXISTE OPERACION PARA VEN: ' 
                        VEN-CIC-IOPD IN VEN ' STATUSLCT. : ' FIO-STAT
               GO TO NXT-INP-SRT.

           ADD 1 TO WSS-LEI-OPD IN WSS-VARI.

           IF OPD-COD-PLZO IN OPD = 'D1' OR 'D2'
              MOVE 3020 TO SRT-COD-PROD IN SRT
           ELSE
           IF OPD-COD-PLZO IN OPD = 'D3'
              MOVE 3025 TO SRT-COD-PROD IN SRT
           ELSE
           IF OPD-COD-PLZO IN OPD = 'D4'
              MOVE 3065 TO SRT-COD-PROD IN SRT.

           MOVE OPD-COD-OFIC IN OPD TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           IF MSG-COD-MENS  = 'COD    NEX' 
              MOVE 00 TO SRT-COD-COMU IN SRT
              MOVE 00 TO SRT-COD-REGI IN SRT
           ELSE
              MOVE OFI-COD-COMU IN OFI TO WSS-GLS-COMU IN WSS-VARI
              MOVE WSS-NUM-COMU IN WSS-VARI TO SRT-COD-COMU IN SRT
              MOVE 'COM'               TO COM-COD-TTAB IN COM
              MOVE OFI-COD-COMU IN OFI TO COM-COD-COMU IN COM
              MOVE FIO-GET-KEY TO FIO-CMND
              PERFORM TAB-FIO-COM
              IF NOT FIO-STAT-OKS
                 MOVE 00 TO SRT-COD-REGI IN SRT
              ELSE
                 MOVE COM-COD-REGI IN COM TO SRT-COD-REGI IN SRT.  

           MOVE VEN-VAL-CAPI IN VEN TO SRT-VAL-MONT IN SRT.  
           IF OPD-COD-VCAM IN OPD = WSS-COD-0999
              MOVE 'CHN' TO SRT-COD-CVAL IN SRT
           ELSE
           IF OPD-COD-VCAM IN OPD = WSS-COD-0998 
              MOVE 'CHR' TO SRT-COD-CVAL IN SRT
              MULTIPLY VEN-VAL-CAPI IN VEN BY 
                       WSS-SGV-0998 IN WSS-VARI GIVING 
                       SRT-VAL-MONT IN SRT
           ELSE
           IF OPD-COD-VCAM IN OPD = WSS-COD-0013
              MOVE 'CHR' TO SRT-COD-CVAL IN SRT
              MULTIPLY VEN-VAL-CAPI IN VEN BY 
                       WSS-SGV-0013 IN WSS-VARI GIVING 
                       SRT-VAL-MONT IN SRT
           ELSE
           IF OPD-COD-VCAM IN OPD = WSS-COD-1013-CMP
              MOVE 'EXT' TO SRT-COD-CVAL IN SRT
              MULTIPLY VEN-VAL-CAPI IN VEN BY 
                       WSS-SGV-1013 IN WSS-VARI GIVING 
                       SRT-VAL-MONT IN SRT.

           MOVE OPD-CIC-IOPD IN OPD TO SRT-CIC-IOPD IN SRT.
       MOV-INP-SRT.
           RELEASE SRT.
      *     DISPLAY 'SRT ' SRT-COD-PROD ' ' SRT-COD-REGI ' '
      *                    SRT-COD-CVAL.
           ADD 1 TO WSS-GRA-SRT IN WSS-VARI. 
       NXT-INP-SRT.
      *Ubica registro en VEN por fecha
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
      *Controla ingreso de registros por fecha termino
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTER IN WSS-VARI
              GO TO FIN-INP-SRT.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
      *Proceso PRO-LIQ por FLIQ REAL
      *     MOVE WSS-NUM-DINI IN WSS-VARI TO LIQ-NUM-DCMP IN LIQ.
      *     MOVE WSS-NUM-MINI IN WSS-VARI TO LIQ-NUM-MCMP IN LIQ.
      *     MOVE WSS-NUM-SINI IN WSS-VARI TO LIQ-NUM-SCMP IN LIQ.
      *     MOVE WSS-NUM-AINI IN WSS-VARI TO LIQ-NUM-ACMP IN LIQ.
      *     MOVE FIO-GET-NLS TO FIO-CMND.
      *     MOVE 'LIQ-FEC-FCMP' TO FIO-AKEY.
      *     PERFORM DAP-FIO-LIQ.
      *     PERFORM PRO-LIQ UNTIL 
      *         ( NOT FIO-STAT-OKS ) OR
      *         ( PGM-STAT-SRT-MAL ) OR
      *         ( LIQ-FEC-FCMP IN LIQ > WSS-FEC-FTER ).

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
      *Limpia FIO-AKEY para acceso a otras FIO
           MOVE SPACES TO FIO-AKEY. 
      *     MOVE 1 TO WSS-STAT-DVM IN WSS-VARI.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           MOVE SRT-COD-PROD IN SRT TO WSS-PROD-ANT.
           MOVE SRT-COD-REGI IN SRT TO WSS-REGI-ANT.
           MOVE SRT-COD-CVAL IN SRT TO WSS-CVAL-ANT.

       LUP-OUT-SRT.
      *     DISPLAY 'LEE SRT ' SRT.
      *     MOVE 0 TO WSS-STAT-DVM IN WSS-VARI.
           MOVE SRT-COD-PROD IN SRT TO WSS-PROD-DVM.
           MOVE SRT-COD-REGI IN SRT TO WSS-REGI-DVM.
           MOVE SRT-COD-CVAL IN SRT TO WSS-CVAL-DVM.
           IF WSS-KEY-DVM NOT = WSS-KEY-ANT             
      *        MOVE 1 TO WSS-STAT-DVM IN WSS-VARI
              PERFORM PRO-DVM
              MOVE ZEROES TO WSS-ACU-DVME IN WSS-VARI
                             WSS-ACU-MONT IN WSS-VARI
              MOVE SRT-COD-PROD IN SRT TO WSS-PROD-ANT
              MOVE SRT-COD-REGI IN SRT TO WSS-REGI-ANT
              MOVE SRT-COD-CVAL IN SRT TO WSS-CVAL-ANT.
               
           ADD 1                   TO WSS-ACU-DVME IN WSS-VARI.
           ADD SRT-VAL-MONT IN SRT TO WSS-ACU-MONT IN WSS-VARI.

           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.

      *     IF WSS-STAT-DVM IN WSS-VARI = 0
              PERFORM PRO-DVM.

           DISPLAY 'TOTALES DE CONTROL DAPPGDVM '.
           DISPLAY '--------------------------- '.
           DISPLAY '     '.
           DISPLAY 'TOTAL REGISTROS LEIDOS VEN   : ' WSS-LEI-VEN.
      *     DISPLAY 'TOTAL REGISTROS LEIDOS LIQ   : ' WSS-LEI-LIQ.
           DISPLAY 'TOTAL REGISTROS LEIDOS OPD   : ' WSS-LEI-OPD.
           DISPLAY 'TOTAL REGISTROS GRABADOS SRT : ' WSS-GRA-SRT.
           DISPLAY 'TOTAL REGISTROS GRABADOS DVM : ' WSS-GRA-DVM.
           DISPLAY '     '.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-DVM.
       EXT-OUT-SRT.
           EXIT.

       COPY DAPBFVEN.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.

      * PRO-LIQ SECTION.
      * INI-PRO-LIQ.
      **     DISPLAY 'PRO-LIQ ' LIQ-CIC-IOPD IN LIQ.
      *     ADD 1 TO WSS-LEI-LIQ IN WSS-VARI.
 
      *LEE OPD
      *     MOVE LIQ-CIC-IOPD IN LIQ TO OPD-CIC-IOPD IN OPD.
      *     MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM DAP-FIO-OPD.
      *     IF NOT FIO-STAT-OKS
      *         DISPLAY ' '
      *         DISPLAY ' PROCESO CANCELADO '
      *         DISPLAY ' ERROR NO EXISTE OPERACION PARA LIQ: '
      *                   OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT
      *         MOVE PGM-SMAL TO PGM-STAT-SRT
      *         GO TO FIN-PRO-LIQ.

      *     ADD 1 TO WSS-LEI-OPD IN WSS-VARI.
           
      *     ADD OPD-NUM-VSLQ IN OPD TO OPD-NUM-VNLQ IN OPD.
      *     IF OPD-NUM-VNLQ IN OPD NOT > 1
      *        GO TO END-PRO-LIQ.

      *     IF OPD-COD-PLZO IN OPD = 'D1' OR 'D2'
      *        MOVE 3020 TO SRT-COD-PROD IN SRT
      *     ELSE
      *     IF OPD-COD-PLZO IN OPD = 'D3'
      *        MOVE 3025 TO SRT-COD-PROD IN SRT
      *     ELSE
      *     IF OPD-COD-PLZO IN OPD = 'D4'
      *        MOVE 3065 TO SRT-COD-PROD IN SRT.

      *     MOVE OPD-COD-OFIC IN OPD TO OFI-COD-OFIC IN OFI.
      *     PERFORM BUS-OFI.
      *     IF MSG-COD-MENS  = 'COD    NEX' 
      *        MOVE 00 TO SRT-COD-COMU IN SRT
      *        MOVE 00 TO SRT-COD-REGI IN SRT
      *     ELSE
      *        MOVE OFI-COD-COMU IN OFI TO WSS-GLS-COMU IN WSS-VARI
      *        MOVE WSS-NUM-COMU IN WSS-VARI TO SRT-COD-COMU IN SRT
      *        MOVE 'COM'               TO COM-COD-TTAB IN COM
      *        MOVE OFI-COD-COMU IN OFI TO COM-COD-COMU IN COM
      *        MOVE FIO-GET-KEY TO FIO-CMND
      *        PERFORM TAB-FIO-COM
      *        IF NOT FIO-STAT-OKS
      *           MOVE 00 TO SRT-COD-REGI IN SRT
      *        ELSE
      *           MOVE COM-COD-REGI IN COM TO SRT-COD-REGI IN SRT.  

      *     IF OPD-COD-VCAM IN OPD = WSS-COD-0999
      *        MOVE 'CHN' TO SRT-COD-CVAL IN SRT
      *        MOVE LIQ-SGV-CAPI IN LIQ TO SRT-VAL-MONT IN SRT  
      *     ELSE
      *     IF OPD-COD-VCAM IN OPD = WSS-COD-0998 
      *        MOVE 'CHR' TO SRT-COD-CVAL IN SRT
      *        MULTIPLY LIQ-SGV-CAPI IN LIQ BY 
      *                 WSS-SGV-0998 IN WSS-VARI GIVING 
      *                 SRT-VAL-MONT IN SRT
      *     ELSE
      *     IF OPD-COD-VCAM IN OPD = WSS-COD-0013
      *        MOVE 'CHR' TO SRT-COD-CVAL IN SRT
      *        MULTIPLY LIQ-SGV-CAPI IN LIQ BY 
      *                 WSS-SGV-0013 IN WSS-VARI GIVING 
      *                 SRT-VAL-MONT IN SRT
      *     ELSE
      *     IF OPD-COD-VCAM IN OPD = WSS-COD-1013
      *        MOVE 'EXT' TO SRT-COD-CVAL IN SRT
      *        MULTIPLY LIQ-SGV-CAPI IN LIQ BY 
      *                 WSS-SGV-1013 IN WSS-VARI GIVING 
      *                 SRT-VAL-MONT IN SRT.

      *     DIVIDE SRT-VAL-MONT IN SRT BY 1000 GIVING 
      *                                 SRT-VAL-MONT IN SRT. 
      *     MOVE OPD-CIC-IOPD IN OPD TO SRT-CIC-IOPD IN SRT.
      *     RELEASE SRT.
      *     DISPLAY 'SRT-RET ' SRT.           
      *     ADD 1 TO WSS-GRA-SRT IN WSS-VARI. 

      * END-PRO-LIQ.
      *     MOVE 'LIQ-FEC-FCMP' TO FIO-AKEY.
      *     MOVE FIO-GET-NXT TO FIO-CMND.
      *     PERFORM DAP-FIO-LIQ.
      * FIN-PRO-LIQ.
      *      EXIT.

       PRO-DVM SECTION.
       INI-PRO-DVM.
           MOVE WSS-PROD-ANT IN WSS-VARI TO DVM-COD-PROD IN DVM.
           MOVE WSS-REGI-ANT IN WSS-VARI TO DVM-COD-REGI IN DVM.
           MOVE WSS-CVAL-ANT IN WSS-VARI TO DVM-COD-CVAL IN DVM.
           MOVE WSS-ACU-DVME IN WSS-VARI TO DVM-NUM-DVME IN DVM.
           MOVE WSS-ACU-MONT IN WSS-VARI TO DVM-VAL-MONT IN DVM.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO DVM-NUM-MPRO IN DVM.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO DVM-NUM-APRO IN DVM.
           DIVIDE DVM-VAL-MONT IN DVM BY 1000 GIVING 
                                       DVM-VAL-MONT IN DVM.
           MOVE DVM-VAL-MONT IN DVM TO WSS-VAL-EDIT.
           DISPLAY ' '.
           DISPLAY 'DVM : ' 
              DVM-COD-PROD IN DVM ' / ' DVM-COD-REGI IN DVM ' / ' 
              DVM-COD-CVAL IN DVM ' / ' DVM-NUM-DVME IN DVM ' / ' 
              WSS-VAL-EDIT        ' / ' DVM-NUM-MPRO IN DVM ' / '
              DVM-NUM-APRO IN DVM.

           MOVE FIO-PUT TO FIO-CMND.
      *  Interfaz Documentos Vencidos del Mes
           PERFORM DAP-FIO-DVM.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR FIO-PUT DVM, FIO-STAT : ' FIO-STAT
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO EXT-OUT-SRT.
           ADD 1 TO WSS-GRA-DVM.
       FIN-PRO-DVM.
           EXIT.

       GET-OPD-VEN SECTION.
       INI-GET-OPD-VEN.
           MOVE VEN-CIC-IOPD IN VEN TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
       FIN-GET-OPD-VEN.
           EXIT.

       BUS-ULT-HAB SECTION.
       INI-BUS-ULT-HAB.
      * BUS-ULTIMO-DIA-HABIL
           MOVE WSS-FEC-FFIN TO FEC-FECH.
       LUP-BUS-ULT-HAB.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM CAL-FEC.
      *VAL-FEC Validar Fecha
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
      *     IF NOT FEC-STAT-HBL
      *        GO TO LUP-BUS-ULT-HAB.

       NOT-BUS-ULT-HAB.
           MOVE FEC-FECH TO WSS-FEC-FFIN.
           DISPLAY 'ULT. FECHA MES ' WSS-FEC-FFIN
           DISPLAY '  '.
       FIN-BUS-ULT-HAB.
           EXIT.

      *Entidades  de GNS
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBFMSC.
       COPY GNSBVMSC.
       COPY GNSBBMSC.

      *Entidades  de TAB
       COPY TABBFOFI.
       COPY TABBFCAM.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY TABBFCOM.

      *Entidades  de DAP
       COPY DAPBFOPD.
      * COPY DAPBFLIQ.
       COPY DAPBFDVM.
-END
/*
//STEP1  EXEC COBUCL,PARM.COB='APOST,LIB,BUF=15000,FLOW=20,STATE'
//COB.SYSPRINT DD SYSOUT=*
//COB.SYSLIB   DD DSN=ADRSF.DSG27.SOURCE.DESA,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES.V31,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES.V30,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//COB.SYSIN    DD DSN=DAPCL.FUENTES(DAPPGDVM),
//             DISP=SHR,SUBSYS=(LAM,'PSWD=CONS')
//LKED.SYSLIN  DD DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD DDNAME=SYSIN
//LKED.SYSLMOD DD DSN=DAPCM.LINEA(DAPPGDVM),
//             DISP=SHR
//LKED.SYSLIB  DD DSN=CICS.V211.LOADLIB,DISP=SHR
//             DD DSN=GNSCM.RUTINAS.CONSIST,DISP=SHR
//             DD DSN=DAPCM.LINEA,DISP=SHR
//             DD DSN=COB.VSCLLIB,DISP=SHR
//LKED.OBJLIB  DD DSN=TABST.OBJECT,DISP=SHR
//             DD DSN=DAPCT.OBJECT,DISP=SHR
//             DD DSN=ADRST.DSG27.OBJECT.DESA,DISP=SHR
//LKED.SYSIN   DD *
 INCLUDE OBJLIB(DAPCGDVM)
 ENTRY BEGIN
 NAME DAPPGDVM(R)
/*
//
