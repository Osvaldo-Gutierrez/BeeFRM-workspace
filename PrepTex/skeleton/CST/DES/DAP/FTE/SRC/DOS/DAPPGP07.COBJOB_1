//ISILVA   JOB ,,CLASS=A,MSGCLASS=X,USER=ISILVA,PASSWORD=SLV93MAY,
//            REGION=4M
/*ROUTE PRINT MVS
//STEP1  EXEC PGM=AFOLIBR
//STEPLIB  DD DSN=ADR.LIB.V38.LOAD,DISP=SHR
//MASTER   DD DSN=DAPCL.FUENTES,DISP=SHR
//OSJOB    DD DUMMY
//SYSPRINT DD SYSOUT=X
//SYSIN    DD *
-ADD DAPPGP07,PSWD=CONS,ARC
     DUMMY
-EMOD
-SEL DAPPGP07,CONS
-REP ALL
       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPGP07.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.     10-MAY-1993 08:01:54.

      * Programa Generador Interfaz Informacion P07 SuperBancos con Sort.
      * 
      * MANTENCIONES
      * ------------
      * JVM: 11/08/92 Control de aborto con Return Code = 100
      * JVM: 30/09/92 Modificacion acceso archivo CID 
      * 

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       01  SRT.
           03 SRT-COD-PROD PIC 9(04).
           03 SRT-COD-REGI PIC 9(02).
           03 SRT-COD-CVAL PIC X(03).
           03 SRT-VAL-MONT PIC 9(11)V99.
           03 SRT-COD-OFIC PIC 9(03).
           03 SRT-COD-COMU PIC 9(04).
           03 SRT-COD-RENO PIC X(01).
           03 SRT-CIC-IOPD PIC X(12).

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY DAPBROPD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGPES.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *Entidades de GNS
       COPY GNSBRMSC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.

      *Entidades de TAB
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY TABBRCOM.

      *Entidades de DAP
       COPY DAPBRP07.
       COPY DAPBRDVM.
       COPY DAPBRCID.

       01  WSS-VARI.
           03  WSS-VAL-EDIT                 PIC Z.ZZZ.ZZZ.ZZZ.ZZZ.
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-DFIN                      PIC 9(02).
               05  WSS-NUM-MFIN                      PIC 9(02).
               05  WSS-NUM-SFIN                      PIC 9(02).
               05  WSS-NUM-AFIN                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-DTER                      PIC 9(02).
           03  WSS-FEC-FHOY.
               05  WSS-NUM-SHOY                      PIC 9(02).
               05  WSS-NUM-AHOY                      PIC 9(02).
               05  WSS-NUM-MHOY                      PIC 9(02).
               05  WSS-NUM-DHOY                      PIC 9(02).
           03  WSS-GLS-COMU.
               05  WSS-NUM-COMU                      PIC 9(03).
               05  WSS-RES-COMU                      PIC X(01).
           03  WSS-NUM-TREN          VALUE ZEROES    PIC 9(07).
           03  WSS-NUM-PREN          VALUE ZEROES    PIC 9(03)V99.
           03  WSS-VAL-TREN          VALUE ZEROES    PIC 9(15)V99.
           03  WSS-VAL-PREN          VALUE ZEROES    PIC 9(15)V99.
           03  WSS-NUM-TDOC          VALUE ZEROES    PIC 9(07).
           03  WSS-VAL-TMON          VALUE ZEROES    PIC 9(15)V99.
           03  WSS-I                 VALUE ZEROES    PIC 9(02).
           03  WSS-J                 VALUE ZEROES    PIC 9(02).
           03  WSS-FIN1              VALUE ZEROES    PIC 9(02).
           03  WSS-FIN2              VALUE ZEROES    PIC 9(02).
           03  WSS-FIN3              VALUE ZEROES    PIC 9(02).
           03  WSS-IND-ERR1          VALUE ZEROES    PIC 9(01).
           03  WSS-PRO-OPD-LEI           VALUE ZEROES    PIC 9(07).
           03  WSS-PRO-OPD-BAD           VALUE ZEROES    PIC 9(07).
           03  WSS-PRO-CID-LEI           VALUE ZEROES    PIC 9(07).
           03  WSS-GRA-SRT           VALUE ZEROES    PIC 9(07).
           03  WSS-GRA-P07           VALUE ZEROES    PIC 9(07).
           03  WSS-STAT-P07          VALUE ZEROES    PIC 9(01).
           03  WSS-STAT-OPD          VALUE ZEROES    PIC 9(01).
           03  WSS-OFIC-ANT          VALUE ZEROES    PIC 9(03).
           03  WSS-GLS-VALI.
               05  WSS-SGV-VALI                      PIC 9(08)V9(02).
           03  WSS-KEY-ANT.
               05  WSS-PROD-ANT                      PIC 9(04).
               05  WSS-REGI-ANT                      PIC 9(02).
               05  WSS-CVAL-ANT                      PIC X(03).
           03  WSS-KEY-P07.
               05  WSS-PROD-P07                      PIC 9(04).
               05  WSS-REGI-P07                      PIC 9(02).
               05  WSS-CVAL-P07                      PIC X(03).
           03  WSS-TBL-P07.
               05  WSS-P07 OCCURS 5 TIMES.
                   07  WSS-DOC-P07                   PIC 9(06).
                   07  WSS-MON-P07                   PIC 9(15)V99.
           03  WSS-CIC-VCAM.
               05 WSS-CIC-TVAL                       PIC X(01).
               05 WSS-CIC-CVAL                       PIC X(03).
               05 WSS-CIC-TCAM                       PIC X(08).
           03  WSS-COD-CTAB.
               05  WSS-IND-CTAB                      PIC X(03).
               05  WSS-RES-CTAB                      PIC X(09).
           03  WSS-COD-0999                          PIC X(06).
           03  WSS-COD-0998                          PIC X(06).
           03  WSS-COD-1013.
               05  WSS-COD-1013-CVAL                 PIC X(04).
               05  WSS-COD-1013-TVAL                 PIC X(02).
           03  WSS-COD-0013                          PIC X(06).
           03  WSS-SGV-0998                          PIC S9(11)V9999.
           03  WSS-SGV-1013                          PIC S9(11)V9999.
           03  WSS-SGV-0013                          PIC S9(11)V9999.
           03  WSS-GLS-P07.
               05  WSS-COD-IEFE                      PIC 9(03).
               05  WSS-COD-IDEN                      PIC X(03).
               05  WSS-COD-PERM                      PIC 9(02).
               05  WSS-COD-PERA                      PIC 9(02).
               05  WSS-GLS-REST                      PIC X(104).
           03 WSS-TBL-SUP1.
              05  WSS-TBL-VSU1  OCCURS 10 TIMES.
                  07  WSS-SGV-VAL1             PIC 9(15)V9(02).

           03 WSS-TBL-SUP2.
              05  WSS-TBL-VSU2  OCCURS 10 TIMES.
                  07  WSS-SGV-VAL2             PIC 9(15)V9(02).

           03 WSS-TBL-SUP3.
              05  WSS-TBL-VSU3  OCCURS 10 TIMES.
                  07  WSS-SGV-VAL3             PIC 9(15)V9(02).
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-COM-REQA==.
      *Entidades de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.


       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPGP07' TO FIO-PROG.
           PERFORM BUS-FSIS.
      * Captura parametros
           DISPLAY ' '.
           DISPLAY ' INICIO CAPTURA PARAMETROS '.
           DISPLAY ' '.
           DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA) : '.
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N  TO FEC-FBLK.
           MOVE FEC-CHOY-N  TO FEC-CHOY.
           MOVE FEC-VHBL-N  TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO ' 
               DISPLAY ' ERROR VALIDACION EN F. PROCESO '
                   WSS-FEC-FPRO IN WSS-VARI ' FS: ' FEC-STAT    
               PERFORM GNS-PRO-ABT.

           PERFORM GET-FHOY.
           MOVE FEC-DHOY TO WSS-NUM-DHOY IN WSS-VARI.
           MOVE FEC-MHOY TO WSS-NUM-MHOY IN WSS-VARI.
           MOVE FEC-SHOY TO WSS-NUM-SHOY IN WSS-VARI.
           MOVE FEC-AHOY TO WSS-NUM-AHOY IN WSS-VARI.

           MOVE 01           TO WSS-NUM-DINI IN WSS-VARI.
           MOVE WSS-NUM-MPRO TO WSS-NUM-MINI IN WSS-VARI.
           MOVE WSS-NUM-SPRO TO WSS-NUM-SINI IN WSS-VARI.
           MOVE WSS-NUM-APRO TO WSS-NUM-AINI IN WSS-VARI.
      *     MOVE HOY-SHOY     TO WSS-NUM-SINI IN WSS-VARI.
      *     MOVE HOY-AHOY     TO WSS-NUM-AINI IN WSS-VARI.

      *Calcular el ultimo dia del mes
           MOVE 01           TO WSS-NUM-DFIN IN WSS-VARI.
           MOVE WSS-NUM-MPRO TO WSS-NUM-MFIN IN WSS-VARI.
           MOVE WSS-NUM-SPRO TO WSS-NUM-SFIN IN WSS-VARI.
           MOVE WSS-NUM-APRO TO WSS-NUM-AFIN IN WSS-VARI.
      *     MOVE HOY-SHOY     TO WSS-NUM-SFIN IN WSS-VARI.
      *     MOVE HOY-AHOY     TO WSS-NUM-AFIN IN WSS-VARI.
           ADD 1 TO WSS-NUM-MFIN IN WSS-VARI.
           IF WSS-NUM-MFIN = 13
              MOVE 01 TO WSS-NUM-MFIN IN WSS-VARI
              ADD 1 TO WSS-NUM-AFIN IN WSS-VARI.
           PERFORM BUS-ULT-HAB.
           IF MSG-COD-MENS > SPACES 
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO ' 
               DISPLAY ' ERROR EN RUTINA ULTIMO DIA DEL MES: '
                         MSG-COD-MENS ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT.

      *Se genera fecha ultimo dia del mes en en wss-fec-fter
           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.

      *Calcular el valor de cambio de la U.F., US$, US$REAJ a F. Termino
      *Obtiene Codigo Valor de Cambio de Pesos Mediante CIC de la Moneda '0999'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-0999   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-0999 IN WSS-VARI.
           DISPLAY ' COD-PESO   : ' WSS-COD-0999 IN WSS-VARI.

      * Obtiene Codigo Valor de Cambio de UF Mediante CIC de la Moneda '0998'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-0998   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0998  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-0998 IN WSS-VARI.
           DISPLAY ' COD-UEFE   : ' WSS-COD-0998 IN WSS-VARI.

      * Obtiene Codigo Valor de Cambio de US$REAJ Mediante CIC de Moneda '0013'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-0013   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0013  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-0013 IN WSS-VARI.
           DISPLAY ' COD-US$REAJ: ' WSS-COD-0013 IN WSS-VARI.

      * Obtiene Codigo Valor de Cambio de US$ Mediante CIC de Moneda '1013'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE PDB-CIC-1013   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC1013  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' MSG-GLS-DESC ' : '
                                            TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-1013 IN WSS-VARI.
           MOVE PDB-COD-TVAL-DOLO   TO WSS-COD-1013-TVAL IN WSS-VARI.
           DISPLAY ' COD-US$    : ' WSS-COD-1013 IN WSS-VARI.

      * Obtiene Valor de Cambio de la UF a Fecha ultimo dia habil mes
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-0998 IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-0999 IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA LA UF. '
                        ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' 
               DISPLAY 'INDICADOR DE VIGENCIA NO VALIDO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA LA UF.'
               PERFORM GNS-PRO-ABT.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-0998 IN WSS-VARI.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-EDIT.
           DISPLAY 'V. CAMBIO UF      : ' WSS-VAL-EDIT.

      * Obtiene Valor de Cambio de la US$ a Fecha Ultimo dia Habil Mes
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-1013 IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-0999 IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' 
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA US$. '
                        ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT. 
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'INDICADOR DE VIGENCIA NO VALIDO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA US$.'
               PERFORM GNS-PRO-ABT.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-1013 IN WSS-VARI.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-EDIT.
           DISPLAY 'V. CAMBIO US$     : ' WSS-VAL-EDIT.

      * Obtiene Valor de Cambio de US$REAJ a Fecha Ultimo dia Habil Mes
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FTER IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-0013 IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-0999 IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA US$REAJ. '
                        ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO ' 
               DISPLAY 'INDICADOR DE VIGENCIA NO VALIDO A ESTA FECHA : '
                        WSS-FEC-FTER IN WSS-VARI ' PARA US$REAJ.'
               PERFORM GNS-PRO-ABT.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-0013 IN WSS-VARI.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-EDIT.
           DISPLAY 'V. CAMBIO US$REAJ : ' WSS-VAL-EDIT.

           DISPLAY ' '.
           DISPLAY ' FIN CAPTURA PARAMETROS '.
           DISPLAY ' '.
           SORT GNSSRT
              ON ASCENDING KEY SRT-COD-PROD IN SRT
              ON ASCENDING KEY SRT-COD-REGI IN SRT
              ON ASCENDING KEY SRT-COD-CVAL IN SRT
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
            IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-DVM.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM DAP-FIO-P07.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-CID.
       FST-INP-SRT.
           PERFORM CLR-TBL-SUP VARYING WSS-J FROM 1 BY 1 UNTIL
                                                    WSS-J > 10.
           PERFORM GEN-TBL-SUP1.
           IF WSS-IND-ERR1 NOT = 0
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR AL GENERAR TABLA SUPER-CHN '
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO FIN-INP-SRT.
           MOVE WSS-J IN WSS-VARI TO WSS-FIN1 IN WSS-VARI.
           DISPLAY 'LIMITE PARA $     '  WSS-FIN1 IN WSS-VARI.

           PERFORM GEN-TBL-SUP2.
           IF WSS-IND-ERR1 NOT = 0
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR AL GENERAR TABLA SUPER-CHR '
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO FIN-INP-SRT.
           MOVE WSS-J IN WSS-VARI TO WSS-FIN2 IN WSS-VARI.
           DISPLAY 'LIMITE PARA UF    '  WSS-FIN2 IN WSS-VARI.

           PERFORM GEN-TBL-SUP3.
           IF WSS-IND-ERR1 NOT = 0
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR AL GENERAR TABLA SUPER-EXT '
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO FIN-INP-SRT.
           MOVE WSS-J IN WSS-VARI TO WSS-FIN3 IN WSS-VARI.
           DISPLAY 'LIMITE PARA US$   '  WSS-FIN3 IN WSS-VARI.
      *
      * RECORRE CID POR LLAVE PRIMARIA
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM DAP-FIO-CID.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN REGISTROS CID '
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.

       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.

           ADD 1 TO WSS-PRO-CID-LEI IN WSS-VARI.

           MOVE CID-CIC-IOPD IN CID TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: NO EXISTE OPD ASOCIADA A CID '
                 OPD-CIC-IOPD IN OPD
               ADD 1 TO WSS-PRO-OPD-BAD
                 GO TO NXT-PRO-CID.

           ADD 1 TO WSS-PRO-OPD-LEI IN WSS-VARI.
      *     DISPLAY 'CID ' CID-CAI-IOPD IN CID '-'
      *                    CID-IIC-IOPD IN CID.

           IF OPD-COD-PLZO IN OPD = 'D1' OR 'D2'
              MOVE 3020 TO SRT-COD-PROD IN SRT
           ELSE
           IF OPD-COD-PLZO IN OPD = 'D3'
              MOVE 3025 TO SRT-COD-PROD IN SRT
           ELSE
           IF OPD-COD-PLZO IN OPD = 'D4'
              MOVE 3065 TO SRT-COD-PROD IN SRT.

           MOVE OPD-COD-OFIC IN OPD TO SRT-COD-OFIC IN SRT
                                       OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           IF MSG-COD-MENS  = 'COD    NEX' 
              MOVE 00 TO SRT-COD-COMU IN SRT
              MOVE 00 TO SRT-COD-REGI IN SRT
           ELSE
              MOVE OFI-COD-COMU IN OFI TO WSS-GLS-COMU IN WSS-VARI
              MOVE WSS-NUM-COMU IN WSS-VARI TO SRT-COD-COMU IN SRT
              MOVE 'COM'               TO COM-COD-TTAB IN COM
              MOVE OFI-COD-COMU IN OFI TO COM-COD-COMU IN COM
              MOVE FIO-GET-KEY TO FIO-CMND
              PERFORM TAB-FIO-COM
              IF NOT FIO-STAT-OKS
                 MOVE 00 TO SRT-COD-REGI IN SRT
              ELSE
                 MOVE COM-COD-REGI IN COM TO SRT-COD-REGI IN SRT.  

           MOVE CID-VAL-SCAP IN CID TO SRT-VAL-MONT IN SRT.
           IF OPD-COD-VCAM IN OPD = WSS-COD-0999
              MOVE 'CHN' TO SRT-COD-CVAL IN SRT
           ELSE
           IF OPD-COD-VCAM IN OPD = WSS-COD-0998
              MOVE 'CHR' TO SRT-COD-CVAL IN SRT
      *        MULTIPLY OPD-VAL-SCAP IN OPD BY 
      *                    WSS-SGV-0998 IN WSS-VARI GIVING 
      *                    SRT-VAL-MONT IN SRT
           ELSE
           IF OPD-COD-VCAM IN OPD = WSS-COD-0013 
              MOVE 'CHR' TO SRT-COD-CVAL IN SRT
      *        MULTIPLY OPD-VAL-SCAP IN OPD BY 
      *                    WSS-SGV-0013 IN WSS-VARI GIVING 
      *                    SRT-VAL-MONT IN SRT
           ELSE
           IF OPD-COD-VCAM IN OPD = WSS-COD-1013 OR '1013'
              MOVE 'EXT' TO SRT-COD-CVAL IN SRT
              MULTIPLY CID-VAL-SCAP IN CID BY 
                          WSS-SGV-1013 IN WSS-VARI GIVING 
                          SRT-VAL-MONT IN SRT
           ELSE
               DISPLAY ' ERROR EN CODIGO DE MONEDA: '
                         OPD-COD-VCAM IN OPD
                 GO TO NXT-PRO-CID.

      *     DISPLAY 'INTD : ' CID-VAL-INTD IN CID.
      *     DISPLAY 'MONT : ' SRT-VAL-MONT IN SRT.
           
           MOVE OPD-CIC-IOPD IN OPD TO SRT-CIC-IOPD IN SRT.

           MOVE 'N' TO SRT-COD-RENO IN SRT.
           IF OPD-COD-RENO IN OPD = 'S'
              IF OPD-FEC-FPCO IN OPD NOT = OPD-FEC-FREA IN OPD
                 MOVE 'S' TO SRT-COD-RENO IN SRT.
       MOV-INP-SRT.
           RELEASE SRT.
           ADD 1 TO WSS-GRA-SRT IN WSS-VARI.
      *     DISPLAY 'SRT ' SRT.  
       NXT-PRO-CID.
      *Ubica registro en OPD por fecha
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-CID.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.


       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
      *Limpia FIO-AKEY para acceso a otras FIO
           MOVE SPACES TO FIO-AKEY. 
           MOVE 1 TO WSS-STAT-P07 IN WSS-VARI.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.

           PERFORM CLR-TBL-P07 VARYING WSS-I FROM 1 BY 1
                                         UNTIL WSS-I > 5.
           MOVE SRT-COD-PROD IN SRT TO WSS-PROD-ANT
           MOVE SRT-COD-REGI IN SRT TO WSS-REGI-ANT.
           MOVE SRT-COD-CVAL IN SRT TO WSS-CVAL-ANT.
           PERFORM PUT-PRI-P07.

       LUP-OUT-SRT.
      *     DISPLAY ' LEE SRT ' SRT.
           MOVE SRT-COD-PROD IN SRT TO WSS-PROD-P07.
           MOVE SRT-COD-REGI IN SRT TO WSS-REGI-P07.
           MOVE SRT-COD-CVAL IN SRT TO WSS-CVAL-P07.
           MOVE 0 TO WSS-STAT-P07 IN WSS-VARI.
           IF WSS-KEY-P07 NOT = WSS-KEY-ANT             
              MOVE 1 TO WSS-STAT-P07 IN WSS-VARI
              PERFORM PRO-P07
              PERFORM CLR-TBL-P07 VARYING WSS-I FROM 1 BY 1
                                           UNTIL WSS-I > 10
              MOVE ZEROES TO WSS-NUM-TDOC IN WSS-VARI
                             WSS-NUM-TREN IN WSS-VARI
                             WSS-VAL-TMON IN WSS-VARI
                             WSS-VAL-TREN IN WSS-VARI
              MOVE SRT-COD-PROD IN SRT TO WSS-PROD-ANT
              MOVE SRT-COD-REGI IN SRT TO WSS-REGI-ANT
              MOVE SRT-COD-CVAL IN SRT TO WSS-CVAL-ANT.
           
           MOVE 1 TO WSS-J.
           IF SRT-COD-CVAL IN SRT = 'CHN'
              PERFORM UBI-TRAMO1 VARYING WSS-J FROM 1 BY 1 
                                         UNTIL WSS-J > WSS-FIN1
           ELSE
           IF SRT-COD-CVAL IN SRT = 'CHR'
              PERFORM UBI-TRAMO2 VARYING WSS-J FROM 1 BY 1 
                                         UNTIL WSS-J > WSS-FIN2
           ELSE
           IF SRT-COD-CVAL IN SRT = 'EXT'
              PERFORM UBI-TRAMO3 VARYING WSS-J FROM 1 BY 1 
                                         UNTIL WSS-J > WSS-FIN3
           ELSE
              DISPLAY 'ERROR AL UBICAR-TRAMO-MONEDA: ' 
                       ' LLAVE : ' WSS-KEY-P07  
                       ' MONEDA: ' SRT-COD-CVAL IN SRT
              GO TO NXT-OUT-SRT.

           ADD 1                   TO WSS-DOC-P07 IN WSS-VARI(WSS-I)
                                      WSS-NUM-TDOC IN WSS-VARI.
           ADD SRT-VAL-MONT IN SRT TO WSS-MON-P07 IN WSS-VARI(WSS-I)
                                      WSS-VAL-TMON IN WSS-VARI.

           IF SRT-COD-RENO IN SRT = 'S'
              ADD 1                   TO WSS-NUM-TREN IN WSS-VARI
              ADD SRT-VAL-MONT IN SRT TO WSS-VAL-TREN IN WSS-VARI.

           GO TO NXT-OUT-SRT.
       GEN-OUT-SRT.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.

           IF WSS-STAT-P07 IN WSS-VARI = 0
              PERFORM PRO-P07.

           DISPLAY '     '.
           DISPLAY 'TOTALES DE CONTROL DAPPGP07 '.
           DISPLAY '--------------------------- '.
           DISPLAY '     '.
           DISPLAY 'TOTAL REGISTROS LEIDOS CID     : ' WSS-PRO-CID-LEI.
           DISPLAY 'TOTAL REGISTROS LEIDOS OPD     : ' WSS-PRO-OPD-LEI.
           DISPLAY 'TOTAL REGISTROS LEIDOS OPD ERR : ' WSS-PRO-OPD-BAD.
           DISPLAY 'TOTAL REGISTROS GRABADOS SRT   : ' WSS-GRA-SRT.
           DISPLAY 'TOTAL REGISTROS GRABADOS P07   : ' WSS-GRA-P07.
           DISPLAY '     '.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-P07.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-DVM.
       EXT-OUT-SRT.
           EXIT.

       COPY DAPBFOPD.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.

       CLR-TBL-SUP SECTION.
       INI-CLR-TBL-SUP.
           MOVE ZEROES TO WSS-SGV-VAL1 IN WSS-VARI(WSS-J).
           MOVE ZEROES TO WSS-SGV-VAL2 IN WSS-VARI(WSS-J).
           MOVE ZEROES TO WSS-SGV-VAL3 IN WSS-VARI(WSS-J).
       FIN-CLR-TBL-SUP.
           EXIT.

       GEN-TBL-SUP1 SECTION.
       INI-GEN-TBL-SUP1.
           MOVE 0 TO WSS-IND-ERR1 IN WSS-VARI.
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'SUP' TO TAB-COD-TTAB IN TAB.
           MOVE 'CHN01' TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS IN MSG = 'COD    NEX'
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR EN ACCESO A TAB-SUP-CHN DAP ' TAB-COD-CTAB
              MOVE 1 TO WSS-IND-ERR1 IN WSS-VARI
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO FIN-GEN-TBL-SUP1.
           MOVE ZEROES TO WSS-J    IN WSS-VARI.
           MOVE ZEROES TO WSS-FIN1 IN WSS-VARI.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI. 

       LUP-SUP1.    
           IF NOT FIO-STAT-OKS 
              GO TO FIN-GEN-TBL-SUP1.
           ADD 1 TO WSS-J IN WSS-VARI
                    WSS-FIN1 IN WSS-VARI.
           MOVE TAB-GLS-DAT1 IN TAB TO 
                                   WSS-GLS-VALI IN WSS-VARI.

           MOVE WSS-SGV-VALI IN WSS-VARI TO 
                                   WSS-SGV-VAL1 IN WSS-VARI(WSS-J).

           MULTIPLY WSS-SGV-VAL1 IN WSS-VARI(WSS-J) BY
                    WSS-SGV-0998
                    GIVING WSS-SGV-VAL1 IN WSS-VARI(WSS-J).

           MOVE WSS-SGV-VAL1 IN WSS-VARI(WSS-J) TO
                WSS-VAL-EDIT.
           DISPLAY 'TRAMO CHN ' WSS-J ' ' WSS-VAL-EDIT.
      *     DISPLAY 'TSUP-CHN ' WSS-J ' ' 
      *                         WSS-TBL-VSU1 IN WSS-VARI(WSS-J).
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI.
           IF NOT ( TAB-COD-TTAB IN TAB = 'SUP' AND
                    WSS-IND-CTAB IN WSS-VARI = 'CHN' )
              MOVE '23' TO FIO-STAT.
           GO TO LUP-SUP1.
       FIN-GEN-TBL-SUP1.
           EXIT.

       GEN-TBL-SUP2 SECTION.
       INI-GEN-TBL-SUP2.
           MOVE 0 TO WSS-IND-ERR1 IN WSS-VARI.
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'SUP' TO TAB-COD-TTAB IN TAB.
           MOVE 'CHR01' TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS IN MSG = 'COD    NEX'
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR EN ACCESO A TAB-SUP-CHR DAP ' TAB-COD-CTAB
              MOVE 1 TO WSS-IND-ERR1 IN WSS-VARI
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO FIN-GEN-TBL-SUP2.
           MOVE ZEROS TO WSS-J    IN WSS-VARI.
           MOVE ZEROS TO WSS-FIN2 IN WSS-VARI.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI. 

       LUP-SUP2.    
           IF NOT FIO-STAT-OKS 
              GO TO FIN-GEN-TBL-SUP2.
           ADD 1 TO WSS-J IN WSS-VARI
                    WSS-FIN2 IN WSS-VARI.
           MOVE TAB-GLS-DAT1 IN TAB TO 
                                   WSS-GLS-VALI IN WSS-VARI.
           MOVE WSS-SGV-VALI IN WSS-VARI TO 
                                   WSS-SGV-VAL2 IN WSS-VARI(WSS-J).
           MULTIPLY WSS-SGV-VAL2 IN WSS-VARI(WSS-J) BY
                    WSS-SGV-0998
                    GIVING WSS-SGV-VAL2 IN WSS-VARI(WSS-J).

           MOVE WSS-SGV-VAL2 IN WSS-VARI(WSS-J) TO
                WSS-VAL-EDIT.
           DISPLAY 'TRAMO CHR ' WSS-J ' ' WSS-VAL-EDIT.
      *     DISPLAY 'TSUP-CHR ' WSS-J ' ' 
      *                         WSS-TBL-VSU2 IN WSS-VARI(WSS-J).
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI. 
           IF NOT ( TAB-COD-TTAB IN TAB = 'SUP' AND 
                    WSS-IND-CTAB IN WSS-VARI = 'CHR' )
              MOVE '23' TO FIO-STAT.
           GO TO LUP-SUP2.
       FIN-GEN-TBL-SUP2.
           EXIT.

       GEN-TBL-SUP3 SECTION.
       INI-GEN-TBL-SUP3.
           MOVE 0 TO WSS-IND-ERR1 IN WSS-VARI.
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'SUP' TO TAB-COD-TTAB IN TAB.
           MOVE 'EXT01' TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS IN MSG = 'COD    NEX'
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR EN ACCESO A TAB-SUP-EXT DAP ' TAB-COD-CTAB
              MOVE 1 TO WSS-IND-ERR1 IN WSS-VARI
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO FIN-GEN-TBL-SUP3.
           MOVE ZEROES TO WSS-J    IN WSS-VARI.
           MOVE ZEROES TO WSS-FIN3 IN WSS-VARI.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI. 

       LUP-SUP3.    
           IF NOT FIO-STAT-OKS 
              GO TO FIN-GEN-TBL-SUP3.
           ADD 1 TO WSS-J IN WSS-VARI
                    WSS-FIN3 IN WSS-VARI.
           MOVE TAB-GLS-DAT1 IN TAB TO 
                                   WSS-GLS-VALI IN WSS-VARI.
           MOVE WSS-SGV-VALI IN WSS-VARI TO 
                                   WSS-SGV-VAL3 IN WSS-VARI(WSS-J).
           MULTIPLY WSS-SGV-VAL3 IN WSS-VARI(WSS-J) BY
                    WSS-SGV-1013
                    GIVING WSS-SGV-VAL3 IN WSS-VARI(WSS-J).

           MOVE WSS-SGV-VAL3 IN WSS-VARI(WSS-J) TO
                WSS-VAL-EDIT.
           DISPLAY 'TRAMO EXT ' WSS-J ' ' WSS-VAL-EDIT.
      *     DISPLAY 'TSUP-EXT ' WSS-J ' ' 
      *                         WSS-TBL-VSU3 IN WSS-VARI(WSS-J).
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-VARI. 
           IF NOT ( TAB-COD-TTAB IN TAB = 'SUP' AND 
                    WSS-IND-CTAB IN WSS-VARI = 'EXT' )
              MOVE '23' TO FIO-STAT.
           GO TO LUP-SUP3.
       FIN-GEN-TBL-SUP3.
           EXIT.

       UBI-TRAMO1 SECTION.
       INI-UBI-TRAMO1.
           IF SRT-VAL-MONT IN SRT NOT > 
                               WSS-SGV-VAL1 IN WSS-VARI(WSS-J)
              MOVE WSS-J TO WSS-I
              ADD 1 TO WSS-FIN1 GIVING WSS-J.
       FIN-UBI-TRAMO1.
           EXIT.

       UBI-TRAMO2 SECTION.
       INI-UBI-TRAMO2.
           IF SRT-VAL-MONT IN SRT NOT > 
                               WSS-SGV-VAL2 IN WSS-VARI(WSS-J)
              MOVE WSS-J TO WSS-I
              ADD 1 TO WSS-FIN2 GIVING WSS-J.
       FIN-UBI-TRAMO2.
           EXIT.

       UBI-TRAMO3 SECTION.
       INI-UBI-TRAMO3.
           IF SRT-VAL-MONT IN SRT NOT > 
                               WSS-SGV-VAL3 IN WSS-VARI(WSS-J)
              MOVE WSS-J TO WSS-I
              ADD 1 TO WSS-FIN3 GIVING WSS-J.
       FIN-UBI-TRAMO3.
           EXIT.

       CLR-TBL-P07 SECTION.
       INI-CLR-TBL-P07.
           MOVE ZEROES TO WSS-DOC-P07 IN WSS-VARI(WSS-I).
           MOVE ZEROES TO WSS-MON-P07 IN WSS-VARI(WSS-I).
       FIN-CLR-TBL-P07.
           EXIT.

       PUT-PRI-P07 SECTION.
       INI-PUT-PRI-P07.
           MOVE 016             TO WSS-COD-IEFE IN WSS-VARI.
           MOVE 'P07'           TO WSS-COD-IDEN IN WSS-VARI.
           MOVE WSS-NUM-MPRO    TO WSS-COD-PERM IN WSS-VARI.
           MOVE WSS-NUM-APRO    TO WSS-COD-PERA IN WSS-VARI.
           MOVE SPACES          TO WSS-GLS-REST IN WSS-VARI.
           MOVE WSS-GLS-P07 IN WSS-VARI TO P07.

      *     DISPLAY 'P071 ' P07.
           MOVE FIO-PUT TO FIO-CMND.
      *  Interfaz Documentos P06-P07 Super-Bancos
           PERFORM DAP-FIO-P07.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR FIO-PUT ENCABEZADO P07: '
                       P07 ' FS: '  FIO-STAT
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO EXT-OUT-SRT.
           ADD 1 TO WSS-GRA-P07.
       FIN-PUT-PRI-P07.
           EXIT.

       PRO-P07 SECTION.
       INI-PRO-PP07.
           MOVE WSS-PROD-ANT IN WSS-VARI   TO P07-COD-PROD IN P07.
           MOVE WSS-REGI-ANT IN WSS-VARI   TO P07-COD-REGI IN P07.
           MOVE WSS-CVAL-ANT IN WSS-VARI   TO P07-COD-CVAL IN P07.
           MOVE WSS-DOC-P07 IN WSS-VARI(1) TO P07-NUM-DOC1 IN P07.
           MOVE WSS-DOC-P07 IN WSS-VARI(2) TO P07-NUM-DOC2 IN P07.
           MOVE WSS-DOC-P07 IN WSS-VARI(3) TO P07-NUM-DOC3 IN P07.
           MOVE WSS-DOC-P07 IN WSS-VARI(4) TO P07-NUM-DOC4 IN P07.
           MOVE WSS-DOC-P07 IN WSS-VARI(5) TO P07-NUM-DOC5 IN P07.

           DIVIDE WSS-MON-P07 IN WSS-VARI(1) BY 1000
                  GIVING WSS-MON-P07 IN WSS-VARI(1).
           MOVE WSS-MON-P07 IN WSS-VARI(1) TO P07-VAL-MON1 IN P07.

           DIVIDE WSS-MON-P07 IN WSS-VARI(2) BY 1000
                  GIVING WSS-MON-P07 IN WSS-VARI(2).
           MOVE WSS-MON-P07 IN WSS-VARI(2) TO P07-VAL-MON2 IN P07.

           DIVIDE WSS-MON-P07 IN WSS-VARI(3) BY 1000
                  GIVING WSS-MON-P07 IN WSS-VARI(3).
           MOVE WSS-MON-P07 IN WSS-VARI(3) TO P07-VAL-MON3 IN P07.

           DIVIDE WSS-MON-P07 IN WSS-VARI(4) BY 1000
                  GIVING WSS-MON-P07 IN WSS-VARI(4).
           MOVE WSS-MON-P07 IN WSS-VARI(4) TO P07-VAL-MON4 IN P07.

           DIVIDE WSS-MON-P07 IN WSS-VARI(5) BY 1000
                  GIVING WSS-MON-P07 IN WSS-VARI(5).
           MOVE WSS-MON-P07 IN WSS-VARI(5) TO P07-VAL-MON5 IN P07.

           MOVE ZEROES TO WSS-NUM-PREN IN WSS-VARI
                          WSS-VAL-PREN IN WSS-VARI.
      *  Busca Registro en Archivo DVM
           MOVE WSS-PROD-ANT IN WSS-VARI   TO DVM-COD-PROD IN DVM.
           MOVE WSS-REGI-ANT IN WSS-VARI   TO DVM-COD-REGI IN DVM.
           MOVE WSS-CVAL-ANT IN WSS-VARI   TO DVM-COD-CVAL IN DVM.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-DVM.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR: NO EXISTE REG. DVM PARA P07 '
                              FIO-STAT ' ' DVM-KEY-IDVM IN DVM
              MOVE ZEROES TO P07-NUM-PREN IN P07
                             P07-VAL-PREN IN P07
           ELSE
              COMPUTE WSS-NUM-PREN IN WSS-VARI = 
           ( (WSS-NUM-TREN IN WSS-VARI / DVM-NUM-DVME IN DVM) * 100 )
              MOVE WSS-NUM-PREN IN WSS-VARI TO P07-NUM-PREN IN P07
            
              COMPUTE WSS-VAL-PREN IN WSS-VARI = 
           ( (WSS-VAL-TREN IN WSS-VARI / DVM-VAL-MONT IN DVM) * 100 )
              MOVE WSS-VAL-PREN IN WSS-VARI TO P07-VAL-PREN IN P07.

      *     PERFORM EXHIBIT-P07.
           MOVE FIO-PUT TO FIO-CMND.
      *  Interfaz Documentos P06-P07 Super-Bancos
           PERFORM DAP-FIO-P07.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY 'ERROR FIO-PUT P07: '
                       P07 ' FS: '  FIO-STAT
              MOVE PGM-SMAL TO PGM-STAT-SRT
              GO TO EXT-OUT-SRT.
           ADD 1 TO WSS-GRA-P07.
       FIN-PUT-P07.
           EXIT.

       EXHIBIT-P07 SECTION.
       INI-EXHIBIT-P07.
           DISPLAY '     '.
           EXHIBIT NAMED P07-COD-PROD IN P07.
           EXHIBIT NAMED P07-COD-REGI IN P07.
           EXHIBIT NAMED P07-COD-CVAL IN P07.
           EXHIBIT NAMED P07-NUM-DOC1 IN P07.
           EXHIBIT NAMED P07-NUM-DOC2 IN P07.
           EXHIBIT NAMED P07-NUM-DOC3 IN P07.
           EXHIBIT NAMED P07-NUM-DOC4 IN P07.
           EXHIBIT NAMED P07-NUM-DOC5 IN P07.
           EXHIBIT NAMED P07-NUM-PREN IN P07.
           EXHIBIT NAMED P07-VAL-MON1 IN P07.
           EXHIBIT NAMED P07-VAL-MON2 IN P07.
           EXHIBIT NAMED P07-VAL-MON3 IN P07.
           EXHIBIT NAMED P07-VAL-MON4 IN P07.
           EXHIBIT NAMED P07-VAL-MON5 IN P07.
           EXHIBIT NAMED P07-VAL-PREN IN P07.
           DISPLAY '  '.
           DISPLAY 'WSS-NUM-TREN/WSS-VARI = ' WSS-NUM-TREN IN WSS-VARI.
           DISPLAY 'WSS-VAL-TREN/WSS-VARI = ' WSS-VAL-TREN IN WSS-VARI.
       FIN-EXHIBIT-P07.
           EXIT.

       BUS-ULT-HAB SECTION.
       INI-BUS-ULT-HAB.
      * BUS-ULTIMO-DIA-HABIL
           MOVE WSS-FEC-FFIN TO FEC-FECH.
       LUP-BUS-ULT-HAB.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM CAL-FEC.
      *VAL-FEC Validar Fecha
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
      *     IF NOT FEC-STAT-HBL
      *        GO TO LUP-BUS-ULT-HAB.

       NOT-BUS-ULT-HAB.
           DISPLAY '     '.
           MOVE FEC-FECH TO WSS-FEC-FFIN.
           DISPLAY 'FECHA DE INICIO   : ' WSS-FEC-FINI.
           DISPLAY 'FECHA ULT. DIA MES: ' WSS-FEC-FFIN.
           DISPLAY '     '.
       FIN-BUS-ULT-HAB.
           EXIT.

      *Entidades  de GNS
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBFMSC.
       COPY GNSBVMSC.
       COPY GNSBBMSC.

      *Entidades  de TAB
       COPY TABBFOFI.
       COPY TABBFCAM.
       COPY TABBFCOM.
       COPY TABBBOFI.
       COPY TABBVOFI.

      *Entidades  de DAP
       COPY DAPBFP07.
       COPY DAPBFDVM.
       COPY DAPBFCID.
-END
/*
//STEP1  EXEC COBUCL,PARM.COB='APOST,LIB,BUF=15000,FLOW=20,STATE'
//COB.SYSPRINT DD SYSOUT=*
//COB.SYSLIB   DD DSN=ADRSF.DSG27.SOURCE.DESA,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES.V31,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES.V30,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FIO.BATCH,DISP=SHR,SUBSYS=LAM
//             DD DSN=SBTCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//COB.SYSIN    DD DSN=DAPCL.FUENTES(DAPPGP07),
//             DISP=SHR,SUBSYS=(LAM,'PSWD=CONS')
//LKED.SYSLIN  DD DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD DDNAME=SYSIN
//LKED.SYSLMOD DD DSN=DAPCM.LINEA(DAPPGP07),
//             DISP=SHR
//LKED.SYSLIB  DD DSN=CICS.V211.LOADLIB,DISP=SHR
//             DD DSN=GNSCM.RUTINAS.CONSIST,DISP=SHR
//             DD DSN=DAPCM.LINEA,DISP=SHR
//             DD DSN=COB.VSCLLIB,DISP=SHR
//LKED.OBJLIB  DD DSN=TABST.OBJECT,DISP=SHR
//             DD DSN=DAPCT.OBJECT,DISP=SHR
//             DD DSN=ADRST.DSG27.OBJECT.DESA,DISP=SHR
//LKED.SYSIN   DD *
 INCLUDE OBJLIB(DAPCGP07)
 ENTRY BEGIN
 NAME DAPPGP07(R)
/*
//
