       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. DAPPPQDP.
       AUTHOR. ADA.
       DATE-WRITTEN. 25-OCT-2002.
      *
      *RECIBE MENSAJE REMOTO Y PROCESA CONSULTAS PARA IMP. DE DAP
      *
       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.
      
       DATA DIVISION.
      *==============
      
       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-IMP-REQA==.
       COPY GNSWGHOY.
       COPY GNSWGFEC.
       COPY GNSWGSYS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFRM.
       COPY GNSWVSCR.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGCPT.
       COPY GNSWGPES.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY TABWGTVC.
       COPY TABBRCAM.
       COPY SGCBRDBC.
       COPY DAPBRRCC.
       COPY DAPBROPD.
       COPY DAPBRVEN.
       COPY DAPBRCAD.
       COPY DAPBRIMP.
       COPY DAPWGQDP.
       01  WSS-VARI.
            03 PGM-STAT-IMP           VALUE '23'      PIC X(02).
                  88 PGM-STAT-IMP-OKS    VALUE '00'.
                  88 PGM-STAT-IMP-NUL    VALUE '  '.
      
           03 WSS-LEN-RCV    VALUE +0        COMP    PIC S9(04).
           03 WSS-LEN-SND    VALUE +0        COMP    PIC S9(04).
           03 WSS-GCOD.
              05 WSS-GTIO                            PIC X(40).
              05 WSS-GAUX                            PIC X(40).
      
       01  SND-SEND VALUE SPACES.
           03 SND-HEAD-SEND.
      *
      *       Status Respuesta 0=OK ; 1=ERR
              05 SND-CAN-STAT                         PIC X(0001).
      *
      *       Fecha Transaccion
              05 SND-FEC-FTRN                         PIC X(0008).
      *
      *       Hora Transaccion
              05 SND-HRS-HTRN                         PIC X(0006).
      *
      *       Indicador Mensajes Pendientes 0=NO ; 1=SI
              05 SND-IND-MSGP                         PIC X(0001).
      *
      *       Largo de la parte Variable del Mensaje
              05 SND-NUM-LENM                         PIC 9(0005).
      *
      *       Codigo Transaccion
              05 SND-COD-TRAN                         PIC X(0008).
      *
      *       Codigo Sistema
              05 SND-COD-SIST                         PIC X(0003).
      *
      *       Codigo Usuario
              05 SND-COD-USER                         PIC X(0012).
      *
      *       Password
              05 SND-COD-PASS                         PIC X(0008).
      *
      *    Salida del usuario
           03 SND-GLS-DOUT.
      *
      *       Filler Vacio y Fijo de 111 Caracteres
              05 SND-GLS-FILL                         PIC X(0111).
      *
      *       DATA a ser transmitida de OUTPUT
              05 SND-GLS-OUTP                         PIC X(1089).
      *
      *
       01  RCV-RECEIVE VALUE SPACES.
      *
      *    Header utilizado por el Driver de Comunicacion
           03 RCV-HEAD-DRIVE.
               05 RCV-HEAD-DRIVE-TRAN                 PIC X(008).
               05 RCV-HEAD-DRIVE-SIST                 PIC X(003).
               05 RCV-HEAD-DRIVE-USER                 PIC X(012).
               05 RCV-HEAD-DRIVE-PSSW                 PIC X(008).
      
      *    DATA a ser transmitida de INPUT
           03 RCV-GLS-DINP                            PIC X(1024).
      
      * Fin variables area de comunicacion BCI
      
       01  TSIDENT.
           03 TERM                     PIC X(04).
      
      
       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                     PIC X(01).
       COPY GNSLGFIO.
      
       PROCEDURE DIVISION.
      *===================
      
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
      
           MOVE 'DAPPPQDP'  TO IDD-PROG FIO-PROG.
           MOVE IDD-VARI    TO SYS-CMMA.
           MOVE +507        TO SYS-TCMA.
           MOVE 'GNSPPIDD'  TO SYS-PROG.
           MOVE SYS-LINK    TO SYS-CMND.
           PERFORM GNS-PRO-SYS.
           MOVE SYS-CMMA TO IDD-VARI.
           MOVE 'DAP' TO SCR-SIST.
           PERFORM PROCESA-MSG.
       FIN-MAIN.
           EXEC CICS RETURN END-EXEC.
       EXT-MAIN.
           EXIT.
      
       PROCESA-MSG SECTION.
       INI-PROCESA-MSG.
      *Inicializa variables
           MOVE SPACES TO QDP-MSJE.
           MOVE '0'    TO QDP-CRET.
      *
      *Recibe Mensaje desde Cliente
           MOVE QDP-LINP TO WSS-LEN-RCV.
           PERFORM RCV-MSG-INP-HST.
      *
      *Procesa el requerimiento solicitado
           MOVE RCV-GLS-DINP TO QDP-DINP.
           IF QDP-TCON = 'SIN'
               PERFORM PROCESA-SIN
           ELSE
               MOVE '1' TO QDP-CRET
               MOVE 'QDPTCONINEXI' TO QDP-CMSG.
      
           IF QDP-CMSG > SPACES
              IF QDP-SIST NOT > SPACES
                  MOVE 'DAP' TO QDP-SIST
              END-IF
              MOVE QDP-SIST TO MSG-COD-SIST
              MOVE QDP-CMSG TO MSG-COD-MENS
              PERFORM GET-MSG
              IF QDP-MEN2 > SPACES
                  MOVE MSG-GLS-DESC IN MSG TO QDP-MEN1
              ELSE
                  MOVE MSG-GLS-DESC IN MSG TO QDP-MENS
              END-IF
              PERFORM ENV-MSG-ERR.
       FIN-PROCESA-MSG.
           EXIT.
      
      *-------------------------------------------------------------
      
       RCV-MSG-INP-HST SECTION.
       INI-RCV-MSG-INP-HST.
           MOVE SPACES TO RCV-RECEIVE.
      *Se agregan los 31 bytes del Header
           ADD 31 TO WSS-LEN-RCV.
           EXEC CICS RECEIVE
                     INTO(RCV-RECEIVE)
                     LENGTH(WSS-LEN-RCV)
                     CONVID(EIBTRMID)
                     END-EXEC.
           MOVE EIBTRMID    TO TERM.
           IF EIBCONF = HIGH-VALUES
               EXEC CICS ISSUE CONFIRMATION
                         CONVID(TERM)
               END-EXEC.
       FIN-RCV-MSG-INP-HST.
           EXIT.
      
      *-------------------------------------------------------------
      
       PROCESA-SIN SECTION.
       INI-PROCESA-SIN.
           MOVE QDP-IOPD TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               MOVE 'OPD    NEX' TO QDP-CMSG
               GO TO FIN-PROCESA-SIN.
           MOVE OPD-CIC-IOPD IN OPD TO IMP-CIC-IOPD IN IMP.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-IMP.
           IF NOT FIO-STAT-OKS
               MOVE 'IMP    NEX' TO QDP-CMSG
               GO TO FIN-PROCESA-SIN.
      
           MOVE FIO-STAT TO PGM-STAT-IMP.
      
           IF OPD-COD-VCAM IN OPD = '0999' AND
              OPD-COD-TOPD IN OPD = 'DPF'  AND
               ( OPD-COD-AOPD IN OPD = 'NRN' OR
                 OPD-COD-AOPD IN OPD = 'NRS' OR
                 OPD-COD-AOPD IN OPD = 'ERN'
               )                           AND
                    PGM-STAT-IMP-OKS
                  MOVE IMP-NUM-DURV IN IMP TO OPD-NUM-PVEN IN OPD
                  MOVE IMP-IND-DURV IN IMP TO OPD-IND-PVEN IN OPD
                  MOVE IMP-FEC-FTER IN IMP TO OPD-FEC-FPVC IN OPD.
      
      *Buscar cliente de DAP
           MOVE OPD-CIC-IOPD IN OPD TO RCC-CIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF NOT FIO-STAT-OKS
               MOVE 'QDPRRCCNEX' TO QDP-CMSG
               GO TO FIN-PROCESA-SIN.
      *Leer registro DBC
           MOVE RCC-CIC-ICLI IN RCC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
              MOVE 'QDPRDBCNEX' TO QDP-CMSG
              GO TO FIN-PROCESA-SIN.
      *Leer entidad VEN
           MOVE OPD-CIC-IOPD IN OPD TO VEN-CIC-IOPD IN VEN.
           MOVE 0                   TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NLS         TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    OPD-CIC-IOPD IN OPD = VEN-CIC-IOPD IN VEN )
               MOVE 'QDPRVENNEX' TO QDP-CMSG
               GO TO FIN-PROCESA-SIN.
      *Leer entidad CAD
           MOVE OPD-CIC-IOPD IN OPD TO CAD-CIC-IOPD IN CAD.
           MOVE '000'               TO CAD-NUM-ILIQ IN CAD.
           MOVE '1'                 TO CAD-NUM-ICAD IN CAD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-CAD.
           IF NOT FIO-STAT-OKS
               MOVE 'QDPRCADNEX' TO QDP-CMSG
               GO TO FIN-PROCESA-SIN.
           PERFORM LLENAR-SALIDA-SIN.
           IF QDP-CMSG > SPACES
               GO TO FIN-PROCESA-SIN.
           PERFORM ENVIA-SALIDA-SIN.
       FIN-PROCESA-SIN.
           EXIT.
      
       LLENAR-SALIDA-SIN SECTION.
       INI-LLENAR-SALIDA-SIN.
      *Llenar datos de OPD en la salida
           MOVE OPD-CIC-IOPD IN OPD TO QDP-OPDO.
           MOVE OPD-COD-VCAM IN OPD TO QDP-VCAM.
           MOVE OPD-VAL-CAPT IN OPD TO QDP-CAPT.
           MOVE OPD-COD-COPD IN OPD TO QDP-COPD.
           MOVE OPD-NUM-PVEN IN OPD TO QDP-PVEN.
           MOVE OPD-IND-PVEN IN OPD TO QDP-IPVE.
           MOVE OPD-FEC-FREA IN OPD TO QDP-FREA.
           MOVE OPD-FEC-FPVC IN OPD TO QDP-FVEN.
           MOVE OPD-VAL-SINT IN OPD TO QDP-TPER.
           MOVE OPD-SGV-TINT IN OPD TO QDP-TINT.
           MOVE OPD-COD-CUST IN OPD TO QDP-CUST.
           MOVE OPD-COD-RENO IN OPD TO QDP-RENO.
      *Llenar datos de DBC en la salida
           MOVE DBC-IDC-ICLI IN DBC TO QDP-IDCO.
           MOVE DBC-VRF-ICLI IN DBC TO QDP-VRFO.
           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN            TO QDP-NOMC.
      *Llenar datos de VEN en la salida
           IF OPD-COD-VCAM IN OPD = '0999' AND
              OPD-COD-TOPD IN OPD = 'DPF'  AND
               ( OPD-COD-AOPD IN OPD = 'NRN' OR
                 OPD-COD-AOPD IN OPD = 'NRS' OR
                 OPD-COD-AOPD IN OPD = 'ERN'
               )                           AND
                    PGM-STAT-IMP-OKS
                  MOVE IMP-SGV-INTP IN IMP TO QDP-VINT
                  MOVE IMP-VAL-VFIN IN IMP TO QDP-VFIN
           ELSE
                  SUBTRACT VEN-VAL-CAPI IN VEN FROM VEN-VAL-FINA IN VEN
                                      GIVING QDP-VINT
                  MOVE VEN-VAL-FINA IN VEN TO QDP-VFIN.
      
      *Llenar datos de CAD en la salida
           MOVE CAD-IND-TCAD IN CAD TO QDP-TCAD.
           MOVE CAD-CIC-ICAD IN CAD TO QDP-CTAC.
      *Llenar glosas en QDP
           MOVE 'TAB'               TO MSC-COD-SIST FIO-SIST.
           MOVE 'MSC'               TO MSC-COD-TTAB IN MSC.
           MOVE 'TVAL'              TO MSC-COD-TMSC IN MSC.
           MOVE OPD-MSC-TVAL IN OPD TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE 'QDPRMSCNEX' TO QDP-CMSG
               MOVE OPD-MSC-TVAL IN OPD TO QDP-MEN2
               GO TO FIN-LLENAR-SALIDA-SIN.
      *Llenar tipo de moneda
           IF MSC-CIC-CMSC IN MSC = '0'
               IF OPD-COD-VCAM IN OPD = CAM-COD-MNAC
                   MOVE '0' TO QDP-IMON
               ELSE
                   MOVE '1' TO QDP-IMON
           ELSE
               MOVE '2' TO QDP-IMON.
      *Llenar Glosa abreviada y corta para la moneda
           MOVE 'TAB' TO TAB-COD-SIST
           MOVE 'VLR' TO TAB-COD-TTAB IN TAB
           MOVE OPD-COD-VCAM IN OPD TO TAB-COD-CTAB IN TAB
           PERFORM BUS-TAB
           MOVE TAB-GLS-ABRV IN TAB TO QDP-GAMO.
           MOVE TAB-GLS-DCOR IN TAB TO QDP-GCMO.
      *Llenar Glosa de la clase de Operacion (Tio+Aux)
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'TIO' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-TOPD IN OPD TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GTIO.
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'AUX' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-COPD IN OPD TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GAUX.
           MOVE WSS-GCOD TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO QDP-GCOD.
      *Llenar Glosa de la unidad de separacion
           MOVE 'TAB' TO MSC-COD-SIST.
           MOVE 'IPER' TO MSC-COD-TMSC IN MSC.
           MOVE OPD-IND-PVEN IN OPD TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           MOVE MSC-GLS-ABRV IN MSC TO QDP-GPVE.
      *Llenar Glosa del codigo de Custodia
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'CUS' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-CUST IN OPD TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO QDP-GCUS.
      *Llenar Glosa del tipo de abono
           MOVE 'TAB' TO MSC-COD-SIST.
           MOVE 'TCYA' TO MSC-COD-TMSC IN MSC.
           MOVE CAD-IND-TCAD IN CAD TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           MOVE MSC-GLS-DESC IN MSC TO QDP-GTCA.
      *LLenar el valor de la captacion en Pesos
      *Lo calcula solo para moneda extranjera, ya que para esta
      *moneda el campo OPD-VAL-IPES esta en la moneda de origen
           IF OPD-MSC-TVAL IN OPD = '1'
               MOVE OPD-VAL-CAPT IN OPD TO TVC-VALO
               MOVE OPD-COD-VCAM IN OPD TO TVC-MON1
               MOVE CAM-COD-MNAC        TO TVC-MON2
               MOVE OPD-NUM-DREA IN OPD TO TVC-DCAM
               MOVE OPD-NUM-MREA IN OPD TO TVC-MCAM
               MOVE OPD-NUM-SREA IN OPD TO TVC-SCAM
               MOVE OPD-NUM-AREA IN OPD TO TVC-ACAM
               PERFORM TAB-PRO-TVC
               IF TVC-CMSG > SPACES
      *Ignora el mensaje en caso que la moneda original sea
      *extranjera. En estos casos devuelve 0 en el valor
                   MOVE 0 TO QDP-CAPP
               ELSE
                   MOVE TVC-VALO TO QDP-CAPP
           ELSE
               MOVE OPD-VAL-IPES IN OPD TO QDP-CAPP.
       FIN-LLENAR-SALIDA-SIN.
           EXIT.
      
       LLENA-HEADER-SEND SECTION.
       INI-LLENA-HEADER-SEND.
           PERFORM GET-FHOY.
           MOVE HOY-FHOY            TO SND-FEC-FTRN.
           MOVE HOY-HHOY            TO SND-HRS-HTRN.
           MOVE RCV-HEAD-DRIVE-TRAN TO SND-COD-TRAN.
           MOVE RCV-HEAD-DRIVE-SIST TO SND-COD-SIST.
           MOVE RCV-HEAD-DRIVE-USER TO SND-COD-USER.
           MOVE RCV-HEAD-DRIVE-PSSW TO SND-COD-PASS.
       FIN-LLENA-HEADER-SEND.
           EXIT.
      
       ENVIA-SALIDA-SIN SECTION.
       INI-ENVIA-SALIDA-SIN.
           PERFORM LLENA-HEADER-SEND.
           MOVE '0'            TO SND-CAN-STAT.
           MOVE '0'            TO SND-IND-MSGP.
           ADD QDP-LOKS 31 GIVING SND-NUM-LENM.
           MOVE '0'      TO QDP-CRET
           MOVE QDP-DOUT TO SND-GLS-DOUT.
           ADD 21 SND-NUM-LENM GIVING WSS-LEN-SND.
           EXEC CICS SEND
                     LENGTH(WSS-LEN-SND)
                     FROM(SND-SEND)
                     CONVID(TERM)
           END-EXEC.
       FIN-ENVIA-SALIDA-SIN.
           EXIT.
      
       ENV-MSG-ERR SECTION.
       INI-ENV-MSG-ERR.
           PERFORM LLENA-HEADER-SEND.
           MOVE '0'            TO SND-CAN-STAT.
           MOVE '0'            TO SND-IND-MSGP.
           ADD QDP-LERR 31 GIVING SND-NUM-LENM.
           MOVE '1'      TO QDP-CRET
           MOVE QDP-DOUT TO SND-GLS-DOUT.
           ADD 21 SND-NUM-LENM GIVING WSS-LEN-SND.
           EXEC CICS SEND
                     LENGTH(WSS-LEN-SND)
                     FROM(SND-SEND)
                     CONVID(TERM)
           END-EXEC.
       FIN-ENV-MSG-ERR.
           EXIT.
      
       COPY GNSBGEND.
       COPY GNSBPFEC.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBHSYS.
       COPY GNSBGSYS.
       COPY GNSBGCPT.
       COPY GNSBGPES.
       COPY GNSBIABT.
       COPY GNSBGDTC.
       COPY GNSBBMSC.
       COPY GNSBFMSC.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY TABBGTVC.
       COPY TABBFCAM.
       COPY DAPBFOPD.
       COPY DAPBFRCC.
       COPY DAPBFVEN.
       COPY DAPBFCAD.
       COPY DAPBFIMP.
       COPY SGCBFDBC.
