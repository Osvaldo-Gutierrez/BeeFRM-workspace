       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPLSTA.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    07-Jun-96 05:08 PM.

      * Programa Listador de Reporte STA con Sort.


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTSTA         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTSTA         ASSIGN TO        UT-S-RPTSTA.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTSTA         ASSIGN TO        UT-S-RPTSTA.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY DAPRRSTA.
       FD  RPTSTA LABEL RECORDS OMITTED
           REPORT IS RPT-STA
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY DAPBROPD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY DAPRWSTA.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
      *Entidades de GNS
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
      *Entidades de TAB
       COPY TABBROFI.
       COPY TABBRVLR.
      *Entidades de DAP
       COPY DAPBRRCC.
       COPY DAPBRPTD.
      *Entidades de SGC
       COPY SGCBRDBC.

       01  WSS-VARI.
           03 WSS-VAL-MONTO                     PIC 9(11)V9(4) VALUE 0.
           03 WSS-CIC-IOPD                        PIC X(12).
           03 WSS-I              VALUE 0          PIC 9(04).
           03 WSS-NUM-JUL        VALUE 0          PIC 9(06).
           03 WSS-NDIA                            PIC 9(04).
           03 WSS-TRM-DIA                         PIC 9(04).
           03 WSS-TRM-MTO                         PIC 9(04).
           03 WSS-IND-TDIA                        PIC X(01).
           03 WSS-IND-TMTO                        PIC X(01).
           03 WSS-IDX-INT                         PIC 9(04).
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-DFIN                      PIC 9(02).
               05  WSS-NUM-MFIN                      PIC 9(02).
               05  WSS-NUM-SFIN                      PIC 9(02).
               05  WSS-NUM-AFIN                      PIC 9(02).
           03  WSS-FEC-FPRI.
               05  WSS-NUM-SPRI                      PIC 9(02).
               05  WSS-NUM-APRI                      PIC 9(02).
               05  WSS-NUM-MPRI                      PIC 9(02).
               05  WSS-NUM-DPRI                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-DTER                      PIC 9(02).
           03  WSS-FEC-FREA                          PIC 9(08).
           03  WSS-FEC-FREA-R REDEFINES WSS-FEC-FREA.
               05  WSS-NUM-SREA                      PIC 9(02).
               05  WSS-NUM-AREA                      PIC 9(02).
               05  WSS-NUM-MREA                      PIC 9(02).
               05  WSS-NUM-DREA                      PIC 9(02).
           03  WSS-FEC-FPVC                          PIC 9(08).
           03  WSS-FEC-FPVC-R REDEFINES WSS-FEC-FPVC.
               05  WSS-NUM-SPVC                      PIC 9(02).
               05  WSS-NUM-APVC                      PIC 9(02).
               05  WSS-NUM-MPVC                      PIC 9(02).
               05  WSS-NUM-DPVC                      PIC 9(02).
           03  WSS-HRA-HRTR                          PIC 9(06).
           03  WSS-HRA-HRTR-R REDEFINES WSS-HRA-HRTR.
               05  WSS-NUM-HHTR                      PIC 9(02).
               05  WSS-NUM-MMTR                      PIC 9(02).
               05  WSS-NUM-SSTR                      PIC 9(02).
           03  WSS-COD-OFIC                          PIC X(03).
           03  WSS-COD-COPD                          PIC X(06).
           03  WSS-COD-VCAM                          PIC X(06).
           03  WSS-COD-VIGE                          PIC X(03).
           03  WSS-TOT-COPD        VALUE ZEROES      PIC 9(05).
           03  WSS-TOT-VCAM        VALUE ZEROES      PIC 9(05).
           03  WSS-TOT-OFIC        VALUE ZEROES      PIC 9(05).
           03  WSS-TOT-FECH        VALUE ZEROES      PIC 9(05).
           03  WSS-GLS-DEST.
               05  WSS-GLS-DES1                      PIC X(20).
               05  WSS-GLS-DES2                      PIC X(20).

      * ISP: VARIABLES WSS-PTD
       COPY DAPWG004.
      *>>>>
      *<<<< WSS_DTC
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-PTD-REQA==.
      *Entidades de SGC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY DAPRFSTA.
           .
       COPY DAPRTSTA.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-STA-OPD-COD-OFIC SECTION.
           USE BEFORE REPORTING CF-STA-OPD-COD-OFIC.
       INI-CF-STA-OPD-COD-OFIC.
      *<<<< CF_STA_OPD_COD_OFIC
           MOVE WSS-TOT-OFIC IN WSS-VARI TO
                          WSS-TOT-OFIC IN WSS-STA.
           MOVE ZEROES TO WSS-TOT-OFIC IN WSS-VARI.
      *>>>>
       FIN-CF-STA-OPD-COD-OFIC.
           EXIT.
       DEC-CF-STA-OPD-FEC-FCMP SECTION.
           USE BEFORE REPORTING CF-STA-OPD-FEC-FCMP.
       INI-CF-STA-OPD-FEC-FCMP.
      *<<<< CF_STA_OPD_FEC_FCMP
           MOVE WSS-TOT-FECH IN WSS-VARI TO
                          WSS-TOT-FECH IN WSS-STA.
           MOVE ZEROES TO WSS-TOT-FECH IN WSS-VARI.
      *>>>>
       FIN-CF-STA-OPD-FEC-FCMP.
           EXIT.
       DEC-CF-STA-OPD-COD-VCAM SECTION.
           USE BEFORE REPORTING CF-STA-OPD-COD-VCAM.
       INI-CF-STA-OPD-COD-VCAM.
      *<<<< CF_STA_OPD_COD_VCAM
           MOVE WSS-TOT-VCAM IN WSS-VARI TO
                          WSS-TOT-VCAM IN WSS-STA.
           MOVE ZEROES TO WSS-TOT-VCAM IN WSS-VARI.
      *>>>>
       FIN-CF-STA-OPD-COD-VCAM.
           EXIT.
       DEC-CF-STA-OPD-COD-COPD SECTION.
           USE BEFORE REPORTING CF-STA-OPD-COD-COPD.
       INI-CF-STA-OPD-COD-COPD.
      *<<<< CF_STA_OPD_COD_COPD
           MOVE WSS-TOT-COPD IN WSS-VARI TO
                          WSS-TOT-COPD IN WSS-STA.
           MOVE ZEROES TO WSS-TOT-COPD IN WSS-VARI.
      *>>>>
       FIN-CF-STA-OPD-COD-COPD.
           EXIT.
       DEC-PH-STA SECTION.
           USE BEFORE REPORTING PH-STA.
       INI-PH-STA.
      *<<<< IF PH_STA
      *Recupera glosa de oficina
           MOVE OPD-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.

      *Recupera glosa de moneda
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'VLR' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-VCAM IN SRT TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-VCAM IN WSS-STA.

      *Recupera glosa de operacion
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'TIO' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-TOPD IN SRT TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-DES1 IN WSS-VARI.
      *
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'AUX' TO TAB-COD-TTAB IN TAB.
           MOVE OPD-COD-COPD IN SRT TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-DES2 IN WSS-VARI.
           MOVE WSS-GLS-DEST IN WSS-VARI TO
                                        WSS-GLS-COPD IN WSS-STA.
      *>>>>
       FIN-PH-STA.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPLSTA' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA PARAMETROS '.
           DISPLAY ' '.
      * Captura codigo de oficina
           DISPLAY 'INGRESE COD. DE OFICINA (*/XXX) : '.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.

           DISPLAY 'INGRESE FECHA DE INICIO (DDMMSSAA) : '.
           ACCEPT WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.

           DISPLAY 'INGRESE FECHA DE TERMINO (DDMMSSAA) : '.
           ACCEPT WSS-FEC-FFIN IN WSS-VARI.
           DISPLAY WSS-FEC-FFIN IN WSS-VARI.

           DISPLAY 'INGRESE CODIGO MONEDA  (*/XXXXXX)   :  '.
           ACCEPT WSS-COD-VCAM IN WSS-VARI.
           DISPLAY WSS-COD-VCAM IN WSS-VARI.

           DISPLAY 'INICIO VALIDACION PARAMETROS '.
           DISPLAY ' '.
      *VAL-OFI Valida Codigo de oficina
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
              MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
              PERFORM VAL-OFI
               IF MSG-GLS-MENS = 'COD    NEX'
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' ERROR VALIDACION OFICINA: ' WSS-COD-OFIC
                  PERFORM GNS-PRO-ABT.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-CHOY-N TO FEC-CHOY.
           MOVE FEC-VHBL-N TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR VALIDACION EN FECHA DE INICIO: '
                         WSS-FEC-FINI '  ' FEC-MENS
               PERFORM GNS-PRO-ABT.
 
      *VAL-FEC Valida Fecha de Termino
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-CHOY-N TO FEC-CHOY.
           MOVE FEC-VHBL-N TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR VALIDACION EN FECHA DE TERMINO: '
                         WSS-FEC-FFIN '  ' FEC-MENS
               PERFORM GNS-PRO-ABT.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' FECHA INICIO > FECHA TERMINO: '
                         WSS-FEC-FINI ' > ' WSS-FEC-FFIN
               PERFORM GNS-PRO-ABT.

      *VAL-TAB Valida Codigo de moneda
           IF WSS-COD-VCAM IN WSS-VARI > SPACES AND
              WSS-COD-VCAM IN WSS-VARI NOT = '*'
              DISPLAY WSS-COD-VCAM IN WSS-VARI
              MOVE 'TAB'               TO TAB-COD-SIST
              MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
              MOVE WSS-COD-VCAM IN WSS-VARI TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
               IF MSG-GLS-MENS = 'COD    NEX'
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' ERROR VALIDACION COD. MONEDA: ' WSS-COD-VCAM
                  PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-STA.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-STA.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-STA.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-STA.
           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DFIN IN WSS-STA.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MFIN IN WSS-STA.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-SFIN IN WSS-STA.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-AFIN IN WSS-STA.
           DISPLAY ' '.
           DISPLAY 'FIN VALIDACION PARAMETROS '.
           DISPLAY ' '.
      *>>>>
           SORT GNSSRT
       COPY DAPRSSTA.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
       FST-INP-SRT.
      *<<<< INI_FST_INP
      *Abre otros archivos del PGM.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-RCC.

      *Genera registros en archivo SORT.
           MOVE WSS-NUM-DINI IN WSS-VARI TO OPD-NUM-DCMP IN OPD.
           MOVE WSS-NUM-MINI IN WSS-VARI TO OPD-NUM-MCMP IN OPD.
           MOVE WSS-NUM-SINI IN WSS-VARI TO OPD-NUM-SCMP IN OPD.
           MOVE WSS-NUM-AINI IN WSS-VARI TO OPD-NUM-ACMP IN OPD.


           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DPRI IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MPRI IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SPRI IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-APRI IN WSS-VARI.
           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.

      * INICIA BUSQUEDA RETROACTIVA
      *GEN-FEC-INI Genera Fecha de Inicio
           MOVE FEC-RST-DIA              TO FEC-CMND.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE 10                       TO FEC-NDIA.
           MOVE OPD-NUM-DCMP IN OPD TO FEC-ITM1.
           MOVE OPD-NUM-MCMP IN OPD TO FEC-ITM2.
           MOVE OPD-NUM-SCMP IN OPD TO FEC-ITM3.
           MOVE OPD-NUM-ACMP IN OPD TO FEC-ITM4.
           PERFORM CAL-FEC.
           MOVE FEC-ITM1 TO OPD-NUM-DCMP IN OPD.
           MOVE FEC-ITM2 TO OPD-NUM-MCMP IN OPD.
           MOVE FEC-ITM3 TO OPD-NUM-SCMP IN OPD.
           MOVE FEC-ITM4 TO OPD-NUM-ACMP IN OPD.

           DISPLAY 'INICIO DE BUSQUEDA ' OPD-NUM-DCMP IN OPD '-'
                                         OPD-NUM-MCMP IN OPD '-'
                                         OPD-NUM-SCMP IN OPD
                                         OPD-NUM-ACMP IN OPD.

      *JVM: Accesa archivo por llave alternativa.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR AL ACCESAR OPD POR OPD-FEC-FCMP : '
                                                     FIO-STAT
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           IF OPD-FEC-FCMP IN OPD > WSS-FEC-FTER IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS OPD EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI 
                       ' A    ' WSS-FEC-FFIN IN WSS-VARI
               GO TO FIN-INP-SRT
           ELSE
               GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      *Controla ingreso de regs. vigentes o no vigentes validos
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
              IF WSS-COD-OFIC IN WSS-VARI NOT = OPD-COD-OFIC IN OPD
                 GO TO NXT-INP-SRT.

           IF WSS-COD-VCAM IN WSS-VARI > SPACES AND
              WSS-COD-VCAM IN WSS-VARI NOT = '*'
              IF WSS-COD-VCAM IN WSS-VARI NOT = OPD-COD-VCAM IN OPD
                 GO TO NXT-INP-SRT.

           IF OPD-COD-EATE IN OPD NOT > SPACES 
              GO TO NXT-INP-SRT.

      * ISP:  22-MAR-1993 07:43:08 
           MOVE OPD-CIC-IOPD IN OPD TO WSS-CIC-IOPD.
           MOVE OPD-CIC-ORIG IN OPD TO OPD-CIC-IOPD IN OPD.

           IF OPD-FEC-FTRN IN OPD < WSS-FEC-FPRI IN WSS-VARI
              DISPLAY 'CAPTACION RECHAZADA FUERA DE RANGO ' 
                      OPD-CIC-IOPD IN OPD
              GO TO NXT-INP-SRT.

           MOVE 1 TO SRT-NUM-NOPD IN SRT.

           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
      *>>>>
       MOV-INP-SRT.
       COPY DAPRMSTA.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
      *Ubica registro en OPD por fecha
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           MOVE WSS-CIC-IOPD TO OPD-CIC-IOPD.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
      *<<<< FIN_NXT_INP
      *Controla ingreso de registros por fecha termino
           IF OPD-FEC-FCMP IN OPD > WSS-FEC-FTER IN WSS-VARI
              GO TO FIN-INP-SRT.
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-OPD.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTSTA.
      *     MOVE 'DAPPLSTA' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-STA.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *Limpia FIO-AKEY para lectura de otros archivos
           MOVE SPACES TO FIO-AKEY.

      *Genera glosas,datos de clientes y tasa de interes  
           MOVE OPD-CAI-IOPD IN SRT TO RCC-CAI-IOPD IN RCC.
           MOVE OPD-IIC-IOPD IN SRT TO RCC-IIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF FIO-STAT-OKS
              PERFORM GET-RCC-DBC
           ELSE
              PERFORM RST-DBC.

      *Recupera tasa maxima
           PERFORM GET-TINT.
           IF MSG-COD-MENS > SPACES
               DISPLAY 'ERROR AL ACCESAR PTD ' MSG-COD-MENS

           ELSE
              PERFORM BUS-SGV-TINT. 
     
      *Recupera glosa de tasa
           MOVE 'TAB'        TO TAB-COD-SIST.
           MOVE 'INT'        TO TAB-COD-TTAB IN TAB.            
           MOVE OPD-COD-TINT IN SRT  TO TAB-COD-CTAB IN TAB.            
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-TINT IN WSS-STA.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-STA.
      *<<<< FIN_GEN_OUT
           ADD SRT-NUM-NOPD IN SRT TO WSS-TOT-VCAM IN WSS-VARI.
           ADD SRT-NUM-NOPD IN SRT TO WSS-TOT-COPD IN WSS-VARI.
           ADD SRT-NUM-NOPD IN SRT TO WSS-TOT-OFIC IN WSS-VARI.
           ADD SRT-NUM-NOPD IN SRT TO WSS-TOT-FECH IN WSS-VARI.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-STA.
           CLOSE RPTSTA.
       EXT-OUT-SRT.
           EXIT.
       COPY DAPBFOPD.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      * Lee con CIC CLIENTE archivo DBC
       GET-RCC-DBC SECTION.
       INI-GET-RCC-DBC.
           MOVE RCC-CIC-ICLI IN RCC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
              PERFORM RST-DBC
           ELSE
              MOVE '-'  TO WSS-GLS-AUXI IN SRT
              MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
              PERFORM CPT-BLKS
              MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC. 
       FIN-GET-RCC-DBC.
           EXIT.

      * Limpia datos cliente
       RST-DBC SECTION.
       INI-RST-DBC.
           MOVE 'CLIENTE INEXISTENTE' TO DBC-GLS-NOMC IN DBC.
           MOVE SPACES TO DBC-NUM-ICLI IN DBC.
           MOVE SPACES TO DBC-VRF-ICLI IN DBC.
           MOVE SPACES TO DBC-IND-ICLI IN DBC.
           MOVE SPACES TO WSS-GLS-AUXI IN SRT.
           MOVE SPACES TO DBC-GLS-ICLI IN DBC.
       FIN-RST-DBC.
           EXIT.

       GET-TINT SECTION.
       INI-GET-TINT.
      *    obtener ptd mas actual y vigente
      *    acceso a ptd con VCAM de la pantalla
           MOVE OPD-COD-VCAM IN SRT     TO PTD-COD-VCAM IN PTD.
           MOVE SPACES                  TO PTD-COD-OFIC IN PTD.
           MOVE SPACES                  TO PTD-COD-TOPD IN PTD.
           MOVE SPACES                  TO PTD-COD-AOPD IN PTD.
           PERFORM GET-FHOY.
           IF OPD-FEC-FREA IN SRT = OPD-FEC-FCMP IN SRT 
              MOVE OPD-HRA-HRTR IN SRT TO WSS-HRA-HRTR IN WSS-VARI
              MOVE WSS-NUM-HHTR IN WSS-VARI TO PTD-NUM-HHPT IN PTD
              MOVE WSS-NUM-MMTR IN WSS-VARI TO PTD-NUM-MMPT IN PTD
              MOVE WSS-NUM-SSTR IN WSS-VARI TO PTD-NUM-SSPT IN PTD
           ELSE
           IF OPD-FEC-FREA IN SRT < OPD-FEC-FCMP IN SRT 
              MOVE '23'                     TO PTD-NUM-HHPT IN PTD
              MOVE '59'                     TO PTD-NUM-MMPT IN PTD
              MOVE '59'                     TO PTD-NUM-SSPT IN PTD
           ELSE
              MOVE 'COD    NEX' TO MSG-COD-MENS
              DISPLAY 'FECHA DE EMISION MAYOR QUE FECHA COMP. ' 
                       OPD-FEC-FREA IN SRT ' ' OPD-FEC-FCMP IN SRT
              GO TO FIN-GET-TINT.

           MOVE OPD-NUM-DREA IN SRT TO PTD-NUM-DPTD IN PTD.
           MOVE OPD-NUM-MREA IN SRT TO PTD-NUM-MPTD IN PTD.
           MOVE OPD-NUM-SREA IN SRT TO PTD-NUM-SPTD IN PTD.
           MOVE OPD-NUM-AREA IN SRT TO PTD-NUM-APTD IN PTD.

      * ISP: DAPBG004
           PERFORM CMP-FEC-PTD.

           MOVE FIO-GET-NLS TO FIO-CMND.

       LUP-GET-TINT.
           PERFORM DAP-FIO-PTD.
           IF NOT ( FIO-STAT-OKS AND
                    OPD-COD-VCAM IN SRT = PTD-COD-VCAM IN PTD )
      *        ptd inexistente
               MOVE 'PTD    NEX' TO MSG-COD-MENS
               GO TO FIN-GET-TINT.

           IF NOT ( ( PTD-COD-STAT IN PTD = 'S' )
                    AND
                    ( PTD-COD-TOPD IN PTD = '*' OR
                      PTD-COD-TOPD IN PTD = OPD-COD-TOPD IN SRT )
                    AND
                    ( PTD-COD-AOPD IN PTD = '*' OR
                      PTD-COD-AOPD IN PTD = OPD-COD-AOPD IN SRT )
                    AND
                    ( PTD-COD-OFIC IN PTD = '*' OR
                      PTD-COD-OFIC IN PTD = OPD-COD-OFIC IN SRT ) )
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-GET-TINT.
       FIN-GET-TINT.
           EXIT.

       BUS-SGV-TINT SECTION.
       INI-BUS-SGV-TINT.
      *VAL-FEC diferencia de fechas para calcular valor final
           MOVE OPD-FEC-FREA IN SRT TO WSS-FEC-FREA IN WSS-VARI.
           MOVE WSS-NUM-DREA IN WSS-VARI TO FEC-DEC1.
           MOVE WSS-NUM-MREA IN WSS-VARI TO FEC-MEC1.
           MOVE WSS-NUM-SREA IN WSS-VARI TO FEC-SEC1.
           MOVE WSS-NUM-AREA IN WSS-VARI TO FEC-AEC1.
           MOVE OPD-FEC-FPVC IN SRT TO WSS-FEC-FPVC IN WSS-VARI.
           MOVE WSS-NUM-DPVC IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MPVC IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SPVC IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-APVC IN WSS-VARI TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA TO FEC-CMND.
           PERFORM CAL-FEC.
           IF FEC-STAT-OKS
               MOVE FEC-NDIA TO WSS-NDIA IN WSS-VARI
           ELSE
               DISPLAY 'NDIA NOESPEC ' WSS-NDIA
               GO TO FIN-BUS-SGV-TINT.

           MOVE 10 TO WSS-TRM-DIA.
           MOVE 5  TO WSS-TRM-MTO.

           MOVE 'N' TO WSS-IND-TDIA.
           PERFORM BUS-TRM-DIA VARYING WSS-I FROM 1 BY 1 UNTIL
                   WSS-I > 10 OR WSS-IND-TDIA = 'S'.

           IF WSS-IND-TDIA = 'N'
      *        tramo dia no especificado
               MOVE 'TDIA NOESPEC' TO MSG-COD-MENS
               DISPLAY 'TDIA NOESPEC ' WSS-IND-TDIA
               GO TO FIN-BUS-SGV-TINT.

           MOVE 'N' TO WSS-IND-TMTO.
           PERFORM BUS-TRM-MTO VARYING WSS-I FROM 5 BY -1 UNTIL
                   WSS-I < 1 OR WSS-IND-TMTO = 'S'.

      *     IF WSS-IND-TMTO = 'N'
      *        tramo monto no especificado
      *         MOVE 'TMTO NOESPEC' TO MSG-COD-MENS
      *         DISPLAY 'TMTO NOESPEC' WSS-IND-TMTO
      *         GO TO FIN-BUS-SGV-TINT.

           IF WSS-IND-TMTO = 'N'
                MOVE 1 TO WSS-TRM-MTO.

           MULTIPLY WSS-TRM-DIA BY 5 GIVING WSS-IDX-INT.
           SUBTRACT 5 FROM WSS-IDX-INT.
           ADD WSS-TRM-MTO TO WSS-IDX-INT.

           IF WSS-IND-TMTO = 'N'
               MOVE PTD-SGV-TMIN IN PTD(WSS-IDX-INT) TO
               PTD-SGV-TMAX IN PTD(WSS-IDX-INT)
               MOVE ZEROES TO PTD-SGV-TADC IN PTD(WSS-TRM-MTO).

           MOVE PTD-SGV-TMAX IN PTD(WSS-IDX-INT) TO 
                                         SRT-SGV-TMAX IN SRT.
           DISPLAY 'WSS-NDIA ' WSS-NDIA.
           DISPLAY 'TRM-DIA  ' WSS-TRM-DIA.
           DISPLAY 'TRM-MTO  ' WSS-TRM-MTO.
           DISPLAY 'IDX-INT  ' WSS-IDX-INT.
           DISPLAY 'SGV-TMAX ' SRT-SGV-TMAX IN SRT.
           DISPLAY '         '.
       FIN-BUS-SGV-TINT.
           EXIT.

       BUS-TRM-DIA SECTION.
       INI-BUS-TRM-DIA.
           IF WSS-NDIA IN WSS-VARI NOT > 
              PTD-NUM-DIAS IN PTD(WSS-I)
               MOVE WSS-I TO WSS-TRM-DIA
               MOVE 'S' TO WSS-IND-TDIA.
       FIN-BUS-TRM-DIA.
           EXIT.

       BUS-TRM-MTO SECTION.
       INI-BUS-TRM-MTO.
           IF PTD-VAL-MNTO IN PTD(WSS-I) > ZEROES
              IF OPD-VAL-CAPT IN SRT > 
                 PTD-VAL-MNTO IN PTD(WSS-I)
                 MOVE WSS-I TO WSS-TRM-MTO
                 MOVE 'S' TO WSS-IND-TMTO.
       FIN-BUS-TRM-MTO.
           EXIT.

      *Entidades  de GNS
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBGCPT.

      *Entidades  de SGC
       COPY SGCBFDBC.

      *Entidades  de DAP
       COPY DAPBFRCC.
       COPY DAPBFPTD.
       COPY DAPBG004.

      *Entidades  de TAB
       COPY TABBFOFI.
       COPY TABBBOFI.
       COPY TABBVOFI.
      *>>>>
