       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPLONP.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    07-Jun-96 05:08 PM.

      * Programa Listador de Reporte ONP con Sort.

      *<<<< CMT_ID
      * ==>>   23-APR-1993 20:55:30 
      *        DEJA AFUERA LOS RENOVABLES LIQUIDADOS
      * ==>>   28-JUL-1993 10:33:00 SE ELIMINAN DISPLAY INFORMATIVOS
      *>>>>

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTONP         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTONP         ASSIGN TO        UT-S-RPTONP.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTONP         ASSIGN TO        UT-S-RPTONP.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY DAPRRONP.
       FD  RPTONP LABEL RECORDS OMITTED
           REPORT IS RPT-ONP
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ONP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY DAPBRONP.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY DAPRWONP.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
      *Entidades de GNS
       COPY GNSBRMSC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
      *Entidades de TAB
       COPY TABBROFI.
       COPY TABBRUSR.
       COPY TABBRVLR.
      *Entidades de DAP
       COPY DAPBRRCC.
      *Entidades de SGC
       COPY SGCBRDBC.
      *Entidades de DAP
       COPY DAPBROPD.

       01  WSS-VARI.
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-DFIN                      PIC 9(02).
               05  WSS-NUM-MFIN                      PIC 9(02).
               05  WSS-NUM-SFIN                      PIC 9(02).
               05  WSS-NUM-AFIN                      PIC 9(02).
           03  WSS-FEC-FIN1.
               05  WSS-NUM-SIN1                      PIC 9(02).
               05  WSS-NUM-AIN1                      PIC 9(02).
               05  WSS-NUM-MIN1                      PIC 9(02).
               05  WSS-NUM-DIN1                      PIC 9(02).
           03  WSS-FEC-FFI1.
               05  WSS-NUM-SFI1                      PIC 9(02).
               05  WSS-NUM-AFI1                      PIC 9(02).
               05  WSS-NUM-MFI1                      PIC 9(02).
               05  WSS-NUM-DFI1                      PIC 9(02).
           03  WSS-SWI-SELI            VALUE 0       PIC 9(01).
           03  WSS-SWI-SELE            VALUE 0       PIC 9(01).
           03  WSS-COD-OFIC                          PIC X(03).
           03  WSS-CIC-IOPD.
               05  WSS-CAI-IOPD                      PIC X(04).
               05  WSS-IIC-IOPD                      PIC X(08).
      *>>>>
      *<<<< WSS_DTC
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      *Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCC-REQA==.
      *Entidades de SGC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY DAPRFONP.
           .
       COPY DAPRTONP.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-ONP-OPD-COD-OFIC SECTION.
           USE BEFORE REPORTING CF-ONP-OPD-COD-OFIC.
       INI-CF-ONP-OPD-COD-OFIC.
       FIN-CF-ONP-OPD-COD-OFIC.
           EXIT.
       DEC-PH-ONP SECTION.
           USE BEFORE REPORTING PH-ONP.
       INI-PH-ONP.
      *<<<< IF PH_ONP
           MOVE OPD-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
           PERFORM BUS-OFI.
      *>>>>
       FIN-PH-ONP.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPLONP' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA PARAMETROS '.
           DISPLAY ' '.
      * Captura codigo de oficina
           DISPLAY 'INGRESE CODIGO DE OFICINA : '.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           IF WSS-COD-OFIC NOT > SPACES
               MOVE '*' TO WSS-COD-OFIC.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.

           DISPLAY 'INGRESE FECHA DE INICIO  '.
           ACCEPT WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.

           DISPLAY 'INGRESE FECHA DE TERMINO '.
           ACCEPT WSS-FEC-FFIN IN WSS-VARI.
           DISPLAY WSS-FEC-FFIN IN WSS-VARI.

           DISPLAY 'INICIO VALIDACION PARAMETROS '.
           DISPLAY ' '.
      *VAL-OFI Valida Codigo de oficina
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
              MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
              PERFORM VAL-OFI
              IF MSG-GLS-MENS = 'COD    NEX'
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' ERROR VALIDACION OFICINA: ' WSS-COD-OFIC
                  PERFORM GNS-PRO-ABT.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-CHOY-N TO FEC-CHOY.
           MOVE FEC-VHBL-N TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR VALIDACION EN FECHA DE INICIO: '
                        WSS-FEC-FINI '  ' FEC-MENS
              PERFORM GNS-PRO-ABT.
 
      *VAL-FEC Valida Fecha de Termino
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-CHOY-N TO FEC-CHOY.
           MOVE FEC-VHBL-N TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR VALIDACION EN FECHA DE TERMINO: '
                        WSS-FEC-FFIN '  ' FEC-MENS
              PERFORM GNS-PRO-ABT.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' FECHA INICIO > FECHA TERMINO: '
                         WSS-FEC-FINI ' > ' WSS-FEC-FFIN
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-ONP.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-ONP.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-ONP.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-ONP.

           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DFIN IN WSS-ONP.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MFIN IN WSS-ONP.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-SFIN IN WSS-ONP.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-AFIN IN WSS-ONP.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DIN1 IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MIN1 IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SIN1 IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AIN1 IN WSS-VARI.

           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DFI1 IN WSS-VARI.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MFI1 IN WSS-VARI.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-SFI1 IN WSS-VARI.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-AFI1 IN WSS-VARI.
           DISPLAY ' '.
           DISPLAY 'FIN VALIDACION PARAMETROS '.
           DISPLAY ' '.
      *>>>>
           SORT GNSSRT
       COPY DAPRSONP.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-ONP.
       FST-INP-SRT.
      *<<<< INI_FST_INP
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-OPD.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM DAP-FIO-RCC.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM DAP-FIO-ONP.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      *Controla ingreso de registros en SORT
           MOVE 0 TO WSS-SWI-SELI IN WSS-VARI
                     WSS-SWI-SELE IN WSS-VARI.

           IF ONP-FEC-FING IN ONP > WSS-FEC-FFI1 IN WSS-VARI OR
              ONP-FEC-FING IN ONP < WSS-FEC-FIN1 IN WSS-VARI
                MOVE 1 TO WSS-SWI-SELI IN WSS-VARI. 

           IF ONP-FEC-FELI IN ONP NOT > ZEROES
                MOVE 1 TO WSS-SWI-SELE IN WSS-VARI
           ELSE
           IF ONP-FEC-FELI IN ONP > WSS-FEC-FFI1 IN WSS-VARI OR
              ONP-FEC-FELI IN ONP < WSS-FEC-FIN1 IN WSS-VARI
                 MOVE 1 TO WSS-SWI-SELE IN WSS-VARI. 

           IF WSS-SWI-SELI IN WSS-VARI = 1 AND
              WSS-SWI-SELE IN WSS-VARI = 1
              GO TO NXT-INP-SRT.

           IF WSS-COD-OFIC IN WSS-VARI NOT = '*' AND
              WSS-COD-OFIC IN WSS-VARI NOT = ONP-COD-OFIC IN ONP
               GO TO NXT-INP-SRT.

           PERFORM GET-ONP-OPD.
      * DEJA AFUERA LAS '1XXX' Y LAS RENOVABLES LIQUIDADAS
           IF OPD-COD-STAT IN OPD = 'L' AND
              OPD-COD-RENO = 'S'
      *          DISPLAY OPD-CIC-IOPD IN OPD ' '
      *                  'OPD RECHAZADA: RENOVABLE LIQUIDADO'
                GO TO NXT-INP-SRT.
      * DEJA AFUERA LAS FIJOS LIQUIDADOS
           IF OPD-COD-STAT IN OPD = 'L' AND
              OPD-COD-SCTB IN OPD = 'CAN'
      *          DISPLAY OPD-CIC-IOPD IN OPD ' '
      *                  'OPD RECHAZADA: FIJO PAGADO'
                GO TO NXT-INP-SRT.

      *>>>>
       MOV-INP-SRT.
       COPY DAPRMONP.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-ONP.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-ONP.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTONP.
      *     MOVE 'DAPPLONP' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-ONP.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *Genera glosas,datos de clientes.
           MOVE ONP-CAI-IOPD IN SRT TO WSS-CAI-IOPD IN WSS-VARI.
           MOVE ONP-IIC-IOPD IN SRT TO WSS-IIC-IOPD IN WSS-VARI.
           MOVE ONP-CAI-IOPD IN SRT TO RCC-CAI-IOPD IN RCC.
           MOVE ONP-IIC-IOPD IN SRT TO RCC-IIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF FIO-STAT-OKS
              PERFORM LUP-GET-RCC
           ELSE
              PERFORM RST-DBC.
           PERFORM BUS-GLS-ONP.

      *Este parrafo es una iteracion para recuperar cliente por error  
      *en archivo RCC
      *     MOVE ONP-CAI-IOPD IN SRT TO WSS-CAI-IOPD IN WSS-VARI.
      *     MOVE ONP-IIC-IOPD IN SRT TO WSS-IIC-IOPD IN WSS-VARI.
      *     MOVE FIO-GET-FST TO FIO-CMND.
      *     PERFORM DAP-FIO-RCC.
      *     PERFORM LUP-GET-RCC.
      *     PERFORM BUS-GLS-ONP.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-ONP.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-ONP.
           CLOSE RPTONP.
       EXT-OUT-SRT.
           EXIT.
       COPY DAPBFONP.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      * Lee con CIC OPERACION archivo ONP
       GET-ONP-OPD SECTION.
       INI-GET-ONP-OPD.
           MOVE ONP-CAI-IOPD IN ONP TO OPD-CAI-IOPD IN OPD.
           MOVE ONP-IIC-IOPD IN ONP TO OPD-IIC-IOPD IN OPD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF FIO-STAT-OKS
              MOVE OPD-COD-COPD IN OPD TO WSS-COD-COPD IN WSS-ONP
           ELSE
              PERFORM RST-OPD.
       FIN-GET-ONP-OPD.
           EXIT.

      * Limpia datos operacion
       RST-OPD SECTION.
       INI-RST-OPD.
           MOVE SPACES TO WSS-COD-COPD IN WSS-ONP.
       FIN-RST-OPD.
           EXIT.

      * Lee con LUP hasta encontrar CLIENTE 
       LUP-GET-RCC SECTION.
       INI-LUP-GET-RCC.
           IF NOT FIO-STAT-OKS
              GO TO FIN-LUP-GET-RCC.
           IF WSS-CIC-IOPD IN WSS-VARI = RCC-CIC-IOPD IN RCC
              PERFORM GET-RCC-DBC
              GO TO FIN-LUP-GET-RCC.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           GO TO INI-LUP-GET-RCC.   
       FIN-LUP-GET-RCC.
           EXIT.

      * Lee con CIC CLIENTE archivo DBC
       GET-RCC-DBC SECTION.
       INI-GET-RCC-DBC.
           MOVE RCC-CIC-ICLI IN RCC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
              PERFORM RST-DBC
           ELSE
              MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
              PERFORM CPT-BLKS
              MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC. 
           IF DBC-GLS-ICLI IN DBC > SPACES 
              MOVE '-' TO WSS-GLS-AUXI IN WSS-ONP
           ELSE
              MOVE SPACES TO WSS-GLS-AUXI IN WSS-ONP.
       FIN-GET-RCC-DBC.
           EXIT.

      * Limpia datos cliente
       RST-DBC SECTION.
       INI-RST-DBC.
           MOVE 'CLIENTE INEXISTENTE' TO DBC-GLS-NOMC IN DBC.
           MOVE ZEROES TO DBC-NUM-ICLI IN DBC.
           MOVE SPACES TO DBC-VRF-ICLI IN DBC.
           MOVE SPACES TO DBC-IND-ICLI IN DBC.
           MOVE SPACES TO DBC-GLS-ICLI IN DBC.
       FIN-RST-DBC.
           EXIT.

      * Recupera glosas desde archivo SORT
       BUS-GLS-ONP SECTION.
       INI-BUS-GLS-ONP.
      *     IF WSS-COD-COPD IN SRT > SPACES
      *        MOVE 'TAB' TO TAB-COD-SIST
      *        MOVE 'AUX' TO TAB-COD-TTAB IN TAB
      *        MOVE WSS-COD-COPD IN SRT TO TAB-COD-CTAB IN TAB
      *        PERFORM BUS-TAB
      *        MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-COPD IN WSS-ONP 
      *     ELSE
      *        MOVE SPACES TO WSS-GLS-COPD IN WSS-ONP.

           MOVE WSS-COD-COPD IN SRT TO WSS-GLS-COPD IN WSS-ONP.

           IF ONP-COD-OFIC IN SRT > SPACES
              MOVE ONP-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
              PERFORM BUS-OFI
              MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIC IN WSS-ONP
           ELSE
              MOVE SPACES TO WSS-GLS-OFIC IN WSS-ONP.

           IF ONP-COD-OFEL IN SRT > SPACES
              MOVE ONP-COD-OFEL IN SRT TO OFI-COD-OFIC IN OFI
              PERFORM BUS-OFI
              MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFEL IN WSS-ONP
           ELSE
              MOVE SPACES TO WSS-GLS-OFEL IN WSS-ONP.

           IF ONP-COD-MONP IN SRT > SPACES
              MOVE 'DAP' TO TAB-COD-SIST
              MOVE 'ONP' TO TAB-COD-TTAB IN TAB
              MOVE ONP-COD-MONP IN SRT TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
              MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-MONP IN WSS-ONP 
           ELSE
              MOVE SPACES TO WSS-GLS-MONP IN WSS-ONP.

       FIN-BUS-GLS-ONP.
           EXIT.

      *Entidades  de GNS
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBFMSC.
       COPY GNSBVMSC.
       COPY GNSBBMSC.
       COPY GNSBGCPT.

      *Entidades  de SGC
       COPY SGCBFDBC.

      *Entidades  de DAP
       COPY DAPBFRCC.
       COPY DAPBFOPD.

      *Entidades  de TAB
       COPY TABBFOFI.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY TABBFUSR.
       COPY TABBBUSR.
       COPY TABBVUSR.
      *>>>>
