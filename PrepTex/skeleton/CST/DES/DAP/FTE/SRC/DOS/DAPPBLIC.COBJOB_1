       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPBLIC.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    24-OCT-1995

      * Programa Batch que Permite Activar LIQ'S por SAFE sin Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVNUM.
       COPY GNSWCNUM.

      *Entidades  de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRTAB.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGFEC.
       COPY GNSWGHOY.

      *Entidades  de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY DAPBROPD.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LIQ-REQA==.
       COPY DAPBRLIQ.
      *JUS-INI 27-OCT-1995
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAD-REQA==.
       COPY DAPBRCAD.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY DAPBRVEN.
      *JUS-FIN

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LIC-REQA==.
       COPY DAPBRLIC.

       01  WSS-VARI.
      *RVM-BEE-INI 29-JUL-1997
           03 WSS-VAL-DISP      VALUE ZEROES  PIC ---.---.---.---,----.
      *RVM-BEE-FIN 29-JUL-1997
           03 WSS-IND-MAL       VALUE SPACES          PIC X(01).
           03 WSS-CIC-IOPD      VALUE SPACES          PIC X(12).
           03 BKP-KEY-ILIQ      VALUE SPACES          PIC X(15).
           03 DSP-FIO-STAT          VALUE 'N'         PIC X(01).
              88 DESPLIEGA-FIO-STAT VALUE 'S'.
           03  WSS-GLS-DATA.
               05  WSS-GLS-DATA-FILL                 PIC X(01).
               05  WSS-GLS-PZO-DAP                   PIC X(01).
           03  WSS-I                                 PIC 9(02).
           03  WSS-FEC-FINP.
               05  WSS-NUM-DINP                      PIC 9(02).
               05  WSS-NUM-MINP                      PIC 9(02).
               05  WSS-NUM-SINP                      PIC 9(02).
               05  WSS-NUM-AINP                      PIC 9(02).
           03  WSS-FEC-FPRO.
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-DPRO                      PIC 9(02).
      * INDICADOR DE DESPLIEGUE PARAMETRICO
           03  WSS-SWI-DISP  VALUE 'S'               PIC X(01).
      * VARIABLES PROCESO LIC
           03  WSS-STAT-ACT         VALUE ZEROES     PIC 9(01).
      *TOTAL DE REGISTROS EN ARCHIVO LIC
           03  WSS-LEI-LIC                           PIC 9(06).
      *TOTAL DE REGISTROS ACTUALIZADOS LIQ,OPD
           03  WSS-GRA-LIQ                           PIC 9(06).
           03  WSS-GRA-OPD                           PIC 9(06).
           03  SCR-SON-S                             PIC X(01).
      *JUS-INI 24-OCT-1995
           03 WSS-VAL-EDIT                 PIC ZZZ.ZZZ.ZZZ.ZZ9,9999.
      *JUS-FIN
       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPBLIC' TO FIO-PROG.
           PERFORM BUS-FSIS.
       INI-INP-SRT.
      *<<<< INI_INP
           MOVE ZEROES TO WSS-LEI-LIC
                          WSS-GRA-LIQ
                          WSS-GRA-OPD.

           DISPLAY ' '.
           DISPLAY ' INICIO CAPTURA PARAMETROS '.
           DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA) : '.
           ACCEPT WSS-FEC-FINP IN WSS-VARI.
           DISPLAY WSS-FEC-FINP IN WSS-VARI.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FINP IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N  TO FEC-FBLK.
           MOVE FEC-CHOY-N  TO FEC-CHOY.
           MOVE FEC-VHBL-N  TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR EN VALIDACION DE FECHA DE PROCESO'
                                    FEC-STAT  ' ' WSS-FEC-FPRO
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DINP TO WSS-NUM-DPRO.
           MOVE WSS-NUM-MINP TO WSS-NUM-MPRO.
           MOVE WSS-NUM-SINP TO WSS-NUM-SPRO.
           MOVE WSS-NUM-AINP TO WSS-NUM-APRO.

      *Obtiene valor indicador de afirmativo
           MOVE 'GNS' TO TAB-COD-SIST.
           MOVE 'SON' TO TAB-CIC-TTAB IN TAB.
           MOVE 'S'   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR EN VALIDACION INDICADOR S/N '
                                          MSG-COD-MENS
               PERFORM GNS-PRO-ABT
           ELSE
               MOVE TAB-COD-CTAB IN TAB TO SCR-SON-S
               DISPLAY ' '
               DISPLAY 'IND. AFIRMATIVO: ' TAB-COD-CTAB IN TAB.

           DISPLAY ' '.
           DISPLAY ' FIN CAPTURA PARAMETROS '.
           DISPLAY ' '.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-LIC.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.

       PRO-LIC.
           DISPLAY ' '.
           DISPLAY ' INICIO PROCESO '.
           DISPLAY ' '.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM DAP-FIO-LIC.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' NO EXISTEN REGISTROS LIC A PROCESAR'
                                              FIO-STAT
               PERFORM GNS-PRO-ABT
           ELSE
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM DAP-FIO-LIC
               IF NOT FIO-STAT-OKS
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' NO EXISTE REG. LIC YA SELECCIONADO: '
                            LIC-CIC-ISER IN LIC ' FS: ' FIO-STAT
                  PERFORM GNS-PRO-ABT.

       LUP-MAIN.
           IF NOT FIO-STAT-OKS
                GO TO FIN-MAIN.
           ADD 1 TO WSS-LEI-LIC.
           MOVE ZEROES TO WSS-STAT-ACT.

           PERFORM BUS-LIC-OPD.
      *RVM-BEE-INI 29-JUL-1997
           IF NOT FIO-STAT-OKS
               GO TO NXT-MAIN.
      *RVM-BEE-FIN 29-JUL-1997

           IF LIC-COD-STAT IN LIC = 'A'
              PERFORM ACT-LIQ-OPD
              MOVE FIO-GET-KEY-UPD TO FIO-CMND
              PERFORM DAP-FIO-LIC
              IF NOT FIO-STAT-OKS
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' ERROR EN UPDATE REG. LIC '
                            LIC-CIC-ISER IN LIC ' FS: ' FIO-STAT
                  PERFORM GNS-PRO-ABT
              ELSE
                MOVE FIO-DEL TO FIO-CMND
                PERFORM DAP-FIO-LIC
                IF NOT FIO-STAT-OKS
                   DISPLAY ' PROCESO CANCELADO '
                   DISPLAY ' ERROR AL ELIMINAR REG. LIC: '
                             LIC-CIC-ISER IN LIC ' FS: ' FIO-STAT
                  PERFORM GNS-PRO-ABT.

           IF LIC-COD-STAT IN LIC = 'S'
              IF OPD-COD-RENO IN OPD = SCR-SON-S
                 MOVE 'S' TO WSS-IND-MAL
                 DISPLAY ' '
                 DISPLAY ' CLIENTE INFORMO RETIRO DE INT. O LIQ. TOT '
                 DISPLAY ' Y NO RETIRO EFECTIVAMENTE DESDE CAJA'
                 DISPLAY ' SERIAL LIC : ' LIC-CIC-ISER IN LIC
                 DISPLAY ' CIC OPD    : ' OPD-CIC-IOPD IN OPD
                 DISPLAY ' SERIAL OPD : ' OPD-CIC-ISER IN OPD
      *JUS-INI 27-OCT-1995
                 PERFORM PRO-REV-LIQ.
      *JUS-FIN
      *        ELSE
      *           DISPLAY ' '
      *           DISPLAY ' MONTOS DISPONIBLES EN CAJA PARA CLIENTE '
      *           DISPLAY ' VENCIO UN FIJO Y NO HA RETIRADO '
      *           DISPLAY ' SERIAL LIC : ' LIC-CIC-ISER IN LIC
      *           DISPLAY ' CIC OPD    : ' OPD-CIC-IOPD IN OPD
      *           DISPLAY ' SERIAL OPD : ' OPD-CIC-ISER IN OPD.
       NXT-MAIN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-LIC.
           IF FIO-STAT-OKS
              GO TO LUP-MAIN.
       FIN-MAIN.
           DISPLAY ' '.
           DISPLAY ' FIN PROCESO '.
           DISPLAY ' '.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-LIC.
           PERFORM DAP-FIO-LIQ.
           PERFORM DAP-FIO-OPD.
           PERFORM FIN-PRO-END.

       FIN-PRO-END SECTION.
       INI-FIN-PRO-END.
           DISPLAY ' '.
           DISPLAY ' ESTADISTICAS DAPPBLIC '.
           DISPLAY ' ===================== '.
           DISPLAY ' '.
           DISPLAY ' REGISTROS LIC LEIDOS         : ' WSS-LEI-LIC.
           DISPLAY ' REGISTROS LIQ ACTIVADOS      : ' WSS-GRA-LIQ.
           DISPLAY ' REGISTROS OPD ACTUALIZADOS   : ' WSS-GRA-OPD.
           DISPLAY '  '.

      *     IF WSS-IND-MAL = 'S'
      *         DISPLAY ' '
      *         DISPLAY ' PROCESO TERMINADO ANORMALMENTE'
      *         DISPLAY ' LIQUIDACION(ES) DE RENOVABLES '
      *         DISPLAY ' NO RETIRADAS '
      *         PERFORM GNS-PRO-ABT
      *     ELSE
               PERFORM GNS-PRO-END.
       FIN-FIN-PRO-END.
           EXIT.

       BUS-LIC-OPD SECTION.
       INI-BUS-LIC-OPD.
           MOVE LIC-CIC-IOPD IN LIC TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT ( FIO-STAT-OKS )
      *RVM-CST-INI 29-JUL-1997
      *         DISPLAY ' '
      *         DISPLAY ' PROCESO CANCELADO '
      *         DISPLAY ' NO EXISTE NRO. OPERACION EN LIC: '
      *                   LIC-CIC-ISER IN LIC ' FS: ' FIO-STAT
      *         PERFORM GNS-PRO-ABT.
               MOVE LIC-SGV-TPAG IN LIC TO WSS-VAL-DISP IN WSS-VARI
               DISPLAY ' '
                  ' ************ ATENCION ************ '
                  ' NO EXISTE NRO. OPERACION LIC EN OPD' 
                  '     OPERACION EN LIC                : '
                                              LIC-CIC-ISER IN LIC
                  '     FECHA LIQUIDACION               : '
                                              LIC-FEC-FLIQ IN LIC
                  '     FECHA LIQUIDACION               : '
                                              LIC-FEC-FLIQ IN LIC
                  '     ESTADO LIQUIDACION              : '
                                              LIC-COD-STAT IN LIC
                  '     OFICINA LIQUIDACION SAFE        : '
                                              LIC-COD-OFIC IN LIC
                  '     VALOR DE CAMBIO (MONEDA)        : '
                                              LIC-COD-VCAM IN LIC
                  '     GLOSA DISPONIBLE                : '
                                              LIC-GLS-DISP IN LIC
                  '     LIC-SGV-TPAG                    : '
                                              WSS-VAL-DISP IN WSS-VARI
                  '     COD. INT. COMP. OPD (CLAVE)     : '
                                              LIC-CIC-IOPD IN LIC
                  '     RESERVADO SAFE                  : '
                                              LIC-GLS-SAFE IN LIC
                  '     STATUS                          : '
                                              FIO-STAT
                  ' '.
      *RVM-CST-FIN 29-JUL-1997
       FIN-BUS-LIC-OPD.
           EXIT.

       ACT-LIQ-OPD SECTION.
       INI-ACT-LIQ-OPD.
       ACT-OPD.
      *     MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM DAP-FIO-OPD.
      *     IF NOT FIO-STAT-OKS
      *        DISPLAY ' '
      *        DISPLAY ' PROCESO CANCELADO '
      *        DISPLAY ' NO EXISTE OPERACION YA SELECCIONADA: '
      *                  OPD-KEY-IOPD IN OPD ' FS: ' FIO-STAT
      *        PERFORM GNS-PRO-ABT.

      *     MOVE FIO-MOD TO FIO-CMD.
      *     PERFORM DAP-FIO-OPD.
      *     IF NOT FIO-STAT-OKS
      *         DISPLAY ' '
      *         DISPLAY ' PROCESO CANCELADO '
      *         DISPLAY ' ERROR EN ACTUALIZACION REG. OPD: '
      *                   OPD-KEY-IOPD IN OPD ' FS: ' FIO-STAT
      *         PERFORM GNS-PRO-ABT.
      *     ADD 1 TO WSS-GRA-OPD.

       ACT-LIQ.
      *JUS-INI 24-OCT-1995
           IF OPD-COD-STAT IN OPD = 'G' AND
              OPD-VAL-SCAP IN OPD NOT > ZEROES
              PERFORM VER-CON-STAT.
      *JUS-FIN
           IF OPD-COD-STAT IN OPD = 'G'
      * LIQUIDACION DE INTERESES
               IF NOT ( OPD-COD-RENO IN OPD = SCR-SON-S AND
                        ( OPD-CAI-GREN IN OPD > SPACES OR
                          OPD-IIC-GREN IN OPD > ZEROES ) )
                    DISPLAY ' '
                    DISPLAY ' PROCESO CANCELADO '
                    DISPLAY ' ERROR: OPERACION VIGENTE Y '
                            ' ( NO RENOVABLE O SIN INDICADOR'
                            ' DE RENOVACION ) '
                              OPD-KEY-IOPD IN OPD ' FS: ' FIO-STAT
                    PERFORM GNS-PRO-ABT
                ELSE
                   MOVE OPD-CIC-GREN IN OPD TO WSS-CIC-IOPD
           ELSE
               MOVE OPD-CIC-IOPD IN OPD TO WSS-CIC-IOPD.


           MOVE WSS-CIC-IOPD TO LIQ-CIC-IOPD IN LIQ.
           MOVE 1 TO LIQ-NUM-ILIQ IN LIQ.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' NO EXISTE REG. LIQ PARA ACT-LIQ: '
                         LIQ-KEY-ILIQ IN LIQ ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
           MOVE LIQ-KEY-ILIQ IN LIQ TO BKP-KEY-ILIQ.

       LUP-ACT-LIQ.
      *     IF LIQ-COD-STAT IN LIQ = 'A'
      *        GO TO NXT-LUP-ACT-LIQ.


           MOVE 'A'                 TO LIQ-COD-STAT IN LIQ.
           MOVE LIC-FEC-FLIQ IN LIC TO LIQ-FEC-FREA IN LIQ
                                       LIQ-FEC-FCMP IN LIQ.
           MOVE LIC-COD-OFIC IN LIC TO LIQ-COD-OFIC IN LIQ.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR EN ACTUALIZACION REG. LIQ: '
                         LIQ-KEY-ILIQ IN LIQ ' FS: ' FIO-STAT
               MOVE 1 TO WSS-STAT-ACT
               MOVE FIO-ULK-REC TO FIO-CMND
               PERFORM DAP-FIO-LIQ
               PERFORM GNS-PRO-ABT.
           ADD 1 TO WSS-GRA-LIQ.

       NXT-LUP-ACT-LIQ.
           MOVE BKP-KEY-ILIQ TO LIQ-KEY-ILIQ IN LIQ.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR AL LEER LIQUIDACION YA EXISTENTE: '
                         LIQ-KEY-ILIQ IN LIQ ' FS: ' FIO-STAT
              PERFORM GNS-PRO-ABT.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           IF ( FIO-STAT-OKS AND
                LIQ-CIC-IOPD IN LIQ = WSS-CIC-IOPD )
                MOVE LIQ-KEY-ILIQ IN LIQ TO BKP-KEY-ILIQ
              GO TO LUP-ACT-LIQ.
       FIN-ACT-LIQ-OPD.
           EXIT.
      *JUS-INI 24-OCT-1995
       VER-CON-STAT SECTION.
       INI-VER-CON-STAT.
           MOVE OPD-VAL-SCAP IN OPD TO WSS-VAL-EDIT IN WSS-VARI.
           DISPLAY ' '.
           DISPLAY ' ATENCION: OPERACION: ' OPD-CIC-IOPD IN OPD.
           DISPLAY ' ESTADO OPERACION   : ' OPD-COD-STAT IN OPD.
           DISPLAY ' Y SALDO            : ' WSS-VAL-EDIT IN WSS-VARI.
      *
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           MOVE 'L'   TO OPD-COD-STAT IN OPD.
           MOVE 'CAN' TO OPD-COD-SCTB IN OPD.
           IF NOT FIO-STAT-OKS
               DISPLAY ' Y HUBO ERROR AL CORREGIR, PROCESO CANCELADO'
               PERFORM GNS-PRO-ABT
           ELSE
              MOVE FIO-MOD TO FIO-CMND
              PERFORM DAP-FIO-OPD
              IF NOT FIO-STAT-OKS
                 DISPLAY ' Y HUBO ERROR AL CORREGIR, PROCESO CANCELADO'
                 PERFORM GNS-PRO-ABT.
           DISPLAY ' OPERACION FUE CORREGIDA'.
       FIN-VER-CON-STAT.
           EXIT.
      *JUS-FIN
      *JUS-INI 27-OCT-1995
      * Proceso de Reverso Automatico
       PRO-REV-LIQ SECTION.
       INI-PRO-REV-LIQ.
           IF NOT (LIC-COD-STAT IN LIC = 'S' AND
                   LIC-SGV-TPAG IN LIC > ZEROES AND
                   LIC-FEC-FLIQ IN LIC = WSS-FEC-FPRO IN WSS-VARI)
              GO TO FIN-PRO-REV-LIQ.
           DISPLAY 'SE VA A ANALIZAR CONSISTENCIA PARA REVERSAR OPD: '
                    LIC-CIC-IOPD IN LIC.
      *
           PERFORM BUS-LIC-OPD.
      *RVM-BEE-INI 29-JUL-1997
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-REV-LIQ.
      *RVM-BEE-FIN 29-JUL-1997

           IF OPD-COD-STAT IN OPD = 'G' AND
              OPD-VAL-CAPT IN OPD NOT > ZEROES
              PERFORM VER-CON-STAT.
      *
           IF OPD-COD-STAT IN OPD = 'L' AND
              OPD-VAL-CAPT IN OPD > ZEROES
              MOVE OPD-VAL-CAPT IN OPD TO WSS-VAL-EDIT IN WSS-VARI
              DISPLAY ' '
              DISPLAY 'INCONSISTENCIA OPD: ' OPD-CIC-IOPD IN OPD
              DISPLAY 'ESTADO OPD        : ' OPD-COD-STAT IN OPD
              DISPLAY 'SALDO             : ' WSS-VAL-EDIT IN WSS-VARI
              GO TO FIN-PRO-REV-LIQ.
      *
            IF OPD-COD-STAT IN OPD = 'G'
               MOVE 1                   TO LIQ-NUM-ILIQ IN LIQ
               MOVE OPD-CIC-GREN IN OPD TO LIQ-CIC-IOPD IN LIQ
               MOVE FIO-GET-KEY         TO FIO-CMND
               PERFORM DAP-FIO-LIQ
               IF NOT FIO-STAT-OKS
                  DISPLAY 'ERROR: NO EXISTE LIQ ASOCIADA A OPD (ST:G): '
                           OPD-CIC-GREN IN OPD
                  GO TO FIN-PRO-REV-LIQ.
      *
            IF OPD-COD-STAT IN OPD = 'L'
               MOVE 1                   TO LIQ-NUM-ILIQ IN LIQ
               MOVE OPD-CIC-IOPD IN OPD TO LIQ-CIC-IOPD IN LIQ
               MOVE FIO-GET-KEY         TO FIO-CMND
               PERFORM DAP-FIO-LIQ
               IF NOT FIO-STAT-OKS
                  DISPLAY 'ERROR: NO EXISTE LIQ ASOCIADA A OPD (ST:L): '
                           OPD-CIC-IOPD IN OPD
                  GO TO FIN-PRO-REV-LIQ.
      *
           DISPLAY 'SE VA A REVERSAR LA OPD: ' LIC-CIC-IOPD IN LIC.
      *
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF FIO-STAT-OKS
              MOVE 'G'                 TO OPD-COD-STAT IN OPD
              MOVE 'VIG'               TO OPD-COD-SCTB IN OPD
              ADD LIQ-SGV-TPAG IN LIQ  TO OPD-VAL-CAPT IN OPD
              PERFORM REV-PESO
              MOVE OPD-VAL-CAPT IN OPD TO OPD-VAL-SCAP IN OPD
              MOVE ZEROES              TO OPD-SGV-MPRE IN OPD
              MOVE FIO-MOD TO FIO-CMND
              PERFORM DAP-FIO-OPD
              IF NOT FIO-STAT-OKS
                 DISPLAY 'ERROR AL REVERSAR OPD (MOD)'
                 MOVE FIO-BCK-OUT TO FIO-CMND
                 PERFORM DAP-FIO-OPD
                 GO TO FIN-PRO-REV-LIQ
              ELSE
                 NEXT SENTENCE
           ELSE
              DISPLAY 'ERROR AL REVERSAR OPD (UPD)'
              GO TO FIN-PRO-REV-LIQ.
      *
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR LIQ'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-LIQ
              GO TO FIN-PRO-REV-LIQ.

           MOVE LIQ-CIC-IOPD IN LIQ TO CAD-CIC-IOPD IN CAD.
           MOVE LIQ-NUM-ILIQ IN LIQ TO CAD-NUM-ILIQ IN CAD.
           MOVE 1                   TO CAD-NUM-ICAD IN CAD.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-CAD.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR CAD'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-CAD
              GO TO FIN-PRO-REV-LIQ.

           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-CAD.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR CAD'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-CAD
              GO TO FIN-PRO-REV-LIQ.

           MOVE OPD-CIC-ISER IN OPD TO LIC-CIC-ISER IN LIC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-LIC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR LIC'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-LIC
              GO TO FIN-PRO-REV-LIQ.

           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-LIC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR LIC'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-LIC
              GO TO FIN-PRO-REV-LIQ.

           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR LIQ'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-LIQ
              GO TO FIN-PRO-REV-LIQ.
      *
           MOVE OPD-CIC-IOPD IN OPD TO VEN-CIC-IOPD IN VEN.
           MOVE 1                   TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR VEN'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-VEN
              GO TO FIN-PRO-REV-LIQ.

           MOVE 'N'                 TO VEN-COD-STAT IN VEN.
           MOVE ZEROES              TO VEN-VAL-CAPI IN VEN.
           MOVE ZEROES              TO VEN-VAL-SCAP IN VEN.
           MOVE ZEROES              TO VEN-VAL-SFIN IN VEN.
           MOVE ZEROES              TO VEN-VAL-FINA IN VEN.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO VEN-FEC-FVEN IN VEN.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL REVERSAR VEN'
              MOVE FIO-BCK-OUT TO FIO-CMND
              PERFORM DAP-FIO-VEN
              GO TO FIN-PRO-REV-LIQ.
      *
           DISPLAY 'OPD REVERSADA OK!'.
       FIN-PRO-REV-LIQ.
           EXIT.
       REV-PESO SECTION.
       INI-REV-PESO.
           IF LIQ-COD-LIQU IN LIQ = 'LP'
              ADD LIQ-SGV-PESO IN LIQ  TO OPD-VAL-IPES IN OPD.
       REV-REV-PESO.
           EXIT.
       COPY DAPBFCAD.
       COPY DAPBFVEN.
      *JUS-FIN
       COPY DAPBFOPD.
       COPY DAPBFLIC.
       COPY DAPBFLIQ.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBVTAB.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
      *<<<< EOF
      *Entidades  de GNS
       COPY GNSBGABT.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.

