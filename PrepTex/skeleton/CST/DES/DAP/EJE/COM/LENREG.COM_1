$ P1:
$	IF P1 .NES. "" THEN GOTO P2
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P1 "Registro ? "
$	GOTO P1
$ P2:
$	IF P2 .NES. "" THEN GOTO MAIN
$	IF F$MODE() .EQS. "BATCH" THEN GOTO MAIN
$	INQUIRE P2 "String de Termino ? "
$ MAIN:
$	ON WARNING   THEN GOTO FIN
$	ON CONTROL_Y THEN GOTO FIN
$	OPEN/READ FILE_INPUT 'P1'
$	ON WARNING   THEN GOTO CLO
$	ON CONTROL_Y THEN GOTO CLO
$	CNT = 0
$	C01 = 0
$ LEE:
$	READ/END_OF_FILE=CLO FILE_INPUT REG
$	IF P2 .NES. "" THEN -
			IF F$LOCATE(P2,REG) .NES. F$LENGTH(REG) THEN GOTO CLO
$	IF F$EXTRACT(6,1,REG) .EQS. "*" THEN GOTO LEE
$!	CONSIDERAR REDEFINES EN UN SOLO NIVEL
$	IF F$LOCATE("REDEFINE",REG) .NES. F$LENGTH(REG) THEN GOTO LEE
$!
$	IF F$EXTRACT(7,3,REG) .EQS. "01 " THEN C01 = C01 + 1
$	IF C01 .GT. 1 THEN GOTO CLO
$	PIC = F$ELEMENT(1,"(",REG)
$	IF PIC .EQS. "(" THEN GOTO LEE
$	ENT = F$ELEMENT(0,")",PIC)
$	IF ENT .EQS. ")" THEN GOTO LEE
$
$	DEC = 0
$	PDC = F$ELEMENT(2,"(",REG)
$	IF PDC .EQS. "(" THEN GOTO CNT
$	PDC = F$ELEMENT(0,")",PDC)
$	IF PDC .EQS. ")" THEN GOTO CNT
$	DEC = PDC
$ CNT:
$	COMP3 = 0
$	COMP  = 0
$	CEN = 0
$	IF F$LOCATE("COMP-3",REG) .NES. F$LENGTH(REG) THEN COMP3 = 1
$	IF F$LOCATE("COMP ",REG)  .NES. F$LENGTH(REG) THEN COMP  = 1
$	CEN = CEN + ENT + DEC
$	IF COMP  THEN CEN = ( CEN + 1 ) / 2
$	IF COMP3 THEN CEN = ( CEN / 2 ) + 1
$	POC = F$LOCATE("OCCUR",REG)
$	IF POC .EQS. F$LENGTH(REG) THEN GOTO SUM
$	POC = F$EXTRAC(POC,F$LENGTH(REG),REG)
$	OCC = F$ELEMENT(1," ",POC)
$	IF OCC .EQS. " " THEN GOTO SUM
$	OCC = F$ELEMENT(0," ",OCC)
$	IF OCC .EQS. " " THEN GOTO SUM
$	CEN = CEN * OCC
$ SUM:
$	write sys$output cen
$	CNT = CNT + CEN
$	GOTO LEE
$ CLO:
$	CLOSE FILE_INPUT
$	WRITE SYS$OUTPUT CNT
$ FIN:
$	EXIT
