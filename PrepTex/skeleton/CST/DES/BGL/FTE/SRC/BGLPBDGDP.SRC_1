
       IDENTIFICATION DIVISION.                                  
      *=======================                                          
       PROGRAM-ID.  GARPBGDP.                                     
       AUTHOR.      M. ALBORNOZ D.                               
      * REMARKS.     MODIFICADO POR F. LIZANA B.
      **************************************************************    
      *  OBJETIVO :                                                *    
      *           CALCULAR VALORES Y FECHA DE PROXIMA DEPRECIACION *    
      *           SI CORRESPONDE, ACTUALIZANDO LOS ARCHIVOS        *    
      *           INVOLUCRADOS                                     *    
      *                                                            *    
      ********************* ARCHIVOS  USADOS************************    
      *         INPUT :                                            *    
      *                                                            *    
      *         I/O   : GDP, GDG, GVT, GTP, GMO                    *    
      *                                                            *    
      **************************************************************    
                                                                        
       ENVIRONMENT DIVISION.                                     
      *=====================                                          
       CONFIGURATION SECTION.                                    
      *----------------------                                           
       SPECIAL-NAMES.                                            
             DECIMAL-POINT IS COMMA.
                                                                        
       DATA DIVISION.                                            
      *==============                                                    
                                                                        
       WORKING-STORAGE SECTION.                                  
      *========================                                         
                                                                        
       77    WK-RESTO                  PIC 9(04).                
       77    WK-CUOC                   PIC 9(03).                
       77    WK-FACTOR                 PIC 9(04)V9999.           
       77    WK-FAC-DOB                PIC 9(04)V9999.           
       77    WK-DIVISOR                PIC 9(08).                
       77    WK-COD-TSN                PIC 9(03).                
       77    WK-COD-TSN-NW             PIC 9(03).                
       77    WK-CONT-GDP-LEIDOS        PIC 9(06)             VALUE 0.   
       77    WK-CONT-GDG-LEIDOS        PIC 9(06)             VALUE 0.   
       77    WK-CONT-GVT-LEIDOS        PIC 9(06)             VALUE 0.   
       77    WK-CONT-GTP-LEIDOS        PIC 9(06)             VALUE 0.   
       77    WK-CONT-MOV-GDP           PIC 9(05)             VALUE 0.   
       77    WK-CONT-MOV-GDG           PIC 9(05)             VALUE 0.   
       77    WK-CONT-MOV-GVT           PIC 9(05)             VALUE 0.   
       77    WK-CONT-MOV-GTP           PIC 9(05)             VALUE 0.   
       77    WK-CONT-MOV-GMO           PIC 9(05)             VALUE 0.   
       77    WK-NUM-CORREL-MVTO        PIC 9(03)             VALUE 0.   
       77    WK-FA-PXM-DPN             PIC 9(04)             VALUE 0.   
       77    WK-FM-PXM-DPN             PIC 9(02)             VALUE 0.   
       77    WK-VAL-ULT-DPN            PIC S9(11)V9999       VALUE +0.  
       77    WK-VAL-PXM-DPN            PIC S9(11)V9999       VALUE +0.  
       77    WK-VAL-LIQ-TSN            PIC S9(11)V9999       VALUE +0.  
       77    WK-VAL-PON                PIC S9(11)V9999       VALUE +0.  
                                                                        
       01   LINEA-DETALLE.                                       
            03 LD-RUT            PIC X(08).                      
            03 FILLER            PIC X(01)  VALUE '-'.           
            03 LD-VRF            PIC X(01).                      
            03 FILLER            PIC X(01)  VALUE SPACES.        
            03 LD-COD-MON        PIC X(02).                      
            03 FILLER            PIC X(01)  VALUE SPACES.        
            03 LD-COD-MOV        PIC X(01).                      
            03 FILLER            PIC X(01)  VALUE SPACES.        
            03 LD-FD-MOV         PIC X(02).                      
            03 LD-FM-MOV         PIC X(02).                      
            03 LD-FA-MOV         PIC X(04).                      
            03 FILLER            PIC X(01)  VALUE SPACES.        
            03 LD-COD-OFI        PIC X(03).                      
            03 FILLER            PIC X(01)  VALUE SPACES.        
            03 LD-FD-EMI         PIC X(02).                      
            03 LD-FM-EMI         PIC X(02).                      
            03 LD-FA-EMI         PIC X(04).                      
            03 FILLER            PIC X(01)  VALUE SPACES.        
            03 LD-FD-ULT-VTO     PIC X(02).                      
            03 LD-FM-ULT-VTO     PIC X(02).                      
            03 LD-FA-ULT-VTO     PIC X(04).                      
            03 FILLER            PIC X(01)  VALUE SPACES.        
            03 LD-VAL            PIC ZZZZZ.ZZZ.ZZ9,999.          
            03 FILLER            PIC X(02)  VALUE SPACES.        
            03 LD-GLOSA          PIC X(50).                      
                                                                        
       01    WK-FEC.                                             
             06 A-FEC-W          PIC 9(4).                       
             06 M-FEC-W          PIC 9(2).                       
             06 D-FEC-W          PIC 9(2).                       
                                                                        
       01    WK-AUX-CODIG-C.                                     
             03 WK-MES-A-SUM           PIC 9(02).                
             03 FILLER                 PIC X(21).                
                                                                        
       01    MESES                     PIC X(24) VALUE                  
                                       '312831303130313130313031'.      
       01    ARR-MES  REDEFINES MESES.                           
             03 WK-MESES OCCURS 12 TIMES.                        
                05 WK-DIAS-MES                 PIC X(02).
                                                                        
       01    WK-FECHA-SSAAMMDD.                                  
             03 WK-SS                          PIC 9(02).
             03 WK-AA                          PIC 9(02).                
             03 WK-MM                          PIC 9(02).                
             03 WK-DD                  PIC 9(02).                
                                                                        
       01  WSS-VARI.
*% IF PGM_DTC
      *     03  WSS-NUM-DBIN                        PIC 9(03).
*% END
           03 WSS-COD-PESO                          PIC X(06).

       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWGHOY.
       COPY GNSWGFEC.
       COPY GNSWGSYS.
       COPY GNSWGPES.
       COPY GNSWGPDB.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSBRERR.

*% IF PGM_DTC
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GTP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GMO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
      * REQA de Respaldo
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RES-REQA==.
*% END

       COPY GARBRGDP.
       COPY GARBRGDG.
       COPY GARBRGVT.
       COPY GARBRGTP.
       COPY GARBRGMO.

       PROCEDURE DIVISION.                                       
      *==================                                               
       MAIN SECTION.
       INI-MAIN.
*% IF PGM_DTC
       COPY GNSBGEDB.
*% END
           PERFORM INI.
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN.
       LUP-MAIN.
           PERFORM CHK-PNT.
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN.
                                                                        
*% IF PGM_DTC
           MOVE ADR-GDP-REQA TO ADR-RES-REQA.
*% END
           PERFORM B1-PROCESA-GDP.

*% IF PGM_DTC
       RBCK-MAIN.
           IF ERR-IND-RBCK IN ERR-VARI = ERR-RBCK-YES IN ERR-VARI
               PERFORM BCK-OUT
               IF NOT FIO-STAT-OKS                  
                   GO TO FIN-MAIN.
           MOVE ADR-RES-REQA TO ADR-GDP-REQA.
*% END
       NXT-MAIN.
           MOVE FIO-KDEF-S TO FIO-KDEF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GDP.
           IF FIO-STAT-OKS
              GO TO LUP-MAIN.    
       FIN-MAIN.
           PERFORM IMP-ESTD.
           PERFORM CLO-FIL.
       COPY GNSBGGBK.


       INI SECTION.
       INI-INI.
           MOVE 'GARPBGDP' TO FIO-PROG.
           MOVE ERR-TERR-LGC IN ERR-VARI TO ERR-COD-TERR IN ERR.
           MOVE 'GARPBGDP'               TO ERR-COD-ATRN IN ERR.
           MOVE ERR-TTRN-BAT IN ERR-VARI TO ERR-MSC-TTRN IN ERR.
           MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR.
           MOVE 'M' TO ERR-MSC-TACC IN ERR.
*% IF PGM_DTC
           PERFORM GNS-BUS-IDD.
*% END
           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO WK-DD.
           MOVE HOY-MHOY TO WK-MM.
           MOVE HOY-SHOY TO WK-SS.
           MOVE HOY-AHOY TO WK-AA.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GDP.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GTP.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.

      * BUS-TAB TABLA DE FECHAS DE PROCESO
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'GAR' TO FIO-SIST.
           MOVE 'TFP' TO TAB-COD-TTAB IN TAB.
           MOVE '09'  TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DAT1 TO FEC-FECH
               MOVE FEC-ITM1 TO FEC-SVLD
               MOVE FEC-ITM2 TO FEC-AVLD
               MOVE FEC-ITM3 TO FEC-MVLD
               MOVE FEC-ITM4 TO FEC-DVLD
               MOVE FEC-FVLD TO WK-FEC
           ELSE
              MOVE 'TABTFPNEX' TO ERR-COD-MENS IN ERR
              MOVE '09'        TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
              MOVE SPACES      TO ADR-REQA
*% ELSE
              MOVE TAB-COD-CTAB IN TAB TO ERR-GLS-VKEY IN ERR
*% END
              PERFORM PRO-ERR
              PERFORM PRG-ABT.

           DISPLAY 'FECHA PROCESO : ' WK-FEC.

      * Obtiene Codigo Valor de Cambio de Pesos Mediante CIC de la Moneda '0999'
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM PRG-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

      * POSICIONA GDP.

           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-GDP.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR
               PERFORM PRO-ERR
               PERFORM PRG-ABT
           ELSE
               MOVE ERR-SVRT-REC IN ERR-VARI TO ERR-IND-SVRT IN ERR.
       FIN-INI.
           EXIT.

       CHK-PNT SECTION.
       INI-CHK-PNT.
*% IF PGM_DTC
           MOVE SPACES TO ERR-COD-MENS IN ERR.
           MOVE SPACES TO ERR-IND-RBCK IN ERR-VARI.
      *     MOVE WSS-NUM-DBIN IN WSS-VARI TO ADR-DBID IN ADR-REQA.
           MOVE FIO-CHK-PNT TO FIO-CMND.
           PERFORM GNS-FIO-DTC.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
*% END
       FIN-CHK-PNT.
           EXIT.

       B1-PROCESA-GDP SECTION.
       INI-B1-PROCESA-GDP.
      *-------------------                                                   
           ADD 1 TO WK-CONT-GDP-LEIDOS.                      
           IF ((GAR-IND-VIG-DPN = 'S') AND                             
               (GAR-FEC-PXM-DPN NOT > WK-FEC))                          
      * Estas variable WK- no se usan
      *         MOVE GAR-VAL-ULT-DPN  TO WK-VAL-ULT-DPN                  
      *         MOVE GAR-VAL-PXM-DPN  TO WK-VAL-PXM-DPN                  
               MOVE GAR-CLV-GDP-1    TO GAR-CLV-GDG-1
               MOVE FIO-GET-KEY TO FIO-CMND

               PERFORM GAR-FIO-GDG
               IF NOT FIO-STAT-OKS
                   MOVE 'GDG    NEX'    TO ERR-COD-MENS IN ERR
                   MOVE 'GAR-CLV-GDG-1' TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
                   MOVE ADR-GDG-REQA    TO ADR-REQA
*% ELSE
                   MOVE GAR-CLV-GDG-1 IN GDG TO ERR-GLS-VKEY IN ERR
*% END
                   PERFORM PRO-ERR
                   PERFORM PRG-ABT
                   GO TO FIN-B1-PROCESA-GDP
               ELSE
               ADD 1 TO WK-CONT-GDG-LEIDOS
               IF GAR-IND-CPL = 'S' AND                                 
                  (GAR-ES1 > '09' AND < '20')                           
                  MOVE FIO-GET-KEY-UPD TO FIO-CMND
                  PERFORM GAR-FIO-GDP
                  IF FIO-STAT-OKS
                      PERFORM B1-PROCESA2-GDP
                  ELSE
                      MOVE 'GDP    NEX'    TO ERR-COD-MENS IN ERR
                      MOVE 'GAR-CLV-GDP-1' TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
                      MOVE ADR-GDP-REQA    TO ADR-REQA
*% ELSE
                      MOVE GAR-CLV-GDP-1 IN GDP TO ERR-GLS-VKEY IN ERR
*% END
                      PERFORM PRO-ERR
                      PERFORM PRG-ABT.
       FIN-B1-PROCESA-GDP.
           EXIT.
                                                                        
       B1-PROCESA2-GDP SECTION.
       INI-B1-PROCESA2-GDP.
      *--------------------
           MOVE GAR-COD-TSN IN GDG TO WK-COD-TSN.             
           MOVE GAR-COD-TSN IN GDG TO WK-COD-TSN-NW.          
           ADD 1                   TO WK-COD-TSN-NW.               
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               MOVE 'GDG    NEX'             TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-GDG-1'          TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
               MOVE ADR-GDG-REQA             TO ADR-REQA
*% ELSE
               MOVE GAR-CLV-GDG-1 IN GDG     TO ERR-GLS-VKEY IN ERR
*% END
               PERFORM PRO-ERR
               PERFORM PRG-ABT
               GO TO FIN-B1-PROCESA2-GDP.

           MOVE WK-COD-TSN-NW TO GAR-COD-TSN IN GDG.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               MOVE 'GDG    NEX'             TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-GDG-1'          TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
               MOVE ADR-GDG-REQA             TO ADR-REQA
*% ELSE
               MOVE GAR-CLV-GDG-1 IN GDG     TO ERR-GLS-VKEY IN ERR
*% END
               PERFORM PRO-ERR
               PERFORM PRG-ABT
               GO TO FIN-B1-PROCESA2-GDP.
           ADD 1 TO WK-CONT-MOV-GDG.                          

           PERFORM B1-COMPROC-GTP.                               

           MOVE GAR-NUM-SIS IN GDP TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GDP TO GAR-COD-CRR IN GVT.
           MOVE WK-COD-TSN         TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               MOVE 'GVT    NEX'             TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-GVT-1'          TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
               MOVE ADR-GVT-REQA             TO ADR-REQA
*% ELSE
               MOVE GAR-CLV-GVT-1 IN GVT     TO ERR-GLS-VKEY IN ERR
*% END
               PERFORM PRO-ERR
               PERFORM PRG-ABT
               GO TO FIN-B1-PROCESA2-GDP.
           ADD 1 TO WK-CONT-GVT-LEIDOS.                       
           MOVE GAR-VAL-LIQ-TSN  TO WK-VAL-LIQ-TSN.              
           PERFORM B1-2-PROCESA-GVT.                             
           IF NOT FIO-STAT-OKS
               MOVE 'GVT    MOD'             TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-GVT-1'          TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
               MOVE ADR-GVT-REQA             TO ADR-REQA
*% ELSE
               MOVE GAR-CLV-GVT-1 IN GVT     TO ERR-GLS-VKEY IN ERR
*% END
               PERFORM PRO-ERR
               PERFORM PRG-ABT
               GO TO FIN-B1-PROCESA2-GDP.
           ADD 1 TO WK-CONT-MOV-GVT.                          

           MOVE GAR-FEC-PXM-DPN  TO GAR-FEC-ULT-DPN.             
           MOVE GAR-VAL-PXM-DPN  TO GAR-VAL-ULT-DPN.             
           IF ((GAR-CTD-PDO-DPN = GAR-PDO-ULT-DPN) OR                   
              (GAR-VAL-LIQ-TSN = 1))                                    
              MOVE 'N' TO GAR-IND-VIG-DPN IN GDP                   
              MOVE 0   TO GAR-VAL-PXM-DPN                                 
              ADD  1   TO GAR-PDO-ULT-DPN                                 
           ELSE                                                         
              ADD 1    TO GAR-PDO-ULT-DPN                                  
              PERFORM B1-4-CALC-PROX-DEP.                        

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GDP.                             
           IF NOT FIO-STAT-OKS
               MOVE 'GDP    MOD'             TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-GDP-1'          TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
               MOVE ADR-GDP-REQA             TO ADR-REQA
*% ELSE
               MOVE GAR-CLV-GDP-1 IN GDP     TO ERR-GLS-VKEY IN ERR
*% END
               PERFORM PRO-ERR
               PERFORM PRG-ABT
               GO TO FIN-B1-PROCESA2-GDP.

           ADD 1 TO WK-CONT-MOV-GDP.                          
                                                                        
           PERFORM B1-ACTUALIZA-GMO.                             
      *   ACTUALIZAR  GVT Y GTP Y GDP GENERANDO UN REGISTRO GMO         
       FIN-B1-PROCESA2-GDP.
           EXIT.
                                                                        
       B1-COMPROC-GTP SECTION.
       INI-B1-COMPROC-GTP.
      *-------------------
           IF GAR-IND-EXI-GTP = 'S'
               MOVE GAR-NUM-SIS IN GDP TO GAR-NUM-SIS IN GTP
               MOVE GAR-COD-CRR IN GDP TO GAR-COD-CRR IN GTP
               MOVE WK-COD-TSN         TO GAR-COD-TSN IN GTP
               MOVE ZEROES             TO GAR-COD-ITM-TSN IN GTP
               MOVE FIO-GET-NLS TO FIO-CMND
               PERFORM GAR-FIO-GTP
               PERFORM B1-1-PROCESA-GTP UNTIL NOT FIO-STAT-OKS.
       FIN-B1-COMPROC-GTP.
           EXIT.                                                      
                                                                        
       REDONDEO SECTION.
       INI-REDONDEO.
      *-------------
            IF GAR-VCB IN GDG = WSS-COD-PESO IN WSS-VARI
               ADD 0 TO GAR-VAL-PXM-DPN ROUNDED.                 
       FIN-REDONDEO.
           EXIT.                                                      
                                                                        
       B1-2-PROCESA-GVT SECTION.                                         
       INI-B1-2-PROCESA-GVT.
      *---------------------
      *                                                                 
      *       QUE SIGNIFICA SOBRE UN NUEVO REGISTRO                     
      *                                                                 
           IF GAR-VAL-PXM-DPN NOT < WK-VAL-LIQ-TSN                      
              SUBTRACT 1 FROM GAR-VAL-LIQ-TSN GIVING GAR-VAL-PXM-DPN    
              PERFORM REDONDEO.                                  
           COMPUTE GAR-VAL-LIQ-TSN = GAR-VAL-LIQ-TSN - GAR-VAL-PXM-DPN. 
           MOVE WK-COD-TSN-NW     TO GAR-COD-TSN IN GVT. 
           MOVE GAR-VAL-PON       TO WK-VAL-PON.                 
           COMPUTE GAR-VAL-PON     IN GVT ROUNDED =
                   GAR-VAL-LIQ-TSN IN GVT *
                   GAR-PCT-PON     IN GVT / 100.

           IF GAR-VCB IN GDG = WSS-COD-PESO IN WSS-VARI
               MOVE GAR-VAL-PON TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO GAR-VAL-PON.

           MOVE WK-FECHA-SSAAMMDD TO GVT-FEC-ING-SIS  IN GVT. 
           MOVE 0                 TO GVT-FEC-ULT-ACT  IN GVT. 
           MOVE 0                 TO GVT-HOR-ACT      IN GVT. 
           MOVE SPACES            TO GVT-TML-ACT      IN GVT. 
           MOVE SPACES            TO GVT-IDN-USU-ACT  IN GVT. 
           MOVE 'PBGDP'           TO GVT-IDN-USU-ING  IN GVT. 
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
       FIN-B1-2-PROCESA-GVT.
           EXIT.
                                                                        
       B1-4-CALC-PROX-DEP SECTION.                                       
       INI-B1-4-CALC-PROX-DEP.
      *-----------------------
           IF GAR-TIP-DPN = 'A'                                         
              COMPUTE WK-DIVISOR = GAR-CTD-PDO-DPN + 1                  
              COMPUTE WK-DIVISOR = ( GAR-CTD-PDO-DPN * WK-DIVISOR ) / 2 
              SUBTRACT GAR-PDO-ULT-DPN FROM GAR-CTD-PDO-DPN             
                       GIVING WK-FACTOR                                 
              COMPUTE WK-FACTOR = WK-FACTOR / WK-DIVISOR                
              COMPUTE GAR-VAL-PXM-DPN = WK-FACTOR * GAR-VAL-ORI-TSN     
              PERFORM REDONDEO                                          
           ELSE                                                         
              IF GAR-TIP-DPN = 'D'                                      
                 DIVIDE 2 BY GAR-CTD-PDO-DPN GIVING WK-FAC-DOB          
              COMPUTE GAR-VAL-PXM-DPN = GAR-VAL-LIQ-TSN * WK-FAC-DOB    
              PERFORM REDONDEO.                                  

      * BUS-TAB  Busca Nro meses a sumar
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'GAR' TO FIO-SIST.
           MOVE 'PDP' TO TAB-COD-TTAB IN TAB.
           MOVE GAR-TIP-PDO-DPN  TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DAT1 TO WK-AUX-CODIG-C
           ELSE
               MOVE 'TABPDPNEX'              TO ERR-COD-MENS IN ERR
               MOVE 'GAR-TIP-PDO-DPN'        TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
               MOVE SPACES                   TO ADR-REQA
*% ELSE
               MOVE TAB-COD-CTAB IN TAB      TO ERR-GLS-VKEY IN ERR
*% END
               PERFORM PRO-ERR
               PERFORM PRG-ABT
               GO TO FIN-B1-4-CALC-PROX-DEP.

           MOVE GAR-FD-ULT-DPN TO FEC-ITM1.
           MOVE GAR-FM-ULT-DPN TO FEC-ITM2.
           MOVE GAR-FA-ULT-DPN TO FEC-FANO.
           MOVE FEC-SVLD       TO FEC-ITM3.
           MOVE FEC-AVLD       TO FEC-ITM4.

           MOVE WK-MES-A-SUM TO FEC-NMES.
           MOVE FEC-SUM-MES  TO FEC-CMND.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ERROR AL OBTENER FECHA PROXIMA DEPRECIACION '
                  FEC-FVLD ' ' GAR-NUM-SIS IN GDP ' ' GAR-COD-CRR IN GDP
       COPY GNSBGGBK.

           MOVE FEC-FANO TO GAR-FA-PXM-DPN IN GDP.
           MOVE FEC-FMES TO GAR-FM-PXM-DPN IN GDP.
           MOVE FEC-FDIA TO GAR-FD-PXM-DPN IN GDP.

      *---------------------
      *     COMPUTE WK-FM-PXM-DPN = GAR-FM-ULT-DPN + WK-MES-A-SUM.
      
      *     IF WK-FM-PXM-DPN > 12                                        
      *        DIVIDE  WK-FM-PXM-DPN BY 12 GIVING WK-CUOC                
      *                REMAINDER WK-RESTO                                
      *     ELSE                                                         
      *         MOVE 0 TO WK-CUOC                                        
      *         MOVE WK-FM-PXM-DPN TO WK-RESTO.                   
      *     COMPUTE WK-FA-PXM-DPN = GAR-FA-PXM-DPN + WK-CUOC.     
      *     COMPUTE WK-FM-PXM-DPN = WK-RESTO.                     
      *     DIVIDE  WK-FA-PXM-DPN BY 4 GIVING WK-CUOC                    
      *             REMAINDER WK-RESTO.                           
      *     IF WK-RESTO = 0                                              
      *        MOVE '29' TO WK-DIAS-MES (2)                              
      *     ELSE                                                         
      *        MOVE '28' TO WK-DIAS-MES (2).                      
      *     MOVE WK-DIAS-MES (WK-FM-PXM-DPN) TO GAR-FD-PXM-DPN.   
      *     MOVE WK-FM-PXM-DPN               TO GAR-FM-PXM-DPN.   
      *     MOVE WK-FA-PXM-DPN               TO GAR-FA-PXM-DPN.   
       FIN-B1-4-CALC-PROX-DEP.
           EXIT.
                                                                        
       B1-1-PROCESA-GTP SECTION.
       INI-B1-1-PROCESA-GTP.
      *---------------------
           MOVE WK-COD-TSN-NW     TO GAR-COD-TSN      IN GTP. 
           MOVE WK-FECHA-SSAAMMDD TO GTP-FEC-ING-SIS  IN GTP. 
           MOVE 0                 TO GTP-FEC-ULT-ACT  IN GTP. 
           MOVE 0                 TO GTP-HOR-ACT      IN GTP. 
           MOVE SPACES            TO GTP-TML-ACT      IN GTP. 
           MOVE SPACES            TO GTP-IDN-USU-ACT  IN GTP. 
           MOVE 'PBGDP'           TO GTP-IDN-USU-ING  IN GTP. 
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GTP.
           IF NOT FIO-STAT-OKS
               MOVE 'GTP    PUT'             TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-GTP-1'          TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
               MOVE ADR-GTP-REQA             TO ADR-REQA
*% ELSE
               MOVE GAR-CLV-GTP-1 IN GTP     TO ERR-GLS-VKEY IN ERR
*% END
               PERFORM PRO-ERR
               PERFORM PRG-ABT
               GO TO FIN-B1-1-PROCESA-GTP.
           ADD 1 TO WK-CONT-MOV-GTP.                          

           MOVE WK-COD-TSN        TO GAR-COD-TSN     IN GTP. 
           ADD 1                  TO GAR-COD-ITM-TSN IN GTP.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GTP.
           IF FIO-STAT-OKS
              ADD 1 TO WK-CONT-GTP-LEIDOS.                       
       FIN-B1-1-PROCESA-GTP.
           EXIT.
                                                                        
       B1-ACTUALIZA-GMO SECTION.
       INI-B1-ACTUALIZA-GMO.
      *---------------------
           MOVE '2'                    TO GAR-COD-MOV     IN GMO.    
           MOVE 'GVT001'               TO GAR-COD-CPO     IN GMO.    
           MOVE WK-VAL-LIQ-TSN         TO GAR-VAL-CPO-ANT IN GMO.    
           MOVE GAR-VAL-LIQ-TSN        TO GAR-VAL-CPO-NVO IN GMO.    
           PERFORM CE-GENERAR-MVTO.
                                                                        
           MOVE 'GVT003'               TO GAR-COD-CPO     IN GMO.    
           MOVE WK-VAL-PON             TO GAR-VAL-CPO-ANT IN GMO.    
           MOVE GAR-VAL-PON            TO GAR-VAL-CPO-NVO IN GMO.    
           PERFORM CE-GENERAR-MVTO.                              
       FIN-B1-ACTUALIZA-GMO.
           EXIT.
                                                                        
       CE-GENERAR-MVTO SECTION.
       INI-CE-GENERAR-MVTO.
      *--------------------
           MOVE WK-FECHA-SSAAMMDD        TO GAR-FEC-MOV IN GMO.      
           PERFORM GET-FHOY.
           MOVE HOY-HHOY                 TO GAR-HOR-MOV IN GMO.      
           MOVE 'BGDP'                   TO GAR-TML-MOV IN GMO.      
           MOVE GAR-NUM-SIS IN GDP       TO GAR-NUM-SIS IN GMO.      
           MOVE GAR-COD-CRR IN GDP       TO GAR-COD-CRR IN GMO.      
           ADD  1                        TO WK-NUM-CORREL-MVTO.
           MOVE WK-NUM-CORREL-MVTO       TO GAR-CRR-MOV IN GMO.      
           MOVE 'S'                      TO GAR-IND-VAL IN GMO.      
           MOVE 'N'                      TO GAR-IND-CLV IN GMO.      
           MOVE SPACES                   TO GAR-COD-CLV-ANT IN GMO   
                                            GAR-COD-CLV-NVO IN GMO   
                                            GAR-GLS-CPO-ANT IN GMO   
                                            GAR-GLS-CPO-NVO IN GMO.  
           MOVE GAR-COD-OFI IN GDG       TO GAR-COD-OFI-MOV IN GMO.  
           MOVE 'PBGDP'                  TO GMO-IDN-USU-ING IN GMO.  
           MOVE WK-FECHA-SSAAMMDD        TO GMO-FEC-ING-SIS IN GMO.  

*% IF PGM_DTC
           MOVE ADR-GMO-REQA             TO ADR-REQA.
*% ELSE
           MOVE GAR-CLV-GMO-1 IN GMO     TO ERR-GLS-VKEY IN ERR.
*% END
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           IF NOT FIO-STAT-OKS
              MOVE 'GMO   PUT'              TO ERR-COD-MENS IN ERR
              MOVE 'GAR-CLV-GMO-1'          TO ERR-COD-CMPO IN ERR
*% IF PGM_DTC
              MOVE ADR-GMO-REQA TO ADR-REQA 
*% ELSE
              MOVE GAR-CLV-GMO-1 IN GMO     TO ERR-GLS-VKEY IN ERR
*% END
              PERFORM PRO-ERR
              PERFORM PRG-ABT
           ELSE                                                         
              ADD 1 TO WK-CONT-MOV-GMO.                          
       FIN-CE-GENERAR-MVTO.
           EXIT.
                                                                        
       IMP-ESTD SECTION.
       INI-IMP-ESTD.
      *-------------
           DISPLAY '==========================================='.
           DISPLAY '= REGS. LEIDOS GDP .................'               
                      WK-CONT-GDP-LEIDOS ' ='.                   
           DISPLAY '= REGS. LEIDOS GDG .................'               
                      WK-CONT-GDG-LEIDOS ' ='.                   
           DISPLAY '= REGS. LEIDOS GVT .................'               
                      WK-CONT-GVT-LEIDOS ' ='.                   
           DISPLAY '= REGS. LEIDOS GTP .................'               
                      WK-CONT-GTP-LEIDOS ' ='.                   
           DISPLAY '= MVTOS. GDP GRABADOS...............'               
                      WK-CONT-MOV-GDP   ' ='.                    
           DISPLAY '= MVTOS. GDG GRABADOS...............'               
                      WK-CONT-MOV-GDG   ' ='.                    
           DISPLAY '= MVTOS. GVT GRABADOS...............'               
                      WK-CONT-MOV-GVT   ' ='.                    
           DISPLAY '= MVTOS. GTP GRABADOS...............'               
                      WK-CONT-MOV-GTP   ' ='.                    
           DISPLAY '= MVTOS. GMO GRABADOS...............'               
                      WK-CONT-MOV-GMO   ' ='.                    
           DISPLAY '=                                         ='.
           DISPLAY '==========================================='.
       FIN-IMP-ESTD.
           EXIT.

       CLO-FIL SECTION.
       INI-CLO-FIL.
           MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-GDP.
           PERFORM GAR-FIO-GDG.
           PERFORM GAR-FIO-GVT.
           PERFORM GAR-FIO-GTP.
           PERFORM GAR-FIO-GMO.
      *    PERFORM GNS-FIO-ERR.
       FIN-CLO-FIL.
           EXIT.
     
       BCK-OUT SECTION.
       INI-BCK-OUT.
*% IF PGM_DTC
      *     MOVE WSS-NUM-DBIN IN WSS-VARI TO ADR-DBID IN ADR-REQA.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           PERFORM GNS-FIO-DTC.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
*% END
       FIN-BCK-OUT.
           EXIT.

       COPY GARBFGDP.
       COPY GARBFGDG.
       COPY GARBFGVT.
       COPY GARBFGTP.
       COPY GARBFGMO.

       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
      *COPY GNSBFERR.
       COPY GNSBGERR.
*% IF PGM_DTC
       COPY GNSBGDTC.
       COPY GNSBGIDD.
*% END
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGHOY.
       COPY GNSBGPES.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
