      ******************************************************************
      *RVM-BEE-INI 14-MAR-1997
      *
      *    PARA LA CARGA Y BUSQUEDA DE LOS CODIGO, SE DEBEN
      *    INCORPORAR LOS SIGUIENTES COPYS :
      *      WORKING      : GNSWGPTA
      *      PROCEDURE    : GNSBGPTA
      *
      *    PROCESO PARA CARGAR TABLA PTA + PROGRAMA EN MEMORIA
      *
      *    INPUT   :  PTA-SIST IN PTA-VARI
      *               PTA-NPGM IN PTA-VARI
      *    OUTPUT  :  PTA-CMSG IN PTA-VARI
      *               PTA-MENS IN PTA-VARI
      *    PERFORM :  GNS-CRG-PTA.
      *
      *
      *    PROCESO PARA BUSCAR TIPO DE OPERACION EN TABLA EN MEMORIA
      *
      *    INPUT   :  PTA-COOC IN PTA-VARI
      *    OUTPUT  :  PTA-CMSG IN PTA-VARI
      *               PTA-MENS IN PTA-VARI
      *    PERFORM :  GNS-BUS-PTA.
      *
      *
      *PROCESO PARA CARGAR TABLA PTA + PROGRAMA EN MEMORIA
       GNS-CRG-PTA SECTION.
       INI-GNS-CRG-PTA.
           MOVE SPACES TO PTA-TABL IN PTA-VARI
                          PTA-CMSG IN PTA-VARI
                          PTA-MENS IN PTA-VARI
                          PTA-CTAB IN PTA-VARI
                          PTA-COOC-AUX IN PTA-VARI.
           MOVE ZEROES TO PTA-INDI IN PTA-VARI.

      * INICIO DE CARGA TABLA PTA + PROGRAMA EN MEMORIA
           MOVE PTA-SIST TO TAB-COD-SIST FIO-SIST.
           MOVE 'PTA'    TO TAB-COD-TTAB IN TAB.
           MOVE PTA-NPGM TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR
              TAB-COD-TTAB IN TAB NOT = 'PTA'
               MOVE 'TABPTA NOHAY' TO PTA-CMSG IN PTA-VARI
               MOVE SPACES         TO PTA-MENS IN PTA-VARI
               GO TO FIN-GNS-CRG-PTA.
       LUP-GNS-CRG-PTA.
      *BUSCA TABLA SOLICITADA DE ACUERDO AL NOMBRE DEL PROGRAMA
      *
           MOVE TAB-COD-CTAB IN TAB TO PTA-CTAB IN PTA-VARI.
      *
           IF PTA-NPGM IN PTA-VARI NOT = PTA-NPGM-TAB IN PTA-VARI
               GO TO FIN-GNS-CRG-PTA.

           ADD  1  TO PTA-INDI IN PTA-VARI.

           IF PTA-INDI IN PTA-VARI > PTA-OCUR IN PTA-VARI
               MOVE 'TAB    CHICA'     TO PTA-CMSG IN PTA-VARI
               MOVE 'PROGRAMA TIO AUX' TO PTA-MENS IN PTA-VARI
               GO TO FIN-GNS-CRG-PTA.

           MOVE PTA-COOC-TAB IN PTA-VARI TO
                PTA-COOC-VEC IN PTA-VARI(PTA-INDI IN PTA-VARI).
           MOVE TAB-IND-VIGE IN TAB TO
                PTA-COOC-VIG-VEC IN PTA-VARI(PTA-INDI IN PTA-VARI).

       NXT-GNS-CRG-PTA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'PTA'
               GO TO LUP-GNS-CRG-PTA.

       FIN-GNS-CRG-PTA.
           IF PTA-COOC-VEC IN PTA-VARI(1) NOT > SPACES AND
              PTA-CMSG IN PTA-VARI        NOT > SPACES
               MOVE 'TABPTANOTFND'           TO PTA-CMSG IN PTA-VARI
               MOVE 'NO ENCONTRE REG EN PTA' TO PTA-MENS IN PTA-VARI.
       EXT-GNS-CRG-PTA.
           EXIT.
      *****************************************************************
      *BUSCA TIPO DE OPERACION EN TABLA CARGADA EN MEMORIA
       GNS-BUS-PTA SECTION.
       INI-GNS-BUS-PTA.
           MOVE 'PTACOD NOFND' TO PTA-CMSG IN PTA-VARI.
           MOVE SPACES         TO PTA-MENS IN PTA-VARI.

      *BUSCA TIO + AUX
           MOVE PTA-COOC IN PTA-VARI TO PTA-COOC-AUX IN PTA-VARI.

           SET PTA-NUME TO 1.
           SEARCH PTA-VECT VARYING PTA-NUME
                WHEN ( PTA-COOC-VEC IN PTA-VARI(PTA-NUME)   =
                       PTA-COOC-AUX IN PTA-VARI )
                      MOVE SPACES TO PTA-CMSG IN PTA-VARI.

           IF PTA-CMSG = SPACES
               IF PTA-COOC-VIG-VEC IN PTA-VARI(PTA-NUME) = 'N'
                   MOVE 'PTACOD NOFND' TO PTA-CMSG IN PTA-VARI
                   GO TO FIN-GNS-BUS-PTA.

      *BUSCA TIO
           MOVE SPACES  TO PTA-AOOC-AUX IN PTA-VARI.

           SET PTA-NUME TO 1.
           SEARCH PTA-VECT VARYING PTA-NUME
                WHEN ( PTA-COOC-VEC IN PTA-VARI(PTA-NUME)   =
                       PTA-COOC-AUX IN PTA-VARI )
                      MOVE SPACES TO PTA-CMSG IN PTA-VARI.

           IF PTA-CMSG = SPACES
               IF PTA-COOC-VIG-VEC IN PTA-VARI(PTA-NUME) = 'N'
                   MOVE 'PTACOD NOFND' TO PTA-CMSG IN PTA-VARI
                   GO TO FIN-GNS-BUS-PTA.

      *BUSCA '*'
           MOVE '*  '   TO PTA-TOOC-AUX IN PTA-VARI.
           MOVE SPACES  TO PTA-AOOC-AUX IN PTA-VARI.

           SET PTA-NUME TO 1.
           SEARCH PTA-VECT VARYING PTA-NUME
                WHEN ( PTA-COOC-VEC IN PTA-VARI(PTA-NUME)   =
                       PTA-COOC-AUX IN PTA-VARI )
                      MOVE SPACES TO PTA-CMSG IN PTA-VARI.

           IF PTA-CMSG = SPACES
               IF PTA-COOC-VIG-VEC IN PTA-VARI(PTA-NUME) = 'N'
                   MOVE 'PTACOD NOFND' TO PTA-CMSG IN PTA-VARI
                   GO TO FIN-GNS-BUS-PTA.

       FND-GNS-BUS-PTA.
           IF PTA-CMSG > SPACES
               MOVE PTA-COOC IN PTA-VARI TO PTA-MENS IN PTA-VARI.
       FIN-GNS-BUS-PTA.
           EXIT.
      ******************************************************************
