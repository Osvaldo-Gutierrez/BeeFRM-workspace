      * ==============================================================
      * ==============================================================
      * NOMBRE RUTINA:    GNSBGAGP
      * --------------------------------------------------------------
      *
      * DESCRIPCION:      VALIDACION GENERICA DE PARAMETROS.
      *
      * DATOS DE ENTRADA: * SISTEMA
      *                   * AMBITO DE PARAMETROS
      * SALIDA:           * RUTINA ABORTA PGM, SI PARAMETRO NO VALIDO.
      * --------------------------------------------------------------
      * RESPONSABLE:    IVAN SILVA P.
      * AUTOR:          ERCIRA NEIRA E.
      * FECHA CREACION: MI-16-02-2005.
      * --------------------------------------------------------------
      * MODIFICACIONES:
      *
      * 1. DESCRIPCION: NUEVOS REQUERIMIENTOS:
      *                 - CONSIDERAR PRIORIDAD DE PROCESAMIENTO POR
      *                   CADA PARAMETRO. CONSISTENCIA ENTRE JCL/RUTINA
      *                 - DEFINE NOMBRES EST@NDAR POR TIPO DE PARAMETRO
      *    FECHA:       21-02-2005
      *    RESPONSABLE: ERCIRA NEIRA E.
      *
      * 2. DESCRIPCION: ---
      *    FECHA:       ---
      *    MARCA:       ---
      *    RESPONSABLE: ---
      * ==============================================================
      * ==============================================================
      
      
      * ===============================================================
      *** 1. PROCESO PPAL./  PROCESA PARAMETROS ENVIADOS DESDE PGM.
      * ===============================================================
       PRO-AGP SECTION.
       INI-PRO-AGP.
      
           MOVE 'S'    TO AGP-IND-VALI.
      
      *    CARGA TAB-INT CON TAB-AGP DE SISTEMA; SORT POR PRIORIDAD.
           PERFORM PRO-LOAD-AGP-INT.
      
      *    CONTROLA CANTIDAD DE PARAM CARGADOS, Y SIGUE SI ES OK.
           PERFORM PRO-VAL-AGP-COMPL.
      
      
           MOVE 'S'   TO AGP-IND-VIGE.
      
           INITIALIZE AGP-COD-AMBI-AUX1
                      AGP-COD-AMBI-AUX2
                      AGP-MSK-DAT-ADIC
                      AGP-WSS-CONT.
      
           PERFORM VAL-PARAM.
      
      
        FIN-PRO-AGP.
           EXIT.
      * ===============================================================
      * ===============================================================
      * ================ INICIO-SECCION PROCEDIMIENTOS ================
      
      * ===============================================================
      ***  0.1 INGRESA PARAMETROS EN TAB.INTERNA, POR PRIORIDAD.
      * ===============================================================
       CRG-KEY-CTAB SECTION.
       INI-CRG-KEY-CTAB.
      
           MOVE AGP-COD-INP-SIST TO FIO-SIST.
           MOVE AGP-COD-TTAB TO TAB-COD-TTAB.
      
       FIN-CRG-KEY-CTAB.
           EXIT.
      * ===============================================================
      ***  1.1 INGRESA PARAMETROS EN TAB.INTERNA, POR PRIORIDAD.
      * ===============================================================
       PRO-LOAD-AGP-INT SECTION.
       INI-PRO-LOAD-AGP-INT.
      
DDD        DISPLAY 'SECCION ========================================='.
DDD        DISPLAY '1.1 CARGA AGP EN TAB-INT: PRO-LOAD-AGP-INT'.
DDD        DISPLAY ' '.
      
           PERFORM CRG-KEY-CTAB.
      
           INITIALIZE AGP-TOT-FECH
                      CONT-PARAM-TIPO-OFI
                      AGP-WSS-CORR.
      
           INITIALIZE AGP-MSK-DAT-ADIC.
      
      *    PARA OBTENER REG. IGUAL O MAYOR A CONDICION (NOM-PGM_'  ').
           MOVE AGP-COD-AMBP            TO      AGP-COD-AMBP-AUX1.
           MOVE SPACES                  TO      AGP-COD-CORR-AUX1.
           MOVE AGP-COD-AMBI-AUX1       TO      TAB-COD-CTAB.
      
DDD        DISPLAY 'DATOS PATA OBT. REG-TAB-AGP/SIST: '
DDD        DISPLAY ' '.
DDD        DISPLAY 'FIO-SIST: ' FIO-SIST.
DDD        DISPLAY 'TAB-COD-TTAB: ' TAB-COD-TTAB.
DDD        DISPLAY 'AGP-COD-AMBI-AUX1: ' TAB-COD-CTAB.
DDD        DISPLAY ' '.
      
           MOVE FIO-GET-NLS   TO   FIO-CMND.
      
       LOOP-PRO-LOAD-AGP-INT.
      
           PERFORM GNS-FIO-TAB.
      
           IF FIO-STAT-OKS
              IF  TAB-COD-TTAB IN TAB = AGP-COD-TTAB AND
                  TAB-COD-CTAB IN TAB = SPACES
      
                  MOVE FIO-GET-NXT TO FIO-CMND
                  GO TO LOOP-PRO-LOAD-AGP-INT
              ELSE
                  IF TAB-COD-TTAB IN TAB  = AGP-COD-TTAB
      
                     MOVE TAB-COD-CTAB   TO AGP-COD-AMBI-AUX2
      
DDD                  DISPLAY ' '
DDD                  DISPLAY 'OBTIENE REG.SIGTE: ' AGP-COD-AMBI-AUX2
      
      *              * SI ES PARAMETRO DEL PROGRAMA EN PROCESO
                     IF AGP-COD-AMBP-AUX2  =  AGP-COD-AMBP
      
                        MOVE TAB-GLS-DATA    TO AGP-MSK-DAT-ADIC
                        IF  NOT ( AGP-MSK-GNR-GLOS = 'S' OR
                                  AGP-MSK-GNR-GLOS = 'G' )
                           DISPLAY 'ERROR EN TIPO DE DESPLIEGUE GLOSA:'
                           AGP-MSK-GNR-GLOS
                           PERFORM GNS-PRO-ABT
                        END-IF
      
                        MOVE AGP-MSK-GNR-PRIO TO AGP-WSS-CONT
      
DDD                     DISPLAY 'REG. CON PRIORIDAD: ' AGP-WSS-CONT
      
                        IF AGP-WSS-REG-USAD(AGP-WSS-CONT) = 'S'
                          DISPLAY ' '
                          DISPLAY ' CANCELAR PROCESO '
                          DISPLAY ' REG. CON PRIORIDAD DUPLICADA'
                          PERFORM GNS-PRO-ABT
                        ELSE
                          MOVE TAB-COD-CTAB IN TAB
                               TO AGP-BKP-CTAB
      
                          MOVE TAB-COD-CTAB IN TAB
                               TO AGP-WSS-COD-CTAB(AGP-WSS-CONT)
                          MOVE TAB-GLS-ABRV IN TAB
                               TO AGP-WSS-GLS-ABRV(AGP-WSS-CONT)
                          MOVE TAB-GLS-DESC IN TAB
                               TO AGP-WSS-GLS-DESC(AGP-WSS-CONT)
                          MOVE TAB-GLS-DATA IN TAB
                               TO AGP-WSS-GLS-DATA(AGP-WSS-CONT)
                          MOVE 'S' TO AGP-WSS-REG-USAD(AGP-WSS-CONT)
      
                          ADD   1  TO AGP-WSS-CORR
      
DDD                       DISPLAY ' '
DDD                       DISPLAY 'AGP-WSS-CORR: ' AGP-WSS-CORR
      
                          PERFORM OBT-DAT-GNS-PARAM
      
                          PERFORM PRO-NXT-PARAM
                          GO TO LOOP-PRO-LOAD-AGP-INT
                        END-IF
                     ELSE
                         PERFORM PRO-NXT-PARAM
                         GO TO LOOP-PRO-LOAD-AGP-INT
                     END-IF
                  END-IF
              END-IF
           END-IF.
      
       FIN-PRO-LOAD-AGP-INT.
           EXIT.
      
      * ===============================================================
      *** 1.1.1 OBTENER DATOS DESDE GNS/TAB-AGP ASOCIADOS A TIPO PARAM.
      * ===============================================================
       OBT-DAT-GNS-PARAM SECTION.
       INI-OBT-DAT-GNS-PARAM.
      
DDD        DISPLAY 'SECCION ========================================='.
DDD        DISPLAY '1.1.1 OBTIENE GLOSA GENERICA DESDE TAB-AGP/GNS.'
DDD        DISPLAY ' '.
      
           MOVE 'GNS'                      TO   FIO-SIST.
           MOVE AGP-COD-TTAB               TO   TAB-COD-TTAB.
           MOVE AGP-WSS-GLS-ABRV(AGP-WSS-CONT)  TO   TAB-COD-CTAB.
      
DDD        DISPLAY 'CONSULTA CON LOS DATOS SGTES: '.
DDD        DISPLAY ' '.
DDD        DISPLAY 'FIO-SIST: ' FIO-SIST.
DDD        DISPLAY 'TAB-COD-TTAB: ' TAB-COD-TTAB.
DDD        DISPLAY 'TAB-GLS-ABRV: ' TAB-COD-CTAB.
      
           MOVE FIO-GET-KEY        TO    FIO-CMND.
           PERFORM GNS-FIO-TAB.
      
           IF NOT ( FIO-STAT-OKS  AND  TAB-IND-VIGE = 'S' )
               DISPLAY ' '
               DISPLAY ' CANCELAR PROCESO '
               DISPLAY ' NO EXISTE REGISTRO AGP DE GNS'
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT
           ELSE
               IF AGP-MSK-GNR-GLOS = 'G'
                   MOVE TAB-GLS-DESC IN TAB
                        TO AGP-WSS-GLS-DESC(AGP-WSS-CONT)
DDD                DISPLAY 'OBTIENE GLOSA GENERICA SGTE: '
DDD                DISPLAY ' '
DDD                DISPLAY 'TAB-GLS-DESC: ' AGP-WSS-GLS-DESC(AGP-WSS-CONT)
               END-IF
           END-IF.
      
       FIN-OBT-DAT-GNS-PARAM.
           EXIT.
      * ===============================================================
      ***  1.2 VALIDAR CANTIDAD DE PARAMETROS INGRESADOS A TAB-INTERNA.
      * ===============================================================
       PRO-VAL-AGP-COMPL SECTION.
       INI-PRO-VAL-AGP-COMPL.
      
DDD        DISPLAY 'SECCION ========================================='.
DDD        DISPLAY '1.2 VALIDA QUE TAB-INT ESTE COMPLETA CON AGP.'
DDD        DISPLAY ' '.
      
           INITIALIZE AGP-WSS-CUEN.
      
           MOVE 1 TO AGP-WSS-CUEN.
      
       LOOP-VAL-AGP-COMPL.
      
DDD        DISPLAY 'CONT REG AGP-INTERNA: AGP-WSS-CUEN: ' AGP-WSS-CUEN
DDD        DISPLAY 'MARCA DE REG.CARGADO: ' AGP-WSS-REG-USAD(AGP-WSS-CUEN)
      
           IF AGP-WSS-REG-USAD(AGP-WSS-CUEN) NOT = 'S'
              DISPLAY ' '
              DISPLAY ' CANCELAR PROCESO '
              DISPLAY ' INGRESO DE TABLA INTERNA AGP ERRONEO'
              PERFORM  GNS-PRO-ABT
           ELSE
              ADD 1 TO AGP-WSS-CUEN
      
              IF AGP-WSS-CUEN NOT > AGP-WSS-CORR
                 GO TO LOOP-VAL-AGP-COMPL.
      
       FIN-PRO-VAL-AGP-COMPL.
           EXIT.
      * ===============================================================
      ***  1.3 VALIDAR PARAMETROS LEIDOS DESDE TAB-INTERNA AGP/SIS.
      * ===============================================================
       VAL-PARAM SECTION.
       INI-VAL-PARAM.
      
DDD        DISPLAY 'SECCION ========================================='.
DDD        DISPLAY '1.3 VALIDA PARAMETROS DE PROGRAMA.'
DDD        DISPLAY ' '.
      
           INITIALIZE AGP-WSS-CONT.
      
       LOP-VAL-PARAM.
      
           ADD  1      TO AGP-WSS-CONT.
           DISPLAY 'AGP-WSS-CONT, CUANDO VALIDA PARAM: ' AGP-WSS-CONT.
      
      *    SI PARAM ES DE TIPO 'FEC', VALIDO FECHA DE PROCESO.
      
           IF AGP-WSS-GLS-ABRV(AGP-WSS-CONT) = AGP-CTE-TIP-FECH
      
DDD             DISPLAY 'PARAMETRO ES DE TIPO "FEC" = TAB-GLS-ABRV: '
DDD             TAB-GLS-ABRV IN TAB
      
                MOVE AGP-WSS-GLS-DATA(AGP-WSS-CONT) TO AGP-MSK-DAT-ADIC
      
                PERFORM PRO-CARAC-FEC
      
      
                DISPLAY 'INGRESE  >>>>: ' AGP-WSS-GLS-DESC(AGP-WSS-CONT)
      
                ACCEPT AGP-PRI-FEC-FFEC
      
                DISPLAY 'INGRESO  >>>>: ' AGP-WSS-GLS-DESC(AGP-WSS-CONT)
      
                MOVE AGP-PRI-FEC-FFEC  TO   FEC-FECH
                MOVE FEC-VAL-FEC   TO   FEC-CMND
DDD             DISPLAY ' '
DDD             DISPLAY 'FECHA AGP-FEC-FECH: ' FEC-FECH
DDD             DISPLAY 'CMND ' FEC-CMND
      
                PERFORM PRO-FEC
      
                IF NOT FEC-STAT-OKS
                     DISPLAY ' '
                     DISPLAY ' CANCELAR PROCESO '
                     DISPLAY ' ERROR EN VALIDACION F. DE PROCESO : '
                               AGP-PRI-FEC-FFEC IN AGP-VARI
                     DISPLAY FEC-MENS
                     PERFORM GNS-PRO-ABT
                     MOVE 'N' TO AGP-IND-VALI
                 ELSE
                     ADD 1 TO AGP-TOT-FECH
                     DISPLAY 'FECHA VALIDADA OK'
                 END-IF
           END-IF.
      
      *    * SE NOMBRAN VARIABLES ESTANDAR PARA TIPO FECHA.
      
           IF AGP-TOT-FECH = 1
              MOVE AGP-PRI-FEC-FFEC TO AGP-PRO-FEC-FFE1.
      
           IF AGP-TOT-FECH = 2
              MOVE AGP-PRI-FEC-FFEC TO AGP-PRO-FEC-FFE2.
      
           IF AGP-TOT-FECH = 3
              MOVE AGP-PRI-FEC-FFEC TO AGP-PRO-FEC-FFE3.
      
           IF AGP-TOT-FECH = 4
              MOVE AGP-PRI-FEC-FFEC TO AGP-PRO-FEC-FFE4.
      
           IF AGP-TOT-FECH = 5
              MOVE AGP-PRI-FEC-FFEC TO AGP-PRO-FEC-FFE5.
      
           IF AGP-TOT-FECH = 6
              MOVE AGP-PRI-FEC-FFEC TO AGP-PARAM-FEC6.
      
           IF AGP-TOT-FECH = 7
              MOVE AGP-PRI-FEC-FFEC TO AGP-PARAM-FEC7.
      
      
      
           IF AGP-WSS-CONT NOT > AGP-WSS-CORR
              GO TO LOP-VAL-PARAM.
      
       FIN-VAL-PARAM.
           EXIT.
      * ===============================================================
      ***  1.3.1 PROCESA PARAMETRO TIPO FECHA.
      * ===============================================================
       PRO-CARAC-FEC SECTION.
       INI-PRO-CARAC-FEC.
      
DDD        DISPLAY 'SECCION ========================================='.
DDD        DISPLAY '1.3.1 CONFIGURA CARACTERISTICAS DE FECHA.'
DDD        DISPLAY ' '.
      
           MOVE FEC-FORM-FEC  TO  FEC-FORM.
      
           IF  AGP-MSK-FEC-OBL = 'S'
               MOVE FEC-FBLK-S TO FEC-FBLK
           ELSE
           IF  AGP-MSK-FEC-OBL = 'N'
               MOVE FEC-FBLK-N TO FEC-FBLK
           ELSE
               DISPLAY 'ERROR EN INDICADOR FECHA OBLIG'.
      
      
           IF  AGP-MSK-FEC-HBL = 'S'
               MOVE FEC-VHBL-S TO FEC-VHBL
           ELSE
           IF  AGP-MSK-FEC-HBL = 'N'
               MOVE FEC-VHBL-N TO FEC-VHBL
           ELSE
               DISPLAY 'ERROR EN INDICADOR FECHA HABIL '.
      
      
           IF  AGP-MSK-FEC-HOY = 'L ' OR AGP-MSK-FEC-HOY = '< '
               MOVE FEC-CHOY-L TO FEC-CHOY
           ELSE
           IF  AGP-MSK-FEC-HOY = 'EQ' OR AGP-MSK-FEC-HOY = '= '
               MOVE FEC-CHOY-E TO FEC-CHOY
           ELSE
           IF  AGP-MSK-FEC-HOY = 'GR' OR AGP-MSK-FEC-HOY = '> '
               MOVE FEC-CHOY-G TO FEC-CHOY
           ELSE
           IF  AGP-MSK-FEC-HOY = 'LE' OR AGP-MSK-FEC-HOY = '<='
               MOVE FEC-CHOY-LE TO FEC-CHOY
           ELSE
           IF  AGP-MSK-FEC-HOY = 'GE' OR AGP-MSK-FEC-HOY = '>='
               MOVE FEC-CHOY-GE TO FEC-CHOY
           ELSE
           IF  AGP-MSK-FEC-HOY = 'LG' OR AGP-MSK-FEC-HOY = 'LG'
               MOVE FEC-CHOY-LG TO FEC-CHOY
           ELSE
           IF  AGP-MSK-FEC-HOY = 'NO' OR AGP-MSK-FEC-HOY = 'N '
               MOVE FEC-CHOY-N  TO FEC-CHOY
           ELSE
               DISPLAY 'ERROR EN COMPARACION HOY '.
      
DDD        DISPLAY 'DATOS ADICIONALES DE FECHA: '
DDD        DISPLAY ' '
DDD        DISPLAY 'FEC-FORM-FEC: '     FEC-FORM.
DDD        DISPLAY 'AGP-MSK-FEC-OBL: '  FEC-FBLK.
DDD        DISPLAY 'AGP-MSK-FEC-HBL: '  FEC-VHBL.
DDD        DISPLAY 'AGP-MSK-FEC-HOY: '  FEC-CHOY.
      
       FIN-PRO-PARAM-FEC.
           EXIT.
      * ===============================================================
      ***  OBTENER SIGUIENTE REG. DE TAB-AGP / SIS.
      * ===============================================================
       PRO-NXT-PARAM SECTION.
       INI-PRO-NXT-PARAM.
      
           PERFORM CRG-KEY-CTAB.
           MOVE AGP-BKP-CTAB TO TAB-COD-CTAB IN TAB.
      
           DISPLAY ' '.
           DISPLAY 'RECUPERANDO ' TAB-COD-CTAB IN TAB
             ' EN TABLA ' TAB-COD-TTAB IN TAB ' DE SISTEMA '
             FIO-SIST.
      
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
      
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR EN RECUPERACION DE REGISTRO '
              TAB-COD-CTAB IN TAB.
      
           MOVE FIO-GET-NXT TO FIO-CMND.
       FIN-PRO-NXT-PARAM.
           EXIT.
      * ===============================================================
      * ===============================================================
