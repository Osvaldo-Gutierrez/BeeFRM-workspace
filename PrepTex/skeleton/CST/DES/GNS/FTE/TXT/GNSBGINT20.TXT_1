      *Tasa Interes             : INT-TIPT
      *Valor Tasa Informada     : INT-TINT
      *Valor de Cambio          : INT-VCAM
      *Fecha Colocacion         : INT-FCOL
      *Fecha Inicial Periodo    : INT-FUNO
      *Fecha Final   Periodo    : INT-FDOS
      *Fecha Basa Tasa Variable : INT-FBTV ( opcional )
      *Fecha de Proceso         : INT-FPRO ( la rutina la obtiene)
      *
      *devuelve:
      *INT-TIPE                 : Tasa calculada para el periodo
      *INT-CMSG, INT-MENS       : Codigo y mensaje
      *INT-IETB                 : Indicador de existencia de tasa base
       GNS-CAL-INT SECTION.
       INI-GNS-CAL-INT.
           MOVE SPACES TO INT-CMSG INT-MENS INT-IETB.
      *Para sumar o multiplicar tasas segun sea simple o compuesta
           IF INT-SPBT = 'S'
               MOVE 0 TO INT-TIPA
           ELSE
               MOVE 1 TO INT-TIPA.
      *Obtiene fecha de hoy para el chequeo de tasa variable
           IF INT-CVTI > SPACES
               PERFORM GET-FHOY
               MOVE HOY-DHOY TO INT-DPRO
               MOVE HOY-MHOY TO INT-MPRO
               MOVE HOY-SHOY TO INT-SPRO
               MOVE HOY-AHOY TO INT-APRO
               MOVE HOY-HHHY TO INT-HHPR
               MOVE HOY-MMHY TO INT-MMPR
               MOVE HOY-SSHY TO INT-SSPR.
      *
           IF NOT ( INT-PREP > ZEROES AND INT-TFPR = 'C' )
               MOVE INT-DUNO TO FEC-DEC1
               MOVE INT-MUNO TO FEC-MEC1
               MOVE INT-SUNO TO FEC-SEC1
               MOVE INT-AUNO TO FEC-AEC1
               MOVE INT-DDOS TO FEC-DEC2
               MOVE INT-MDOS TO FEC-MEC2
               MOVE INT-SDOS TO FEC-SEC2
               MOVE INT-ADOS TO FEC-AEC2
               MOVE FEC-FORM-FEC TO FEC-FORM
               MOVE FEC-DIF-DIA  TO FEC-CMND
      *Para GENESIS version 3.1
      *        PERFORM CAL-FEC
               PERFORM PRO-FEC
               IF NOT FEC-STAT-OKS
                   MOVE 'INTFEC1FEC2-' TO INT-CMSG
                   GO TO FIN-GNS-CAL-INT
               ELSE
                   MOVE FEC-NDIA TO INT-SEPD
                   MOVE INT-DUNO TO INT-DDMA
                   MOVE INT-MUNO TO INT-MDMA
                   MOVE INT-SUNO TO INT-SDMA
                   MOVE INT-AUNO TO INT-ADMA
                   PERFORM GNS-CAL-TPE
                   GO TO FIN-GNS-CAL-INT.
           MOVE INT-DCOL TO FEC-ITM1 INT-DDMA.
           MOVE INT-MCOL TO FEC-ITM2 INT-MDMA.
           MOVE INT-SCOL TO FEC-ITM3 INT-SDMA.
           MOVE INT-ACOL TO FEC-ITM4 INT-ADMA.
           IF INT-SPR2 > 0
               MOVE INT-TINT TO INT-TINC
               MOVE INT-SPR2 TO INT-TINT
               MOVE INT-TINC TO INT-SPR2
           ELSE
               MOVE INT-TINT TO INT-SPR2.
       SUM-GNS-CAL-INT.
           IF INT-UTPR = 'D'
               MOVE INT-PREP    TO FEC-NDIA
               MOVE FEC-SUM-DIA TO FEC-CMND
           ELSE
           IF INT-UTPR = 'M'
               MOVE INT-PREP    TO FEC-NMES
               MOVE FEC-SUM-MES TO FEC-CMND
           ELSE
               MOVE INT-PREP    TO FEC-NANO
               MOVE FEC-SUM-ANO TO FEC-CMND.
           MOVE FEC-FORM-FEC TO FEC-FORM.
      *Para GENESIS version 3.1
      *    PERFORM CAL-FEC.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'INTFCOLPREP+' TO INT-CMSG
               GO TO FIN-GNS-CAL-INT.
           MOVE FEC-ITM1 TO INT-DAU1.
           MOVE FEC-ITM2 TO INT-MAU1.
           MOVE FEC-ITM3 TO INT-SAU1.
           MOVE FEC-ITM4 TO INT-AAU1.
           MOVE INT-DUNO TO INT-DAU2.
           MOVE INT-MUNO TO INT-MAU2.
           MOVE INT-SUNO TO INT-SAU2.
           MOVE INT-AUNO TO INT-AAU2.
           IF INT-FAU1 < INT-FAU2
               MOVE FEC-ITM1 TO INT-DDMA
               MOVE FEC-ITM2 TO INT-MDMA
               MOVE FEC-ITM3 TO INT-SDMA
               MOVE FEC-ITM4 TO INT-ADMA
               GO TO SUM-GNS-CAL-INT
           ELSE
           IF INT-FAU1 = INT-FAU2
               MOVE FEC-ITM1 TO INT-DDMA
               MOVE FEC-ITM2 TO INT-MDMA
               MOVE FEC-ITM3 TO INT-SDMA
               MOVE FEC-ITM4 TO INT-ADMA.
      **********************************
      *Obtener interes entre INT-DUNO y MIN(FECH,FDOS)
           MOVE INT-DUNO TO FEC-DEC1.
           MOVE INT-MUNO TO FEC-MEC1.
           MOVE INT-SUNO TO FEC-SEC1.
           MOVE INT-AUNO TO FEC-AEC1.
       CON-GNS-CAL-INT.
           MOVE FEC-ITM1 TO INT-DAU1.
           MOVE FEC-ITM2 TO INT-MAU1.
           MOVE FEC-ITM3 TO INT-SAU1.
           MOVE FEC-ITM4 TO INT-AAU1.
           MOVE INT-DDOS TO INT-DAU2.
           MOVE INT-MDOS TO INT-MAU2.
           MOVE INT-SDOS TO INT-SAU2.
           MOVE INT-ADOS TO INT-AAU2.
           IF INT-FAU1 < INT-FAU2
               MOVE INT-DAU1 TO FEC-DEC2
               MOVE INT-MAU1 TO FEC-MEC2
               MOVE INT-SAU1 TO FEC-SEC2
               MOVE INT-AAU1 TO FEC-AEC2
           ELSE
               MOVE INT-DAU2 TO FEC-DEC2
               MOVE INT-MAU2 TO FEC-MEC2
               MOVE INT-SAU2 TO FEC-SEC2
               MOVE INT-AAU2 TO FEC-AEC2.
           MOVE FEC-DIF-DIA TO FEC-CMND.
      *Para GENESIS version 3.1
      *    PERFORM CAL-FEC.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'INTFEC1FEC2-' TO INT-CMSG
               GO TO FIN-GNS-CAL-INT.
           MOVE FEC-NDIA TO INT-SEPD.
           PERFORM GNS-CAL-TPE.
           IF INT-CMSG > SPACES
      *         DISPLAY 'INT-CMSG : ' INT-CMSG
      *         DISPLAY 'INT-MENS : ' INT-MENS
               GO TO FIN-GNS-CAL-INT.
      *     DISPLAY 'FEC-FEC1 - FEC-FEC2 : ' FEC-FEC1 ' - ' FEC-FEC2.
      *     DISPLAY 'FEC-NDIA            : ' FEC-NDIA.
      *     DISPLAY 'INT-TIPE            : ' INT-TIPE WITH CONVERSION.
      *     DISPLAY '------------------------------------------------'.
           MOVE INT-SPR2 TO INT-TINT.
      *Se suma o se multiplica segun sea simple o compuesta
           IF INT-SPBT = 'S'
               ADD INT-TIPE TO INT-TIPA
           ELSE
               ADD 1 TO INT-TIPE
               MULTIPLY INT-TIPE BY INT-TIPA GIVING INT-TIPA.
      *
      *Se termina si la fecha sumada es mayor que la fecha Dos
           IF ( INT-FAU1 NOT < INT-FAU2 )
               IF INT-SPBT = 'S'
                   MOVE INT-TIPA TO INT-TIPE
                   GO TO FIN-GNS-CAL-INT
               ELSE
                   SUBTRACT 1 FROM INT-TIPA GIVING INT-TIPE
                   GO TO FIN-GNS-CAL-INT.
           MOVE INT-DAU1 TO FEC-ITM1.
           MOVE INT-MAU1 TO FEC-ITM2.
           MOVE INT-SAU1 TO FEC-ITM3.
           MOVE INT-AAU1 TO FEC-ITM4.
           IF INT-UTPR = 'D'
               MOVE INT-PREP    TO FEC-NDIA
               MOVE FEC-SUM-DIA TO FEC-CMND
           ELSE
           IF INT-UTPR = 'M'
               MOVE INT-PREP    TO FEC-NMES
               MOVE FEC-SUM-MES TO FEC-CMND
           ELSE
               MOVE INT-PREP    TO FEC-NANO
               MOVE FEC-SUM-ANO TO FEC-CMND.
           MOVE FEC-FORM-FEC TO FEC-FORM.
      *Para GENESIS version 3.1
      *    PERFORM CAL-FEC.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'INTFCOLPREP+' TO INT-CMSG
               GO TO FIN-GNS-CAL-INT.
           MOVE INT-DAU1 TO FEC-DEC1 INT-DDMA.
           MOVE INT-MAU1 TO FEC-MEC1 INT-MDMA.
           MOVE INT-SAU1 TO FEC-SEC1 INT-SDMA.
           MOVE INT-AAU1 TO FEC-AEC1 INT-ADMA.
           GO TO CON-GNS-CAL-INT.
       FIN-GNS-CAL-INT.
           MOVE ZEROES TO INT-FBTV.
           MOVE 0      TO INT-SPR2.
       EXT-GNS-CAL-INT.
           EXIT.

      *Transforma INT-SEPA segun Tipo de Periodo INT-IPER
      * 07-MAY-1990 14:55
       PRO-IPER SECTION.
       INI-PRO-IPER.
      *Busca el CIC de INT-IPER en MSC IPER de sistema TAB
           MOVE SPACES TO INT-CMSG INT-MENS.
           MOVE 'TAB'    TO MSC-COD-SIST FIO-SIST.
           MOVE 'IPER'   TO MSC-COD-TMSC IN MSC.
           MOVE INT-IPER TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF FIO-STAT-OKS
               MOVE MSC-CIC-CMSC IN MSC TO INT-IPER
           ELSE
               MOVE 'CICIPERNEX' TO INT-CMSG.
       FIN-PRO-IPER.
           EXIT.

      *Obtiene tasa de interes dado un periodo, un tipo de tasa y
      *un valor de tasa
       OBT-VTIN SECTION.
       INI-OBT-VTIN.
      *Obtiene fecha de hoy para el chequeo de tasa variable
           IF INT-CVTI > SPACES
               PERFORM GET-FHOY
               MOVE HOY-DHOY TO INT-DPRO
               MOVE HOY-MHOY TO INT-MPRO
               MOVE HOY-SHOY TO INT-SPRO
               MOVE HOY-AHOY TO INT-APRO
               MOVE HOY-HHHY TO INT-HHPR
               MOVE HOY-MMHY TO INT-MMPR
               MOVE HOY-SSHY TO INT-SSPR.
           PERFORM GNS-CAL-TPE.
       FIN-OBT-VTIN.
           EXIT.

       GNS-CAL-TPE SECTION.
       INI-GNS-CAL-TPE.
           PERFORM OBT-SEPA.
           IF INT-CMSG > SPACES
               GO TO FIN-GNS-CAL-TPE.
           PERFORM OBT-TINT.
           IF INT-CMSG > SPACES
               GO TO FIN-GNS-CAL-TPE.
           PERFORM OBT-TIPE.
           IF INT-CMSG > SPACES
               GO TO FIN-GNS-CAL-TPE.
       FIN-GNS-CAL-TPE.
           EXIT.

      *    Descripcion     : Separacion Periodo Interes
      *    Observaciones   :
      *      - Ampliar otros casos Periodo Base ( futuro ? )
       OBT-SEPA SECTION.
       INI-OBT-SEPA.
           MOVE SPACES TO INT-CMSG INT-MENS.
           IF INT-PBTI = 'D'
      *JSS     MOVE 0 TO INT-S30D , INT-SMES , INT-S360 , INT-SANO
               MOVE 0 TO INT-S30D , INT-S360
               MOVE INT-SEPD TO INT-SDIA
           ELSE
           IF INT-PBTI = 'T' OR 'M'
      *JSS     MOVE 0 TO INT-SANO , INT-SMES , INT-S360
               MOVE 0 TO INT-S360
               DIVIDE 30 INTO INT-SEPD GIVING INT-S30D
                   REMAINDER INT-SDIA
           ELSE
           IF INT-PBTI = 'X' OR 'A'
      *JSS     MOVE 0 TO INT-S30D , INT-SMES , INT-SANO
               MOVE 0 TO INT-S30D
               DIVIDE 360 INTO INT-SEPD GIVING INT-S360
                   REMAINDER INT-SDIA
           ELSE
               MOVE 'INTPBTIINV' TO INT-CMSG.
       FIN-OBT-SEPA.
           EXIT.

      *    Descripcion     : Preparacion Tasas Formula Unica
      *    Observaciones   :
      *
       OBT-TINT SECTION.
       INI-OBT-TINT.
           MOVE SPACES TO INT-CMSG INT-MENS.
           IF INT-PBTI = 'D'
               MOVE INT-SPBT TO INT-MCTI
           ELSE
           IF INT-PBTI = 'T' OR 'M'
               IF INT-SEPD > 30
                   MOVE INT-SPBT TO INT-MCTI
               ELSE
                   MOVE INT-BPBT TO INT-MCTI
           ELSE
           IF INT-PBTI = 'X' OR 'A'
               IF INT-SEPD > 360
                   MOVE INT-SPBT TO INT-MCTI
               ELSE
                   MOVE INT-BPBT TO INT-MCTI
           ELSE
               MOVE 'INTPBTIINV' TO INT-CMSG
               GO TO FIN-OBT-TINT.
           MOVE INT-TINT TO INT-TINC.

      *Si es tasa variable
      *      IF ( INT-CVTI > SPACES ) AND
      *         ( INT-IBTV NOT = INT-IBTV-NADA-MAS )
           IF ( INT-CVTI > SPACES )
                MOVE INT-FINT TO CAM-FEC-FCAM IN CAM(1)
                MOVE INT-FINT TO CAM-FEC-FCAM IN CAM(2)

                MOVE 'I'      TO CAM-MSC-TVAL IN CAM(1)
                MOVE INT-CVTI TO CAM-COD-CVAL IN CAM(1)
                MOVE INT-PBTI TO CAM-COD-TCAM IN CAM(1)

                MOVE INT-VCAM TO CAM-COD-VCAM IN CAM(2)

                MOVE FIO-GET-KEY TO FIO-CMND
                PERFORM TAB-FIO-CAM

                IF ( NOT FIO-STAT-OKS ) OR
                   ( CAM-MSC-STAT IN CAM NOT = 'S' )
                    PERFORM GNS-CHK-BTV
                    IF INT-CMSG > SPACES
                        GO TO FIN-OBT-TINT
                    ELSE
                        ADD CAM-SGV-VCAM IN CAM TO INT-TINC
                ELSE
                    ADD CAM-SGV-VCAM IN CAM TO INT-TINC.
      *     IF INT-CVTI > SPACES AND INT-IBTV = INT-IBTV-UNA-VEZ
      *         ADD CAM-SGV-VCAM IN CAM TO INT-TINT
      *         MOVE INT-IBTV-NADA-MAS TO INT-IBTV.

           IF INT-BCTI = 'A'
               COMPUTE INT-TINC ROUNDED =
                   ( INT-TINC * 100 ) / ( 100 - INT-TINC ).

           IF INT-PBTI = 'D'
               COMPUTE INT-TDIA = INT-TINC / 100
               COMPUTE INT-T30D = INT-TINC * 30 / 100
               COMPUTE INT-T360 = INT-TINC * 360 / 100
           ELSE
           IF INT-PBTI = 'T' OR 'M'
               COMPUTE INT-T30D = INT-TINC / 100
               COMPUTE INT-T360 = INT-TINC * 12 / 100
               IF INT-MCTI = 'D'
                   COMPUTE INT-TDIA =
                       ( 1 + ( INT-TINC / 100 ) )**( 1 / 30 ) - 1
               ELSE
                   COMPUTE INT-TDIA = INT-TINC / 3000
           ELSE
           IF INT-PBTI = 'X' OR 'A'
               COMPUTE INT-T30D = INT-TINC / 1200
               COMPUTE INT-T360 = INT-TINC / 100
               IF INT-MCTI = 'D'
                   COMPUTE INT-TDIA =
                       ( 1 + ( INT-TINC / 100 ) )**( 1 / 360 ) - 1
               ELSE
                   COMPUTE INT-TDIA = INT-TINC / 36000
           ELSE
               MOVE 'INTPBTIINV' TO INT-CMSG.
       FIN-OBT-TINT.
           EXIT.

       GNS-CHK-BTV SECTION.
       INI-GNS-CHK-BTV.
           MOVE 'N' TO INT-IETB.
           IF NOT FIO-STAT-OKS
               MOVE 'CAMBTSANXABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2
           ELSE
               MOVE 'CAMBTSANVABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2.
           IF ( CAM-FEC-FCAM IN CAM(1) < INT-FPRO ) OR
              ( CAM-FEC-FCAM IN CAM(1) = INT-FPRO AND
                INT-HPRO > '120000' )
               GO TO FIN-GNS-CHK-BTV.
           IF INT-FBTV NOT > ZEROES
               GO TO FIN-GNS-CHK-BTV.
      *
           MOVE INT-DBTV TO CAM-NUM-DCAM IN CAM(1)
                            CAM-NUM-DCAM IN CAM(2).
           MOVE INT-MBTV TO CAM-NUM-MCAM IN CAM(1)
                            CAM-NUM-MCAM IN CAM(2).
           MOVE INT-SBTV TO CAM-NUM-SCAM IN CAM(1)
                            CAM-NUM-SCAM IN CAM(2).
           MOVE INT-ABTV TO CAM-NUM-ACAM IN CAM(1)
                            CAM-NUM-ACAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               MOVE 'CAMBTSANXABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2
           ELSE
           IF CAM-MSC-STAT IN CAM NOT = 'S'
               MOVE 'CAMBTSANVABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2
           ELSE
               MOVE SPACES              TO INT-CMSG
               MOVE SPACES              TO INT-MENS.
       FIN-GNS-CHK-BTV.
           EXIT.

      *    Descripcion     : Obtencion Tasa del Periodo
      *    Observaciones   :
      *      - Modificar el calculo para Interes Compuesto considerando
      *        Base de Calculo para Bajo y Sobre Periodo Expresion Tasa
       OBT-TIPE SECTION.
       INI-OBT-TIPE.
           MOVE SPACES TO INT-CMSG INT-MENS.

           IF INT-MCTI = 'S'
               COMPUTE INT-TIPE ROUNDED =
                   INT-TDIA * INT-SDIA + INT-T30D * INT-S30D +
                   INT-T360 * INT-S360
           ELSE
           IF INT-MCTI = 'C'
               IF INT-PBTI = 'D'
                   COMPUTE INT-TIPE ROUNDED =
                       ( INT-TDIA + 1 ) ** INT-SDIA - 1
               ELSE
               IF INT-PBTI = 'T' OR 'M'
                   COMPUTE INT-TIPE ROUNDED =
                       ( INT-T30D + 1 ) ** INT-S30D *
                       ( INT-SDIA * INT-TDIA + 1 ) - 1
               ELSE
               IF INT-PBTI = 'X' OR 'A'
                   COMPUTE INT-TIPE ROUNDED =
                       ( INT-T360 + 1 ) ** INT-S360 *
                       ( INT-SDIA * INT-TDIA + 1 ) - 1
               ELSE
                   MOVE 'INTPBTIINV' TO INT-CMSG
                   GO TO FIN-OBT-TIPE
           ELSE
           IF INT-MCTI = 'D'
               COMPUTE INT-TIPE ROUNDED =
                   ( INT-TDIA + 1 ) ** INT-SEPD - 1
           ELSE
               MOVE 'INTMCTIINV' TO INT-CMSG
               GO TO FIN-OBT-TIPE.
       FIN-OBT-TIPE.
           EXIT.
