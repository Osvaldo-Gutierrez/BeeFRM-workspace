      * Modulo Generico de acceso a archivos DATACOM
       GNS-FIO-DTC SECTION.
       INI-GNS-FIO-DTC.
           MOVE '00'     TO FIO-STAT.
           MOVE SPACES   TO FIO-MENS.
           MOVE '  '     TO ADR-STAT IN ADR-REQA.
           IF FIO-CMND = 'GETNL' GO TO FIO-GNLS
           ELSE
           IF FIO-CMND = 'GETLE' GO TO FIO-GLEQ
           ELSE
           IF FIO-CMND = 'GETPR' GO TO FIO-GPRV
           ELSE
           IF FIO-CMND = 'GETFS' GO TO FIO-GFST
           ELSE
           IF FIO-CMND = 'FNDFS' GO TO FIO-FFST
           ELSE
           IF FIO-CMND = 'INPUT' OR 'OUTPT' OR 'INPOU' OR 'CRRAR'
               GO TO FIO-NULL.
       FIO-CDTC.
           MOVE FIO-CMND TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           GO TO SET-STAT.
       FIO-GNLS.
           MOVE 'LOCKY' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           IF NOT ADR-STAT-OKS IN ADR-REQA
               GO TO SET-STAT.
           MOVE 'REDKY' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           GO TO SET-STAT.
       FIO-GLEQ.
           MOVE 'LOCKL' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           IF NOT ADR-STAT-OKS IN ADR-REQA
               GO TO SET-STAT.
           MOVE 'REDLE' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           GO TO SET-STAT.
       FIO-GPRV.
           MOVE 'LOCBR' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           IF NOT ADR-STAT-OKS IN ADR-REQA
               GO TO SET-STAT.
           MOVE 'REDLE' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           GO TO SET-STAT.
       FIO-GFST.
           MOVE LOW-VALUES TO ADR-VKEY IN ADR-REQA.
           MOVE 'LOCKY' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           IF NOT ADR-STAT-OKS IN ADR-REQA
               GO TO SET-STAT.
           MOVE 'REDKY' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           GO TO SET-STAT.
       FIO-FFST.
           MOVE LOW-VALUES TO ADR-VKEY IN ADR-REQA.
           MOVE 'LOCKY' TO ADR-CMND IN ADR-REQA.
           CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS.
           GO TO SET-STAT.
       FIO-NULL.
           MOVE 'Comando sin operacion asociada : ' TO FIO-MEN1.
           MOVE FIO-CMND TO FIO-MENS-CMD.
           GO TO FIN-GNS-FIO-DTC.
       SET-STAT.
           IF ADR-SSTT IN ADR-REQA NOT > SPACES
               MOVE SPACES TO ADR-SSTT IN ADR-REQA.
           IF NOT ADR-STAT-OKS IN ADR-REQA
               MOVE 'Status DTC :'       TO FIO-MENS-DSC
               MOVE ADR-STAT IN ADR-REQA TO FIO-MENS-STT
               MOVE ADR-SSTT IN ADR-REQA TO FIO-MENS-SST
               MOVE 'Key'                TO FIO-MENS-GKY
               MOVE ADR-DKEY IN ADR-REQA TO FIO-MENS-KDT
               MOVE 'Base Datos'         TO FIO-MENS-GBD
               MOVE ADR-DBID IN ADR-REQA TO FIO-MENS-DBI
               MOVE 'Cmd'                TO FIO-MENS-GCM
               MOVE ADR-CMND IN ADR-REQA TO FIO-MENS-CMD
               MOVE 'File'               TO FIO-MENS-GFL.
           IF ADR-STAT-OKS IN ADR-REQA
               MOVE '00' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '10'
               MOVE '22' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '11'
               MOVE '23' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '12'
               MOVE '43' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '14'
               MOVE '23' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '16'
               MOVE 'LOGTB' TO ADR-CMND IN ADR-REQA
               CALL 'DBNTRY' USING ADR-UIFB ADR-REQA FIO-DFLD ADR-ELLS
               MOVE '47' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '18'
               MOVE '44' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '19' OR '29'
               MOVE '10' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '92'
               MOVE '45' TO FIO-STAT
           ELSE
           IF ADR-STAT IN ADR-REQA = '93'
               MOVE '46' TO FIO-STAT
           ELSE
               MOVE 'Aborto, DTC:'       TO FIO-MENS-DSC
               MOVE '99' TO FIO-STAT.
       FIN-SET-STAT.
       FIN-GNS-FIO-DTC.
           EXIT.
