      ********************* FIO *********************
       FIO SECTION.
       INI-FIO.
           MOVE '00'   TO FIO-STAT.
           MOVE SPACES TO FIO-MENS.
           IF FIO-IVEZ NOT > SPACES
               MOVE 'N' TO FIO-IVEZ
               EXEC CICS IGNORE CONDITION
                     DSIDERR
                     DUPKEY
                     DUPREC
                     ENDFILE
                     ILLOGIC
                     IOERR
                     NOSPACE
                     NOTFND
                     NOTOPEN
               END-EXEC.
      *
           MOVE FIO-CMND TO FIO-IGET.

           IF FIO-CMND = FIO-EBR-ALL
               GO TO CON-FIO.

           MOVE 1 TO FIO-IAUX.
           SET FIO-ITBL TO 1.
           SEARCH FIO-VSFL VARYING FIO-IAUX
               AT END
                   MOVE 'N' TO FIO-ISBR
               WHEN FIO-NFIL( FIO-ITBL ) = FIO-VOID
                   MOVE 'S' TO FIO-ISBR.
           IF FIO-ISBR = 'S' AND ( NOT FIO-ICBR-YES ) AND
              ( FIO-CMND NOT = FIO-END-BRW )
               PERFORM CIC-FIO-EBRW.
       CON-FIO.
      *
           GO TO CIC-FIO-GKEY CIC-FIO-GNXT CIC-FIO-GFST CIC-FIO-GNLS
                 CIC-FIO-FNSF
                 CIC-FIO-PUTF CIC-FIO-MODF CIC-FIO-DELF CIC-FIO-FNSF
                 CIC-FIO-FDMY CIC-FIO-FDMY CIC-FIO-FDMY CIC-FIO-FDMY
                 CIC-FIO-FEQL
                 CIC-FIO-FFST CIC-FIO-FNLS CIC-FIO-FNSF CIC-FIO-ULKR
                 CIC-FIO-FNSF CIC-FIO-FNSF CIC-FIO-FDMY CIC-FIO-FNSF
                 CIC-FIO-FNSF CIC-FIO-FNSF CIC-FIO-FNSF
                 CIC-FIO-GKEY CIC-FIO-GFST CIC-FIO-GNLS CIC-FIO-FNSF
                 CIC-FIO-FEQL
                 CIC-FIO-FFST CIC-FIO-FNLS CIC-FIO-FNSF CIC-FIO-GNXT
                 CIC-FIO-GUPD CIC-FIO-GUPD CIC-FIO-EBRW CIC-FIO-BKOU
                 CIC-FIO-GLEQ CIC-FIO-GPRV CIC-FIO-CPNT CIC-BFR-RFSH
                 CIC-FIO-EBAL
           DEPENDING ON FIO-CMND.
           MOVE 'ABORTO: COMANDO INVALIDO PARA ARCHIVO ' TO FIO-MEN1.
           MOVE FIO-VOID                                 TO FIO-MEN2.
           GO TO CIC-FIO-FABT.

       CIC-FIO-GKEY.
           EXEC CICS READ
                     DATASET(FIO-VOID)
                     SET(FIO-PNT0)
                     LENGTH(FIO-RLEN)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
       FIN-CIC-FIO-GKEY.
           GO TO STT-FIO.

       CIC-FIO-GNXT.
           EXEC CICS READNEXT
                     DATASET(FIO-VOID)
                     SET(FIO-PNT0)
                     LENGTH(FIO-RLEN)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
       FIN-CIC-FIO-GNXT.
           GO TO STT-FIO.

       CIC-FIO-GFST.
           MOVE LOW-VALUES TO FIO-VKEY.
           EXEC CICS STARTBR
                     DATASET(FIO-VOID)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
           IF EIBRESP = ZEROES
      *
               PERFORM GNS-SBR-VSI
      *
               EXEC CICS READNEXT
                         DATASET(FIO-VOID)
                         SET(FIO-PNT0)
                         LENGTH(FIO-RLEN)
                         RIDFLD(FIO-VKEY)
               END-EXEC.
       FIN-CIC-FIO-GFST.
           GO TO STT-FIO.

       CIC-FIO-GNLS.
           EXEC CICS STARTBR
                     DATASET(FIO-VOID)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
           IF EIBRESP = ZEROES
      *
               PERFORM GNS-SBR-VSI
      *
               EXEC CICS READNEXT
                         DATASET(FIO-VOID)
                         SET(FIO-PNT0)
                         LENGTH(FIO-RLEN)
                         RIDFLD(FIO-VKEY)
               END-EXEC.
       FIN-CIC-FIO-GNLS.
           GO TO STT-FIO.

       CIC-FIO-PUTF.
           EXEC CICS WRITE
                     DATASET(FIO-VOID)
                     FROM(FIO-DFLD)
                     LENGTH(FIO-RLEN)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
       FIN-CIC-FIO-PUTF.
           GO TO STT-FIO.

       CIC-FIO-MODF.
           EXEC CICS REWRITE
                     DATASET(FIO-VOID)
                     FROM(FIO-DFLD)
                     LENGTH(FIO-RLEN)
           END-EXEC.
       FIN-CIC-FIO-MODF.
           GO TO STT-FIO.

       CIC-FIO-DELF.
           EXEC CICS DELETE
                     DATASET(FIO-VOID)
           END-EXEC.
       FIN-CIC-FIO-DELF.
           GO TO STT-FIO.

       CIC-FIO-FEQL.
           EXEC CICS STARTBR
                     DATASET(FIO-VOID)
                     RIDFLD(FIO-VKEY)
                     EQUAL
           END-EXEC.
      *
           IF EIBRESP = ZEROES
               PERFORM GNS-SBR-VSI.
      *
       FIN-CIC-FIO-FEQL.
           GO TO STT-FIO.

       CIC-FIO-FFST.
           MOVE SPACES TO FIO-VKEY.
           EXEC CICS STARTBR
                     DATASET(FIO-VOID)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
      *
           IF EIBRESP = ZEROES
               PERFORM GNS-SBR-VSI.
      *
       FIN-CIC-FIO-FFST.
           GO TO STT-FIO.

       CIC-FIO-FNLS.
           EXEC CICS STARTBR
                     DATASET(FIO-VOID)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
      *
           IF EIBRESP = ZEROES
               PERFORM GNS-SBR-VSI.
      *
       FIN-CIC-FIO-FNLS.
           GO TO STT-FIO.

       CIC-FIO-ULKR.
           EXEC CICS UNLOCK
                     DATASET(FIO-VOID)
           END-EXEC.
       FIN-CIC-FIO-ULKR.
           GO TO STT-FIO.

       CIC-FIO-GUPD.
           EXEC CICS READ
                     DATASET(FIO-VOID)
                     SET(FIO-PNT0)
                     LENGTH(FIO-RLEN)
                     RIDFLD(FIO-VKEY)
                     UPDATE
           END-EXEC.
       FIN-CIC-FIO-GUPD.
           GO TO STT-FIO.

       CIC-FIO-EBRW.
           EXEC CICS ENDBR
                     DATASET(FIO-VOID)
           END-EXEC.
      *
           IF FIO-ISBR = 'S'
               MOVE SPACES TO FIO-NFIL( FIO-IAUX ).
      *
       FIN-CIC-FIO-EBRW.
           GO TO STT-FIO.

       CIC-FIO-GPRV.
           IF FIO-KDEF = FIO-KDEF-S
               MOVE FIO-KDEF-N TO FIO-KDEF
               EXEC CICS STARTBR
                         DATASET(FIO-VOID)
                         RIDFLD(FIO-VKEY)
               END-EXEC
               IF EIBRESP = ZEROES
      *
                   PERFORM GNS-SBR-VSI
      *
                   EXEC CICS READPREV
                         DATASET(FIO-VOID)
                         SET(FIO-PNT0)
                         LENGTH(FIO-RLEN)
                         RIDFLD(FIO-VKEY)
                   END-EXEC
               ELSE
                   NEXT SENTENCE
           ELSE
               EXEC CICS READPREV
                     DATASET(FIO-VOID)
                     SET(FIO-PNT0)
                     LENGTH(FIO-RLEN)
                     RIDFLD(FIO-VKEY)
               END-EXEC.
       FIN-CIC-FIO-GPRV.
           GO TO STT-FIO.

       CIC-BFR-RFSH.
           MOVE LOW-VALUES TO FIO-VKEY.
           EXEC CICS STARTBR
                     DATASET(FIO-VOID)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
           IF EIBRESP = ZEROES
               EXEC CICS ENDBR
                     DATASET(FIO-VOID)
               END-EXEC.
       FIN-CIC-BFR-RFSH.
           GO TO STT-FIO.

       CIC-FIO-GLEQ.
           EXEC CICS PUSH HANDLE END-EXEC.
           EXEC CICS
                HANDLE CONDITION NOTFND(CIC-FIO-GLEQ-ENDFILE)
           END-EXEC.
           MOVE FIO-VKEY TO FIO-CKEY.
           EXEC CICS STARTBR
                     DATASET(FIO-VOID)
                     RIDFLD(FIO-VKEY)
           END-EXEC.
           EXEC CICS POP HANDLE END-EXEC.
           GO TO CON-CIC-FIO-GLEQ.

       CIC-FIO-GLEQ-ENDFILE.
      *    IF EIBRESP = ZEROES
               EXEC CICS POP HANDLE END-EXEC.
               MOVE HIGH-VALUE TO FIO-VKEY
               EXEC CICS STARTBR
                         DATASET(FIO-VOID)
                         RIDFLD(FIO-VKEY)
               END-EXEC.

           IF EIBRESP = ZEROES
      *
               PERFORM GNS-SBR-VSI.
      *
           IF EIBRESP = ZEROES
                  EXEC CICS READPREV
                            DATASET(FIO-VOID)
                            SET(FIO-PNT0)
                            LENGTH(FIO-RLEN)
                            RIDFLD(FIO-VKEY)
                  END-EXEC.


      *           IF NOT EIBRESP = ZEROES
      *              EXEC CICS ENDBR
      *                        DATASET(FIO-VOID)
      *              END-EXEC
      *              MOVE '23' TO FIO-STAT
      *              GO TO FIN-FIO.

           GO TO STT-FIO.
      *
       CON-CIC-FIO-GLEQ.

           IF EIBRESP = ZEROES
      *
               PERFORM GNS-SBR-VSI.
      *
           IF EIBRESP = ZEROES
              EXEC CICS READNEXT
                        DATASET(FIO-VOID)
                        SET(FIO-PNT0)
                        LENGTH(FIO-RLEN)
                        RIDFLD(FIO-VKEY)
              END-EXEC

               IF EIBRESP = ZEROES
                  EXEC CICS READPREV
                            DATASET(FIO-VOID)
                            SET(FIO-PNT0)
                            LENGTH(FIO-RLEN)
                            RIDFLD(FIO-VKEY)
                  END-EXEC

                   IF EIBRESP = ZEROES
                       IF FIO-VKEY NOT = FIO-CKEY
                          EXEC CICS READPREV
                                    DATASET(FIO-VOID)
                                    SET(FIO-PNT0)
                                    LENGTH(FIO-RLEN)
                                    RIDFLD(FIO-VKEY)
                          END-EXEC

                           IF NOT EIBRESP = ZEROES
                               EXEC CICS ENDBR
                                    DATASET(FIO-VOID)
                               END-EXEC
                               MOVE '23' TO FIO-STAT
                               GO TO FIN-FIO.
       FIN-CIC-FIO-GLEQ.
           GO TO STT-FIO.

       CIC-FIO-EBAL.
           MOVE 1 TO FIO-IAUX.
       LUP-CIC-FIO-EBAL.
           IF FIO-NFIL( FIO-IAUX ) > SPACES
               MOVE FIO-NFIL( FIO-IAUX ) TO FIO-VOID
               EXEC CICS ENDBR
                     DATASET(FIO-VOID)
               END-EXEC
               MOVE SPACES TO FIO-NFIL( FIO-IAUX )
               ADD 1 TO FIO-IAUX
               IF FIO-IAUX NOT > FIO-MXFL
                   GO TO LUP-CIC-FIO-EBAL.
       FIN-CIC-FIO-EBAL.
      *    GO TO STT-FIO.
           GO TO FIN-FIO.

       CIC-FIO-FNSF.
           MOVE 'ABORTO : COMANDO NO SOPORTADO, PARA ARCHIVO '
                         TO FIO-MEN1.
           MOVE FIO-VOID TO FIO-MEN2.
       CIC-FIO-FABT.
           EXEC CICS XCTL
                     PROGRAM('GNSPPABT')
                     COMMAREA(FIO-MENS)
                     LENGTH(+79)
           END-EXEC.
           EXEC CICS RETURN END-EXEC.

       CIC-FIO-BKOU.
      *JSS-INI 16-DIC-1994
      *    IF FRM-MENS NOT > SPACES
      *        MOVE FIO-MENS TO FRM-MENS.
      *    EXEC CICS XCTL
      *              PROGRAM('GNSPPABT')
      *              COMMAREA(FRM-MENS)
      *              LENGTH(+79)
      *    END-EXEC.
      *    EXEC CICS RETURN END-EXEC.
           EXEC CICS SYNCPOINT ROLLBACK END-EXEC.
      *JSS-FIN 16-DIC-1994

      *JSS-INI 21-SEP-1995
       FIN-CIC-FIO-BKOU.
           GO TO STT-FIO.
       CIC-FIO-CPNT.
           EXEC CICS SYNCPOINT END-EXEC.
      *JSS-FIN 21-SEP-1995

       STT-FIO.
           IF EIBRESP = 12 AND
              FIO-VOID = 'GNSMCCS'
               MOVE 13 TO EIBRESP
           ELSE
           IF EIBRESP NOT = ZEROES
               MOVE SPACES               TO FIO-MENS-DSC
               MOVE 'Status VSI :'       TO FIO-MENS-DSC
               MOVE EIBRESP              TO FIO-MENS-NTT
               MOVE 'Key'                TO FIO-MENS-GKY
               MOVE 'CMD'                TO FIO-MENS-GCM
               MOVE FIO-CMND             TO FIO-MENS-CMD
               MOVE 'FILE'               TO FIO-MENS-GFL
               MOVE FIO-VOID             TO FIO-MENS-FIL
               IF FIO-AKEY > SPACES
                   MOVE FIO-AKEY         TO FIO-MENS-KEY
               ELSE
                   MOVE 'PRINCIPAL'      TO FIO-MENS-KEY.
           IF EIBRESP = ZEROES
               MOVE '00' TO FIO-STAT
           ELSE
           IF EIBRESP = 15
               MOVE '02' TO FIO-STAT
           ELSE
           IF EIBRESP = 14
               MOVE '22' TO FIO-STAT
           ELSE
           IF EIBRESP = 20
               MOVE '10' TO FIO-STAT
           ELSE
           IF EIBRESP = 13
               MOVE '23' TO FIO-STAT
           ELSE
      *         MOVE 'Aborto, VSI:'       TO FIO-MENS-DSC
      *         MOVE '99' TO FIO-STAT
      *         GO TO CIC-FIO-FABT.
               MOVE '23' TO FIO-STAT.
       FIN-STT-FIO.

       CIC-FIO-FDMY.
       FIN-FIO.
           EXIT.

       GNS-SBR-VSI SECTION.
       INI-GNS-SBR-VSI.
           MOVE 1 TO FIO-IAUX.
       LUP-GNS-SBR-VSI.
           IF FIO-NFIL( FIO-IAUX ) > SPACES
               ADD 1 TO FIO-IAUX
               IF FIO-IAUX NOT > FIO-MXFL
                   GO TO LUP-GNS-SBR-VSI
               ELSE
                   MOVE 'OVERFLOW:VARIABLE FIO-NFIL, EN GNS-SBR-VSI' TO
                        FIO-MENS
                   PERFORM CIC-FIO-FABT.
           MOVE FIO-VOID TO FIO-NFIL( FIO-IAUX ).
       FIN-GNS-SBR-VSI.
           EXIT.


