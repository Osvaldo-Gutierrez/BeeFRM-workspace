      ******************************************************************
      *DAD ini  17-DEC-1991
      *
      *    Proceso para Cargar tablas MISCELANEAS en Memoria y Busqueda
      *                 tablas en Memoria de un Codigo miscelaneo especi
      *
      *    COPY    :  GNSBRMSC. ( Registro )
      *               GNSWGCMM. ( Variables Trabajo )
      *               GNSBFMSC. ( Fio )
      *-----------------------------------------------------------------
      *    Proceso :  Cargar tablas MISCELANEAS  en Memoria.
      *               Perform GNS-CRG-CMM.
      *
      *    INPUT   :  CMM-INIC. ( Inicializa ? "N")
      *               CMM-SIST. ( Sistema )
      *               CMM-TMSC. ( Miscelaneo )
      *               CMM-VIGE. ( Vigencia ? "S" )
      *
      *    OUTPUT  :  CMM-VECT. ( con los registros GNSBRMSC cargados )
      *               CMM-CMSG.
      *               CMM-MEN2.
      *-----------------------------------------------------------------
      *    Proceso :  Buscar en tablas MISCELANEAS  en Memoria, de un Co
      *               miscelaneo especifico, entregandolo en el registro
      *               Perform GNS-BUS-CMM.
      *
      *    INPUT   :  CMM-SIST.
      *               CMM-TMSC.
      *               CMM-CMSC.
      *
      *    OUTPUT  :  GNSBRMSC. ( Registro )
      *               CMM-CMSG.
      *               CMM-MEN2.
      *
      *DAD fin  17-DEC-1991
      ******************************************************************
      *DAD ini  17-DEC-1991
      * Proceso para Cargar tablas MISCELANEAS  en Memoria
       GNS-CRG-CMM SECTION.
       INI-GNS-CRG-CMM.
      * Inicializacion de Variables para Proceso de Cargar tablas MSC en
           IF CMM-INIC IN CMM-VARI = "S" OR
              CMM-PASO IN CMM-VARI = 0
               MOVE ZEROES TO CMM-MAXI IN CMM-VARI
               MOVE SPACES TO CMM-VECT IN CMM-VARI.
      
           IF CMM-PASO IN CMM-VARI = 0
               MOVE  1  TO CMM-PASO IN CMM-VARI.
      
           MOVE ZEROES TO CMM-SUBI IN CMM-VARI.
           MOVE SPACES TO CMM-CMSG IN CMM-VARI
                          CMM-MEN2 IN CMM-VARI.
      
      * Inicio Carga de tablas MISCELANEAS  en Memoria
           MOVE CMM-SIST IN CMM-VARI TO MSC-COD-SIST  FIO-SIST.
           MOVE CMM-TMSC IN CMM-VARI TO MSC-COD-TMSC IN MSC.
           MOVE SPACES               TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS OR
              MSC-COD-TTAB IN MSC NOT = "MSC" OR
              ( CMM-TMSC IN CMM-VARI NOT = SPACES AND
                CMM-TMSC IN CMM-VARI NOT = MSC-COD-TMSC IN MSC )
               MOVE "TABMSC NOHAY"       TO CMM-CMSG IN CMM-VARI
               MOVE CMM-TMSC IN CMM-VARI TO CMM-MEN2 IN CMM-VARI
               GO TO FIN-GNS-CRG-CMM.
       LUP-GNS-CRG-CMM.
      * Llena Vector con Codigo y Datos de tablas MISCELANEAS
           IF CMM-VIGE IN CMM-VARI NOT = "S"
               ADD  1  TO CMM-SUBI IN CMM-VARI
               MOVE CMM-SIST IN CMM-VARI
                                TO CMM-SMSC IN CMM-VARI(CMM-SUBI)
               MOVE MSC         TO CMM-RMSC IN CMM-VARI(CMM-SUBI)
           ELSE
           IF CMM-VIGE IN CMM-VARI = MSC-IND-VIGE IN MSC
               ADD  1  TO CMM-SUBI IN CMM-VARI
               MOVE CMM-SIST IN CMM-VARI
                                TO CMM-SMSC IN CMM-VARI(CMM-SUBI)
               MOVE MSC         TO CMM-RMSC IN CMM-VARI(CMM-SUBI).
      
           IF CMM-SUBI IN CMM-VARI > CMM-OCUR IN CMM-VARI
               MOVE "TAB    CHICA"    TO CMM-CMSG IN CMM-VARI
               MOVE "DE MISCELANEOS " TO CMM-MEN2 IN CMM-VARI
               GO TO FIN-GNS-CRG-CMM.
       NXT-GNS-CRG-CMM.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS OR
              MSC-COD-TTAB IN MSC NOT = "MSC" OR
              ( CMM-TMSC IN CMM-VARI NOT = SPACES AND
                CMM-TMSC IN CMM-VARI NOT = MSC-COD-TMSC IN MSC )
               MOVE CMM-SUBI IN CMM-VARI TO CMM-MAXI IN CMM-VARI
               GO TO FIN-GNS-CRG-CMM.
           GO TO LUP-GNS-CRG-CMM.
       FIN-GNS-CRG-CMM.
           EXIT.
      *DAD fin  17-DEC-1991
      ******************************************************************
      *DAD ini  17-DEC-1991
      * Busca con Codigo y Sistema en la tablas MISCELANEAS cargada en M
      * la informacion miscelanea requerida
       GNS-BUS-CMM SECTION.
       INI-GNS-BUS-CMM.
           MOVE SPACES TO MSC
                          CMM-CMSG IN CMM-VARI
                          CMM-MEN2 IN CMM-VARI.
           SET CMM-NUME             TO 1.
           SEARCH CMM-VMSC IN CMM-VARI VARYING CMM-NUME
                  WHEN ( CMM-SMSC(CMM-NUME) = CMM-SIST IN CMM-VARI AND
                         CMM-KMSC(CMM-NUME) = CMM-TABL IN CMM-VARI )
                       GO TO FND-GNS-BUS-CMM.
           MOVE "TABCOD NOFND"   TO CMM-CMSG IN CMM-VARI.
           MOVE "DE MISCELANEOS" TO CMM-MEN2 IN CMM-VARI.
           GO TO FIN-GNS-BUS-CMM.
       FND-GNS-BUS-CMM.
           MOVE CMM-RMSC(CMM-NUME) TO MSC.
       FIN-GNS-BUS-CMM.
           EXIT.
      *DAD fin  17-DEC-1991
      ******************************************************************
