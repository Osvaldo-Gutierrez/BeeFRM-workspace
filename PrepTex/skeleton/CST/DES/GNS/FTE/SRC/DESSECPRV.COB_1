IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	DESSECPRV.
AUTHOR.		GUZMAN Y CIA.

* Genera Archivo Temporal PRV para Manejo de Privilegios

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============

WORKING-STORAGE SECTION.
*-----------------------
COPY	'DESREG.SEC'	FROM	DICTIONARY
	REPLACING
	==SEC==		BY	==SEC EXTERNAL==.
COPY	'DESREG.PRV'	FROM	DICTIONARY
	REPLACING
	==PRV==		BY	==PRV EXTERNAL==.
COPY	SYSWSS	IN	TXT.
COPY	STRWSS	IN	TXT.
COPY	PRVWSS	IN	DESTXT.
COPY	FIOWSS	IN	DESTXT.
01 WSS-VARI.
	03 WSS-ZERO	COMP		VALUE 0	PIC 9(04).
	03 WSS-AUXI	COMP			PIC 9(04).

PROCEDURE DIVISION.
*==================

MAIN SECTION.
*------------
INI-MAIN.
	MOVE FIO-IFNF-PGM TO FIO-IFNF.
	CALL 'DESFIOSEC' USING FIO-FNF.
	CALL 'DESFIOSEC' USING FIO-INP.
	IF NOT FIO-STAT-OKS
		CALL 'DESFIOPRV' USING FIO-OUT
		CALL 'DESFIOPRV' USING FIO-CLO
		STOP RUN.
	CALL 'DESFIOPRV' USING FIO-OUT.
	CALL 'SYS$GETJPI'	USING
		BY VALUE 	0 0 0
		BY REFERENCE SYS-IUSE
		BY VALUE	0 0 0.
	CALL 'SYS$GETJPI'	USING
		BY VALUE 	0 0 0
		BY REFERENCE SYS-IUIC
		BY VALUE	0 0 0.
	CALL 'SYS$IDTOASC'	USING
		BY VALUE	SYS-UUIC
		BY REFERENCE	SYS-LOWN
		BY DESCRIPTOR	SYS-OWNE
		BY REFERENCE	SYS-RESI
				SYS-ATTR
				SYS-CNTX.
LUP-MAIN.
	MOVE SPACES TO SEC.
	CALL 'DESFIOSEC' USING FIO-GET-NXT.
	IF NOT FIO-STAT-OKS
		GO TO FIN-MAIN.
	MOVE SPACES TO PRV-USER PRV-OWNE.
	UNSTRING SEC DELIMITED BY '/' INTO PRV-USER PRV-OWNE PRV-NEMO
					   PRV-CMND PRV-STAT.
	MOVE SYS-USER TO STR-CAND.
	MOVE PRV-USER TO STR-PATT.
	PERFORM PRV-MTCH.
	IF NOT STR-MTCH-YES
		GO TO LUP-MAIN.
	MOVE SYS-OWNE TO STR-CAND.
	MOVE PRV-OWNE TO STR-PATT.
	PERFORM PRV-MTCH.
	IF NOT STR-MTCH-YES
		GO TO LUP-MAIN.
	MOVE SEC TO PRV.
	CALL 'DESFIOPRV' USING FIO-PUT.
	IF NOT FIO-STAT-OKS
		DISPLAY 'ERROR al crear registro en archivo PRV : ABORTO'
		DIVIDE WSS-ZERO INTO WSS-AUXI.
	GO TO LUP-MAIN.
FIN-MAIN.
	CALL 'DESFIOSEC' USING FIO-CLO.
	CALL 'DESFIOPRV' USING FIO-CLO.
	STOP RUN.
	
PRV-MTCH SECTION.
*----------------
INI-PRV-MTCH.
	IF SPACES = STR-PATT OR STR-CAND OR NOT STR-PCHR(STR-ILIM)
		GO TO MTC-PRV-MTCH.
	PERFORM BUS-CHAR VARYING STR-IAST FROM STR-ILIM BY -1
		UNTIL STR-IAST < 1 OR STR-PCHR(STR-IAST) = '*'.
	IF STR-IAST < 1
		GO TO MTC-PRV-MTCH.
	PERFORM BUS-CHAR VARYING STR-IMAX FROM STR-ILIM BY -1 UNTIL
		STR-CCHR(STR-IMAX) NOT = SPACES
	PERFORM BUS-CHAR VARYING STR-INBL FROM STR-ILIM BY -1 UNTIL
		STR-PCHR(STR-INBL) NOT = SPACES.
	IF STR-INBL NOT < STR-IMAX
		GO TO MTC-PRV-MTCH.
	PERFORM MOV-CHAR VARYING STR-ISTR FROM 1 BY 1
		UNTIL STR-ISTR > STR-INBL - STR-IAST.
	PERFORM PUT-ASTE VARYING STR-ISTR FROM 1 BY 1
		UNTIL STR-ISTR > STR-IMAX - STR-INBL.
MTC-PRV-MTCH.
	CALL 'STR$MATCH_WILD'	USING
		BY DESCRIPTOR	STR-CAND
		BY DESCRIPTOR	STR-PATT
		GIVING		STR-MTCH.
FIN-PRV-MTCH.
	EXIT.

BUS-CHAR SECTION.
*----------------
INI-BUS-CHAR.
FIN-BUS-CHAR.
	EXIT.

MOV-CHAR SECTION.
*----------------
INI-MOV-CHAR.
	MOVE STR-PCHR( STR-INBL - STR-ISTR + 1 ) TO
		STR-PCHR( STR-IMAX - STR-ISTR + 1 ).
FIN-MOV-CHAR.
	EXIT.

PUT-ASTE SECTION.
*----------------
INI-PUT-ASTE.
	MOVE '*' TO STR-PCHR( STR-IAST + STR-ISTR ).
FIN-PUT-ASTE.
	EXIT.
