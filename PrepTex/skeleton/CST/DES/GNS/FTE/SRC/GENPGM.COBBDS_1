IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	GENPGM.
AUTHOR.		Julio Sepulveda.

* Genera Archivo de Edicion para Generacion de Programa Interactivo.

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============

WORKING-STORAGE SECTION.
*-----------------------
COPY	'FRMDATRPF'	IN	GEN.
COPY	'DESREG.RPF'	FROM	DICTIONARY
	REPLACING
	==RPF==		BY	==RPF EXTERNAL==.
COPY	FIOWSS		IN	DESTXT.
01 WSS-VARI.
	03 WSS-KEY-DPRO				PIC X(12).
	03 WSS-MENS				PIC X(80).
	03 WSS-STAT	COMP		VALUE 0	PIC 9(09).
		88 WSS-STAT-OKS		VALUE 0.
		88 WSS-STAT-MAL		VALUE 1.

PROCEDURE DIVISION.
*==================

MAIN SECTION.
*------------
INI-MAIN.
	CALL 'DESFIORPF' USING  '<INP>'.
	CALL 'LIB$GET_SYMBOL' USING
		BY DESCRIPTOR	'FIL'
				WSS-KEY-DPRO.
	PERFORM FND-KEY.
	IF NOT FIO-STAT-OKS
		MOVE 'NO EXISTE Definicion en Archivo RPF' TO WSS-MENS
		GO TO MSG-MAIN.
	PERFORM LEE-RPF.
	IF NOT FIO-STAT-OKS
		MOVE 'Definicion INCOMPLETA en Archivo RPF' TO WSS-MENS
		GO TO MSG-MAIN.
	CALL 'GENPGMEDT' USING
		BY DESCRIPTOR	RPF-KEY-DPRO IN RPF-FLD
				RPF-GLS-DAUT IN RPF-FLD
				RPF-GLS-ABAS IN RPF-FLD
				RPF-GLS-NFRM IN RPF-FLD
				RPF-GLS-LFRM IN RPF-FLD
				RPF-GLS-UARA IN RPF-FLD
		BY REFERENCE	WSS-STAT.
	CALL 'DESFIORPF' USING '<CLO>'.
	IF NOT WSS-STAT-MAL
		GO TO FIN-MAIN.
	MOVE 'ERROR en GENERACION de Archivo de EDICION' TO WSS-MENS.
MSG-MAIN.
	DISPLAY WSS-KEY-DPRO , ': ' , WSS-MENS.
	CALL 'SYS$EXIT' USING
		BY VALUE 0.
FIN-MAIN.
	STOP RUN.

FND-KEY SECTION.
*---------------
INI-FND-KEY.
	MOVE SPACES		TO RPF-KEY-DPTR.
	MOVE WSS-KEY-DPRO	TO RPF-KEY-DPRO IN RPF.
	MOVE 'A'		TO RPF-KEY-TREG.
	CALL 'DESFIORPF' USING  '<GET_KEY>'.
FIN-FND-KEY.
*-----------
	EXIT.

LEE-RPF SECTION.
*---------------
INI-LEE-RPF.
	MOVE RPF-KEY-DPRO IN RPF TO RPF-KEY-DPRO IN RPF-FLD.
	MOVE RPF-GLS-DAUT IN RPF TO RPF-GLS-DAUT IN RPF-FLD.

	MOVE 'B' TO RPF-KEY-TREG.
	CALL 'DESFIORPF' USING  '<GET_KEY>'.
	IF NOT FIO-STAT-OKS
		GO TO FIN-LEE-RPF.
	MOVE RPF-GLS-ABAS IN RPF TO RPF-GLS-ABAS IN RPF-FLD.

	MOVE 'C ' TO RPF-KEY-TRYC.
	CALL 'DESFIORPF' USING  '<GET_KEY>'.
	IF NOT FIO-STAT-OKS
		GO TO FIN-LEE-RPF.
	MOVE RPF-GLS-UARA IN RPF TO RPF-GLS-UARA IN RPF-FLD.

	MOVE 'P0' TO RPF-KEY-TRYC.
	CALL 'DESFIORPF' USING  '<GET_KEY>'.
	IF NOT FIO-STAT-OKS
		GO TO FIN-LEE-RPF.
	MOVE RPF-GLS-LFRM IN RPF TO RPF-GLS-LFRM IN RPF-FLD.
	MOVE RPF-GLS-NFRM IN RPF TO RPF-GLS-NFRM IN RPF-FLD.
FIN-LEE-RPF.
*-----------
	EXIT.
