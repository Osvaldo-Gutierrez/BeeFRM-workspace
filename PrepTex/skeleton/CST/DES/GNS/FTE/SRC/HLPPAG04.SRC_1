*%! SETEAR AMBIENTE PARA GENERAR VSI O DTC
       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. HLPPAG04.
       AUTHOR. CST.
       DATE-WRITTEN.  3-FEB-1993 18:36:00.

      * PROCESADOR DE AYUDA DE TABLAS GENERALES CON Y SIN
      * BUSQUEDA GENERICA SEGUN PARAMETRO

       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
                     DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------

       COPY GNSWVSCR.
       COPY GNSWCSCR.
       COPY GNSBRTAB.
       COPY GNSBRMSC.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFRM.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       COPY GNSWGQUE.

       COPY GNSWGCBG.
       COPY GNSWIDOH.

*% IF PGM_DTC

       COPY GNSBRIDD.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.

       01  WSS-IDD.
           03  WSS-QIDD.
               05  WSS-NIDD VALUE 'GIDD'    PIC X(04).
               05  WSS-TIDD                 PIC X(04).
*% END

       01  WSS-SCR-VARI-V20.
           03  WSS-SCR-TCMA-V20     COMP         PIC S9(04).
           03  WSS-SCR-SIST-V20                  PIC  X(03).
           03  WSS-SCR-FUNC-V20                  PIC  X(08).
           03  WSS-SCR-NEMO-V20                  PIC  X(12).
           03  WSS-SCR-RFBY-V20                  PIC  X(01).
           03  WSS-SCR-NTRV-V20                  PIC  X(04).
           03  FILLER                            PIC  X(01).
           03  WSS-SCR-NTRN-V20                  PIC  X(04).
           
       01  WSS-SCR-VARI-V31.
           03  WSS-SCR-TCMA-V31     COMP         PIC S9(04).
           03  WSS-SCR-SIST-V31                  PIC  X(03).
           03  WSS-SCR-SISG-V31                  PIC  X(03).
           03  WSS-SCR-NTRN-V31                  PIC  X(04).
           
       01  WSS-VARI.
      *       Tamano del mapa
           03 WSS-TFLD  COMP      VALUE +0             PIC S9(04).
      *       Posicion del cursor
           03 WSS-PCUR  COMP      VALUE +0             PIC S9(04).
      *       Mapa
           03 WSS-DFRM                                 PIC X(3000).
      *       Common Area auxiliar
           03 WSS-CMMA                                 PIC X(3500).
           03 WSS-FEC-FHOY.
              05 WSS-NUM-DHOY                PIC 9(02).
              05 FILLER                      PIC X(01) VALUE '/'.
              05 WSS-NUM-MHOY                PIC 9(02).
              05 FILLER                      PIC X(01) VALUE '/'.
              05 WSS-NUM-SHOY                PIC 9(02).
              05 WSS-NUM-AHOY                PIC 9(02).


           03 WSS-TAB-COD-TTAB              PIC X(04).
           03 WSS-TAB-COD-CTAB              PIC X(12).
           03 WSS-CMA-ARG-TTAB              PIC X(04).
      *
      *    Codigo Miscelaneo
           03  WSS-MSC-COD-MSCL.
      *
      *        Tipo Miscelaneo
               05  WSS-MSC-COD-TMSC                 PIC X(04).
      *
      *        Codigo Miscelaneo
               05  WSS-MSC-COD-CMSC                 PIC X(08).

       01  LLAVE               VALUE SPACES.
           02 TABL             PIC X(3).
           02 LLAV             PIC X(12).
           COPY DFHBMSCA.

       01  VARIABLES.
           02 WSS-TOT-TABL     VALUE 19 PIC 9(02).
           02 BUSCA-PRIMER-REGISTRO  VALUE 'NO' PIC X(02).
           02 WSS-ARG.
              04 WSS-ARG-ELM      OCCURS 40 PIC X.
           02 I                PIC 99.
           02 J                PIC 99.
           02 K                PIC 99.
           02 L                PIC 99.
           02 M                PIC 99.
      * ISP: LIMITE DE BUSQUEDA DE STRING RN B. GENERICA
      *      SE PUEDE ACORTAR PARAR MEJORAR PERFORMANCE
           02 WSS-LIM-BUSQ     PIC 9(02) VALUE 40.
           02 WSS-HLP-TRAN     PIC X(04) VALUE 'HL04'.
           02 REC              PIC XX.
           02 ENC              PIC XX.
           02 WSS-DIR-LEC      PIC X(03).
           02 STOPP            PIC XX.
           02 TSQ.
              03 TSQ-TERM      PIC X(4).
              03 FILLER        PIC X(4) VALUE 'HELP'.

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER           PIC X(3500).

*% IF NOT PGM_DTC
       COPY GNSLGFIO.
*% END
       PROCEDURE DIVISION.
      *===================
*% IF PGM_DTC
           ENTRY 'DBMSCBL'.
*% ELSE
           PERFORM GNS-HDL-VSM.
*% END
           EXEC CICS HANDLE AID
                 CLEAR(TERMINAR)
                 PA1(TERMINAR)
                 PA2(TERMINAR)
           END-EXEC.

           MOVE DFHCOMMAREA TO CMA-ARG.

*% IF PGM_DTC
           MOVE EIBTRMID TO WSS-TIDD.
           MOVE WSS-QIDD TO SCR-QIDD.
           MOVE +490 TO SCR-LIDD.
           MOVE 'HLPPAG04' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
*% END

           IF CMA-ARG-IVEZ = 'S'
               MOVE 'N' TO CMA-ARG-IVEZ
               PERFORM PRIMERA-VEZ.
           GO TO RECIBIR.

       PRIMERA-VEZ.
      *------------
           MOVE SPACES      TO CMA-ARG-ASCE.
           MOVE SPACES      TO CMA-ARG-DESC.
           MOVE SPACES         TO LLAVE.
           MOVE CMA-ARG-TTAB        TO TABL.

           MOVE LOW-VALUES     TO DOH-FLD.
           MOVE 'SI' TO BUSCA-PRIMER-REGISTRO
           MOVE 'PF8' TO WSS-DIR-LEC.
           MOVE TABL           TO TAB-COD-TTAB IN TAB.
           MOVE CMA-ARG-TTAB-MSC TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM CST-FIO-TAB.

           PERFORM GET-FHOY.
           MOVE HOY-DHOY     TO WSS-NUM-DHOY.
           MOVE HOY-MHOY     TO WSS-NUM-MHOY.
           MOVE HOY-SHOY     TO WSS-NUM-SHOY.
           MOVE HOY-AHOY     TO WSS-NUM-AHOY.
           MOVE WSS-FEC-FHOY TO FRM-GLS-FPRO.

           IF CMA-ARG-ARGB > SPACES
                PERFORM DET-CMA-ARG-SIZE VARYING
                        CMA-ARG-SIZE FROM 10 BY -1
                 UNTIL CMA-ARG-SIZE = 0 OR
                 CMA-ARG-ARGB-ELM(CMA-ARG-SIZE) > SPACE.

           PERFORM SCRL-TAB-TAB-PF8.

           PERFORM ENVIAR-MAPA-COMPLETO.
           PERFORM AUTO-INVOC.

       ENVIAR-MAPA-COMPLETO.
      *---------------------
           EXEC CICS SEND MAP('HLPDETA')
                               MAPSET('HLPADET')
                               FROM(DOH-FLD)
                               ERASE
           END-EXEC.

       ENVIAR-MAPA-DATOS.
      *------------------
           EXEC CICS SEND      MAP('HLPDETA')
                               MAPSET('HLPADET')
                               FROM(DOH-FLD)
                               DATAONLY
           END-EXEC.

       AUTO-INVOC.
      *-----------
           EXEC CICS RETURN    TRANSID(WSS-HLP-TRAN)
                               COMMAREA(CMA-ARG)
                               LENGTH(CMA-ARG-L)
           END-EXEC.

       RECIBIR-MAPA.
      *-------------
           EXEC CICS RECEIVE   MAP('HLPDETA') MAPSET('HLPADET')
                               INTO(DOH-FLD)
           END-EXEC.
           MOVE SPACES TO DOH-GLS-MENS.

       RECONOCER.
      *----------
           MOVE 1    TO K.
           MOVE 'NO' TO REC.
           PERFORM BUSCAR THRU F-BUS.

       BUSCAR.
      *-------
           MOVE 1    TO L.
           MOVE 'NO' TO ENC.
           PERFORM ENCLAVAR VARYING M FROM K BY 1 UNTIL
                               ENC = 'SI' OR M > WSS-LIM-BUSQ
           IF ENC = 'SI'
               MOVE 'NO' TO STOPP
               COMPUTE K = M - 1
               PERFORM COMPAR VARYING M FROM K BY 1
                              UNTIL M > WSS-LIM-BUSQ     OR
                              L > CMA-ARG-SIZE     OR
                              STOPP = 'SI'
               IF L > CMA-ARG-SIZE
                  MOVE 'SI'  TO REC
               ELSE
                  IF (WSS-LIM-BUSQ - K) > CMA-ARG-SIZE
                       ADD 1 TO K
                       GO TO BUSCAR.
       F-BUS.
      *------
       ENCLAVAR.
      *---------
           IF CMA-ARG-ARGB-ELM(L) = WSS-ARG-ELM (M)
                 MOVE 'SI' TO ENC.
       COMPAR.
      *-------
           IF CMA-ARG-ARGB-ELM(L) = WSS-ARG-ELM (M)
               ADD 1 TO L
           ELSE
               MOVE 'SI' TO STOPP.

       RECIBIR.
      *--------
           PERFORM RECIBIR-MAPA.
      *ISP: PF12
           IF EIBAID = '@' OR '<'
                PERFORM GNS-RET-MEN
           ELSE
           IF EIBAID = '3' OR 'C'
                PERFORM TERMINAR
           ELSE
           IF EIBAID = '7' OR 'G'
                MOVE 'PF7' TO WSS-DIR-LEC
                PERFORM SCRL-TAB-TAB-PF7
           ELSE
           IF EIBAID = '8' OR 'H'
                MOVE 'PF8' TO WSS-DIR-LEC
                PERFORM SCRL-TAB-TAB-PF8.

           PERFORM ENVIAR-MAPA-DATOS THRU AUTO-INVOC.

       GNS-RET-MEN.
      *------------
           MOVE SPACES TO CMA-ARG-ARGB.
           MOVE 'S'    TO CMA-ARG-IVEZ.
           MOVE 'S'    TO CMA-ARG-GENE.
           MOVE SPACES TO CMA-ARG-ASCE.
           MOVE SPACES TO CMA-ARG-DESC.
           MOVE ZEROES TO CMA-ARG-SIZE.
           ADD 1 TO CMA-ARG-L.
           EXEC CICS XCTL
                 PROGRAM ('GNSPPPK1')
                 COMMAREA(CMA-ARG)
                 LENGTH  (CMA-ARG-L)
           END-EXEC.

       CANCELAR.
      *---------
           PERFORM ENVIAR-MAPA-DATOS THRU AUTO-INVOC.
       TERMINAR.
      *---------
           PERFORM RET-FPEN.
       FIN-TERMINAR.

       RET-FPEN SECTION.
       INI-RET-FPEN.
           PERFORM GNS-ERR-QUE.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE 1        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-RET-FPEN.
      *Ya leyo primer registro de la cola para detectar si esta existia
           IF QUE-IKEY NOT = 'MAP'
               GO TO FIN-RET-FPEN.
           MOVE QUE-DATA TO WSS-DFRM.
           MOVE QUE-LDAT TO WSS-TFLD.
           MOVE QUE-PCUR TO WSS-PCUR.
           MOVE 2        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-RET-FPEN.

           IF NOT ( QUE-IKEY = 'CM2' OR 'CM3' )
               GO TO FIN-RET-FPEN.
           MOVE QUE-LDAT TO SCR-TCMA.
           MOVE QUE-DATA TO WSS-CMMA.

           IF QUE-IKEY = 'CM2'
               MOVE QUE-DATA TO WSS-SCR-VARI-V20
               MOVE WSS-SCR-NTRN-V20 TO SCR-NTRN.

           IF QUE-IKEY = 'CM3'
               MOVE QUE-DATA TO WSS-SCR-VARI-V31
               MOVE WSS-SCR-NTRN-V31 TO SCR-NTRN.

           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE QUE-DEL  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           EXEC CICS SEND FROM(WSS-DFRM)
                          LENGTH(WSS-TFLD)
                          ERASE
           END-EXEC.
           EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
                          TERMINAL
           END-EXEC.
           PERFORM SCR-EXIT.
       FIN-RET-FPEN.
           EXIT.

       SCR-EXIT SECTION.
       INI-SCR-EXIT.
           EXEC CICS RETURN
                     TRANSID(SCR-NTRN)
                     COMMAREA(WSS-CMMA)
                     LENGTH(SCR-TCMA)
           END-EXEC.
       COPY GNSBGGBK.
       FIN-SCR-EXIT.
           EXIT.
       COPY GNSBGFRM.
       COPY GNSBGQUE.
       COPY GNSBGHOY.
       COPY GNSBGSYS.
       COPY GNSBFTAB.
       COPY GNSBIABT.
*% IF PGM_DTC
       COPY GNSBBIDD.
       COPY GNSBGDTC.

       SET-NUM-IDD SECTION.
       INI-SET-NUM-IDD.
      * ISP: SI EN HLPMPOS EXISTE UN NUMERO SE DEJA UNA IDD PARA EL
      *      SISTEMA '***' CON NUMERO DE BASE DE DATOS DE HLPMPOS
      *
           IF CMA-ARG-SIST IS NUMERIC
                MOVE '***'    TO FIO-SIST

                MOVE '***'   TO IDD-SIST(1)
                MOVE CMA-ARG-SIST TO IDD-IENT(1)
                MOVE 'C'     TO IDD-GRPO(1)
                MOVE SPACES  TO IDD-RGST(1)
                MOVE 'D'     TO IDD-TENT(1)
           ELSE
                MOVE CMA-ARG-SIST  TO FIO-SIST.
       FIN-SET-NUM-IDD.
           EXIT.

*% ELSE
       SET-NUM-IDD SECTION.
       INI-SET-NUM-IDD.
           MOVE CMA-ARG-SIST TO FIO-SIST.
       FIN-SET-NUM-IDD.
           EXIT.

       COPY GNSBGVSM.
       COPY GNSBHVSM.
*% END


       PUT-TIT SECTION.
       INI-PUT-TIT.
      *ISP TITULA SISTEMA, TABLA
           MOVE CMA-ARG-SIST        TO DOH-COD-SIST.
           MOVE TAB-COD-TTAB IN TAB TO DOH-COD-TTAB.
           IF TAB-COD-TTAB IN TAB = 'MSC'
               MOVE '/'                 TO DOH-COD-DMSC
               MOVE TAB-COD-CTAB IN TAB TO DOH-COD-TMSC
           ELSE
               MOVE SPACES              TO DOH-COD-TMSC
                                           DOH-COD-TMSC.
           IF FIO-STAT-OKS
               MOVE TAB-COD-TABL TO CMA-ARG-ASCE
               MOVE TAB-GLS-DESC IN TAB TO DOH-GLS-TTAB
           ELSE
               MOVE 'PF3: Fin No Existe Tabla' TO DOH-GLS-MENS
               MOVE CMA-ARG-TTAB TO SOURX
               PERFORM ENVIAR-MAPA-COMPLETO
               PERFORM AUTO-INVOC.
       FIN-PUT-TIT.
           EXIT.

       CST-FIO-TAB SECTION.
       INI-CST-FIO-TAB.

           PERFORM SET-NUM-IDD.

       CIC-CST-FIO-TAB.

           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
                   GO TO FIN-CST-FIO-TAB.

           MOVE TAB TO MSC.

           IF CMA-ARG-TTAB = 'MSC' AND
      *ISP: HELP DE MISCELANEOS DESDE FORMULARIO 'GNSFTAB'
              CMA-ARG-TTAB-MSC > SPACES
               MOVE MSC-COD-TMSC IN MSC TO WSS-TAB-COD-TTAB
               MOVE CMA-ARG-TTAB-MSC    TO WSS-CMA-ARG-TTAB
               MOVE MSC-COD-CMSC IN MSC TO WSS-TAB-COD-CTAB
           ELSE
               MOVE CMA-ARG-TTAB        TO WSS-CMA-ARG-TTAB
               MOVE TAB-COD-TTAB IN TAB TO WSS-TAB-COD-TTAB
               MOVE TAB-COD-CTAB IN TAB TO WSS-TAB-COD-CTAB.

           IF FIO-STAT-OKS
               IF WSS-CMA-ARG-TTAB NOT = WSS-TAB-COD-TTAB
                   MOVE '23' TO FIO-STAT
                   GO TO FIN-CST-FIO-TAB.

           IF WSS-TAB-COD-CTAB NOT > SPACES AND
              BUSCA-PRIMER-REGISTRO = 'SI'
                PERFORM PUT-TIT.

      *     IF BUSCA-PRIMER-REGISTRO = 'SI'
      *           MOVE TAB-COD-TABL TO CMA-ARG-ASCE
      *      ELSE
      *           MOVE 'Tabla Sin Registros ( PF3 Para Retornar )'
      *                                            TO DOH-GLS-MENS
      *           MOVE CMA-ARG-TTAB TO SOURX
      *           GO TO FIN-CST-FIO-TAB.

      *ISP: NO CONSIDERA REGISTROS NO VIGENTES
      *ISP:
      *     IF TAB-COD-TTAB IN TAB NOT = 'MSC'
      *         IF TAB-COD-CTAB IN TAB > SPACES AND
      *            TAB-IND-VIGE IN TAB NOT = 'S'
      *               PERFORM SET-DIR-LEC
      *               GO TO CIC-CST-FIO-TAB
      *         ELSE
      *            NEXT SENTENCE
      *     ELSE
      *         IF MSC-COD-CMSC IN MSC > SPACES AND
      *            MSC-IND-VIGE IN MSC NOT = 'S'
      *               PERFORM SET-DIR-LEC
      *               GO TO CIC-CST-FIO-TAB.

           IF CMA-ARG-ARGB NOT = SPACES
                IF WSS-TAB-COD-CTAB > SPACES
                   MOVE TAB-GLS-DESC IN TAB TO WSS-ARG
                   PERFORM RECONOCER
                   IF REC = 'NO'
                       PERFORM SET-DIR-LEC
                       GO TO CIC-CST-FIO-TAB.
       FIN-CST-FIO-TAB.
           EXIT.

       SET-DIR-LEC SECTION.
       INI-SET-DIR-LEC.
           IF WSS-DIR-LEC = 'PF7'
               MOVE FIO-GET-PRV TO FIO-CMND
           ELSE
               MOVE FIO-GET-NXT TO FIO-CMND.
       FIN-SET-DIR-LEC.
           EXIT.

       DET-CMA-ARG-SIZE SECTION.
       INI-DET-CMA-ARG-SIZE.
       FIN-DET-CMA-ARG-SIZE.
           EXIT.

       SCRL-TAB-TAB-PF8 SECTION.
       INI-SCRL-TAB-TAB-PF8.
           IF CMA-ARG-ASCE = HIGH-VALUES
               MOVE 'Fin de paginas' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF8.
           MOVE CMA-ARG-ASCE TO TAB-COD-TABL IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-COD-TABL IN TAB = CMA-ARG-ASCE )
               MOVE 'No existe tabla' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF8.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF FIO-STAT-OKS
               PERFORM LLENAR-TABLA-ASCE
               GO TO FIN-SCRL-TAB-TAB-PF8.
           MOVE 'Fin de paginas' TO DOH-GLS-MENS.
       FIN-SCRL-TAB-TAB-PF8.
           EXIT.

       LLENAR-TABLA-ASCE SECTION.
       INI-LLENAR-TABLA-ASCE.
           MOVE 1 TO FRM-IFLD.
       LUP-LLENAR-TABLA-ASCE.
           PERFORM CARGAR-TABLA.
           IF FRM-IFLD = 1
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-DESC
               MOVE HIGH-VALUES         TO CMA-ARG-ASCE
           ELSE
           IF FRM-IFLD = WSS-TOT-TABL
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-ASCE.
       NXT-LLENAR-TABLA-ASCE.
           ADD 1 TO FRM-IFLD.
           IF FRM-IFLD > WSS-TOT-TABL
               GO TO FIN-LLENAR-TABLA-ASCE.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF FIO-STAT-OKS
               GO TO LUP-LLENAR-TABLA-ASCE.

           PERFORM LIMPIAR-TABLA VARYING FRM-IFLD FROM FRM-IFLD BY 1
                                 UNTIL   FRM-IFLD > WSS-TOT-TABL.

       FIN-LLENAR-TABLA-ASCE.
           EXIT.

       SCRL-TAB-TAB-PF7 SECTION.
       INI-SCRL-TAB-TAB-PF7.
           IF CMA-ARG-DESC NOT > SPACES
               MOVE 'No existen paginas anteriores' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF7.
           MOVE CMA-ARG-DESC TO TAB-COD-TABL IN TAB.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-COD-TABL IN TAB = CMA-ARG-DESC )
               MOVE 'No existe tabla' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF7.
           MOVE FIO-GET-PRV TO FIO-CMND.
           PERFORM CST-FIO-TAB.

           IF FIO-STAT-OKS
               PERFORM LLENAR-TABLA-DESC
               GO TO FIN-SCRL-TAB-TAB-PF7.
           MOVE 'FIN DE PAGINAS' TO DOH-GLS-MENS.
       FIN-SCRL-TAB-TAB-PF7.
           EXIT.

       LLENAR-TABLA-DESC SECTION.
       INI-LLENAR-TABLA-DESC.
           MOVE WSS-TOT-TABL TO FRM-IFLD.
       LUP-LLENAR-TABLA-DESC.
           PERFORM CARGAR-TABLA.
           IF FRM-IFLD = WSS-TOT-TABL
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-ASCE
               MOVE SPACES              TO CMA-ARG-DESC
           ELSE
           IF FRM-IFLD = 1
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-DESC
               MOVE FIO-GET-PRV TO FIO-CMND
               PERFORM CST-FIO-TAB
               IF NOT ( FIO-STAT-OKS AND
                        WSS-TAB-COD-CTAB > SPACES )
                    MOVE SPACES              TO CMA-ARG-DESC.

       PRV-LLENAR-TABLA-DESC.
           SUBTRACT 1 FROM FRM-IFLD.
           IF FRM-IFLD < 1
               GO TO FIN-LLENAR-TABLA-DESC.
           MOVE FIO-GET-PRV TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF FIO-STAT-OKS
               GO TO LUP-LLENAR-TABLA-DESC.
       FIN-LLENAR-TABLA-DESC.
           EXIT.

       LIMPIAR-TABLA SECTION.
       INI-LIMPIAR-TABLA.
           MOVE SPACES TO DOH-COD-OPCI(FRM-IFLD)
                          DOH-GLS-DESC(FRM-IFLD)
                          DOH-IND-VIGE(FRM-IFLD).
       FIN-LIMPIAR-TABLA.
           EXIT.

       CARGAR-TABLA SECTION.
       INI-CARGAR-TABLA.
           MOVE TAB-COD-CTAB IN TAB TO DOH-COD-OPCI (FRM-IFLD).
           IF CMA-ARG-TTAB = 'MSC' AND
              CMA-ARG-TTAB-MSC > SPACES
               MOVE TAB-COD-CTAB IN TAB TO WSS-MSC-COD-MSCL
               MOVE WSS-MSC-COD-CMSC TO DOH-COD-OPCI (FRM-IFLD).
               
           MOVE TAB-GLS-DESC IN TAB TO DOH-GLS-DESC (FRM-IFLD).
           MOVE TAB-IND-VIGE IN TAB TO DOH-IND-VIGE (FRM-IFLD).
       FIN-CARGAR-TABLA.
           EXIT.
