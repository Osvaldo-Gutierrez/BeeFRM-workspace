      $FEDLEVEL = 5 SHARING = SHAREDBYALL
       IDENTIFICATION DIVISION.
      *=======================
      *PROGRAM-ID.    GNSPVNUM INITIAL.
       PROGRAM-ID.    GNSPVNUM.
       AUTHOR.        GUZMAN Y CIA.
      * Rutina para Validacion Numerica
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       01  WSS-VARI.
           03 WSS-MAUT.
              05 WSS-FILA OCCURS 14.
                 07 WSS-COLU OCCURS 9.
                    09 WSS-AUTO                         PIC 9(02).
           03 WSS-MAUT-RED REDEFINES WSS-MAUT.
              05 WSS-ITM1                               PIC X(58).
              05 WSS-ITM2                               PIC X(58).
              05 WSS-ITM3                               PIC X(58).
              05 WSS-ITM4                               PIC X(58).
              05 WSS-ITM5                               PIC X(20).
           03 WSS-NUME.
              05 WSS-CNUM  OCCURS 25                    PIC X(01).
           03 WSS-NAUX.
              05 WSS-CAUX  OCCURS 25                    PIC X(01).
           03 WSS-STDO                          VALUE 1 PIC 9(02).
           03 WSS-TKEN                                  PIC 9(02).
           03 WSS-SIND.
              05 I                                      PIC 9(02).
              05 J                                      PIC 9(02).
              05 K                                      PIC 9(02).
           03 WSS-IDCM                          VALUE 0 PIC 9(01).
              88 TIENE-DECIMALES                VALUE 1.
           03 WSS-ISGN                          VALUE 0 PIC 9(01).
              88 TIENE-SIGNO                    VALUE 1 2.
              88 TIENE-SIGNO-POSITIVO           VALUE 1.
              88 TIENE-SIGNO-NEGATIVO           VALUE 2.
           03 WSS-IZER                          VALUE 0 PIC 9(01).
              88 ES-CERO                        VALUE 0.
           03 WSS-NENT                          VALUE 0 PIC 9(02).
           03 WSS-NDCM                          VALUE 0 PIC 9(02).
       COPY GNSWCNUM.
       LINKAGE SECTION.
      *---------------
       COPY GNSWVNUM.
       PROCEDURE DIVISION USING NUM-VARI.
      *==================================
       MAIN SECTION.
       INI-MAIN.
           MOVE 0 TO NUM-STAT-LNK.
           MOVE SPACES TO NUM-MENS.
           MOVE
           '0102040012030301000102040012000014000002040012000003000105'
                                                           TO WSS-ITM1.
           MOVE
           '0508120000140001060608120000140001070708120000140001070700'
                                                           TO WSS-ITM2.
           MOVE
           '1200001400000909000000000000001010000000000000001111000000'
                                                           TO WSS-ITM3.
           MOVE
           '0000000100000812000014000013130000000000000113130000000014'
                                                           TO WSS-ITM4.
           MOVE '00010000000000001400'                     TO WSS-ITM5.
           MOVE NUM-NUME TO WSS-NUME.
           MOVE 1 TO WSS-STDO.
           MOVE 0 TO WSS-IZER WSS-ISGN WSS-IDCM WSS-NENT WSS-NDCM.
           IF WSS-NUME NOT > SPACES
               MOVE SPACES TO WSS-NUME
               MOVE ZERO TO WSS-CNUM(1)
               MOVE 0 TO WSS-IZER
           ELSE
               PERFORM PRO-AUTO VARYING I FROM 1 BY 1 UNTIL I > 25 OR
                                                           WSS-STDO = 0.
           PERFORM CHK-STDO.
           IF NUM-STAT-LNK = 0
               PERFORM EDT-NUME.
       FIN-MAIN.
           EXIT PROGRAM.
       PRO-AUTO SECTION.
       INI-PRO-AUTO.
           IF WSS-CNUM(I) = '0'
               MOVE 2 TO WSS-TKEN
               IF TIENE-DECIMALES
                   ADD 1 TO WSS-NDCM
               ELSE
                   IF WSS-NENT > 0
                       ADD 1 TO WSS-NENT
                   ELSE
                       NEXT SENTENCE
           ELSE
           IF WSS-CNUM(I) NOT < '1' AND WSS-CNUM(I) NOT > '9'
               MOVE 3 TO WSS-TKEN
               MOVE 1 TO WSS-IZER
               IF TIENE-DECIMALES
                   ADD 1 TO WSS-NDCM
               ELSE
                   ADD 1 TO WSS-NENT
           ELSE
           IF WSS-CNUM(I) = '.'
               MOVE 4 TO WSS-TKEN
           ELSE
           IF WSS-CNUM(I) = ','
               MOVE 5 TO WSS-TKEN
               MOVE 1 TO WSS-IDCM
           ELSE
           IF WSS-CNUM(I) = '+'
               MOVE 6 TO WSS-TKEN
               MOVE 1 TO WSS-ISGN
           ELSE
           IF WSS-CNUM(I) = '-'
               MOVE 7 TO WSS-TKEN
               MOVE 2 TO WSS-ISGN
           ELSE
           IF WSS-CNUM(I) NOT > SPACES
               MOVE 8 TO WSS-TKEN
           ELSE
               MOVE 9 TO WSS-TKEN.
           MOVE WSS-AUTO(WSS-STDO , WSS-TKEN) TO WSS-STDO.
       FIN-PRO-AUTO.
           EXIT.
       CHK-STDO SECTION.
       INI-CHK-STDO.
           IF WSS-STDO NOT = 0
               IF WSS-AUTO(WSS-STDO , 1) = 1
                   GO TO CON-CHK-STDO.
           MOVE 1 TO NUM-STAT-LNK.
           MOVE 'FORMATO NUMERICO INVALIDO' TO NUM-MENS.
           GO TO FIN-CHK-STDO.
       CON-CHK-STDO.
           IF ES-CERO AND NUM-ZERO = NUM-ZERO-N
               MOVE 2 TO NUM-STAT-LNK
               MOVE 'DATO OBLIGATORIO' TO NUM-MENS
               GO TO FIN-CHK-STDO.
           IF WSS-NDCM > 0
               IF NUM-NDCM = 0
                   MOVE 3 TO NUM-STAT-LNK
                   MOVE 'NO SE PERMITEN DECIMALES' TO NUM-MENS
                   GO TO FIN-CHK-STDO
               ELSE
               IF WSS-NDCM > NUM-NDCM OR NUM-NDCM > 5
                   MOVE 4 TO NUM-STAT-LNK
                   MOVE 'NUMERO DE DECIMALES EXCEDE EL PERMITIDO'
                                                            TO NUM-MENS
                   GO TO FIN-CHK-STDO.
           IF WSS-NENT > NUM-NENT OR NUM-NENT > 13
               MOVE 5 TO NUM-STAT-LNK
               MOVE 'NUMERO DE ENTEROS EXCEDE EL PERMITIDO' TO NUM-MENS
               GO TO FIN-CHK-STDO.
           IF TIENE-SIGNO AND NUM-SGNO = NUM-SGNO-N
               MOVE 6 TO NUM-STAT-LNK
               MOVE 'NUMERO NO PERMITE SIGNO' TO NUM-MENS
               GO TO FIN-CHK-STDO.
       FIN-CHK-STDO.
           EXIT.
       EDT-NUME SECTION.
       INI-EDT-NUME.
           MOVE ZEROES TO NUM-PENT.
           MOVE ZEROES TO NUM-PDCM.
           PERFORM JUS-RIGH.
           IF NOT TIENE-DECIMALES
               MOVE 26 TO I
           ELSE
               PERFORM BUS-COMA VARYING I FROM 1 BY 1 UNTIL I > 25 OR
                                                     WSS-CNUM(I) = ','.
           MOVE I TO J.
           SUBTRACT 1 FROM J.
           MOVE 13 TO K.
           PERFORM PUT-PENT VARYING I FROM J BY -1 UNTIL I <  1 OR
                                 K < 1 OR WSS-CNUM(I) NOT > SPACES.
           ADD 2 TO J.
           MOVE 1 TO K.
           IF TIENE-DECIMALES
               PERFORM PUT-PDCM VARYING I FROM J BY  1
                                UNTIL I > 25 OR
                                   K > 5 OR WSS-CNUM(I) NOT > SPACES.
           IF TIENE-SIGNO-NEGATIVO
               MULTIPLY -1 BY NUM-NS9V.
       FIN-EDT-NUME.
           EXIT.

       JUS-RIGH SECTION.
       INI-JUS-RIGH.
           MOVE 25 TO K.
           MOVE SPACES TO WSS-NAUX.
           PERFORM SHF-CHAR VARYING I FROM 25 BY -1 UNTIL I < 1.
           MOVE WSS-NAUX TO WSS-NUME.
       FIN-JUS-RIGH.
           EXIT.
       SHF-CHAR SECTION.
       INI-SHF-CHAR.
           IF WSS-CNUM(I) > SPACES
               MOVE WSS-CNUM(I) TO WSS-CAUX(K)
               SUBTRACT 1 FROM K.
       FIN-SHF-CHAR.
           EXIT.
       BUS-COMA SECTION.
       INI-BUS-COMA.
       FIN-BUS-COMA.
           EXIT.
       PUT-PENT SECTION.
       INI-PUT-PENT.
           IF WSS-CNUM(I) NOT < '0' AND WSS-CNUM(I) NOT > '9'
               MOVE WSS-CNUM(I) TO NUM-PENT-RED(K)
               SUBTRACT 1 FROM K.
       FIN-PUT-PENT.
           EXIT.
       PUT-PDCM SECTION.
       INI-PUT-PDCM.
           IF WSS-CNUM(I) NOT < '0' AND WSS-CNUM(I) NOT > '9'
               MOVE WSS-CNUM(I) TO NUM-PDCM-RED(K)
               ADD 1 TO K.
       FIN-PUT-PDCM.
           EXIT.
