      $SET LIST
       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GNSPPPHK.
       AUTHOR.          BEE.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
                DECIMAL-POINT IS COMMA.
      
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT GNSPHK  ASSIGN TO DISK
                 ORGANIZATION IS SEQUENTIAL.
       DATA DIVISION.
      *=============
       FILE SECTION.
       FD  GNSPHK
           LABEL RECORD ARE STANDARD
           VALUE OF FILETYPE IS 7.
       01  WSS-TAB.
           03 WSS-TAB-COD-CTAB.
              05 WSS-TAB-COD-CTAB-1    PIC X(06).
              05 WSS-TAB-COD-CTAB-2    PIC X(06).
              05 WSS-TAB-COD-CTAB-3    PIC X(06).
           03 WSS-TAB-GLS-DESC         PIC X(40).
           03 WSS-TAB-GLS-DATA         PIC X(60).
       DATA-BASE SECTION.
       DB BDCST.
       01  AUDITAREA.
       01  GNSTAB.
       WORKING-STORAGE SECTION.
      *-----------------------
       77  WSS-EOF                       PIC X(03).
       77  WSS-TAB-COD-CTAB-CMP VALUE SPACES  PIC X(12).
       01  WSS-REG1.
           03  WSS-REG1-KEY              PIC X(12).
           03  WSS-REG1-COR              PIC 9(02).
           03  WSS-REG1-COR-RED REDEFINES
               WSS-REG1-COR             PIC X(02).
           03  WSS-REG1-H03              PIC 9(04).
           03  WSS-REG1-H03-RED REDEFINES
               WSS-REG1-H03              PIC X(04).
           03  WSS-REG1-TBL-ATR.
               05 WSS-REG1-TBL-ATR-OCC OCCURS 20 TIMES.
                  07 WSS-REG1-ATR1       PIC X(01).
                  07 WSS-REG1-ATR2       PIC X(03).
      
       01  WSS-TBL-ATR.
           03 WSS-TBL-ATR-OCC OCCURS 20 TIMES.
              05 WSS-TBL-ATR-ELM    PIC X(01).
       01  WSS-TBL-ATR-RED REDEFINES WSS-TBL-ATR.
           03 WSS-TBL-ATR-RED1      PIC X(12).
           03 WSS-TBL-ATR-RED2      PIC X(12).
      
       77  WSS-COD-KEY1                  PIC X(12).
       77  WSS-NUM-5                     PIC 9(05).
       77  WSS-IDX-ATR                   PIC 9(05).
       01 WSS-TAB-ATR.
           03 WSS-TAB-ATRI.
              05 WSS-TAB-GLS-DCOR      PIC X(12).
              05 WSS-TAB-COD-ATRN      PIC X(12).
           03 WSS-TAB-GLS-ABRV         PIC X(05).
           03 WSS-TAB-EXT-CTAB         PIC X(05).
       77  WSS-TAB-COD-TTAB VALUE SPACES PIC X(03).
       77  WSS-CMT          VALUE SPACES PIC X(01).
       01  WSS-DSP-SEC VALUE "N"   PIC X(01).
       77  WSS-CONT-PUT-2    VALUE ZEROES PIC 9(01).
       77  WSS-CONT-PUT-5    VALUE ZEROES PIC 9(05).
       77  WSS-CONT-PUT-5-EDT             PIC ZZZZ9.
       COPY GNSWCFIO OF GNS.
       COPY GNSWVFIO OF GNS.
       COPY GNSWSFIO OF GNS.
       COPY GNSWGDRC OF GNS.
       COPY GNSBRTAB OF GNS.
       COPY GNSWRTAB OF GNS.
       COPY GNSBRMSG OF GNS.
       COPY GNSWRMSG OF GNS.
      
       PROCEDURE DIVISION.
      *===================
      
       MAIN SECTION.
      *------------
       INI-MAIN.
           MOVE "GNSPPPHK" TO FIO-PROG.
           MOVE "N" TO FIO-BTRN.
           DISPLAY "INICIO PROGRAMA GNSPPPHK  ".
           MOVE FIO-UPD TO FIO-CMND.
           MOVE "GNS" TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
                DISPLAY "ERROR APERTURA ARCHIVO TAB"
                STOP RUN.
           DISPLAY "OPEN OK BASE DE DATOS".
           PERFORM OPEN-FILE.
           DISPLAY "OPEN OK ARCH INPUT".
           MOVE "PHK" TO WSS-TAB-COD-TTAB.
           PERFORM ELI-ALL-PHK.
           DISPLAY "HIZO ELI-ALL-PHK PARA TABLA  " WSS-TAB-COD-TTAB.
      
       LUP-MAIN.
           READ GNSPHK  RECORD
                 AT END      DISPLAY "FIN DE ARCHIVO "
                             DISPLAY "TERMINO DE PROGRAMA"
                             STOP RUN.
           IF WSS-TAB = "EOF"
               DISPLAY "FIN DE ARCHIVO SIN PROCESAR NADA"
               DISPLAY "TERMINO DE PROGRAMA"
               STOP RUN.
      
           MOVE WSS-TAB-COD-CTAB TO WSS-CMT.
           IF WSS-CMT = "*"
               GO TO LUP-MAIN.
      
           IF WSS-TAB NOT > SPACES
               GO TO LUP-MAIN.
      
           IF WSS-TAB-COD-CTAB-1 NOT = "INITAB"
               DISPLAY "DEBE INFORMAR TABLA A ACTUALIZAR"
               DISPLAY "TERMINO DE PROGRAMA"
               STOP RUN.
      
           MOVE WSS-TAB-COD-CTAB-2 TO WSS-TAB-COD-TTAB.
      
           PERFORM PUT-ALL-PHK.
      
      *    MOVE "GNS" TO FIO-SIST.
      *    MOVE FIO-CLO TO FIO-CMND.
      *    PERFORM GNS-FIO-TAB.
           PERFORM CLOSE-FILE.
           STOP RUN.
       FIN-MAIN.
           EXIT.
      
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           OPEN I-O GNSPHK.
       FIN-OPEN-FILE.
           EXIT.
      
      
       ELI-ALL-PHK SECTION.
       INI-ELI-ALL-PHK.
           MOVE FIO-BEG-TRN TO FIO-CMND.
           MOVE "GNS" TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           DISPLAY "FIO-BGN-TRN OK PARA ELI".
       LUP-ELI-ALL-PHK.
           MOVE SPACES TO TAB.
           MOVE "GNS" TO FIO-SIST.
           MOVE WSS-TAB-COD-TTAB TO TAB-COD-TTAB IN TAB.
           MOVE "A     " TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-COD-TTAB IN TAB = WSS-TAB-COD-TTAB )
                 GO TO FIN-ELI-ALL-PHK.
      *    DISPLAY "REGISTRO A PROCESAR " TAB-COD-TABL IN TAB.
           MOVE "GNS" TO FIO-SIST.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
                DISPLAY "ERROR EN FIO-UPD TAB"
                DISPLAY "TAB-COD-TABL " TAB-COD-TABL IN TAB
                DISPLAY "FIN DE PROGRAMA"
                STOP RUN
           ELSE
                MOVE "GNS" TO FIO-SIST
                DISPLAY "ELIMINA TAB-COD-TABL : " TAB-COD-TABL IN TAB
                MOVE FIO-DEL TO FIO-CMND
                PERFORM GNS-FIO-TAB
                IF NOT FIO-STAT-OKS
                    DISPLAY "ERROR EN FIO-DEL TAB"
                    DISPLAY "TAB-COD-TABL " TAB-COD-TABL IN TAB
                    DISPLAY "FIN DE PROGRAMA"
                    STOP RUN.
      
           GO TO LUP-ELI-ALL-PHK.
      
      
       FIN-ELI-ALL-PHK.
           MOVE FIO-CHK-PNT TO FIO-CMND.
           MOVE "GNS" TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
       END-ELI-ALL-PHK.
           EXIT.
      
       PUT-ALL-PHK SECTION.
       INI-PUT-ALL-PHK.
           DISPLAY "EN INI-PUT-ALL-PHK".
           READ GNSPHK  RECORD
                 AT END      DISPLAY "ERROR EN NEXT  GNSPHK"
                             GO TO FIN-PUT-ALL-PHK.
           MOVE FIO-BEG-TRN TO FIO-CMND.
           MOVE "GNS" TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
       LUP-PUT-ALL-PHK.
      *    DISPLAY "WSS-TAB " WSS-TAB.
           MOVE WSS-TAB TO WSS-EOF.
           IF WSS-EOF = "EOF"
               GO TO FIN-PUT-ALL-PHK.
           MOVE WSS-TAB-COD-CTAB TO WSS-CMT.
           IF WSS-CMT = "*"
                GO TO NXT-PUT-ALL-PHK.
           IF WSS-TAB NOT > SPACES
               GO TO NXT-PUT-ALL-PHK.
           IF WSS-TAB-COD-CTAB-1 = "INITAB"
               MOVE WSS-TAB-COD-CTAB-2 TO WSS-TAB-COD-TTAB
               GO TO NXT-PUT-ALL-PHK.
      
           IF WSS-TAB-COD-CTAB-CMP = SPACES
               MOVE WSS-TAB-COD-CTAB TO WSS-TAB-COD-CTAB-CMP
               PERFORM PRO-REG1
               GO TO NXT-PUT-ALL-PHK.
           IF WSS-TAB-COD-CTAB NOT = WSS-TAB-COD-CTAB-CMP
                DISPLAY "ERROR EN SECUENCIA DE REGISTROS "
                DISPLAY "REG-1 : " WSS-TAB-COD-CTAB-CMP
                DISPLAY "REG-2 : " WSS-TAB-COD-CTAB
                DISPLAY "FIN DE PROCESO"
                STOP RUN.
           MOVE SPACES TO WSS-TAB-COD-CTAB-CMP.
           MOVE SPACES TO TAB.
           MOVE "GNS" TO FIO-SIST.
           MOVE WSS-TAB-COD-TTAB TO TAB-COD-TTAB IN TAB.
           MOVE WSS-TAB-COD-CTAB TO TAB-COD-CTAB IN TAB.
           MOVE WSS-TAB-COD-CTAB TO TAB-CIC-CTAB IN TAB.
           MOVE WSS-TAB-GLS-DESC TO TAB-GLS-DESC IN TAB.
           MOVE WSS-TAB-GLS-DATA TO TAB-GLS-DATA IN TAB.
           MOVE "S"              TO TAB-IND-VIGE IN TAB.
           MOVE WSS-TAB-GLS-DCOR TO TAB-GLS-DCOR IN TAB.
           MOVE WSS-TAB-COD-ATRN TO TAB-COD-ATRN IN TAB.
           MOVE WSS-TAB-GLS-ABRV TO TAB-GLS-ABRV IN TAB.
           MOVE WSS-TAB-EXT-CTAB TO TAB-EXT-CTAB IN TAB.
      
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
                DISPLAY "ERROR EN FIO-PUT TAB"
                GO TO FIN-PUT-ALL-PHK.
           ADD 1 TO WSS-CONT-PUT-2.
           ADD 1 TO WSS-CONT-PUT-5.
           IF WSS-CONT-PUT-2 = ZEROES
               MOVE WSS-CONT-PUT-5 TO WSS-CONT-PUT-5-EDT
               DISPLAY "TABLA " WSS-TAB-COD-TTAB " TOTAL PUT : "
                        WSS-CONT-PUT-5-EDT.
      *    DISPLAY "PUT-ATBL : " TAB-COD-TABL IN TAB.
      *    DISPLAY "DESC :" WSS-TAB-GLS-DESC.
      *    DISPLAY "DATA :" WSS-TAB-GLS-DATA.
      *    DISPLAY "-------------------------------------------".
       NXT-PUT-ALL-PHK.
           READ GNSPHK RECORD
                 AT END GO TO FIN-PUT-ALL-PHK.
      
           GO TO LUP-PUT-ALL-PHK.
      
       FIN-PUT-ALL-PHK.
           MOVE FIO-CHK-PNT TO FIO-CMND.
           MOVE "GNS" TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
       END-PUT-ALL-PHK.
           EXIT.
       PRO-REG1 SECTION.
       INI-PRO-REG1.
           MOVE WSS-TAB TO WSS-REG1.
           IF WSS-REG1-COR-RED = SPACES OR
              WSS-REG1-COR-RED NOT NUMERIC
                DISPLAY "INDICADOR DE CORRELATIVO "
                DISPLAY "PARA POSICIONAMIENTO DE CURSOR ERRONEO"
                DISPLAY "REGISTRO : " WSS-REG1-KEY
                DISPLAY "ABORTO"
                STOP RUN.
           IF WSS-REG1-H03-RED = SPACES
                MOVE ALL ZEROES TO WSS-REG1-H03-RED.
           IF WSS-REG1-H03-RED NOT NUMERIC
                DISPLAY "INDICADOR DE ULTIMO BYTE "
                DISPLAY "PARA POSICIONAMIENTO DE CURSOR ERRONEO"
                DISPLAY "REGISTRO : " WSS-REG1-KEY
                DISPLAY "ABORTO"
                STOP RUN.
           MOVE WSS-REG1-COR TO WSS-NUM-5.
           MOVE WSS-NUM-5 TO WSS-TAB-GLS-ABRV.
           MOVE WSS-REG1-H03 TO WSS-NUM-5.
           MOVE WSS-NUM-5 TO WSS-TAB-EXT-CTAB.
           PERFORM PUT-ATR VARYING WSS-IDX-ATR FROM 1 BY 1 UNTIL
                    WSS-IDX-ATR > 20.
           MOVE WSS-TBL-ATR-RED1 TO WSS-TAB-GLS-DCOR.
           MOVE WSS-TBL-ATR-RED2 TO WSS-TAB-COD-ATRN.
      
      
      
       FIN-PRO-REG1.
           EXIT.
      
       PUT-ATR SECTION.
       INI-PUT-ATR.
           MOVE WSS-REG1-ATR1(WSS-IDX-ATR) TO
                WSS-TBL-ATR-ELM(WSS-IDX-ATR).
       FIN-PUT-ATR.
           EXIT.
       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           CLOSE GNSPHK WITH LOCK.
       FIN-CLOSE-FILE.
           EXIT.
      
       COPY GNSBGDMD OF GNS.
       COPY GNSBFTAB OF GNS.
       COPY GNSBBTAB OF GNS.
       COPY GNSBFMSG OF GNS.
       COPY GNSBBMSG OF GNS.
