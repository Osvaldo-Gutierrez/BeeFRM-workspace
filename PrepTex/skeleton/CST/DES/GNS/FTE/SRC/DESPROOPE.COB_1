IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	DESPROOPE.
AUTHOR.		GUZMAN Y CIA.

* Subrutina manejo Operaciones

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============

WORKING-STORAGE SECTION.
*-----------------------
COPY	FIOWSS	IN	DESTXT.
COPY	OPEWSS	IN	DESTXT.
COPY	VRFWSS	IN	DESTXT.
COPY	'DESREG.OPE'	FROM	DICTIONARY
	REPLACING
	==OPE==		BY	==OPE EXTERNAL==.
01 WSS-VARI.
	03 WSS-CMND				PIC X(80).
	03 WSS-STAT-OKS			VALUE 0	PIC 9(01).
	03 WSS-STAT-NEX			VALUE 1	PIC 9(01).
	03 WSS-STAT-NVG			VALUE 2	PIC 9(01).
	03 WSS-STAT-RLK			VALUE 3	PIC 9(01).
	03 WSS-STAT-ERR			VALUE 4	PIC 9(01).
	03 WSS-SIND	COMP.
		05 I				PIC 9(04).

LINKAGE SECTION.
*---------------
01 LNK-CMND					PIC X(80).

PROCEDURE DIVISION USING LNK-CMND.
*=================================

MAIN SECTION.
*------------
INI-MAIN.
	MOVE WSS-STAT-OKS TO OPE-STAT.
	MOVE SPACES TO OPE-MENS.
	UNSTRING LNK-CMND DELIMITED BY '<' OR '>'
		INTO WSS-CMND OPE-CMND WSS-CMND.
	IF OPE-VAL
 		PERFORM OPE-OVAL
		GO TO FIN-MAIN.
	IF OPE-GET
		PERFORM OPE-OGET
		GO TO FIN-MAIN.
	IF OPE-UPD
		PERFORM OPE-OUPD
		GO TO FIN-MAIN.
	IF OPE-INP
		PERFORM OPE-OINP
		GO TO FIN-MAIN.
	IF OPE-CLO
		PERFORM OPE-OCLO
		GO TO FIN-MAIN.
	DISPLAY '? Codigo '
		LINE 23 COLUMN 1 WITH BELL REVERSED.
ERR-MAIN.
	DISPLAY	LNK-CMND REVERSED
		'  invalido en DESPROOPE : ABORTO'
		LINE 24 COLUMN 1 REVERSED.
	STOP RUN.
FIN-MAIN.
	EXIT PROGRAM.

OPE-OVAL SECTION.
*----------------
INI-OPE-OVAL.
	PERFORM OPE-OKEY.
	IF NOT OPE-STAT-RLK
		CALL 'DESFIOOPE' USING '<ULK_REC>'.
FIN-OPE-OVAL.
	EXIT.

OPE-OGET SECTION.
*----------------
INI-OPE-OGET.
	PERFORM OPE-OKEY.
	IF NOT OPE-STAT-OKS
		IF NOT OPE-STAT-RLK
			CALL 'DESFIOOPE' USING '<ULK_REC>'
			GO TO FIN-OPE-OGET
		ELSE
			GO TO FIN-OPE-OGET.
	ADD 1 TO OPE-NUM-CORR.
	MOVE OPE-NUM-CORR TO OPE-CORR.
	CALL 'DESFIOOPE' USING '<MOD>'.
	IF NOT FIO-STAT-OKS
		CALL 'DESFIOOPE' USING '<ULK_REC>'
		MOVE WSS-STAT-ERR TO OPE-STAT
		MOVE 'ERROR AL ASIGNAR CORRELATIVO' TO OPE-MENS
		GO TO FIN-OPE-OGET.
	MOVE VRF-SVRF-0 TO VRF-SVRF.
	MOVE OPE-COPE TO VRF-NUME.
	CALL 'DESPROVRF' USING '<GET>'.
	MOVE VRF-DVRF TO OPE-VRFO.
FIN-OPE-OGET.
	EXIT.

OPE-OUPD SECTION.
*----------------
INI-OPE-OUPD.
	MOVE FIO-ACCS-UPD TO FIO-ACCS.
	CALL 'DESFIOOPE' USING '<ACC>'.
	MOVE FIO-RLCK-PGM TO FIO-RLCK.
	CALL 'DESFIOOPE' USING '<REC_LCK>'.
	CALL 'DESFIOOPE' USING '<UPD>'.
FIN-OPE-OUPD.
	EXIT.

OPE-OINP SECTION.
*----------------
INI-OPE-OINP.
	MOVE FIO-ACCS-UPD TO FIO-ACCS.
	CALL 'DESFIOOPE' USING '<ACC>'.
	MOVE FIO-RLCK-PGM TO FIO-RLCK.
	CALL 'DESFIOOPE' USING '<REC_LCK>'.
	CALL 'DESFIOOPE' USING '<INP>'.
FIN-OPE-OINP.
	EXIT.

OPE-OCLO SECTION.
*----------------
INI-OPE-OCLO.
	CALL 'DESFIOOPE' USING '<CLO>'.
FIN-OPE-OCLO.
	EXIT.

OPE-OKEY SECTION.
*----------------
INI-OPE-OKEY.
	MOVE OPE-TMPO TO OPE-KEY-TMPO.
	PERFORM OPE-GKEY.
	PERFORM OPE-GKEY VARYING I FROM 2 BY 1 UNTIL I > 5
				OR NOT FIO-STAT-RLK.
	IF FIO-STAT-RLK
		MOVE WSS-STAT-RLK TO OPE-STAT
		MOVE 'REGISTRO OPERACION OCUPADO : INTENTE OTRA VEZ'
							TO OPE-MENS
		GO TO FIN-OPE-OKEY.
	IF FIO-STAT-OKS
		IF OPE-IND-IVIG = 'S'
			NEXT SENTENCE
		ELSE
			MOVE WSS-STAT-NVG TO OPE-STAT
			MOVE 'OPERACION NO VIGENTE' TO OPE-MENS
	ELSE
		MOVE WSS-STAT-NEX TO OPE-STAT
		MOVE 'OPERACION INEXISTENTE' TO OPE-MENS.
FIN-OPE-OKEY.
	EXIT.

OPE-GKEY SECTION.
*----------------
INI-OPE-GKEY.
	CALL 'DESFIOOPE' USING '<GET_KEY>'.
FIN-OPE-GKEY.
	EXIT.
