IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	DESFUNHDR.
AUTHOR.		GUZMAN Y CIA.

* Obtiene variables para Header standard de Formulario.

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============
WORKING-STORAGE SECTION.
*-----------------------
COPY	'DESREG.MEN'	FROM	DICTIONARY
	REPLACING
	==MEN==		BY	==MEN EXTERNAL==.
COPY	'DESREG.FUN'	FROM	DICTIONARY
	REPLACING
	==FUN==		BY	==FUN EXTERNAL==.
COPY	FIOWSS	IN	DESTXT.
COPY	FRMWSS	IN	DESTXT.
COPY	SCRWSS	IN	DESTXT.
01 HDR-FRM.
	03 HDR-RWSP				PIC X(12).
01 HDR-FLD.
	03 HDR-DFLD.
		05 HDR-COD-SIST			PIC X(03).
		05 HDR-GLS-FPRO			PIC X(61).
		05 HDR-COD-FUNC			PIC X(08).
		05 HDR-FEC-DMSA			PIC X(08).
01 WSS-VARI.
	03 WSS-ZERO	COMP	VALUE 0		PIC 9(04).
	03 WSS-AUXI	COMP			PIC 9(04).
	03 WSS-SIMP		VALUE SPACES.
		05 WSS-GLS-SIMP	OCCURS 31	PIC X(01).
	03 WSS-DOBL		VALUE SPACES.
		05 WSS-GLS-DOBL	OCCURS 61	PIC X(01).
	03 WSS-SIND.
		05 I		VALUE 0		PIC 9(02).
		05 IMIN		VALUE 0		PIC 9(02).
		05 IMAX		VALUE 0		PIC 9(02).
		05 J		VALUE 0		PIC 9(02).

PROCEDURE DIVISION.
*==================

MAIN SECTION.
*------------
INI-MAIN.
	PERFORM INI.
	PERFORM BUS-HDR.
	PERFORM PUT-HDR.
	PERFORM FIN.
FIN-MAIN.
	EXIT PROGRAM.

INI SECTION.
*-----------
INI-INI.
	CALL 'DESPROFRM' USING '<ATERM>'.
	PERFORM HDR-AWKSP.
	MOVE FRM-FTCA-NOT  TO FRM-FTCA.
	MOVE FRM-FWSP-NOT  TO FRM-FWSP.
	MOVE FRM-SINI-DBLF TO FRM-SINI.
	MOVE 92	      TO FRM-CLIB.
	MOVE 'DESFMS' TO FRM-NLIB.
	MOVE 'HDR'    TO FRM-NAME.
	CALL 'DESPROFRM' USING '<INI>'.
FIN-INI.
	EXIT.

BUS-HDR SECTION.
*----------------
INI-BUS-HDR.
	MOVE FIO-IFNF-PGM TO FIO-IFNF.
	CALL 'DESFIOMEN' USING '<FNF>'.
	CALL 'DESFIOMEN' USING '<INP>'.
	IF FIO-STAT-FNF
		CALL 'DESFIOMEN' USING '<OUT>'
	ELSE
		CALL 'DESFIOMEN' USING '<GET_NXT>'
		IF NOT ( FIO-STAT-OKS AND MEN-COD-TFUN = 'MEN' )
			CALL 'DESFIOMEN' USING '<CLO>'
			CALL 'DESFIOMEN' USING '<OUT>'
		ELSE
			GO TO FIN-BUS-HDR.
	MOVE ZEROES TO FUN-KEY-FUNC.
	CALL 'DESFIOFUN' USING '<GET_KEY>'.
	IF FIO-STAT-OKS
		MOVE FUN TO MEN
	ELSE
		MOVE 'NO EXISTE Registro SISTEMA : ABORTO' TO FRM-MENS
		CALL 'DESPROFRM' USING '<MSG>'
		CALL 'DESPROFRM' USING '<FIN>'
		DIVIDE WSS-ZERO INTO WSS-AUXI.
	CALL 'DESFIOMEN' USING '<PUT>'.
	MOVE FUN TO MEN.
FIN-BUS-HDR.
	CALL 'DESFIOMEN' USING '<CLO>'.

PUT-HDR SECTION.
*---------------
INI-PUT-HDR.
	MOVE MEN-COD-SIST TO HDR-COD-SIST.
	MOVE MEN-GLS-FPRO TO WSS-SIMP.
	PERFORM HDR-TITU.
	MOVE WSS-DOBL	  TO HDR-GLS-FPRO.
	MOVE MEN-COD-FUNC TO HDR-COD-FUNC.
	IF SCR-FAMD = LOW-VALUES
		ACCEPT SCR-FAMD FROM DATE.
	IF SCR-FDMA = LOW-VALUES
		MOVE SCR-DAMD TO SCR-DDMA
		MOVE SCR-MAMD TO SCR-MDMA
		MOVE 19       TO SCR-SDMA
		MOVE SCR-AAMD TO SCR-ADMA.
	MOVE SCR-FDMA TO HDR-FEC-DMSA.
	MOVE HDR-DFLD TO FRM-DFLD.
	CALL 'DESPROFRM' USING '<PUTAL>'.
	CALL 'DESPROFRM' USING '<CLR_DSP>'.
FIN-PUT-HDR.
	EXIT.

FIN SECTION.
*-----------
INI-FIN.
	MOVE SCR-IHDR-YES  TO SCR-IHDR.
	MOVE FRM-FWSP-YES  TO FRM-FWSP.
	MOVE FRM-SINI-DBNF TO FRM-SINI.
	MOVE 93	    TO	FRM-CLIB.
	MOVE SPACES TO	FRM-NLIB
			FRM-NAME
			FRM-MENS
			FRM-DFLD.
FIN-FIN.
	EXIT.

HDR-TITU SECTION.
*----------------
INI-HDR-TITU.
	IF WSS-SIMP = SPACES
		GO TO FIN-HDR-TITU.
	PERFORM BUS-LIMI VARYING IMIN FROM 1 BY 1
		UNTIL WSS-GLS-SIMP(IMIN) NOT = SPACES.
	PERFORM BUS-LIMI VARYING IMAX FROM 31 BY -1
		UNTIL WSS-GLS-SIMP(IMAX) NOT = SPACES.
	COMPUTE J = ( 61 - ( 2 * ( IMAX - IMIN + 1 ) - 1 ) ) / 2 + 1.
	PERFORM PUT-TITU VARYING I FROM IMIN BY 1 UNTIL I > IMAX.
FIN-HDR-TITU.
	EXIT.

BUS-LIMI SECTION.
*----------------
INI-BUS-LIMI.
FIN-BUS-LIMI.
	EXIT.

PUT-TITU SECTION.
*----------------
INI-PUT-TITU.
	MOVE WSS-GLS-SIMP(I) TO WSS-GLS-DOBL(J).
	ADD 2 TO J.
FIN-PUT-TITU.
	EXIT.

HDR-AWKSP SECTION.
*-----------------
INI-HDR-AWKSP.
	CALL 'FDV$AWKSP' USING
		BY DESCRIPTOR HDR-RWSP
		BY REFERENCE  FRM-TWSP.
FIN-HDR-AWKSP.
	EXIT.
