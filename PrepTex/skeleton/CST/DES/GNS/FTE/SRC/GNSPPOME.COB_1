       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.     GNSPPOME.
       AUTHOR.         CONSIST.

      * SUBRUTINA MONTO ESCRITO.

       DATE-WRITTEN.   MARZO 1984.

       DATE-COMPILED.

       ENVIRONMENT DIVISION.
      *=====================

       DATA DIVISION.
      *==============
       WORKING-STORAGE SECTION.
      *------------------------
       77  I                    COMP                    PIC S9(09).
       77  J                    COMP                    PIC S9(09).
       77  K                    COMP                    PIC S9(09).
       77  L                    COMP                    PIC S9(09).
       77  M                    COMP                    PIC S9(09).
       77  SIND-WSS             COMP                    PIC S9(09).
       01  VEC-UNI  DISPLAY.
           03 FILLER                 VALUE 'UNO'        PIC X(07).
           03 FILLER                 VALUE 'DOS'        PIC X(07).
           03 FILLER                 VALUE 'TRES'       PIC X(07).
           03 FILLER                 VALUE 'CUATRO'     PIC X(07).
           03 FILLER                 VALUE 'CINCO'      PIC X(07).
           03 FILLER                 VALUE 'SEIS'       PIC X(07).
           03 FILLER                 VALUE 'SIETE'      PIC X(07).
           03 FILLER                 VALUE 'OCHO'       PIC X(07).
           03 FILLER                 VALUE 'NUEVE'      PIC X(07).
           03 FILLER                 VALUE 'DIEZ'       PIC X(07).
           03 FILLER                 VALUE 'ONCE'       PIC X(07).
           03 FILLER                 VALUE 'DOCE'       PIC X(07).
           03 FILLER                 VALUE 'TRECE'      PIC X(07).
           03 FILLER                 VALUE 'CATORCE'    PIC X(07).
           03 FILLER                 VALUE 'QUINCE'     PIC X(07).
       01  VEC-UNI-RED REDEFINES VEC-UNI.
           03 VAL-UNI                OCCURS 15          PIC X(07).
       01  VEC-DEC  DISPLAY.
           03 FILLER                 VALUE 'DIEZ'       PIC X(09).
           03 FILLER                 VALUE 'VEINTE'     PIC X(09).
           03 FILLER                 VALUE 'TREINTA'    PIC X(09).
           03 FILLER                 VALUE 'CUARENTA'   PIC X(09).
           03 FILLER                 VALUE 'CINCUENTA'  PIC X(09).
           03 FILLER                 VALUE 'SESENTA'    PIC X(09).
           03 FILLER                 VALUE 'SETENTA'    PIC X(09).
           03 FILLER                 VALUE 'OCHENTA'    PIC X(09).
           03 FILLER                 VALUE 'NOVENTA'    PIC X(09).
       01  VEC-DEC-RED REDEFINES VEC-DEC.
           03 VAL-DEC                OCCURS 9           PIC X(09).
       01  VEC-CEN  DISPLAY.
           03 FILLER              VALUE 'CIENTO'        PIC X(13).
           03 FILLER              VALUE 'DOSCIENTOS'    PIC X(13).
           03 FILLER              VALUE 'TRESCIENTOS'   PIC X(13).
           03 FILLER              VALUE 'CUATROCIENTOS' PIC X(13).
           03 FILLER              VALUE 'QUINIENTOS'    PIC X(13).
           03 FILLER              VALUE 'SEISCIENTOS'   PIC X(13).
           03 FILLER              VALUE 'SETECIENTOS'   PIC X(13).
           03 FILLER              VALUE 'OCHOCIENTOS'   PIC X(13).
           03 FILLER              VALUE 'NOVECIENTOS'   PIC X(13).
       01  VEC-CEN-RED REDEFINES VEC-CEN.
           03 VAL-CEN             OCCURS 9              PIC X(13).
       01  VEC-ME    DISPLAY.
           03 MMUN-ME                                   PIC X(07).
           03 MILL-ME                                   PIC X(09).
           03 MCEN-ME                                   PIC X(14).
           03 MDEC-ME                                   PIC X(14).
           03 MAND-ME                                   PIC X(02).
           03 MUNI-ME                                   PIC X(07).
           03 MMIL-ME                                   PIC X(04).
           03 CCEN-ME                                   PIC X(14).
           03 CDEC-ME                                   PIC X(10).
           03 CAND-ME                                   PIC X(02).
           03 CUNI-ME                                   PIC X(07).
           03 DAND-ME                                   PIC X(01).
           03 DCEN-ME                                   PIC X(02).
           03 CIEN-ME                                   PIC X(04).
           03 PRAY-ME                                   PIC X(02).
       01  VEC-ME-RED REDEFINES VEC-ME.
           03 VAL-ME              OCCURS 99             PIC X(01).

       LINKAGE SECTION.
      *----------------
       01  VARI-LNK DISPLAY.
           03 NUME-LNK                                  PIC 9(10)V9(02).
           03 NUME-LNK-RED REDEFINES NUME-LNK.
              05 FILLER                                 PIC X(03).
              05 MMIL-LNK                               PIC 9(01).
              05 CMIL-LNK                               PIC 9(01).
              05 NDMI-LNK.
                 07 DMIL-LNK                            PIC 9(01).
                 07 UMIL-LNK                            PIC 9(01).
              05 NDMR-LNK REDEFINES NDMI-LNK            PIC 9(02).
              05 CENT-LNK                               PIC 9(01).
              05 NDEC-LNK.
                 07 DECE-LNK                            PIC 9(01).
                 07 UNID-LNK                            PIC 9(01).
              05 NDER-LNK REDEFINES NDEC-LNK PIC 9(02).
              05 DCEN-LNK                               PIC 9(02).
           03 VEC1-LNK.
              05 V1ME-LNK OCCURS 99                     PIC X(01).
           03 VEC2-LNK.
              05 V2ME-LNK OCCURS 99                     PIC X(01).
           03 LVE1-LNK                                  PIC 9(02).

       PROCEDURE DIVISION USING VARI-LNK.
      *----------------------------------

       MAIN SECTION.
       INI-MAIN.
           PERFORM PRO-MILL.
           PERFORM PRO-NMIL.
           PERFORM PRO-CIEN.
           PERFORM PRO-CENT.
           PERFORM OBT-V1ME.
           PERFORM OBT-V2ME.
       FIN-MAIN.
           EXIT PROGRAM.

       PRO-MILL SECTION.
       UNI-MILL.
           MOVE SPACES TO VEC-ME.
           IF MMIL-LNK = 0
                      NEXT SENTENCE
           ELSE
           IF MMIL-LNK = 1
                      MOVE 'UN' TO MMUN-ME
                      MOVE 'MILLON' TO MILL-ME
           ELSE
                      MOVE MMIL-LNK TO SIND-WSS
                      MOVE VAL-UNI(SIND-WSS) TO MMUN-ME
                      MOVE 'MILLONES' TO MILL-ME.
       FIN-PRO-MILL.
           EXIT.

       PRO-NMIL SECTION.
       CEN-NMIL.
           IF CMIL-LNK = 0
                      NEXT SENTENCE
           ELSE
           MOVE 'MIL' TO MMIL-ME
           IF CMIL-LNK = 1 AND NDMR-LNK = 0
                      MOVE 'CIEN' TO MCEN-ME
                      GO TO FIN-NMIL
           ELSE
                      MOVE CMIL-LNK TO SIND-WSS
                      MOVE VAL-CEN(SIND-WSS) TO MCEN-ME.
       DEC-NMIL.
           IF DMIL-LNK = 0
                      NEXT SENTENCE
           ELSE
           MOVE 'MIL' TO MMIL-ME
           IF NDMR-LNK < 16
                      MOVE NDMR-LNK TO SIND-WSS
                      MOVE VAL-UNI(SIND-WSS) TO MDEC-ME
                      GO TO FIN-NMIL
           ELSE
                      MOVE DMIL-LNK TO SIND-WSS
                      MOVE VAL-DEC(SIND-WSS) TO MDEC-ME
                      IF UMIL-LNK > 0
                                 MOVE 'Y' TO MAND-ME.
       UNI-NMIL.
           IF UMIL-LNK = 0
                      NEXT SENTENCE
           ELSE
           MOVE 'MIL' TO MMIL-ME
           IF UMIL-LNK = 1
                      MOVE 'UN' TO MUNI-ME
           ELSE
                      MOVE UMIL-LNK TO SIND-WSS
                      MOVE VAL-UNI(SIND-WSS) TO MUNI-ME.
       FIN-NMIL.
           EXIT.

       PRO-CIEN SECTION.
       CEN-CIEN.
           IF CENT-LNK = 0
                      NEXT SENTENCE
           ELSE
           IF CENT-LNK = 1 AND NDER-LNK = 0
                      MOVE 'CIEN' TO CCEN-ME
                      GO TO FIN-CIEN
           ELSE
                      MOVE CENT-LNK TO SIND-WSS
                      MOVE VAL-CEN(SIND-WSS) TO CCEN-ME.
       DEC-CIEN.
           IF DECE-LNK = 0
                      NEXT SENTENCE
           ELSE
           IF NDER-LNK < 16
                      MOVE NDER-LNK TO SIND-WSS
                      MOVE VAL-UNI(SIND-WSS) TO CDEC-ME
                      GO TO FIN-CIEN
           ELSE
                      MOVE DECE-LNK TO SIND-WSS
                      MOVE VAL-DEC(SIND-WSS) TO CDEC-ME
                      IF UNID-LNK > 0
                                 MOVE 'Y' TO CAND-ME.
       UNI-CIEN.
           IF UNID-LNK = 0
                      NEXT SENTENCE
           ELSE
                      MOVE UNID-LNK TO SIND-WSS
                      MOVE VAL-UNI(SIND-WSS) TO CUNI-ME.
       FIN-CIEN.
           EXIT.

       PRO-CENT SECTION.
       INI-CENT.
           MOVE DCEN-LNK TO DCEN-ME.
           MOVE '/100' TO CIEN-ME.
           MOVE '.-' TO PRAY-ME.
       FIN-PRO-CENT.
           EXIT.

       OBT-V1ME SECTION.
       INI-OBT-V1ME.
           MOVE SPACES TO VEC1-LNK.
           MOVE 1 TO J.
           MOVE 0 TO I.
       LOOP-OBT-V1ME.
           PERFORM MOV-V1ME VARYING J FROM J BY 1
                      UNTIL J > 97 OR VAL-ME(J) = SPACES.
           PERFORM MOV-V1ME.
           ADD 1 TO J.
           PERFORM BUS-BLAN VARYING J FROM J BY 1
                      UNTIL J > 99 OR VAL-ME(J) NOT = SPACES.
           IF J NOT > 99 GO TO LOOP-OBT-V1ME.
       FIN-OBT-V1ME.
           EXIT.

       MOV-V1ME SECTION.
       INI-MOV-V1ME.
           ADD 1 TO I.
           MOVE VAL-ME(J) TO V1ME-LNK(I).
       FIN-MOV-V1ME.
           EXIT.

       BUS-BLAN SECTION.
       INI-BUS-BLAN.
           EXIT.

       OBT-V2ME SECTION.
       INI-OBT-V2ME.
           MOVE SPACES TO VEC2-LNK.
           MOVE LVE1-LNK TO L.
           ADD 1 , L GIVING M.
           IF V1ME-LNK(L) = SPACES AND V1ME-LNK(M) = SPACES
                      GO TO FIN-OBT-V2ME.
           PERFORM BUS-BLAN VARYING I FROM L BY -1
                      UNTIL I < 1 OR V1ME-LNK(I) = SPACES.
           ADD 1 , I GIVING J.
           MOVE 0 TO K.
           PERFORM MOV-V2ME VARYING I FROM J BY 1 UNTIL I > 99.
           PERFORM MOV-BLAN VARYING I FROM J BY 1 UNTIL I > 99.
       FIN-OBT-V2ME.
           EXIT.

       MOV-V2ME SECTION.
       INI-MOV-V2ME.
           ADD 1 TO K.
           MOVE V1ME-LNK(I) TO V2ME-LNK(K).
       FIN-MOV-V2ME.
           EXIT.

       MOV-BLAN SECTION.
       INI-MOV-BLAN.
           MOVE SPACES TO V1ME-LNK(I).
       FIN-MOV-BLAN.
           EXIT.
