       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GNSP2TAB.
       AUTHOR.          CONSIST.
      * 
       ENVIRONMENT DIVISION.
      *====================
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT GNSTAS ASSIGN TO VID-GNSTAS
                  ORGANIZATION        IS SEQUENTIAL
                  ACCESS              IS SEQUENTIAL
                  STATUS              IS FIO-STAT.

       DATA DIVISION.
      *=============
       FILE SECTION.
       FD GNSTAS
               LABEL RECORDS STANDARD
               RECORD 194
               VALUE OF ID VID-GNSTAS
       .
      *
      *COPY GNSBRTAS.
      *    Registro Generico de Tablas
      *    ---------------------------
      * 
      *    Nombre Registro : TAS
      *    Organizacion    : Secuencial
      *    Largo           : 194
      *    Bloqueo         : 
      *    Observaciones   : 
      *
       01  TAS. 
      *
      *    Filler Compatibilidad VSAM/MVS
           03  TAB-GLS-FLAG                             PIC X(01).      
      *
      *    Clave Primaria Registro
           03  TAB-KEY-IREG.
      *
      *KEY     Tipo + Codigo ( LLave Primaria )
               05  TAB-COD-TABL.
      *
      *            Codigo Tipo Tabla
                   07  TAB-COD-TTAB                     PIC X(03).
      *
      *            Codigo Tabla
                   07  TAB-COD-CTAB                     PIC X(12).
      *
      *        Time Stamp Transaccion
               05  TAB-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  TAB-FEC-FTRN.
                       09  TAB-NUM-STRN                 PIC 9(02).
                       09  TAB-NUM-ATRN                 PIC 9(02).
                       09  TAB-NUM-MTRN                 PIC 9(02).
                       09  TAB-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  TAB-HRA-HRTR.
                       09  TAB-NUM-HHTR                 PIC 9(02).
                       09  TAB-NUM-MMTR                 PIC 9(02).
                       09  TAB-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  TAB-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  TAB-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  TAB-IND-VIGE  REDEFINES  TAB-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  TAB-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  TAB-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  TAB-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  TAB-COD-ATRN                             PIC X(12).            
      *
      *KEY Tipo + Codigo ( Interno Computacional )
           03  TAB-CIC-TABL.
      *
      *        Codigo Tipo Tabla
               05  TAB-CIC-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  TAB-CIC-CTAB                         PIC X(12).
      *
      *    Codigo Externo
           03  TAB-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  TAB-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  TAB-EXT-CTAB                         PIC X(05).
      *
      *    Descripcion Corta
           03  TAB-GLS-DCOR                             PIC X(12).
      *
      *    Abreviacion Descripcion
           03  TAB-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion del Codigo
           03  TAB-GLS-DESC                             PIC X(40).
      *
      *    Datos Anexos
           03  TAB-GLS-DATA.
               05  TAB-GLS-DAT1.
                   07  TAB-COD-DAT1     OCCURS 10       PIC X(01).
               05  TAB-GLS-DAT2.
                   07  TAB-COD-DAT2     OCCURS 10       PIC X(02).
               05  TAB-GLS-DAT3.
                   07  TAB-COD-DAT3     OCCURS 10       PIC X(03).
      *
      *    Clave Soundex
           03  TAB-SNX-TABL.
               05  TAB-SNX-TTAB                         PIC X(03).
               05  TAB-SNX-CTAB                         PIC X(03).
      *
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS
       01  VID-GNSTAS               PIC X(26).
       01  WSS-VARI.
           03 WSS-ILCK     COMP     VALUE 0          PIC 9(04).
           03 WSS-RLCK              VALUE 0          PIC 9(01).
           03 WSS-IFNF              VALUE 0          PIC 9(01).
       01  WSS-I       VALUE ZEROES PIC 9(06).
      * WORKING PARA RUTINAS.
       COPY GNSWGSYS.
      * COPY GNSWGFEC.
      * COPY GNSWGHOY.
       COPY GNSWCFIO.
       COPY GNSWVFIO.

       COPY GNSBRTAB.

       PROCEDURE DIVISION.
      *==================
       COPY GNSBEFIO
           REPLACING
           FIO-IFNF     BY     WSS-IFNF
           FIO-RLCK     BY     WSS-RLCK
           FIO-VOID     BY     VID-GNSTAS.
       MAIN SECTION.
      *------------
       INI-MAIN.
           MOVE 'GNSTAS' TO VID-GNSTAS.
           PERFORM OPEN-FILE.
           PERFORM PRO-PRINCIPAL.
           PERFORM CLOSE-FILE.
           STOP RUN.
       FIN-MAIN.
           EXIT.

       OPEN-FILE SECTION.
      *------------------
       INI-OPE-FILE.
           OPEN INPUT GNSTAS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO INPUT FS ' FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
           MOVE FIO-VID  TO FIO-CMND.
           MOVE 'TABRMS' TO FIO-VOID.
           PERFORM GNS-FIO-TAB.
      *
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO OUTPUT  FS ' FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
       FIN-OPEN-FILE.
           EXIT.

       PRO-PRINCIPAL SECTION.
      *----------------------
       INI-PRO-PRINCIPAL.
           MOVE ZEROES TO WSS-I.
           PERFORM READ-GNSTAS.
           IF NOT FIO-STAT-OKS
              DISPLAY 'NO EXISTEN REGISTROS EN GNSTAS'
              DISPLAY 'ABORTO'
              STOP RUN.
           PERFORM TAS-TRS-TAB UNTIL NOT FIO-STAT-OKS.
       FIN-PRO-PRINCIPAL.
           EXIT.

       READ-GNSTAS SECTION.
       INI-READ-GNSTAS.
           READ GNSTAS.
       FIN-READ-GNSTAS.
           EXIT.

       TAS-TRS-TAB SECTION.
       INI-TAS-TRS-TAB.
      *GRABA TAB-VAX DESDE TAS-REA.
           ADD  1   TO WSS-I.
      *
           MOVE CORR TAS TO TAB.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL GRABAR ARCHIVO OUTPUT FS ' FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
           PERFORM READ-GNSTAS.
       FIN-TAS-TRS-TAB.
           EXIT.

       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'NRO REG. TRASPASADOS A TAB-VAX :' WSS-I.
           CLOSE GNSTAS.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
       FIN-CLOSE-FILE.
           EXIT.

      * RUTINAS
       COPY GNSBBSYS.
       COPY GNSBGEND.

      * FIOS RMS.
      * COPY GNSBFTAB.
      * Modulo FIO Batch para archivo VSAM GNSTAB, 10-OCT-1988 12:53:06.57
       GNS-FIO-TAB SECTION.
       INI-GNS-FIO-TAB.
           IF FIO-CMND = FIO-PUT OR FIO-MOD
               MOVE TAB-COD-TTAB IN TAB TO TAB-CIC-TTAB IN TAB
                                           TAB-EXT-TTAB IN TAB
                                           TAB-SNX-TTAB IN TAB.
           CALL 'GNSPFTAB' USING FIO-VARI TAB.
       FIN-GNS-FIO-TAB.
           EXIT.
