IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	GENLIS.
AUTHOR.		GUZMAN Y CIA.

* Genera Archivo de Edicion para Generacion de Listador.

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============

WORKING-STORAGE SECTION.
*-----------------------
COPY	'FRMDATRPR'	IN	GEN.
COPY	'DESREG.RPR'	FROM	DICTIONARY
	REPLACING
	==RPR==		BY	==RPR EXTERNAL==.
COPY	FIOWSS		IN	DESTXT.
01 WSS-VARI.
	03 WSS-KEY-DPRO				PIC X(12).
	03 WSS-MENS				PIC X(80).
	03 WSS-STAT	COMP		VALUE 0	PIC 9(09).
		88 WSS-STAT-OKS		VALUE 0.
		88 WSS-STAT-MAL		VALUE 1.

PROCEDURE DIVISION.
*==================

MAIN SECTION.
*------------
INI-MAIN.
	CALL 'DESFIORPR' USING  '<INP>'.
	CALL 'LIB$GET_SYMBOL' USING
		BY DESCRIPTOR	'FIL'
				WSS-KEY-DPRO.
	PERFORM FND-KEY.
	IF NOT FIO-STAT-OKS
		MOVE 'NO EXISTE Definicion en Archivo RPR' TO WSS-MENS
		GO TO MSG-MAIN.
	PERFORM LEE-RPR.
	IF NOT FIO-STAT-OKS
		MOVE 'Definicion INCOMPLETA en Archivo RPR' TO WSS-MENS
		GO TO MSG-MAIN.
	CALL 'GENLISEDT' USING
		BY DESCRIPTOR	RPR-KEY-DPRO IN RPR-FLD
				RPR-GLS-DAUT IN RPR-FLD
				RPR-GLS-ABAS IN RPR-FLD
				RPR-GLS-DREP IN RPR-FLD
		BY REFERENCE	WSS-STAT.
	CALL 'DESFIORPR' USING '<CLO>'.
	IF NOT WSS-STAT-MAL
		CALL 'LIB$SET_SYMBOL' USING
			BY DESCRIPTOR	'REP'
					RPR-GLS-DREP IN RPR-FLD
		CALL 'LIB$SET_SYMBOL' USING
			BY DESCRIPTOR	'SRT'
					RPR-IND-SORT IN RPR-FLD
		GO TO FIN-MAIN.
	MOVE 'ERROR en GENERACION de Archivo de EDICION' TO WSS-MENS.
MSG-MAIN.
	DISPLAY WSS-KEY-DPRO , ': ' , WSS-MENS.
	CALL 'SYS$EXIT' USING
		BY VALUE 0.
FIN-MAIN.
	STOP RUN.

FND-KEY SECTION.
*---------------
INI-FND-KEY.
	MOVE SPACES TO	RPR-KEY-TRYC
			RPR-KEY-DARC.
	MOVE WSS-KEY-DPRO TO RPR-KEY-DPRO IN RPR.
	MOVE 'A' TO RPR-KEY-TREG.
	CALL 'DESFIORPR' USING  '<GET_KEY>'.
FIN-FND-KEY.
	EXIT.

LEE-RPR SECTION.
*---------------
INI-LEE-RPR.
	MOVE RPR-KEY-DPRO IN RPR TO RPR-KEY-DPRO IN RPR-FLD.
	MOVE RPR-GLS-DAUT IN RPR TO RPR-GLS-DAUT IN RPR-FLD.

	MOVE 'B' TO RPR-KEY-TREG.
	CALL 'DESFIORPR' USING '<GET_KEY>'.
	IF NOT FIO-STAT-OKS
		GO TO FIN-LEE-RPR.
	MOVE RPR-GLS-ABAS IN RPR TO RPR-GLS-ABAS IN RPR-FLD.

	MOVE 'S' TO RPR-KEY-TREG.
	CALL 'DESFIORPR' USING '<GET_KEY>'.
	IF NOT FIO-STAT-OKS
		GO TO FIN-LEE-RPR.
	MOVE RPR-IND-SORT IN RPR TO RPR-IND-SORT IN RPR-FLD.

	MOVE 'R' TO RPR-KEY-TREG.
	CALL 'DESFIORPR' USING '<GET_KEY>'.
	IF NOT FIO-STAT-OKS
		GO TO FIN-LEE-RPR.
	MOVE RPR-GLS-DREP IN RPR TO RPR-GLS-DREP IN RPR-FLD.
FIN-LEE-RPR.
	EXIT.
