       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.     GNSPPHDR.
       AUTHOR.         CONSIST.

      * Obtiene variables para Header standard de Formulario.

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSBRMEN REPLACING ==MEN== BY ==MEN EXTERNAL==.
       COPY GNSBRMNU REPLACING ==MNU== BY ==MNU EXTERNAL==.
       COPY GNSBRRPF.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFRM.
       COPY GNSWGSCR.
       COPY GNSWGPRV.
       COPY GNSWIHDR.
       01 WSS-VARI.
          03 WSS-IVEZ           VALUE SPACES               PIC X(02).
             88 WSS-PRIMERA-VEZ VALUE SPACES.
          03 WSS-SIMP           VALUE SPACES.
             05 WSS-GLS-SIMP    OCCURS 31                  PIC X(01).
          03 WSS-DOBL           VALUE SPACES.
             05 WSS-GLS-DOBL    OCCURS 61                  PIC X(01).
          03 WSS-SIND.
             05 I               VALUE 0                    PIC 9(02).
             05 K               VALUE 0                    PIC 9(02).
             05 L               VALUE 0                    PIC 9(02).
             05 IMIN            VALUE 0                    PIC 9(02).
             05 IMAX            VALUE 0                    PIC 9(02).
          03 WSS-MSTT                         VALUE SPACES PIC X(02).
          03 WSS-SIST                                      PIC X(03).
          03 WSS-PROG.
             05 WSS-CSIS                                   PIC X(03).
             05 FILLER                                     PIC X(09).

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
           PERFORM INI.
           PERFORM BUS-HDR.
           IF WSS-MSTT = SPACES
               PERFORM PUT-HDR.
           PERFORM FIN.
       FIN-MAIN.
           EXIT PROGRAM.

       INI SECTION.
      *-----------
       INI-INI.
      *JSS-INI 12-DEC-1995
           CALL 'LIB$GET_SYMBOL' USING
                    BY DESCRIPTOR 'GNS$SYS'
                                  SCR-USER.
      *JSS-FIN 12-DEC-1995
           MOVE SPACES TO HDR-FLD WSS-SIMP WSS-DOBL.
           CALL 'LIB$GET_SYMBOL' USING BY DESCRIPTOR 'GNS$PRY'
                                       BY DESCRIPTOR WSS-SIST.
      *     IF WSS-PRIMERA-VEZ
      *         MOVE 'NO' TO WSS-IVEZ
               PERFORM BUS-CRPF.

           MOVE FDV-ATERM TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
           PERFORM HDR-AWKSP.
           MOVE FRM-FTCA-NOT  TO FRM-FTCA.
           MOVE FRM-FWSP-NOT  TO FRM-FWSP.
           MOVE FRM-SINI-DBLF TO FRM-SINI.
           MOVE 92            TO FRM-CLIB.
           MOVE 'GNSFMS' TO FRM-NLIB.
           MOVE 'HDR'    TO FRM-NAME.
           MOVE FRM-INI TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
       FIN-INI.
           EXIT.

       BUS-CRPF SECTION.
      *----------------
       INI-BUS-CRPF.
           MOVE SPACES TO SCR-CRPF.
           MOVE ' Comando RET, Funcion PF4' TO SCR-PRMC.
           MOVE 'RET Seguir, PF4 a Comando' TO SCR-ABRT.

           MOVE FIO-IFNF-PGM TO FIO-IFNF.
           MOVE FIO-FNF      TO FIO-CMND.
           PERFORM GNS-FIO-RPF.

           MOVE SCR-PROG TO WSS-PROG.
           IF WSS-CSIS = 'GNS'
               MOVE 'GNSRPF' TO FIO-VOID
               MOVE FIO-VID  TO FIO-CMND
               PERFORM GNS-FIO-RPF.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-RPF.
           IF FIO-STAT-FNF
               MOVE FIO-IFNF-FIO TO FIO-IFNF
               MOVE FIO-FNF      TO FIO-CMND
               PERFORM GNS-FIO-RPF
               GO TO FIN-BUS-CRPF.

           MOVE SPACES   TO RPF-KEY-DPTR.
           MOVE SCR-PROG TO RPF-KEY-DPRO.
           MOVE 'C ' TO RPF-KEY-TRYC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-RPF.
           IF NOT FIO-STAT-OKS
               MOVE FIO-CLO TO FIO-CMND
               PERFORM GNS-FIO-RPF
               GO TO FIN-BUS-CRPF.

           IF RPF-GLS-DCMN NOT > SPACES
               NEXT SENTENCE
           ELSE
           IF RPF-GLS-DCMN NOT = 'NOMSG'
               MOVE RPF-GLS-DCMN TO SCR-PRMC
           ELSE
               MOVE SPACES TO SCR-PRMC.

           IF RPF-GLS-ABRT NOT > SPACES
               NEXT SENTENCE
           ELSE
           IF RPF-GLS-ABRT NOT = 'NOMSG'
               MOVE RPF-GLS-ABRT TO SCR-ABRT
           ELSE
               MOVE SPACES TO SCR-ABRT.

           PERFORM LEE-CRPF.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-RPF.
           MOVE SCR-PROG TO PRV-PROC.
           CALL 'GNSPCPRV'.
           PERFORM BUS-GLOS VARYING I FROM 1 BY 1 UNTIL I > 10.
       FIN-BUS-CRPF.
           EXIT.

       LEE-CRPF SECTION.
      *----------------
       INI-LEE-CRPF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-RPF.
           IF NOT ( FIO-STAT-OKS AND RPF-KEY-DPRO = SCR-PROG AND
                                     RPF-KEY-TREG = 'C' )
               GO TO FIN-LEE-CRPF.
           MOVE RPF-KEY-CORR TO I.
           MOVE RPF-COD-CCMN TO SCR-COD-CCMN( I + 1 )
           MOVE RPF-COD-CCPP TO SCR-COD-CCPP( I + 1 )
           MOVE RPF-IND-STPP TO SCR-IND-STPP( I + 1 )
           MOVE RPF-GLS-PRMT TO SCR-GLS-PRMT( I + 1 )
           MOVE RPF-GLS-CONF TO SCR-GLS-CONF( I + 1 )
           MOVE RPF-GLS-UARC TO SCR-GLS-UARC( I + 1 )
           GO TO INI-LEE-CRPF.
       FIN-LEE-CRPF.
           EXIT.

       BUS-GLOS SECTION.
       INI-BUS-GLOS.
           IF SCR-COD-CCMN( I ) NOT > SPACES
               GO TO FIN-BUS-GLOS.

           IF SCR-GLS-PRMT( I ) NOT > SPACES
               IF SCR-GLS-UARC( I ) NOT > SPACES
                   IF SCR-COD-CCPP(I) = 'ING'
                       MOVE 'Ingrese lo Deseado' TO SCR-GLS-PRMT( I )
                   ELSE
                   IF SCR-COD-CCPP(I) = 'MOD'
                       MOVE 'Modifique lo Deseado' TO SCR-GLS-PRMT( I )
                   ELSE
                   IF SCR-COD-CCPP(I) = 'ACT'
                       MOVE 'Digite lo Deseado' TO SCR-GLS-PRMT( I )
                   ELSE
                   IF SCR-COD-CCPP(I) = 'ELI'
                       MOVE 'Elimino Este ?' TO SCR-GLS-PRMT( I )
                   ELSE
                   IF SCR-COD-CCPP(I) = 'ACC' AND SCR-IND-STPP(I) = 'C'
                       MOVE 'Digite RETURN o PF4' TO SCR-GLS-PRMT( I )
                   ELSE
                       NEXT SENTENCE
               ELSE
                   MOVE 'Procesando Subrutina' TO SCR-GLS-PRMT( I )
           ELSE
           IF SCR-GLS-PRMT( I ) = 'NOMSG'
               MOVE SPACES TO SCR-GLS-PRMT( I ).
           IF SCR-GLS-CONF( I ) NOT > SPACES
               MOVE 'Esta Seguro ?' TO SCR-GLS-CONF( I )
           ELSE
           IF SCR-GLS-CONF( I ) = 'NOMSG'
               MOVE SPACES TO SCR-GLS-CONF( I ).
       FIN-BUS-GLOS.
           EXIT.

       BUS-HDR SECTION.
      *----------------
       INI-BUS-HDR.
           CALL 'GNSPGMEN' USING WSS-MSTT.
           IF WSS-MSTT = SPACES
               MOVE MEN TO MNU
           ELSE
               MOVE SPACES TO MNU.
       SCR-BUS-HDR.
           IF MNU-COD-SIST NOT > SPACES
               MOVE WSS-SIST TO MEN-COD-SIST
               MOVE WSS-SIST TO MNU-COD-SIST.

           MOVE MNU-COD-SIST TO SCR-SIST.
           MOVE WSS-SIST     TO SCR-SISG.

      *    MOVE MNU-COD-OPCI TO SCR-FUNC.
           MOVE MNU-COD-NEMO TO SCR-NEMO.
      *
      *    MOVE MNU-GLS-TRAN TO SCR-NTRN.
      *    MOVE MNU-GLS-PROG TO SCR-PROG.
      *    MOVE MNU-GLS-LFRM TO SCR-NLIB.
           MOVE MNU-GLS-LFVX TO SCR-NLIB.
      *    MOVE MNU-GLS-NFRM TO SCR-NMAP.
           MOVE MNU-GLS-NFVX TO SCR-NMAP.
      *    MOVE MNU-GLS-LDOC TO SCR-LDOC.
      *    MOVE MNU-GLS-NDOC TO SCR-NDOC.
           MOVE MNU-COD-FRET TO SCR-FRET.
      *    MOVE MNU-IND-IPRV TO SCR-IPRV.
           MOVE MNU-COD-TFUN TO SCR-TFUN.
       FIN-BUS-HDR.
           EXIT.

       PUT-HDR SECTION.
      *---------------
       INI-PUT-HDR.
           MOVE MEN-COD-SIST TO HDR-COD-SIST.
           MOVE MEN-COD-OPCI TO HDR-COD-OPCI.
           MOVE MEN-GLS-FPRO TO WSS-SIMP.
           PERFORM HDR-TITU.
           MOVE WSS-DOBL TO HDR-GLS-FPRO.
           IF SCR-FAMD = LOW-VALUES
               ACCEPT SCR-FAMD FROM DATE.
           IF SCR-FDMA = LOW-VALUES
               MOVE SCR-DAMD TO SCR-DDMA
               MOVE SCR-MAMD TO SCR-MDMA
               MOVE 19       TO SCR-SDMA
               MOVE SCR-AAMD TO SCR-ADMA.
           MOVE SCR-DDMA TO HDR-NUM-DMSA.
           MOVE '/'      TO HDR-GLS-1MSA.
           MOVE SCR-MDMA TO HDR-NUM-MMSA.
           MOVE '/'      TO HDR-GLS-2MSA.
           MOVE SCR-SDMA TO HDR-NUM-SMSA.
           MOVE SCR-ADMA TO HDR-NUM-AMSA.

           MOVE SPACES TO HDR-GLS-SISG.

           MOVE HDR-DFLD TO FRM-DFLD.
           MOVE FDV-PUTAL TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
           MOVE FRM-CLR-DSP TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
       FIN-PUT-HDR.
           EXIT.

       FIN SECTION.
      *-----------
       INI-FIN.
           MOVE SCR-IHDR-YES  TO SCR-IHDR.
           MOVE FRM-FWSP-YES  TO FRM-FWSP.
           MOVE FRM-SINI-DBNF TO FRM-SINI.
           MOVE 93            TO FRM-CLIB.
           MOVE SPACES        TO FRM-NLIB FRM-NAME FRM-MENS FRM-DFLD.
       FIN-FIN.
           EXIT.

       HDR-TITU SECTION.
      *----------------
       INI-HDR-TITU.
           IF WSS-SIMP = SPACES
               GO TO FIN-HDR-TITU.
           PERFORM BUS-LIMI VARYING IMIN FROM 1 BY 1
                            UNTIL WSS-GLS-SIMP(IMIN) NOT = SPACES.
           PERFORM BUS-LIMI VARYING IMAX FROM 31 BY -1
                            UNTIL WSS-GLS-SIMP(IMAX) NOT = SPACES.
           IF MEN-IND-EXGL IN MEN = 'N'
               COMPUTE L = ( 61 - ( IMAX - IMIN + 1 ) ) / 2
               MOVE 1 TO I
           ELSE
               COMPUTE
                  L = ( 61 - ( 2 * ( IMAX - IMIN + 1 ) - 1 ) ) / 2 + 1
               MOVE 2 TO I.
           PERFORM PUT-TITU
                   VARYING K FROM IMIN BY 1 UNTIL K > IMAX.
       FIN-HDR-TITU.
           EXIT.

       BUS-LIMI SECTION.
      *----------------
       INI-BUS-LIMI.
       FIN-BUS-LIMI.
           EXIT.

       PUT-TITU SECTION.
       INI-PUT-TITU.
           MOVE WSS-GLS-SIMP( K ) TO WSS-GLS-DOBL( L ).
           ADD I TO L.
       FIN-PUT-TITU.
           EXIT.

       HDR-AWKSP SECTION.
      *-----------------
       INI-HDR-AWKSP.
           CALL 'FDV$AWKSP' USING
                      BY DESCRIPTOR HDR-RWSP
                      BY REFERENCE  FRM-TWSP.
       FIN-HDR-AWKSP.
           EXIT.

       COPY GNSBGFRM.
       COPY GNSBFRPF.
