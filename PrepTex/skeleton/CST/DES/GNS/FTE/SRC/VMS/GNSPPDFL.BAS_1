10	sub gnsppdfl by desc(string  dfl_gls_nfil,			&
                             integer dfl_num_nkey by ref,		&
                             string dfl_gls_vkey(),			&
                             integer dfl_num_nchr by ref,		&
                             string dfl_gls_vchr(),			&
                             string dfl_gls_rreg,			&
                             string dfl_gls_rcod,			&
                             string dfl_gls_rkey,			&
                             string dfl_gls_rchr,			&
                             string dfl_gls_prmt,			&
                             integer dfl_num_filo by ref,		&
                             integer dfl_num_colo by ref,		&
                             integer dfl_num_nlin by ref,		&
                             string dfl_gls_sclr,			&
                             string dfl_gls_wclr			)


100	DIMENSION	DFL_VEC_REGI$(1000)
	DECLARE INTEGER	DFL_NUM_CFIL, DFL_NUM_CHRL, DFL_NUM_IMAX, DFL_NUM_NPAG
 !!!
	declare string	constant cursor_a	= esc+"[A"
	declare string	constant cursor_b	= esc+"[B"
	declare string	constant cursor_c	= esc+"[C"
	declare string	constant cursor_d	= esc+"[D"
	declare string	constant rst		= esc+"[0m"
	declare string	constant bold		= esc+"[1m"
	declare string	constant under		= esc+"[4m"
	declare string	constant blink		= esc+"[5m"
	declare string	constant reverse	= esc+"[7m"
	declare string	constant clear		= esc+"[H"+esc+"[J"
	declare string	constant w132		= esc+"[?3h"
	declare string	constant w80		= esc+"[?3l"
	declare string	constant scroll_up	= esc+"E"
	declare string	constant scroll_down	= esc+"[L"
	declare string	constant PF1		= "PF1"
	declare string	constant PF2		= "PF2"
	declare string	constant PF3		= "PF3"
	declare string	constant PF4		= "PF4"
 !!!
	external integer function sys$qiow
	external integer constant	io$_ttyreadall,	&
					io$m_noecho,	&
					io$m_nofiltr,	&
					io$m_trmnoecho,	&
					io$m_escape,	&
					io$_writevblk,	&
					io$_readvblk
	external integer function sys$assign
	map(m$get_map) chan%,iosb%(2),buf$=1%,primera_vez
	
115	def string cursor (linea,columna)
		cursor = esc+"["+str$(linea)+";"+str$(columna)+"f"
	end def
	
120	def get_char$
		func% = io$_readvblk + io$m_noecho + io$_nofiltr
		s% = sys$qiow	(,chan% by value,		&
				 func% by value,		&
				 iosb%() by ref			&
				 ,,,				&
				 buf$ by ref,			&
				 1% by value,			&
				 0% by value ,,,)
		if (s% and 1%) = 0% then
			print "ERROR en $QIOW : ";s%;
		end if
		get_char$ = buf$
	end def
	
130	def put_chars(caracter$)
		largo_string% = len(caracter$)
		func% = io$_writevblk
		s% = sys$qiow	(,chan% by value,		&
				 func% by value,		&
				 iosb%() by ref			&
				 ,,,				&
				 caracter$ by ref,		&
				 largo_string% by value,	&
				 0% by value ,,,)
		if (s% and 1%) = 0% then
			print "ERROR en $QIOW : ";s%;
		end if
	end def
	
140	s% = sys$assign('sys$command',chan%, , , )
	if (s% and 1%) = 0% then
		print "ERROR al inicializar el TERMINAL";
	end if
 !!!
	ON ERROR GO TO DFL_LBL_ERR
	dfl_gls_sclr = edit$(dfl_gls_sclr,32%)
	dfl_gls_wclr = edit$(dfl_gls_wclr,32%)
	DFL_NUM_CFIL = 1
	if not ( dfl_num_nlin > 0 ) then
	    dfl_num_nlin = 19
	end if
	if dfl_num_filo > 0 then
	    filo% = dfl_num_filo
	else
	    filo% = 3
	end if
	if dfl_num_colo > 0 then
	    colo% = dfl_num_colo
	else
	    if dfl_num_nchr > 0 then
	        colo% = 3
	    else
	        colo% = 1
	    end if
	end if
	I% = 0

	OPEN DFL_GLS_NFIL FOR INPUT AS FILE #DFL_NUM_CFIL,RECORDTYPE ANY, &
			ORGANIZATION SEQUENTIAL VARIABLE, RECORDSIZE 255

	if dfl_gls_sclr = "A" then
	    PRINT ESC;"[H";ESC;"[J";
	else
	    if dfl_gls_sclr = "B" then
	        PRINT CURSOR(filo%,1);ESC;"[J";
	    else
	        if not ( dfl_gls_sclr = "N" ) then
	            PRINT ESC;"[H";ESC;"[J";
                else
                end if
            end if
	end if

	print ESC;"[";STR$(FILO%);";";STR$(DFL_NUM_NLIN+FILO%-1);"r";CURSOR(FILO%,COLO%);
	if dfl_gls_wclr = "A" then
	    print cursor(filo%,1);ESC;"[";str$(dfl_num_nlin);"L"
        else
	    if not ( dfl_gls_wclr = "N" ) then
	        print cursor(filo%,1);ESC;"[";str$(dfl_num_nlin);"L"
	    else
	    end if
	end if

	j% = filo% - 1
200
	GET #DFL_NUM_CFIL
	DFL_NUM_CHRL = RECOUNT
	MOVE FROM #DFL_NUM_CFIL , DFL_GLS_REGI$ = DFL_NUM_CHRL
	I% = I% + 1
	j% = j% + 1
	IF I% <= DFL_NUM_NLIN THEN
		if i% < dfl_num_nlin then
			print cursor(j%,colo%);DFL_GLS_REGI$
		else
			print cursor(j%,colo%);DFL_GLS_REGI$;
		end if
	END IF
	DFL_VEC_REGI$(I%) = DFL_GLS_REGI$
	DFL_NUM_IMAX = I%
	GO TO 200
 !!!
300	j% = DFL_NUM_NLIN + filo%
 !!!	print cursor(j%,1);dfl_gls_prmt;
	print cursor(23%,1);dfl_gls_prmt;
	dfl_num_npag = 1
	fil% = filo%
	col% = colo%
	i% = 1
	print cursor(fil%,col%);reverse;dfl_vec_regi$(i%);rst;cursor(fil%,col%);
	dfl_gls_msg$ = ""
	teclar$ = " "
310	tecla$ = ""
	PRINT CURSOR(FIL%,1);
	x$ = get_char$
	if dfl_gls_msg$ <> "" then
		print cursor(24,1);esc;"[K"
		dfl_gls_msg$ = ""
	end if
	select x$
	case chr$(27)
		x$ = get_char$
		select x$
		case "["
			x$ = get_char$
			select x$
			case "A"
				tecla$ = "AUP"
			case "B"
				tecla$ = "ADW"
			case "C"
				tecla$ = "ARG"
			case "D"
				tecla$ = "ALF"
			case else
			end select
		case "O"
			x$ = get_char$
			select x$
			case "P"
				tecla$ = "PF1"
			case "Q"
				tecla$ = "PF2"
			case "R"
				tecla$ = "PF3"
			case "S"
				tecla$ = "PF4"
			case else
			end select
		case else
		end select
	case chr$(8)
		tecla$ = "AUP"
	case chr$(9)
		tecla$ = "ADW"
	case chr$(13)
                tecla$ = "RET"
	case chr$(32) to chr$(126)
		tecla$ = x$
	case else
	end select

320	select tecla$
	case "AUP"
		if i% > 1 then
                   teclar$ = " "
		   if dfl_num_nchr > 0 then
			print cursor(fil%,1%);" ";
		   end if
		   print cursor(fil%,col%);rst;dfl_vec_regi$(i%);cursor(fil%,col%);
		   i% = i% - 1
		   if fil% = filo% then
		      print cursor(filo%,colo%);esc;"[L";
		      print cursor(fil%,col%);reverse;dfl_vec_regi$(i%);rst;cursor(fil%,col%);
		   else
		      fil% = fil% - 1
		      print cursor_a;reverse;dfl_vec_regi$(i%);rst;cursor(fil%,col%);
		   end if
		else
 !!!		   print cursor(fil%,col%);reverse;dfl_vec_regi$(i%);rst;cursor(fil%,col%);
		end if
		go to 310
	case "ADW"
		if i% < dfl_num_imax then
                   teclar$ = " "
		   if dfl_num_nchr > 0 then
			print cursor(fil%,1%);" ";
		   end if
		   print cursor(fil%,col%);rst;dfl_vec_regi$(i%);cursor(fil%,col%);
		   i% = i% + 1
		   if fil% = ( dfl_num_nlin + ( filo% - 1 ) ) then
		      print cursor(filo%,colo%);esc;"[M";
		      print cursor(fil%,col%);reverse;dfl_vec_regi$(i%);rst;cursor(fil%,col%);
		   else
		      fil% = fil% + 1
		      print cursor_b;reverse;dfl_vec_regi$(i%);rst;cursor(fil%,col%);
		   end if
		else
 !!!		   print cursor(fil%,col%);reverse;dfl_vec_regi$(i%);rst;cursor(fil%,col%);
		end if
		go to 310
	case else
	end select

        sw% = 0
        k% = 1
        while k% <= dfl_num_nchr
            if edit$(tecla$,32%) = edit$(dfl_gls_vchr(k%),32%) then
                sw% = 1
                print cursor(fil%,1);tecla$
                teclar$ = edit$(tecla$,32%)
                k% = dfl_num_nchr + 1
            else
                k% = k% + 1
            end if
        next
        if sw% = 1 then
            goto 310
        end if

        sw% = 0
        k% = 1
        while k% <= dfl_num_nkey
            if tecla$ = dfl_gls_vkey(k%) then
                k% = dfl_num_nkey + 1
                if dfl_num_nchr < 1 then
                    sw% = 1
                else
                    if teclar$ = " " then
                        sw% = 0
                        j% = 1
                        while j% <= dfl_num_nchr
                           if dfl_gls_vchr(j%) = " " then
                                sw% = 1
                                j% = dfl_num_nchr + 1
                            else
                                j% = j% + 1
                            end if
                        next
                        if sw% = 0 then
		            dfl_gls_msg$ = "Debe digitar opcion"
                            print cursor(24,1);dfl_gls_msg$
			    go to 310
			end if
                    else
                        sw% = 1
                        k% = dfl_num_nkey + 1
                    end if
                end if
            else
                k% = k% + 1
            end if
        next
        if sw% = 0 then
            goto 310
        end if
 !!!
	dfl_gls_rreg = dfl_vec_regi$(i%)
	dfl_gls_rkey = tecla$
	dfl_gls_rchr = teclar$

	sw% = 0
        i% = 1
	dfl_gls_rcod = ""
	while i% <= len(dfl_gls_rreg)
		if mid$(dfl_gls_rreg,i%,1) <> " " then
		    sw% = i%
		    i% = len(dfl_gls_rreg) + 1
		else
		    i% = i% + 1
		end if
	next
	i% = sw%
	while i% <= len(dfl_gls_rreg)
		if mid$(dfl_gls_rreg,i%,1) <> " " then
			dfl_gls_rcod = dfl_gls_rcod + mid$(dfl_gls_rreg,i%,1)
			i% = i% + 1
		else
			i% = len(dfl_gls_rreg) + 1
		end if
	next

	PRINT ESC;"[1;24r";cursor(23,1)
	GO TO 9999

 DFL_LBL_ERR:
	IF ERR = 11 THEN
		CLOSE #DFL_NUM_CFIL
		RESUME 300
	ELSE 
		PRINT	"Error(";ERR;") ";ERT$(ERR);" en linea ";erl; &
			", Modulo ";ern$
		RESUME 9999
	END IF
9999
	END SUB
