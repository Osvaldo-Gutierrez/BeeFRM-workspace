       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.    GNSPPMNU.
       AUTHOR.        GUZMAN Y CIA.

      * Procesa Menus

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWIOPC.
       COPY GNSBRMEN REPLACING ==MEN== BY ==MEN EXTERNAL==.
       COPY GNSBRMNU REPLACING ==MNU== BY ==MNU EXTERNAL==.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFRM.
       COPY GNSWGSCR.
       COPY GNSWGPRV.
       COPY GNSWIHDR.
       01  WSS-VARI.
           03 WSS-ZERO           COMP             VALUE 0 PIC 9(04).
           03 WSS-AUXI           COMP                     PIC 9(04).
           03 WSS-STAT                         VALUE '  ' PIC X(02).
           03 WSS-STAT-OKS                     VALUE '  ' PIC X(02).
           03 WSS-STAT-WRN                     VALUE 'WR' PIC X(02).
           03 WSS-STAT-MAL                     VALUE 'ML' PIC X(02).
           03 WSS-MSTT                         VALUE '  ' PIC X(02).
           03 WSS-IVEZ                            VALUE 0 PIC 9(01).
              88 PRIMERA-VEZ                      VALUE 0.
           03 WSS-SIND.
              05 WSS-I           COMP                     PIC 9(02).
       
       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
           PERFORM INI.
       LUP-MAIN.
           CALL 'GNSPMHDR'.
           MOVE WSS-STAT-OKS TO WSS-STAT.
           MOVE SPACES TO OPC-DFLD.
           PERFORM BUS-MENU.
           PERFORM BUS-FUNC UNTIL NOT
                      ( WSS-STAT = WSS-STAT-OKS AND FIO-STAT-OKS AND
                        MEN-COD-MENU = MNU-COD-CMEN )
           PERFORM PUT-HDR.
           IF WSS-STAT = WSS-STAT-OKS
                      PERFORM PUT-FUNC.
           PERFORM FIN.
           MOVE 1 TO WSS-IVEZ.
           GO TO LUP-MAIN.
       FIN-MAIN.
           EXIT.

       INI SECTION.
      *-----------
       INI-INI.
           MOVE 'GNSPPMNU' TO SCR-PROG.

           MOVE FIO-IFNF-PGM TO FIO-IFNF.
           MOVE FIO-FNF      TO FIO-CMND.
           PERFORM GNS-FIO-MNU.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-MNU.
           IF FIO-STAT-FNF
               DISPLAY '? No existe archivo MNU' LINE 22 COLUMN 1
                       WITH BELL REVERSED
               PERFORM EXT-PROG.

           PERFORM BUS-HDR.
           MOVE OPC-UARV    TO FRM-UARV.

           MOVE FDV-ATERM TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
           PERFORM HDR-AWKSP.
           MOVE FRM-FTCA-NOT  TO FRM-FTCA.
           MOVE FRM-FWSP-NOT  TO FRM-FWSP.
           MOVE FRM-SINI-DBLF TO FRM-SINI.
           MOVE 92            TO FRM-CLIB.
           MOVE 'GNSFMS' TO FRM-NLIB.
           MOVE 'HDR'    TO FRM-NAME.
           MOVE FRM-INI TO FRM-CMND.
           PERFORM GNS-PRO-FRM.

           PERFORM OPC-AWKSP.
           MOVE OPC-RWSP                      TO FRM-RWSP.
           MOVE 92                                 TO FRM-CLIB.
           MOVE FRM-SINI-MBLF           TO FRM-SINI.
           MOVE 'GNSFMS'                      TO FRM-NLIB.
           MOVE 'OPC'                      TO FRM-NAME.
           MOVE FRM-INI TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
       FIN-INI.
           EXIT.

       BUS-HDR SECTION.
      *---------------
       INI-BUS-HDR.
           CALL 'GNSPGMEN' USING WSS-MSTT.
           IF WSS-MSTT > SPACES
               GO TO SIS-BUS-HDR.
           IF MEN-COD-TFUN = 'MEN'
               GO TO FIN-BUS-HDR.
           IF MEN-COD-FRET = SPACES
               GO TO SIS-BUS-HDR.

           MOVE MEN-COD-FRET TO MNU-COD-NEMO.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM GNS-FIO-MNU.
           IF FIO-STAT-OKS
               IF MNU-COD-TFUN = 'MEN'
                   MOVE MNU TO MEN
                   CALL 'GNSPWMEN' USING WSS-MSTT
                   GO TO FIN-BUS-HDR
               ELSE
                   IF MNU-COD-TFUN = 'SUB'
                       MOVE SPACES TO FRM-MENS
                       STRING 'Funcion tipo SUBMENU ' MEN-COD-FRET
                              ' NO puede ser INICIAL : ABORTO'
                              DELIMITED BY SIZE INTO FRM-MENS
                       DISPLAY FRM-MENS LINE 22 COLUMN 1
                                        WITH BELL REVERSED
                       PERFORM EXT-PROG
                   ELSE
                       MOVE MNU TO MEN
                       CALL 'GNSPWMEN' USING WSS-MSTT
                       MOVE FIO-CLO TO FIO-CMND
                       PERFORM GNS-FIO-MNU
                       CALL 'LIB$DO_COMMAND' USING
                            BY DESCRIPTOR MNU-GLS-CMND
           ELSE
                      MOVE SPACES TO FRM-MENS
                      STRING  'NO EXISTE Funcion ( Nemotecnica ) '
                                 MEN-COD-FRET ' : ABORTO'
                                 DELIMITED BY SIZE INTO FRM-MENS
                      DISPLAY FRM-MENS LINE 22 COLUMN 1
                                       WITH BELL REVERSED
                      PERFORM EXT-PROG.
       SIS-BUS-HDR.
           MOVE 'SIS' TO MNU-COD-NEMO.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MNU.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE Registro SISTEMA : ABORTO' TO FRM-MENS
               DISPLAY FRM-MENS LINE 22 COLUMN 1
                                WITH BELL REVERSED
               PERFORM EXT-PROG.

           MOVE MNU TO MEN
           CALL 'GNSPWMEN' USING WSS-MSTT.
       FIN-BUS-HDR.
           EXIT.

       BUS-MENU SECTION.
      *----------------
       INI-BUS-MENU.
           MOVE MEN-COD-NEMO TO PRV-PROC.
           CALL 'GNSPPPRV'.
           IF PRV-STAT-OKS
               GO TO CON-BUS-MENU.
           IF MEN-COD-NEMO = 'SIS'
               MOVE 'NO tiene ACCESO al SISTEMA : ABORTO' TO FRM-MENS
               MOVE WSS-STAT-MAL TO WSS-STAT
           ELSE
               MOVE 'NO tiene PRIVILEGIOS para este MENU' TO FRM-MENS
               MOVE WSS-STAT-WRN TO WSS-STAT.
           GO TO FIN-BUS-MENU.
       CON-BUS-MENU.
           MOVE SPACES       TO MNU-COD-IMEN.
           MOVE MEN-COD-MENU TO MNU-COD-CMEN.
           MOVE 'MNU-COD-IMEN' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM GNS-FIO-MNU.
           IF FIO-STAT-OKS AND MNU-COD-CMEN = MEN-COD-MENU
               GO TO FIN-BUS-MENU.
           IF MEN-COD-NEMO = 'SIS'
               MOVE 'NO existen OPCIONES para este SISTEMA : ABORTO'
                    TO FRM-MENS
               MOVE WSS-STAT-MAL TO WSS-STAT
           ELSE
               MOVE 'NO existen OPCIONES para este MENU' TO FRM-MENS
               MOVE WSS-STAT-WRN TO WSS-STAT.
       FIN-BUS-MENU.
           EXIT.

       BUS-FUNC SECTION.
      *----------------
       INI-BUS-FUNC.
           MOVE MNU-COD-NEMO TO PRV-PROC.
           CALL 'GNSPPPRV'.
           IF NOT PRV-STAT-OKS
               GO TO NXT-BUS-FUNC.
           IF MNU-NUM-FILA < 3 OR MNU-NUM-FILA > 22
               MOVE 'FILA NO VALIDA para opcion del MENU' TO FRM-MENS
               MOVE WSS-STAT-WRN TO WSS-STAT
               GO TO FIN-BUS-FUNC.
           SUBTRACT 2 FROM MNU-NUM-FILA.
           IF MNU-NUM-COLU = 1
               MOVE MNU-GLS-FMEN TO OPC-GLS-FMEN(MNU-NUM-FILA,1)
               MOVE MNU-COD-OPCI TO OPC-COD-OPCI(MNU-NUM-FILA,1)
           ELSE
           IF MNU-NUM-COLU = 41
               MOVE MNU-GLS-FMEN TO OPC-GLS-FMEN(MNU-NUM-FILA,2)
               MOVE MNU-COD-OPCI TO OPC-COD-OPCI(MNU-NUM-FILA,2)
           ELSE
           IF MNU-NUM-COLU = 24
               MOVE MNU-GLS-FMEN TO OPC-GLS-FME3(MNU-NUM-FILA)
               MOVE MNU-COD-OPCI TO OPC-COD-OPC3(MNU-NUM-FILA)
           ELSE
               MOVE 'COLUMNA NO VALIDA para opcion del MENU' TO FRM-MENS
               MOVE WSS-STAT-WRN TO WSS-STAT
               GO TO FIN-BUS-FUNC.
       NXT-BUS-FUNC.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           MOVE 'MNU-COD-IMEN' TO FIO-AKEY.
           PERFORM GNS-FIO-MNU.
       FIN-BUS-FUNC.
           EXIT.

       PUT-HDR SECTION.
      *---------------
       INI-PUT-HDR.
           PERFORM HDR-SWKSP.
           MOVE HDR-DFLD TO FRM-DFLD.
           IF NOT PRIMERA-VEZ
               MOVE FDV-PUTAL TO FRM-CMND
               PERFORM GNS-PRO-FRM
           ELSE
               MOVE FDV-PUTAL TO FRM-CMND
               PERFORM GNS-PRO-FRM
               MOVE FRM-CLR-DSP TO FRM-CMND
               PERFORM GNS-PRO-FRM.
       FIN-PUT-HDR.
           EXIT.

       PUT-FUNC SECTION.
      *----------------
       INI-PUT-FUNC.
           PERFORM OPC-SWKSP.
           MOVE OPC-DFLD TO FRM-DFLD.
           IF NOT PRIMERA-VEZ
               MOVE FDV-PUTAL TO FRM-CMND
               PERFORM GNS-PRO-FRM
           ELSE
               MOVE FDV-PUTAL TO FRM-CMND
               PERFORM GNS-PRO-FRM
               MOVE FRM-DSP-FRM TO FRM-CMND
               PERFORM GNS-PRO-FRM.
       FIN-PUT-FUNC.
           EXIT.

       FIN SECTION.
      *-----------
       INI-FIN.
           IF WSS-STAT = WSS-STAT-WRN
               MOVE FRM-MSG TO FRM-CMND
               PERFORM GNS-PRO-FRM.
           IF WSS-STAT = WSS-STAT-MAL
               DISPLAY FRM-MENS LINE 22 COLUMN 1
                                WITH BELL REVERSED
               PERFORM EXT-PROG
           ELSE
               CALL 'GNSPMCMN'.
       FIN-FIN.
           EXIT.

       HDR-AWKSP SECTION.
      *-----------------
       INI-HDR-AWKSP.
           CALL 'FDV$AWKSP' USING
                      BY DESCRIPTOR HDR-RWSP
                      BY REFERENCE  FRM-TWSP.
       FIN-HDR-AWKSP.
           EXIT.

       HDR-SWKSP SECTION.
      *-----------------
       INI-HDR-SWKSP.
           CALL 'FDV$SWKSP' USING
                      BY DESCRIPTOR HDR-RWSP.
       FIN-HDR-SWKSP.
           EXIT.

       OPC-AWKSP SECTION.
      *-----------------
       INI-OPC-AWKSP.
           CALL 'FDV$AWKSP' USING
                      BY DESCRIPTOR OPC-RWSP
                      BY REFERENCE  FRM-TWSP.
       FIN-OPC-AWKSP.
           EXIT.

       OPC-SWKSP SECTION.
      *-----------------
       INI-OPC-SWKSP.
           CALL 'FDV$SWKSP' USING
                      BY DESCRIPTOR OPC-RWSP.
       FIN-OPC-SWKSP.
           EXIT.

       EXT-PROG SECTION.
       INI-EXT-PROG.
           DISPLAY "".
           CALL 'SYS$EXIT' USING BY VALUE ZERO.
       FIN-EXT-PROG.
           EXIT.

       COPY GNSBGFRM.
       COPY GNSBFMNU.
