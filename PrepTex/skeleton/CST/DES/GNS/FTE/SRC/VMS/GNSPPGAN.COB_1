       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GNSPPGAN.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    12-Feb-92 08:23 PM.

      * Programa Listador de Reporte GAN sin Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
           SELECT RPTGAN       ASSIGN TO RPTGAN.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       FD  RPTGAN LABEL RECORD OMITTED
           REPORT IS RPT-GAN
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSBRTEX.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSRWGAN.
       COPY GNSWGSYS.
       COPY GNSWGHOY.
       COPY GNSWGFEC.
       01  WSS-VARI.
           03 WSS-INIC               VALUE   0 PIC 9(03).
           03 WSS-INDI               VALUE   0 PIC 9(03).
           03 WSS-JOTA               VALUE   0 PIC 9(03).
           03 WSS-KKKK               VALUE   0 PIC 9(03).
           03 WSS-LLLL               VALUE   0 PIC 9(03).
           03 WSS-XXXX               VALUE   0 PIC 9(03).
           03 WSS-OMES               VALUE   0 PIC 9(02).
           03 WSS-NCHR               VALUE   0 PIC 9(03).
           03 WSS-MRGN               VALUE   0 PIC 9(03).
           03 WSS-LIMI               VALUE 132 PIC 9(03).
           03 WSS-NDIA               VALUE   0 PIC 9(01).
           03 WSS-NMES               VALUE   0 PIC 9(02).
           03 WSS-IVEZ               VALUE   0 PIC 9(01).
           03 WSS-ISEM               VALUE   0 PIC 9(01).
           03 WSS-IMES               VALUE   0 PIC 9(01).
           03 WSS-GDIA                         PIC Z9.
           03 WSS-GDIA-RED REDEFINES WSS-GDIA.
              05 WSS-DED1                      PIC X(01).
              05 WSS-DED2                      PIC X(01).
           03 WSS-FINI.
              05 WSS-DINI                      PIC 9(02).
              05 WSS-MINI                      PIC 9(02).
              05 WSS-SINI                      PIC 9(02).
              05 WSS-AINI                      PIC 9(02).
           03 WSS-DET1.
              05 WSS-CHR-DET1       OCCURS 132 PIC X(01).
           03 WSS-DET2.
              05 WSS-CHR-DET2       OCCURS 132 PIC X(01).
           03 WSS-TIT1.
              05 WSS-CHR-TIT1       OCCURS 132 PIC X(01).
           03 WSS-TIT3.
              05 WSS-CHR-TIT3       OCCURS 132 PIC X(01).
           03 WSS-TIT4.
              05 WSS-CHR-TIT4       OCCURS 132 PIC X(01).
           03 WSS-IBLK                         PIC X(02).
           03 WSS-TMES.
              05 WSS-GENE  VALUE 'ENERO'       PIC X(10).
              05 WSS-GFEB  VALUE 'FEBRERO'     PIC X(10).
              05 WSS-GMAR  VALUE 'MARZO'       PIC X(10).
              05 WSS-GABR  VALUE 'ABRIL'       PIC X(10).
              05 WSS-GMAY  VALUE 'MAYO'        PIC X(10).
              05 WSS-GJUN  VALUE 'JUNIO'       PIC X(10).
              05 WSS-GJUL  VALUE 'JULIO'       PIC X(10).
              05 WSS-GAGO  VALUE 'AGOSTO'      PIC X(10).
              05 WSS-GSEP  VALUE 'SEPTIEMBRE'  PIC X(10).
              05 WSS-GOCT  VALUE 'OCTUBRE'     PIC X(10).
              05 WSS-GNOV  VALUE 'NOVIEMBRE'   PIC X(10).
              05 WSS-GDIC  VALUE 'DICIEMBRE'   PIC X(10).
              05 WSS-LENE  VALUE  5            PIC 9(02).
              05 WSS-LFEB  VALUE  7            PIC 9(02).
              05 WSS-LMAR  VALUE  5            PIC 9(02).
              05 WSS-LABR  VALUE  5            PIC 9(02).
              05 WSS-LMAY  VALUE  4            PIC 9(02).
              05 WSS-LJUN  VALUE  5            PIC 9(02).
              05 WSS-LJUL  VALUE  5            PIC 9(02).
              05 WSS-LAGO  VALUE  6            PIC 9(02).
              05 WSS-LSEP  VALUE 10            PIC 9(02).
              05 WSS-LOCT  VALUE  7            PIC 9(02).
              05 WSS-LNOV  VALUE  9            PIC 9(02).
              05 WSS-LDIC  VALUE  9            PIC 9(02).
           03 WSS-TMES-RED REDEFINES WSS-TMES.
              05 WSS-GMES  OCCURS 12           PIC X(10).
              05 WSS-LMES  OCCURS 12           PIC 9(02).
           03 WSS-GAUX.
              05 WSS-CAUX  OCCURS 10           PIC X(01).

       REPORT SECTION.
      *--------------
       COPY GNSRFGAN.
           .
       COPY GNSRTGAN.

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
           MOVE 'GNSPPGAN' TO FIO-PROG.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-INP      TO FIO-CMND.
           PERFORM GNS-FIO-TEX.

           PERFORM INI.
           MOVE WSS-TIT1 TO WSS-GLS-TIT1.
           MOVE WSS-DET2 TO WSS-GLS-TIT2
                            WSS-GLS-DET2.
           MOVE WSS-TIT3 TO WSS-GLS-TIT3.
           MOVE WSS-TIT4 TO WSS-GLS-TIT4.
           MOVE WSS-DET2 TO WSS-GLS-TIT5.

           CALL 'GNSPLHDR' USING RPT-VARI.
           OPEN OUTPUT RPTGAN.
           INITIATE RPT-GAN.
           MOVE 0    TO WSS-INDI.
           MOVE 'SI' TO WSS-IBLK.
       LUP-MAIN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TEX.
           IF NOT FIO-STAT-SOK
               GO TO TMT-MAIN.
           IF TEX-GLS-TEXT IN TEX NOT > SPACES
               IF WSS-IBLK = 'SI'
                   GO TO LUP-MAIN
               ELSE
                   GENERATE DL-SEP
                   MOVE 'SI' TO WSS-IBLK
                   GO TO LUP-MAIN
           ELSE
               MOVE 'NO' TO WSS-IBLK.
           MOVE 0 TO WSS-INDI.
       LU1-MAIN.
           ADD 1 TO WSS-INDI.
           IF WSS-INDI NOT < WSS-INIC
               GO TO CON-MAIN.
           IF TEX-CHR-TEXT IN TEX(WSS-INDI) = '	'
               MOVE ' ' TO WSS-CHR-DET1(WSS-INDI)
           ELSE
               MOVE TEX-CHR-TEXT IN TEX(WSS-INDI) TO
                    WSS-CHR-DET1(WSS-INDI).
           GO TO LU1-MAIN.
       CON-MAIN.
           MOVE WSS-DET1 TO WSS-GLS-DET1
           GENERATE DL-GAN.
           GO TO LUP-MAIN.
       TMT-MAIN.
           GENERATE DL-SEP.
           TERMINATE RPT-GAN.
           CLOSE RPTGAN.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INI SECTION.
       INI-INI.
           ACCEPT WSS-FINI.
           IF WSS-FINI NOT > ZEROES
               PERFORM GET-FHOY
               MOVE HOY-DHOY TO WSS-DINI
               MOVE HOY-MHOY TO WSS-MINI
               MOVE HOY-SHOY TO WSS-SINI
               MOVE HOY-AHOY TO WSS-AINI.
           ACCEPT WSS-INIC WITH CONVERSION.
           IF WSS-INIC = 0
               MOVE 30 TO WSS-INIC
           ELSE
               ADD 1 TO WSS-INIC.

           MOVE SPACES TO WSS-TIT1 WSS-TIT3 WSS-TIT4
                          WSS-DET1 WSS-DET2.
           MOVE 0 TO WSS-IVEZ.
           MOVE 0 TO WSS-NMES.
           MOVE 0 TO WSS-INDI.
       LUP-INI.
           ADD 1 TO WSS-INDI.
           IF WSS-INDI < WSS-INIC
               MOVE '-' TO WSS-CHR-DET2(WSS-INDI)
               GO TO LUP-INI.
           SUBTRACT 1 FROM WSS-INIC GIVING WSS-INDI.

           MOVE WSS-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF ( NOT FEC-STAT-OKS ) AND ( NOT FEC-STAT-FER )
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
       LU1-INI.
           MOVE FEC-RET-JUL TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
           DIVIDE FEC-FJUL BY 7 GIVING WSS-NDIA REMAINDER WSS-NDIA.
           IF WSS-NDIA = 6 OR 0
               GO TO NXT-INI.
           IF WSS-NMES > 0 AND FEC-ITM2 NOT = WSS-NMES
               MOVE 0 TO WSS-IMES
           ELSE
               MOVE 1 TO WSS-IMES
               IF WSS-NDIA = 1 OR WSS-IVEZ = 0
                   MOVE 0 TO WSS-ISEM
               ELSE
                   MOVE 1 TO WSS-ISEM.
           MOVE FEC-ITM2 TO WSS-NMES.
           ADD 1 TO WSS-INDI.
           IF WSS-INDI > WSS-LIMI
               GO TO CON-INI.
           IF WSS-IVEZ = 0
               MOVE FEC-ITM2 TO WSS-OMES
               MOVE '|' TO WSS-CHR-TIT1(WSS-INDI).
           IF WSS-IMES = 0
               MOVE 'T' TO WSS-CHR-TIT1(WSS-INDI)
               MOVE 'T' TO WSS-CHR-TIT3(WSS-INDI)
               MOVE 'T' TO WSS-CHR-TIT4(WSS-INDI)
               MOVE 'T' TO WSS-CHR-DET1(WSS-INDI)
               MOVE 'T' TO WSS-CHR-DET2(WSS-INDI)
           ELSE
           IF WSS-ISEM = 0
               MOVE '|' TO WSS-CHR-TIT3(WSS-INDI)
               MOVE '|' TO WSS-CHR-TIT4(WSS-INDI)
               MOVE '|' TO WSS-CHR-DET1(WSS-INDI)
               MOVE '+' TO WSS-CHR-DET2(WSS-INDI)
           ELSE
               MOVE '.' TO WSS-CHR-DET1(WSS-INDI)
               MOVE '-' TO WSS-CHR-DET2(WSS-INDI).
           ADD 1 TO WSS-INDI.
           IF WSS-INDI > WSS-LIMI
               GO TO CON-INI.
           MOVE ' '      TO WSS-CHR-DET1(WSS-INDI).
           MOVE '-'      TO WSS-CHR-DET2(WSS-INDI).
           MOVE FEC-ITM1 TO WSS-GDIA.
           MOVE WSS-DED1 TO WSS-CHR-TIT3(WSS-INDI).
           MOVE WSS-DED2 TO WSS-CHR-TIT4(WSS-INDI).
           MOVE 1 TO WSS-IVEZ.
       NXT-INI.
           MOVE 1           TO FEC-NDIA.
           MOVE FEC-SUM-DIA TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
           GO TO LU1-INI.
       CON-INI.
           MOVE WSS-INIC     TO     WSS-INDI.
           ADD 1 TO WSS-INDI GIVING WSS-JOTA.
       LU2-INI.
           ADD 1 TO WSS-INDI.
           IF WSS-INDI > WSS-LIMI
               PERFORM PRO-GMES
               GO TO FIN-INI.
           IF WSS-CHR-TIT1(WSS-INDI) = 'T'
               PERFORM PRO-GMES
               ADD 1 TO WSS-INDI GIVING WSS-JOTA.
           GO TO LU2-INI.
       FIN-INI.
           EXIT.

       PRO-GMES SECTION.
       INI-PRO-GMES.
           SUBTRACT WSS-JOTA FROM WSS-INDI GIVING WSS-NCHR.
           IF WSS-NCHR > WSS-LMES(WSS-OMES)
               COMPUTE WSS-MRGN = ( WSS-NCHR - WSS-LMES(WSS-OMES) ) / 2
           ELSE
               MOVE 0 TO WSS-MRGN.
           MOVE WSS-GMES(WSS-OMES) TO WSS-GAUX.
           MOVE 0 TO WSS-KKKK.
           COMPUTE   WSS-LLLL = WSS-JOTA + WSS-MRGN - 1.
       LUP-PRO-GMES.
           ADD 1 TO WSS-KKKK.
           ADD 1 TO WSS-LLLL.
           SUBTRACT 1 FROM WSS-INDI GIVING WSS-XXXX.
           IF WSS-KKKK > WSS-LMES(WSS-OMES) OR WSS-LLLL > WSS-XXXX
               GO TO FIN-PRO-GMES.
           MOVE WSS-CAUX(WSS-KKKK) TO WSS-CHR-TIT1(WSS-LLLL).
           GO TO LUP-PRO-GMES.
       FIN-PRO-GMES.
           ADD 1 TO WSS-OMES.
           IF WSS-OMES > 12
               MOVE 1 TO WSS-OMES.
       EXT-PRO-GMES.
           EXIT.

       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFTEX.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBGHOY.
       COPY GNSBGFEC.

       GNS-PRO-END SECTION.
       INI-GNS-PRO-END.
           STOP RUN.
       FIN-GNS-PRO-END.
           EXIT.
