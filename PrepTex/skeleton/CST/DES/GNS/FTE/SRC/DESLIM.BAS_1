100	DECLARE STRING	FILE_INP, FILE_OUT, REGISTRO, EXTEN
	DECLARE INTEGER C_FILE_INP,C_FILE_OUT
	ON ERROR GO TO LAB_ERR

 !!!	  Este programa procesa un archivo de entrada con extension "SRT" y
 !!!    genera un archivo de salida con extension "TXT" o con  la extension
 !!!	que se desee.
 !!!	  Si en FILE_INP no se recibe un ".", se  asume  que  la extension 
 !!!	del archivo de salida es "TXT", si en FILE_INP se recibe un "." la
 !!!	extension del archivo de salida es el substring a continuacion del
 !!!	"." hasta el final de FILE_INP
 !!!    Ej: si FILE_INP  RPR 	 ==>	archivo de entrada RPR.SRT
 !!!	    				archivo de salida  RPR.TXT
 !!!	
 !!!	    si FILE_INP  SCR.FLG ==>	archivo de entrada SCR.SRT
 !!!					archivo de salida      SCR.FLG
 !!!	  La funcion de este programa es sacar a cada registro los 9  pri-
 !!!	meros caracteres , los cuales se usaron para ordenar el archivo.
 !!! 
	INPUT FILE_INP
	I% = INSTR(1%,FILE_INP,".")
	IF I% = 0 THEN
		EXTEN = ".TXT"
	ELSE
		EXTEN = MID$(FILE_INP,I%,LEN(FILE_INP) -  (I% - 1))
		FILE_INP = MID$(FILE_INP,1%,I% - 1)
	END IF
	FILE_OUT = FILE_INP + EXTEN
	FILE_INP = FILE_INP + ".SRT"
	C_FILE_INP = 1
	C_FILE_OUT = 2

	OPEN FILE_INP FOR INPUT AS FILE #C_FILE_INP,			&
		ORGANIZATION SEQUENTIAL VARIABLE, RECORDSIZE 255,	&
		RECORDTYPE ANY

200	OPEN FILE_OUT FOR OUTPUT AS FILE #C_FILE_OUT,			&
		ORGANIZATION SEQUENTIAL VARIABLE, RECORDSIZE 255,	&
		RECORDTYPE ANY
 SIGA:
300		GET #C_FILE_INP
	     	MOVE FROM #C_FILE_INP , REGISTRO = RECOUNT
		REGISTRO = MID$(REGISTRO, 10,LEN(REGISTRO) - 9)
		REGISTRO = edit$(REGISTRO,128%)
		PRINT #C_FILE_OUT, REGISTRO
		GO TO SIGA
 LAB_ERR:	
	IF ERR <> 11 THEN
		PRINT "ERROR(";err;") ";ert$(err);" en linea ";erl;", MODULO ";ern$
	END IF
	CLOSE #C_FILE_INP
	CLOSE #C_FILE_OUT
	RESUME 400
400
	END	
