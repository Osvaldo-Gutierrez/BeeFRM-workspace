IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	DESSCRHDR.
AUTHOR.		GUZMAN Y CIA.

* Obtiene variables para Header standard de Formulario.

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============
WORKING-STORAGE SECTION.
*-----------------------
COPY	'DESREG.MEN'	FROM	DICTIONARY
	REPLACING
	==MEN==		BY	==MEN EXTERNAL==.
COPY	FIOWSS	IN	DESTXT.
COPY	FRMWSS	IN	DESTXT.
COPY	SCRWSS	IN	DESTXT.
01 HDR-FRM.
	03 HDR-RWSP				PIC X(12).
01 HDR-FLD.
	03 HDR-DFLD.
		05 HDR-COD-SIST			PIC X(03).
		05 HDR-GLS-FPRO			PIC X(61).
		05 HDR-COD-FUNC			PIC X(08).
		05 HDR-FEC-DMSA			PIC X(08).
01 WSS-VARI.
	03 WSS-ZERO	COMP	VALUE 0		PIC 9(04).
	03 WSS-AUXI	COMP			PIC 9(04).
	03 WSS-SIMP		VALUE SPACES.
		05 WSS-GLS-SIMP	OCCURS 31	PIC X(01).
	03 WSS-DOBL		VALUE SPACES.
		05 WSS-GLS-DOBL	OCCURS 61	PIC X(01).
	03 WSS-SIND.
		05 I		VALUE 0		PIC 9(02).
		05 IMIN		VALUE 0		PIC 9(02).
		05 IMAX		VALUE 0		PIC 9(02).
		05 J		VALUE 0		PIC 9(02).

PROCEDURE DIVISION.
*==================

MAIN SECTION.
*------------
INI-MAIN.
	PERFORM INI.
	PERFORM BUS-HDR.
	PERFORM PUT-HDR.
	PERFORM FIN.
FIN-MAIN.
	EXIT PROGRAM.

INI SECTION.
*-----------
INI-INI.
	CALL 'DESPROFRM' USING FDV-ATERM.
	PERFORM HDR-AWKSP.
	MOVE FRM-FTCA-NOT  TO FRM-FTCA.
	MOVE FRM-FWSP-NOT  TO FRM-FWSP.
	MOVE FRM-SINI-DBLF TO FRM-SINI.
	MOVE 92	      TO FRM-CLIB.
	MOVE 'DESFMS' TO FRM-NLIB.
	MOVE 'HDR'    TO FRM-NAME.
	CALL 'DESPROFRM' USING FRM-INI.
FIN-INI.
	EXIT.

BUS-HDR SECTION.
*----------------
INI-BUS-HDR.
	MOVE FIO-IFNF-PGM TO FIO-IFNF.
	CALL 'DESFIOMEN' USING FIO-FNF.
	CALL 'DESFIOMEN' USING FIO-INP.
	IF NOT FIO-STAT-OKS
		MOVE 'NO EXISTE Archivo MEN : ABORTO' TO FRM-MENS
		GO TO ERR-BUS-HDR.
	CALL 'DESFIOMEN' USING FIO-GET-NXT.
	IF FIO-STAT-OKS
		CALL 'DESFIOMEN' USING FIO-CLO
		GO TO FIN-BUS-HDR.
	MOVE 'Archivo MEN VACIO : ABORTO' TO FRM-MENS.
ERR-BUS-HDR.
	CALL 'DESFIOMEN' USING FIO-CLO.
	CALL 'DESPROFRM' USING FRM-MSG.
	CALL 'DESPROFRM' USING FRM-FIN.
	DIVIDE WSS-ZERO INTO WSS-AUXI.
FIN-BUS-HDR.
	EXIT.

PUT-HDR SECTION.
*---------------
INI-PUT-HDR.
	MOVE MEN-COD-SIST TO HDR-COD-SIST.
	MOVE MEN-GLS-FPRO TO WSS-SIMP.
	PERFORM HDR-TITU.
	MOVE WSS-DOBL	  TO HDR-GLS-FPRO.
	MOVE MEN-COD-FUNC TO HDR-COD-FUNC.
	IF SCR-FAMD = LOW-VALUES
		ACCEPT SCR-FAMD FROM DATE.
	IF SCR-FDMA = LOW-VALUES
		MOVE SCR-DAMD TO SCR-DDMA
		MOVE SCR-MAMD TO SCR-MDMA
		MOVE 19       TO SCR-SDMA
		MOVE SCR-AAMD TO SCR-ADMA.
	MOVE SCR-FDMA TO HDR-FEC-DMSA.
	MOVE HDR-DFLD TO FRM-DFLD.
	CALL 'DESPROFRM' USING FDV-PUTAL.
	CALL 'DESPROFRM' USING FRM-CLR-DSP.
FIN-PUT-HDR.
	EXIT.

FIN SECTION.
*-----------
INI-FIN.
	MOVE SCR-IHDR-YES  TO SCR-IHDR.
	MOVE FRM-FWSP-YES  TO FRM-FWSP.
	MOVE FRM-SINI-DBNF TO FRM-SINI.
	MOVE 93	    TO	FRM-CLIB.
	MOVE SPACES TO	FRM-NLIB
			FRM-NAME
			FRM-MENS
			FRM-DFLD.
FIN-FIN.
	EXIT.

HDR-TITU SECTION.
*----------------
INI-HDR-TITU.
	IF WSS-SIMP = SPACES
		GO TO FIN-HDR-TITU.
	PERFORM BUS-LIMI VARYING IMIN FROM 1 BY 1
		UNTIL WSS-GLS-SIMP(IMIN) NOT = SPACES.
	PERFORM BUS-LIMI VARYING IMAX FROM 31 BY -1
		UNTIL WSS-GLS-SIMP(IMAX) NOT = SPACES.
	COMPUTE J = ( 61 - ( 2 * ( IMAX - IMIN + 1 ) - 1 ) ) / 2 + 1.
	PERFORM PUT-TITU VARYING I FROM IMIN BY 1 UNTIL I > IMAX.
FIN-HDR-TITU.
	EXIT.

BUS-LIMI SECTION.
*----------------
INI-BUS-LIMI.
FIN-BUS-LIMI.
	EXIT.

PUT-TITU SECTION.
*----------------
INI-PUT-TITU.
	MOVE WSS-GLS-SIMP(I) TO WSS-GLS-DOBL(J).
	ADD 2 TO J.
FIN-PUT-TITU.
	EXIT.

HDR-AWKSP SECTION.
*-----------------
INI-HDR-AWKSP.
	CALL 'FDV$AWKSP' USING
		BY DESCRIPTOR HDR-RWSP
		BY REFERENCE  FRM-TWSP.
FIN-HDR-AWKSP.
	EXIT.
