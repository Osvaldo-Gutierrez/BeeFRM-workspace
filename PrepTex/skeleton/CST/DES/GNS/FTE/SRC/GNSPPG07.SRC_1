*%! SETEAR AMBIENTE PARA GENERAR VSI O DTC
       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. GNSPPG07.
       AUTHOR. BEE-TEAM.
       DATE-WRITTEN.  3-FEB-1993 18:36:00.

      * PROCESADOR DE AYUDA CON BUSQUEDA GENERICA

       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
                     DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWCSCR.
       COPY GNSWVSCR.
       COPY GNSBRTAB.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFRM.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       COPY GNSWGQUE.
       COPY TABBRUSR.
       COPY TABBROFI.
       COPY GNSWIDOH.
       COPY GNSWGCBG.
*% IF PGM_DTC
       COPY GNSBRIDD.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       01  WSS-IDD.
           03  WSS-QIDD.
               05  WSS-NIDD VALUE 'GIDD'    PIC X(04).
               05  WSS-TIDD                 PIC X(04).
*% END
       01  WSS-SCR-VARI-V20.
           03  WSS-SCR-TCMA-V20     COMP         PIC S9(04).
           03  WSS-SCR-SIST-V20                  PIC  X(03).
           03  WSS-SCR-FUNC-V20                  PIC  X(08).
           03  WSS-SCR-NEMO-V20                  PIC  X(12).
           03  WSS-SCR-RFBY-V20                  PIC  X(01).
           03  WSS-SCR-NTRV-V20                  PIC  X(04).
           03  FILLER                            PIC  X(01).
           03  WSS-SCR-NTRN-V20                  PIC  X(04).

       01  WSS-SCR-VARI-V31.
           03  WSS-SCR-TCMA-V31     COMP         PIC S9(04).
           03  WSS-SCR-SIST-V31                  PIC  X(03).
           03  WSS-SCR-SISG-V31                  PIC  X(03).
           03  WSS-SCR-NTRN-V31                  PIC  X(04).

       01  WSS-VARI.
      *       Tamano del mapa
           03 WSS-TFLD  COMP      VALUE +0             PIC S9(04).
      *       Posicion del cursor
           03 WSS-PCUR  COMP      VALUE +0             PIC S9(04).
      *       Mapa
           03 WSS-DFRM                                 PIC X(3000).
      *       Common Area auxiliar
           03 WSS-CMMA                                 PIC X(3500).
           03 WSS-FEC-FHOY.
              05 WSS-NUM-DHOY                PIC 9(02).
              05 FILLER                      PIC X(01) VALUE '/'.
              05 WSS-NUM-MHOY                PIC 9(02).
              05 FILLER                      PIC X(01) VALUE '/'.
              05 WSS-NUM-SHOY                PIC 9(02).
              05 WSS-NUM-AHOY                PIC 9(02).

       01  WSS-USR.
           03 WSS-USR-COD-TTAB              PIC X(03).
           03 FILLER     VALUE SPACES       PIC X(01).
           03 WSS-USR-GLS-DESC              PIC X(40).

       01  LLAVE               VALUE SPACES.
           02 TABL             PIC X(3).
           02 LLAV             PIC X(12).
           COPY DFHBMSCA.

       01  VARIABLES.
           02 WSS-TOT-TABL     VALUE 19 PIC 9(02).
           02 BUSCA-PRIMER-REGISTRO  VALUE 'NO' PIC X(02).
           02 WSS-ARG.
              04 WSS-ARG-ELM      OCCURS 40 PIC X.
           02 I                PIC 99.
           02 J                PIC 99.
           02 WSS-K                PIC 99.
           02 L                PIC 99.
           02 M                PIC 99.
      * ISP: LIMITE DE BUSQUEDA DE STRING RN B. GENERICA
      *      SE PUEDE ACORTAR PARAR MEJORAR PERFORMANCE
           02 WSS-LIM-BUSQ     PIC 9(02) VALUE 40.
           02 WSS-HLP-TRAN     PIC X(04) VALUE 'GP07'.
           02 REC              PIC XX.
           02 ENC              PIC XX.
           02 WSS-DIR-LEC      PIC X(03).
           02 STOPP            PIC XX.
           02 TSQ.
              03 TSQ-TERM      PIC X(4).
              03 FILLER        PIC X(4) VALUE 'HELP'.

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER           PIC X(3500).
*% IF NOT PGM_DTC
       COPY GNSLGFIO.
*% END
       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
       INI-MAIN.
*% IF PGM_DTC
           ENTRY 'DBMSCBL'.
*% ELSE
           PERFORM GNS-HDL-VSM.
*% END
           EXEC CICS HANDLE AID
                 CLEAR(RET-FPEN)
                 PA1(RET-FPEN)
                 PA2(RET-FPEN)
           END-EXEC.

           MOVE DFHCOMMAREA TO CMA-ARG.

*% IF PGM_DTC
           MOVE EIBTRMID TO WSS-TIDD.
           MOVE WSS-QIDD TO SCR-QIDD.
           MOVE +490 TO SCR-LIDD.
           MOVE 'GNSPPG07' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
*% END
           IF CMA-ARG-IVEZ = 'S'
               MOVE 'N' TO CMA-ARG-IVEZ
               MOVE 'N' TO CMA-ARG-IVEZ
               MOVE 'N' TO CMA-ARG-IVEZ
               MOVE 'N' TO CMA-ARG-IVEZ
               MOVE 'N' TO CMA-ARG-IVEZ
               MOVE 'N' TO CMA-ARG-IVEZ
               PERFORM FST-VEZ
           ELSE
               PERFORM RCV-CIC.
       FIN-MAIN.
           EXIT.

       RCV-CIC SECTION.
       INI-RCV-CIC.
           PERFORM RCV-MAPA.
      *ISP: PF12
           IF EIBAID = '@' OR '<'
                PERFORM GNS-RET-MEN
           ELSE
           IF EIBAID = '3' OR 'C'
                PERFORM RET-FPEN
           ELSE
           IF EIBAID = '7' OR 'G'
                MOVE 'PF7' TO WSS-DIR-LEC
                PERFORM SCRL-TAB-TAB-PF7
           ELSE
           IF EIBAID = '8' OR 'H'
                MOVE 'PF8' TO WSS-DIR-LEC
                PERFORM SCRL-TAB-TAB-PF8.

           PERFORM SEND-SCRN.
           PERFORM RTRN-CICL.
       FIN-RCV-CIC.
           EXIT.

       GNS-RET-MEN SECTION.
       INI-GNS-RET-MEN.
           MOVE SPACES TO CMA-ARG-ARGB.
           MOVE 'S'    TO CMA-ARG-IVEZ.
           MOVE 'S'    TO CMA-ARG-GENE.
           MOVE SPACES TO CMA-ARG-ASCE.
           MOVE SPACES TO CMA-ARG-DESC.
           MOVE ZEROES TO CMA-ARG-SIZE.
           ADD 1 TO CMA-ARG-L.
           EXEC CICS XCTL
                 PROGRAM ('GNSPPPK1')
                 COMMAREA(CMA-ARG)
                 LENGTH  (CMA-ARG-L)
           END-EXEC.
       FIN-GNS-RET-MEN.
           EXIT.

       RET-FPEN SECTION.
       INI-RET-FPEN.
           PERFORM GNS-ERR-QUE.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE 1        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-RET-FPEN.
      *Ya leyo primer registro de la cola para detectar si esta existia
           IF QUE-IKEY NOT = 'MAP'
               GO TO FIN-RET-FPEN.
           MOVE QUE-DATA TO WSS-DFRM.
           MOVE QUE-LDAT TO WSS-TFLD.
           MOVE QUE-PCUR TO WSS-PCUR.
           MOVE 2        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-RET-FPEN.

           IF NOT ( QUE-IKEY = 'CM2' OR 'CM3' )
               GO TO FIN-RET-FPEN.
           MOVE QUE-LDAT TO SCR-TCMA.
           MOVE QUE-DATA TO WSS-CMMA.

           IF QUE-IKEY = 'CM2'
               MOVE QUE-DATA TO WSS-SCR-VARI-V20
               MOVE WSS-SCR-NTRN-V20 TO SCR-NTRN.

           IF QUE-IKEY = 'CM3'
               MOVE QUE-DATA TO WSS-SCR-VARI-V31
               MOVE WSS-SCR-NTRN-V31 TO SCR-NTRN.

           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE QUE-DEL  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           EXEC CICS SEND FROM(WSS-DFRM)
                          LENGTH(WSS-TFLD)
                          ERASE
           END-EXEC.
           EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
                          TERMINAL
           END-EXEC.
           PERFORM SCR-EXIT.
       FIN-RET-FPEN.
           EXIT.

       SCR-EXIT SECTION.
       INI-SCR-EXIT.
           EXEC CICS RETURN
                     TRANSID(SCR-NTRN)
                     COMMAREA(WSS-CMMA)
                     LENGTH(SCR-TCMA)
           END-EXEC.
       COPY GNSBGGBK.
       FIN-SCR-EXIT.
           EXIT.
       COPY GNSBGFRM.
       COPY GNSBGQUE.
       COPY GNSBGHOY.
       COPY GNSBGSYS.
       COPY TABBFUSR.
       COPY TABBFOFI.
       COPY GNSBFTAB.
       COPY GNSBIABT.
*% IF PGM_DTC
       COPY GNSBBIDD.
       COPY GNSBGDTC.
       SET-NUM-IDD SECTION.
       INI-SET-NUM-IDD.
      * ISP: SI EN HLPMPOS EXISTE UN NUMERO SE DEJA UNA IDD PARA EL
      *      SISTEMA '***' CON NUMERO DE BASE DE DATOS DE HLPMPOS
      *
           IF CMA-ARG-SIST IS NUMERIC
                MOVE '***'    TO FIO-SIST

                MOVE '***'   TO IDD-SIST(1)
                MOVE CMA-ARG-SIST TO IDD-IENT(1)
                MOVE 'C'     TO IDD-GRPO(1)
                MOVE SPACES  TO IDD-RGST(1)
                MOVE 'D'     TO IDD-TENT(1)
           ELSE
                MOVE CMA-ARG-SIST  TO FIO-SIST.
       FIN-SET-NUM-IDD.
           EXIT.

*% ELSE
       COPY GNSBGVSM.
       COPY GNSBHVSM.

       SET-NUM-IDD SECTION.
       INI-SET-NUM-IDD.
           MOVE CMA-ARG-SIST  TO FIO-SIST.
       FIN-SET-NUM-IDD.
           EXIT.

*% END

       PUT-TIT SECTION.
       INI-PUT-TIT.
      *ISP TITULA SISTEMA, TABLA
           MOVE CMA-ARG-TTAB TO TAB-COD-TTAB IN TAB
                                DOH-COD-TTAB IN DOH-FLD.
           MOVE SPACES       TO TAB-COD-CTAB IN TAB.      
           MOVE CMA-ARG-SIST TO DOH-COD-SIST IN DOH-FLD.
           MOVE 'TAB'        TO FIO-SIST.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
                MOVE TAB-GLS-DESC IN TAB
                                           TO DOH-GLS-TTAB
           ELSE
                MOVE 'Registro padre no existe'
                                           TO DOH-GLS-TTAB
               MOVE CMA-ARG-TTAB TO SOURX
               PERFORM INIT-SCRN
               PERFORM RTRN-CICL.
       FIN-PUT-TIT.
           EXIT.

       CST-FIO-TAB SECTION.
       INI-CST-FIO-TAB.

       CIC-CST-FIO-TAB.
           PERFORM SET-NUM-IDD.
           MOVE TAB-COD-CTAB IN TAB TO OFI-COD-OFIC IN OFI.
           MOVE TAB-COD-CTAB IN TAB TO USR-COD-USER IN USR.
           IF CMA-ARG-TTAB = 'OFI'
               PERFORM TAB-FIO-OFI
           ELSE
               MOVE 'USR-COD-USER' TO FIO-AKEY
               PERFORM TAB-FIO-USR.

           IF NOT FIO-STAT-OKS
               GO TO FIN-CST-FIO-TAB.

           IF CMA-ARG-TTAB = 'OFI'
               MOVE OFI-COD-OFIC IN OFI TO TAB-COD-CTAB IN TAB
               MOVE OFI-GLS-DESC IN OFI TO TAB-GLS-DESC IN TAB
               MOVE OFI-IND-VIGE IN OFI TO TAB-IND-VIGE IN TAB.

           IF CMA-ARG-TTAB = 'USR'
               MOVE USR-COD-USER IN USR TO TAB-COD-CTAB IN TAB

               MOVE USR-COD-TTAB IN USR TO WSS-USR-COD-TTAB
               MOVE USR-GLS-DESC IN USR TO WSS-USR-GLS-DESC
               MOVE WSS-USR             TO USR-GLS-DESC IN USR
               MOVE USR-GLS-DESC IN USR TO TAB-GLS-DESC IN TAB

               MOVE USR-IND-VIGE IN USR TO TAB-IND-VIGE IN TAB.

      *     IF FIO-STAT-OKS
      *         IF CMA-ARG-TTAB NOT = TAB-COD-TTAB IN TAB
      *             MOVE '23' TO FIO-STAT
      *             GO TO FIN-CST-FIO-TAB.


      *     IF BUSCA-PRIMER-REGISTRO = 'SI'
      *           MOVE TAB-COD-TABL TO CMA-ARG-ASCE
      *      ELSE
      *           MOVE 'Tabla Sin Registros ( PF3 Para Retornar )'
      *                                            TO DOH-GLS-MENS
      *           MOVE CMA-ARG-TTAB TO SOURX
      *           GO TO FIN-CST-FIO-TAB.

      *ISP: NO CONSIDERA REGISTROS NO VIGENTES
      *ISP:
      *     IF TAB-COD-CTAB IN TAB > SPACES AND
      *        TAB-IND-VIGE IN TAB NOT = 'S'
      *          PERFORM SET-DIR-LEC
      *          GO TO CIC-CST-FIO-TAB.

           IF CMA-ARG-ARGB NOT = SPACES
              IF TAB-COD-CTAB IN TAB > SPACES
                  MOVE TAB-GLS-DESC IN TAB TO WSS-ARG
                  PERFORM FIND-ARG
                  IF REC = 'NO'
                      PERFORM SET-DIR-LEC
                      GO TO CIC-CST-FIO-TAB.
       FIN-CST-FIO-TAB.
           EXIT.

       SET-DIR-LEC SECTION.
       INI-SET-DIR-LEC.
           IF WSS-DIR-LEC = 'PF7'
               MOVE FIO-GET-PRV TO FIO-CMND
           ELSE
               MOVE FIO-GET-NXT TO FIO-CMND.
       FIN-SET-DIR-LEC.
           EXIT.

       DET-CMA-ARG-SIZE SECTION.
       INI-DET-CMA-ARG-SIZE.
       FIN-DET-CMA-ARG-SIZE.
           EXIT.

       SCRL-TAB-TAB-PF8 SECTION.
       INI-SCRL-TAB-TAB-PF8.
           IF CMA-ARG-ASCE = HIGH-VALUES
               MOVE 'Fin de paginas' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF8.
           MOVE CMA-ARG-ASCE TO TAB-COD-TABL IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-COD-TABL IN TAB = CMA-ARG-ASCE )
               MOVE 'Registro eliminado' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF8.
      * ISP: NO SE SALTA PRIMER REGISTRO COMO EN TABLAS GENERALES
           IF BUSCA-PRIMER-REGISTRO = 'SI'
               MOVE 'N' TO BUSCA-PRIMER-REGISTRO
               PERFORM LLENAR-TABLA-ASCE
               GO TO FIN-SCRL-TAB-TAB-PF8
           ELSE
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM CST-FIO-TAB
               IF FIO-STAT-OKS
                   PERFORM LLENAR-TABLA-ASCE
                   GO TO FIN-SCRL-TAB-TAB-PF8.
           MOVE 'Fin de paginas' TO DOH-GLS-MENS.
       FIN-SCRL-TAB-TAB-PF8.
           EXIT.

       LLENAR-TABLA-ASCE SECTION.
       INI-LLENAR-TABLA-ASCE.
           MOVE 1 TO FRM-IFLD.
       LUP-LLENAR-TABLA-ASCE.
           PERFORM CARGAR-TABLA.
           IF FRM-IFLD = 1
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-DESC
               MOVE HIGH-VALUES         TO CMA-ARG-ASCE
           ELSE
           IF FRM-IFLD = WSS-TOT-TABL
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-ASCE.
       NXT-LLENAR-TABLA-ASCE.
           ADD 1 TO FRM-IFLD.
           IF FRM-IFLD > WSS-TOT-TABL
               GO TO FIN-LLENAR-TABLA-ASCE.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF FIO-STAT-OKS
               GO TO LUP-LLENAR-TABLA-ASCE.

           PERFORM LIMPIAR-TABLA VARYING FRM-IFLD FROM FRM-IFLD BY 1
                                 UNTIL   FRM-IFLD > WSS-TOT-TABL.

       FIN-LLENAR-TABLA-ASCE.
           EXIT.

       SCRL-TAB-TAB-PF7 SECTION.
       INI-SCRL-TAB-TAB-PF7.
           IF CMA-ARG-DESC NOT > SPACES
               MOVE 'No existen paginas anteriores' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF7.
           MOVE CMA-ARG-DESC TO TAB-COD-TABL IN TAB.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-COD-TABL IN TAB = CMA-ARG-DESC )
               MOVE 'Registro eliminado' TO DOH-GLS-MENS
               GO TO FIN-SCRL-TAB-TAB-PF7.
           MOVE FIO-GET-PRV TO FIO-CMND.
           PERFORM CST-FIO-TAB.

           IF FIO-STAT-OKS
               PERFORM LLENAR-TABLA-DESC
               GO TO FIN-SCRL-TAB-TAB-PF7.
           MOVE 'FIN DE PAGINAS' TO DOH-GLS-MENS.
       FIN-SCRL-TAB-TAB-PF7.
           EXIT.

       LLENAR-TABLA-DESC SECTION.
       INI-LLENAR-TABLA-DESC.
           MOVE WSS-TOT-TABL TO FRM-IFLD.
       LUP-LLENAR-TABLA-DESC.
           PERFORM CARGAR-TABLA.
           IF FRM-IFLD = WSS-TOT-TABL
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-ASCE
               MOVE SPACES              TO CMA-ARG-DESC
           ELSE
           IF FRM-IFLD = 1
               MOVE TAB-COD-TABL IN TAB TO CMA-ARG-DESC
               MOVE FIO-GET-PRV TO FIO-CMND
               PERFORM CST-FIO-TAB
               IF NOT ( FIO-STAT-OKS AND
                        TAB-COD-CTAB IN TAB > SPACES )
                    MOVE SPACES              TO CMA-ARG-DESC.

       PRV-LLENAR-TABLA-DESC.
           SUBTRACT 1 FROM FRM-IFLD.
           IF FRM-IFLD < 1
               GO TO FIN-LLENAR-TABLA-DESC.
           MOVE FIO-GET-PRV TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           IF FIO-STAT-OKS
               GO TO LUP-LLENAR-TABLA-DESC.
       FIN-LLENAR-TABLA-DESC.
           EXIT.

       LIMPIAR-TABLA SECTION.
       INI-LIMPIAR-TABLA.
           MOVE SPACES TO DOH-COD-OPCI(FRM-IFLD)
                          DOH-GLS-DESC(FRM-IFLD)
                          DOH-IND-VIGE(FRM-IFLD).
       FIN-LIMPIAR-TABLA.
           EXIT.

       CARGAR-TABLA SECTION.
       INI-CARGAR-TABLA.
           MOVE TAB-COD-CTAB IN TAB TO DOH-COD-OPCI (FRM-IFLD).
           MOVE TAB-GLS-DESC IN TAB TO DOH-GLS-DESC (FRM-IFLD).
           MOVE TAB-IND-VIGE IN TAB TO DOH-IND-VIGE (FRM-IFLD).
       FIN-CARGAR-TABLA.
           EXIT.

       FST-VEZ SECTION.
       INI-FST-VEZ.

           IF CMA-ARG-ARGB > SPACES
                PERFORM DET-CMA-ARG-SIZE VARYING
                        CMA-ARG-SIZE FROM 10 BY -1
                 UNTIL CMA-ARG-SIZE = 0 OR
                 CMA-ARG-ARGB-ELM(CMA-ARG-SIZE) > SPACE.

           MOVE SPACES      TO CMA-ARG-ASCE.
           MOVE SPACES      TO CMA-ARG-DESC.
           MOVE SPACES         TO LLAVE.
           MOVE CMA-ARG-TTAB        TO TABL.

           MOVE LOW-VALUES     TO DOH-FLD.
           MOVE 'SI' TO BUSCA-PRIMER-REGISTRO
           IF BUSCA-PRIMER-REGISTRO = 'SI'
                PERFORM PUT-TIT.
           MOVE 'PF8' TO WSS-DIR-LEC.
           MOVE TABL           TO TAB-COD-TTAB IN TAB.
           MOVE SPACES         TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM CST-FIO-TAB.
           MOVE TAB-COD-TABL TO CMA-ARG-ASCE.

           PERFORM GET-FHOY.
           MOVE HOY-DHOY     TO WSS-NUM-DHOY.
           MOVE HOY-MHOY     TO WSS-NUM-MHOY.
           MOVE HOY-SHOY     TO WSS-NUM-SHOY.
           MOVE HOY-AHOY     TO WSS-NUM-AHOY.
           MOVE WSS-FEC-FHOY TO FRM-GLS-FPRO.
           PERFORM SCRL-TAB-TAB-PF8.

           PERFORM INIT-SCRN.
           PERFORM RTRN-CICL.
       FIN-FST-VEZ.
           EXIT.

       INIT-SCRN SECTION.
       INI-INIT-SCRN.
           EXEC CICS SEND
                MAP('GNSFG04')
                MAPSET('GNSAG04')
                FROM(DOH-FLD)
                ERASE
           END-EXEC.
       FIN-INIT-SCRN.
           EXIT.

       RTRN-CICL SECTION.
       INI-RTRN-CICL.
           EXEC CICS RETURN    TRANSID(WSS-HLP-TRAN)
                               COMMAREA(CMA-ARG)
                               LENGTH(CMA-ARG-L)
           END-EXEC.
       FIN-RTRN-CICL.
           EXIT.

       SEND-SCRN SECTION.
       INI-SEND-SCRN.
           EXEC CICS SEND      MAP('GNSFG04')
                               MAPSET('GNSAG04')
                               FROM(DOH-FLD)
                               DATAONLY
           END-EXEC.
       FIN-SEND-SCRN.
           EXIT.

       RCV-MAPA SECTION.
       INI-RCV-MAPA.
           EXEC CICS RECEIVE   MAP('GNSFG04') 
			       MAPSET('GNSAG04')
                               INTO(DOH-FLD)
           END-EXEC.
           MOVE SPACES TO DOH-GLS-MENS.
       FIN-RCV-MAPA.
           EXIT.

       FIND-ARG SECTION.
       INI-FIND-ARG.
           MOVE 1    TO WSS-K.
           MOVE 'NO' TO REC.
           PERFORM FIND-ELEM.
       FIN-FIND-ARG.
           EXIT.

       FIND-ELEM SECTION.
       INI-FIND-ELEM.
           MOVE 1    TO L.
           MOVE 'NO' TO ENC.
           PERFORM ENCLAVAR VARYING M FROM WSS-K BY 1 UNTIL
                               ENC = 'SI' OR M > WSS-LIM-BUSQ
           IF ENC = 'SI'
               MOVE 'NO' TO STOPP
               COMPUTE WSS-K = M - 1
               PERFORM COMPAR VARYING M FROM WSS-K BY 1
                              UNTIL M > WSS-LIM-BUSQ     OR
                              L > CMA-ARG-SIZE     OR
                              STOPP = 'SI'
               IF L > CMA-ARG-SIZE
                  MOVE 'SI'  TO REC
               ELSE
                  IF (WSS-LIM-BUSQ - WSS-K) > CMA-ARG-SIZE
                       ADD 1 TO WSS-K
                       GO TO FIND-ELEM.
       FIN-FIND-ELEM.
           EXIT.

       ENCLAVAR SECTION.
       INI-ENCLAVAR.
           IF CMA-ARG-ARGB-ELM(L) = WSS-ARG-ELM (M)
                 MOVE 'SI' TO ENC.
       FIN-ENCLAVAR.
           EXIT.

       COMPAR SECTION.
       INI-COMPAR.
           IF CMA-ARG-ARGB-ELM(L) = WSS-ARG-ELM (M)
               ADD 1 TO L
           ELSE
               MOVE 'SI' TO STOPP.
       FIN-COMPAR.
           EXIT.
