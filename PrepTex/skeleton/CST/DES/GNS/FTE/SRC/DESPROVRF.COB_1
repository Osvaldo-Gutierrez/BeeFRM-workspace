IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	DESPROVRF.
AUTHOR.		GUZMAN Y CIA.

* Rutina de Manejo de Digito Verificador

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============

WORKING-STORAGE SECTION.
*-----------------------
COPY	VRFWSS	IN	DESTXT.
01 WSS-VARI.
	03 WSS-CMND				PIC X(80).
	03 WSS-AUXI	COMP			PIC 9(09).
	03 WSS-DVRF				PIC X(01).
	03 WSS-NVRF				PIC 9(01).
	03 WSS-STAT-OKS			VALUE 0	PIC 9(01).
	03 WSS-STAT-NUM			VALUE 1	PIC 9(01).
	03 WSS-STAT-VRF			VALUE 2	PIC 9(01).

LINKAGE SECTION.
*---------------
01 LNK-CMND					PIC X(80).

PROCEDURE DIVISION USING LNK-CMND.
*=================================

MAIN SECTION.
*------------
INI-MAIN.
	MOVE WSS-STAT-OKS TO VRF-STAT.
	MOVE SPACES       TO VRF-MENS.
	UNSTRING LNK-CMND DELIMITED BY '<' OR '>'
		INTO WSS-CMND VRF-CMND WSS-CMND.
	IF VRF-VAL
		PERFORM VRF-VVAL
		GO TO FIN-MAIN.
	IF VRF-GET
		PERFORM VRF-VGET
		GO TO FIN-MAIN.
	DISPLAY '? Codigo '
		LINE 23 COLUMN 1 WITH BELL REVERSED.
ERR-MAIN.
	DISPLAY	LNK-CMND REVERSED
		'  invalido en DESPROVRF : ABORTO'
		LINE 24 COLUMN 1 REVERSED.
	STOP RUN.
FIN-MAIN.
	EXIT PROGRAM.

VRF-VVAL SECTION.
*----------------
INI-VRF-VVAL.
	PERFORM VRF-GETV.
	IF VRF-STAT-OKS AND WSS-DVRF NOT = VRF-DVRF
		MOVE WSS-STAT-VRF TO VRF-STAT
		MOVE 'DIGITO VERIFICADOR NO CORRESPONDE' TO VRF-MENS.
FIN-VRF-VVAL.
	EXIT.

VRF-VGET SECTION.
*-----------------
INI-VRF-VGET.
	PERFORM VRF-GETV.
	IF VRF-STAT-OKS
		MOVE WSS-DVRF TO VRF-DVRF.
FIN-VRF-VGET.
	EXIT.

VRF-GETV SECTION.
*----------------
INI-VRF-GETV.
	INSPECT VRF-NUME REPLACING LEADING SPACES BY ZEROES.
	IF VRF-NUME NOT NUMERIC
		MOVE WSS-STAT-NUM TO VRF-STAT
		MOVE 'DEBE SER NUMERICO' TO VRF-MENS
		GO TO FIN-VRF-GETV.
	COMPUTE WSS-AUXI =
		  VRF-DIGI(1)  * 7 + VRF-DIGI(2)  * 6 + VRF-DIGI(3)  * 5 
		+ VRF-DIGI(4)  * 4 + VRF-DIGI(5)  * 3 + VRF-DIGI(6)  * 2
		+ VRF-DIGI(7)  * 7 + VRF-DIGI(8)  * 6 + VRF-DIGI(9)  * 5
		+ VRF-DIGI(10) * 4 + VRF-DIGI(11) * 3 + VRF-DIGI(12) * 2.
	DIVIDE 11 INTO WSS-AUXI GIVING WSS-AUXI REMAINDER WSS-AUXI.
	IF WSS-AUXI > 1
		SUBTRACT WSS-AUXI FROM 11 GIVING WSS-NVRF
		MOVE WSS-NVRF TO WSS-DVRF
	ELSE
	IF WSS-AUXI = 1 AND VRF-SVRF NOT = VRF-SVRF-0
		MOVE 'K' TO WSS-DVRF
	ELSE
		MOVE '0' TO WSS-DVRF.
FIN-VRF-GETV.
	EXIT.
