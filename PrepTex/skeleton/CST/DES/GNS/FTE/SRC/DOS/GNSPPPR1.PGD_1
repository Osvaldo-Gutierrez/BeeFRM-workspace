       IDENTIFICATION DIVISION.
      *========================
      *JSS-UYP PROGRAM-ID. GNSPPMEN.
       PROGRAM-ID. GNSPPPR1.
       AUTHOR. GUZMAN Y CIA.

      * DESPLIEGA MAPA PARA MENU

       ENVIRONMENT DIVISION.
      *=====================
       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSBRFUN.
       COPY GNSBRMEN.
       COPY GNSBRRPF.
       COPY GNSWGSCR.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFRM.
       COPY GNSWIHDR.
       COPY GNSBRMSG.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
      *
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
      *
       01  OPC-FLD.
           03 OPC-DFLD.
               05 FILLER                           PIC X(12).
               05 OPC-GLS-FILA      OCCURS 20.
                   07 OPC-ATR-GLOS                 PIC X(03).
                   07 OPC-GLS-COLU  OCCURS 2.
                       09 OPC-KEY-NIVE             PIC X(02).
                       09 OPC-GLS-DIS1             PIC X(01).
                       09 OPC-GLS-FMEN             PIC X(31).
                       09 OPC-GLS-DIS2             PIC X(04).
               05 OPC-GLS-MENS-ATR                 PIC S9(04).
               05 OPC-GLS-MENS-LEN                 PIC X(01).
               05 OPC-GLS-MENS                     PIC X(79).
           03 OPC-DFLD-RED REDEFINES OPC-DFLD.
              05 FILLER                            PIC X(12).
              05 OPC-GLS-FILA-RED   OCCURS 20.
                   07 OPC-ATR-GLO1                 PIC X(03).
                   07 OPC-GLS-DIS3                 PIC X(23).
                   07 OPC-KEY-NIV3                 PIC X(02).
                   07 OPC-GLS-DIS4                 PIC X(01).
                   07 OPC-GLS-FME3                 PIC X(31).
                   07 OPC-GLS-DIS5                 PIC X(19).
              05 FILLER                            PIC X(82).
       01  WSS-VARI.
           03 WSS-STAT              VALUE 0        PIC 9(01).
              88 WSS-STAT-OKS       VALUE 0.
              88 WSS-STAT-WRN       VALUE 1.
              88 WSS-STAT-MAL       VALUE 2.
           03 WSS-SIND.
              05 I                                 PIC 9(02).
              05 J                                 PIC 9(02).
              05 K                  VALUE 0        PIC 9(02).
              05 L                  VALUE 0        PIC 9(02).
              05 IMIN               VALUE 0        PIC 9(02).
              05 IMAX               VALUE 0        PIC 9(02).
       01  WSS-DFLD.
           03 WSS-NTRN                             PIC X(04).
           03 WSS-SIST                             PIC X(03).
           03 WSS-FILL                             PIC X(01).
           03 WSS-SISG                             PIC X(03).
           03 WSS-DISP                             PIC X(09).
       01  WSS-CMMA.
           03 WSS-RFUN                             PIC X(300).
           03 WSS-NSIS                             PIC X(003).
           03 WSS-GSIS                             PIC X(003).
           03 WSS-NEMO                             PIC X(011).
           03 WSS-NEMO-RED REDEFINES WSS-NEMO.
              05 WSS-CMEN                          PIC X(03).
              05 WSS-CACC                          PIC X(03).
              05 WSS-NREG                          PIC X(05).
      *Variables para GNSSCRHDR
       01  WSS-VHDR.
           03 WSS-NFRM.
              05 FILLER                            PIC X(04).
              05 WSS-MAPA                          PIC X(03).
           03 WSS-HORA.
              05 WSS-HHMS                          PIC 9(02).
              05 WSS-MHMS                          PIC 9(02).
              05 WSS-SHMS                          PIC 9(02).
           03 WSS-SIMP              VALUE SPACES.
              05 WSS-GLS-SIMP       OCCURS 31      PIC X(01).
           03 WSS-DOBL              VALUE SPACES.
              05 WSS-GLS-DOBL       OCCURS 61      PIC X(01).
           03 WSS-FEC-DMSA.
              05 WSS-NUM-DMSA                      PIC 9(02).
              05 WSS-GLS-SLA1                      PIC X(01).
              05 WSS-NUM-MMSA                      PIC 9(02).
              05 WSS-GLS-SLA2                      PIC X(01).
              05 WSS-NUM-SMSA                      PIC 9(02).
              05 WSS-NUM-AMSA                      PIC 9(02).
       01  WSS-COD-FUNC.
           03 WSS-COD-NIV1                         PIC X(02).
           03 WSS-GLS-PTO1                         PIC X(01).
           03 WSS-COD-NIV2                         PIC X(02).
           03 WSS-GLS-PTO2                         PIC X(01).
           03 WSS-COD-NIV3                         PIC X(02).
           03 WSS-GLS-PTO3                         PIC X(01).
           03 WSS-COD-NIV4                         PIC X(02).
      * Variables para Manejo de Usuarios y privilegios.
       COPY USPC113.
       01  CFN-VARI.
           03 CFN-KEY-NEMO.
                      05 CFN-KEY-MENU                      PIC X(03).
                      05 CFN-KEY-CACC                      PIC X(03).
                      05 CFN-KEY-NREG                      PIC X(06).
           03 CFN-VEC-SEPA           VALUE
           '2222222222222222222211111111111111111111'.
                      05 CFN-SEPA           OCCURS 40      PIC 9(01).
           03 CFN-VEC-FINI           VALUE
           '121110090807060504030707060605050404030307070707060606060505
      -    '05050404040403030303'.
                      05 CFN-FINI           OCCURS 40      PIC 9(02).
           03 CFN-TAB-FICO.
                      05 CFN-VEC-FICO OCCURS 40.
                                 07 CFN-FICO OCCURS 2      PIC 9(02).
           03 CFN-VEC-KEYS.
              05 CFN-VEC-FUNC OCCURS 40.
                 07 CFN-KEY-FUNC                           PIC X(08).
                 07 CFN-NUM-FILA                           PIC 9(02).
                 07 CFN-NUM-COLU                           PIC 9(02).
                 07 CFN-GLS-FMEN                           PIC X(31).
           03 CFN-GLS-AUXI                                 PIC X(43).
           03 CFN-REST                                     PIC 9(01).
           03 CFN-FILA                                     PIC 9(02).
           03 CFN-COLU                                     PIC 9(02).
           03 CFN-SIND.
                      05 CFN-I                             PIC 9(02).
                      05 CFN-J                             PIC 9(02).
                      05 CFN-IMAX                          PIC 9(02).
                      05 CFN-LMAX                          PIC 9(02).
                      05 CFN-M                             PIC 9(02).
                      05 CFN-N                             PIC 9(02).
           03 CFN-STAT                COMP                 PIC 9(09).
                      88 CFN-STAT-OKS VALUE 0.
                      88 CFN-STAT-MAL VALUE 1.
           03 CFN-MENS                                     PIC X(80).


       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                               PIC X(317).
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================

       MAIN SECTION.
       INI-MAIN.
      * Rutina que detecta transaccion invocada directamente
       COPY USPC143.
           PERFORM CIC-ERR THRU FIN-CIC-ERR.
           PERFORM BUS-FSIS.
           PERFORM INI.
           PERFORM BUS-CRPF.
           PERFORM PRO-MENU.
           MOVE WSS-RFUN TO FUN.
           PERFORM PUT-MHDR.
           PERFORM PUT-MSCR.
           PERFORM CAL-CMND.
       FIN-MAIN.
           EXIT.

       INI SECTION.
       INI-INI.
           IF EIBCALEN = 0
                MOVE +20 TO FRM-LFLD
                MOVE FRM-RCV TO FRM-CMND
                PERFORM GNS-PRO-FRM
                MOVE FRM-DFLD TO WSS-DFLD
                MOVE WSS-SIST TO SCR-SIST
                MOVE WSS-SISG TO SCR-SISG
                PERFORM INI-SIST
                MOVE FUN TO WSS-RFUN
           ELSE
                MOVE DFHCOMMAREA TO WSS-CMMA
                MOVE WSS-RFUN TO FUN
                MOVE WSS-GSIS TO SCR-SISG
                IF WSS-NSIS EQUAL SPACES OR
                   WSS-NSIS EQUAL LOW-VALUES OR
                   WSS-NSIS EQUAL FUN-COD-SIST
                       NEXT SENTENCE
                ELSE
                       MOVE WSS-NSIS TO SCR-SIST
                       PERFORM INI-SIST.
           MOVE FUN-COD-SIST TO SCR-SIST.
           MOVE FUN-COD-FUNC TO SCR-FUNC.
           MOVE FUN-COD-NEMO TO SCR-NEMO.
           MOVE FUN-GLS-TRAN TO SCR-NTRN.
           MOVE FUN-GLS-PROG TO SCR-PROG.
           MOVE FUN-GLS-LFRM TO SCR-NLIB.
           MOVE FUN-GLS-NFRM TO SCR-NMAP.
           MOVE FUN-GLS-LDOC TO SCR-LDOC.
           MOVE FUN-GLS-NDOC TO SCR-NDOC.
           MOVE FUN-COD-FRET TO SCR-FRET.
           MOVE FUN-IND-IPRV TO SCR-IPRV.
           MOVE FUN-COD-TFUN TO SCR-TFUN.
           MOVE +1110        TO SCR-TCMA.
       FIN-INI.
           EXIT.

       INI-SIST SECTION.
       INI-INI-SIST.
           MOVE '00000000'  TO FUN-KEY-FUNC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM VID-FUN.
           IF NOT FIO-STAT-OKS
               MOVE 'GNS' TO MSG-COD-SIST
               MOVE 'MENSISTNEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO FRM-MENS
               MOVE FRM-MSG      TO FRM-CMND
               PERFORM GNS-PRO-FRM
               GOBACK
           ELSE
               MOVE SCR-SIST TO FUN-COD-SIST.
       FIN-INI-SIST.
           EXIT.

       PUT-MHDR SECTION.
       INI-PUT-MHDR.
           PERFORM GNSSCRHDR.
      *JSS MOVE 'GNSFHDR'     TO FRM-NAME.
           MOVE 'GNSAHDR'     TO FRM-NAME.

           MOVE 'GNSAHDR'     TO FRM-NLIB.
           MOVE HDR-FLD       TO FRM-DFLD.
           MOVE FRM-SINI-DB1F TO FRM-SINI.
           MOVE FRM-INI       TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
       FIN-PUT-MHDR.
           EXIT.

       PUT-MSCR SECTION.
       INI-PUT-MSCR.
           MOVE SCR-NMAP      TO FRM-NAME.
           MOVE SCR-NLIB      TO FRM-NLIB.
           MOVE OPC-DFLD      TO FRM-DFLD.
           MOVE FRM-SINI-DBNF TO FRM-SINI.
           MOVE FRM-INI       TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
       FIN-PUT-MSCR.
           EXIT.

       CAL-CMND SECTION.
       INI-CAL-CMND.
      *JSS MOVE SCR-CMNT TO FRM-NTRN.
      *    MOVE SCR-VARI TO FRM-CMMA.
      *    MOVE SCR-TCMA TO FRM-TCMA.
      *    MOVE FRM-EXT  TO FRM-CMND.
      *    PERFORM GNS-PRO-FRM.
           MOVE 'M'          TO SCR-ICMN.
           MOVE SCR-INST-HCM TO SCR-INST.
           MOVE SCR-CMNP     TO FRM-PROG.
           MOVE SCR-VARI     TO FRM-CMMA.
           MOVE SCR-TCMA     TO FRM-TCMA.
           MOVE FRM-XCT      TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
           MOVE FRM-CLR-FRM TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
           MOVE 'XCTERR    ' TO MSG-COD-MENS.
           MOVE 'GNS'        TO MSG-COD-SIST.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC TO FRM-MENS.
           MOVE FRM-MSG      TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
           GOBACK.
       FIN-CAL-CMND.
           EXIT.

       BUS-CRPF SECTION.
       INI-BUS-CRPF.
           MOVE SCR-PROG TO WSS-SIST.
           IF WSS-SIST NOT = 'GNS'
               MOVE SCR-SIST TO WSS-SIST.

           MOVE SPACES      TO SCR-CRPF.
           MOVE SCR-PROG    TO RPF-KEY-DPRO.
           MOVE 'C '        TO RPF-KEY-TRYC.
           MOVE SPACES      TO RPF-KEY-DARC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM VID-RPF.
           IF NOT FIO-STAT-OKS
      *JSS-UYP  MOVE 'GNSPPCOM' TO SCR-CMNP
                MOVE 'GNSPPPR2' TO SCR-CMNP
                PERFORM BUS-PRMC
                GO TO FIN-BUS-CRPF.
           IF RPF-GLS-PRMT NOT > SPACES
               PERFORM BUS-PRMC
           ELSE
           IF RPF-GLS-PRMT NOT = 'NOMSG'
               MOVE RPF-GLS-PRMT TO SCR-PRMC
           ELSE
               MOVE SPACES TO SCR-PRMC.
           IF RPF-GLS-UARC NOT > SPACES
      *JSS-UYP  MOVE 'GNSPPCOM' TO SCR-CMNP
                MOVE 'GNSPPPR2' TO SCR-CMNP
           ELSE
                MOVE RPF-GLS-UARC TO SCR-CMNP.
           MOVE SCR-PROG     TO RPF-KEY-DPRO.
           MOVE 'C0'         TO RPF-KEY-TRYC.
           MOVE FIO-FND-NLS  TO FIO-CMND.
           PERFORM VID-RPF.
           IF NOT FIO-STAT-OKS
                 GO TO FIN-BUS-CRPF.
           PERFORM LEE-CRPF.
           MOVE FIO-END-BRW TO FIO-CMND.
           PERFORM VID-RPF.
      *JSS MOVE SCR-PROG    TO RPF-KEY-DPRO.
      *    MOVE 'P0'        TO RPF-KEY-TRYC.
      *    MOVE FIO-GET-KEY TO FIO-CMND.
      *    PERFORM VID-RPF.
      *    IF FIO-STAT-OKS
      *        MOVE RPF-GLS-NFRM TO WSS-NFRM.
           PERFORM BUS-GLOS VARYING I FROM 1 BY 1 UNTIL I > 10.
       FIN-BUS-CRPF.
           EXIT.

       BUS-PRMC SECTION.
       INI-BUS-PRMC.
           IF SCR-TFUN = 'MEN'
               MOVE 'PRMMEN' TO MSG-COD-MENS
           ELSE
               MOVE 'PRMPGI' TO MSG-COD-MENS.
           MOVE 'GNS'    TO MSG-COD-SIST.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC TO SCR-PRMC.
       FIN-BUS-PRMC.
           EXIT.

       BUS-GLOS SECTION.
       INI-BUS-GLOS.
           IF SCR-COD-CCMN( I ) NOT > SPACES
               GO TO FIN-BUS-GLOS.
           IF SCR-GLS-PRMT( I ) NOT > SPACES
               MOVE SPACES TO MSG-COD-MENS
               IF SCR-GLS-UARC( I ) NOT > SPACES
                   STRING 'FLD' SCR-COD-CCPP( I ) DELIMITED BY SIZE
                          INTO MSG-COD-TMSG
                   MOVE 'GNS' TO MSG-COD-SIST
                   PERFORM GET-MSG
                   MOVE MSG-GLS-DESC TO SCR-GLS-PRMT( I )
               ELSE
                   STRING 'SUB' SCR-COD-CCPP( I ) DELIMITED BY SIZE
                          INTO MSG-COD-TMSG
                   MOVE 'GNS' TO MSG-COD-SIST
                   PERFORM GET-MSG
                   MOVE MSG-GLS-DESC TO SCR-GLS-PRMT( I )
           ELSE
           IF SCR-GLS-PRMT( I ) = 'NOMSG'
               MOVE SPACES TO SCR-GLS-PRMT( I ).
           IF SCR-GLS-CONF( I ) NOT > SPACES
               MOVE SPACES TO MSG-COD-MENS
               STRING 'CNF' SCR-COD-CCPP( I ) DELIMITED BY SIZE
                      INTO MSG-COD-TMSG
               MOVE 'GNS' TO MSG-COD-SIST
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO SCR-GLS-CONF( I )
           ELSE
           IF SCR-GLS-CONF( I ) = 'NOMSG'
               MOVE SPACES TO SCR-GLS-CONF( I ).
       FIN-BUS-GLOS.
           EXIT.

       LEE-CRPF SECTION.
       INI-LEE-CRPF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM VID-RPF.
           IF NOT ( FIO-STAT-OKS AND RPF-KEY-DPRO = SCR-PROG AND
                                     RPF-KEY-TREG = 'C' )
                   GO TO FIN-LEE-CRPF.
           MOVE RPF-KEY-CORR TO I.
           COMPUTE K = I + 1.
           MOVE RPF-COD-CCMN TO SCR-COD-CCMN( K )
           MOVE RPF-COD-CCPP TO SCR-COD-CCPP( K )
           MOVE RPF-IND-STPP TO SCR-IND-STPP( K )
           MOVE RPF-GLS-PRMT TO SCR-GLS-PRMT( K )
           MOVE RPF-GLS-CONF TO SCR-GLS-CONF( K )
           MOVE RPF-GLS-UARC TO SCR-GLS-UARC( K )
           GO TO INI-LEE-CRPF.
       FIN-LEE-CRPF.
           EXIT.

       PRO-MENU SECTION.
       INI-PRO-MENU.
           MOVE FUN TO MEN.
           PERFORM BUS-MENU.
           IF NOT WSS-STAT-OKS
                  MOVE LOW-VALUES TO OPC-DFLD
                  MOVE FRM-MENS   TO OPC-GLS-MENS
                  GO TO FIN-PRO-MENU.
           PERFORM INI-OPCD VARYING J FROM 1 BY 1 UNTIL J > 20.
      *JSS
           MOVE 0 TO CFN-IMAX.
           PERFORM BUS-FUNC THRU FIN-BUS-FUNC UNTIL NOT
                                 ( WSS-STAT-OKS AND FIO-STAT-OKS AND
                                   MEN-COD-MENU = FUN-KEY-CMEN ).
           PERFORM CAL-FUN.
           PERFORM PUT-FUNC VARYING CFN-I FROM 1 BY 1
                            UNTIL CFN-I > CFN-IMAX.

           IF NOT WSS-STAT-OKS
                  MOVE LOW-VALUES TO OPC-DFLD
                  MOVE FRM-MENS   TO OPC-GLS-MENS.
       FIN-PRO-MENU.
           EXIT.

       INI-OPCD SECTION.
       INI-INI-OPCD.
           MOVE SPACES TO OPC-KEY-NIVE( J , 1 ).
           MOVE SPACES TO OPC-KEY-NIVE( J , 2 ).
           MOVE SPACES TO OPC-GLS-DIS1( J , 1 ).
           MOVE SPACES TO OPC-GLS-DIS1( J , 2 ).
           MOVE SPACES TO OPC-GLS-FMEN( J , 1 ).
           MOVE SPACES TO OPC-GLS-FMEN( J , 2 ).
           MOVE SPACES TO OPC-GLS-DIS2( J , 1 ).
           MOVE SPACES TO OPC-GLS-DIS2( J , 2 ).
       FIN-INI-OPCD.
           EXIT.

       BUS-MENU SECTION.
       INI-BUS-MENU.
      *    MOVE MEN-COD-NEMO TO PRV-PROC.
      *    CALL 'DESPROPRV'.
      *    IF PRV-STAT-OKS
      *           GO TO NIV-BUS-MENU.
      *    IF MEN-COD-FUNC = ZEROES
      *           MOVE 'NO TIENE ACCESO AL SISTEMA : ABORTO' TO
      *                 FRM-MENS
      *           MOVE 2 TO WSS-STAT
      *    ELSE
      *           MOVE 'NO TIENE PRIVILEGIOS PARA ESTE MENU' TO
      *                 FRM-MENS
      *           MOVE 1 TO WSS-STAT.
      *    GO TO FIN-BUS-MENU.
       NIV-BUS-MENU.
           PERFORM BUS-NIVE VARYING I FROM 4 BY -1 UNTIL I < 1 OR
                      MEN-COD-NIVE(I) NOT EQUAL ZEROES.
           ADD 1 TO I.
           MOVE MEN-COD-MENU    TO FUN-KEY-CMEN.
           MOVE SPACES          TO FUN-KEY-CACC.
           MOVE SPACES          TO FUN-KEY-NREG.
           MOVE 'FUN-KEY-NEMO'  TO FIO-AKEY.
           MOVE FIO-GET-NLS-ALT TO FIO-CMND.
           PERFORM VID-FUN.
           IF FIO-STAT-OKS AND ( FUN-KEY-CMEN EQUAL MEN-COD-MENU )
                  GO TO FIN-BUS-MENU.
           IF MEN-COD-FUNC = ZEROES
               MOVE 'NO EXISTEN OPCIONES PARA MENU PRINCIPAL' TO
                                                              FRM-MENS
               MOVE 2 TO WSS-STAT
           ELSE
               MOVE 'NO EXISTEN OPCIONES PARA ESTE MENU' TO
                                                         FRM-MENS
               MOVE 1 TO WSS-STAT.
       FIN-BUS-MENU.
           EXIT.

       BUS-NIVE SECTION.
       INI-BUS-NIVE.
       FIN-BUS-NIVE.
           EXIT.

       BUS-FUNC SECTION.
       INI-BUS-FUNC.
      *    MOVE FUN-KEY-NEMO TO PRV-PROC.
      *    CALL 'DESPROPRV'.
      *    IF NOT PRV-STAT-OKS
      *           GO TO NXT-BUS-FUNC.
      *JSS-UYP
           MOVE FUN-KEY-NEMO TO USP-PTA-STR.
      * Modulo que contiene la llamada a la rutina de verificacion de permisos
       COPY USPC103.
           IF USP-PERMISO NOT = 'P'
               GO TO NXT-BUS-FUNC.
           ADD 1 TO CFN-IMAX.
           MOVE FUN-NUM-FILA TO CFN-NUM-FILA( CFN-IMAX ).
           MOVE FUN-NUM-COLU TO CFN-NUM-COLU( CFN-IMAX ).
           MOVE FUN-GLS-FMEN TO CFN-GLS-FMEN( CFN-IMAX ).
           MOVE FUN-KEY-FUNC TO CFN-KEY-FUNC( CFN-IMAX ).
       NXT-BUS-FUNC.
           MOVE 'FUN-KEY-NEMO'  TO FIO-AKEY.
           MOVE FIO-GET-NXT-ALT TO FIO-CMND.
           PERFORM VID-FUN.
       FIN-BUS-FUNC.
           EXIT.

       PUT-FUNC SECTION.
       INI-PUT-FUNC.
           MOVE CFN-NUM-FILA( CFN-I ) TO FUN-NUM-FILA.
           MOVE CFN-NUM-COLU( CFN-I ) TO FUN-NUM-COLU.
           MOVE CFN-GLS-FMEN( CFN-I ) TO FUN-GLS-FMEN.
           MOVE CFN-KEY-FUNC( CFN-I ) TO FUN-KEY-FUNC.
      
           IF FUN-NUM-FILA < 3 OR FUN-NUM-FILA > 22
                MOVE 'FILA NO VALIDA PARA OPCION DEL MENU' TO
                                                           FRM-MENS
                MOVE 1 TO WSS-STAT
                GO TO FIN-PUT-FUNC.
           SUBTRACT 2 FROM FUN-NUM-FILA.
           IF FUN-NUM-COLU = 1
                MOVE FUN-GLS-FMEN    TO OPC-GLS-FMEN( FUN-NUM-FILA , 1 )
                MOVE FUN-KEY-NIVE(I) TO OPC-KEY-NIVE( FUN-NUM-FILA , 1 )
           ELSE
           IF FUN-NUM-COLU = 41
                MOVE FUN-GLS-FMEN    TO OPC-GLS-FMEN( FUN-NUM-FILA , 2 )
                MOVE FUN-KEY-NIVE(I) TO OPC-KEY-NIVE( FUN-NUM-FILA , 2 )
           ELSE
           IF FUN-NUM-COLU = 24
                MOVE FUN-GLS-FMEN    TO OPC-GLS-FME3(FUN-NUM-FILA)
                MOVE FUN-KEY-NIVE(I) TO OPC-KEY-NIV3(FUN-NUM-FILA)
           ELSE
                MOVE 'COLUMNA NO VALIDA PARA OPCION DEL MENU' TO
                                                          FRM-MENS
                MOVE 1 TO WSS-STAT
                GO TO FIN-PUT-FUNC.
       FIN-PUT-FUNC.
            EXIT.

       VID-FUN SECTION.
       INI-VID-FUN.
           MOVE SCR-SIST TO FIO-SIST.
           PERFORM GNS-FIO-FUN.
       FIN-VID-FUN.
           EXIT.

       VID-RPF SECTION.
       INI-VID-RPF.
           MOVE WSS-SIST TO FIO-SIST.
           PERFORM GNS-FIO-RPF.
       FIN-VID-RPF.
           EXIT.

       GNSSCRHDR SECTION.
       INI-GNSSCRHDR.
           MOVE FUN-GLS-NFRM      TO WSS-NFRM.
           MOVE SPACES            TO WSS-DOBL.
           MOVE FUN-GLS-FPRO      TO WSS-SIMP.
           PERFORM HDR-TITU.
           MOVE SCR-SIST        TO HDR-COD-SIST.
           MOVE WSS-DOBL        TO HDR-GLS-FPRO.
           MOVE '.'             TO WSS-GLS-PTO1
                                   WSS-GLS-PTO2
                                   WSS-GLS-PTO3.
           MOVE FUN-COD-NIVE(1) TO WSS-COD-NIV1.
           MOVE FUN-COD-NIVE(2) TO WSS-COD-NIV2.
           MOVE FUN-COD-NIVE(3) TO WSS-COD-NIV3.
           MOVE FUN-COD-NIVE(4) TO WSS-COD-NIV4.
           MOVE WSS-COD-FUNC    TO HDR-COD-FUNC.
           MOVE '/'             TO WSS-GLS-SLA1
                                   WSS-GLS-SLA2.
           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO WSS-NUM-DMSA IN WSS-FEC-DMSA.
           MOVE HOY-MHOY TO WSS-NUM-MMSA IN WSS-FEC-DMSA.
           MOVE HOY-SHOY TO WSS-NUM-SMSA IN WSS-FEC-DMSA.
           MOVE HOY-AHOY TO WSS-NUM-AMSA IN WSS-FEC-DMSA.
           MOVE WSS-FEC-DMSA    TO HDR-FEC-DMSA IN HDR-FLD.
           MOVE WSS-MAPA        TO HDR-GLS-NFRM.
      *JSS MOVE EIBTRMID        TO HDR-GLS-TRMI.
           MOVE HOY-HHOY        TO WSS-HORA.
           MOVE WSS-HHMS        TO HDR-NUM-HHMS.
           MOVE WSS-MHMS        TO HDR-NUM-MHMS.
           MOVE WSS-SHMS        TO HDR-NUM-SHMS.
           MOVE ':'             TO HDR-GLS-1HMS
                                   HDR-GLS-2HMS.
       FIN-GNSSCRHDR.
           EXIT.

       HDR-TITU SECTION.
       INI-HDR-TITU.
           IF WSS-SIMP = SPACES
                      GO TO FIN-HDR-TITU.
           PERFORM BUS-LIMI VARYING IMIN FROM 1 BY 1
                      UNTIL WSS-GLS-SIMP(IMIN) NOT = SPACES.
           PERFORM BUS-LIMI VARYING IMAX FROM 31 BY -1
                      UNTIL WSS-GLS-SIMP(IMAX) NOT = SPACES.
           COMPUTE L = ( 61 - ( 2 * ( IMAX - IMIN + 1 ) - 1 ) ) / 2 + 1.
           PERFORM PUT-TITU
                   VARYING K FROM IMIN BY 1 UNTIL K > IMAX.
       FIN-HDR-TITU.
           EXIT.

       BUS-LIMI SECTION.
       INI-BUS-LIMI.
       FIN-BUS-LIMI.
           EXIT.

       PUT-TITU SECTION.
       INI-PUT-TITU.
           MOVE WSS-GLS-SIMP( K ) TO WSS-GLS-DOBL( L ).
           ADD 2 TO L.
       FIN-PUT-TITU.
           EXIT.

       COPY GNSBGVSM.
       COPY GNSBGDTC.
       COPY GNSBGFRM.
       COPY GNSBECIC.
       COPY GNSBFRPF.
       COPY GNSBFFUN.
       COPY GNSBGMSG.
       COPY GNSBFMSG.
       COPY GNSBGIFD.
       COPY GNSBIABT.
       COPY GNSBGHOY.
       COPY GNSBGSYS.

       CAL-FUN SECTION.
       INI-CAL-FUN.
           PERFORM INI-CAL.
           PERFORM ORD-KEY VARYING CFN-M FROM 1 BY 1
                           UNTIL CFN-M > (CFN-IMAX - 1).
           PERFORM CAL-POS.
           PERFORM PUT-POS VARYING CFN-I FROM 1 BY 1
                           UNTIL CFN-I > CFN-IMAX.
       FIN-CAL-FUN.
           EXIT.

       INI-CAL SECTION.
       INI-INI-CAL.
      *JSS MOVE 0 TO CFN-IMAX.
           MOVE 0 TO CFN-STAT.
           MOVE 'CALCULO EFECTUADO OK' TO CFN-MENS.
       FIN-INI-CAL.
           EXIT.

       ORD-KEY SECTION.
       INI-ORD-KEY.
           PERFORM ORD-ORD VARYING CFN-N FROM CFN-M BY 1
                           UNTIL CFN-N > CFN-IMAX.
       FIN-ORD-KEY.
           EXIT.

       ORD-ORD SECTION.
       INI-ORD-ORD.
           IF CFN-KEY-FUNC(CFN-M) > CFN-KEY-FUNC(CFN-N)
               MOVE CFN-VEC-FUNC(CFN-N) TO CFN-GLS-AUXI
               MOVE CFN-VEC-FUNC(CFN-M) TO CFN-VEC-FUNC(CFN-N)
               MOVE CFN-GLS-AUXI TO CFN-VEC-FUNC(CFN-M).
       FIN-ORD-ORD.
           EXIT.

       CAL-POS SECTION.
       INI-CAL-POS.
           MOVE 0 TO CFN-J.
           MOVE CFN-FINI(CFN-IMAX) TO CFN-FILA.
           IF CFN-IMAX NOT > 10
                MOVE 24 TO CFN-COLU
                PERFORM CAL-FIL VARYING CFN-I FROM 1 BY 1
                                UNTIL CFN-I > CFN-IMAX
                GO TO FIN-CAL-POS.
           DIVIDE CFN-IMAX BY 2 GIVING CFN-LMAX REMAINDER CFN-REST.
           IF CFN-REST NOT = 0
               ADD 1 TO CFN-LMAX.
           MOVE 1 TO CFN-COLU.
           PERFORM CAL-FIL VARYING CFN-I FROM 1 BY 1
                           UNTIL CFN-I > CFN-LMAX.
           MOVE CFN-FINI(CFN-IMAX) TO CFN-FILA.
           DIVIDE CFN-IMAX BY 2 GIVING CFN-LMAX.
           MOVE 41 TO CFN-COLU.
           PERFORM CAL-FIL VARYING CFN-I FROM 1 BY 1 UNTIL
                           CFN-I > CFN-LMAX.
       FIN-CAL-POS.
           EXIT.

       CAL-FIL SECTION.
       INI-CAL-FIL.
           ADD 1 TO CFN-J.
           MOVE CFN-FILA TO CFN-FICO(CFN-J, 1)
           MOVE CFN-COLU TO CFN-FICO(CFN-J, 2)
           ADD CFN-SEPA(CFN-IMAX) TO CFN-FILA.
       FIN-CAL-FIL.
           EXIT.

       PUT-POS SECTION.
       INI-PUT-POS.
           MOVE CFN-FICO(CFN-I, 1) TO CFN-NUM-FILA(CFN-I).
           MOVE CFN-FICO(CFN-I, 2) TO CFN-NUM-COLU(CFN-I).
       FIN-PUT-POS.
           EXIT.
