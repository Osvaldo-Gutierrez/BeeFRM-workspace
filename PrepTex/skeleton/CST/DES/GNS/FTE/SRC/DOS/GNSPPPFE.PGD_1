//ISILVA  JOB ,,CLASS=A,MSGCLASS=X,USER=ISILVA,PASSWORD=SLVMAR,
//            REGION=4M
/*ROUTE PRINT MVS
//STEP1  EXEC PGM=AFOLIBR
//STEPLIB  DD DSN=ADR.LIB.V38.LOAD,DISP=SHR
//MASTER   DD DSN=GNSCL.FUENTES,DISP=SHR
//OSJOB    DD DUMMY
//SYSPRINT DD SYSOUT=X
//SYSIN    DD *
-ADD GNSPPPFE,PSWD=CONS,ARC
     DUMMY
-EMOD
-SEL GNSPPPFE,CONS
-REP ALL
       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. GNSPPPFE.
       AUTHOR. CONSIST.
       DATE-WRITTEN.    29-AUG-1991.
      *
      * RECUPERA DE LLAVES 2.0 3.0
      *
      * COMPATIBLE CON NUEVA VERSION DE MENU PARA MOSTRAR EN HEADER
      * 'PA2' CUANDO HAY LLAVES SETEADAS ( LARGO HEADER 168 )
      * AL NO EXISTIR LLAVES SETEADAS NO DESPLEIGA EL MAPA DOS VECES
      *
       ENVIRONMENT DIVISION.
      *=====================
       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWGTSC.
       COPY GNSWGSCR.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
      *
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRTAB.
       COPY GNSBRIDD.
       01  IFD.
           03 FILLER                            PIC X(01).
           03 IFD-COD-CIFD                      PIC X(12).
           03 IFD-GLS-VSIS OCCURS 30.
              05 IFD-COD-DSIS                   PIC X(04).
              05 IFD-COD-GSIS                   PIC X(03).
           03 FILLER                            PIC X(18).
      *
       COPY GNSWGFRM.
       COPY GNSWGQUE.
      *
       01  WSS-VARI.
           03 WSS-TFLD  COMP      VALUE +0             PIC S9(04).
           03 WSS-PCUR  COMP      VALUE +0             PIC S9(04).
           03 WSS-PMSG  COMP      VALUE +1880          PIC S9(04).
           03 WSS-TCMA  COMP      VALUE +0             PIC S9(04).
           03 WSS-CMMA                                 PIC X(3500).

           03 WSS-I             VALUE 0                PIC 9(04).
           03 WSS-J             VALUE 0                PIC 9(04).
           03 WSS-K             VALUE 0                PIC 9(04).
           03 WSS-THDR          VALUE 168              PIC 9(04).
           03 WSS-IFLD          VALUE 1                PIC 9(04).
           03 WSS-IKEY          VALUE ' '              PIC X(01).
              88 WSS-EXISTEN-KEY    VALUE 'S'.
              88 WSS-NO-EXISTEN-KEY VALUE 'N'.
       01  WSS-TAB.
           03 WSS-TAB-GLS-DESC                         PIC X(40).
           03 WSS-TAB-GLS-DATA                         PIC X(60).
       01  WSS-TAB-RED         REDEFINES WSS-TAB.
           03 WSS-TAB-TKYM OCCURS 11 TIMES.
              05 WSS-TKEY                              PIC X(03).
              05 WSS-PKEY                              PIC 9(04).
              05 WSS-LKEY                              PIC 9(02).
           03 FILLER                                   PIC X(01).
       01  WSS-DFLD.
           03 WSS-DFLD-CHR    OCCURS 3000              PIC X(01).
       01  WSS-QFLD.
           03 WSS-TKEY-Q                               PIC X(03).
           03 WSS-QFLD-CHR    OCCURS 100               PIC X(01).

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                             PIC X(3500).
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           PERFORM CIC-ERR THRU FIN-CIC-ERR.
           MOVE DFHCOMMAREA TO WSS-CMMA SCR-VARI.
           MOVE EIBCALEN    TO WSS-TCMA.
      *
      *Buscar TS que usa procesador de MENU
           PERFORM GNS-GET-TSC.
      *
      *Buscar idd con codigo TSC-CIDD.
           PERFORM GNS-GET-IDD.
      *
      *Obtener el contexo de la pantalla
           EXEC CICS HANDLE CONDITION LENGERR(CON-MAIN)
                                      NOSPACE(CON-MAIN)
           END-EXEC.
           MOVE +3000 TO WSS-TFLD.
           EXEC CICS RECEIVE INTO(WSS-DFLD) LENGTH(WSS-TFLD) BUFFER
           END-EXEC.
           MOVE EIBCPOSN TO WSS-PCUR.
       CON-MAIN.
      *Procesar la tabla KYM
           MOVE TSC-SIST TO FIO-SIST.
           MOVE 'KYM'    TO TAB-COD-TTAB IN TAB.
      *Se usa SCR-NMAP solo para poder manejar la situacion de programas
      *enganchados que se van pasando el control
      *    MOVE TSC-NMAP TO TAB-COD-CTAB IN TAB.
           MOVE SCR-NMAP TO TAB-COD-CTAB IN TAB.
      *
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'ESPECIFICACION DE LLAVES INEXISTENTE' TO FRM-MENS
               PERFORM PUT-MSG
               PERFORM RET-PRG
           ELSE
               MOVE TAB-GLS-DESC IN TAB TO WSS-TAB-GLS-DESC IN WSS-TAB
               MOVE TAB-GLS-DATA IN TAB TO WSS-TAB-GLS-DATA IN WSS-TAB.
      *
      *Lee registros de cola existente y busca en tabla
           PERFORM GNS-ERR-QUE.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'KYM'    TO QUE-TYPE.
           MOVE 1        TO QUE-NITM.
           MOVE 'N'      TO WSS-IKEY.
       CIC-MAIN.
           MOVE QUE-GET  TO QUE-CMND.
           MOVE +3000 TO QUE-LITM.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT = QUE-STAT-OKS
               MOVE QUE-ITEM TO WSS-QFLD
               PERFORM SCN-TAB
               ADD 1 TO QUE-NITM
               GO TO CIC-MAIN.
           IF QUE-NITM = 1
                MOVE 'LLAVES NO SETEADAS' TO FRM-MENS
                PERFORM PUT-MSG
           ELSE
                IF WSS-EXISTEN-KEY
                    EXEC CICS SEND FROM(WSS-DFLD)
                          LENGTH(WSS-TFLD)
                          ERASE
                    END-EXEC
                    EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
                          TERMINAL
                    END-EXEC.
           PERFORM RET-PRG.
           GOBACK.
       FIN-MAIN.
           EXIT.

       SCN-TAB SECTION.
       INI-SCN-TAB.
           MOVE 1 TO WSS-IFLD.
       LUP-SCN-TAB.
           IF WSS-TKEY(WSS-IFLD) NOT > SPACES
               GO TO FIN-SCN-TAB.
           IF WSS-TKEY(WSS-IFLD) = WSS-TKEY-Q
               PERFORM MOV-TAB
               GO TO FIN-SCN-TAB.
           ADD 1 TO WSS-IFLD.
           IF WSS-IFLD NOT > 11
               GO TO LUP-SCN-TAB.
       FIN-SCN-TAB.
           EXIT.

       MOV-TAB SECTION.
       INI-MOV-TAB.
           MOVE WSS-PKEY(WSS-IFLD) TO WSS-J.
           PERFORM MOV-KEY VARYING WSS-I FROM 1 BY 1 UNTIL
                                   WSS-I > WSS-LKEY(WSS-IFLD).
       FIN-MOV-TAB.
           EXIT.

       MOV-KEY SECTION.
       INI-MOV-KEY.
           ADD WSS-J WSS-THDR GIVING WSS-K.
           MOVE WSS-QFLD-CHR(WSS-I) TO WSS-DFLD-CHR(WSS-K).
           IF WSS-QFLD-CHR(WSS-I) > SPACES
               MOVE 'S' TO WSS-IKEY.
           ADD 1 TO WSS-J.
       FIN-MOV-KEY.
           EXIT.

       RET-PRG SECTION.
       INI-RET-PRG.
           EXEC CICS SEND CONTROL FREEKB
           END-EXEC.
           PERFORM SCR-EXIT.
       FIN-RET-PRG.
           EXIT.

       PUT-MSG SECTION.
       INI-PUT-MSG.
      *     EXEC CICS SEND CONTROL CURSOR(WSS-PMSG)
      *           TERMINAL
      *     END-EXEC.
      *     MOVE FRM-MSG TO FRM-CMND.
      *     PERFORM GNS-PRO-FRM.
      *     EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
      *           TERMINAL
      *     END-EXEC.
       FIN-PUT-MSG.
           EXIT.

       SCR-EXIT SECTION.
       INI-SCR-EXIT.
           IF SCR-DESDE-CMN
      *          EXEC CICS XCTL
      *                  PROGRAM(WSS-PROG)
                EXEC CICS RETURN
                        TRANSID(SCR-NTRN)
                        COMMAREA(SCR-VARI)
                        LENGTH(SCR-TCMA)
                END-EXEC
           ELSE
                EXEC CICS RETURN
                       TRANSID(EIBTRNID)
                       COMMAREA(WSS-CMMA)
                       LENGTH(WSS-TCMA)
                END-EXEC.
       FIN-SCR-EXIT.
           EXIT.

       GNS-GET-TSC SECTION.
       INI-GNS-GET-TSC.
           EXEC CICS HANDLE CONDITION
                     QIDERR (NEX-GNS-GET-TSC)
           END-EXEC.
           MOVE EIBTRMID TO TSC-TERM.
           MOVE TSC-TTSC TO TSC-LITM.
           EXEC CICS READQ TS
                     QUEUE(TSC-COLA)
                     INTO(TSC-ITEM)
                     LENGTH(TSC-LITM)
                     ITEM(1)
           END-EXEC.
           GO TO FIN-GNS-GET-TSC.
       NEX-GNS-GET-TSC.
           MOVE 'SISTEMA DEBE EJECUTARSE CON PROCESADOR MENU COMPATIBLE'
                        TO FRM-MENS.
           MOVE FRM-ABT TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
       FIN-GNS-GET-TSC.
           EXIT.

       GNS-GET-IDD SECTION.
       INI-GNS-GET-IDD.
           IF ( TSC-TIND = 'C' ) OR ( TSC-TIND NOT > SPACES )
               GO TO IFD-GNS-GET-IDD.
      *La independencia de datos es IDD EN TEMPORARY STORAGE ( VERSION 3.0 )
           MOVE TSC-CIDD TO IDD-COD-CIDD IN IDD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IDD.
           IF FIO-STAT-OKS
               MOVE IDD TO IDD-REGI
               GO TO FIN-GNS-GET-IDD.
           MOVE 'DEFAULT' TO IDD-COD-CIDD IN IDD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IDD.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE REGISTRO IDD CON LLAVE : ' TO FRM-MEN1
               MOVE IDD-COD-CIDD IN IDD                   TO FRM-MEN2
               MOVE FRM-ABT TO FRM-CMND
               PERFORM GNS-PRO-FRM
           ELSE
               MOVE IDD TO IDD-REGI.
           GO TO FIN-GNS-GET-IDD.
       IFD-GNS-GET-IDD.
      *La independencia de datos es IFD EN COMMAREA ( VERSION 2.0 )
           MOVE TSC-CIDD TO IFD-COD-CIFD IN IFD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IFD.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE REGISTRO IFD CON LLAVE :' TO FRM-MEN1
               MOVE IFD-COD-CIFD IN IFD                  TO FRM-MEN2
               MOVE FRM-ABT TO FRM-CMND
               PERFORM GNS-PRO-FRM
           ELSE
               MOVE SPACES TO IFD-FSIS
               PERFORM MOV-IFD VARYING WSS-I FROM 1 BY 1 UNTIL
                                   WSS-I > 30.
       FIN-GNS-GET-IDD.
           EXIT.

       MOV-IFD SECTION.
       INI-MOV-IFD.
           MOVE IFD-COD-DSIS(WSS-I) TO IFD-DSIS(WSS-I).
           MOVE IFD-COD-GSIS(WSS-I) TO IFD-GSIS(WSS-I).
       FIN-MOV-IFD.
           EXIT.

       COPY GNSBFTAB.
       COPY GNSBFIDD.
       COPY GNSBFIFD.
       COPY GNSBGQUE.
       COPY GNSBGDTC.
       COPY GNSBGVSM.
       COPY GNSBIABT.
       COPY GNSBGFRM.
      * COPY GNSBECIC.
      ********************* ERR *********************
       CIC-ERR SECTION.
       INI-CIC-ERR.
      *     EXEC CICS HANDLE AID
      *               CLEAR    (CIC-ERR-ACL)
      *               PA1      (CIC-ERR-PA1)
      *               PA2      (CIC-ERR-PA2)
      *     END-EXEC.
           EXEC CICS HANDLE CONDITION
                     MAPFAIL  (CIC-ERR-MFA)
                     DSIDERR  (CIC-ERR-BFI)
                     DUPKEY   (CIC-ERR-DAK)
                     DUPREC   (CIC-ERR-CDK)
                     ENDFILE  (CIC-ERR-EOF)
                     ILLOGIC  (CIC-ERR-LGE)
                     INVREQ   (CIC-ERR-OPE)
                     IOERR    (CIC-ERR-DDE)
                     LENGERR  (CIC-ERR-IFI)
                     NOSPACE  (CIC-ERR-BVE)
                     NOTFND   (CIC-ERR-IKY)
                     NOTOPEN  (CIC-ERR-RNA)
                     PGMIDERR (CIC-ERR-PGM)
           END-EXEC.
       FIN-CIC-ERR.
           EXIT.

       CIC-ERR-ACL.
           MOVE 'GNSPPCLR' TO FRM-PROG.
           MOVE 0          TO FRM-TCMA.
           MOVE SPACES     TO FRM-CMMA.
           EXEC CICS XCTL
                     PROGRAM(FRM-PROG)
                     COMMAREA(FRM-CMMA)
                     LENGTH(FRM-TCMA)
           END-EXEC.
       CIC-ERR-PA1.
           MOVE FRM-FFLD-PA1 TO FRM-FFLD.
           GO TO CIC-ERR-FRM.
       CIC-ERR-PA2.
           MOVE FRM-FFLD-PA2 TO FRM-FFLD.
           GO TO CIC-ERR-FRM.
       CIC-ERR-BFI.
           MOVE '96'                    TO FIO-STAT.
           MOVE 'ARCHIVO NO ENCONTRADO' TO FIO-MEN1.
           MOVE FIO-VOID                TO FIO-MEN2.
           MOVE FIO-MENS                TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-DAK.
           MOVE '22'                TO FIO-STAT.
           MOVE 'DUPLICATE ALT KEY' TO FIO-MEN1.
           MOVE FIO-VOID            TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-CDK.
           MOVE '22'                 TO FIO-STAT.
           MOVE 'REGISTRO YA EXISTE' TO FIO-MEN1.
           MOVE FIO-VOID             TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-EOF.
           MOVE '10'             TO FIO-STAT.
           MOVE 'FIN DE ARCHIVO' TO FIO-MEN1.
           MOVE FIO-VOID         TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-LGE.
           MOVE '92'           TO FIO-STAT.
           MOVE 'ERROR LOGICO' TO FIO-MEN1.
           MOVE FIO-VOID       TO FIO-MEN2.
           MOVE FIO-MENS       TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-OPE.
           MOVE '30'               TO FIO-STAT.
           MOVE 'ERROR PERMANENTE' TO FIO-MEN1.
           MOVE FIO-VOID           TO FIO-MEN2.
           MOVE FIO-MENS           TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-DDE.
           MOVE '90'                TO FIO-STAT.
           MOVE 'ERROR DISPOSITIVO' TO FIO-MEN1.
           MOVE FIO-VOID            TO FIO-MEN2.
           MOVE FIO-MENS            TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-IFI.
           MOVE '95'             TO FIO-STAT.
           MOVE 'ERROR DE LARGO' TO FIO-MEN1.
           MOVE FIO-VOID         TO FIO-MEN2.
           MOVE FIO-MENS         TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-BVE.
           MOVE '24'               TO FIO-STAT.
           MOVE 'ERROR DE ESPACIO' TO FIO-MEN1.
           MOVE FIO-VOID           TO FIO-MEN2.
           MOVE FIO-MENS           TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-IKY.
           MOVE '23'          TO FIO-STAT.
           MOVE 'INVALID KEY' TO FIO-MEN1.
           MOVE FIO-VOID      TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-PGM.
           MOVE '98'               TO FIO-STAT.
           MOVE 'PROGRAM ID ERROR' TO FRM-MENS.
           GO TO CIC-ERR-FRM.
       CIC-ERR-RNA.
           MOVE '93'              TO FIO-STAT.
           MOVE 'ARCHIVO CERRADO' TO FIO-MEN1.
           MOVE FIO-VOID          TO FIO-MEN2.
           MOVE FIO-MENS          TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-MFA.
           MOVE 'ABORTO : MAP FAIL' TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-FIO.
           GO TO FIN-FIO.
       CIC-ERR-FRM.
           GO TO FIN-GNS-PRO-FRM.
       CIC-ERR-ABT.
           MOVE 'GNSPPABT' TO FRM-PROG.
           MOVE +79        TO FRM-TCMA.
      *    MOVE FRM-MENS   TO FRM-CMMA.
           EXEC CICS XCTL
                     PROGRAM(FRM-PROG)
                     COMMAREA(FRM-MENS)
                     LENGTH(FRM-TCMA)
           END-EXEC.
-END
/*
//STEP1  EXEC PGM=AFOLIBR,PARM='NJTA,NRJS'
//STEPLIB  DD DSN=ADR.LIB.V38.LOAD,DISP=SHR
//MASTER   DD DSN=GNSCL.FUENTES,DISP=SHR
//OSJOB    DD DSN=&&TEMP,DISP=(,PASS),SPACE=(CYL,(1,1)),UNIT=SYSDA
//SYSPRINT DD SYSOUT=*
//SYSPUNCH DD SYSOUT=*
//SYSIN    DD *
-SEL GNSPPPFE,CONS,EXEC,CCOPY,COPYDD
-END
/*
//STEP2   EXEC DFHEITCL,PARM.LKED='MAP,XREF'
//TRN.SYSIN    DD DSN=&&TEMP,DISP=OLD
//COB.SYSLIB   DD DSN=GNSCL.FUENTES.V30,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=DEUCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DEUCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=COLCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=COLCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=SUPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SUPCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=SBTCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SBTCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=CICS.V211.COBLIB,DISP=SHR,SUBSYS=LAM
//LKED.SYSLIB  DD DSN=CICS.V211.LOADLIB,DISP=SHR
//             DD DSN=GNSCM.RUTINAS.CONSIST,DISP=SHR
//             DD DSN=COB.VSCLLIB,DISP=SHR
//LKED.SYSLMOD DD DSN=GNSCM.LINEA(GNSPPPFE),DISP=SHR
//LKED.SYSLIN  DD DSN=CICS.V211.COBLIB(DFHEILIC),DISP=SHR
//             DD DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD DSN=ADR.DB75.OBJECT.DESA(DBCSVPR),DISP=SHR
//
