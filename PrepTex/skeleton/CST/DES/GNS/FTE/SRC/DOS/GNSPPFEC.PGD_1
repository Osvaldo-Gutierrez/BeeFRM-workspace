       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.    GNSPPFEC.
       AUTHOR.        CONSIST LTDA.

      * Programa procesador de Fechas

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.

       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       01  IFD-VARI.
           03 IFD-DAUX.
              05 IFD-SAUX                 PIC X(03).
              05 IFD-QAUX                 PIC X(01).
           03 IFD-DBID                    PIC 9(03).
           03 IFD-IAUX                    PIC 9(02).
           03 IFD-FSIS.
              05 IFD-VSIS OCCURS 30   INDEXED BY IFD-ITBL.
                 07 IFD-DSIS.
                    09 IFD-SIST           PIC X(03).
                    09 IFD-QSIS           PIC X(01).
                 07 IFD-GSIS              PIC X(03).

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           03 FILLER                  PIC X(203).

       PROCEDURE DIVISION.
      *===================

      * Modulo para Proceso de Fechas y Hora
      * 24-MAY-1989 20:21:31
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'GNSPPFEC' TO FIO-PROG.
           MOVE 'GNSC'     TO IFD-DSIS(1).
           MOVE '680'      TO IFD-GSIS(1).
           MOVE DFHCOMMAREA TO FEC-VARI.
           PERFORM PRO-FEC.
           MOVE FEC-VARI TO DFHCOMMAREA.
       FIN-MAIN.
           EXIT PROGRAM.

       PRO-FEC SECTION.
       INI-PRO-FEC.
           MOVE FEC-SOKS TO FEC-STT1
                            FEC-STT2.
           MOVE SPACES TO MSG-COD-MENS IN MSG.
           IF FEC-CMND = FEC-VAL-FEC
               PERFORM FEC-VALD-FEC
      *        IF FEC-STAT-OKS AND NOT FEC-STAT-BLK AND FEC-TIENE-DIA
               IF FEC-STAT-OKS AND NOT FEC-STAT-BLK
                   PERFORM FEC-OTRA-VAL
                   GO TO MSG-PRO-FEC
               ELSE
                   GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-CMP-FEC
               PERFORM FEC-COMP-FEC
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-CMP-HOY
               PERFORM FEC-COMP-HOY
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-RET-JUL
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-RTRN-JUL
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-RET-FEC
               PERFORM FEC-RTRN-FEC
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-SIG-HBL
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-SIGU-HBL
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-SUM-DIA
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-SUMA-DIA
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-SUM-MES
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-SUMA-MES
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-SUM-ANO
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-SUMA-ANO
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-RST-DIA
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-REST-DIA
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-RST-MES
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-REST-MES
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-RST-ANO
               PERFORM FEC-EXT-SLSH
               PERFORM FEC-TRDC-FEC
               PERFORM FEC-REST-ANO
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-DIF-DIA
               PERFORM FEC-DIFF-DIA
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-DIF-MES
               PERFORM FEC-DIFF-MES
               GO TO MSG-PRO-FEC.
           IF FEC-CMND = FEC-DIF-ANO
               PERFORM FEC-DIFF-ANO
               GO TO MSG-PRO-FEC.
       ERR-PRO-FEC.
           MOVE 'FECCMNDNEX' TO MSG-COD-MENS.
       MSG-PRO-FEC.
           IF MSG-COD-MENS > SPACES
               MOVE 'GNS'     TO MSG-COD-SIST
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC IN MSG TO FEC-MENS
           ELSE
           IF NOT FEC-STAT-OKS
               MOVE 'FECSTAT' TO MSG-COD-TMSG IN MSG
               MOVE FEC-STAT  TO MSG-COD-CMSG IN MSG
               MOVE 'GNS'     TO MSG-COD-SIST
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC IN MSG TO FEC-MENS
           ELSE
           IF ( FEC-CMND = FEC-VAL-FEC OR FEC-RET-FEC OR FEC-SIG-HBL OR
                           FEC-SUM-DIA OR FEC-SUM-MES OR FEC-SUM-ANO OR
                           FEC-RST-DIA OR FEC-RST-MES OR FEC-RST-ANO )
              AND FEC-IEDT > SPACES
               PERFORM FEC-PUT-SLSH.
       FIN-PRO-FEC.
           EXIT.

       FEC-VALD-FEC SECTION.
       INI-FEC-VALD-FEC.
      *     IF FEC-IEDT > SPACES
               PERFORM FEC-EXT-SLSH.

           MOVE 1 TO FEC-SIND.
       LUP-FEC-VALD-FEC.
           IF FEC-FECH-CHR( FEC-SIND ) NOT > SPACES
               MOVE ZEROES TO FEC-FECH-CHR( FEC-SIND ).
           ADD 1 TO FEC-SIND.
           IF FEC-SIND NOT > 8
               GO TO LUP-FEC-VALD-FEC.
           MOVE FEC-ITM1-RED TO FEC-SVLD-RED.
           MOVE FEC-ITM2-RED TO FEC-AVLD-RED.
           MOVE FEC-ITM3-RED TO FEC-MVLD-RED.
           MOVE FEC-ITM4-RED TO FEC-DVLD-RED.
           PERFORM FEC-VNUM-FEC.
           IF NOT FEC-STAT-OKS
               GO TO FIN-FEC-VALD-FEC.
           MOVE FEC-FVLD TO FEC-FECH.
           IF FEC-STAT-BLK
               GO TO FIN-FEC-VALD-FEC.
           PERFORM FEC-TRDC-FEC.
           IF FEC-MVLD < 1 OR FEC-MVLD > 12
               MOVE FEC-SMES TO FEC-STAT
               GO TO FIN-FEC-VALD-FEC.
           IF FEC-MVLD NOT = 02
              IF FEC-DVLD < 1 OR FEC-DVLD > FEC-DMES(FEC-MVLD)
                  MOVE FEC-SDIA TO FEC-STAT
                  GO TO FIN-FEC-VALD-FEC
              ELSE
                  NEXT SENTENCE
           ELSE
               DIVIDE FEC-AVLD BY 4 GIVING FEC-REST REMAINDER FEC-REST
               IF FEC-REST = 0
                   IF FEC-DVLD < 1 OR FEC-DVLD > 29
                       MOVE FEC-SDIA TO FEC-STAT
                       GO TO FIN-FEC-VALD-FEC
                   ELSE
                       NEXT SENTENCE
               ELSE
                   IF FEC-DVLD < 1 OR FEC-DVLD > 28
                       MOVE FEC-SDIA TO FEC-STAT
                       GO TO FIN-FEC-VALD-FEC.
           IF FEC-SVLD < 18 OR FEC-SVLD > 20
               MOVE FEC-SSIG TO FEC-STAT
               GO TO FIN-FEC-VALD-FEC.
       FIN-FEC-VALD-FEC.
           EXIT.

       FEC-OTRA-VAL SECTION.
       INI-FEC-OTRA-VAL.
           IF FEC-CHOY = FEC-CHOY-L  OR FEC-CHOY-E  OR FEC-CHOY-G OR
                         FEC-CHOY-LE OR FEC-CHOY-GE OR FEC-CHOY-LG
               MOVE FEC-DVLD TO FEC-DEC1
               MOVE FEC-MVLD TO FEC-MEC1
               MOVE FEC-SVLD TO FEC-SEC1
               MOVE FEC-AVLD TO FEC-AEC1
               MOVE FEC-DHOY TO FEC-DEC2
               MOVE FEC-MHOY TO FEC-MEC2
               MOVE FEC-SHOY TO FEC-SEC2
               MOVE FEC-AHOY TO FEC-AEC2
               PERFORM CMP-FEC-COMP-FEC THRU FIN-FEC-COMP-FEC
               IF FEC-STAT-LES AND
                  ( FEC-CHOY = FEC-CHOY-E OR FEC-CHOY-G OR FEC-CHOY-GE )
                   MOVE 'FEC    <HOY' TO MSG-COD-MENS
                   MOVE FEC-SMAL TO FEC-STAT
                   GO TO FIN-FEC-OTRA-VAL
               ELSE
               IF FEC-STAT-EQL AND
                  ( FEC-CHOY = FEC-CHOY-L OR FEC-CHOY-G OR FEC-CHOY-LG )
                   MOVE 'FEC    =HOY' TO MSG-COD-MENS
                   MOVE FEC-SMAL TO FEC-STAT
                   GO TO FIN-FEC-OTRA-VAL
               ELSE
               IF FEC-STAT-GRT AND
                  ( FEC-CHOY = FEC-CHOY-L OR FEC-CHOY-E OR FEC-CHOY-LE )
                   MOVE 'FEC    >HOY' TO MSG-COD-MENS
                   MOVE FEC-SMAL TO FEC-STAT
                   GO TO FIN-FEC-OTRA-VAL.
           IF FEC-VHBL = FEC-VHBL-S AND FEC-TIENE-DIA
               PERFORM FEC-VALD-HBL.
       FIN-FEC-OTRA-VAL.
           EXIT.

       FEC-COMP-FEC SECTION.
       INI-FEC-COMP-FEC.
           MOVE FEC-FEC1 TO FEC-FECH.
           PERFORM FEC-EXT-SLSH.
           PERFORM FEC-TRDC-FEC.
           MOVE FEC-DVLD TO FEC-DEC1.
           MOVE FEC-MVLD TO FEC-MEC1.
           MOVE FEC-SVLD TO FEC-SEC1.
           MOVE FEC-AVLD TO FEC-AEC1.
           MOVE FEC-FEC2 TO FEC-FECH.
           PERFORM FEC-EXT-SLSH.
           PERFORM FEC-TRDC-FEC.
           MOVE FEC-DVLD TO FEC-DEC2.
           MOVE FEC-MVLD TO FEC-MEC2.
           MOVE FEC-SVLD TO FEC-SEC2.
           MOVE FEC-AVLD TO FEC-AEC2.
       CMP-FEC-COMP-FEC.
           IF FEC-SEC1 > FEC-SEC2
               MOVE FEC-SGRT TO FEC-STAT
           ELSE
           IF FEC-SEC1 < FEC-SEC2
               MOVE FEC-SLES TO FEC-STAT
           ELSE
               MOVE FEC-SEQL TO FEC-STAT
               IF FEC-AEC1 > FEC-AEC2
                   MOVE FEC-SGRT TO FEC-STAT
               ELSE
               IF FEC-AEC1 < FEC-AEC2
                   MOVE FEC-SLES TO FEC-STAT
               ELSE
                   MOVE FEC-SEQL TO FEC-STAT
                   IF FEC-MEC1 > FEC-MEC2
                       MOVE FEC-SGRT TO FEC-STAT
                   ELSE
                   IF FEC-MEC1 < FEC-MEC2
                       MOVE FEC-SLES TO FEC-STAT
                   ELSE
                       MOVE FEC-SEQL TO FEC-STAT
                       IF FEC-DEC1 > FEC-DEC2
                           MOVE FEC-SGRT TO FEC-STAT
                       ELSE
                       IF FEC-DEC1 < FEC-DEC2
                           MOVE FEC-SLES TO FEC-STAT
                       ELSE
                           MOVE FEC-SEQL TO FEC-STAT.
       FIN-FEC-COMP-FEC.
           EXIT.

       FEC-COMP-HOY SECTION.
       INI-FEC-COMP-HOY.
           PERFORM FEC-EXT-SLSH.
           PERFORM FEC-TRDC-FEC.
           MOVE FEC-DVLD TO FEC-DEC1.
           MOVE FEC-MVLD TO FEC-MEC1.
           MOVE FEC-SVLD TO FEC-SEC1.
           MOVE FEC-AVLD TO FEC-AEC1.
           PERFORM FEC-GDAT.
           MOVE FEC-DHOY TO FEC-DEC2.
           MOVE FEC-MHOY TO FEC-MEC2.
           MOVE FEC-SHOY TO FEC-SEC2.
           MOVE FEC-AHOY TO FEC-AEC2.
           PERFORM CMP-FEC-COMP-FEC THRU FIN-FEC-COMP-FEC.
       FIN-FEC-COMP-HOY.
           EXIT.

       FEC-TRDC-FEC SECTION.
       INI-FEC-TRDC-FEC.
           PERFORM FEC-GDAT.
           IF NOT FEC-TIENE-MES
               MOVE FEC-MHOY TO FEC-MVLD
           ELSE
           IF FEC-TIENE-DIA
               MOVE FEC-ITM2 TO FEC-MVLD
           ELSE
               MOVE FEC-ITM1 TO FEC-MVLD.
           IF FEC-TIENE-DIA
               MOVE FEC-ITM1 TO FEC-DVLD
           ELSE
               IF FEC-MHOY = FEC-MVLD
                   MOVE FEC-DHOY TO FEC-DVLD
               ELSE
                   MOVE 1 TO FEC-DVLD.
           IF NOT FEC-TIENE-SIGLO
               MOVE FEC-SHOY TO FEC-SVLD
           ELSE
           IF FEC-TIENE-DIA AND FEC-TIENE-MES
               MOVE FEC-ITM3 TO FEC-SVLD
           ELSE
           IF ( NOT FEC-TIENE-DIA ) AND ( NOT FEC-TIENE-MES )
               MOVE FEC-ITM1  TO FEC-SVLD
           ELSE
               MOVE FEC-ITM2 TO FEC-SVLD.
           IF NOT FEC-TIENE-ANO
               MOVE FEC-AHOY TO FEC-AVLD
           ELSE
           IF FEC-TIENE-DIA AND FEC-TIENE-MES AND FEC-TIENE-SIGLO
               MOVE FEC-ITM4 TO FEC-AVLD
           ELSE
           IF ( FEC-TIENE-DIA AND FEC-TIENE-MES   ) OR
              ( FEC-TIENE-MES AND FEC-TIENE-SIGLO )
               MOVE FEC-ITM3 TO FEC-AVLD
           ELSE
           IF FEC-TIENE-MES OR FEC-TIENE-SIGLO
               MOVE FEC-ITM2 TO FEC-AVLD
           ELSE
               MOVE FEC-ITM1 TO FEC-AVLD.
           IF ( NOT FEC-TIENE-SIGLO ) AND ( FEC-TIENE-QUIEBRE ) AND
              ( FEC-AVLD NOT > 50 )
               ADD 1 TO FEC-SVLD.
       FIN-FEC-TRDC-FEC.
           EXIT.

       FEC-VNUM-FEC SECTION.
       INI-FEC-VNUM-FEC.
           IF ( FEC-FVLD NOT > SPACES OR FEC-FVLD = ZEROES ) AND
              FEC-FBLK = FEC-FBLK-S
               MOVE ZEROES TO FEC-FVLD
               MOVE FEC-SBLK TO FEC-STAT
               GO TO FIN-FEC-VNUM-FEC.
           IF ( FEC-FVLD NOT > SPACES OR FEC-FVLD = ZEROES ) AND
              FEC-FBLK = FEC-FBLK-N
                MOVE FEC-SOBL TO FEC-STAT
                GO TO FIN-FEC-VNUM-FEC.
           IF FEC-FVLD IS NOT NUMERIC
               MOVE FEC-SNN1 TO FEC-STAT.
       FIN-FEC-VNUM-FEC.
           EXIT.

       FEC-GDAT SECTION.
       INI-FEC-GDAT.
           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO FEC-DHOY.
           MOVE HOY-MHOY TO FEC-MHOY.
           MOVE HOY-SHOY TO FEC-SHOY.
           MOVE HOY-AHOY TO FEC-AHOY.
           MOVE HOY-HHHY TO FEC-HHHY.
           MOVE HOY-MMHY TO FEC-MMHY.
           MOVE HOY-SSHY TO FEC-SSHY.
       FIN-FEC-GDAT.
           EXIT.

       FEC-VALD-HBL SECTION.
       INI-FEC-VALD-HBL.
           PERFORM FEC-RTRN-JUL.
           DIVIDE FEC-FJUL BY 7 GIVING FEC-REST REMAINDER FEC-REST.
           SUBTRACT FEC-DIA1 FROM 7 GIVING FEC-AUXI.
           ADD 1 FEC-AUXI GIVING FEC-SIND.
           IF FEC-SIND > 6
               MOVE 0 TO FEC-SIND.
           IF FEC-REST = FEC-AUXI OR FEC-SIND
               MOVE FEC-SFER TO FEC-STAT
           ELSE
               MOVE 'FER'        TO TAB-COD-TTAB IN TAB
               MOVE FEC-FECH-FDM TO TAB-COD-CTAB IN TAB
               MOVE 'GNS'        TO FIO-SIST
               MOVE FIO-GET-KEY  TO FIO-CMND
               PERFORM GNS-FIO-TAB
               IF FIO-STAT-OKS AND TAB-IND-VIGE IN TAB NOT = 'N'
                   MOVE FEC-SFER TO FEC-STAT
               ELSE
                   MOVE 'FER'    TO TAB-COD-TTAB IN TAB
                   MOVE FEC-FECH TO TAB-COD-CTAB IN TAB
                   MOVE 'GNS'       TO FIO-SIST
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM GNS-FIO-TAB
                   IF FIO-STAT-OKS AND TAB-IND-VIGE IN TAB NOT = 'N'
                       MOVE FEC-SFER TO FEC-STAT
                   ELSE
                       MOVE FEC-SHBL TO FEC-STAT.
       FIN-FEC-VALD-HBL.
           EXIT.

       FEC-RTRN-JUL SECTION.
       INI-FEC-RTRN-JUL.
           IF FEC-BST1 < FEC-FANO
               SUBTRACT FEC-BST1 FROM FEC-FANO GIVING FEC-FJUL
           ELSE
               MOVE 0 TO FEC-FJUL.
           DIVIDE FEC-FJUL BY 4 GIVING FEC-FJUL REMAINDER FEC-REST.
           COMPUTE FEC-FJUL = FEC-FJUL + ( FEC-FANO - FEC-ANO1 ) * 365.
           IF FEC-REST > 0
               ADD 1 TO FEC-FJUL.
           MOVE 1 TO FEC-SIND.
       LUP-FEC-RTRN-JUL.
           IF FEC-SIND < FEC-FMES
               ADD FEC-DMES( FEC-SIND ) TO FEC-FJUL
               ADD 1 TO FEC-SIND
               GO TO LUP-FEC-RTRN-JUL.
           ADD FEC-FDIA TO FEC-FJUL.
           DIVIDE FEC-FANO BY 4 GIVING FEC-REST REMAINDER FEC-REST.
           IF FEC-REST NOT = 0 AND FEC-FMES > 2
               SUBTRACT 1 FROM FEC-FJUL.
       FIN-FEC-RTRN-JUL.
           EXIT.

       FEC-RTRN-FEC SECTION.
       INI-FEC-RTRN-FEC.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE SPACES       TO FEC-FECH.
           DIVIDE FEC-FJUL BY 365 GIVING FEC-NANO REMAINDER FEC-NDIA.
           COMPUTE FEC-NABI = FEC-NANO - ( FEC-BST1 - FEC-ANO1 ) + 3.
           DIVIDE FEC-NABI BY 4 GIVING FEC-NABI REMAINDER FEC-REST.
      *    IF FEC-NABI > 24
      *        SUBTRACT 1 FROM FEC-NABI.
           IF FEC-NDIA NOT < FEC-NABI
               SUBTRACT FEC-NABI FROM FEC-NDIA GIVING FEC-NDIA
           ELSE
               SUBTRACT 1 FROM FEC-NANO
               IF FEC-NANO = 0
                   MOVE 0 TO FEC-NABI
                   IF FEC-REST = 0
                       COMPUTE FEC-NDIA = 366 + FEC-NDIA
                   ELSE
                       COMPUTE FEC-NDIA = 365 + FEC-NDIA
               ELSE
                   IF FEC-REST = 0
                       COMPUTE FEC-NDIA = 366 - ( FEC-NABI - FEC-NDIA )
                   ELSE
                       COMPUTE FEC-NDIA = 365 - ( FEC-NABI - FEC-NDIA ).
           ADD FEC-ANO1 FEC-NANO GIVING FEC-FANO.
           IF FEC-NDIA = 0
               SUBTRACT 1 FROM FEC-FANO
               MOVE 31       TO FEC-ITM1
               MOVE 12       TO FEC-ITM2
               MOVE FEC-SVLD TO FEC-ITM3
               MOVE FEC-AVLD TO FEC-ITM4
           ELSE
               MOVE FEC-SVLD TO FEC-ITM3
               MOVE FEC-AVLD TO FEC-ITM4
               DIVIDE FEC-FANO BY 4 GIVING FEC-REST REMAINDER FEC-REST
               MOVE 0 TO FEC-AUXI
               PERFORM FEC-OBT-MES VARYING FEC-ITM2 FROM 1 BY 1
                       UNTIL FEC-ITM2 > 12 OR FEC-AUXI = 1
      *JSS    IF FEC-ITM2 < 12
      *            SUBTRACT 1 FROM FEC-ITM2
      *        ELSE
      *            MOVE 12 TO FEC-ITM2.
              IF FEC-ITM2 > 1
                  SUBTRACT 1 FROM FEC-ITM2.
       FIN-FEC-RTRN-FEC.
           EXIT.

       FEC-OBT-MES SECTION.
       INI-FEC-OBT-MES.
           MOVE FEC-DACU( FEC-ITM2 ) TO FEC-FANO.
           IF FEC-REST NOT = 0 AND FEC-ITM2 > 1
               SUBTRACT 1 FROM FEC-FANO.
           IF FEC-NDIA NOT > FEC-FANO
               MOVE 1 TO FEC-AUXI
               IF FEC-ITM2 > 1
                   SUBTRACT 1 FROM FEC-ITM2 GIVING FEC-SIND
                   SUBTRACT FEC-DACU( FEC-SIND ) FROM FEC-NDIA
                            GIVING FEC-ITM1
                   IF FEC-REST NOT = 0 AND FEC-ITM2 > 2
                       ADD 1 TO FEC-ITM1
                   ELSE
                       NEXT SENTENCE
               ELSE
                   MOVE FEC-NDIA TO FEC-ITM1.
       FIN-FEC-OBT-MES.
           EXIT.

       FEC-SIGU-HBL SECTION.
       INI-FEC-SIGU-HBL.
           PERFORM FEC-RTRN-JUL.
       LUP-FEC-SIGU-HBL.
           ADD 1 TO FEC-FJUL.
           PERFORM FEC-RTRN-FEC.
           MOVE FEC-ITM1 TO FEC-DVLD.
           MOVE FEC-ITM2 TO FEC-MVLD.
           MOVE FEC-ITM3 TO FEC-SVLD.
           MOVE FEC-ITM4 TO FEC-AVLD.
           PERFORM FEC-VALD-HBL.
           IF FEC-STAT = FEC-SFER
               GO TO LUP-FEC-SIGU-HBL.
       FIN-FEC-SIGU-HBL.
           EXIT.

       FEC-SUMA-DIA SECTION.
       INI-FEC-SUMA-DIA.
           PERFORM FEC-RTRN-JUL.
           ADD FEC-NDIA TO FEC-FJUL.
           PERFORM FEC-RTRN-FEC.
       FIN-FEC-SUMA-DIA.
           EXIT.

       FEC-SUMA-MES SECTION.
       INI-FEC-SUMA-MES.
           DIVIDE FEC-NMES BY 12 GIVING FEC-NANO REMAINDER FEC-REST.
           MOVE FEC-REST TO FEC-NMES.
           ADD FEC-NANO TO FEC-FANO.
           ADD FEC-REST TO FEC-FMES.
           IF FEC-FMES > 12
               ADD 1 TO FEC-FANO
               SUBTRACT 12 FROM FEC-FMES.
           IF FEC-FDIA NOT < FEC-DMES(FEC-FMES)
               MOVE FEC-DMES(FEC-FMES) TO FEC-FDIA
               IF FEC-FMES = 2
                   DIVIDE FEC-FANO BY 4 GIVING FEC-REST
                                        REMAINDER FEC-REST
                   IF FEC-REST NOT = 0
                       SUBTRACT 1 FROM FEC-FDIA.
           MOVE FEC-DVLD TO FEC-ITM1.
           MOVE FEC-MVLD TO FEC-ITM2.
           MOVE FEC-SVLD TO FEC-ITM3.
           MOVE FEC-AVLD TO FEC-ITM4.
       FIN-FEC-SUMA-MES.
           EXIT.

       FEC-SUMA-ANO SECTION.
       INI-FEC-SUMA-ANO.
           ADD FEC-NANO TO FEC-FANO.
           MOVE FEC-DVLD TO FEC-ITM1.
           MOVE FEC-MVLD TO FEC-ITM2.
           MOVE FEC-SVLD TO FEC-ITM3.
           MOVE FEC-AVLD TO FEC-ITM4.
       FIN-FEC-SUMA-ANO.
           EXIT.

       FEC-REST-DIA SECTION.
       INI-FEC-REST-DIA.
           PERFORM FEC-RTRN-JUL.
           SUBTRACT FEC-NDIA FROM FEC-FJUL.
           PERFORM FEC-RTRN-FEC.
       FIN-FEC-REST-DIA.
           EXIT.

       FEC-REST-MES SECTION.
       INI-FEC-REST-MES.
           DIVIDE FEC-NMES BY 12 GIVING FEC-NANO REMAINDER FEC-NMES.
           SUBTRACT FEC-NANO FROM FEC-FANO.
           IF FEC-NMES < FEC-FMES
               SUBTRACT FEC-NMES FROM FEC-FMES
           ELSE
               SUBTRACT 1        FROM FEC-FANO
               SUBTRACT FEC-FMES FROM FEC-NMES
               SUBTRACT FEC-NMES FROM 12 GIVING FEC-FMES.
           IF FEC-FDIA > FEC-DMES(FEC-FMES)
               MOVE FEC-DMES(FEC-FMES) TO FEC-FDIA.
           IF FEC-FDIA = FEC-DMES(2) AND FEC-FMES = 2
               DIVIDE FEC-FANO BY 4 GIVING FEC-REST REMAINDER FEC-REST
               IF FEC-REST NOT = 0
                   SUBTRACT 1 FROM FEC-FDIA.
           MOVE FEC-DVLD TO FEC-ITM1.
           MOVE FEC-MVLD TO FEC-ITM2.
           MOVE FEC-SVLD TO FEC-ITM3.
           MOVE FEC-AVLD TO FEC-ITM4.
       FIN-FEC-REST-MES.
           EXIT.

       FEC-REST-ANO SECTION.
       INI-FEC-REST-ANO.
           SUBTRACT FEC-NANO FROM FEC-FANO.
           IF FEC-FDIA = FEC-DMES(2) AND FEC-FMES = 2
               DIVIDE FEC-FANO BY 4 GIVING FEC-REST REMAINDER FEC-REST
               IF FEC-REST NOT = 0
                   SUBTRACT 1 FROM FEC-FDIA.
           MOVE FEC-DVLD TO FEC-ITM1.
           MOVE FEC-MVLD TO FEC-ITM2.
           MOVE FEC-SVLD TO FEC-ITM3.
           MOVE FEC-AVLD TO FEC-ITM4.
       FIN-FEC-REST-ANO.
           EXIT.

       FEC-DIFF-DIA SECTION.
       INI-FEC-DIFF-DIA.
           PERFORM FEC-CMP-F12.
           IF MSG-COD-MENS > SPACES
               GO TO FIN-FEC-DIFF-DIA.

           MOVE FEC-DEC1 TO FEC-DVLD.
           MOVE FEC-MEC1 TO FEC-MVLD.
           MOVE FEC-SEC1 TO FEC-SVLD.
           MOVE FEC-AEC1 TO FEC-AVLD.
           PERFORM FEC-RTRN-JUL.
           MOVE FEC-FJUL TO FEC-XJUL.

           MOVE FEC-DEC2 TO FEC-DVLD.
           MOVE FEC-MEC2 TO FEC-MVLD.
           MOVE FEC-SEC2 TO FEC-SVLD.
           MOVE FEC-AEC2 TO FEC-AVLD.
           PERFORM FEC-RTRN-JUL.

           IF FEC-FJUL NOT < FEC-XJUL
               SUBTRACT FEC-XJUL FROM FEC-FJUL GIVING FEC-NDIA
           ELSE
               SUBTRACT FEC-FJUL FROM FEC-XJUL GIVING FEC-NDIA.
       FIN-FEC-DIFF-DIA.
           EXIT.

       FEC-DIFF-MES SECTION.
       INI-FEC-DIFF-MES.
           PERFORM FEC-CMP-F12.
           IF MSG-COD-MENS > SPACES
               GO TO FIN-FEC-DIFF-MES.
           MOVE 0 TO FEC-NDIA.
           IF FEC-MEC1 = 2
               DIVIDE FEC-AEC1 BY 4 GIVING FEC-REST REMAINDER FEC-REST
               IF FEC-REST = 0
                   MOVE FEC-DMES(2) TO FEC-SIND
               ELSE
                   SUBTRACT 1 FROM FEC-DMES(2) GIVING FEC-SIND
           ELSE
               MOVE FEC-DMES(FEC-MEC1) TO FEC-SIND.
           IF FEC-MEC2 = 2
               DIVIDE FEC-AEC2 BY 4 GIVING FEC-REST REMAINDER FEC-REST
               IF FEC-REST = 0
                   MOVE FEC-DMES(2) TO FEC-REST
               ELSE
                   SUBTRACT 1 FROM FEC-DMES(2) GIVING FEC-REST
           ELSE
               MOVE FEC-DMES(FEC-MEC2) TO FEC-REST.

           MOVE 0 TO FEC-NMES.
           SUBTRACT FEC-FSA1 FROM FEC-FSA2 GIVING FEC-NANO.
           IF FEC-NANO > 0
               IF ( FEC-MEC2 < FEC-MEC1 ) OR
                  ( FEC-MEC2 = FEC-MEC1 AND FEC-DEC2 < FEC-DEC1 AND
                    ( FEC-DEC1 NOT = FEC-SIND OR
                      FEC-DEC2 NOT = FEC-REST ) )
                   SUBTRACT 1 FROM FEC-NANO GIVING FEC-NANO.
           IF FEC-MEC2 = FEC-MEC1 AND FEC-DEC1 = FEC-DEC2
               GO TO CPU-FEC-DIFF-MES.

           IF FEC-MEC2 < FEC-MEC1
               SUBTRACT FEC-MEC1 FROM 12 GIVING FEC-NMES
               ADD FEC-MEC2 TO FEC-NMES
           ELSE
           IF FEC-MEC2 > FEC-MEC1
               SUBTRACT FEC-MEC1 FROM FEC-MEC2 GIVING FEC-NMES
           ELSE
               IF FEC-DEC2 NOT < FEC-DEC1
                   MOVE 0 TO FEC-NMES
               ELSE
                   IF FEC-FSA1 NOT = FEC-FSA2
                       SUBTRACT FEC-MEC1 FROM 12 GIVING FEC-NMES
                       ADD FEC-MEC2 TO FEC-NMES
                   ELSE
                       MOVE 0 TO FEC-NMES.

       CPU-FEC-DIFF-MES.
           COMPUTE FEC-NMES = FEC-NMES + ( FEC-NANO * 12 ).
           IF FEC-DEC2 < FEC-DEC1 AND FEC-NMES > 0 AND
      *       ( FEC-DEC1 NOT = FEC-SIND OR FEC-DEC2 NOT = FEC-REST )
              ( FEC-DEC2 NOT = FEC-REST )
               SUBTRACT 1 FROM FEC-NMES.
           IF ( FEC-DEC1 = FEC-SIND AND FEC-DEC2 = FEC-REST ) OR
              ( FEC-DEC2 < FEC-DEC1 AND FEC-DEC2 = FEC-REST )
               MOVE 0 TO FEC-NDIA
               GO TO FIN-FEC-DIFF-MES.
           IF FEC-DEC1 < FEC-DEC2
               SUBTRACT FEC-DEC1 FROM FEC-DEC2 GIVING FEC-NDIA
           ELSE
           IF FEC-DEC1 > FEC-DEC2
               SUBTRACT FEC-DEC1 FROM FEC-SIND GIVING FEC-NDIA
               ADD FEC-DEC2 TO FEC-NDIA.
       FIN-FEC-DIFF-MES.
           EXIT.

       FEC-DIFF-ANO SECTION.
       INI-FEC-DIFF-ANO.
           PERFORM FEC-CMP-F12.
           IF MSG-COD-MENS > SPACES
               GO TO FIN-FEC-DIFF-ANO.
           SUBTRACT FEC-FSA1 FROM FEC-FSA2 GIVING FEC-NANO.
           IF FEC-NANO > 0
               IF ( FEC-MEC2 < FEC-MEC1 ) OR
                  ( FEC-MEC2 = FEC-MEC1 AND FEC-DEC2 < FEC-DEC1 )
                   SUBTRACT 1 FROM FEC-NANO  GIVING FEC-NANO.
       FIN-FEC-DIFF-ANO.
           EXIT.

       FEC-CMP-F12 SECTION.
       INI-FEC-CMP-F12.
           MOVE FEC-FEC1 TO FEC-FECH.
           PERFORM FEC-EXT-SLSH.
           PERFORM FEC-TRDC-FEC.
           MOVE FEC-DVLD TO FEC-DEC1.
           MOVE FEC-MVLD TO FEC-MEC1.
           MOVE FEC-SVLD TO FEC-SEC1.
           MOVE FEC-AVLD TO FEC-AEC1.

           MOVE FEC-FEC2 TO FEC-FECH.
           PERFORM FEC-EXT-SLSH.
           PERFORM FEC-TRDC-FEC.
           MOVE FEC-DVLD TO FEC-DEC2.
           MOVE FEC-MVLD TO FEC-MEC2.
           MOVE FEC-SVLD TO FEC-SEC2.
           MOVE FEC-AVLD TO FEC-AEC2.

           IF ( FEC-SEC1 > FEC-SEC2 )                         OR
              ( FEC-SEC1 = FEC-SEC2 AND FEC-AEC1 > FEC-AEC2 ) OR
              ( FEC-SEC1 = FEC-SEC2 AND FEC-AEC1 = FEC-AEC2 AND
                FEC-MEC1 > FEC-MEC2 )                         OR
              ( FEC-SEC1 = FEC-SEC2 AND FEC-AEC1 = FEC-AEC2 AND
                FEC-MEC1 = FEC-MEC2 AND FEC-DEC1 > FEC-DEC2 )
               MOVE '99' TO FEC-STAT
               MOVE 'FECFEC1FEC2>' TO MSG-COD-MENS.
       FIN-FEC-CMP-F12.
           EXIT.

       FEC-EXT-SLSH SECTION.
       INI-FEC-EXT-SLSH.
           MOVE FEC-FECH TO FEC-FSLH.
           IF FEC-FORM = FEC-FORM-FEC OR FEC-FORM-DMA
      *        IF FEC-FSLH-SL1 = FEC-IEDT AND FEC-FSLH-SL2 = FEC-IEDT
               IF FEC-FSLH-SL1 = '/' AND FEC-FSLH-SL2 = '/'
                   MOVE SPACES       TO FEC-FECH
                   MOVE FEC-FSLH-IT1 TO FEC-ITM1
                   MOVE FEC-FSLH-IT2 TO FEC-ITM2
                   MOVE FEC-FSLH-IT3 TO FEC-ITM3
                   MOVE FEC-FSLH-IT4 TO FEC-ITM4
               ELSE
                   NEXT SENTENCE
           ELSE
           IF FEC-FORM = FEC-FORM-MSA OR FEC-FORM-FMA
      *        IF FEC-FSLH-SL1 = FEC-IEDT
               IF FEC-FSLH-SL1 = '/'
                   MOVE SPACES       TO FEC-FECH
                   MOVE FEC-FSLH-IT1 TO FEC-ITM1
                   MOVE FEC-FSLH-IT2 TO FEC-ITM2
                   MOVE FEC-FSLH-IT5 TO FEC-ITM3
                   MOVE FEC-FSLH-IT6 TO FEC-ITM4
               ELSE
                   NEXT SENTENCE.
       FIN-FEC-EXT-SLSH.
           EXIT.

       FEC-PUT-SLSH SECTION.
       INI-FEC-PUT-SLSH.
           MOVE SPACES TO FEC-FSLH.
           IF FEC-FORM = FEC-FORM-FEC OR FEC-FORM-DMA
               MOVE FEC-ITM1 TO FEC-FSLH-IT1
      *        MOVE FEC-IEDT TO FEC-FSLH-SL1
               MOVE '/'      TO FEC-FSLH-SL1
               MOVE FEC-ITM2 TO FEC-FSLH-IT2
      *        MOVE FEC-IEDT TO FEC-FSLH-SL2
               MOVE '/'      TO FEC-FSLH-SL2
               MOVE FEC-ITM3 TO FEC-FSLH-IT3
               MOVE FEC-ITM4 TO FEC-FSLH-IT4
               MOVE FEC-FSLH TO FEC-FECH
           ELSE
           IF FEC-FORM = FEC-FORM-MSA OR FEC-FORM-FMA
               MOVE FEC-ITM1 TO FEC-FSLH-IT1
      *        MOVE FEC-IEDT TO FEC-FSLH-SL1
               MOVE '/'      TO FEC-FSLH-SL1
               MOVE FEC-ITM2 TO FEC-FSLH-IT5
               MOVE FEC-ITM3 TO FEC-FSLH-IT6
               MOVE FEC-FSLH TO FEC-FECH.
           MOVE SPACES TO FEC-IEDT.
       FIN-FEC-PUT-SLSH.
           EXIT.

       PRG-ABT SECTION.
       INI-PRG-ABT.
           EXEC CICS SEND
                     CONTROL ERASE
           END-EXEC.
           EXEC CICS SEND
                     FROM(FIO-MENS)
                     LENGTH(79)
           END-EXEC.
           EXEC CICS RETURN
           END-EXEC.
           GOBACK.
       FIN-PRG-ABT.
           EXIT.

       COPY GNSBFTAB.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBGDTC.
       COPY GNSBGHOY.
       COPY GNSBGSYS.
