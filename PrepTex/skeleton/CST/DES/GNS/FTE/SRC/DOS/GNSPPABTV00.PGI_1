       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. GNSPPABT.
       AUTHOR.     CONSIST.
       DATE-WRITTEN. 06-SEP-1989 13:43.

      * Rutina de Aborto.

       ENVIRONMENT DIVISION.
      *=====================

       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWGFRM.
       01  WSS-VARI.
           03 WSS-QUEU.
              05 WSS-TERM                        PIC X(04).
              05 WSS-OPID                        PIC X(03).
      *
           03 WSS-MENS                           PIC X(79).
           03 WSS-MENS-RED1 REDEFINES WSS-MENS.
              05 WSS-MCLO                        PIC X(15).
                 88 WSS-MCLO-YES VALUE 'ARCHIVO CERRADO'.
              05 FILLER                          PIC X(56).
              05 WSS-MSIS                        PIC X(03).
              05 WSS-MFIL                        PIC X(04).
                 88 WSS-MMNU-YES VALUE 'MMNU'.
                 88 WSS-MMN1-YES VALUE 'MMN1'.
                 88 WSS-MMN2-YES VALUE 'MMN2'.
                 88 WSS-MRPF-YES VALUE 'MRPF'.
           03 WSS-MENS-RED2 REDEFINES WSS-MENS.
              05 WSS-MXCT                        PIC X(37).
                 88 WSS-MXCT-YES VALUE
                    'ABORTO : ERROR EN XCONTROL A PROGRAMA'.
           03 WSS-MENS-RED3 REDEFINES WSS-MENS.
              05 WSS-MADT                        PIC X(13).
                 88 WSS-MADT-YES VALUE 'Aborto, DTC:'.
              05 FILLER                          PIC X(01).
              05 WSS-MSTT                        PIC X(02).
                 88 WSS-MS36-YES VALUE '36'.
                 
       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                             PIC X(79).

       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
       INI-MAIN.
      *    PERFORM CIC-ERR THRU FIN-CIC-ERR.

           IF EIBCALEN = 0
               GO TO FIN-MAIN.
           MOVE DFHCOMMAREA TO FRM-MENS.
           MOVE FRM-MENS    TO WSS-MENS.

           EXEC CICS HANDLE CONDITION
                             QIDERR (QUE-ERR)
           END-EXEC.

           MOVE EIBTRMID TO WSS-TERM.
           EXEC CICS ASSIGN
                     OPID(WSS-OPID)
           END-EXEC.

           EXEC CICS DELETEQ TS
                     QUEUE(WSS-QUEU)
           END-EXEC.

       QUE-ERR.

           EXEC CICS SEND
                     CONTROL ERASE
           END-EXEC.
           EXEC CICS SEND
                     FROM(FRM-MENS)
                     LENGTH(79)
                     ERASE
           END-EXEC.
      *
           IF WSS-MMNU-YES OR WSS-MMN1-YES OR WSS-MMN2-YES OR
              WSS-MRPF-YES OR WSS-MXCT-YES OR
              ( WSS-MADT-YES AND WSS-MS36-YES )
               GO TO FIN-MAIN.
      *
           MOVE 160 TO FRM-PCUR.
           EXEC CICS SEND
                     CONTROL CURSOR(FRM-PCUR)
           END-EXEC.
           MOVE '...ESPERE...' TO FRM-MENS.
           EXEC CICS SEND
                     FROM(FRM-MENS)
                     LENGTH(79)
           END-EXEC.
           EXEC CICS ABEND
                     ABCODE('ERRC')
           END-EXEC.
       FIN-MAIN.
           EXEC CICS SEND
                     CONTROL FREEKB
           END-EXEC.
           EXEC CICS RETURN END-EXEC.
       EXT-MAIN.
           EXIT.

      *COPY GNSBECIC.
      *COPY GNSBGFRM.
