       IDENTIFICATION DIVISION.
      *=======================*
       PROGRAM-ID.      GNSPXCCS.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.  26-FEB-1997 13:05:20.
      *
      * Programa para indexar registro secuencial (GNSMVAR) emitido por 
      * aplicacion Visual Basic, quedando en archivo GNSMCCS.MAE. luego
      * se queda en un LOOP esperando que un programa denominado TRADUCTOR
      * grabe en el mismo archivo GNSMCCS.MAE una respuesta, para luego
      * secuencializarla en un archivo denominado GNSMRAV para ser leida por
      * la aplizacion Visual Basic.
      *
       ENVIRONMENT DIVISION.
      *====================*
       CONFIGURATION SECTION.
      *---------------------*
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------*
      *
       COPY GNSBRCCS.
       COPY GNSBRVAR.
       COPY GNSBRRAV.
      *
      *
      * INI VARIABLES PARA ENTORNO PC-DOS
       01  PCDOS-STR-NAM             VALUE SPACES            PIC X(07).
       01  PCDOS-STR-VAL             VALUE SPACES            PIC X(07).
       01  PCDOS-STR-VAL-LEN         COMP-5                  PIC S9(4).
      * FIN VARIABLES PARA ENTORNO PC-DOS
       01  PCDOS-STR-VAL-NUM                                 PIC 9(07).
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.

       01  WSS-VARI.
           03 WSS-TOPE VALUE      1000    PIC 9(07).
           03 WSS-CONTADOR                PIC 9(07). 

           03 WSS-TOT-LECC-CCS VALUE 100000   PIC 9(07).
           03 WSS-IDX-LECC-CCS                PIC 9(07). 
           03 WSS-TOT-LEC1-CCS VALUE 1000000  PIC 9(07).
           03 WSS-IDX-LEC1-CCS                PIC 9(07). 

           03 WSS-IND-DS01                    PIC X(01).


       PROCEDURE DIVISION.
      *==================*
       MAIN SECTION.
       INI-MAIN.
           PERFORM OPEN-FIL.
           PERFORM PRO-VAR.
           IF NOT FIO-STAT-OKS
              GO TO CON-MAIN.
           PERFORM PRO-RPS-CCS.
       CON-MAIN.
           PERFORM CLOS-FIL.
       FIN-MAIN.
           STOP RUN.

       OPEN-FIL SECTION.
       INI-OPEN-FIL.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-VAR.
           IF NOT FIO-STAT-OKS
               DISPLAY 'GNSPXCCS: ERROR AL ABRIR  GNSMVAR.PAR: ST: '
               FIO-STAT.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GNS-FIO-RAV.
           IF NOT FIO-STAT-OKS
               DISPLAY 'GNSPXCCS: ERROR AL ABRIR  GNSMRAV.PAR: ST: '
               FIO-STAT.
       FIN-OPEN-FIL.
           EXIT.

       CLOS-FIL SECTION.
       INI-CLOS-FIL.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-VAR.
           IF NOT FIO-STAT-OKS
               DISPLAY 'GNSPXCCS: ERROR AL CERRAR GNSMVAR.PAR: ST: '
               FIO-STAT.
           PERFORM GNS-FIO-RAV.
           IF NOT FIO-STAT-OKS
               DISPLAY 'GNSPXCCS: ERROR AL CERRAR GNSMRAV.PAR: ST: '
               FIO-STAT.

       FIN-CLOS-FIL.
           EXIT.

       PRO-VAR SECTION.
       INI-PRO-VAR.
           PERFORM GET-FST-VAR.
           IF NOT FIO-STAT-OKS
              DISPLAY 'GNSPXCCS: ERROR AL LEER ARCHIVO GNSMVAR FS :'
                      FIO-STAT
              GO TO FIN-PRO-VAR.
       LUP-PRO-VAR.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GNS-FIO-CCS.
           IF NOT FIO-STAT-OKS
              DISPLAY 'GNSPXCCS: NO PUEDO ABRIR GNSMCCS: ST: '
                      FIO-STAT
              PERFORM PRO-WAIT
              GO TO LUP-PRO-VAR.

           MOVE 'TERM'           TO CCS-COD-TERM IN CCS.
           MOVE 1                TO CCS-NUM-CORR IN CCS.
           MOVE FIO-GET-KEY-UPD  TO FIO-CMND.
           PERFORM GNS-FIO-CCS.
           IF NOT FIO-STAT-OKS
              MOVE FIO-PUT TO FIO-CMND
           ELSE
              MOVE FIO-MOD TO FIO-CMND.

           MOVE '1'                 TO CCS-IND-COMU IN CCS.
           MOVE VAR-GLS-DISP IN VAR TO CCS-GLS-MENS IN CCS.
           PERFORM GNS-FIO-CCS.
           IF NOT FIO-STAT-OKS
              DISPLAY 'GNSPXCCS: ERROR AL GRABAR REG.CCS'
           ELSE
              DISPLAY 'GNSPXCCS: PROCESANDO APLICACIONES HOST...'.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-CCS.

       FIN-PRO-VAR.
           EXIT.

       GET-FST-VAR SECTION.
       INI-GET-FST-VAR.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GNS-FIO-VAR.
       FIN-GET-FST-VAR.
           EXIT.

      * PRocesa ResPueSta CCS
       PRO-RPS-CCS SECTION.
       INI-PRO-RPS-CCS.
           PERFORM GET-PRM-DS01.
           PERFORM WAIT-LEC1-CCS.
       CIC-PRO-RPS-CCS.
           PERFORM WAIT-LECC-CCS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-CCS.
           IF NOT FIO-STAT-OKS
              GO TO CIC-PRO-RPS-CCS.

           MOVE 'TERM'           TO CCS-COD-TERM IN CCS.
           MOVE 1                TO CCS-NUM-CORR IN CCS.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-CCS.
           IF NOT FIO-STAT-OKS
              MOVE FIO-CLO TO FIO-CMND
              PERFORM GNS-FIO-CCS
              GO TO CIC-PRO-RPS-CCS.

           IF CCS-IND-COMU = '1'
              MOVE FIO-CLO TO FIO-CMND
              PERFORM GNS-FIO-CCS
              GO TO CIC-PRO-RPS-CCS.

           IF CCS-IND-COMU = 'X'
              MOVE FIO-CLO TO FIO-CMND
              PERFORM GNS-FIO-CCS
              STOP RUN.

      *     DISPLAY 'RECIBIENDO CONTROL DEL HOST....'.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-CCS.
           MOVE CCS-GLS-MENS IN CCS TO RAV-GLS-DISP IN RAV.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GNS-FIO-RAV.
       FIN-PRO-RPS-CCS.
           EXIT.
      *
      * WAIT LECtura Ciclo CCS
       WAIT-LECC-CCS SECTION.
       INI-WAIT-LECC-CCS.
           MOVE ZEROES TO WSS-IDX-LECC-CCS IN WSS-VARI.

      * MTX retardo de LEctura Ccs en Ciclo
           MOVE 'MTXLECC' TO PCDOS-STR-NAM.
           MOVE SPACES    TO PCDOS-STR-VAL.
           MOVE +8 TO PCDOS-STR-VAL-LEN.
           CALL 'DOS_GET_ENV_STRING' USING PCDOS-STR-NAM PCDOS-STR-VAL
                PCDOS-STR-VAL-LEN.

           IF PCDOS-STR-VAL IS NUMERIC
               MOVE PCDOS-STR-VAL TO PCDOS-STR-VAL-NUM
               IF PCDOS-STR-VAL-NUM NOT = ZEROES
                   MOVE PCDOS-STR-VAL-NUM TO WSS-TOT-LECC-CCS.

           IF WSS-IND-DS01 = 'S'
                DISPLAY 'GNSPXCCS: INTERVALO LECC : '
                        WSS-TOT-LECC-CCS.
       CIC-WAIT-LECC-CCS.
           ADD 1 TO WSS-IDX-LECC-CCS IN WSS-VARI.
           IF WSS-IDX-LECC-CCS < WSS-TOT-LECC-CCS IN WSS-VARI
              GO TO CIC-WAIT-LECC-CCS.
       FIN-WAIT-LECC-CCS.
           EXIT.

      *
      * WAIT LECtura nro 1 CCS
       WAIT-LEC1-CCS SECTION.
       INI-WAIT-LEC1-CCS.
           MOVE ZEROES TO WSS-IDX-LEC1-CCS IN WSS-VARI.

      * MTX retardo de LEctura Ccs en Ciclo
           MOVE 'MTXLEC1' TO PCDOS-STR-NAM.
           MOVE SPACES    TO PCDOS-STR-VAL.
           MOVE +8 TO PCDOS-STR-VAL-LEN.
           CALL 'DOS_GET_ENV_STRING' USING PCDOS-STR-NAM PCDOS-STR-VAL
                PCDOS-STR-VAL-LEN.

           IF PCDOS-STR-VAL IS NUMERIC
               MOVE PCDOS-STR-VAL TO PCDOS-STR-VAL-NUM
               IF PCDOS-STR-VAL-NUM NOT = ZEROES
                   MOVE PCDOS-STR-VAL-NUM TO WSS-TOT-LEC1-CCS.

           IF WSS-IND-DS01 = 'S'
               DISPLAY 'GNSPXCCS: INTERVALO LEC1 : '
                       WSS-TOT-LEC1-CCS.
       CIC-WAIT-LEC1-CCS.
           ADD 1 TO WSS-IDX-LEC1-CCS IN WSS-VARI.
           IF WSS-IDX-LEC1-CCS < WSS-TOT-LEC1-CCS IN WSS-VARI
              GO TO CIC-WAIT-LEC1-CCS.
       FIN-WAIT-LEC1-CCS.
           EXIT.

      *
      * GET PaRaMetro de DiSPlay para programa 01
       GET-PRM-DS01 SECTION.
       INI-GET-PRM-DS01.
           MOVE 'MTXDS01' TO PCDOS-STR-NAM.
           MOVE SPACES    TO PCDOS-STR-VAL.
           MOVE +8 TO PCDOS-STR-VAL-LEN.
           CALL 'DOS_GET_ENV_STRING' USING PCDOS-STR-NAM PCDOS-STR-VAL
                PCDOS-STR-VAL-LEN.

           MOVE PCDOS-STR-VAL TO WSS-IND-DS01.
       FIN-GET-PRM-DS01.
           EXIT.

       PRO-WAIT SECTION.
       INI-PRO-WAIT.
           MOVE ZEROES TO WSS-CONTADOR IN WSS-VARI.
       CON-PRO-WAIT.
           ADD 1 TO WSS-CONTADOR IN WSS-VARI.
           IF WSS-CONTADOR < WSS-TOPE IN WSS-VARI
              GO TO CON-PRO-WAIT.
       FIN-PRO-WAIT.
           EXIT.
      *
      *
       COPY GNSBFCCS.
       COPY GNSBFVAR.
       COPY GNSBFRAV.
      *
       COPY GNSBBSYS.
