//JUSEPUL JOB ,,CLASS=A,MSGCLASS=X,USER=JUSEPUL,PASSWORD=JSPJUN,
//            REGION=4M
/*ROUTE PRINT MVS
//STEP1  EXEC PGM=AFOLIBR
//STEPLIB  DD DSN=ADR.LIB.V38.LOAD,DISP=SHR
//MASTER   DD DSN=GNSCL.FUENTES,DISP=SHR
//OSJOB    DD DUMMY
//SYSPRINT DD SYSOUT=X
//SYSIN    DD *
-ADD GNSPPPFB,PSWD=CONS,ARC
     DUMMY
-EMOD
-SEL GNSPPPFB,CONS
-REP ALL
       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. GNSPPPFB.
       AUTHOR.     CONSIST.
       DATE-WRITTEN.  29-AUG-1991.
      * 
      * PFB - CONGELAR PARA PROGRAMAS VERSION 2.0 3.0 COMPATIBLE
      * 
      * COMPATIBLE CON NUEVA VERSION DE MENU PARA MOSTRAR EN HEADER
      * OPCION DE MENU DONDE SE CONGELO.
      * 
      * CONGELA EN COLA UNICA PKB.
      * 
      * CUANDO SE CONGELO EN 3.1 Y SE DESCONGELA EN 2.0, LLAMA A
      * DESCONGELADOR 3.1
      *
      *  27-FEB-1992 08:17:32 
      *  INHIBE CONGELAMIENTO CONTROLADO POR TABLA
      *
       ENVIRONMENT DIVISION.
      *=====================
       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWGSCR.
       COPY GNSWGFRM.
       COPY GNSWGTSC.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
      *
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRTAB.
       COPY GNSBRIDD.
       01  IFD.
           03 FILLER                            PIC X(01).
           03 IFD-COD-CIFD                      PIC X(12).
           03 IFD-GLS-VSIS OCCURS 30.
              05 IFD-COD-DSIS                   PIC X(04).
              05 IFD-COD-GSIS                   PIC X(03).
           03 FILLER                            PIC X(18).
      *

      *JSS-PEN
       COPY GNSWGQUE.
       01  WSS-VARI.
           03 WSS-I             VALUE 0                PIC 9(03).
           03 WSS-COD-CTAB.
              05 WSS-CGL-TRAN                         PIC X(04).
              05 WSS-CGL-CMND                         PIC X(03).
              05 WSS-CGL-INST                         PIC X(03).
      *       Tamano del mapa
           03 WSS-TFLD  COMP      VALUE +0             PIC S9(04).
      *       Posicion del cursor
           03 WSS-PCUR  COMP      VALUE +0             PIC S9(04).
      *       Mapa
           03 WSS-DFRM                                 PIC X(3000).
      *       Common Area auxiliar
           03 WSS-CMMA                                 PIC X(3500).
      * Variables para Manejo de Usuarios y privilegios.
           COPY USPC113.

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                             PIC X(3500).
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           PERFORM CIC-ERR THRU FIN-CIC-ERR.
           MOVE DFHCOMMAREA TO SCR-VARI WSS-CMMA.

           PERFORM GNS-ERR-QUE.
           PERFORM GNS-ERR-QUE.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'PKB'    TO QUE-TYPE.
           MOVE 1        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               PERFORM PUT-FPEN
               MOVE 'ABORTO : PROGRAMA GNSPPPFB, NO PUDO CONGELAR' TO
                                                             FRM-MENS
           ELSE
               PERFORM RET-FPEN
               MOVE 'ABORTO : PROGRAMA GNSPPPFB, NO PUDO RESTAURAR' TO
                                                              FRM-MENS.
           MOVE FRM-ABT TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
           GOBACK.
       FIN-MAIN.
           EXIT.

       PUT-FPEN SECTION.
       INI-PUT-FPEN.
      *
      *Buscar TS que usa procesador de MENU
           PERFORM GNS-GET-TSC.
      *
      *Buscar idd con codigo TSC-CIDD.
           PERFORM GNS-GET-IDD.
      *
           IF SCR-SISG > SPACES
               MOVE SCR-SISG TO FIO-SIST
           ELSE
               MOVE SCR-SIST TO FIO-SIST.
           MOVE 'CGL' TO TAB-COD-TTAB IN TAB.
           MOVE EIBTRNID TO WSS-CGL-TRAN.
           MOVE SCR-CMND TO WSS-CGL-CMND.
           MOVE SCR-INST TO WSS-CGL-INST.
           MOVE WSS-COD-CTAB TO TAB-COD-CTAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS AND
              TAB-IND-VIGE IN TAB = 'S'
                EXEC CICS RETURN
                  TRANSID(EIBTRNID)
                  COMMAREA(WSS-CMMA)
                  LENGTH(SCR-TCMA)
                END-EXEC.

           EXEC CICS HANDLE CONDITION LENGERR(CON-PUT-FPEN)
                                      NOSPACE(CON-PUT-FPEN)
           END-EXEC.
           MOVE +3000 TO WSS-TFLD.
           EXEC CICS RECEIVE INTO(WSS-DFRM) LENGTH(WSS-TFLD) BUFFER
           END-EXEC.
       CON-PUT-FPEN.
           PERFORM SET-TSC.

           PERFORM SET-CMA.
           MOVE QUE-PUT TO QUE-CMND.
           PERFORM GNS-PRO-QUE.

           PERFORM SET-MAP.
           MOVE QUE-PUT TO QUE-CMND.
           PERFORM GNS-PRO-QUE.

           MOVE SCR-INST-HCM TO SCR-INST.
           MOVE SCR-CMNP     TO FRM-PROG.
      *    MOVE DFHCOMMAREA  TO FRM-CMMA.
      *    MOVE EIBCALEN     TO FRM-TCMA.
           MOVE SCR-VARI     TO FRM-CMMA.
           MOVE SCR-TCMA     TO FRM-TCMA.
           MOVE FRM-XCT      TO FRM-CMND.
           MOVE 0 TO EIBAID.
           PERFORM GNS-PRO-FRM.
       FIN-PUT-FPEN.
           EXIT.

       SET-CMA SECTION.
       INI-SET-CMA.
           COMPUTE QUE-LITM = 7 + EIBCALEN.
           MOVE EIBTRMID    TO QUE-TERM.
           MOVE 'PKB'       TO QUE-TYPE.
           MOVE 'CMA'       TO QUE-IKEY.
           MOVE EIBCALEN    TO QUE-LDAT.
           MOVE DFHCOMMAREA TO QUE-DATA.
           MOVE +0          TO QUE-PCUR.
       FIN-SET-CMA.
           EXIT.

       SET-MAP SECTION.
       INI-SET-MAP.
           COMPUTE QUE-LITM = 7 + WSS-TFLD.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'PKB'    TO QUE-TYPE.
           MOVE 'MAP'    TO QUE-IKEY.
           MOVE WSS-TFLD TO QUE-LDAT.
           MOVE WSS-DFRM TO QUE-DATA.
           MOVE EIBCPOSN TO QUE-PCUR.
       FIN-SET-MAP.
           EXIT.

       SET-TSC SECTION.
       INI-SET-TSC.
           MOVE EIBTRMID TO TSC-TERM.
           MOVE TSC-COLA TO QUE-COLA.
           MOVE 1        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-SET-TSC.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'PKB'    TO QUE-TYPE.
           MOVE QUE-PUT  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
       FIN-SET-TSC.
           EXIT.

       RET-FPEN SECTION.
       INI-RET-FPEN.
      *Ya leyo primer registro de la cola para detectar si esta existia
           IF QUE-IKEY = 'V31'
               MOVE 'GNSPPPKB' TO FRM-PROG
               MOVE ' '        TO FRM-CMMA
               MOVE +1         TO FRM-TCMA
               MOVE FRM-XCT    TO FRM-CMND
               PERFORM GNS-PRO-FRM.

           PERFORM MOD-TSC.

           MOVE 2        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'PKB'    TO QUE-TYPE.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-RET-FPEN.
           IF QUE-IKEY NOT = 'CMA'
               GO TO FIN-RET-FPEN.
           MOVE QUE-DATA TO SCR-VARI WSS-CMMA.
           MOVE QUE-LDAT TO SCR-TCMA.

           MOVE 3        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'PKB'    TO QUE-TYPE.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           MOVE QUE-DATA TO WSS-DFRM.
           MOVE QUE-LDAT TO WSS-TFLD.
           MOVE QUE-PCUR TO WSS-PCUR.

           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'PKB'    TO QUE-TYPE.
           MOVE QUE-DEL  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           EXEC CICS SEND FROM(WSS-DFRM)
                          LENGTH(WSS-TFLD)
                          ERASE
           END-EXEC.
           EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
                          TERMINAL
           END-EXEC.
           PERFORM SCR-EXIT.
       FIN-RET-FPEN.
           EXIT.

       MOD-TSC SECTION.
       INI-MOD-TSC.
      *Para dejar variables de TSC cargadas
           MOVE QUE-ITEM TO TSC-ITEM.
      *
           MOVE EIBTRMID TO TSC-TERM.
           MOVE TSC-COLA TO QUE-COLA.
           MOVE 1        TO QUE-NITM.
           MOVE QUE-MOD  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT = QUE-STAT-OKS
               GO TO FIN-MOD-TSC.
           MOVE QUE-PUT TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
       FIN-MOD-TSC.
      *llamada a USP para setear perfil de SISTEMA/USUARIO
           MOVE TSC-SIST TO USP-SISTEMA.
           COPY USPC104.
       EXT-MOD-TSC.
           EXIT.

       SCR-EXIT SECTION.
       INI-SCR-EXIT.
           EXEC CICS RETURN
                     TRANSID(SCR-NTRN)
                     COMMAREA(WSS-CMMA)
                     LENGTH(SCR-TCMA)
           END-EXEC.
       FIN-SCR-EXIT.
           EXIT.


       GNS-GET-TSC SECTION.
       INI-GNS-GET-TSC.
           EXEC CICS HANDLE CONDITION
                     QIDERR (NEX-GNS-GET-TSC)
           END-EXEC.
           MOVE EIBTRMID TO TSC-TERM.
           MOVE TSC-TTSC TO TSC-LITM.
           EXEC CICS READQ TS
                     QUEUE(TSC-COLA)
                     INTO(TSC-ITEM)
                     LENGTH(TSC-LITM)
                     ITEM(1)
           END-EXEC.
           GO TO FIN-GNS-GET-TSC.
       NEX-GNS-GET-TSC.
           MOVE 'SISTEMA DEBE EJECUTARSE CON PROCESADOR MENU COMPATIBLE'
                        TO FRM-MENS.
           MOVE FRM-ABT TO FRM-CMND.
           PERFORM GNS-PRO-FRM.
       FIN-GNS-GET-TSC.
           EXIT.

       GNS-GET-IDD SECTION.
       INI-GNS-GET-IDD.
           IF ( TSC-TIND = 'C' ) OR ( TSC-TIND NOT > SPACES )
               GO TO IFD-GNS-GET-IDD.
      *La independencia de datos es IDD EN TEMPORARY STORAGE ( VERSION 3.0 )
           MOVE TSC-CIDD TO IDD-COD-CIDD IN IDD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IDD.
           IF FIO-STAT-OKS
               MOVE IDD TO IDD-REGI
               GO TO FIN-GNS-GET-IDD.
           MOVE 'DEFAULT' TO IDD-COD-CIDD IN IDD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IDD.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE REGISTRO IDD CON LLAVE : ' TO FRM-MEN1
               MOVE IDD-COD-CIDD IN IDD                   TO FRM-MEN2
               MOVE FRM-ABT TO FRM-CMND
               PERFORM GNS-PRO-FRM
           ELSE
               MOVE IDD TO IDD-REGI.
           GO TO FIN-GNS-GET-IDD.
       IFD-GNS-GET-IDD.
      *La independencia de datos es IFD EN COMMAREA ( VERSION 2.0 )
           MOVE TSC-CIDD TO IFD-COD-CIFD IN IFD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IFD.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE REGISTRO IFD CON LLAVE :' TO FRM-MEN1
               MOVE IFD-COD-CIFD IN IFD                  TO FRM-MEN2
               MOVE FRM-ABT TO FRM-CMND
               PERFORM GNS-PRO-FRM
           ELSE
               MOVE SPACES TO IFD-FSIS
               PERFORM MOV-IFD VARYING WSS-I FROM 1 BY 1 UNTIL
                                   WSS-I > 30.
       FIN-GNS-GET-IDD.
           EXIT.

       MOV-IFD SECTION.
       INI-MOV-IFD.
           MOVE IFD-COD-DSIS(WSS-I) TO IFD-DSIS(WSS-I).
           MOVE IFD-COD-GSIS(WSS-I) TO IFD-GSIS(WSS-I).
       FIN-MOV-IFD.
           EXIT.

       COPY GNSBFTAB.
       COPY GNSBFIDD.
       COPY GNSBFIFD.
       COPY GNSBGDTC.
       COPY GNSBGVSM.
       COPY GNSBIABT.
       COPY GNSBGFRM.
       COPY GNSBGQUE.
      ********************* ERR *********************
       CIC-ERR SECTION.
       INI-CIC-ERR.
      *     EXEC CICS HANDLE AID
      *               CLEAR    (CIC-ERR-ACL)
      *               PA1      (CIC-ERR-PA1)
      *               PA2      (CIC-ERR-PA2)
      *     END-EXEC.
           EXEC CICS HANDLE CONDITION
                     MAPFAIL  (CIC-ERR-MFA)
                     DSIDERR  (CIC-ERR-BFI)
                     DUPKEY   (CIC-ERR-DAK)
                     DUPREC   (CIC-ERR-CDK)
                     ENDFILE  (CIC-ERR-EOF)
                     ILLOGIC  (CIC-ERR-LGE)
                     INVREQ   (CIC-ERR-OPE)
                     IOERR    (CIC-ERR-DDE)
                     LENGERR  (CIC-ERR-IFI)
                     NOSPACE  (CIC-ERR-BVE)
                     NOTFND   (CIC-ERR-IKY)
                     NOTOPEN  (CIC-ERR-RNA)
                     PGMIDERR (CIC-ERR-PGM)
           END-EXEC.
       FIN-CIC-ERR.
           EXIT.

       CIC-ERR-ACL.
           MOVE 'GNSPPCLR' TO FRM-PROG.
           MOVE 0          TO FRM-TCMA.
           MOVE SPACES     TO FRM-CMMA.
           EXEC CICS XCTL
                     PROGRAM(FRM-PROG)
                     COMMAREA(FRM-CMMA)
                     LENGTH(FRM-TCMA)
           END-EXEC.
       CIC-ERR-PA1.
           MOVE FRM-FFLD-PA1 TO FRM-FFLD.
           GO TO CIC-ERR-FRM.
       CIC-ERR-PA2.
           MOVE FRM-FFLD-PA2 TO FRM-FFLD.
           GO TO CIC-ERR-FRM.
       CIC-ERR-BFI.
           MOVE '96'                    TO FIO-STAT.
           MOVE 'ARCHIVO NO ENCONTRADO' TO FIO-MEN1.
           MOVE FIO-VOID                TO FIO-MEN2.
           MOVE FIO-MENS                TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-DAK.
           MOVE '22'                TO FIO-STAT.
           MOVE 'DUPLICATE ALT KEY' TO FIO-MEN1.
           MOVE FIO-VOID            TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-CDK.
           MOVE '22'                 TO FIO-STAT.
           MOVE 'REGISTRO YA EXISTE' TO FIO-MEN1.
           MOVE FIO-VOID             TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-EOF.
           MOVE '10'             TO FIO-STAT.
           MOVE 'FIN DE ARCHIVO' TO FIO-MEN1.
           MOVE FIO-VOID         TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-LGE.
           MOVE '92'           TO FIO-STAT.
           MOVE 'ERROR LOGICO' TO FIO-MEN1.
           MOVE FIO-VOID       TO FIO-MEN2.
           MOVE FIO-MENS       TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-OPE.
           MOVE '30'               TO FIO-STAT.
           MOVE 'ERROR PERMANENTE' TO FIO-MEN1.
           MOVE FIO-VOID           TO FIO-MEN2.
           MOVE FIO-MENS           TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-DDE.
           MOVE '90'                TO FIO-STAT.
           MOVE 'ERROR DISPOSITIVO' TO FIO-MEN1.
           MOVE FIO-VOID            TO FIO-MEN2.
           MOVE FIO-MENS            TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-IFI.
           MOVE '95'             TO FIO-STAT.
           MOVE 'ERROR DE LARGO' TO FIO-MEN1.
           MOVE FIO-VOID         TO FIO-MEN2.
           MOVE FIO-MENS         TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-BVE.
           MOVE '24'               TO FIO-STAT.
           MOVE 'ERROR DE ESPACIO' TO FIO-MEN1.
           MOVE FIO-VOID           TO FIO-MEN2.
           MOVE FIO-MENS           TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-IKY.
           MOVE '23'          TO FIO-STAT.
           MOVE 'INVALID KEY' TO FIO-MEN1.
           MOVE FIO-VOID      TO FIO-MEN2.
           GO TO CIC-ERR-FIO.
       CIC-ERR-PGM.
           MOVE '98'               TO FIO-STAT.
           MOVE 'PROGRAM ID ERROR' TO FRM-MENS.
           GO TO CIC-ERR-FRM.
       CIC-ERR-RNA.
           MOVE '93'              TO FIO-STAT.
           MOVE 'ARCHIVO CERRADO' TO FIO-MEN1.
           MOVE FIO-VOID          TO FIO-MEN2.
           MOVE FIO-MENS          TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-MFA.
           MOVE 'ABORTO : MAP FAIL' TO FRM-MENS.
           GO TO CIC-ERR-ABT.
       CIC-ERR-FIO.
           GO TO FIN-FIO.
       CIC-ERR-FRM.
           GO TO FIN-GNS-PRO-FRM.
       CIC-ERR-ABT.
           MOVE 'GNSPPABT' TO FRM-PROG.
           MOVE +79        TO FRM-TCMA.
      *    MOVE FRM-MENS   TO FRM-CMMA.
           EXEC CICS XCTL
                     PROGRAM(FRM-PROG)
                     COMMAREA(FRM-MENS)
                     LENGTH(FRM-TCMA)
           END-EXEC.
-END
/*
//STEP1  EXEC PGM=AFOLIBR,PARM='NJTA,NRJS'
//STEPLIB  DD DSN=ADR.LIB.V38.LOAD,DISP=SHR
//MASTER   DD DSN=GNSCL.FUENTES,DISP=SHR
//OSJOB    DD DSN=&&TEMP,DISP=(,PASS),SPACE=(CYL,(1,1)),UNIT=SYSDA
//SYSPRINT DD SYSOUT=*
//SYSPUNCH DD SYSOUT=*
//SYSIN    DD *
-SEL GNSPPPFB,CONS,EXEC,CCOPY,COPYDD
-END
/*
//STEP2   EXEC DFHEITCL,PARM.LKED='MAP,XREF'
//TRN.SYSIN    DD DSN=&&TEMP,DISP=OLD
//COB.SYSLIB   DD DSN=GNSCL.FUENTES.V30,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=GNSCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=TABCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SGCCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=DEUCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DEUCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=COLCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=COLCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=SUPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SUPCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=DAPCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=SBTCL.FUENTES,DISP=SHR,SUBSYS=LAM
//             DD DSN=SBTCL.FIO.LINEA,DISP=SHR,SUBSYS=LAM
//             DD DSN=CICS.V211.COBLIB,DISP=SHR,SUBSYS=LAM
//LKED.SYSLIB  DD DSN=CICS.V211.LOADLIB,DISP=SHR
//             DD DSN=GNSCM.RUTINAS.CONSIST,DISP=SHR
//             DD DSN=COB.VSCLLIB,DISP=SHR
//LKED.SYSLMOD DD DSN=GNSCM.LINEA(GNSPPPFB),DISP=SHR
//LKED.SYSLIN  DD DSN=CICS.V211.COBLIB(DFHEILIC),DISP=SHR
//             DD DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD DSN=ADR.DB75.OBJECT.DESA(DBCSVPR),DISP=SHR
//
