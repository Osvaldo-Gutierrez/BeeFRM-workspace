       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GNSPPCTM.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.     18-FEB-1997 09:31:44 

      * Programa actualizador de parametros MTX Realia Y DTC

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
      * INPUT-OUTPUT SECTION.
      *--------------------
      * FILE-CONTROL.

       DATA DIVISION.
      *=============
      * FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MNU-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RPF-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.

       COPY GNSBRMNU.
       COPY GNSBRRPF.
       COPY GNSBRTAB.

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.

      * 01  PGM-STAT.
      *     03 PGM-STAT-SRT        VALUE ' ' PIC X(01).
      *        88 PGM-STAT-SRT-OKS VALUE ' '.
      *        88 PGM-STAT-SRT-MAL VALUE 'M'.
      *     03 PGM-SOKS            VALUE ' ' PIC X(01).
      *     03 PGM-SMAL            VALUE 'M' PIC X(01).

       01  BUF-ACP.
           03 BUF-ACP1                      PIC X(06).
           03 BUF-ACP2                      PIC X(03).
           03 BUF-ACP3                      PIC X(51).

       01  WSS-MNU.
           03 WSS-MNU1                      PIC X(60).
           03 WSS-MNU2                      PIC X(60).
           03 WSS-MNU3                      PIC X(60).
           03 WSS-MNU4                      PIC X(60).
           03 WSS-MNU5                      PIC X(60).
           03 WSS-MNU6                      PIC X(60).
           03 WSS-MNU7                      PIC X(60).
           03 WSS-MNU8                      PIC X(60).
           03 WSS-MNU9                      PIC X(60).

       01  WSS-TOT.
           03 WSS-MNU-ERP     VALUE ZEROES  PIC 9(05).
           03 WSS-MNU-ERP-EDT               PIC ZZZZ9.
           03 WSS-MNU-ERM     VALUE ZEROES  PIC 9(05).
           03 WSS-MNU-ERM-EDT               PIC ZZZZ9.
           03 WSS-MNU-PUT     VALUE ZEROES  PIC 9(05).
           03 WSS-MNU-PUT-EDT               PIC ZZZZ9.
           03 WSS-MNU-MOD     VALUE ZEROES  PIC 9(05).
           03 WSS-MNU-MOD-EDT               PIC ZZZZ9.

           03 WSS-RPF-ERP     VALUE ZEROES  PIC 9(05).
           03 WSS-RPF-ERP-EDT               PIC ZZZZ9.
           03 WSS-RPF-ERM     VALUE ZEROES  PIC 9(05).
           03 WSS-RPF-ERM-EDT               PIC ZZZZ9.
           03 WSS-RPF-PUT     VALUE ZEROES  PIC 9(05).
           03 WSS-RPF-PUT-EDT               PIC ZZZZ9.
           03 WSS-RPF-MOD     VALUE ZEROES  PIC 9(05).
           03 WSS-RPF-MOD-EDT               PIC ZZZZ9.

           03 WSS-TAB-ERP     VALUE ZEROES  PIC 9(05).
           03 WSS-TAB-ERP-EDT               PIC ZZZZ9.
           03 WSS-TAB-ERM     VALUE ZEROES  PIC 9(05).
           03 WSS-TAB-ERM-EDT               PIC ZZZZ9.
           03 WSS-TAB-PUT     VALUE ZEROES  PIC 9(05).
           03 WSS-TAB-PUT-EDT               PIC ZZZZ9.
           03 WSS-TAB-MOD     VALUE ZEROES  PIC 9(05).
           03 WSS-TAB-MOD-EDT               PIC ZZZZ9.

       01  WSS-RPF.
           03 WSS-RPF1                      PIC X(60).
           03 WSS-RPF2                      PIC X(60).

       01  WSS-TAB.
           03 WSS-TAB1                      PIC X(60).
           03 WSS-TAB2                      PIC X(60).
           03 WSS-TAB3                      PIC X(60).
           03 WSS-TAB4                      PIC X(60).

       01  WSS-VARI.
           03 WSS-I           VALUE ZEROES  PIC 9(06).
           03 WSS-SIST        VALUE '   '   PIC X(03).

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
      *     PERFORM GNS-BUS-IDD.
           PERFORM PRO-INP.
           PERFORM DSP-STD.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.

       PRO-INP SECTION.
       INI-PRO-INP.
           MOVE SPACES TO BUF-ACP.
           ACCEPT  BUF-ACP.

           IF BUF-ACP1 = 'REGMNU'
              PERFORM PRO-ACT-MNU
           ELSE
           IF BUF-ACP1 = 'REGRPF'
              PERFORM PRO-ACT-RPF
           ELSE
           IF BUF-ACP1 = 'REGTAB'
              PERFORM PRO-ACT-TAB
           ELSE
           IF BUF-ACP1 NOT = 'FINPI1'
              DISPLAY 'PARAMETRO INVALIDO :'
              DISPLAY BUF-ACP
           ELSE
              GO TO FIN-PRO-INP.
           GO TO INI-PRO-INP.

       FIN-PRO-INP.
           EXIT.

       PRO-ACT-MNU SECTION.
       INI-PRO-ACT-MNU.
           IF BUF-ACP2 NOT > SPACES
              DISPLAY 'NO SE INDICA SISTEMA DE MNU : ' BUF-ACP
              GO TO FIN-PRO-ACT-MNU.

           MOVE FIO-UPD  TO FIO-CMND.
           MOVE BUF-ACP2 TO FIO-SIST WSS-SIST.
           PERFORM GNS-FIO-MNU.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL ABRIR MNU FIO-STAT : ' FIO-STAT
              GO TO FIN-PRO-ACT-MNU.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU1.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU2.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU3.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU4.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU5.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU6.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU7.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU8.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-MNU9.

           MOVE WSS-MNU         TO MNU.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           MOVE WSS-SIST        TO FIO-SIST.
           PERFORM GNS-FIO-MNU.
           IF FIO-STAT-OKS
              MOVE WSS-MNU TO MNU
              MOVE FIO-MOD TO FIO-CMND
           ELSE
              MOVE WSS-MNU TO MNU
              MOVE FIO-PUT TO FIO-CMND.

           MOVE WSS-SIST        TO FIO-SIST.
           PERFORM GNS-FIO-MNU.
           IF NOT FIO-STAT-OKS
               IF FIO-CMND = FIO-PUT
                   ADD 1 TO WSS-MNU-ERP
                   DISPLAY 'ERROR AL GRABAR MNU '
                   DISPLAY 'FIO-STAT MNU-COD-NEMO : ' FIO-STAT ' '
                                               MNU-COD-NEMO IN MNU
               ELSE
                   ADD 1 TO WSS-MNU-ERM
                   DISPLAY 'ERROR AL MODIFICAR MNU '
                   DISPLAY 'FIO-STAT MNU-COD-NEMO : ' FIO-STAT ' '
                                               MNU-COD-NEMO IN MNU
           ELSE
              IF FIO-CMND = FIO-PUT
                  DISPLAY MNU-COD-NEMO '           : MNU INGRESADO' 
                  ADD 1 TO WSS-MNU-PUT
              ELSE
                  DISPLAY MNU-COD-NEMO '           : MNU MODIFICADO' 
                  ADD 1 TO WSS-MNU-MOD.

           MOVE FIO-CLO  TO FIO-CMND.
           MOVE WSS-SIST TO FIO-SIST.
           PERFORM GNS-FIO-MNU.

       FIN-PRO-ACT-MNU.
           EXIT.

       PRO-ACT-RPF SECTION.
       INI-PRO-ACT-RPF.
           IF BUF-ACP2 NOT > SPACES
              DISPLAY 'NO SE INDICA SISTEMA DE RPF : ' BUF-ACP
              GO TO FIN-PRO-ACT-RPF.

           MOVE FIO-UPD  TO FIO-CMND.
           MOVE BUF-ACP2 TO FIO-SIST WSS-SIST.
           PERFORM GNS-FIO-RPF.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL ABRIR RPF FIO-STAT : ' FIO-STAT
              GO TO FIN-PRO-ACT-RPF.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-RPF1.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-RPF2.

           MOVE WSS-RPF         TO RPF.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           MOVE WSS-SIST        TO FIO-SIST.
           PERFORM GNS-FIO-RPF.
           IF FIO-STAT-OKS
              MOVE WSS-RPF TO RPF
              MOVE FIO-MOD TO FIO-CMND
           ELSE
              MOVE WSS-RPF TO RPF
              MOVE FIO-PUT TO FIO-CMND.

           MOVE WSS-SIST        TO FIO-SIST.
           PERFORM GNS-FIO-RPF.

           IF NOT FIO-STAT-OKS
              IF FIO-CMND = FIO-PUT
                  ADD 1 TO WSS-RPF-ERP
                  DISPLAY 'ERROR AL GRABAR RPF '
                  DISPLAY 'FIO-STAT RPF-COD-DPTR : ' FIO-STAT ' '
                                               RPF-COD-DPTR IN RPF
              ELSE
                  ADD 1 TO WSS-RPF-ERM
                  DISPLAY 'ERROR AL MODIFICAR RPF '
                  DISPLAY 'FIO-STAT RPF-COD-DPTR : ' FIO-STAT ' '
                                               RPF-COD-DPTR IN RPF
           ELSE
              IF FIO-CMND = FIO-PUT
                  DISPLAY RPF-COD-DPTR ' : RPF INGRESADO' 
                  ADD 1 TO WSS-RPF-PUT
              ELSE
                  DISPLAY RPF-COD-DPTR ' : RPF MODIFICADO' 
                  ADD 1 TO WSS-RPF-MOD.

           MOVE FIO-CLO  TO FIO-CMND.
           MOVE WSS-SIST TO FIO-SIST.
           PERFORM GNS-FIO-RPF.

       FIN-PRO-ACT-RPF.
           EXIT.

       PRO-ACT-TAB SECTION.
       INI-PRO-ACT-TAB.

           IF BUF-ACP2 NOT > SPACES
              DISPLAY 'NO SE INDICA SISTEMA DE TAB : ' BUF-ACP
              GO TO FIN-PRO-ACT-TAB.

           MOVE FIO-UPD  TO FIO-CMND.
           MOVE BUF-ACP2 TO FIO-SIST WSS-SIST.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL ABRIR TAB FIO-STAT : ' FIO-STAT
              GO TO FIN-PRO-ACT-TAB.


           MOVE BUF-ACP2 TO FIO-SIST WSS-SIST.
           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-TAB1.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-TAB2.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-TAB3.

           MOVE SPACES TO BUF-ACP.
           ACCEPT BUF-ACP.
           MOVE BUF-ACP TO WSS-TAB4.

           MOVE WSS-TAB         TO TAB.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           MOVE WSS-SIST        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
              MOVE WSS-TAB TO TAB
              MOVE FIO-MOD TO FIO-CMND
           ELSE
              MOVE WSS-TAB TO TAB
              MOVE FIO-PUT TO FIO-CMND.

           MOVE WSS-SIST        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           IF NOT FIO-STAT-OKS
              IF FIO-CMND = FIO-PUT
                  ADD 1 TO WSS-TAB-ERP
                  DISPLAY 'ERROR AL GRABAR TAB '
                  DISPLAY 'FIO-STAT TAB-COD-CTAB : ' FIO-STAT ' '
                                               TAB-COD-CTAB IN TAB
              ELSE
                  ADD 1 TO WSS-TAB-ERM
                  DISPLAY 'ERROR AL MODIFICAR TAB '
                  DISPLAY 'FIO-STAT TAB-COD-CTAB : ' FIO-STAT ' '
                                               TAB-COD-CTAB IN TAB
           ELSE
              IF FIO-CMND = FIO-PUT
                  DISPLAY TAB-COD-CTAB '           : TAB INGRESADO' 
                  ADD 1 TO WSS-TAB-PUT
              ELSE
                  DISPLAY TAB-COD-CTAB '           : TAB MODIFICADO' 
                  ADD 1 TO WSS-TAB-MOD.

           MOVE FIO-CLO  TO FIO-CMND.
           MOVE WSS-SIST TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

       FIN-PRO-ACT-TAB.
           EXIT.

       DSP-STD SECTION.
       INI-DSP-STD.
           MOVE WSS-MNU-ERP TO WSS-MNU-ERP-EDT.
           MOVE WSS-MNU-ERM TO WSS-MNU-ERM-EDT.
           MOVE WSS-MNU-PUT TO WSS-MNU-PUT-EDT.
           MOVE WSS-MNU-MOD TO WSS-MNU-MOD-EDT.
           DISPLAY '===== ESTADISTICAS MNU ====='.
           DISPLAY 'PUT OK      MOD OK      PUT ERR      MOD ERR'
           DISPLAY '======      ======      =======      ======='
           DISPLAY ' ' WSS-MNU-PUT-EDT '       ' WSS-MNU-MOD-EDT 
                   '        '
                   WSS-MNU-ERP-EDT '        ' WSS-MNU-ERM-EDT.

           DISPLAY ' '.

           MOVE WSS-RPF-ERP TO WSS-RPF-ERP-EDT.
           MOVE WSS-RPF-ERM TO WSS-RPF-ERM-EDT.
           MOVE WSS-RPF-PUT TO WSS-RPF-PUT-EDT.
           MOVE WSS-RPF-MOD TO WSS-RPF-MOD-EDT.
           DISPLAY '===== ESTADISTICAS RPF ====='.
           DISPLAY 'PUT OK      MOD OK      PUT ERR      MOD ERR'
           DISPLAY '======      ======      =======      ======='
           DISPLAY ' ' WSS-RPF-PUT-EDT '       ' WSS-RPF-MOD-EDT 
                   '        '
                   WSS-RPF-ERP-EDT '        ' WSS-RPF-ERM-EDT.

           DISPLAY ' '.

           MOVE WSS-TAB-ERP TO WSS-TAB-ERP-EDT.
           MOVE WSS-TAB-ERM TO WSS-TAB-ERM-EDT.
           MOVE WSS-TAB-PUT TO WSS-TAB-PUT-EDT.
           MOVE WSS-TAB-MOD TO WSS-TAB-MOD-EDT.
           DISPLAY '===== ESTADISTICAS TAB ====='.
           DISPLAY 'PUT OK      MOD OK      PUT ERR      MOD ERR'
           DISPLAY '======      ======      =======      ======='
           DISPLAY ' ' WSS-TAB-PUT-EDT '       ' WSS-TAB-MOD-EDT
                   '        '
                   WSS-TAB-ERP-EDT '        ' WSS-TAB-ERM-EDT.

           DISPLAY ' '.
       FIN-DSP-STD.
           EXIT.

       COPY GNSBFMNU.
       COPY GNSBFRPF.
       COPY GNSBFTAB.

       COPY GNSBGDTC.
      * COPY GNSBGIDD.
      * COPY GNSBTABT.
      * COPY GNSBBSYS.
       COPY GNSBGEND.
      * COPY GNSBGABT.
