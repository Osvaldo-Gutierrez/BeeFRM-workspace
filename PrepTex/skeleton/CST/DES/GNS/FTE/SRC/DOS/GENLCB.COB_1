       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID. GENLCB.
       AUTHOR. CONSIST.

      * Genera Archivo de Edicion para Generacion de Listador.

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------
      *Registro Relacion Programa Reporte
       01  RPR-FLD.
           03 RPR-COD-DPRO                   PIC X(12).
           03 RPR-GLS-DAUT                   PIC X(42).
           03 RPR-GLS-ABAS                   PIC X(08).
           03 RPR-GLS-DREP                   PIC X(06).
           03 RPR-IND-SORT                   PIC X(01).
       COPY GNSBRRPR.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       01 WSS-VARI.
           03 WSS-COD-DPRO                   PIC X(12).
           03 WSS-MENS                       PIC X(80).
           03 WSS-STAT   COMP VALUE 0           PIC 9(09).
              88 WSS-STAT-OKS VALUE 0.
              88 WSS-STAT-MAL VALUE 1.
           03 WSS-GLS-DREP.
              05 WSS-SRPT                    PIC X(03).
              05 WSS-NRPT                    PIC X(03).

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-RPR.
           CALL 'LIB$GET_SYMBOL' USING
              BY DESCRIPTOR 'FIL'
                 WSS-COD-DPRO.
           PERFORM FND-KEY.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE Definicion en Archivo RPR' TO WSS-MENS
               GO TO MSG-MAIN.
           PERFORM LEE-RPR.
           IF NOT FIO-STAT-OKS
               MOVE 'Definicion INCOMPLETA en Archivo RPR' TO WSS-MENS
               GO TO MSG-MAIN.
           MOVE RPR-GLS-DREP IN RPR-FLD TO WSS-GLS-DREP.

      *     CALL 'GENLISEDT' USING
           CALL 'GENLCBEDT' USING
                BY DESCRIPTOR RPR-COD-DPRO IN RPR-FLD
                   RPR-GLS-DAUT IN RPR-FLD
                   RPR-GLS-ABAS IN RPR-FLD
                   RPR-GLS-DREP IN RPR-FLD
                   BY REFERENCE WSS-STAT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-RPR.
           IF NOT WSS-STAT-MAL
                CALL 'LIB$SET_SYMBOL' USING
                     BY DESCRIPTOR 'REP'
                        WSS-NRPT
                CALL 'LIB$SET_SYMBOL' USING
                    BY DESCRIPTOR 'SRT'
                       RPR-IND-SORT IN RPR-FLD
                GO TO FIN-MAIN.
           MOVE 'ERROR en GENERACION de Archivo de EDICION' TO WSS-MENS.
       MSG-MAIN.
           DISPLAY WSS-COD-DPRO , ': ' , WSS-MENS.
           CALL 'SYS$EXIT' USING
                 BY VALUE 0.
       FIN-MAIN.
           STOP RUN.

       FND-KEY SECTION.
      *---------------
       INI-FND-KEY.
           MOVE SPACES TO RPR-COD-TRYC
                          RPR-COD-DARC.
           MOVE WSS-COD-DPRO TO RPR-COD-DPRO IN RPR.
           MOVE 'A' TO RPR-COD-TREG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-RPR.
       FIN-FND-KEY.
           EXIT.

       LEE-RPR SECTION.
      *---------------
       INI-LEE-RPR.
           MOVE RPR-COD-DPRO IN RPR TO RPR-COD-DPRO IN RPR-FLD.
           MOVE RPR-GLS-DAUT IN RPR TO RPR-GLS-DAUT IN RPR-FLD.

           MOVE 'B' TO RPR-COD-TREG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-RPR.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LEE-RPR.
           MOVE RPR-GLS-ABAS IN RPR TO RPR-GLS-ABAS IN RPR-FLD.

           MOVE 'S' TO RPR-COD-TREG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-RPR.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LEE-RPR.
           MOVE RPR-IND-SORT IN RPR TO RPR-IND-SORT IN RPR-FLD.

           MOVE 'R' TO RPR-COD-TREG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-RPR.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LEE-RPR.
           MOVE RPR-GLS-DREP IN RPR TO RPR-GLS-DREP IN RPR-FLD.
       FIN-LEE-RPR.
           EXIT.
       COPY GNSBFRPR.
