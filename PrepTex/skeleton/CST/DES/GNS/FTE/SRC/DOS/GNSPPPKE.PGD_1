       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. GNSPPPKE.
       AUTHOR. CONSIST.
       DATE-WRITTEN. 13-APR-1992.
      *
      * RECUPERA DE LLAVES V 3.1
      *
      * COMPATIBLE CON NUEVA VERSION DE MENU PARA MOSTRAR EN HEADER
      * 'PA2' CUANDO HAY LLAVES SETEADAS ( LARGO HEADER 168 )
      * AL NO EXISTIR LLAVES SETEADAS NO DESPLIEGA EL MAPA DOS VECES
      *
       ENVIRONMENT DIVISION.
      *=====================
       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWGSYS.
       COPY GNSWGTSC.
       COPY GNSWCSCR.
       COPY GNSWVSCR.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
      *
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWVIDD.
       COPY GNSBRIDD.
       COPY GNSBRTAB.
       COPY GNSWGFRM.
       COPY GNSWGQUE.
       COPY GNSWIMSG.
      *
       01  WSS-VARI.
           03 WSS-TFLD  COMP      VALUE +0             PIC S9(04).
           03 WSS-PCUR  COMP      VALUE +0             PIC S9(04).
           03 WSS-PMSG  COMP      VALUE +1880          PIC S9(04).
           03 WSS-TCMA  COMP      VALUE +0             PIC S9(04).
           03 WSS-CMMA                                 PIC X(3500).

           03  WSS-QIDD.
               05  WSS-NIDD VALUE 'GIDD'    PIC X(04).
               05  WSS-TIDD                 PIC X(04).

           03 WSS-I             VALUE 0                PIC 9(04).
           03 WSS-J             VALUE 0                PIC 9(04).
           03 WSS-K             VALUE 0                PIC 9(04).
           03 WSS-THDR          VALUE 168              PIC 9(04).
           03 WSS-IFLD          VALUE 1                PIC 9(04).
           03 WSS-IKEY          VALUE ' '              PIC X(01).
              88 WSS-EXISTEN-KEY    VALUE 'S'.
              88 WSS-NO-EXISTEN-KEY VALUE 'N'.
       01  WSS-TAB.
           03 WSS-TAB-GLS-DESC                         PIC X(40).
           03 WSS-TAB-GLS-DATA                         PIC X(60).
       01  WSS-TAB-RED         REDEFINES WSS-TAB.
           03 WSS-TAB-TKYM OCCURS 11 TIMES.
              05 WSS-TKEY                              PIC X(03).
              05 WSS-PKEY                              PIC 9(04).
              05 WSS-LKEY                              PIC 9(02).
           03 FILLER                                   PIC X(01).
       01  WSS-DFLD.
           03 WSS-DFLD-CHR    OCCURS 3000              PIC X(01).
       01  WSS-QFLD.
           03 WSS-TKEY-Q                               PIC X(03).
           03 WSS-QFLD-CHR    OCCURS 100               PIC X(01).

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                             PIC X(3500).
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================

       MAIN SECTION.
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           PERFORM GNS-HDL-VSM.
           MOVE EIBTRMID TO WSS-TIDD.
           MOVE WSS-QIDD TO SCR-QIDD.
           MOVE +490 TO SCR-LIDD.
           MOVE 'GNSPPPKE' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           MOVE DFHCOMMAREA TO WSS-CMMA SCR-VARI.
           MOVE EIBCALEN    TO WSS-TCMA.
      *
      *Buscar TS que usa procesador de MENU
           PERFORM GNS-GET-TSC.
      *
      *Buscar idd con codigo TSC-CIDD.
           PERFORM GNS-GET-IDD.
      *
      *Obtener el contexo de la pantalla
           EXEC CICS HANDLE CONDITION LENGERR(CON-MAIN)
                                      NOSPACE(CON-MAIN)
           END-EXEC.
           MOVE +3000 TO WSS-TFLD.
           EXEC CICS RECEIVE INTO(WSS-DFLD) LENGTH(WSS-TFLD) BUFFER
           END-EXEC.
           MOVE EIBCPOSN TO WSS-PCUR.
       CON-MAIN.
      *Procesar la tabla KYM
           MOVE TSC-SIST TO FIO-SIST.
           MOVE 'KYM'    TO TAB-COD-TTAB IN TAB.
      *Se usa SCR-NMAP solo para poder manejar la situacion de programas
      *enganchados que se van pasando el control
      *    MOVE TSC-NMAP TO TAB-COD-CTAB IN TAB.
           MOVE SCR-NMAP TO TAB-COD-CTAB IN TAB.
      *
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE LOW-VALUES TO MSG-FLD
               MOVE 'No existe especificacion de llaves'
                               TO MSG-GLS-MENS
               EXEC CICS SEND
                     MAP      ('GNSFMSG')
                     MAPSET   ('GNSAMSG')
                     FROM     (MSG-FLD)
                     CURSOR   (EIBCPOSN)
               END-EXEC
      *         EXEC CICS RETURN
      *               TRANSID  (EIBTRNID)
      *               COMMAREA (DFHCOMMAREA)
      *               LENGTH   (EIBCALEN)
      *         END-EXEC.
               PERFORM RET-PRG
           ELSE
               MOVE TAB-GLS-DESC IN TAB TO WSS-TAB-GLS-DESC IN WSS-TAB
               MOVE TAB-GLS-DATA IN TAB TO WSS-TAB-GLS-DATA IN WSS-TAB.
      *
      *Lee registros de cola existente y busca en tabla
           PERFORM GNS-ERR-QUE.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'KYM'    TO QUE-TYPE.
           MOVE 1        TO QUE-NITM.
           MOVE 'N'      TO WSS-IKEY.
       CIC-MAIN.
           MOVE QUE-GET  TO QUE-CMND.
           MOVE +3000 TO QUE-LITM.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT = QUE-STAT-OKS
               MOVE QUE-ITEM TO WSS-QFLD
               PERFORM SCN-TAB
               ADD 1 TO QUE-NITM
               GO TO CIC-MAIN.
           IF QUE-NITM = 1
               MOVE LOW-VALUES TO MSG-FLD
               MOVE 'No existe seteo'
                               TO MSG-GLS-MENS
               EXEC CICS SEND
                     MAP      ('GNSFMSG')
                     MAPSET   ('GNSAMSG')
                     FROM     (MSG-FLD)
                     CURSOR   (EIBCPOSN)
               END-EXEC
      *         EXEC CICS RETURN
      *               TRANSID  (EIBTRNID)
      *               COMMAREA (DFHCOMMAREA)
      *               LENGTH   (EIBCALEN)
      *         END-EXEC.
               PERFORM RET-PRG
           ELSE
                IF WSS-EXISTEN-KEY
                    EXEC CICS SEND FROM(WSS-DFLD)
                          LENGTH(WSS-TFLD)
                          ERASE
                    END-EXEC
                    EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
                          TERMINAL
                    END-EXEC.
           PERFORM RET-PRG.
       COPY GNSBGGBK.
       FIN-MAIN.
           EXIT.

       SCN-TAB SECTION.
       INI-SCN-TAB.
           MOVE 1 TO WSS-IFLD.
       LUP-SCN-TAB.
           IF WSS-TKEY(WSS-IFLD) NOT > SPACES
               GO TO FIN-SCN-TAB.
           IF WSS-TKEY(WSS-IFLD) = WSS-TKEY-Q
               PERFORM MOV-TAB
               GO TO FIN-SCN-TAB.
           ADD 1 TO WSS-IFLD.
           IF WSS-IFLD NOT > 11
               GO TO LUP-SCN-TAB.
       FIN-SCN-TAB.
           EXIT.

       MOV-TAB SECTION.
       INI-MOV-TAB.
           MOVE WSS-PKEY(WSS-IFLD) TO WSS-J.
           PERFORM MOV-KEY VARYING WSS-I FROM 1 BY 1 UNTIL
                                   WSS-I > WSS-LKEY(WSS-IFLD).
       FIN-MOV-TAB.
           EXIT.

       MOV-KEY SECTION.
       INI-MOV-KEY.
           ADD WSS-J WSS-THDR GIVING WSS-K.
           MOVE WSS-QFLD-CHR(WSS-I) TO WSS-DFLD-CHR(WSS-K).
           IF WSS-QFLD-CHR(WSS-I) > SPACES
               MOVE 'S' TO WSS-IKEY.
           ADD 1 TO WSS-J.
       FIN-MOV-KEY.
           EXIT.

       RET-PRG SECTION.
       INI-RET-PRG.
           EXEC CICS SEND CONTROL FREEKB
           END-EXEC.
           PERFORM SCR-EXIT.
       FIN-RET-PRG.
           EXIT.

       PUT-MSG SECTION.
       INI-PUT-MSG.
      *     EXEC CICS SEND CONTROL CURSOR(WSS-PMSG)
      *           TERMINAL
      *     END-EXEC.
      *NO SE DEBE USAR GNS-PRO-FRM CON FRM-CMND = FRM-MSG, PUES DESPLIEG
      *EL MENSAJE CON LEN(79) Y SE 'PISA' AREAS DEL MAPA.
      *     EXEC CICS SEND
      *               FROM(FRM-MENS)
      *               LENGTH(40)
      *     END-EXEC.
      *     EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
      *           TERMINAL
      *     END-EXEC.
       FIN-PUT-MSG.
           EXIT.

       SCR-EXIT SECTION.
       INI-SCR-EXIT.
           IF SCR-DESDE-CMN
               IF WSS-EXISTEN-KEY
                   EXEC CICS RETURN
                          TRANSID(SCR-NTRN)
                          COMMAREA(SCR-VARI)
                          LENGTH(SCR-TCMA)
                   END-EXEC
               ELSE
                   MOVE 'HKY' TO SCR-INST
                   MOVE SCR-VARI TO SYS-CMMA
                   MOVE SCR-TCMA TO SYS-TCMA
                   MOVE SCR-PROG TO SYS-PROG
                   MOVE SYS-XCTL TO SYS-CMND
                   PERFORM GNS-PRO-SYS
           ELSE
               EXEC CICS RETURN
                         TRANSID(EIBTRNID)
                         COMMAREA(WSS-CMMA)
                         LENGTH(WSS-TCMA)
               END-EXEC.
       FIN-SCR-EXIT.
           EXIT.

       GNS-GET-TSC SECTION.
       INI-GNS-GET-TSC.
           EXEC CICS HANDLE CONDITION
                     QIDERR (NEX-GNS-GET-TSC)
           END-EXEC.
           MOVE EIBTRMID TO TSC-TERM.
           MOVE TSC-TTSC TO TSC-LITM.
           EXEC CICS READQ TS
                     QUEUE(TSC-COLA)
                     INTO(TSC-ITEM)
                     LENGTH(TSC-LITM)
                     ITEM(1)
           END-EXEC.
           GO TO FIN-GNS-GET-TSC.
       NEX-GNS-GET-TSC.
           MOVE 'SISTEMA DEBE EJECUTARSE CON PROCESADOR MENU COMPATIBLE'
                        TO SYS-MENS.
           MOVE SYS-ABOR TO SYS-CMND.
           PERFORM GNS-PRO-SYS.
       FIN-GNS-GET-TSC.
           EXIT.

       GNS-GET-IDD SECTION.
       INI-GNS-GET-IDD.
      *La independencia de datos es IDD EN TEMPORARY STORAGE ( VERSION 3
           MOVE TSC-CIDD TO IDD-COD-CIDD IN IDD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IDD.
           IF FIO-STAT-OKS
               MOVE IDD TO IDD-REGI
               GO TO FIN-GNS-GET-IDD.
           MOVE 'DEFAULT' TO IDD-COD-CIDD IN IDD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-IDD.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE REGISTRO IDD CON LLAVE : ' TO SYS-MEN1
               MOVE IDD-COD-CIDD IN IDD                   TO SYS-MEN2
               MOVE SYS-ABOR TO SYS-CMND
               PERFORM GNS-PRO-SYS
           ELSE
               MOVE IDD TO IDD-REGI.
       FIN-GNS-GET-IDD.
           EXIT.

       COPY GNSBBIDD.
       COPY GNSBFIDD.
       COPY GNSBGDTC.
       COPY GNSBGVSM.
       COPY GNSBHVSM.

       COPY GNSBFTAB.
       COPY GNSBGQUE.
       COPY GNSBIABT.
       COPY GNSBGFRM.
       COPY GNSBHSYS.
       COPY GNSBGSYS.


