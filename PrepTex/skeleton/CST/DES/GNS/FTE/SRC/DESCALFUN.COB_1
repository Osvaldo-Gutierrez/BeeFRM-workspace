IDENTIFICATION DIVISION.
*=======================
PROGRAM-ID.	DESCALFUN.
AUTHOR.		GUZMAN Y CIA.

* Calculo Fila - Columna de Archivo FUN.

ENVIRONMENT DIVISION.
*====================

DATA DIVISION.
*=============

WORKING-STORAGE SECTION.
*-----------------------
COPY	'DESREG.FUN'	FROM	DICTIONARY
	REPLACING
	==FUN==		BY	==FUN EXTERNAL==.
COPY	FIOWSS	IN 	DESTXT.	
01 WSS-VARI.
	03 WSS-KEY-FUNC				PIC X(08).
	03 WSS-KEY-AUXI				PIC X(08).
	03 WSS-KEY-NEMO.
		05 WSS-KEY-MENU			PIC X(03).
		05 WSS-KEY-CACC			PIC X(03).
		05 WSS-KEY-NREG			PIC X(06).
	03 WSS-VEC-SEPA	VALUE
	'2222222222222222222211111111111111111111'.
		05 WSS-SEPA	OCCURS 40	PIC 9(01).
	03 WSS-VEC-FINI	VALUE
	'12111009080706050403070706060505040403030707070706060606050505050404040403030303'.
		05 WSS-FINI	OCCURS 40	PIC 9(02).
	03 WSS-TAB-FICO.
		05 WSS-VEC-FICO OCCURS 40.
			07 WSS-FICO OCCURS 2	PIC 9(02).
	03 WSS-VEC-KEYS.
		05 WSS-KEYS	OCCURS 40	PIC X(08).
	03 WSS-REST				PIC 9(01).
	03 WSS-FILA				PIC 9(02).
	03 WSS-COLU				PIC 9(02).
	03 WSS-SIND.
		05 I				PIC 9(02).
		05 J				PIC 9(02).
		05 IMAX				PIC 9(02).
		05 LMAX				PIC 9(02).
		05 M				PIC 9(02).
		05 N				PIC 9(02).

LINKAGE SECTION.
*---------------
01 LNK-VARI.
	03 LNK-STAT	COMP			PIC 9(09).
		88 LNK-STAT-OKS		VALUE 0.
		88 LNK-STAT-MAL		VALUE 1.
	03 LNK-MENS				PIC X(80).

PROCEDURE DIVISION USING LNK-VARI.
*=================================

MAIN SECTION.
*------------
INI-MAIN.
	PERFORM INI.
	PERFORM BUS-FUN.
	IF LNK-STAT-MAL
		GO TO FIN-MAIN.
	PERFORM ORD-KEY VARYING M FROM 1 BY 1 UNTIL M > (IMAX - 1).
	PERFORM CAL-POS.
	PERFORM PUT-POS VARYING I FROM 1 BY 1 UNTIL I > IMAX.
FIN-MAIN.
	EXIT PROGRAM.

INI SECTION.
*-----------
INI-INI.
	MOVE 0 TO IMAX.
	MOVE 0 TO LNK-STAT.
	MOVE 'CALCULO EFECTUADO OK' TO LNK-MENS.
FIN-INI.
	EXIT.

BUS-FUN SECTION.
*---------------
INI-BUS-FUN.
	IF FUN-COD-TFUN NOT = 'MEN'
		MOVE 1 TO LNK-STAT
		MOVE 'TIPO FUNCION debe ser MEN (Menu)' TO LNK-MENS
		GO TO FIN-BUS-FUN.
	MOVE FUN-COD-MENU	TO WSS-KEY-MENU.
	MOVE FUN-COD-MENU	TO FUN-KEY-CMEN.
	MOVE SPACES		TO FUN-KEY-CACC.
	MOVE SPACES		TO FUN-KEY-NREG.
	MOVE 'FUN-KEY-NEMO'	TO FIO-AKEY.
	CALL 'DESFIOFUN' USING FIO-GET-NLS-ALT.
	IF NOT ( FIO-STAT-OKS AND FUN-KEY-CMEN = WSS-KEY-MENU )
		MOVE 1 TO LNK-STAT
		MOVE 'NO EXISTEN OPCIONES PARA ESTE MENU' TO LNK-MENS
		GO TO FIN-BUS-FUN.
LUP-BUS-FUN.
	ADD 1 TO IMAX.
	MOVE FUN-KEY-FUNC TO WSS-KEYS(IMAX).
	CALL 'DESFIOFUN' USING FIO-GET-NXT.
	IF FIO-STAT-OKS AND FUN-KEY-CMEN = WSS-KEY-MENU
		GO TO LUP-BUS-FUN.
FIN-BUS-FUN.
	EXIT.

ORD-KEY SECTION.
*---------------
INI-ORD-KEY.
	PERFORM ORD-ORD VARYING N FROM M BY 1 UNTIL N > IMAX.
FIN-ORD-KEY.
	EXIT.

ORD-ORD SECTION.
*---------------
INI-ORD-ORD.
	IF WSS-KEYS(M) > WSS-KEYS(N)
		MOVE WSS-KEYS(N)	TO WSS-KEY-AUXI
		MOVE WSS-KEYS(M)	TO WSS-KEYS(N)
		MOVE WSS-KEY-AUXI	TO WSS-KEYS(M).
FIN-ORD-ORD.
	EXIT.

CAL-POS SECTION.
*---------------
INI-CAL-POS.
	MOVE 0 TO J.
	MOVE WSS-FINI(IMAX) TO WSS-FILA.
	IF IMAX NOT > 10
		MOVE 24 TO WSS-COLU
		PERFORM CAL-FIL VARYING I FROM 1 BY 1 UNTIL I > IMAX
		GO TO FIN-CAL-POS.
	DIVIDE IMAX BY 2 GIVING LMAX REMAINDER WSS-REST.
	IF WSS-REST NOT = 0
		ADD 1 TO LMAX.
	MOVE 1 TO WSS-COLU.
	PERFORM CAL-FIL VARYING I FROM 1 BY 1 UNTIL I > LMAX.
	MOVE WSS-FINI(IMAX) TO WSS-FILA.
	DIVIDE IMAX BY 2 GIVING LMAX.
	MOVE 41 TO WSS-COLU.
	PERFORM CAL-FIL VARYING I FROM 1 BY 1 UNTIL I > LMAX.
FIN-CAL-POS.
	EXIT.

CAL-FIL SECTION.
*---------------
INI-CAL-FIL.
	ADD 1 TO J.
	MOVE WSS-FILA TO WSS-FICO(J,1)
	MOVE WSS-COLU TO WSS-FICO(J,2)
	ADD WSS-SEPA(IMAX) TO WSS-FILA.
FIN-CAL-FIL.
	EXIT.

PUT-POS SECTION.
*---------------
INI-PUT-POS.
	MOVE WSS-KEYS(I) TO FUN-KEY-FUNC.
	CALL 'DESFIOFUN' USING FIO-GET-KEY.
	MOVE WSS-FICO(I,1) TO FUN-NUM-FILA.
	MOVE WSS-FICO(I,2) TO FUN-NUM-COLU.
	CALL 'DESFIOFUN' USING FIO-MOD.
	IF NOT FIO-STAT-OKS
		MOVE 1 TO LNK-STAT
		MOVE 'ERROR al MODIFICAR FUN' TO LNK-MENS
		MOVE IMAX TO I.
FIN-PUT-POS.
	EXIT.
