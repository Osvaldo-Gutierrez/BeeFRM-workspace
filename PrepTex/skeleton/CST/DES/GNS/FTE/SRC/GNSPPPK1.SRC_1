*%! RVM : ESTE PROGRAMA NO TIENE ELEMENTOS DTC, PERO SE MANTIENE LA DIFERENCIA
*%!       DTC-VSI ( A NIVEL DE JCL DE COMPILACION ) PARA TENER DOS VERSIONES 
*%!       DE FUENTES Y EJECUTABLES IDENTICAS EN BIBLIOTECAS SEPARADAS 
*%!       (DTC Y VSI)
*%!       ADEMAS A PARTIR DE ESTE 'FUENTE' ES POSIBLE GENERAR CON PREPTEX
*%!       GNSPPPF1 VSAM Y DTC ( 2.0 )
*%!       GNSPPPK1 VSAM Y DTC ( 3.1 )
*%!
*%!! JSS
*% SW_GNSPPPK1 = TRUE
*% IF SW_GNSPPPK1 THEN
*%      PGMHELP = "GNSPPPK1"
*% ELSE
*%      PGMHELP = "GNSPPPF1"
*% END
*%! IF PGM_V31
*%!      PGMHELP = "GNSPPPK1"
*%! ELSE
*%!      PGMHELP = "GNSPPPF1"
*%! END
       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. {PGMHELP}.
       AUTHOR.     CONSIST.
       DATE-WRITTEN.  3-FEB-1993 18:36:00.

      * PK1 - MONITOR DE TRANSACCIONES HELP 3.1
*% IF SW_REALIA THEN
      *
      *                  CAPTURA DE TABLA GENERICA TIPO REALIA
      *
*% ELSE
      *
      *                  CAPTURA DE TABLA GENERICA TIPO HOST-IBM
      *
*% END
       ENVIRONMENT DIVISION.
      *=====================

       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
*% IF SW_GNSPPPK1 THEN
       COPY GNSWVSCR.
*% ELSE
       COPY GNSWGSCR.
*% END
       COPY GNSWGFRM.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGHOY.
       COPY GNSWGQUE.
       COPY GNSBRHLP.
       COPY GNSWGCBG.
       COPY GNSWIMBG.
       COPY GNSWIMSG.

*% IF PGM_DTC

       COPY GNSBRIDD.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-HLP-REQA==.

       01  WSS-IDD.
           03  WSS-QIDD.
               05  WSS-NIDD VALUE 'GIDD'    PIC X(04).
               05  WSS-TIDD                 PIC X(04).
*% END
*% IF SW_GNSPPPK1 THEN
       01  WSS-GLS-TRAN       VALUE 'GS47'   PIC X(04).
       01  WSS-COD-CMA        VALUE 'CM3'    PIC X(03).
*% ELSE
       01  WSS-GLS-TRAN       VALUE 'GS48'   PIC X(04).
       01  WSS-COD-CMA        VALUE 'CM2'    PIC X(03).
*% END
      * ISP: variable de receive estructurada para campo codigo de tabla
      *      en mapa gnsftab
       01  WSS-DFRM.
*% IF SW_REALIA THEN
      *    HOST IBM
      *    03 WSS-DFRM-FIL1                        PIC X(0282).
      *    REALIA
           03 WSS-DFRM-FIL1                        PIC X(0304).
*% ELSE
      *    HOST IBM
           03 WSS-DFRM-FIL1                        PIC X(0282).
      *    REALIA
      *    03 WSS-DFRM-FIL1                        PIC X(0304).
*% END
           03 WSS-DFRM-TTAB                        PIC X(0003).
*% IF SW_REALIA THEN
      *    HOST IBM
      *    03 WSS-DFRM-FIL3                        PIC X(3215).
      *    REALIA
           03 WSS-DFRM-FIL3                        PIC X(2193).
*% ELSE
      *    HOST IBM
           03 WSS-DFRM-FIL3                        PIC X(3215).
      *    REALIA
      *    03 WSS-DFRM-FIL3                        PIC X(2193).
*% END
      *                                               -------
      *                                      SUMA ==>    3500
       01  WSS-VARI.
           03 WSS-CMMA                                 PIC X(3500).
      *       Tamano del mapa
           03 WSS-TFLD  COMP      VALUE +2500          PIC S9(04).
      *       Posicion del cursor
           03 WSS-PCUR  COMP      VALUE +0             PIC S9(04).
           03 WSS-FEC-FHOY.
              05 WSS-NUM-DHOY                PIC 9(02).
              05 FILLER                      PIC X(01) VALUE '/'.
              05 WSS-NUM-MHOY                PIC 9(02).
              05 FILLER                      PIC X(01) VALUE '/'.
              05 WSS-NUM-SHOY                PIC 9(02).
              05 WSS-NUM-AHOY                PIC 9(02).

           03 TSQ.
              05 TSQ-TERM      PIC X(4).
              05 FILLER        PIC X(4) VALUE 'HELP'.

       01  WSS-SCR-VARI-V20.
           03  WSS-SCR-TCMA-V20     COMP         PIC S9(04).
           03  WSS-SCR-SIST-V20                  PIC  X(03).
           03  WSS-SCR-FUNC-V20                  PIC  X(08).
           03  WSS-SCR-NEMO-V20                  PIC  X(12).
           03  WSS-SCR-RFBY-V20                  PIC  X(01).
           03  WSS-SCR-NTRV-V20                  PIC  X(04).
           03  FILLER                            PIC  X(01).
           03  WSS-SCR-NTRN-V20                  PIC  X(04).
           
       01  WSS-SCR-VARI-V31.
           03  WSS-SCR-TCMA-V31     COMP         PIC S9(04).
           03  WSS-SCR-SIST-V31                  PIC  X(03).
           03  WSS-SCR-SISG-V31                  PIC  X(03).
           03  WSS-SCR-NTRN-V31                  PIC  X(04).

       01  WSS-COD-MSC4.
           03  WSS-COD-MSC3                      PIC X(03).
           03  WSS-COD-MSC1                      PIC X(01).
           
       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                             PIC X(3500).
       COPY GNSLGFIO.
       PROCEDURE DIVISION.
      *===================
*% IF PGM_DTC
           ENTRY 'DBMSCBL'.
*% ELSE
           PERFORM GNS-HDL-VSM.
*% END
           MOVE SPACES TO WSS-DFRM.
*% IF PGM_DTC
           MOVE EIBTRMID TO WSS-TIDD.
           MOVE WSS-QIDD TO SCR-QIDD.
           MOVE +490 TO SCR-LIDD.
           MOVE 'GNSPPPK1' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
*% END
           IF EIBCALEN = CMA-ARG-L
      * ISP: INGRESO PARAMETRO DE BUSQUEDA GENERICA
               MOVE DFHCOMMAREA TO CMA-ARG
               PERFORM ING-PBG
           ELSE
               MOVE 71 TO CMA-ARG-L
               IF EIBCALEN = CMA-ARG-L
      * ISP: DIGITO PF12
                    MOVE 70 TO CMA-ARG-L
                    MOVE DFHCOMMAREA TO CMA-ARG
                    PERFORM INI-BSQ
               ELSE
      * ISP: DIGITO PF1
                   MOVE 70 TO CMA-ARG-L
                   MOVE DFHCOMMAREA TO SCR-VARI
                   PERFORM DGT-PF1.
       FIN-MAIN.
           EXIT.

       DGT-PF1 SECTION.
       INI-DGT-PF1.
      * ISP: Ingresa a esta section cuando el usuario digito pf1


           PERFORM GET-HLP.

           PERFORM SAV-CTX.

           PERFORM INI-BSQ.
       FIN-DGT-PF1.
           EXIT.

       GET-HLP SECTION.
       INI-GET-HLP.
      * ISP : Obtiene datos para campos de registro hlp,
      *       leyendo o simulando lectura

           IF ( SCR-NMAP = 'GNSFTAB' AND EIBCPOSN = 275 ) OR
              ( SCR-NMAP = 'GNSFTAB' AND EIBCPOSN = 271 ) OR
              ( SCR-NMAP = 'GNSFJES' AND EIBCPOSN = 259 ) OR
              ( SCR-NMAP = 'GNSFBCH' AND EIBCPOSN = 179 ) OR
              ( SCR-NMAP = 'GNSFMNU' AND EIBCPOSN = 266 ) OR
              ( SCR-NMAP = 'GNSFRPF' AND EIBCPOSN = 170 )
               PERFORM FIJ-HLP
           ELSE
               PERFORM LEE-HLP.

           MOVE HLP-COD-SIST     TO CMA-ARG-SIST.
           MOVE HLP-COD-TABL     TO CMA-ARG-TTAB.
           MOVE HLP-COD-TABL-MSC TO CMA-ARG-TTAB-MSC.
           MOVE HLP-IND-GENE     TO CMA-ARG-GENE.
           MOVE SCR-SIST         TO CMA-ARG-SSIS.
           MOVE SPACES           TO CMA-ARG-ARGB.
           MOVE SPACES           TO CMA-ARG-FILL.
       FIN-GET-HLP.
           EXIT.

       FIJ-HLP SECTION.
       INI-FIJ-HLP.
      * ISP: simula lectura archivo hlp poniendo valores fijos
           MOVE SPACES        TO HLP-KEY-IHLP.
           MOVE SPACES        TO HLP-COD-FIL1.
           IF SCR-SIST = 'GNS' AND
              SCR-SISG > SPACES
                MOVE SCR-SISG TO HLP-COD-SIST
           ELSE
                MOVE SCR-SIST TO HLP-COD-SIST.
           MOVE SPACES        TO HLP-COD-FIL1.
           MOVE SPACES        TO HLP-IND-TIPO.
           MOVE SPACES        TO HLP-COD-TABL-MSC.

           IF SCR-NMAP = 'GNSFTAB'
               MOVE 'S'        TO HLP-IND-GENE
               MOVE 'GNSPPG04' TO HLP-COD-PROG
           ELSE
           IF SCR-NMAP = 'GNSFJES' OR
              SCR-NMAP = 'GNSFBCH'
               MOVE 'N'        TO HLP-IND-GENE
               MOVE 'GNSPPHBC' TO HLP-COD-PROG
           ELSE
           IF SCR-NMAP = 'GNSFMNU'
               MOVE 'N'        TO HLP-IND-GENE
               MOVE 'GNSPPHMN' TO HLP-COD-PROG
           ELSE
           IF SCR-NMAP = 'GNSFRPF'
               MOVE 'N'        TO HLP-IND-GENE
               MOVE 'GNSPPHRP' TO HLP-COD-PROG.

           MOVE SPACES        TO HLP-COD-FIL3.
       FIN-FIJ-HLP.
           EXIT.

       LEE-HLP SECTION.
       INI-LEE-HLP.
*% IF PGM_VSI
           PERFORM GNS-HDL-VSM.
*% END
           MOVE SCR-NMAP       TO HLP-COD-MAPA.
           MOVE EIBCPOSN       TO HLP-NUM-POSI.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-HLP.
           IF NOT FIO-STAT-OKS
               MOVE LOW-VALUES TO MSG-FLD
               MOVE 'No existe ayuda para este campo'
                               TO MSG-GLS-MENS
               EXEC CICS SEND
                      MAP      ('GNSFMSG')
                      MAPSET   ('GNSAMSG')
                      FROM     (MSG-FLD)
                      CURSOR   (EIBCPOSN) 
               END-EXEC
               EXEC CICS RETURN
                      TRANSID  (EIBTRNID)
                      COMMAREA (DFHCOMMAREA)
                      LENGTH   (EIBCALEN)
               END-EXEC.
       FIN-LEE-HLP.
           EXIT.

       SAV-CTX SECTION.
       INI-SAV-CTX.
      * ISP: Salva el contexto del programa donde se digito pf1

           PERFORM DEL-HLP.

           PERFORM RCV-SCR.

           PERFORM SAV-MAP.

           PERFORM SAV-CMA.

       FIN-SAV-CTX.
           EXIT.

       DEL-HLP SECTION.
       INI-DEL-HLP.
      * ISP: Borra cola de contexto help
           PERFORM GNS-ERR-QUE.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE QUE-DEL  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
       FIN-DEL-HLP.
           EXIT.

       RCV-SCR SECTION.
       INI-RCV-SCR.
           EXEC CICS HANDLE CONDITION
                 LENGERR(INI-SAV-CTX)
                 NOSPACE(INI-SAV-CTX)
           END-EXEC.

           EXEC CICS RECEIVE
                  INTO(WSS-DFRM)
                  LENGTH(WSS-TFLD)
                  BUFFER
           END-EXEC.
           IF SCR-NMAP = 'GNSFTAB'
               IF EIBCPOSN = 275
                    MOVE WSS-DFRM-TTAB TO CMA-ARG-TTAB
               ELSE
                    MOVE 'TBL'         TO CMA-ARG-TTAB.
       FIN-RCV-SCR.
           EXIT.

       SAV-MAP SECTION.
       INI-SAV-MAP.
           COMPUTE QUE-LITM = 7 + WSS-TFLD.
           MOVE 1        TO QUE-NITM.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE 'MAP'    TO QUE-IKEY.
           MOVE WSS-TFLD TO QUE-LDAT.
           MOVE WSS-DFRM TO QUE-DATA.
           MOVE EIBCPOSN TO QUE-PCUR.
           MOVE QUE-PUT TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
       FIN-SAV-MAP.
           EXIT.

       SAV-CMA SECTION.
       INI-SAV-CMA.
           COMPUTE QUE-LITM = 7 + EIBCALEN.
           MOVE 2           TO QUE-NITM.
           MOVE EIBTRMID    TO QUE-TERM.
           MOVE 'HLP'       TO QUE-TYPE.
           MOVE WSS-COD-CMA TO QUE-IKEY.
           MOVE EIBCALEN    TO QUE-LDAT.
           MOVE DFHCOMMAREA TO QUE-DATA.
           MOVE +0          TO QUE-PCUR.
           MOVE QUE-PUT TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
       FIN-SAV-CMA.
           EXIT.

       INI-BSQ SECTION.
       INI-INI-BSQ.
           PERFORM CHQ-TSQ.

           IF HLP-COD-PROG = 'GNSPPHBC' OR
                             'GNSPPHMN' OR
                             'GNSPPHRP'
               PERFORM XCTL-GNS.

           IF CMA-ARG-TTAB = 'USR' OR 'OFI'
               MOVE 'S' TO CMA-ARG-GENE.

      * ISP: SI NO TIENE BUSQUEDA GENERICA => ESTE PROGRAMA SOLO
      *      TRANSFIERE EL CONTROL A PROGRAMA GNSPPG04 CON PALABRA VACIA
           IF CMA-ARG-GENE = 'N'
                MOVE SPACES TO CMA-ARG-ARGB
                PERFORM XCTL-G04.

           MOVE LOW-VALUES   TO MBG-FLD.
           MOVE CMA-ARG-SSIS TO FRM-COD-SIST IN MBG-FLD.
           MOVE 'HLP'        TO FRM-COD-OPCI IN MBG-FLD.
           PERFORM GET-FHOY.
           MOVE HOY-DHOY     TO WSS-NUM-DHOY.
           MOVE HOY-MHOY     TO WSS-NUM-MHOY.
           MOVE HOY-SHOY     TO WSS-NUM-SHOY.
           MOVE HOY-AHOY     TO WSS-NUM-AHOY.
           MOVE WSS-FEC-FHOY TO FRM-GLS-FPRO.
           MOVE 'Ingrese Palabra Buscada y Presione Enter'
                             TO MBG-GLS-MENS.
           MOVE 'I' TO FRM-COD-BUSQ-ATR.
           MOVE -1  TO FRM-COD-BUSQ-LEN.
           EXEC CICS SEND
                 MAP   ('GNSFMBG')
                 MAPSET('GNSAMBG')
                 FROM(MBG-FLD)
                 ERASE
           END-EXEC.
           EXEC CICS RETURN
                 TRANSID (WSS-GLS-TRAN)
                 COMMAREA(CMA-ARG)
                 LENGTH  (CMA-ARG-L)
           END-EXEC.
       FIN-INI-BSQ.
           EXIT.

       CHQ-TSQ SECTION.
       INI-CHQ-TSQ.
           EXEC CICS HANDLE CONDITION
                 QIDERR(FIN-CHQ-TSQ)
           END-EXEC.
           MOVE EIBTRMID       TO TSQ-TERM
           EXEC CICS DELETEQ TS
                 QUEUE(TSQ)
           END-EXEC.
       FIN-CHQ-TSQ.
           EXIT.

       ING-PBG SECTION.
       INI-ING-PBG.
           EXEC CICS HANDLE AID
                 CLEAR(INI-RET-FPEN)
           END-EXEC.
           EXEC CICS RECEIVE
                 MAP   ('GNSFMBG')
                 MAPSET('GNSAMBG')
                 INTO(MBG-FLD)
           END-EXEC.

           IF EIBAID = '3' OR 'C'
               PERFORM RET-FPEN.

           IF EIBAID NOT = ''''
               MOVE 'Tecla invalida en este contexto' TO
                  MSG-GLS-MENS
                  EXEC CICS SEND
                        MAP   ('GNSFMSG')
                        MAPSET('GNSAMSG')
                        FROM  (MSG-FLD)
                        CURSOR(EIBCPOSN) 
                        DATAONLY
                  END-EXEC
                  EXEC CICS RETURN
                        TRANSID (WSS-GLS-TRAN)
                        COMMAREA (DFHCOMMAREA)
                        LENGTH   (EIBCALEN)
                  END-EXEC.

           IF FRM-COD-BUSQ NOT > SPACES
                MOVE SPACES TO FRM-COD-BUSQ.

           MOVE FRM-COD-BUSQ IN MBG-FLD TO CMA-ARG-ARGB.

           IF CMA-ARG-TTAB = 'USR' OR 'OFI'
                   PERFORM XCTL-G07
           ELSE
                   PERFORM XCTL-G04.

       FIN-ING-PBG.
           EXIT.

       RET-FPEN SECTION.
       INI-RET-FPEN.
           PERFORM GNS-ERR-QUE.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE 1        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-RET-FPEN.
      *Ya leyo primer registro de la cola para detectar si esta existia
           IF QUE-IKEY NOT = 'MAP'
               GO TO FIN-RET-FPEN.
           MOVE QUE-DATA TO WSS-DFRM.
           MOVE QUE-LDAT TO WSS-TFLD.
           MOVE QUE-PCUR TO WSS-PCUR.
           MOVE 2        TO QUE-NITM.
           MOVE +9999    TO QUE-LITM.
           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE QUE-GET  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           IF QUE-STAT NOT = QUE-STAT-OKS
               GO TO FIN-RET-FPEN.
           IF NOT ( QUE-IKEY = 'CM2' OR 'CM3' )
               GO TO FIN-RET-FPEN.

           MOVE QUE-LDAT TO SCR-TCMA.
           MOVE QUE-DATA TO WSS-CMMA.

           IF QUE-IKEY = 'CM2'
               MOVE QUE-DATA TO WSS-SCR-VARI-V20
               MOVE WSS-SCR-NTRN-V20 TO SCR-NTRN.

           IF QUE-IKEY = 'CM3'
               MOVE QUE-DATA TO WSS-SCR-VARI-V31
               MOVE WSS-SCR-NTRN-V31 TO SCR-NTRN.

           MOVE EIBTRMID TO QUE-TERM.
           MOVE 'HLP'    TO QUE-TYPE.
           MOVE QUE-DEL  TO QUE-CMND.
           PERFORM GNS-PRO-QUE.
           EXEC CICS SEND FROM(WSS-DFRM)
                          LENGTH(WSS-TFLD)
                          ERASE
           END-EXEC.
           EXEC CICS SEND CONTROL CURSOR(WSS-PCUR)
                          TERMINAL
           END-EXEC.
           PERFORM SCR-EXIT.
       FIN-RET-FPEN.
           EXIT.

       SCR-EXIT SECTION.
       INI-SCR-EXIT.
           EXEC CICS RETURN
                     TRANSID (SCR-NTRN)
                     COMMAREA(WSS-CMMA)
                     LENGTH  (SCR-TCMA)
           END-EXEC.
       FIN-SCR-EXIT.
           EXIT.

       XCTL-G04 SECTION.
       INI-XCTL-G04.
           PERFORM SET-VER.
           EXEC CICS XCTL
                 PROGRAM ('GNSPPG04')
                 COMMAREA(CMA-ARG)
                 LENGTH  (CMA-ARG-L)
           END-EXEC.
       FIN-XCTL-G04.
           EXIT.

       XCTL-G07 SECTION.
       INI-XCTL-G07.
           PERFORM SET-VER.
           EXEC CICS XCTL
                 PROGRAM ('GNSPPG07')
                 COMMAREA(CMA-ARG)
                 LENGTH  (CMA-ARG-L)
           END-EXEC.
       FIN-XCTL-G07.
           EXIT.

       XCTL-GNS SECTION.
       INI-XCTL-GNS.
           PERFORM SET-VER.
           EXEC CICS XCTL
                 PROGRAM (HLP-COD-PROG)
                 COMMAREA(CMA-ARG)
                 LENGTH  (CMA-ARG-L)
           END-EXEC.
       FIN-XCTL-GNS.
           EXIT.

       SET-VER SECTION.
       INI-SET-VER.
           MOVE 'S' TO CMA-ARG-IVEZ.
       FIN-SET-VER.
           EXIT.

*% IF PGM_DTC
       COPY GNSBBIDD.
       COPY GNSBGDTC.
*% ELSE
       COPY GNSBGVSM.
       COPY GNSBHVSM.
*% END

       COPY GNSBGSYS.
       COPY GNSBGHOY.
       COPY GNSBIABT.
       COPY GNSBGQUE.
       COPY GNSBFHLP.
