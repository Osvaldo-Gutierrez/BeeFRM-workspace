$!  PROGRAMA   : SECCIONA UN ARCHIVO PARA PODER COMPILAR EN REALIA
$!  CREADO POR : DANIEL SALAZAR PANFICHI
$!  USO        : SEC P1 P2
$!               DONDE :
$!                       - SEC : CORRESPONDE AL NOMBRE DEL UTILITARIO A USAR
$!                       - P1  : NOMBRE DE PROGRAMA A SECCIONAR
$!                       - P2  : NUMERO DE CORTES A REALIZAR (SI NO INGRESA
$!                               SE ASUMEN DOS CORTES)
$ ITER     = 0
$ CONT_SEC = 1
$ CONT     = 0
$ P1:
$	IF P1 .NES. "" THEN GOTO P2
$	INQUIRE P1 "INGRESE NOMBRE ARCHIVO "
$       GOTO P1
$ P2:
$	IF P2 .NES. "" THEN GOTO MAIN 
$       P2 = 3
$ MAIN:
$	ON WARNING   THEN GOTO FIN
$	ON CONTROL_Y THEN GOTO FIN
$	IF F$MODE() .EQS. "BATCH" THEN 'BAT'
$	WRITE SYS$OUTPUT "[5m[1m",-
		"			Seccionando...[0m"
$       SEARC 'P1 " SECTION" /OUT=TEMP1.TEMP1 /NUMBER
$       OPEN/READ INPUT TEMP1.TEMP1
$       LOOP_READ:
$           READ/END_OF_FILE=END_LOOP INPUT REGISTRO
$           CONT = CONT + 1
$           GOTO LOOP_READ
$       END_LOOP:
$       CLOSE INPUT 
$       IF CONT / P2 .LE. 0 THEN GOTO FIN
$       OPEN/READ INPUT TEMP1.TEMP1
$       OPEN/WRITE FILE TEMP2.TEMP2
$       LOOP_READ2:
$           READ/END_OF_FILE=END_LOOP2 INPUT REGISTRO
$	REGEDT = F$EDIT(REGISTRO,"COMPRESS")
$       IF (F$LOCATE("CONFIGURATION SECTION",REGEDT)   .EQS. F$LENGTH(REGEDT) .AND. -
            F$LOCATE("WORKING-STORAGE SECTION",REGEDT) .EQS. F$LENGTH(REGEDT) .AND. -
            F$LOCATE("INPUT-OUTPUT SECTION",REGEDT)    .EQS. F$LENGTH(REGEDT) .AND. -
            F$LOCATE("FILE SECTION",REGEDT)            .EQS. F$LENGTH(REGEDT) .AND. -
            F$LOCATE("LINKAGE SECTION",REGEDT)         .EQS. F$LENGTH(REGEDT) )
$                THEN
$                ITER = ITER + 1
$                IF ITER .GT. CONT / P2
$                   THEN
$                    CONT_SEC = CONT_SEC + 1  
$                    ITER = 0 
$                   ENDIF
$                WRITE FILE "S/SECTION/SECTION ",CONT_SEC,"/",F$EXTRACT(1,05,REGISTRO),"/NOTYPE"
$                ENDIF
$           GOTO LOOP_READ2
$       END_LOOP2:
$       WRITE FILE "EXIT"
$       CLOSE INPUT 
$       CLOSE FILE
$       EDIT 'P1'/COMM=TEMP2.TEMP2/OUT='P1'
$       DEL TEMP1.TEMP1.
$       DEL TEMP2.TEMP2.
$ FIN:       
$	WRITE SYS$OUTPUT "[5m[1m",-
		"			Seccionado...[0m"
$ EXIT
