$!	Crea estructura de identificadores para proyecto GENESIS
$! P1 : Proyecto
$! P2 : Valor inicial identificador {pry}_DES
$! P3 : Jefe de proyectos
$! P4 : Lista de programadores
$! P5 : Indicador creacion estructura fisica para proyecto
$! P6 : Indicador creacion estructura fisica para comunicacion programadores
$	ON CONTROL_Y THEN GOTO FIN_PGM
$	ASS/NOLOG DUA3:[PGV.DES.GNS.EJE.COM] GNSCOM
$	UAF :== "$SYS$SYSTEM:AUTHORIZE"
$	DISKQ :== "$SYS$SYSTEM:DISKQUOTA"
$	IF F$MODE() .EQS. "BATCH" THEN 'BAT'
$	NOD =	F$PARSE("[]",,,"NODE")
$	DEV =	F$PARSE("[]",,,"DEVICE")
$	DIR =	F$PARSE("[]",,,"DIRECTORY")
$	SET DEF SYS$SYSTEM
$
$	I = 0
$ P1:
$	IF P1 .NES. "" THEN GOTO P2
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P1 -
        "Proyecto                                                  ? "
$	GOTO P1
$
$ P2:
$	VAL = P2
$	IF P2 .NES. "" THEN GOTO P3
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P2 -
        "Valor decimal identificador inicial ( ''P1'_DES / N )       ? "
$	GOTO P2
$ P3:
$
$	IF P3 .NES. "" THEN GOTO P4
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P3 -
        "Jefe de proyecto ( JDP / N )                              ? "
$	GOTO P3
$
$ P4:
$	IF P4 .NES. "" THEN GOTO PROC
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P4 -
        "Programadores ( PGM1,PGM2.../ N )                         ? "
$	GOTO P4
$
$ PROC:
$	IF VAL .EQS. "N" THEN GOTO NOIDE
$	ON WARNING THEN CONTINUE
$	UAF ADD/IDENT 'P1'_DES /ATTR=(NODYN,RES)/VALUE=IDENT:'VAL'
$
$	VAL = VAL + 1
$	ON WARNING THEN CONTINUE
$	UAF ADD/IDENT 'P1'_WRK /ATTR=(NODYN,RES)/VALUE=IDENT:'VAL'
$	
$	VAL = VAL + 1
$	ON WARNING THEN CONTINUE
$	UAF ADD/IDENT 'P1'_JDP /ATTR=(NODYN,NORES)/VALUE=IDENT:'VAL'
$	
$	VAL = VAL + 1
$	ON WARNING THEN CONTINUE
$	UAF ADD/IDENT 'P1'_PGM /ATTR=(NODYN,NORES)/VALUE=IDENT:'VAL'
$
$	VAL = VAL + 1
$	ON WARNING THEN CONTINUE
$	UAF ADD/IDENT 'P1'_EXP /ATTR=(NODYN,RES)
$
$	VAL = VAL + 1
$	ON WARNING THEN CONTINUE
$	UAF ADD/IDENT 'P1'_FIL /ATTR=(NODYN,RES)/VALUE=IDENT:'VAL'
$
$	VAL = VAL + 1
$	ON WARNING THEN CONTINUE
$	UAF ADD/IDENT 'P1'_OPR /ATTR=(NODYN,NORES)/VALUE=IDENT:'VAL'
$
$	DISKQ ADD 'P1'_DES /PERM=15000 /OVER=2000
$	DISKQ ADD 'P1'_WRK /PERM=5000  /OVER=2000
$	DISKQ ADD 'P1'_EXP /PERM=30000 /OVER=2000
$	DISKQ ADD 'P1'_FIL /PERM=30000 /OVER=5000
$!
$ NOIDE:
$	IF P3 .EQS. "N" THEN GOTO P5
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_DES 'P3' /ATTR=(NODYN,RES)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_WRK 'P3' /ATTR=(NODYN,RES)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_JDP 'P3' /ATTR=(NODYN,NORES)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_EXP 'P3' /ATTR=(NODYN,RES)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_FIL 'P3' /ATTR=(NODYN,RES)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_OPR 'P3' /ATTR=(NODYN,NORES)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT  GNS_OPR 'P3'
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT  GNS_PGM 'P3'
$
$ P5:
$	INQUIRE P5 -
        "Desea crear estructura fisica del proyecto ( S / N )      ? "
$	IF P5 .EQS. "N" THEN GOTO INI_PGM
$	IF P5 .NES. "S" THEN GOTO P5
$	@GNSCOM:AMBCRE 'P1'
$
$ INI_PGM:
$	IF P4 .EQS. "N" THEN GOTO FIN_PGM
$	PGM = F$ELEMENT(I,",",P4)
$	IF PGM .EQS. "," THEN GOTO FIN_PGM
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDEN 'P1'_PGM 'PGM' /ATTR=(NORES,NODYN)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDEN 'P1'_WRK 'PGM' /ATTR=(RES,NODYN)
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDEN  GNS_OPR 'PGM'
$
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDEN  GNS_PGM 'PGM'
$
$	INQUIRE P6 -
        "Desea crear area comunicacion programador ''PGM' ( S /N ) ? "
$	IF P6 .EQS. "S" THEN @GNSCOM:PGMCRE 'PGM' 'P1' 
$	I = I + 1
$	GOTO INI_PGM
$ FIN_PGM:
$
$ SET DEF 'NOD''DEV''DIR'
