$ set noverify
$ on control_y then goto cleanup
$ t1 = ""
$ t2 = ""
$ count = 0
$ tottot = 0
$ totfre = 0
$ ver=f$getsyi("version")
$ On warning then exit
$ date=f$extract(0,11,f$time())
$ l1=f$locate(".",f$time())
$ l2=l1-12
$ time=f$extract(12,l2,f$time())
$ If p1 .eqs. "" then goto all
$ cyl=f$getdvi(p1,"DEVCLASS")
$ If cyl .ne. 1 then goto wrong_dev
$ for=f$getdvi(p1,"FOR")
$ If for then goto mount_for
$ lab=f$getdvi(p1,"VOLNAM")
$ If lab .eqs. "" then goto not_mount
$ fre=f$getdvi(p1,"FREEBLOCKS")
$ tot=f$getdvi(p1,"MAXBLOCK")
$ use = tot - fre
$ peru = (use*100)/tot
$ perf = 100 - peru
$ If "''f$length(lab)'" .lt. 8 then lab=lab+"	"
$ s1 = f$length(use) + f$length(peru) + 3
$ s2 = f$length(fre) + f$length(perf) + 3
$ If s1 .lt. 8 then t1 = "	"
$ If s2 .lt. 8 then t2 = "	"
$ goto show
$ all:
$ assign/user freespace.tmp sys$output
$ show device/mounted
$ write sys$output " VAX/VMS ''ver' Disk Usage summary on ''date' at ''time'"
$ write sys$output -
"Device	   Volume Label	       Used Blocks      Free Blocks     Total Blocks"
$ open/read file freespace.tmp
$! read file null
$! read file null
$! read file null
$ read_rec:
$ read/end=done file rec
$ If "''f$locate(":",rec)'" .eqs. "''f$length(rec)'" then goto read_rec
$ disk=f$extract(0,f$locate(":",rec),rec)
$ cyl=f$getdvi("''disk'","DEVCLASS")
$ If cyl .ne. 1 then goto read_rec
$ for=f$getdvi("''disk'","FOR")
$ If for then goto read_rec
$ label=f$getdvi("''disk'","VOLNAM")
$ free=f$getdvi("''disk'","FREEBLOCKS")
$ total=f$getdvi("''disk'","MAXBLOCK")
$ used = total - free
$ peruse = (used*100)/total
$ perfre = 100 - peruse
$ If "''f$length(label)'" .lt. 8 then label=label+"	"
$ s1 = f$length(used) + f$length(peruse) + 3
$ s2 = f$length(free) + f$length(perfre) + 3
$ If s1 .lt. 8 then t1 = "	"
$ If s2 .lt. 8 then t2 = "	"
$ write sys$output "''disk'	''label'	''used'(''peruse'%)''t1'	''free'(''perfre'%)''t2'	''total'"
$ count = count + 1
$ tottot  = tottot  + total
$ totfre = totfre + free
$ pertotfre = (totfre*100)/tottot
$ pertotuse = 100 - pertotfre
$ goto read_rec
$ done:
$ close file
$ If count .le. 1 then goto skip_sum
$ write sys$output ""
$ write sys$output "There are ''totfre' free blocks on ''count' structures."
$ write sys$output "Total disk space is ''pertotuse' percent used."
$ skip_sum:
$ delete/nolog freespace.tmp;*
$ exit
$ show:
$ write sys$output -
"Device	Volume Label	Used Blocks	Free Blocks	Total Blocks"
$ write sys$output "''p1'	''lab'	''use'(''peru'%)''t1'	''fre'(''perf'%)''t2'	''tot'"
$ exit
$ not_mount:
$ write sys$output "Device is not mounted"
$ exit
$ wrong_dev:
$ write sys$output "Device is not file structured"
$ exit
$ mount_for:
$ write sys$output "Device is mounted foreign"
$ exit
$ cleanup:
$ close/error=no file
$ no:
$ open/error=not_open file freespace.tmp
$ close file
$ delete/nolog freespace.tmp;*
$ not_open:
$ exit
