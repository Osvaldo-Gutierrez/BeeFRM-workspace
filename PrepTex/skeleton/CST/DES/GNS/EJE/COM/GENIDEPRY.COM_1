$	@GNSCOM:MNUSYSLNM.COM
$!Parametro P1 : PROYECTO
$	IF F$MODE() .EQS. "BATCH" THEN 'BAT'
$ P1:
$	IF P1 .NES. "" THEN GOTO P2
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P1 "Proyecto (Para abandonar digite solo RETURN)    ? "
$	IF P1 .EQS. "" THEN EXIT
$ P2:
$	IF P2 .NES. "" THEN GOTO MAIN
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P2 "Disco para definir cuotas [Asume ''GNS$DSKQUO'] ? "
$	IF P2 .EQS. "" THEN P2 = GNS$DSKQUO
$ MAIN:
$	I = 0
$ LUPMAIN:
$	ARC = F$SEARC("GNSPAR:GNS$IDEPRY.PAR;1")
$	IF ARC .EQS. "" THEN BSE = 500
$	IF ARC .EQS. "" THEN COR = 1
$	IF ARC .EQS. "" THEN GOTO OPENWRITE
$	ON ERROR THEN GOTO ERROROPEN
$ OPENIDE:
$	OPEN/READ FILE_IDE GNSPAR:GNS$IDEPRY.PAR;1
$	READ/END_OF_FILE=CLOSE FILE_IDE REG
$	CLOSE FILE_IDE
$	BSE = F$ELEMENT(1,"/",REG)
$	COR = F$ELEMENT(2,"/",REG)
$	DELETE/NOLOG/NOCONFIRM GNSPAR:GNS$IDEPRY.PAR;*
$ OPENWRITE:
$	BSENEW = BSE
$	CORNEW = COR + 1
$	IF CORNEW .EQ. 78 THEN BSENEW = BSENEW + 1
$	IF CORNEW .EQ. 78 THEN CORNEW = 1
$	IF F$EXTRACT(F$LENGTH(CORNEW)-1,1,CORNEW) .EQS. "8" THEN -
            CORNEW = CORNEW + 2
$	ON ERROR THEN GOTO ERRORCREA
$	OPEN/WRITE FILE_IDE GNSPAR:GNS$IDEPRY.PAR;1
$	WRITE FILE_IDE "''F$TIME()'" + "/" + "''BSENEW'" + "/" + "''CORNEW'"
$	CLOSE FILE_IDE
$!
$	VAL = BSE * 10000 + COR * 100
$	write sys$output "[1mGENIDEPRY : GENERACION DE IDENTIFICADORES DE PROYECTO[0m"
$	WRITE SYS$OUTPUT "[1mPara asignar como base el valor :''VAL', al Proyecto ''P1'[0m"
$!
$	NOD =	F$PARSE("[]",,,"NODE")
$	DEV =	F$PARSE("[]",,,"DEVICE")
$	DIR =	F$PARSE("[]",,,"DIRECTORY")
$	ON CONTROL_Y THEN GOTO FIN_PGM
$	UAF :== "$SYS$SYSTEM:AUTHORIZE"
$	DISKQ :== "$SYS$SYSTEM:DISKQUOTA"
$	SET DEF SYS$SYSTEM
$	VAL = VAL + 1
$	ON ERROR THEN GOTO ERRADDIDE
$	UAF ADD/IDENT 'P1'_DES /ATTR=(NODYN,RES)/VALUE=IDENT:'VAL'
$
$	VAL = VAL + 1
$	ON ERROR THEN GOTO ERRADDIDE
$	UAF ADD/IDENT 'P1'_WRK /ATTR=(NODYN,RES)/VALUE=IDENT:'VAL'
$	
$	VAL = VAL + 1
$	ON ERROR THEN GOTO ERRADDIDE
$	UAF ADD/IDENT 'P1'_JDP /ATTR=(NODYN,NORES)/VALUE=IDENT:'VAL'
$	
$	VAL = VAL + 1
$	ON ERROR THEN GOTO ERRADDIDE
$	UAF ADD/IDENT 'P1'_PGM /ATTR=(NODYN,NORES)/VALUE=IDENT:'VAL'
$
$	VAL = VAL + 1
$	ON ERROR THEN GOTO ERRADDIDE
$	UAF ADD/IDENT 'P1'_EXP /ATTR=(NODYN,RES)/VALUE=IDENT:'VAL'
$
$	VAL = VAL + 1
$	ON ERROR THEN GOTO ERRADDIDE
$	UAF ADD/IDENT 'P1'_FIL /ATTR=(NODYN,RES)/VALUE=IDENT:'VAL'
$
$	VAL = VAL + 1
$	ON ERROR THEN GOTO ERRADDIDE
$	UAF ADD/IDENT 'P1'_OPR /ATTR=(NODYN,NORES)/VALUE=IDENT:'VAL'
$
$!	DISKQ USE 'P2'
$!	DISKQ ADD 'P1'_DES /PERM=30000 /OVER=3000
$!	DISKQ ADD 'P1'_WRK /PERM=10000 /OVER=1000
$!	DISKQ ADD 'P1'_EXP /PERM=20000 /OVER=2000
$!	DISKQ ADD 'P1'_FIL /PERM=10000 /OVER=1000
$!
$!	ESTA MODIFICACION FUE REALIZADA CON EL OBJETO DE SUPERAR
$!	PROBLEMA DE NO CREACION DE CUOTAS EN DISCO
$!
$	OPEN/WRITE FILE_TMP SYS$LOGIN:TMP.COM
$	WRITE FILE_TMP "$ MCR DISKQUOTA"
$	WRITE FILE_TMP "USE ''P2'" 
$	WRITE FILE_TMP "ADD ''P1'_DES /PERM=30000 /OVER=3000"
$	WRITE FILE_TMP "ADD ''P1'_WRK /PERM=10000 /OVER=1000"
$	WRITE FILE_TMP "ADD ''P1'_EXP /PERM=20000 /OVER=2000"
$	WRITE FILE_TMP "ADD ''P1'_FIL /PERM=10000 /OVER=1000"
$	WRITE FILE_TMP "EXIT"
$	WRITE FILE_TMP "$ EXIT"
$	CLOSE FILE_TMP
$	@SYS$LOGIN:TMP.COM
$	DELETE/NOLOG SYS$LOGIN:TMP.COM;*
$	GOTO FIN_PGM
$ ERROROPEN:
$	I = I + 1
$	IF I .LE. 30 THEN GOTO LUPMAIN
$	ON ERROR THEN GOTO FATALOPEN
$	GOTO OPENIDE
$ FATALOPEN:
$	WRITE SYS$OUTPUT "ERROR AL ABRIR ARCHIVO GNSPAR:GNS$IDEPRY.PAR"
$	WRITE SYS$OUTPUT "INTENTE NUEVAMENTE"
$	GOTO ERR_PGM
$ CLOSE:
$	CLOSE FILE_IDE
$	WRITE SYS$OUTPUT "ERROR AL LEER ARCHIVO GNSPAR:GNS$IDEPRY.PAR"
$	WRITE SYS$OUTPUT "INTENTE NUEVAMENTE"
$	GOTO ERR_PGM
$ ERRORCREA:
$	WRITE SYS$OUTPUT "ERROR AL CREAR ARCHIVO GNSPAR:GNS$IDEPRY.PAR"
$	WRITE SYS$OUTPUT "INTENTE NUEVAMENTE"
$	GOTO ERR_PGM
$ ERRADDIDE:
$	WRITE SYS$OUTPUT "ERROR AL ASIGNAR IDENTIFICADOR EN UAF"
$	WRITE SYS$OUTPUT "INTENTE NUEVAMENTE"
$	GOTO ERR_PGM
$ FIN_PGM:
$	SET DEF 'NOD''DEV''DIR'
$	EXIT
$ ERR_PGM:
$	SET DEF 'NOD''DEV''DIR'
$	GNS$MSG :== "ERROR"
$	INQUIRE P1 "Digite RETURN para continuar"
$	EXIT
