$! IF F$MODE() .EQS. "BATCH" THEN WRITE SYS$OUTPUT -
$!     "[7mERROR:  LENREG No Implementado para BATCH[0m",""
$! IF F$MODE() .EQS. "BATCH" THEN EXIT
$	IF F$MODE() .EQS. "BATCH" THEN 'BAT'
$
$	I = 0
$ P1:
$	IF P1 .NES. "" THEN GOTO P2
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P1 "Registro ? "
$	IF P1 .NES. "" THEN GOTO P2
$	IF I .EQ. 1  THEN EXIT
$	I = 1
$	GOTO P1
$ P2:
$	IF F$MODE() .EQS. "BATCH" THEN GOTO MAIN
$	IF P2 .NES. "" THEN GOTO MAIN
$	INQUIRE P2 "Desp. Contadores */N ? "
$ P3:
$	IF P3 .NES. "" .OR. P3 .EQS. "NOSTR" THEN GOTO MAIN
$	INQUIRE P3 "String de Busqueda   ? "
$ P4:
$	IF P4 .NES. "" .OR. P4 .EQS. "NOSTR" THEN GOTO MAIN
$	INQUIRE P4 "String de Termino    ? "
$ MAIN:
$	IF P3 .EQS. "NOSTR" THEN P3 = ""
$	IF P4 .EQS. "NOSTR" THEN P4 = ""
$	ON WARNING   THEN GOTO FIN
$	ON CONTROL_Y THEN GOTO FIN
$	@GNSCOM:FIL 'P1'
$	IF GNS$EXT .EQS. "" THEN GNS$EXT :=="TXT"
$	ARC = F$SEARCH("''GNS$DIR'''GNS$FIL'.''GNS$EXT'")
$	IF ARC .EQS. "" THEN GOTO FILE_NO
$	OPEN/READ FILE_INPUT 'GNS$DIR''GNS$FIL'.'GNS$EXT'
$	ON WARNING   THEN GOTO CLO
$	ON CONTROL_Y THEN GOTO ERR
$	CNT = 0
$	C01 = 0
$ LEECMT:
$	READ/END_OF_FILE=CLO FILE_INPUT REG
$	IF F$EXTRACT(7,1,REG) .EQS. "0" THEN GOTO IMPANT
$       WRITE SYS$OUTPUT REG
$	GOTO LEECMT
$ IMPANT:
$       WRITE SYS$OUTPUT REG
$ LEE:
$	READ/END_OF_FILE=CLO FILE_INPUT REG
$	IF F$EXTRACT(6,1,REG) .EQS. "*" THEN GOTO SKIP
$ CONT:
$	IF F$LOCATE("REDEFINE",REG) .NES. F$LENGTH(REG) 
$          THEN
$	WRITE SYS$OUTPUT "","[1m[A[K",-
	"	========  INI REDEFINICION ======== [0m"
$          GOTO LEE2
$       ENDIF
$	IF P3 .NES. ""
$         THEN
$            IF F$LOCATE(P3,REG) .NES. F$LENGTH(REG)
$              THEN
$                WRITE SYS$OUTPUT " "
$                WRITE SYS$OUTPUT "[1m[A[K","''REG'[0m"
$!                WRITE SYS$OUTPUT "[7m ''REG' [0m"
$                GOTO SKIP
$            ENDIF
$                WRITE SYS$OUTPUT REG
$         ELSE
$            WRITE SYS$OUTPUT REG
$       ENDIF
$ SKIP:
$	IF P4 .NES. "" THEN -
$		IF F$LOCATE(P4,REG) .NES. F$LENGTH(REG) THEN GOTO CLO
$
$	IF F$EXTRACT(6,1,REG) .EQS. "*" THEN GOTO LEE
$
$     !!!	Considerar REDEFINES en un solo nivel	     !!!
$
$	IF F$EXTRACT(7,3,REG) .EQS. "01 " THEN C01 = C01 + 1
$	IF C01 .GT. 1 THEN GOTO CLO
$	PIC = F$ELEMENT(1,"(",REG)
$	IF PIC .EQS. "(" THEN GOTO LEE
$	ENT = F$ELEMENT(0,")",PIC)
$	IF ENT .EQS. ")" THEN GOTO LEE
$
$	DEC = 0
$	PDC = F$ELEMENT(2,"(",REG)
$	IF PDC .EQS. "(" THEN GOTO CNT
$	PDC = F$ELEMENT(0,")",PDC)
$	IF PDC .EQS. ")" THEN GOTO CNT
$	DEC = PDC
$ CNT:
$	COMP3 = 0
$	COMP  = 0
$	CEN = 0
$	IF F$LOCATE("COMP-3",REG) .NES. F$LENGTH(REG) THEN COMP3 = 1
$	IF F$LOCATE("COMP ",REG)  .NES. F$LENGTH(REG) THEN COMP  = 1
$	CEN = CEN + ENT + DEC
$	IF COMP  THEN GOTO COMP
$	IF COMP3 THEN GOTO COMP3
$	GOTO CON
$ COMP:
$	LEN = CEN
$	IF                   CEN .LE.  4 THEN LEN = 2
$	IF CEN .GE.  5 .AND. CEN .LE.  9 THEN LEN = 4
$	IF CEN .GE. 10 .AND. CEN .LE. 18 THEN LEN = 8
$	CEN = LEN
$	GOTO CON
$ COMP3:
$	CEN = ( CEN / 2 ) + 1
$	GOTO CON
$ CON:
$	POC = F$LOCATE("OCCUR",REG)
$	IF POC .EQS. F$LENGTH(REG) THEN GOTO SUM
$	POC = F$EXTRAC(POC,F$LENGTH(REG),REG)
$	OCC = F$ELEMENT(1," ",POC)
$	IF OCC .EQS. " " THEN GOTO SUM
$	OCC = F$ELEMENT(0," ",OCC)
$	IF OCC .EQS. " " THEN GOTO SUM
$	CEN = CEN * OCC
$ SUM:
$	CNT = CNT + CEN
$
$! Pregunta si desea desplegar los contadores
$	IF P2 .EQS. "N" .OR. P2 .EQS. "n" THEN GOTO LEE
$       WRITE SYS$OUTPUT " "
$	WRITE SYS$OUTPUT "Lrg. cpo. antr --->","[7m''CEN'[0m"
$	WRITE SYS$OUTPUT "Pos. cpo. sgte --->","[7m''CNT'[0m"
$       WRITE SYS$OUTPUT " "
$	GOTO LEE
$ FILE_NO:
$	WRITE SYS$OUTPUT "[1m[A[K",-
	"	No existe archivo ''GNS$DIR'''GNS$FIL'.''GNS$EXT'[0m"
$	EXIT
$ ERR:
$	WRITE SYS$OUTPUT "","[1m[A[K",-
	"	             ERROR: LENREG Interrumpido !!! [0m"
$	CLOSE FILE_INPUT
$	EXIT
$ LEE2:
$       WRITE SYS$OUTPUT REG
$       REG2 = F$EDIT(REG,"COMPRESS, TRIM") 
$       REDE = F$EXTRACT(0,3,REG2)
$ LOOP2:
$	READ/END_OF_FILE=CLO FILE_INPUT REG
$	IF F$EXTRACT(6,1,REG) .EQS. "*" THEN GOTO LOOP2
$       WRITE SYS$OUTPUT REG
$       REG2 = F$EDIT(REG,"COMPRESS, TRIM") 
$	IF F$EXTRACT(0,3,REG2) .EQS. REDE 
$          THEN 
$    		WRITE SYS$OUTPUT "","[1m[A[K",-
		"	========  FIN REDEFINICION ======== [0m"
$               GOTO CONT
$       ENDIF
$       GOTO LOOP2
$ CLO:
$	CLOSE FILE_INPUT
$	WRITE SYS$OUTPUT "Largo Registro ====> ","[7m''CNT'[0m"
$	GNS$LRE :== 'CNT'
$ FIN:
$	EXIT
