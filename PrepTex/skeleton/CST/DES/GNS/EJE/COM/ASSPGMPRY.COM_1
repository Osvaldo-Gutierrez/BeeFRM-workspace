$	@GNSCOM:MNUSYSLNM.COM
$! P1 : PROYECTO
$! P2 : PROGRAMADORES SEPARADOS POR COMA
$! P3 : DIRECTORIO RAIZ
$	IF F$MODE() .EQS. "BATCH" THEN 'BAT'
$ P1:
$	IF P1 .NES. "" THEN GOTO P2
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	INQUIRE P1 "Proyecto ? "
$	IF P1 .EQS. "" THEN EXIT
$ P2:
$	IF P2 .NES. "" THEN GOTO P3
$	IF F$MODE() .EQS. "BATCH" THEN EXIT
$	WRITE SYS$OUTPUT "Ingrese Programadores ( separados por coma )"
$	INQUIRE P2 "( Si NO desea asignar solo digite RETURN ) ? "
$	IF P2 .EQS. "" THEN EXIT
$ P3:
$	IF P3 .NES. "" THEN GOTO MAIN
$	IF F$MODE() .EQS. "BATCH" THEN GOTO P3BLK
$	WRITE SYS$OUTPUT "--------------------------------------------------------------------------------"
$	WRITE SYS$OUTPUT "Ingrese disco y directorio bajo el cual se creara el proyecto"
$	WRITE SYS$OUTPUT "        Asume ''GNS$DSKQUO':''GNS$ROTPRY'DES]"
$	INQUIRE P3 "Directorio Raiz ( Disco:[directorio] ) ? "
$ P3BLK:
$	IF P3 .EQS. "" THEN P3 = "''GNS$DSKQUO':''GNS$ROTPRY'DES]"
$ MAIN:
$	write sys$output "[1mASSPGMPRY : ASIGNACION DE PROGRAMADORES A PROYECTO[0m"
$	write sys$output "[1mProyecto                    : ''p1'[0m"
$!
$	NOD =	F$PARSE("[]",,,"NODE")
$	DEV =	F$PARSE("[]",,,"DEVICE")
$	DIR =	F$PARSE("[]",,,"DIRECTORY")
$	ON CONTROL_Y THEN GOTO FIN
$	UAF :== "$SYS$SYSTEM:AUTHORIZE"
$!Asignando Identificadores de programador a 'PGM'
$	SET DEF SYS$SYSTEM
$	I = 0
$ LUP_IDE:
$	PGM = F$ELEMENT(I,",",P2)
$	IF PGM .EQS. "" THEN GOTO NXT_IDE
$	IF PGM .EQS. "," THEN GOTO CON
$	write sys$output "[1mIdentificador a Programador : ''pgm'[0m"
$	ON ERROR THEN GOTO FIN
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_PGM 'PGM' /ATTR=(NORES,NODYN)
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT 'P1'_WRK 'PGM' /ATTR=(RES,NODYN)
$	ON WARNING THEN CONTINUE
$	UAF GRANT/IDENT GNS_OPR 'PGM' /ATTR=(NORES,NODYN)
$	ON WARNING THEN CONTINUE
$	IF P1 .NES. "GNS" THEN UAF GRANT/IDENT GNS_PGM 'PGM' /ATTR=(NORES,NODYN)
$ NXT_IDE:
$	I = I + 1
$	GOTO LUP_IDE
$ CON:
$!Creando Areas de Comunicacion en el proyecto
$	SET DEF 'P3'
$	I = 0
$ LUP_WRK:
$	PGM = F$ELEMENT(I,",",P2)
$	IF PGM .EQS. "" THEN GOTO NXT_WRK
$	IF PGM .EQS. "," THEN GOTO FIN
$	write sys$output "[1mDirectorio a Programador    : ''pgm'[0m"
$	CREATE/DIR [.'P1'.WRK.JDP.'PGM']
$	ON ERROR THEN GOTO FIN
$	SET FILE/ACL=(IDENT='P1'_PGM,OPT=DEF+PROT,ACC=R+E) -
							[.'P1'.WRK.JDP]'PGM'.DIR
$	SET FILE/ACL=(IDENT='P1'_PGM,OPT=PROT,    ACC=R+E) -
							[.'P1'.WRK.JDP]'PGM'.DIR
$	SET FILE/ACL=(IDENT=['PGM'],OPT=DEF+PROT,ACC=R+W+E+D) -
							[.'P1'.WRK.JDP]'PGM'.DIR
$	SET FILE/ACL=(IDENT=['PGM'],OPT=PROT,    ACC=R+W+E)   -
							[.'P1'.WRK.JDP]'PGM'.DIR
$	SET FILE/ACL=(IDENT='P1'_JDP,OPT=DEF+PROT,ACC=R+W+E+D+C) -
							[.'P1'.WRK.JDP]'PGM'.DIR
$	SET FILE/ACL=(IDENT='P1'_JDP,OPT=PROT,    ACC=R+W+E+C)   -
							[.'P1'.WRK.JDP]'PGM'.DIR
$	CREATE/DIR [.'P1'.WRK.PGM.'PGM']
$	SET FILE/ACL=(IDENT='P1'_PGM,OPT=DEF+PROT,ACC=R+E) -
							[.'P1'.WRK.PGM]'PGM'.DIR
$	SET FILE/ACL=(IDENT='P1'_PGM,OPT=PROT,    ACC=R+E) -
							[.'P1'.WRK.PGM]'PGM'.DIR
$	SET FILE/ACL=(IDENT=['PGM'],OPT=DEF+PROT,ACC=R+W+E+D) -
							[.'P1'.WRK.PGM]'PGM'.DIR
$	SET FILE/ACL=(IDENT=['PGM'],OPT=PROT,    ACC=R+W+E)   -
							[.'P1'.WRK.PGM]'PGM'.DIR
$	SET FILE/ACL=(IDENT='P1'_JDP,OPT=DEF+PROT,ACC=R+W+E+D+C) -
							[.'P1'.WRK.PGM]'PGM'.DIR
$	SET FILE/ACL=(IDENT='P1'_JDP,OPT=PROT,    ACC=R+W+E+C)   -
							[.'P1'.WRK.PGM]'PGM'.DIR
$!	Crear area con abreviacion de USERNAME
$	IF GNS$USR .NES. "CST" THEN GOTO NXT_WRK
$	@GNSCOM:SETMAQ'GNS$USR' "''PGM'"
$	IF GNS$ABR .EQS. "" THEN WRITE SYS$OUTPUT "Usuario ''PGM' no tiene abreviacion asociada"
$	IF GNS$ABR .EQS. "" .OR. GNS$ABR .EQS. "DES" THEN GOTO NXT_WRK
$	ROT = F$ELEMENT(0,".",P3)
$	PPT = F$LOCATE(".",P3)
$	CMP = F$EXTRACT(PPT+4,F$LENGTH(P3),P3)
$	ARC = ROT + "." + GNS$ABR + CMP
$	CREATE/DIR/OWN='PGM' 'ARC'
$ NXT_WRK:
$	I = I + 1
$	GOTO LUP_WRK
$ FIN:
$	SET DEF 'NOD''DEV''DIR'
