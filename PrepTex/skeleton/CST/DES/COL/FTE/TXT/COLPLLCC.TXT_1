*%! Codigo adicional para archivo
*% IF WSS
      *<<<< WSS
       COPY GNSBRMSC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY COLWGVEN.
       COPY SGCBRDBC.
       COPY SGCBRRCD.
       COPY COLBROPC.
       COPY COLBRRDC.
       COPY COLBRICG.
       COPY COLBRCYA.
       COPY COLBREVC.
       COPY COLBRVEN.
       COPY COLBRDLC.
       COPY COLBRRCV.
       COPY COLBRTOC.
       COPY TABBROFI.
       COPY TABBRUSR.
       COPY TABBRVLR.
       COPY TABBRCAM.
      *DAD ini  14-AGO-1991 / 23-ABR-1991 
       COPY COLWGCTO.
       COPY TABBRTIO.
      *DAD fin  14-AGO-1991 / 23-ABR-1991 
       COPY GNSWGPES.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY GNSWGCVI.
       COPY GNSWGCPT.
       COPY GNSBRRCE.
       COPY GNSBRCMT.
      *DAD ini  03-ENE-1992   (Memo.02-01-92)
       COPY GNSWGCMM.
      *DAD fin  03-ENE-1992

       01  WSS-VARI.
           03  WSS-SGV-LIQU             COMP-3       PIC 9(11)V9(4).
           03  WSS-SGV-CAPI             COMP-3       PIC S9(11)V9(4).
           03  WSS-SGV-FINA             COMP-3       PIC S9(11)V9(4).
           03  WSS-SGV-FNAL             COMP-3       PIC S9(11)V9(4).
           03  WSS-NUM-IVCT                          PIC 9(03).
           03  WSS-COD-VCOC.
               05  WSS-MSC-TVOC                      PIC X(01).
               05  WSS-COD-CVOC                      PIC X(03).
               05  WSS-COD-TCOC                      PIC X(02).
           03  WSS-CIC-MRXL                          PIC X(12).
           03  WSS-DCO-MRXL                          PIC X(12).
           03  WSS-DES-MRXL                          PIC X(40).
           03  WSS-ABR-MRXL                          PIC X(05).
           03  WSS-CIC-VCOC                          PIC X(06).
           03  WSS-DCO-VCOC                          PIC X(12).
           03  WSS-ABR-VCOC                          PIC X(40).
           03  WSS-COD-TVOC.
               05  WSS-MSC-TVAL                      PIC X(01).
               05  WSS-MSC-REST                      PIC X(04).
           03  WSS-DCO-TCYA                          PIC X(12).
           03  WSS-ABR-TCYA                          PIC X(05).
           03  WSS-DES-M6AL                          PIC X(40).
           03  WSS-NUM-ICYA                          PIC 9(06).
      *DAD ini  27-MAR-1991  era 6 antes
           03  WSS-IMAX                VALUE 5       PIC 9(02).
      *DAD fin  27-MAR-1991 
           03  WSS-ABR-MOV                           PIC X(05).
           03  WSS-GLS-SPAC            VALUE SPACES  PIC X(01).

           03  WSS-COD-VCAM.
               05  WSS-COD-TVTI                      PIC X(01).
               05  WSS-COD-CVTI                      PIC X(03).
               05  WSS-COD-TCTI                      PIC X(02).
           03  WSS-GLS-ICAN.
               05  WSS-CIC-ICAN                      PIC X(12).
               05  WSS-NUM-ICAN                      PIC 9(03).
           03  WSS-IND-SEL1                          PIC X(01).
           03  WSS-FEC-FCMP.
               05  WSS-NUM-SCMP                      PIC 9(02).
               05  WSS-NUM-ACMP                      PIC 9(02).
               05  WSS-NUM-MCMP                      PIC 9(02).
               05  WSS-NUM-DCMP                      PIC 9(02).
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-DTER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
           03  WSS-COD-OFIC             VALUE SPACES PIC X(03).
           03  WSS-CIC-IOPC.
               05  WSS-CAI-IOPC         VALUE SPACES PIC X(04).
               05  WSS-IIC-IOPC         VALUE SPACES PIC X(08).
           03  WSS-COD-LUGA.
               05 WSS-COD-COMU                       PIC X(04).
               05 WSS-GLS-PAIS REDEFINES WSS-COD-COMU.
                  07 WSS-IND-PAIS                    PIC X(01).
                  07 WSS-COD-PAIS                    PIC X(03).
      *DAD ini  27-MAR-1991 
           03 WSS-FEC-FBAS.
              05 WSS-NUM-DBAS                     PIC 9(02).
              05 FILLER              VALUE '-'    PIC X(01).
              05 WSS-NUM-MBAS                     PIC 9(02).
              05 FILLER              VALUE '-'    PIC X(01).
              05 WSS-NUM-SBAS                     PIC 9(02).
              05 WSS-NUM-ABAS                     PIC 9(02).
      *DAD fin  27-MAR-1991 
      *DAD ini  16-ABR-1991 
           03 WSS-COD-COOC.
              05 WSS-COD-TOOC                     PIC X(03).
              05 WSS-COD-AOOC                     PIC X(03).
           03 WSS-GLS-DATA.
              05 WSS-GLS-TIO                      PIC X(40).
              05 WSS-GLS-AUX                      PIC X(40).
           03 WSS-COD-TINT                        PIC X(12).
      *DAD fin  16-ABR-1991 
      *DAD ini  23-ABR-1991 
           03 WSS-SGV-MCAN           COMP-3       PIC S9(11)V9(4).
      *DAD fin  23-ABR-1991 
      *DAD ini  14-JUN-1991 
           03 WSS-SGV-MORA           COMP-3       PIC 9(11)V9(4).
      *DAD fin  14-JUN-1991 
      *DAD ini  03-ENE-1992 / 22-JUL-1991
           03 WSS-IND-FCYA                        PIC 9(01).
      *DAD fin  03-ENE-1992 / 22-JUL-1991
      *>>>>
*% END
*% IF RW
      *<<<< RW
       COPY COLRWLC2.
      *>>>>
*% END
*% IF RT
      *<<<< RT
       COPY COLRTLC2.
      *>>>>
*% END
*% IF WSS_DTC
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CMT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VLR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *>>>>
*% END
*% IF INI_INP
      *<<<<
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      *Captura Codigo de Operacion
           DISPLAY 'ASIGNACION OPERACION : '.
           ACCEPT  WSS-CAI-IOPC IN WSS-VARI.
           DISPLAY WSS-CAI-IOPC IN WSS-VARI.
           DISPLAY 'IDENTIFICACION OPERACION : '.
           ACCEPT  WSS-IIC-IOPC IN WSS-VARI.
           DISPLAY WSS-IIC-IOPC IN WSS-VARI.
      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.

           DISPLAY 'FIN  CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

           IF WSS-CIC-IOPC IN WSS-VARI > SPACES
               IF WSS-CIC-IOPC IN WSS-VARI = '*'
                   MOVE '*' TO WSS-IND-SEL1 IN WSS-VARI
                   GO TO NXT-INP
               ELSE
                   MOVE 'O' TO WSS-IND-SEL1 IN WSS-VARI
                   GO TO NXT-INP.

      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.

      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB'         TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DCMP IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MCMP IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-SCMP IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-ACMP IN WSS-VARI.

           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFIC IN WSS-VARI
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS = 'COD    NEX'
                   DISPLAY MSG-GLS-MENS
                   MOVE PGM-SMAL TO PGM-STAT-SRT
                   GO TO FIN-INP-SRT.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
       NXT-INP.
           MOVE 'COL' TO TAB-COD-SIST.
           MOVE 'LCC' TO TAB-COD-TTAB IN TAB.
           MOVE 'MRX' TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY MSG-GLS-DESC ' : ' TAB-COD-TABL
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           MOVE TAB-GLS-DESC IN TAB TO WSS-DES-MRXL IN WSS-VARI.
           MOVE TAB-GLS-DCOR IN TAB TO WSS-DCO-MRXL IN WSS-VARI.
           MOVE TAB-GLS-ABRV IN TAB TO WSS-ABR-MRXL IN WSS-VARI.
           MOVE TAB-CIC-CTAB IN TAB TO WSS-CIC-MRXL IN WSS-VARI.

           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF FIO-STAT-OKS
               MOVE MSC-COD-CMSC IN MSC TO WSS-COD-TVOC IN WSS-VARI
           ELSE
               DISPLAY 'NO EXISTE CODIGO 0 EN TABLA TVAL DE TAB'
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.

           MOVE 'COL' TO TAB-COD-SIST.
           MOVE 'LCC' TO TAB-COD-TTAB IN TAB.
           MOVE 'M6A' TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY MSG-GLS-DESC ' : ' TAB-COD-TABL
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           MOVE TAB-GLS-DESC IN TAB TO WSS-DES-M6AL IN WSS-VARI.

      *DAD ini  14-AGO-1991
      * Proceso para Cargar las tablas  TAB/TIO  y  COL/TOC  en Memoria
           PERFORM COL-CRG-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
      *DAD fin  14-AGO-1991

      *DAD ini  03-ENE-1992   (Memo.02-01-92)
      * Proceso para Cargar las tablas  MSC/TCYA  en Memoria
           MOVE 'TAB'  TO CMM-SIST IN CMM-VARI.
           MOVE 'TCYA' TO CMM-TMSC IN CMM-VARI.
           PERFORM GNS-CRG-CMM.
           IF CMM-CMSG IN CMM-VARI > SPACES
               MOVE 'GNS'                TO MSG-COD-SIST
               MOVE CMM-CMSG IN CMM-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CMM-MEN2 IN CMM-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
      *DAD fin  03-ENE-1992
           DISPLAY 'Fin Validacion de Parametros'.
      *>>>>
*% END
*% IF INI_FST_INP
      *<<<< INI_FST_INP
           IF WSS-IND-SEL1 IN WSS-VARI = '*'
               GO TO CON-INP-SRT.

           IF WSS-IND-SEL1 IN WSS-VARI = 'O'
               MOVE WSS-CIC-IOPC IN WSS-VARI TO CAN-CIC-IOPC IN CAN
               MOVE ZEROES                   TO CAN-NUM-ICAN IN CAN
               MOVE FIO-GET-NLS TO FIO-CMND
               PERFORM COL-FIO-CAN
               IF NOT ( FIO-STAT-OKS AND
                        CAN-CIC-IOPC IN CAN = WSS-CIC-IOPC IN WSS-VARI )
                   DISPLAY 'CANCELACION INEXISTENTE : ' 
                           CAN-CIC-IOPC IN CAN
                   MOVE PGM-SMAL TO PGM-STAT-SRT
                   GO TO FIN-INP-SRT
               ELSE
                   IF CAN-MSC-STAT IN CAN NOT = 'A' AND
                      CAN-MSC-STAT IN CAN NOT = 'R'
                       DISPLAY 'CANCELACION NO ACTIVADA : ' 
                               CAN-CIC-IOPC IN CAN
                       MOVE PGM-SMAL TO PGM-STAT-SRT
                       GO TO FIN-INP-SRT
                   ELSE
                       GO TO LUP-INP-SRT.

      *DAD ini  14-JUN-1991 
           MOVE WSS-NUM-DINI IN WSS-VARI TO CAN-NUM-DOUT IN CAN.
           MOVE WSS-NUM-MINI IN WSS-VARI TO CAN-NUM-MOUT IN CAN.
           MOVE WSS-NUM-SINI IN WSS-VARI TO CAN-NUM-SOUT IN CAN.
           MOVE WSS-NUM-AINI IN WSS-VARI TO CAN-NUM-AOUT IN CAN.
           MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
      *DAD fin  14-JUN-1991 
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.

      *DAD ini  14-JUN-1991 
           IF CAN-FEC-FOUT IN CAN > WSS-FEC-FCMP IN WSS-VARI
      *DAD fin  14-JUN-1991 
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT
           ELSE
               GO TO LUP-INP-SRT.
       CON-INP-SRT.
      *>>>>
*% END
*% IF LUP_INP
      *<<<< LUP_INP
      *DAD ini  23-ABR-1991 
      * Selecciona SOLO Cancelaciones Activadas y Positivas excluyendo las
      * Cancelaciones Reversadas o Negativas
      *     IF CAN-MSC-STAT IN CAN NOT = 'A' AND
      *        CAN-MSC-STAT IN CAN NOT = 'R'
           IF CAN-MSC-STAT IN CAN NOT = 'A'
               GO TO NXT-INP-SRT.

           MOVE ZEROES TO WSS-SGV-MCAN IN WSS-VARI.
           ADD CAN-SGV-TPAG IN CAN
               CAN-SGV-TREN IN CAN
               CAN-SGV-DIFE IN CAN GIVING WSS-SGV-MCAN IN WSS-VARI.

           IF WSS-SGV-MCAN IN WSS-VARI NOT > ZEROES
               GO TO NXT-INP-SRT.
      *DAD fin  23-ABR-1991 

      *DAD ini  25-OCT-1991
      * Excluye Cancelaciones realizadas desde el SAFE o en forma BATCH
           IF CAN-COD-OTRN IN CAN = 'SAFE' OR 'BACH'
               GO TO NXT-INP-SRT.
      *DAD fin  25-OCT-1991

           IF WSS-CIC-IOPC IN WSS-VARI NOT > SPACES AND
              WSS-COD-OFIC IN WSS-VARI     > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFIC IN WSS-VARI NOT = CAN-COD-OFCA IN CAN
                   GO TO NXT-INP-SRT.

           MOVE CAN-CIC-IOPC IN CAN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE OPERACION : ' OPC-CIC-IOPC IN OPC
               GO TO NXT-INP-SRT.

      *DAD ini  03-ENE-1992   (Memo.02-01-92)
      * Lee el primer CYA asociado a la Cancelacion y Excluye la CAN si ella
      * NO es con Cargo en Cuenta Corriente
           PERFORM LEE-FRST-CYA.
           IF WSS-IND-FCYA IN WSS-VARI = 1 OR
              ( MSC-CIC-MSCL IN MSC NOT = 'TCYACC' AND
                MSC-CIC-MSCL IN MSC NOT = 'TCYACCGCAN' )
               GO TO NXT-INP-SRT.
      *DAD fin  03-ENE-1992

      *DAD ini  25-OCT-1991
      * Oficina del Credito para ordenamiento y titulo sucursal del Reporte
           MOVE OPC-COD-OFOC IN OPC TO SRT-COD-OFOC IN SRT.
      *DAD fin  25-OCT-1991

      *DAD ini  14-AGO-1991 / 23-ABR-1991
      * Busca con el Codigo Tipo Operacion en la Tabla cargada en Memoria el
      * 'Sistema que lo Maneja' y 'Opera Como'
           MOVE OPC-COD-TOOC IN OPC TO CTO-TOOC IN CTO-VARI.
           PERFORM COL-BUS-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
                       'CON CODIGO : '     CTO-TOOC IN CTO-VARI
                       ' EN CREDITO : '    OPC-CIC-IOPC IN OPC
               GO TO NXT-INP-SRT.

      * Valida el 'Sistema que lo Maneja' de la Operacion de Credito
           IF CTO-SIST IN CTO-VARI NOT = 'COL'
               GO TO NXT-INP-SRT.
      *DAD fin  23-ABR-1991

      *DAD ini  16-ABR-1991
      * Valida el 'Opera Como' de la Operacion de Credito
           IF CTO-OPEC IN CTO-VARI = 'D'
               GO TO NXT-INP-SRT.

      * Obtiene 'Opera Como'
           MOVE SPACES               TO TOC.
           MOVE CTO-OPEC IN CTO-VARI TO TOC-MSC-OPEC IN TOC.
      *DAD fin  14-AGO-1991 / 16-ABR-1991

      *DAD ini  03-ENE-1992 / 22-JUL-1991   ( por ahora no )
      * Lee el primer CYA asociado a la Cancelacion Excluyendo aquellas Boletas
      * de Garantia ( Opera Como = 'B' ) con Cargo en Cuenta Corriente
      *     IF TOC-MSC-OPEC IN TOC = 'B'
      *         PERFORM LEE-FRST-CYA
      *         IF WSS-IND-FCYA IN WSS-VARI = 1 OR
      *            MSC-CIC-MSCL IN MSC = 'TCYACC' OR
      *            MSC-CIC-MSCL IN MSC = 'TCYACCGCAN'
      *             GO TO NXT-INP-SRT.
      *DAD fin  03-ENE-1992 / 22-JUL-1991

      * Con la Llave de la Cancelacion forma campo del registro SRT
           MOVE CAN-CIC-IOPC IN CAN      TO WSS-CIC-ICAN IN WSS-VARI.
           MOVE CAN-NUM-ICAN IN CAN      TO WSS-NUM-ICAN IN WSS-VARI.
           MOVE WSS-GLS-ICAN IN WSS-VARI TO SRT-GLS-ICAN IN SRT.

      * Obtiene Antecedentes del Cliente para titulos del Reporte que forman
      * parte del registro SRT
           PERFORM BUS-DEU-DIR.
      *>>>>
*% END
*% IF INI_NXT_INP
      *<<<< INI_NXT_INP
           IF WSS-CIC-IOPC IN WSS-VARI NOT > SPACES
      *DAD ini  14-JUN-1991 
               MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
      *DAD fin  14-JUN-1991 
      *>>>>
*% END
*% IF FIN_NXT_INP
      *<<<< FIN_NXT_INP
           IF WSS-IND-SEL1 IN WSS-VARI = '*'
               GO TO LUP-INP-SRT.

           IF WSS-IND-SEL1 IN WSS-VARI = 'O'
               IF WSS-CIC-IOPC IN WSS-VARI NOT = CAN-CIC-IOPC IN CAN
                   GO TO FIN-INP-SRT
               ELSE
                   GO TO LUP-INP-SRT.

      *DAD ini  14-JUN-1991 
           IF CAN-FEC-FOUT IN CAN > WSS-FEC-FCMP IN WSS-VARI
      *DAD fin  14-JUN-1991 
               GO TO FIN-INP-SRT.
      *>>>>
*% END
*% IF LUP_OUT
      *<<<< LUP_OUT
      *DAD ini  25-OCT-1991
      * Obtiene glosas para titulos del Reporte que forman parte de LC1
           IF CAN-COD-OFCA IN SRT > SPACES
               MOVE CAN-COD-OFCA IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFCA IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-OFCA IN WSS-LC1.
      *DAD fin  25-OCT-1991

           IF OPC-COD-VCOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-CIC-CTAB IN TAB TO WSS-CIC-VCOC IN WSS-VARI
               MOVE TAB-GLS-DCOR IN TAB TO WSS-DCO-VCOC IN WSS-VARI
               MOVE TAB-GLS-ABRV IN TAB TO WSS-ABR-VCOC IN WSS-VARI
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-VCOC IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-VCOC IN WSS-LC1.

           IF CAN-COD-RCAN IN SRT > SPACES
               MOVE CAN-COD-RCAN IN SRT TO USR-COD-USER IN USR
               PERFORM BUS-USR
               MOVE USR-GLS-DESC IN USR TO WSS-GLS-RCAN IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-RCAN IN WSS-LC1.

      *DAD ini  16-ABR-1991 
      * Mediante BUS-TAB Busca Tipo de Operacion Credito de Tabla TIO
           MOVE OPC-COD-COOC IN SRT TO WSS-COD-COOC IN WSS-VARI.
           IF WSS-COD-TOOC IN WSS-VARI > SPACES
               MOVE 'TAB'                    TO TAB-COD-SIST
               MOVE 'TIO'                    TO TAB-COD-TTAB IN TAB
               MOVE WSS-COD-TOOC IN WSS-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-TIO IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-GLS-TIO IN WSS-VARI.

      * Mediante BUS-TAB Busca Clase de Operacion Credito de Tabla AUX
           IF OPC-COD-COOC IN SRT > SPACES
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-COOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-AUX IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-GLS-AUX IN WSS-VARI.

      * Se concatenan las glosas de TIO y de AUX
           MOVE WSS-GLS-DATA IN WSS-VARI TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO WSS-GLS-COOC IN WSS-LC1.
      *DAD fin  16-ABR-1991 

      * Obtiene Antecedentes de la Tasa del Credito
           PERFORM BUS-ICG.
      * Obtiene glosas asociadas a la Tasa del Credito que forman parte de LC1
           IF FIO-STAT-OKS
               PERFORM BUS-GLS-ICG
           ELSE
      *DAD ini  16-ABR-1991 
               MOVE SPACES TO WSS-COD-TINT IN WSS-VARI
               MOVE ZEROES TO WSS-SGV-TINT IN WSS-LC1
               MOVE ZEROES TO WSS-NUM-PREP IN WSS-LC1
      *DAD fin  16-ABR-1991 
               MOVE SPACES TO WSS-GLS-BCTI IN WSS-LC1
               MOVE SPACES TO WSS-GLS-PBTI IN WSS-LC1
               MOVE SPACES TO WSS-GLS-MCTI IN WSS-LC1
               MOVE SPACES TO WSS-GLS-CVTI IN WSS-LC1
               MOVE SPACES TO WSS-GLS-TFPR IN WSS-LC1
               MOVE SPACES TO WSS-GLS-UTPR IN WSS-LC1.

      *------------------- DETALLE VENCIMIENTO ---------------------------------
      * Acumula para diferentes CAN de un mismo VEN el Valor Capital y Final
      * Cancelado que esta en varios RCV 
           PERFORM BUS-RCV.

      * Lee el primer RCV que esta asociado a la CAN
           MOVE CAN-CAI-IOPC IN SRT TO RCV-CAI-IOPC IN RCV.
           MOVE CAN-IIC-IOPC IN SRT TO RCV-IIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN SRT TO RCV-NUM-ICAN IN RCV.
           MOVE CAN-CAI-IOPC IN SRT TO RCV-CAI-IOPV IN RCV.
           MOVE CAN-IIC-IOPC IN SRT TO RCV-IIC-IOPV IN RCV.
           MOVE ZEROES              TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS AND
                    RCV-CAI-IOPC IN RCV = CAN-CAI-IOPC IN SRT AND
                    RCV-IIC-IOPC IN RCV = CAN-IIC-IOPC IN SRT AND
                    RCV-NUM-ICAN IN RCV = CAN-NUM-ICAN IN SRT AND
                    RCV-CAI-IOPV IN RCV = CAN-CAI-IOPC IN SRT AND
                    RCV-IIC-IOPV IN RCV = CAN-IIC-IOPC IN SRT )
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CAN    NORCV' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ':' CAN-CAI-IOPC IN SRT 
                                        CAN-IIC-IOPC IN SRT
               GO TO NXT-OUT-SRT.

      *DAD ini  16-ABR-1991 / 27-MAR-1991 
      * Si Credito tiene Tasa Variable c/r al Flujo se llena la linea detalle
      * Obtener VEN anterior para leer en tabla CAM el TIM base con menor fecha
      * entre FVEN y FCAN
           MOVE SPACES TO WSS-GLS-TBAS IN WSS-LC1.
           MOVE ZEROES TO WSS-VAL-TBAS IN WSS-LC1.
           MOVE SPACES TO WSS-GLS-FBAS IN WSS-LC1.
           MOVE SPACES TO WSS-FEC-FBAS IN WSS-LC1.
           IF ICG-MSC-TFPR IN ICG = 'F'
               PERFORM OBT-VEN-ANT-TIM
               IF FIO-STAT-OKS
                   MOVE 'TASA BASE    :'    TO WSS-GLS-TBAS IN WSS-LC1
                   MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-TBAS IN WSS-LC1
                   MOVE 'AL :'              TO WSS-GLS-FBAS IN WSS-LC1
                   MOVE WSS-FEC-FBAS IN WSS-VARI TO WSS-FEC-FBAS
                                                            IN WSS-LC1
               ELSE
                   GO TO NXT-OUT-SRT.
      *DAD fin  16-ABR-1991 / 27-MAR-1991 

      *DAD ini  14-JUN-1991 (se reubico)
      * Imprime LC1 que forma parte del reporte LCC
           GENERATE DL-LC1.
      *DAD fin  14-JUN-1991 

      * Determina NUM-IVEN del VEN que esta asociado a la RCV leida
           MOVE CAN-CAI-IOPC IN SRT TO OPC-CAI-IOPC IN OPC.
           MOVE CAN-IIC-IOPC IN SRT TO OPC-IIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF OPC-IND-VSTR IN OPC = 'S'
               IF RCV-NUM-IVCT IN RCV NOT > ZEROES
                   MOVE RCV-CIC-IOPV IN RCV TO VEN-CIC-IOPC IN VEN
                   MOVE  1                  TO VEN-NUM-IVEN IN VEN
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM COL-FIO-VEN
                   IF FIO-STAT-OKS
      *DAD ini  16-ABR-1991 
                       MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVEN IN VEN
      *DAD fin  16-ABR-1991 
                   ELSE
                       MOVE 'COL'          TO MSG-COD-SIST
                       MOVE 'OPC    NOVEN' TO MSG-COD-MENS
                       PERFORM GET-MSG
                       DISPLAY MSG-GLS-DESC ':' RCV-CIC-IOPV IN RCV
                                                VEN-NUM-IVEN IN VEN
                       GO TO NXT-OUT-SRT
               ELSE
                   MOVE RCV-NUM-IVCT IN RCV TO VEN-NUM-IVEN IN VEN
           ELSE
               IF RCV-NUM-IVCT IN RCV NOT > ZEROES
                   MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN
               ELSE
                   MOVE RCV-NUM-IVCT IN RCV TO VEN-NUM-IVEN IN VEN.

      * Lee VEN con el NUM-IVEN del VEN que esta asociado a la RCV leida
           MOVE RCV-CIC-IOPV IN RCV TO VEN-CIC-IOPC IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS 
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'RCV    NOVEN' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC ':' RCV-CIC-IOPV IN RCV
                                            VEN-NUM-IVEN IN VEN
                   GO TO NXT-OUT-SRT
               ELSE
                   NEXT SENTENCE
           ELSE
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'RCV    NOVEN' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC ':' RCV-CIC-IOPV IN RCV
                                            VEN-NUM-IVEN IN VEN
                   GO TO NXT-OUT-SRT.

      * Reconstituye el Valor Capital y Valor Final del Vencimiento
           IF CAN-MSC-FCAN IN SRT = 'T'
               MOVE WSS-SGV-CAPI IN WSS-VARI TO VEN-VAL-SCAP IN VEN
               MOVE WSS-SGV-FINA IN WSS-VARI TO VEN-VAL-SFIN IN VEN
           ELSE
               ADD WSS-SGV-CAPI IN WSS-VARI TO VEN-VAL-SCAP IN VEN
               ADD WSS-SGV-FINA IN WSS-VARI TO VEN-VAL-SFIN IN VEN.

      * Obtiene Valor de Capital, Glosa de Moneda y ademas el Valor de Cambio
      * para Moneda Nacional equivalente en pesos a Fecha de Colocacion para
      * la parte Detalle del Vencimiento de LC2
           PERFORM BUS-WSS-GLS-VEXP.
           IF NOT FIO-STAT-OKS
               GO TO NXT-OUT-SRT.
      * Obtiene glosas y valores asociados a la equivalencia en pesos para
      * Moneda Nacional a Fecha de Colocacion y al Valor de Capital de la
      * parte Detalle del Vencimiento de LC2
           PERFORM BUS-EQUI-VCAM.

      * Obtiene Valores de Reajuste y Final Vencimiento, y ademas el Valor de
      * Cambio para Moneda Nacional equivalente en pesos a Fecha de Vencimiento
      * para la parte Detalle del Vencimiento de LC2
           PERFORM BUS-WSS-GLS-SFIN.
           IF NOT FIO-STAT-OKS
               GO TO NXT-OUT-SRT.
      * Obtiene glosas y valores asociados a la equivalencia en pesos para
      * Moneda Nacional a Fecha de Vencimiento y al Valor Final Vencimiento
      * de la parte Detalle del Vencimiento de LC2
           PERFORM BUS-EQUI-VFIN.

      *DAD ini  14-JUN-1991 
      * Determina Valor Reajuste de la parte Detalle del Vencimiento de LC2
           IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC IN WSS-VARI AND
              OPC-COD-VCOC IN SRT NOT = CAM-COD-MNAC IN CAM-VARI
               IF WSS-VAL-REAJ IN WSS-LC2 > ZEROES
                   SUBTRACT WSS-VAL-SCAP IN WSS-LC2 FROM
                            WSS-VAL-REAJ IN WSS-LC2 GIVING
                            WSS-VAL-REAJ IN WSS-LC2.

      * Determina Valor Intereses de la parte Detalle del Vencimiento de LC2
           IF WSS-VAL-SFIN IN WSS-LC2 NOT > ZEROES
               MOVE ZEROES TO WSS-VAL-INTE IN WSS-LC2
           ELSE
               SUBTRACT WSS-VAL-SCAP IN WSS-LC2
                        WSS-VAL-REAJ IN WSS-LC2 FROM
                        WSS-VAL-SFIN IN WSS-LC2 GIVING
                        WSS-VAL-INTE IN WSS-LC2.
      *DAD fin  14-JUN-1991 

      *DAD ini  27-MAR-1991 
      * El valor del interes se limpia cuando es cancelacion total (FHA)
           IF WSS-VAL-SFIN IN WSS-LC2 NOT > ZEROES
               MOVE ZEROES TO WSS-VAL-INTE IN WSS-LC2.
      *DAD fin  27-MAR-1991 

      *DAD ini  16-ABR-1991 / 03-APR-1991 
      * Para Valor Intereses de la parte Detalle del Vencimiento de LC2
      * asume el Valor Vencimiento cuando Opera Como = 'B' (FHA)
           IF TOC-MSC-OPEC IN SRT = 'B'
               MOVE WSS-VAL-SFIN IN WSS-LC2 TO WSS-VAL-INTE IN WSS-LC2.
      *DAD fin  16-ABR-1991 / 03-APR-1991 

      *------------------- DETALLE CANCELACION ---------------------------------
      *DAD ini  14-JUN-1991 (se reubico)
      * Obtiene Antecedentes de la Tasa de Cancelacion del Vencimiento
           PERFORM BUS-ICG-CAN.
      * Obtiene glosas asociadas a la Tasa de Cancelacion del Vencimiento que
      * forman parte del Detalle de la Cancelacion de LC2
           IF FIO-STAT-OKS
               PERFORM BUS-GLS-ICG-CAN
           ELSE
               MOVE ZEROES TO ICG-SGV-TINT IN ICG
               MOVE SPACES TO WSS-GLS-BCTI IN WSS-LC2
               MOVE SPACES TO WSS-GLS-PBTI IN WSS-LC2
               MOVE SPACES TO WSS-GLS-MCTI IN WSS-LC2.
      *DAD fin  14-JUN-1991 

      *DAD ini  27-MAR-1991 
      * Obtiene el Interes Cancelado de la RCV 
           SUBTRACT RCV-SGV-CAPI IN RCV FROM RCV-SGV-FINA IN RCV 
                    GIVING WSS-SGV-INTE IN WSS-LC2.

      * El valor del interes se limpia cuando es cancelacion total (FHA)
           IF RCV-SGV-FINA IN RCV NOT > ZEROES
               MOVE ZEROES TO WSS-SGV-INTE IN WSS-LC2.
      *DAD fin  27-MAR-1991 

      * Guarda el Valor Final Cancelado de la RCV 
           MOVE RCV-SGV-FINA IN RCV TO WSS-SGV-FNAL IN WSS-VARI.

      * Obtiene Valores de Capital, Intereses, Final y Comision, y ademas el
      * Valor de Cambio para Moneda Nacional equivalente en pesos a Fecha de
      * Cancelacion para la parte Detalle de la Cancelacion de LC2
           PERFORM BUS-WSS-GLS-VCAN.
           IF NOT FIO-STAT-OKS
               GO TO NXT-OUT-SRT.
      * Obtiene glosas y valores asociados a la equivalencia en pesos para
      * Moneda Nacional a Fecha de Cancelacion de la parte Detalle de la
      * Cancelacion de LC2
           PERFORM BUS-EQUI-VCAN.

      *DAD ini  27-MAR-1991 
      * Obtiene Monto Cancelado de la parte Detalle de la Cancelacion de LC2
           MOVE ZEROES TO WSS-SGV-MCAN IN WSS-LC2.
           ADD CAN-SGV-TPAG IN SRT
               CAN-SGV-TREN IN SRT
               CAN-SGV-DIFE IN SRT TO WSS-SGV-MCAN IN WSS-LC2.
      *DAD fin  27-MAR-1991 

      *DAD ini  14-AGO-1991 / 22-JUL-1991 / 16-ABR-1991 / 03-APR-1991 
      * Para Valor Intereses de la parte Detalle de la Cancelacion de LC2
      * asume Valor Final Capital cuando Opera Como = 'B' (FHA)
           IF TOC-MSC-OPEC IN SRT = 'B'
               MOVE WSS-SGV-FNAL IN WSS-VARI TO WSS-SGV-INTE IN WSS-LC2.
      *DAD fin  14-AGO-1991 / 22-JUL-1991 / 16-APR-1991 / 03-ABR-1991 

      * Determina MORA o DESCUENTO de la parte Detalle de la Cancelacion de LC2
           MOVE SPACES TO WSS-GLS-TASA IN WSS-LC2.
           MOVE SPACES TO WSS-GLS-MORA IN WSS-LC2.
           MOVE ZEROES TO WSS-SGV-MORA IN WSS-LC2.
      *DAD ini  14-AGO-1991
           MOVE ZEROES TO WSS-SGV-MORA IN WSS-VARI.
      *DAD fin  14-AGO-1991

      *CMP-FEC Compara Fecha de Vencimiento con Fecha de Cancelacion
           MOVE VEN-NUM-DVEN IN VEN TO FEC-DEC1.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-MEC1.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-SEC1.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-AEC1.
           MOVE CAN-NUM-DCAN IN SRT TO FEC-DEC2.
           MOVE CAN-NUM-MCAN IN SRT TO FEC-MEC2.
           MOVE CAN-NUM-SCAN IN SRT TO FEC-SEC2.
           MOVE CAN-NUM-ACAN IN SRT TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
      *DAD ini  14-AGO-1991 / 14-JUN-1991 / 16-ABR-1991 / 27-MAR-1991 
      * Para Cancelacion Especifica el dia del Vencimiento No se determina
      * MORA o DESCUENTO
           IF FEC-STAT-EQL
               IF CAN-MSC-ECAN IN SRT = 'E'
                   GO TO AJUSTE-INTE.

      * Determina MORA de la parte Detalle de la Cancelacion de LC2
      * El valor del Interes se ajusta cuando el Descuento es Cero
           IF FEC-STAT-LES
               MOVE 'TASA MORA :' TO WSS-GLS-TASA IN WSS-LC2
               MOVE 'MONTO MORA:' TO WSS-GLS-MORA IN WSS-LC2
               MOVE ZEROES        TO WSS-VAL-COMI IN WSS-LC2
               IF ICG-SGV-TINT IN ICG > ZEROES
                   SUBTRACT RCV-SGV-CAPI IN RCV
                            RCV-SGV-REAJ IN RCV
                            WSS-SGV-INTE IN WSS-LC2 FROM
                            WSS-SGV-MCAN IN WSS-LC2 GIVING
                            WSS-SGV-MORA IN WSS-VARI
                   MOVE WSS-SGV-MORA IN WSS-VARI TO WSS-SGV-MORA
                                                            IN WSS-LC2
               ELSE
                   IF TOC-MSC-OPEC IN SRT = 'B'
                       NEXT SENTENCE
                   ELSE
                       SUBTRACT RCV-SGV-CAPI IN RCV
                                RCV-SGV-REAJ IN RCV     FROM
                                WSS-SGV-MCAN IN WSS-LC2 GIVING
                                WSS-SGV-INTE IN WSS-LC2
           ELSE
      * Determina DESCUENTO de la parte Detalle de la Cancelacion de LC2
      * El valor del Interes se ajusta cuando el Descuento es Cero
               MOVE 'TASA DSCTO:' TO WSS-GLS-TASA IN WSS-LC2
               MOVE 'DESCUENTO :' TO WSS-GLS-MORA IN WSS-LC2
               IF ICG-SGV-TINT IN ICG > ZEROES
                   SUBTRACT RCV-SGV-CAPI IN RCV
                            RCV-SGV-REAJ IN RCV
                            WSS-SGV-INTE IN WSS-LC2 FROM
                            WSS-SGV-MCAN IN WSS-LC2 GIVING
                            WSS-SGV-MORA IN WSS-VARI
                   MOVE WSS-SGV-MORA IN WSS-VARI TO WSS-SGV-MORA
                                                            IN WSS-LC2
               ELSE
                   IF TOC-MSC-OPEC IN SRT = 'B'
                       NEXT SENTENCE
                   ELSE
                       SUBTRACT RCV-SGV-CAPI IN RCV
                                RCV-SGV-REAJ IN RCV     FROM
                                WSS-SGV-MCAN IN WSS-LC2 GIVING
                                WSS-SGV-INTE IN WSS-LC2.
           GO TO CON-FEC-MODE.
      *DAD fin  14-AGO-1991 / 14-JUN-1991 / 16-ABR-1991 / 27-MAR-1991 

      *DAD ini  14-AGO-1991 
       AJUSTE-INTE.
      * Descuadre ====> se Ajuste Valor Reajuste o Valor Intereses de la parte
      * Detalle de la Cancelacion de LC2
           MOVE ZEROES TO WSS-SGV-MORA IN WSS-VARI.
           SUBTRACT RCV-SGV-CAPI IN RCV
                    RCV-SGV-REAJ IN RCV
                    WSS-SGV-INTE IN WSS-LC2
                    WSS-SGV-MORA IN WSS-LC2 FROM
                    WSS-SGV-MCAN IN WSS-LC2 GIVING
                    WSS-SGV-MORA IN WSS-VARI.
           IF WSS-SGV-MORA IN WSS-VARI NOT = ZEROES
      * Ajusta Valor Intereses de la parte Detalle de la Cancelacion de LC2
               IF TOC-MSC-OPEC IN SRT NOT = 'B' AND
                  WSS-SGV-INTE IN WSS-LC2 > ZEROES
                   SUBTRACT RCV-SGV-CAPI IN RCV
                            RCV-SGV-REAJ IN RCV
                            WSS-SGV-MORA IN WSS-LC2 FROM
                            WSS-SGV-MCAN IN WSS-LC2 GIVING
                            WSS-SGV-INTE IN WSS-LC2
               ELSE
      * Ajusta Valor Reajuste de la parte Detalle de la Cancelacion de LC2
                   IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC 
                                                         IN WSS-VARI AND
                      OPC-COD-VCOC IN SRT NOT  = CAM-COD-MNAC
                                                         IN CAM-VARI
                       SUBTRACT RCV-SGV-CAPI IN RCV
                                WSS-SGV-MORA IN WSS-LC2 FROM
                                WSS-SGV-MCAN IN WSS-LC2 GIVING
                                RCV-SGV-REAJ IN RCV.
      *DAD fin  14-AGO-1991 

       CON-FEC-MODE.
      *DAD ini  27-MAR-1991 
      * Obtiene Glosa y Valor segun el Tipo de Cancelacion asociado al Valor 
      * Total Renovado de la parte Detalle de la Cancelacion de LC2
           MOVE SPACES TO WSS-GLS-TREN IN WSS-LC2.
           MOVE ZEROES TO WSS-VAL-TREN IN WSS-LC2.
           IF CAN-SGV-TREN IN SRT > ZEROES AND
              ( CAN-MSC-TCAN IN SRT = 'N' OR
                CAN-MSC-TCAN IN SRT = 'P' OR
                CAN-MSC-TCAN IN SRT = 'R' )
               MOVE CAN-SGV-TREN IN SRT TO WSS-VAL-TREN IN WSS-LC2
               IF CAN-MSC-TCAN IN SRT = 'N'
                   MOVE 'MONTO NOVADO     :' TO WSS-GLS-TREN IN WSS-LC2
               ELSE
               IF CAN-MSC-TCAN IN SRT = 'P'
                   MOVE 'MONTO PRORROGADO :' TO WSS-GLS-TREN IN WSS-LC2
               ELSE
               IF CAN-MSC-TCAN IN SRT = 'R'
                   MOVE 'MONTO RENOVADO   :' TO WSS-GLS-TREN IN WSS-LC2.
      *DAD fin  27-MAR-1991 

      * Obtiene glosas y valores asociados a la forma de Pago del Detalle de la
      * Cancelacion que forman parte de LC2
           PERFORM GET-CYA.

      * Obtiene Comentarios asociados al Reporte para la parte Detalle de la
      * Cancelacion de LC2
           PERFORM BUS-WSS-GLS-MSG.

      *DAD ini  17-JUL-1991 / 14-JUN-1991 (se reubico)
      * Imprime LC2 que forma parte del reporte LCC
           GENERATE DL-LC21.
           GENERATE DL-NUL.
           GENERATE DL-LC22.

           GO TO NXT-OUT-SRT.
      *DAD fin  17-JUL-1991 / 14-JUN-1991 
      *>>>>
*% END
*% IF EOF
      *<<<< EOF
      **************************************************************************
      *DAD ini  22-JUL-1991
      * Lee el primer CYA asociado a la Cancelacion y obtiene el CIC de Cargo
      * en Cuenta Corriente
       LEE-FRST-CYA SECTION.
       INI-LEE-FRST-CYA.
           MOVE  1  TO WSS-IND-FCYA IN WSS-VARI.
           MOVE CAN-KEY-ICAN IN CAN TO CYA-KEY-ICAN IN CYA.
           MOVE ZEROES              TO CYA-NUM-ICYA IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT ( FIO-STAT-OKS AND
                    CAN-KEY-ICAN IN CAN = CYA-KEY-ICAN IN CYA )
               GO TO FIN-LEE-FRST-CYA.

           IF CYA-IND-TCYA IN CYA NOT > SPACES
               GO TO FIN-LEE-FRST-CYA.

      *DAD ini  03-ENE-1992   (Memo.02-01-92)
      * Busca en la Tabla cargada en Memoria con el Codigo Miscelaneo deseado
           MOVE 'TAB'               TO CMM-SIST IN CMM-VARI.
           MOVE 'TCYA'              TO CMM-TMSC IN CMM-VARI.
           MOVE CYA-IND-TCYA IN CYA TO CMM-CMSC IN CMM-VARI.
           PERFORM GNS-BUS-CMM.
           IF CMM-CMSG IN CMM-VARI > SPACES
               MOVE 'GNS'                TO MSG-COD-SIST
               MOVE CMM-CMSG IN CMM-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CMM-MEN2 IN CMM-VARI
               DISPLAY 'CON CODIGO : '     CMM-MSCL IN CMM-VARI
                       ' EN CREDITO : '    OPC-CIC-IOPC IN OPC
               GO TO FIN-LEE-FRST-CYA.
      *DAD fin  03-ENE-1992

           MOVE  0  TO WSS-IND-FCYA IN WSS-VARI.
       FIN-LEE-FRST-CYA.
           EXIT.
      *DAD fin  22-JUL-1991
      **************************************************************************
      * Obtiene Antecedentes del Cliente para titulos del Reporte que forman
      * parte del registro SRT
       BUS-DEU-DIR SECTION.
       INI-BUS-DEU-DIR.
           MOVE CAN-CAI-IOPC IN CAN TO RDC-CAI-IOPC IN RDC.
           MOVE CAN-IIC-IOPC IN CAN TO RDC-IIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               PERFORM RST-DBC
               PERFORM RST-RCD
               MOVE SPACES TO WSS-GLS-LUGA IN WSS-LC1
               GO TO CON-BUS-DEU-DIR.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               PERFORM RST-DBC
               PERFORM RST-RCD
               MOVE SPACES TO WSS-GLS-LUGA IN WSS-LC1
               GO TO CON-BUS-DEU-DIR.

           PERFORM BUS-DIRE.
           IF RCD-GLS-DIRE IN RCD NOT > SPACES
               PERFORM RST-RCD
               MOVE SPACES TO WSS-GLS-LUGA IN WSS-LC1
               GO TO NXT-BUS-DEU-DIR.

           PERFORM BUS-GLS-COMU.
           MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-LUGA IN WSS-LC1.
       NXT-BUS-DEU-DIR.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF FIO-STAT-OKS AND
              RDC-CAI-IOPC IN RDC = CAN-CAI-IOPC IN CAN AND
              RDC-IIC-IOPC IN RDC = CAN-IIC-IOPC IN CAN AND
              RDC-NUM-IDLC IN RDC = ZEROES
               PERFORM RST-DBC
               PERFORM RST-RCD
               MOVE SPACES TO WSS-GLS-LUGA IN WSS-LC1
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    MDDIR' TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO DBC-GLS-NOMC IN DBC.
       CON-BUS-DEU-DIR.
           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.
       FIN-BUS-DEU-DIR.
           EXIT.      

       BUS-DIRE SECTION.
       INI-BUS-DIRE.
           MOVE DBC-CAI-ICLI IN DBC TO RCD-CAI-ICLI IN RCD.
           MOVE DBC-IIC-ICLI IN DBC TO RCD-IIC-ICLI IN RCD.
           MOVE RDC-NUM-DIRE IN RDC TO RCD-NUM-DIRE IN RCD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-RCD.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO RCD-GLS-DIRE IN RCD.
       FIN-BUS-DIRE.
           EXIT.

       BUS-GLS-COMU SECTION.
       INI-BUS-GLS-COMU.
           IF RCD-COD-LUGA IN RCD > SPACES
               MOVE RCD-COD-LUGA IN RCD TO WSS-COD-LUGA
               IF WSS-IND-PAIS = 'P'
                   MOVE 'TAB' TO TAB-COD-SIST
                   MOVE 'PAI' TO TAB-COD-TTAB IN TAB
                   MOVE WSS-COD-PAIS TO TAB-COD-CTAB IN TAB
                   PERFORM BUS-TAB
               ELSE
                   MOVE 'TAB' TO TAB-COD-SIST
                   MOVE 'COM' TO TAB-COD-TTAB IN TAB
                   MOVE RCD-COD-LUGA IN RCD TO TAB-COD-CTAB IN TAB
                   PERFORM BUS-TAB
           ELSE
               MOVE SPACES TO TAB-GLS-DESC IN TAB.
       FIN-BUS-GLS-COMU.
           EXIT.

       RST-DBC SECTION.
       INI-RST-DBC.
           MOVE 'SGC'        TO MSG-COD-SIST.
           MOVE 'CLI    NEX' TO MSG-COD-MENS.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC TO DBC-GLS-NOMC IN DBC.
           MOVE ZEROES TO DBC-NUM-ICLI IN DBC.
           MOVE SPACES TO DBC-IND-ICLI IN DBC.
           MOVE SPACES TO DBC-VRF-ICLI IN DBC.
           MOVE SPACES TO DBC-GLS-ICLI IN DBC.
       FIN-RST-DBC.
           EXIT.

       RST-RCD SECTION.
       INI-RST-RCD.
           MOVE SPACES TO RCD-GLS-DIRE IN RCD.
           MOVE SPACES TO RCD-GLS-FONO IN RCD.
       FIN-RST-RCD.
           EXIT.
      **************************************************************************
      * Obtiene Antecedentes de la Tasa del Credito
       BUS-ICG SECTION.
       INI-BUS-ICG.
           PERFORM GET-CUR-ICG.
           IF NOT FIO-STAT-OKS
               GO TO FIN-BUS-ICG.
      *     PERFORM GET-NXT-ICG.
      *     IF ICG-COD-TINT IN ICG = 'M'
      *         MOVE 'MULTIPLES'  TO WSS-COD-TTCA IN WSS-CAN
      *         MOVE ZEROES       TO WSS-SGV-TCAN IN WSS-CAN
      *     ELSE
      *         MOVE ICG-COD-TINT IN ICG TO WSS-COD-TTCA IN WSS-CAN
      *         MOVE ICG-SGV-TINT IN ICG TO WSS-SGV-TCAN IN WSS-CAN.
       FIN-BUS-ICG.
           EXIT.

       GET-CUR-ICG SECTION.
       INI-GET-CUR-ICG.
           MOVE CAN-CAI-IOPC IN SRT TO ICG-CAI-IOPC IN ICG.
           MOVE CAN-IIC-IOPC IN SRT TO ICG-IIC-IOPC IN ICG.
           MOVE ZEROES              TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES              TO ICG-CAI-IOPV IN ICG.
           MOVE SPACES              TO ICG-IIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-CAI-IOPC IN ICG = CAN-CAI-IOPC IN SRT AND
                    ICG-IIC-IOPC IN ICG = CAN-IIC-IOPC IN SRT AND
                    ICG-NUM-ICAN IN ICG = ZEROES              AND
                    ICG-CAI-IOPV IN ICG = SPACES              AND
                    ICG-IIC-IOPV IN ICG = SPACES              AND
                    ICG-IND-TTAS IN ICG = 'INT'  )
               MOVE '23' TO FIO-STAT.
       FIN-GET-CUR-ICG.
           EXIT.
      *-------------------------------------------------------------------------
      * Obtiene glosas asociadas a la Tasa del Credito que forman parte de LC1
       BUS-GLS-ICG SECTION.
       INI-BUS-GLS-ICG.
      *DAD ini  16-ABR-1991 
           MOVE ICG-COD-TINT IN ICG TO WSS-COD-TINT IN WSS-VARI.

           IF ICG-COD-CVTI IN ICG > SPACES
               MOVE 'SPREAD       :'    TO WSS-GLS-TTAS IN WSS-LC1
               MOVE ICG-SGV-SPRD IN ICG TO WSS-SGV-TINT IN WSS-LC1
           ELSE
               MOVE 'TASA INTERES :'    TO WSS-GLS-TTAS IN WSS-LC1
               MOVE ICG-SGV-TINT IN ICG TO WSS-SGV-TINT IN WSS-LC1.

           MOVE ICG-NUM-PREP IN ICG TO WSS-NUM-PREP IN WSS-LC1.
      *DAD fin  16-ABR-1991 

           IF ICG-MSC-BCTI IN ICG > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'BCTI'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-BCTI IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO WSS-GLS-BCTI IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-BCTI IN WSS-LC1.

           IF ICG-MSC-PBTI IN ICG > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'PBTI'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-PBTI IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO WSS-GLS-PBTI IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-PBTI IN WSS-LC1.

           IF ICG-MSC-SPBT IN ICG > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'MCTI'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-SPBT IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO WSS-GLS-MCTI IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-MCTI IN WSS-LC1.

           IF ICG-COD-CVTI IN ICG > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE 'I'                 TO WSS-COD-TVTI IN WSS-VARI
               MOVE ICG-COD-CVTI IN ICG TO WSS-COD-CVTI IN WSS-VARI
               MOVE ICG-MSC-PBTI IN ICG TO WSS-COD-TCTI IN WSS-VARI
               MOVE WSS-COD-VCAM IN WSS-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-CVTI IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-CVTI IN WSS-LC1.

           IF ICG-MSC-TFPR IN ICG > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'TFPR'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-TFPR IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO WSS-GLS-TFPR IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-TFPR IN WSS-LC1.

           IF ICG-MSC-UTPR IN ICG > SPACES
               MOVE 'TAB'               TO MSC-COD-SIST
               MOVE 'IDMA'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-UTPR IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO WSS-GLS-UTPR IN WSS-LC1
           ELSE
               MOVE SPACES TO WSS-GLS-UTPR IN WSS-LC1.
       FIN-BUS-GLS-ICG.
           EXIT.
      **************************************************************************
      * Acumula para diferentes CAN de un mismo VEN el Valor Capital y Final
      * Cancelado que esta en varios RCV 
       BUS-RCV SECTION.
       INI-BUS-RCV.
           MOVE ZEROES TO WSS-SGV-CAPI IN WSS-VARI.
           MOVE ZEROES TO WSS-SGV-FINA IN WSS-VARI.
           MOVE CAN-CAI-IOPC IN SRT TO RCV-CAI-IOPC IN RCV.
           MOVE CAN-IIC-IOPC IN SRT TO RCV-IIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN SRT TO RCV-NUM-ICAN IN RCV.
           MOVE CAN-CAI-IOPC IN SRT TO RCV-CAI-IOPV IN RCV.
           MOVE CAN-IIC-IOPC IN SRT TO RCV-IIC-IOPV IN RCV.
           MOVE ZEROES              TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS AND
                    RCV-CAI-IOPC IN RCV = CAN-CAI-IOPC IN SRT AND
                    RCV-IIC-IOPC IN RCV = CAN-IIC-IOPC IN SRT AND
                    RCV-NUM-ICAN IN RCV = CAN-NUM-ICAN IN SRT AND
                    RCV-CAI-IOPV IN RCV = CAN-CAI-IOPC IN SRT AND
                    RCV-IIC-IOPV IN RCV = CAN-IIC-IOPC IN SRT )
               GO TO FIN-BUS-RCV.

           MOVE RCV-NUM-IVCT IN RCV TO WSS-NUM-IVCT IN WSS-VARI.
           ADD  RCV-SGV-CAPI IN RCV TO WSS-SGV-CAPI IN WSS-VARI.
           ADD  RCV-SGV-FINA IN RCV TO WSS-SGV-FINA IN WSS-VARI.
      *DAD ini  27-MAR-1991 
           IF RCV-NUM-IVCT IN RCV > ZEROES
               MOVE RCV-NUM-IVCT IN RCV TO WSS-NUM-IVCT IN WSS-LC2
           ELSE
               MOVE 'TOT' TO WSS-NUM-IVCT IN WSS-LC2.
      *DAD fin  27-MAR-1991 
       GET-NXT-RCV.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS AND
                    RCV-CAI-IOPC IN RCV = CAN-CAI-IOPC IN SRT AND
                    RCV-IIC-IOPC IN RCV = CAN-IIC-IOPC IN SRT )
               GO TO FIN-GET-RCV.

           MOVE RCV-CAI-IOPC IN RCV TO CAN-CAI-IOPC IN CAN.
           MOVE RCV-IIC-IOPC IN RCV TO CAN-IIC-IOPC IN CAN.
           MOVE RCV-NUM-ICAN IN RCV TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF CAN-MSC-STAT IN CAN NOT = 'A' AND
              CAN-MSC-STAT IN CAN NOT = 'R'
               GO TO GET-NXT-RCV.

           IF RCV-NUM-IVCT IN RCV = WSS-NUM-IVCT IN WSS-VARI
               ADD RCV-SGV-CAPI IN RCV TO WSS-SGV-CAPI IN WSS-VARI
               ADD RCV-SGV-FINA IN RCV TO WSS-SGV-FINA IN WSS-VARI.
           GO TO GET-NXT-RCV.
       FIN-GET-RCV.
           MOVE CAN-CAI-IOPC IN SRT TO CAN-CAI-IOPC IN CAN.
           MOVE CAN-IIC-IOPC IN SRT TO CAN-IIC-IOPC IN CAN.
           MOVE CAN-NUM-ICAN IN SRT TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-CAN.
       FIN-BUS-RCV.
           EXIT.
      **************************************************************************
      *DAD ini  27-MAR-1991 
      * Obtener VEN anterior para leer en tabla CAM el TIM base con menor fecha
      * entre FVEN y FCAN
       OBT-VEN-ANT-TIM SECTION.
       INI-OBT-VEN-ANT-TIM.
      * Determina NUM-IVEN del VEN anterior para obtener FVEN, si NUM-IVEN
      * resulta ser cero se usara FCOL y no efectuara lectura de VEN
           MOVE CAN-CAI-IOPC IN SRT TO OPC-CAI-IOPC IN OPC.
           MOVE CAN-IIC-IOPC IN SRT TO OPC-IIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           MOVE OPC-FEC-FCOL IN OPC TO CAM-FEC-FCAM IN CAM(1)
                                       CAM-FEC-FCAM IN CAM(2).
           IF OPC-IND-VSTR IN OPC = 'S'
               IF RCV-NUM-IVCT IN RCV NOT > ZEROES
                   MOVE RCV-CIC-IOPV IN RCV TO VEN-CIC-IOPC IN VEN
                   MOVE  1                  TO VEN-NUM-IVEN IN VEN
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM COL-FIO-VEN
                   IF FIO-STAT-OKS
                       SUBTRACT 1 FROM VEN-NUM-IVCT IN VEN GIVING
                                       VEN-NUM-IVEN IN VEN
                       IF VEN-NUM-IVEN IN VEN NOT > ZEROES
                           GO TO PAS-OBT-VEN-ANT-TIM
                       ELSE
                           NEXT SENTENCE
                   ELSE
                       MOVE 'COL'          TO MSG-COD-SIST
                       MOVE 'OPC    NOVEN' TO MSG-COD-MENS
                       PERFORM GET-MSG
                       DISPLAY MSG-GLS-DESC ':' RCV-CIC-IOPV IN RCV
                                                VEN-NUM-IVEN IN VEN
                       GO TO FIN-OBT-VEN-ANT-TIM
               ELSE
                   SUBTRACT 1 FROM RCV-NUM-IVCT IN RCV GIVING
                                   VEN-NUM-IVEN IN VEN
                   IF VEN-NUM-IVEN IN VEN NOT > ZEROES
                       GO TO PAS-OBT-VEN-ANT-TIM
                   ELSE
                       NEXT SENTENCE
           ELSE
               IF RCV-NUM-IVCT IN RCV NOT > ZEROES
                   SUBTRACT 1 FROM OPC-NUM-IVEN IN OPC GIVING
                                   VEN-NUM-IVEN IN VEN
                   IF VEN-NUM-IVEN IN VEN NOT > ZEROES
                       GO TO PAS-OBT-VEN-ANT-TIM
                   ELSE
                       NEXT SENTENCE
               ELSE
                   SUBTRACT 1 FROM RCV-NUM-IVCT IN RCV GIVING
                                   VEN-NUM-IVEN IN VEN
                   IF VEN-NUM-IVEN IN VEN NOT > ZEROES
                       GO TO PAS-OBT-VEN-ANT-TIM.

      * Lee el VEN anterior para obtener FVEN con NUM-IVEN determinado
           MOVE RCV-CIC-IOPV IN RCV TO VEN-CIC-IOPC IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS 
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'RCV    NOVEN' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC ':' RCV-CIC-IOPV IN RCV
                                            VEN-NUM-IVEN IN VEN
                   MOVE '23' TO FIO-STAT
                   GO TO FIN-OBT-VEN-ANT-TIM
               ELSE
                   NEXT SENTENCE
           ELSE
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'RCV    NOVEN' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC ':' RCV-CIC-IOPV IN RCV
                                            VEN-NUM-IVEN IN VEN
                   GO TO FIN-OBT-VEN-ANT-TIM.

      * Lee tabla CAM para obtener el TIM base con fecha menor entre FVEN y FCAN
           IF VEN-FEC-FVEN IN VEN NOT > CAN-FEC-FCAN IN CAN
               MOVE VEN-FEC-FVEN IN VEN TO CAM-FEC-FCAM IN CAM(1)
                                           CAM-FEC-FCAM IN CAM(2)
           ELSE
               MOVE CAN-FEC-FCAN IN CAN TO CAM-FEC-FCAM IN CAM(1)
                                           CAM-FEC-FCAM IN CAM(2).

      * Se recupera Codigo Tasa Interes del Credito pues al utilizar la rutina
      * COL-OBT-VEN se pisa el ICG
           MOVE WSS-COD-TINT IN WSS-VARI TO ICG-COD-TINT IN ICG.
       PAS-OBT-VEN-ANT-TIM.
      * Si NUM-IVEN resulto ser cero se usara FCOL para obtener TIM base
           MOVE 'I'                 TO CAM-MSC-TVAL IN CAM(1).
           MOVE ICG-COD-CVTI IN ICG TO CAM-COD-CVAL IN CAM(1).
           MOVE ICG-MSC-PBTI IN ICG TO CAM-COD-TCAM IN CAM(1).
           MOVE OPC-COD-VCOC IN SRT TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS OR
              CAM-IND-VIGE NOT = 'S'
               DISPLAY 'NO EXISTE VALOR DE TIM PARA OPERACION : '
                        CAN-CAI-IOPC IN SRT '/'
                        CAN-IIC-IOPC IN SRT '/'
                        CAM-FEC-FCAM IN CAM(1) '/'
                        CAM-COD-VCAM IN CAM(1) '/'
                        CAM-COD-VCAM IN CAM(2)
               MOVE '23' TO FIO-STAT
               GO TO FIN-OBT-VEN-ANT-TIM.

           MOVE CAM-NUM-DCAM IN CAM(1) TO WSS-NUM-DBAS IN WSS-VARI.
           MOVE CAM-NUM-MCAM IN CAM(1) TO WSS-NUM-MBAS IN WSS-VARI.
           MOVE CAM-NUM-SCAM IN CAM(1) TO WSS-NUM-SBAS IN WSS-VARI.
           MOVE CAM-NUM-ACAM IN CAM(1) TO WSS-NUM-ABAS IN WSS-VARI.
       FIN-OBT-VEN-ANT-TIM.
           EXIT.
      *DAD fin  27-MAR-1991 
      **************************************************************************
      * Obtiene Valor de Capital, Glosa de Moneda y ademas el Valor de Cambio
      * para Moneda Nacional equivalente en pesos a Fecha de Colocacion para
      * la parte Detalle del Vencimiento de LC2
       BUS-WSS-GLS-VEXP SECTION.
       INI-BUS-WSS-GLS-VEXP.
           MOVE OPC-COD-VCOC IN SRT TO WSS-COD-VCOC IN WSS-VARI.
           IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC IN WSS-VARI AND
              WSS-CIC-MRXL IN WSS-VARI > SPACES 
               GO TO NXT-BUS-WSS-GLS-VEXP.

      * Para Moneda Extranjera
           MOVE WSS-DCO-VCOC IN WSS-VARI TO WSS-GLS-VEXP IN WSS-LC2.
           MOVE VEN-VAL-SCAP IN VEN      TO WSS-VAL-SCAP IN WSS-LC2.
           MOVE ZEROES                   TO WSS-VAL-VCFC IN WSS-LC2.
           GO TO FIN-BUS-WSS-GLS-VEXP.
       NXT-BUS-WSS-GLS-VEXP.
      * Glosa para Moneda Nacional
           MOVE 'TAB'        TO FIO-SIST.
           MOVE 'VLR'        TO TAB-CIC-TTAB.
           MOVE WSS-CIC-MRXL TO TAB-CIC-CTAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VEXP IN WSS-LC2
           ELSE
               MOVE SPACES TO WSS-GLS-VEXP IN WSS-LC2. 

      * Valor de Cambio para Moneda Nacional a Fecha Colocacion y Capital Vencto
           IF TAB-COD-CTAB IN TAB NOT = OPC-COD-VCOC IN SRT
               MOVE OPC-NUM-DCOL IN SRT TO CAM-NUM-DCAM IN CAM(1)
               MOVE OPC-NUM-MCOL IN SRT TO CAM-NUM-MCAM IN CAM(1)
               MOVE OPC-NUM-SCOL IN SRT TO CAM-NUM-SCAM IN CAM(1)
               MOVE OPC-NUM-ACOL IN SRT TO CAM-NUM-ACAM IN CAM(1)
               MOVE OPC-NUM-DCOL IN SRT TO CAM-NUM-DCAM IN CAM(2)
               MOVE OPC-NUM-MCOL IN SRT TO CAM-NUM-MCAM IN CAM(2)
               MOVE OPC-NUM-SCOL IN SRT TO CAM-NUM-SCAM IN CAM(2)
               MOVE OPC-NUM-ACOL IN SRT TO CAM-NUM-ACAM IN CAM(2)
               MOVE OPC-COD-VCOC IN SRT TO CAM-COD-VCAM IN CAM(1)
               MOVE TAB-COD-CTAB IN TAB TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   MOVE ZEROES TO WSS-VAL-SCAP IN WSS-LC2
                   DISPLAY 'NO EXISTE VALOR CAMBIO OPER : '
                           CAN-CAI-IOPC IN SRT '/'
                           CAN-IIC-IOPC IN SRT '/'
                           CAM-FEC-FCAM IN CAM(1) '/'
                           OPC-COD-VCOC IN SRT '/'
                           TAB-COD-CTAB IN TAB
               ELSE
               IF CAM-IND-VIGE NOT = 'S'
                   MOVE ZEROES TO WSS-VAL-SCAP IN WSS-LC2
                   DISPLAY 'IND. VIGENCIA NO VALIDO OPER : '
                           CAN-CAI-IOPC IN SRT '/'
                           CAN-IIC-IOPC IN SRT '/'
                           CAM-FEC-FCAM IN CAM(1) '/'
                           OPC-COD-VCOC IN SRT '/'
                           TAB-COD-CTAB IN TAB
               ELSE
                   MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VCFC IN WSS-LC2
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN VEN
                                         GIVING WSS-VAL-SCAP IN WSS-LC2
                   MOVE WSS-VAL-SCAP IN WSS-LC2 TO PES-SGV-DECI
                                                             IN PES-VARI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE IN PES-VARI TO WSS-VAL-SCAP
                                                             IN WSS-LC2
           ELSE
               MOVE ZEROES              TO WSS-VAL-VCFC IN WSS-LC2
               MOVE VEN-VAL-SCAP IN VEN TO WSS-VAL-SCAP IN WSS-LC2.
       FIN-BUS-WSS-GLS-VEXP.
           EXIT.
      *-------------------------------------------------------------------------
      * Obtiene glosas y valores asociados a la equivalencia en pesos para
      * Moneda Nacional a Fecha de Colocacion y al Valor de Capital de la
      * parte Detalle del Vencimiento de LC2
       BUS-EQUI-VCAM SECTION.
       INI-BUS-EQUI-VCAM.
           IF WSS-CIC-MRXL IN WSS-VARI = WSS-CIC-VCOC IN WSS-VARI
               MOVE SPACES TO WSS-GLS-EQFC IN WSS-LC2
               MOVE SPACES TO WSS-GLS-VCFC IN WSS-LC2
               MOVE ZEROES TO VEN-VAL-SCAP IN VEN 
               MOVE SPACES TO WSS-GLS-VCCA IN WSS-LC2
               MOVE ZEROES TO WSS-VAL-VCFC IN WSS-LC2
               GO TO FIN-BUS-EQUI-VCAM.

           IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC IN WSS-VARI
               MOVE WSS-DCO-MRXL IN WSS-VARI TO WSS-GLS-EQFC IN WSS-LC2
               MOVE WSS-DES-MRXL IN WSS-VARI TO WSS-GLS-VCFC IN WSS-LC2
               MOVE WSS-ABR-VCOC IN WSS-VARI TO WSS-GLS-VCCA IN WSS-LC2
           ELSE
               MOVE SPACES TO WSS-GLS-EQFC IN WSS-LC2
               MOVE SPACES TO WSS-GLS-VCFC IN WSS-LC2
               MOVE ZEROES TO VEN-VAL-SCAP IN VEN 
               MOVE SPACES TO WSS-GLS-VCCA IN WSS-LC2
               MOVE ZEROES TO WSS-VAL-VCFC IN WSS-LC2.
       FIN-BUS-EQUI-VCAM.
           EXIT.
      **************************************************************************
      * Obtiene Valores de Reajuste y Final Vencimiento, y ademas el Valor de
      * Cambio para Moneda Nacional equivalente en pesos a Fecha de Vencimiento
      * para la parte Detalle del Vencimiento de LC2
       BUS-WSS-GLS-SFIN SECTION.
       INI-BUS-WSS-GLS-SFIN.
           MOVE OPC-COD-VCOC IN SRT TO WSS-COD-VCOC IN WSS-VARI.
           IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC IN WSS-VARI AND
              WSS-CIC-MRXL IN WSS-VARI > SPACES 
               GO TO NXT-BUS-WSS-GLS-SFIN.

      * Para Moneda Extranjera
           MOVE VEN-VAL-SFIN IN VEN TO WSS-VAL-SFIN IN WSS-LC2.
           MOVE ZEROES              TO WSS-VAL-VCFV IN WSS-LC2.
      *DAD ini  14-JUN-1991 
           MOVE ZEROES              TO WSS-VAL-REAJ IN WSS-LC2.
      *DAD fin  14-JUN-1991 
           GO TO FIN-BUS-WSS-GLS-SFIN.
       NXT-BUS-WSS-GLS-SFIN.
      * Valor de Cambio para Moneda Nacional a Fecha Vencimiento y Valores de
      * Reajuste y Final Vencimiento
           MOVE 'TAB'        TO FIO-SIST.
           MOVE 'VLR'        TO TAB-CIC-TTAB.
           MOVE WSS-CIC-MRXL TO TAB-CIC-CTAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF TAB-COD-CTAB IN TAB NOT = OPC-COD-VCOC IN SRT
               MOVE VEN-FEC-FVEN IN VEN TO CAM-FEC-FCAM IN CAM(1)
                                           CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN SRT TO CAM-COD-VCAM IN CAM(1)
               MOVE TAB-COD-CTAB IN TAB TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
      *DAD ini  14-JUN-1991 
                   MOVE '00'   TO FIO-STAT
                   MOVE ZEROES TO WSS-VAL-VCFV IN WSS-LC2
                   MOVE ZEROES TO WSS-VAL-REAJ IN WSS-LC2
      *DAD fin  14-JUN-1991 
                   MOVE ZEROES TO WSS-VAL-SFIN IN WSS-LC2
                   DISPLAY 'NO EXISTE VALOR CAMBIO OPER : '
                           VEN-CAI-IOPC IN VEN '/'
                           VEN-IIC-IOPC IN VEN '/'
                           CAM-FEC-FCAM IN CAM(1) '/'
                           OPC-COD-VCOC IN SRT '/'
                           TAB-COD-CTAB IN TAB
               ELSE
               IF CAM-IND-VIGE NOT = 'S'
      *DAD ini  14-JUN-1991 
                   MOVE ZEROES TO WSS-VAL-REAJ IN WSS-LC2
      *DAD fin  14-JUN-1991 
                   MOVE ZEROES TO WSS-VAL-SFIN IN WSS-LC2
                   DISPLAY 'IND. DE VIGENCIA NO VALIDO OPER : '
                           VEN-CAI-IOPC IN VEN '/'
                           VEN-IIC-IOPC IN VEN '/'
                           CAM-FEC-FCAM IN CAM(1) '/'
                           OPC-COD-VCOC IN SRT '/'
                           TAB-COD-CTAB IN TAB
               ELSE
                   MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VCFV IN WSS-LC2
      *DAD ini  14-JUN-1991 
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN VEN
                                         GIVING WSS-VAL-REAJ IN WSS-LC2
                   MOVE WSS-VAL-REAJ IN WSS-LC2 TO PES-SGV-DECI
                                                             IN PES-VARI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE IN PES-VARI TO WSS-VAL-REAJ
                                                             IN WSS-LC2
      *DAD fin  14-JUN-1991 
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SFIN IN VEN
                                         GIVING WSS-VAL-SFIN IN WSS-LC2
                   MOVE WSS-VAL-SFIN IN WSS-LC2 TO PES-SGV-DECI
                                                             IN PES-VARI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE IN PES-VARI TO WSS-VAL-SFIN
                                                             IN WSS-LC2
           ELSE
               MOVE ZEROES              TO WSS-VAL-VCFV IN WSS-LC2
      *DAD ini  14-JUN-1991 
               MOVE ZEROES              TO WSS-VAL-REAJ IN WSS-LC2
      *DAD fin  14-JUN-1991 
               MOVE VEN-VAL-SFIN IN VEN TO WSS-VAL-SFIN IN WSS-LC2.
       FIN-BUS-WSS-GLS-SFIN.
           EXIT.
      *-------------------------------------------------------------------------
      * Obtiene glosas y valores asociados a la equivalencia en pesos para
      * Moneda Nacional a Fecha de Vencimiento y al Valor Final Vencimiento
      * de la parte Detalle del Vencimiento de LC2
       BUS-EQUI-VFIN SECTION.
       INI-BUS-EQUI-VFIN.
           IF WSS-CIC-MRXL IN WSS-VARI = WSS-CIC-VCOC IN WSS-VARI
               MOVE SPACES TO WSS-GLS-EQFV IN WSS-LC2
               MOVE SPACES TO WSS-GLS-VCFV IN WSS-LC2
               MOVE ZEROES TO VEN-VAL-SFIN IN VEN
               MOVE SPACES TO WSS-GLS-VCVC IN WSS-LC2
               MOVE ZEROES TO WSS-VAL-VCFV IN WSS-LC2
               GO TO FIN-BUS-EQUI-VFIN.

           IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC IN WSS-VARI
               MOVE WSS-DCO-MRXL IN WSS-VARI TO WSS-GLS-EQFV IN WSS-LC2
               MOVE WSS-DES-MRXL IN WSS-VARI TO WSS-GLS-VCFV IN WSS-LC2
               MOVE WSS-ABR-VCOC IN WSS-VARI TO WSS-GLS-VCVC IN WSS-LC2
           ELSE
               MOVE SPACES TO WSS-GLS-EQFV IN WSS-LC2
               MOVE SPACES TO WSS-GLS-VCFV IN WSS-LC2
               MOVE ZEROES TO VEN-VAL-SFIN IN VEN
               MOVE SPACES TO WSS-GLS-VCVC IN WSS-LC2
               MOVE ZEROES TO WSS-VAL-VCFV IN WSS-LC2.
       FIN-BUS-EQUI-VFIN.
           EXIT.
      **************************************************************************
      * Obtiene Valores de Capital, Intereses, Final y Comision, y ademas el
      * Valor de Cambio para Moneda Nacional equivalente en pesos a Fecha de
      * Cancelacion para la parte Detalle de la Cancelacion de LC2
       BUS-WSS-GLS-VCAN SECTION.
       INI-BUS-WSS-GLS-VCAN.
           MOVE OPC-COD-VCOC IN SRT TO WSS-COD-VCOC IN WSS-VARI.
           IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC IN WSS-VARI AND
              WSS-CIC-MRXL IN WSS-VARI > SPACES 
               GO TO NXT-BUS-WSS-GLS-VCAN.

      * Para Moneda Extranjera
           MOVE ZEROES TO WSS-VAL-VCFP IN WSS-LC2.
           GO TO FIN-BUS-WSS-GLS-VCAN.
       NXT-BUS-WSS-GLS-VCAN.
      * Para Moneda Nacional en Pesos
           MOVE 'TAB'        TO FIO-SIST.
           MOVE 'VLR'        TO TAB-CIC-TTAB.
           MOVE WSS-CIC-MRXL TO TAB-CIC-CTAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF TAB-COD-CTAB = OPC-COD-VCOC IN SRT
               MOVE ZEROES TO WSS-VAL-VCFP IN WSS-LC2
               GO TO FIN-BUS-WSS-GLS-VCAN.

      * Valor de Cambio para Moneda Nacional a Fecha Cancelacion
           MOVE CAN-NUM-DCAN IN SRT TO CAM-NUM-DCAM IN CAM(1).
           MOVE CAN-NUM-MCAN IN SRT TO CAM-NUM-MCAM IN CAM(1).
           MOVE CAN-NUM-SCAN IN SRT TO CAM-NUM-SCAM IN CAM(1).
           MOVE CAN-NUM-ACAN IN SRT TO CAM-NUM-ACAM IN CAM(1).
           MOVE CAN-NUM-DCAN IN SRT TO CAM-NUM-DCAM IN CAM(2).
           MOVE CAN-NUM-MCAN IN SRT TO CAM-NUM-MCAM IN CAM(2).
           MOVE CAN-NUM-SCAN IN SRT TO CAM-NUM-SCAM IN CAM(2).
           MOVE CAN-NUM-ACAN IN SRT TO CAM-NUM-ACAM IN CAM(2).
           MOVE OPC-COD-VCOC IN SRT TO CAM-COD-VCAM IN CAM(1).
           MOVE TAB-COD-CTAB IN TAB TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE VALOR CAMBIO OPER : '
                       CAN-CAI-IOPC IN SRT '/'
                       CAN-IIC-IOPC IN SRT '/'
                       CAM-FEC-FCAM IN CAM(1) '/'
                       OPC-COD-VCOC IN SRT '/'
                       TAB-COD-CTAB IN TAB
               GO TO FIN-BUS-WSS-GLS-VCAN.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY 'IND. DE VIGENCIA NO VALIDO OPER : '
                       CAN-CAI-IOPC IN SRT '/'
                       CAN-IIC-IOPC IN SRT '/'
                       CAM-FEC-FCAM IN CAM(1) '/'
                       OPC-COD-VCOC IN SRT '/'
                       TAB-COD-CTAB IN TAB
               GO TO FIN-BUS-WSS-GLS-VCAN.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VCFP IN WSS-LC2.

      *DAD ini  27-MAR-1991 
      * Valor Interes Cancelado de la RCV 
           MULTIPLY CAM-SGV-VCAM IN CAM BY WSS-SGV-INTE IN WSS-LC2
                                    GIVING WSS-SGV-INTE IN WSS-LC2.
           MOVE WSS-SGV-INTE IN WSS-LC2 TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO WSS-SGV-INTE IN WSS-LC2.
      *DAD fin  27-MAR-1991 

      * Valor Comision Cancelado de la CAN
           MULTIPLY CAM-SGV-VCAM IN CAM BY WSS-VAL-COMI IN WSS-LC2
                                    GIVING WSS-VAL-COMI IN WSS-LC2.
           MOVE WSS-VAL-COMI IN WSS-LC2 TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO WSS-VAL-COMI IN WSS-LC2.

      * Valor Final Cancelado de la RCV 
           MULTIPLY CAM-SGV-VCAM IN CAM BY RCV-SGV-FINA IN RCV
                                    GIVING WSS-SGV-FNAL IN WSS-VARI.
           MOVE WSS-SGV-FNAL IN WSS-VARI TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO WSS-SGV-FNAL IN WSS-VARI.

      * Valor de Cambio para Moneda Nacional a Fecha Colocacion
           MOVE OPC-NUM-DCOL IN SRT TO CAM-NUM-DCAM IN CAM(1).
           MOVE OPC-NUM-MCOL IN SRT TO CAM-NUM-MCAM IN CAM(1).
           MOVE OPC-NUM-SCOL IN SRT TO CAM-NUM-SCAM IN CAM(1).
           MOVE OPC-NUM-ACOL IN SRT TO CAM-NUM-ACAM IN CAM(1).
           MOVE OPC-NUM-DCOL IN SRT TO CAM-NUM-DCAM IN CAM(2).
           MOVE OPC-NUM-MCOL IN SRT TO CAM-NUM-MCAM IN CAM(2).
           MOVE OPC-NUM-SCOL IN SRT TO CAM-NUM-SCAM IN CAM(2).
           MOVE OPC-NUM-ACOL IN SRT TO CAM-NUM-ACAM IN CAM(2).
           MOVE OPC-COD-VCOC IN SRT TO CAM-COD-VCAM IN CAM(1).
           MOVE TAB-COD-CTAB IN TAB TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE VALOR CAMBIO OPER : '
                       CAN-CAI-IOPC IN SRT '/'
                       CAN-IIC-IOPC IN SRT '/'
                       CAM-FEC-FCAM IN CAM(1) '/'
                       OPC-COD-VCOC IN SRT '/'
                       TAB-COD-CTAB IN TAB
               GO TO FIN-BUS-WSS-GLS-VCAN.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY 'IND. VIGENCIA NO VALIDO OPER : '
                       CAN-CAI-IOPC IN SRT '/'
                       CAN-IIC-IOPC IN SRT '/'
                       CAM-FEC-FCAM IN CAM(1) '/'
                       OPC-COD-VCOC IN SRT '/'
                       TAB-COD-CTAB IN TAB
               GO TO FIN-BUS-WSS-GLS-VCAN.

      * Valor Capital Cancelado de la RCV 
           MULTIPLY CAM-SGV-VCAM IN CAM BY RCV-SGV-CAPI IN RCV
                                    GIVING RCV-SGV-CAPI IN RCV.
           MOVE RCV-SGV-CAPI IN RCV TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO RCV-SGV-CAPI IN RCV.
       FIN-BUS-WSS-GLS-VCAN.
           EXIT.
      *-------------------------------------------------------------------------
      * Obtiene glosas y valores asociados a la equivalencia en pesos para
      * Moneda Nacional a Fecha de Cancelacion de la parte Detalle de la
      * Cancelacion de LC2
       BUS-EQUI-VCAN SECTION.
       INI-BUS-EQUI-VCAN.
           IF WSS-CIC-MRXL IN WSS-VARI = WSS-CIC-VCOC IN WSS-VARI
               MOVE ZEROES TO WSS-VAL-VCFP IN WSS-LC2
               MOVE SPACES TO WSS-GLS-VCFP IN WSS-LC2
               GO TO FIN-BUS-EQUI-VCAN.

           IF WSS-MSC-TVAL IN WSS-VARI = WSS-MSC-TVOC IN WSS-VARI
               MOVE WSS-DES-MRXL IN WSS-VARI TO WSS-GLS-VCFP IN WSS-LC2
           ELSE
               MOVE SPACES TO WSS-GLS-VCFP IN WSS-LC2
               MOVE ZEROES TO WSS-VAL-VCFP IN WSS-LC2.
       FIN-BUS-EQUI-VCAN.
           EXIT.
      **************************************************************************
      * Obtiene glosas y valores asociados a la Forma de Pago del Detalle de la
      * Cancelacion que forman parte de LC2
       GET-CYA SECTION.
       INI-GET-CYA.
           MOVE ZEROES TO WSS-NUM-ICYA IN WSS-VARI.
           MOVE SPACES TO WSS-GLS-VCC1 IN WSS-LC2.
           MOVE ZEROES TO WSS-VAL-VCC1 IN WSS-LC2.
           MOVE SPACES TO WSS-GLS-VCC2 IN WSS-LC2.
           MOVE ZEROES TO WSS-VAL-VCC2 IN WSS-LC2.
           MOVE SPACES TO WSS-GLS-VCC3 IN WSS-LC2.
           MOVE ZEROES TO WSS-VAL-VCC3 IN WSS-LC2.
           MOVE SPACES TO WSS-GLS-VCC4 IN WSS-LC2.
           MOVE ZEROES TO WSS-VAL-VCC4 IN WSS-LC2.
           MOVE SPACES TO WSS-GLS-VCC5 IN WSS-LC2.
           MOVE ZEROES TO WSS-VAL-VCC5 IN WSS-LC2.
           MOVE CAN-CAI-IOPC IN SRT TO CYA-CAI-IOPC IN CYA.
           MOVE CAN-IIC-IOPC IN SRT TO CYA-IIC-IOPC IN CYA.
           MOVE CAN-NUM-ICAN IN SRT TO CYA-NUM-ICAN IN CYA.
           MOVE ZEROES              TO CYA-NUM-ICYA IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT ( FIO-STAT-OKS AND
                    CAN-CAI-IOPC IN SRT = CYA-CAI-IOPC IN CYA AND
                    CAN-IIC-IOPC IN SRT = CYA-IIC-IOPC IN CYA AND
                    CAN-NUM-ICAN IN SRT = CYA-NUM-ICAN IN CYA )
               GO TO FIN-GET-CYA.
       LUP-GET-CYA.
           IF CYA-IIC-ICYA IN CYA = '00000000'
               MOVE SPACES TO CYA-IIC-ICYA IN CYA.
           ADD 1 TO WSS-NUM-ICYA IN WSS-VARI.
      *DAD ini  27-MAR-1991 
      * Acumula Valores para Formas de Pago superiores al WSS-IMAX
           IF WSS-NUM-ICYA IN WSS-VARI > WSS-IMAX IN WSS-VARI
               ADD  CYA-SGV-LIQU IN CYA      TO WSS-VAL-VCC5 IN WSS-LC2
               MOVE WSS-DES-M6AL IN WSS-VARI TO WSS-GLS-VCC5 IN WSS-LC2
               GO TO NXT-GET-CYA.
      *DAD fin  27-MAR-1991 

           PERFORM BUS-MOV.
           PERFORM BUS-TCYA.
           IF WSS-NUM-ICYA IN WSS-VARI = 1
               MOVE '(AVISO DE CARGO)' TO WSS-GLS-FPAG IN WSS-LC2
               STRING WSS-ABR-MOV  IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      WSS-ABR-TCYA IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      CYA-IIC-ICYA IN CYA DELIMITED BY SIZE INTO
                      WSS-GLS-VCC1 IN WSS-LC2
               MOVE CYA-SGV-LIQU IN CYA TO WSS-VAL-VCC1 IN WSS-LC2
           ELSE
           IF WSS-NUM-ICYA IN WSS-VARI = 2
               STRING WSS-ABR-MOV  IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      WSS-ABR-TCYA IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      CYA-IIC-ICYA IN CYA DELIMITED BY SIZE INTO
                      WSS-GLS-VCC2 IN WSS-LC2
               MOVE CYA-SGV-LIQU IN CYA TO WSS-VAL-VCC2 IN WSS-LC2
           ELSE
           IF WSS-NUM-ICYA IN WSS-VARI = 3
               STRING WSS-ABR-MOV  IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      WSS-ABR-TCYA IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      CYA-IIC-ICYA IN CYA DELIMITED BY SIZE INTO
                      WSS-GLS-VCC3 IN WSS-LC2
               MOVE CYA-SGV-LIQU IN CYA TO WSS-VAL-VCC3 IN WSS-LC2
           ELSE
           IF WSS-NUM-ICYA IN WSS-VARI = 4
               STRING WSS-ABR-MOV  IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      WSS-ABR-TCYA IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      CYA-IIC-ICYA IN CYA DELIMITED BY SIZE INTO
                      WSS-GLS-VCC4 IN WSS-LC2
      *DAD ini  13-MAY-1991 / 27-MAR-1991 
               MOVE CYA-SGV-LIQU IN CYA TO WSS-VAL-VCC4 IN WSS-LC2
           ELSE
           IF WSS-NUM-ICYA IN WSS-VARI = 5
               STRING WSS-ABR-MOV  IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      WSS-ABR-TCYA IN WSS-VARI
                      WSS-GLS-SPAC IN WSS-VARI
                      CYA-IIC-ICYA IN CYA DELIMITED BY SIZE INTO
                      WSS-GLS-VCC5 IN WSS-LC2
               MOVE CYA-SGV-LIQU IN CYA TO WSS-VAL-VCC5 IN WSS-LC2.
      *DAD fin  13-MAY-1991 / 27-MAR-1991 
       NXT-GET-CYA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF FIO-STAT-OKS AND
              CAN-CAI-IOPC IN SRT = CYA-CAI-IOPC IN CYA AND
              CAN-IIC-IOPC IN SRT = CYA-IIC-IOPC IN CYA AND
              CAN-NUM-ICAN IN SRT = CYA-NUM-ICAN IN CYA
               GO TO LUP-GET-CYA.
           MOVE '00' TO FIO-STAT.
       FIN-GET-CYA.
           EXIT.

       BUS-MOV SECTION.
       INI-BUS-MOV.
           IF CYA-NUM-ICAN IN CYA > ZEROES
               MOVE 'C' TO TAB-CIC-CTAB IN TAB
           ELSE
               MOVE 'A' TO TAB-CIC-CTAB IN TAB.
           IF CYA-SGV-LIQU IN CYA < ZEROES
               MOVE CYA-SGV-LIQU IN CYA      TO WSS-SGV-LIQU IN WSS-VARI
               MOVE WSS-SGV-LIQU IN WSS-VARI TO CYA-SGV-LIQU IN CYA
               MOVE 'A'                      TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'MOV' TO TAB-CIC-TTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-ABRV IN TAB TO WSS-ABR-MOV IN WSS-VARI.
       FIN-BUS-MOV.
           EXIT.

       BUS-TCYA SECTION.
       INI-BUS-TCYA.
           IF CYA-IND-TCYA IN CYA > SPACES
               MOVE 'TAB'               TO MSC-COD-SIST
               MOVE 'TCYA'              TO MSC-COD-TMSC IN MSC
               MOVE CYA-IND-TCYA IN CYA TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO WSS-ABR-TCYA IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-ABR-TCYA IN WSS-VARI.
       FIN-BUS-TCYA.
           EXIT.
      **************************************************************************
      * Obtiene Comentarios asociados al Reporte para la parte Detalle de la
      * Cancelacion de LC2
       BUS-WSS-GLS-MSG SECTION.
       INI-BUS-WSS-GLS-MSG.
           MOVE SPACES TO WSS-GLS-MSG1 IN WSS-LC2.
           MOVE SPACES TO WSS-GLS-MSG2 IN WSS-LC2.
           MOVE 'COL'  TO RCE-COD-SIST IN RCE.
           MOVE 'LCC'  TO RCE-COD-ENTI IN RCE.
           MOVE SPACES TO RCE-GLS-IENT IN RCE.
           MOVE SPACES TO RCE-COD-SENT IN RCE.
           MOVE 'RCE-KEY-IENT' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-RCE.
           IF NOT FIO-STAT-OKS
               GO TO FIN-BUS-WSS-GLS-MSG.

           MOVE RCE-CIC-IENT IN RCE TO CMT-CIC-IENT IN CMT.
           MOVE SPACES              TO CMT-FEC-FCMT IN CMT.
           MOVE ZEROES              TO CMT-NUM-ICMT IN CMT.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-CMT.
           IF NOT ( FIO-STAT-OKS AND
                    RCE-CIC-IENT IN RCE = CMT-CIC-IENT IN CMT )
               GO TO FIN-BUS-WSS-GLS-MSG.

           MOVE CMT-GLS-REFE IN CMT TO WSS-GLS-MSG1 IN WSS-LC2.
           MOVE CMT-GLS-FILL IN CMT TO WSS-GLS-MSG2 IN WSS-LC2.
       FIN-BUS-WSS-GLS-MSG.
           EXIT.
      **************************************************************************
      * Obtiene Antecedentes de la Tasa de Cancelacion del Vencimiento
       BUS-ICG-CAN SECTION.
       INI-BUS-ICG-CAN.
           PERFORM GET-CUR-ICG-CAN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-BUS-ICG-CAN.
      *     PERFORM GET-NXT-ICG-CAN.
      *     IF ICG-COD-TINT IN ICG = 'M'
      *         MOVE 'MULTIPLES'  TO WSS-COD-TTCA IN WSS-CAN
      *         MOVE ZEROES       TO WSS-SGV-TCAN IN WSS-CAN
      *     ELSE
      *         MOVE ICG-COD-TINT IN ICG TO WSS-COD-TTCA IN WSS-CAN
      *         MOVE ICG-SGV-TINT IN ICG TO WSS-SGV-TCAN IN WSS-CAN.
       FIN-BUS-ICG-CAN.
           EXIT.

       GET-CUR-ICG-CAN SECTION.
       INI-GET-CUR-ICG-CAN.
           MOVE CAN-CAI-IOPC IN SRT TO ICG-CAI-IOPC IN ICG.
           MOVE CAN-IIC-IOPC IN SRT TO ICG-IIC-IOPC IN ICG.
           MOVE CAN-NUM-ICAN IN SRT TO ICG-NUM-ICAN IN ICG.
           MOVE CAN-CAI-IOPC IN SRT TO ICG-CAI-IOPV IN ICG.
           MOVE CAN-IIC-IOPC IN SRT TO ICG-IIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS                              AND
                    ICG-CAI-IOPC IN ICG = CAN-CAI-IOPC IN SRT AND
                    ICG-IIC-IOPC IN ICG = CAN-IIC-IOPC IN SRT AND
                    ICG-NUM-ICAN IN ICG = CAN-NUM-ICAN IN SRT AND
                    ICG-CAI-IOPV IN ICG = CAN-CAI-IOPC IN SRT AND
                    ICG-IIC-IOPV IN ICG = CAN-IIC-IOPC IN SRT AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               MOVE '23' TO FIO-STAT.
       FIN-GET-CUR-ICG-CAN.
           EXIT.
      *-------------------------------------------------------------------------
      * ESTE MODULO NO SE EJECUTA                     -- DAD --
       GET-NXT-ICG-CAN SECTION.
       INI-GET-NXT-ICG-CAN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF FIO-STAT-OKS                              AND
              ICG-CAI-IOPC IN ICG = CAN-CAI-IOPC IN SRT AND
              ICG-IIC-IOPC IN ICG = CAN-IIC-IOPC IN SRT AND
              ICG-NUM-ICAN IN ICG = CAN-NUM-ICAN IN SRT AND
              ICG-CAI-IOPV IN ICG = CAN-CAI-IOPC IN SRT AND
              ICG-IIC-IOPV IN ICG = CAN-IIC-IOPC IN SRT AND
              ICG-IND-TTAS IN ICG = 'INT'
                MOVE 'M' TO ICG-COD-TINT IN ICG
           ELSE
                MOVE SPACES TO ICG-COD-TINT IN ICG
                PERFORM GET-CUR-ICG-CAN.
       FIN-GET-NXT-ICG-CAN.
           EXIT.
      *-------------------------------------------------------------------------
      * Obtiene glosas asociadas a la Tasa de Cancelacion del Vencimiento que
      * forman parte del Detalle de la Cancelacion de LC2
       BUS-GLS-ICG-CAN SECTION.
       INI-BUS-GLS-ICG-CAN.
      *DAD ini  27-MAR-1991 
      * El valor del SPREAD se utiliza solo si tiene infomacion (lapsus cancel)
           IF ICG-COD-CVTI IN ICG > SPACES
               IF ICG-SGV-SPRD IN ICG > ZEROES
                   MOVE ICG-SGV-SPRD IN ICG TO ICG-SGV-TINT IN ICG.
      *DAD fin  27-MAR-1991 

           IF ICG-MSC-BCTI IN ICG > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'BCTI'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-BCTI IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DCOR IN MSC TO WSS-GLS-BCTI IN WSS-LC2
           ELSE
               MOVE SPACES TO WSS-GLS-BCTI IN WSS-LC2.

           IF ICG-MSC-PBTI IN ICG > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'PBTI'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-PBTI IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DCOR IN MSC TO WSS-GLS-PBTI IN WSS-LC2
           ELSE
               MOVE SPACES TO WSS-GLS-PBTI IN WSS-LC2.

           IF ICG-MSC-SPBT IN ICG > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'MCTI'              TO MSC-COD-TMSC IN MSC
               MOVE ICG-MSC-SPBT IN ICG TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO WSS-GLS-MCTI IN WSS-LC2
           ELSE
               MOVE SPACES TO WSS-GLS-MCTI IN WSS-LC2.
       FIN-BUS-GLS-ICG-CAN.
           EXIT.
      **************************************************************************

       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY SGCBFDBC.
       COPY SGCBFRCD.
       COPY COLBFOPC.
       COPY COLBFRDC.
       COPY COLBFICG.
       COPY COLBFVEN.
       COPY COLBFEVC.
       COPY COLBFDLC.
       COPY COLBFCYA.
       COPY COLBFRCV.
       COPY COLBFTOC.
       COPY TABBFOFI.
       COPY TABBFUSR.
       COPY TABBFVLR.
       COPY TABBFCAM.
       COPY GNSBGCPT.
       COPY GNSBBTAB.
       COPY TABBBOFI.
       COPY TABBBUSR.
       COPY TABBVOFI.
       COPY TABBVUSR.
       COPY GNSBVTAB.
       COPY GNSBBMSG.
       COPY GNSBFMSC.
       COPY GNSBVMSC.
       COPY GNSBBMSC.
       COPY GNSBFRCE.
       COPY GNSBFCMT.
       COPY GNSBGPES.
      *SMC-INI 09.09.1993
       COPY COLBGINT.
      *SMC-FIN 09.09.1993
       COPY GNSBGCVI.
       COPY GNSBPFEC.
      *DAD ini  03-ENE-1992   (Memo.02-01-92)
       COPY GNSBGCMM.
      *DAD fin  03-ENE-1992
       COPY COLBGVEN.
      *DAD ini  14-AGO-1991
       COPY COLBGCTO.
      *DAD fin  14-AGO-1991
      *>>>>
*% END
