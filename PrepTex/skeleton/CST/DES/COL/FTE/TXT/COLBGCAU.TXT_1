      **************************************************************************
      *DAD ini  23-JUL-1991
      *
      *    Proceso para Cargar la tabla  COL/AUX  en Memoria
      *    INPUT   :  La tabla  COL/AUX.
      *    OUTPUT  :  La tabla CAU-VECT con los campos cargados
      *               CAU-CMSG IN CAU-VARI
      *               CAU-MEN2 IN CAU-VARI.
      *    PERFORM :  COL-CRG-CAU.
      *
      *    Busca con el Codigo Clase Operacion en la Tabla cargada en Memoria
      *    el Indicador de Exclusion o Inclusion segun programa
      *    INPUT   :  CAU-COOC IN CAU-VARI.
      *    OUTPUT  :  CAU-DAT3 IN CAU-VARI
      *               CAU-CMSG IN CAU-VARI
      *               CAU-MEN2 IN CAU-VARI.
      *    PERFORM :  COL-BUS-CAU.
      *
      *DAD fin  23-JUL-1991
      **************************************************************************
      *DAD ini  23-JUL-1991
      * Proceso para Cargar la tabla  COL/AUX  en Memoria
       COL-CRG-CAU SECTION.
       INI-COL-CRG-CAU.
           MOVE SPACES TO CAU-TABL IN CAU-VARI
                          CAU-CMSG IN CAU-VARI
                          CAU-MEN2 IN CAU-VARI.
           MOVE ZEROES TO CAU-INDI IN CAU-VARI.
       INI-COL-CRG-CAU-AUX.
      * Inicio Carga de la tabla  COL/AUX  en Memoria
           MOVE 'COL'  TO TAB-COD-SIST FIO-SIST.
           MOVE 'AUX'  TO TAB-COD-TTAB IN TAB.
           MOVE SPACES TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR
              TAB-COD-TTAB IN TAB NOT = 'AUX'
               MOVE 'TABAUX NOHAY' TO CAU-CMSG IN CAU-VARI
               MOVE SPACES         TO CAU-MEN2 IN CAU-VARI
               GO TO FIN-COL-CRG-CAU.
       LUP-COL-CRG-CAU-AUX.
      * Obtiene 'Codigo Clase Operacion' y 'Datos Anexos 3' en COL/AUX
           ADD  1  TO CAU-INDI IN CAU-VARI.
           MOVE TAB-COD-CTAB IN TAB TO CAU-CTAB(CAU-INDI).
           MOVE TAB-GLS-DAT3 IN TAB TO CAU-AUX3(CAU-INDI).
           IF CAU-INDI IN CAU-VARI > CAU-OCUR IN CAU-VARI
               MOVE 'TAB    CHICA'         TO CAU-CMSG IN CAU-VARI
               MOVE 'CLASE DE OPERACIONES' TO CAU-MEN2 IN CAU-VARI
               GO TO FIN-COL-CRG-CAU.
       NXT-COL-CRG-CAU-AUX.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'AUX'
               GO TO LUP-COL-CRG-CAU-AUX.
           MOVE CAU-INDI IN CAU-VARI TO CAU-MAXI IN CAU-VARI.
       FIN-COL-CRG-CAU.
           EXIT.
      *DAD fin  23-JUL-1991
      **************************************************************************
      *DAD ini  23-JUL-1991
      * Busca con el Codigo Clase Operacion en la Tabla cargada en Memoria
      * el Indicador de Exclusion o Inclusion segun programa
       COL-BUS-CAU SECTION.
       INI-COL-BUS-CAU.
           MOVE SPACES TO CAU-DAT3 IN CAU-VARI
                          CAU-CMSG IN CAU-VARI
                          CAU-MEN2 IN CAU-VARI.
           SET CAU-NUME TO 1.
           SEARCH CAU-VECT VARYING CAU-NUME
                  WHEN CAU-CTAB(CAU-NUME) = CAU-COOC IN CAU-VARI
                       GO TO FND-COL-BUS-CAU.
           MOVE 'TABCOD NOFND'         TO CAU-CMSG IN CAU-VARI.
           MOVE 'CLASE DE OPERACIONES' TO CAU-MEN2 IN CAU-VARI.
           GO TO FIN-COL-BUS-CAU.
       FND-COL-BUS-CAU.
           MOVE CAU-AUX3(CAU-NUME) TO CAU-DAT3 IN CAU-VARI.
       FIN-COL-BUS-CAU.
           EXIT.
      *DAD fin  23-JUL-1991
      **************************************************************************
