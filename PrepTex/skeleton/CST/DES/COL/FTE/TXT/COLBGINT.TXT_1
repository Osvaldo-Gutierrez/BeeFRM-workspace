      *Operacion                : INT-IOPC
      *Numero de la IDLC        : INT-IDLC
      *Valor de Cambio          : INT-VCAM
      *Fecha Colocacion         : INT-FCOL
      *Fecha Inicial Periodo    : INT-FUNO
      *Fecha Final   Periodo    : INT-FDOS
      *Fecha Basa Tasa Variable : INT-FBTV ( opcional )
      *Fecha de Proceso         : INT-FPRO ( la rutina la obtiene)
      *
      *Devuelve:
      *INT-TIPE                 : Tasa calculada para el periodo
      *INT-CMSG, INT-MENS       : Codigo y mensaje
      *INT-IETB                 : Indicador de existencia de la tasa base

       COL-CAL-INT SECTION.
       INI-COL-CAL-INT.
      * Inicializa con espacios las variables de seteo de códigos y
      * mensajes de error.
           MOVE SPACES TO INT-CMSG INT-MENS INT-IETB.
      * Busca tasa(s) en registro(s) ICG(s)
           PERFORM COL-BUS-TIN.
      * Si se encontró con algún error en la búsqueda de ICG, finaliza
           IF INT-CMSG > SPACES
               GO TO FIN-COL-CAL-INT.

      *DAD ini  31-MAR-1994   (Memo.04-02-94)
      * Si viene con un tipo tasa se utiliza codigo y monto tasa trae
           IF INT-TIP2 > SPACES
               MOVE INT-TIP2 TO INT-TIPT
               MOVE INT-SPR2 TO INT-TINT.
      *DAD fin  31-MAR-1994

      * Para sumar o multiplicar tasas segun el INT-SPBT que puede ser
      * S (simple) o C (compuesta).
           IF INT-SPBT = 'S'
               MOVE 0 TO INT-TIPA
           ELSE
               MOVE 1 TO INT-TIPA.

      * Si el INT-CVTI > SPACES significa que tiene una base, por tanto
      * es tasa variable y se obtiene la fecha y hora computacional,
      * seteando las variables INT-FPRO (Fecha Hoy) e INT-HPRO (Hora Hoy)
      * para el chequeo de tasa variable
           IF INT-CVTI > SPACES
               PERFORM GET-FHOY
               MOVE HOY-DHOY TO INT-DPRO
               MOVE HOY-MHOY TO INT-MPRO
               MOVE HOY-SHOY TO INT-SPRO
               MOVE HOY-AHOY TO INT-APRO
               MOVE HOY-HHHY TO INT-HHPR
               MOVE HOY-MMHY TO INT-MMPR
               MOVE HOY-SSHY TO INT-SSPR.

      * Entra al IF, si son operaciones con respecto al flujo (normal) o
      * bien cualquier operación con respecto al flujo, cuyo período de 
      * cálculo FUNO-FDOS sea cubierto por una ICG (esto no quita, si el
      * crédito tiene múltiples ICG, puesto que una de ellas puede cubrir
      * el período de cálculo)
      * Se obtiene cantidad de días que hay entre la FUNO y la FDOS.
      * Aquí si el crédito es tasa variable se supone que la base no
      * cambio entre estas 2 fechas, por tanto se obtiene la diferencia
      * en días de forma directa.
           IF NOT ( ( INT-TFPR = 'C' AND INT-PREP > ZEROES ) OR
                    INT-IMAX > 1 )
               MOVE INT-DUNO TO FEC-DEC1
               MOVE INT-MUNO TO FEC-MEC1
               MOVE INT-SUNO TO FEC-SEC1
               MOVE INT-AUNO TO FEC-AEC1
               MOVE INT-DDOS TO FEC-DEC2
               MOVE INT-MDOS TO FEC-MEC2
               MOVE INT-SDOS TO FEC-SEC2
               MOVE INT-ADOS TO FEC-AEC2
               MOVE FEC-FORM-FEC TO FEC-FORM
               MOVE FEC-DIF-DIA  TO FEC-CMND
      *Para GENESIS version 3.1
               PERFORM CAL-FEC
      *        PERFORM PRO-FEC
               IF NOT FEC-STAT-OKS
                   MOVE 'INTFEC1FEC2-' TO INT-CMSG
                   GO TO FIN-COL-CAL-INT
               ELSE
                   MOVE FEC-NDIA TO INT-SEPD
      *SMC-INI 22.09.1993
      * Se setea INT-DDMA, INT-MDMA, INT-SDMA, INT-ADMA que son los items
      * elementales de la variable INT-FINT, la cual sirve sólo para
      * determinar la fecha de la base de un crédito tasa variable y así
      * obtener su base en la tabla CAM (valores de cambio).
      *
      * Cómo se setea la INT-FINT, en esta rama de la rutina:
      * - Si la ICG es con respecto al flujo y tiene ZEROES en período de
      *   reparto, significa que la operación se comporta en esta parte
      *   de la línea del tiempo FUNO, FDOS como una operación en que su
      *   cálculo de interés de una próxima cuota se fija mirando su base
      *   a la fecha de vencimiento del Vcto. anterior al que se está
      *   calculando, por tanto aquí hay que tener atención que el FUNO
      *   debe ser sólo la Fecha de Vencimiento de un VEN y la FDOS puede
      *   ser < o = a la Fecha de Vencimiento del siguiente VEN.
                   IF ( INT-TFPR = 'F' AND INT-PREP = ZEROES )
                        MOVE INT-DUNO TO INT-DDMA
                        MOVE INT-MUNO TO INT-MDMA
                        MOVE INT-SUNO TO INT-SDMA
                        MOVE INT-AUNO TO INT-ADMA
                        PERFORM GNS-CAL-TPE
                        GO TO FIN-COL-CAL-INT
                   ELSE
      * - Si la ICG es con respecto al flujo y su período de reparto es
      *   mayor que 0 (INT-PREP > 0) significa que es una operación de
      *   crédito repactable cada INT-PREP cuotas. Si es la primera ICG,
      *   significa que el INT-FINT es igual a ZERO por tanto la base se
      *   debe tomar a la fecha de colocación de la operación, sino (es
      *   decir INT-FINT <> 0) significa que la base se toma al inicio de
      *   esa ICG.
                        IF ( INT-TFPR = 'F' AND INT-PREP > ZEROES AND
                             INT-FINT =ZEROS )
                             MOVE INT-DCOL TO INT-DDMA
                             MOVE INT-MCOL TO INT-MDMA
                             MOVE INT-SCOL TO INT-SDMA
                             MOVE INT-ACOL TO INT-ADMA
                             PERFORM GNS-CAL-TPE
                             GO TO FIN-COL-CAL-INT
                        ELSE
                            PERFORM GNS-CAL-TPE
                            GO TO FIN-COL-CAL-INT.
      *SMC-FIN 22.09.1993

      *SMC-INI 03.09.1993
           IF INT-DDMA NOT > ZEROS
              MOVE INT-DCOL TO  INT-DDMA
              MOVE INT-MCOL TO  INT-MDMA
              MOVE INT-SCOL TO  INT-SDMA
              MOVE INT-ACOL TO  INT-ADMA.

           IF INT-SPR2 > 0
               MOVE INT-TINT TO INT-TINC
               MOVE INT-SPR2 TO INT-TINT
               MOVE INT-TINC TO INT-SPR2
           ELSE
               MOVE INT-TINT TO INT-SPR2.

           IF INT-CVTI NOT > SPACES OR INT-TFPR = 'F'
               GO TO UNO-COL-CAL-INT.
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *     MOVE INT-DCOL TO FEC-ITM1 INT-DDMA.
      *     MOVE INT-MCOL TO FEC-ITM2 INT-MDMA.
      *     MOVE INT-SCOL TO FEC-ITM3 INT-SDMA.
      *     MOVE INT-ACOL TO FEC-ITM4 INT-ADMA.

           MOVE  INT-DDMA TO FEC-ITM1
           MOVE  INT-MDMA TO FEC-ITM2
           MOVE  INT-SDMA TO FEC-ITM3
           MOVE  INT-ADMA TO FEC-ITM4.
      *SMC-FIN 03.09.1993

       SUM-COL-CAL-INT.
           IF INT-UTPR = 'D'
               MOVE INT-PREP    TO FEC-NDIA
               MOVE FEC-SUM-DIA TO FEC-CMND
           ELSE
           IF INT-UTPR = 'M'
               MOVE INT-PREP    TO FEC-NMES
               MOVE FEC-SUM-MES TO FEC-CMND
           ELSE
               MOVE INT-PREP    TO FEC-NANO
               MOVE FEC-SUM-ANO TO FEC-CMND.
           MOVE FEC-FORM-FEC TO FEC-FORM.
      *Para GENESIS version 3.1
           PERFORM CAL-FEC.
      *    PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'INTFCOLPREP+' TO INT-CMSG
               GO TO FIN-COL-CAL-INT.
           MOVE FEC-ITM1 TO INT-DAU1.
           MOVE FEC-ITM2 TO INT-MAU1.
           MOVE FEC-ITM3 TO INT-SAU1.
           MOVE FEC-ITM4 TO INT-AAU1.
           MOVE INT-DUNO TO INT-DAU2.
           MOVE INT-MUNO TO INT-MAU2.
           MOVE INT-SUNO TO INT-SAU2.
           MOVE INT-AUNO TO INT-AAU2.
           IF INT-FAU1 NOT > INT-FAU2
               MOVE INT-DDMA TO INT-DAU2
               MOVE INT-MDMA TO INT-MAU2
               MOVE INT-SDMA TO INT-SAU2
               MOVE INT-ADMA TO INT-AAU2
               IF INT-FAU1 > INT-FAU2
                   MOVE INT-DAU1 TO INT-DDMA
                   MOVE INT-MAU1 TO INT-MDMA
                   MOVE INT-SAU1 TO INT-SDMA
                   MOVE INT-AAU1 TO INT-ADMA
                   GO TO SUM-COL-CAL-INT
               ELSE
                   GO TO SUM-COL-CAL-INT.
           MOVE FEC-ITM1 TO INT-DTIN(INT-IMAX).
           MOVE FEC-ITM2 TO INT-MTIN(INT-IMAX).
           MOVE FEC-ITM3 TO INT-STIN(INT-IMAX).
           MOVE FEC-ITM4 TO INT-ATIN(INT-IMAX).
       UNO-COL-CAL-INT.
      *Obtener interes entre INT-DUNO y MIN(FTIN(1...INT-IMAX),FDOS)
           MOVE INT-DUNO TO FEC-DEC1.
           MOVE INT-MUNO TO FEC-MEC1.
           MOVE INT-SUNO TO FEC-SEC1.
           MOVE INT-AUNO TO FEC-AEC1.
       CON-COL-CAL-INT.
           MOVE 0 TO INT-SIND INT-AUX1.
           MOVE '99999999' TO INT-FAU1.
       LUP-COL-CAL-INT.
           MOVE INT-TINX TO INT-TINT.
      *SMC-INI 03.09.1993
           MOVE INT-TIPX TO INT-TIPT
      *SMC-FIN 03.09.1993

           ADD 1 TO INT-SIND.
           IF ( INT-CVTI NOT > SPACES OR INT-TFPR = 'F' ) AND
              ( INT-SIND = INT-IMAX )
               NEXT SENTENCE
           ELSE
           IF INT-SIND NOT > INT-IMAX
               IF INT-FTIN(INT-SIND) < INT-FAU1 AND
                  INT-ITIN(INT-SIND) NOT > SPACES
                   MOVE INT-FTIN(INT-SIND) TO INT-FAU1
                   MOVE INT-SIND           TO INT-AUX1
                   GO TO LUP-COL-CAL-INT
               ELSE
                   GO TO LUP-COL-CAL-INT.
      *Cambio de SPREAD
           IF INT-AUX1 > 0 AND INT-AUX1 NOT = INT-IMAX
               MOVE INT-VTIN(INT-AUX1) TO INT-TINX
      *SMC-INI 03.09.1993
               MOVE INT-CTIN(INT-AUX1) TO INT-TIPX
      *SMC-FIN 03.09.1993
               MOVE 'X' TO INT-ITIN(INT-AUX1).
           MOVE INT-DDOS TO INT-DAU2.
           MOVE INT-MDOS TO INT-MAU2.
           MOVE INT-SDOS TO INT-SAU2.
           MOVE INT-ADOS TO INT-AAU2.
           IF INT-FAU1 < INT-FAU2
               MOVE INT-DAU1 TO FEC-DEC2
               MOVE INT-MAU1 TO FEC-MEC2
               MOVE INT-SAU1 TO FEC-SEC2
               MOVE INT-AAU1 TO FEC-AEC2
           ELSE
               MOVE INT-DAU2 TO FEC-DEC2
               MOVE INT-MAU2 TO FEC-MEC2
               MOVE INT-SAU2 TO FEC-SEC2
               MOVE INT-AAU2 TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *Para GENESIS version 3.1
           PERFORM CAL-FEC.
      *    PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'INTFEC1FEC2-' TO INT-CMSG
               GO TO FIN-COL-CAL-INT.
           MOVE FEC-NDIA TO INT-SEPD.
           PERFORM GNS-CAL-TPE.
           IF INT-CMSG > SPACES
      *        DISPLAY 'INT-CMSG : ' INT-CMSG
      *        DISPLAY 'INT-MENS : ' INT-MENS
               GO TO FIN-COL-CAL-INT.
      *     DISPLAY '---------------------------------------------- '
      *     DISPLAY 'FEC-FEC1 - FEC-FEC2 : ' FEC-FEC1 ' - ' FEC-FEC2.
      *     DISPLAY 'FEC-NDIA            : ' FEC-NDIA.
      *     DISPLAY 'INT-TIPT            : ' INT-TIPT.
      *     DISPLAY 'INT-TINT            : ' INT-TINT WITH CONVERSION.
      *     DISPLAY 'INT-TIPE            : ' INT-TIPE WITH CONVERSION.
      *     DISPLAY '------------------------------------------------'.
           MOVE INT-SPR2 TO INT-TINT.
      * Se suma o se multiplica segun sea simple o compuesta
           IF INT-SPBT = 'S'
               ADD INT-TIPE TO INT-TIPA
           ELSE
               ADD 1 TO INT-TIPE
               MULTIPLY INT-TIPE BY INT-TIPA GIVING INT-TIPA.
      *
      * Se termina si la fecha sumada es mayor o igual que la fecha Dos
           IF ( INT-FAU1 NOT < INT-FAU2 )
               IF INT-SPBT = 'S'
                   MOVE INT-TIPA TO INT-TIPE
                   GO TO FIN-COL-CAL-INT
               ELSE
                   SUBTRACT 1 FROM INT-TIPA GIVING INT-TIPE
                   GO TO FIN-COL-CAL-INT.

           MOVE FEC-DEC2 TO FEC-DEC1 INT-DDMA.
           MOVE FEC-MEC2 TO FEC-MEC1 INT-MDMA.
           MOVE FEC-SEC2 TO FEC-SEC1 INT-SDMA.
           MOVE FEC-AEC2 TO FEC-AEC1 INT-ADMA.
      * Si la fecha minima no fue el periodo de repacto, continua
      * buscando minimo
           IF INT-AUX1 NOT = INT-IMAX
      *SMC-INI 03.09.1993
               MOVE INT-CTIN(INT-AUX1) TO INT-TIPT
               IF INT-CVTI NOT > SPACES OR INT-TFPR = 'F'
                  GO TO CON-COL-CAL-INT
               ELSE
                  MOVE INT-DTIN(INT-AUX1) TO INT-DTIN(INT-IMAX)
                  MOVE INT-MTIN(INT-AUX1) TO INT-MTIN(INT-IMAX)
                  MOVE INT-STIN(INT-AUX1) TO INT-STIN(INT-IMAX)
                  MOVE INT-ATIN(INT-AUX1) TO INT-ATIN(INT-IMAX).
      *SMC-FIN 03.09.1993

           MOVE INT-DTIN(INT-IMAX) TO FEC-ITM1.
           MOVE INT-MTIN(INT-IMAX) TO FEC-ITM2.
           MOVE INT-STIN(INT-IMAX) TO FEC-ITM3.
           MOVE INT-ATIN(INT-IMAX) TO FEC-ITM4.
           IF INT-UTPR = 'D'
               MOVE INT-PREP    TO FEC-NDIA
               MOVE FEC-SUM-DIA TO FEC-CMND
           ELSE
           IF INT-UTPR = 'M'
               MOVE INT-PREP    TO FEC-NMES
               MOVE FEC-SUM-MES TO FEC-CMND
           ELSE
               MOVE INT-PREP    TO FEC-NANO
               MOVE FEC-SUM-ANO TO FEC-CMND.
           MOVE FEC-FORM-FEC TO FEC-FORM.
      *Para GENESIS version 3.1
           PERFORM CAL-FEC.
      *    PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'INTFCOLPREP+' TO INT-CMSG
               GO TO FIN-COL-CAL-INT.
           MOVE FEC-ITM1 TO INT-DTIN(INT-IMAX).
           MOVE FEC-ITM2 TO INT-MTIN(INT-IMAX).
           MOVE FEC-ITM3 TO INT-STIN(INT-IMAX).
           MOVE FEC-ITM4 TO INT-ATIN(INT-IMAX).
           GO TO CON-COL-CAL-INT.
       FIN-COL-CAL-INT.
           MOVE ZEROES TO INT-FBTV.
           MOVE 0      TO INT-SPR2.
      *DAD ini  31-MAR-1994   (Memo.04-02-94)
           MOVE SPACES TO INT-TIP2.
      *DAD fin  31-MAR-1994
       EXT-COL-CAL-INT.
           EXIT.

      *Transforma INT-SEPA segun Tipo de Periodo INT-IPER
      * 07-MAY-1990 14:55
       PRO-IPER SECTION.
       INI-PRO-IPER.
      *Busca el CIC de INT-IPER en MSC IPER de sistema TAB
           MOVE SPACES TO INT-CMSG INT-MENS.
           MOVE 'TAB'    TO MSC-COD-SIST FIO-SIST.
           MOVE 'IPER'   TO MSC-COD-TMSC IN MSC.
           MOVE INT-IPER TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF FIO-STAT-OKS
               MOVE MSC-CIC-CMSC IN MSC TO INT-IPER
           ELSE
               MOVE 'CICIPERNEX' TO INT-CMSG.
       FIN-PRO-IPER.
           EXIT.

      *Obtiene tasa de interes dado un periodo, un tipo de tasa y
      *un valor de tasa
       OBT-VTIN SECTION.
       INI-OBT-VTIN.
      * Obtiene fecha de hoy para el chequeo de tasa variable
           IF INT-CVTI > SPACES
               PERFORM GET-FHOY
               MOVE HOY-DHOY TO INT-DPRO
               MOVE HOY-MHOY TO INT-MPRO
               MOVE HOY-SHOY TO INT-SPRO
               MOVE HOY-AHOY TO INT-APRO
               MOVE HOY-HHHY TO INT-HHPR
               MOVE HOY-MMHY TO INT-MMPR
               MOVE HOY-SSHY TO INT-SSPR.
           PERFORM GNS-CAL-TPE.
       FIN-OBT-VTIN.
           EXIT.

       GNS-CAL-TPE SECTION.
       INI-GNS-CAL-TPE.
           PERFORM OBT-SEPA.
           IF INT-CMSG > SPACES
               GO TO FIN-GNS-CAL-TPE.
           PERFORM OBT-TINT.
           IF INT-CMSG > SPACES
               GO TO FIN-GNS-CAL-TPE.
           PERFORM OBT-TIPE.
           IF INT-CMSG > SPACES
               GO TO FIN-GNS-CAL-TPE.
       FIN-GNS-CAL-TPE.
           EXIT.

      * Descripcion     : Separacion Periodo Interes
      * Observaciones   :
      *      - Ampliar otros casos Periodo Base ( futuro ? )
       OBT-SEPA SECTION.
       INI-OBT-SEPA.
      * Inicializa variables para seteo de código y mensaje de error.
           MOVE SPACES TO INT-CMSG INT-MENS.

      * Esta sección transforma el período de intereses (INT-SEPD) que
      * está en días a su base de cálculo, es decir :
      * - Si INT-PBTI = 'D'         (Base de cálculo diario) 
      *   Mueve directamente INT-SEPD a INT-SDIA
      * - Si (INT-PBTI = 'T' o 'M') (Base de cálculo mensual)
      *   Mueve INT-SEPD / 30 a INT-S30D,
      *   el resto de esta división queda en INT-SDIA
      * - Si (INT-PBTI = 'X' o 'A') (Base de cálculo anual)
      *   Mueve INT-SEPD / 360 a INT-S360,
      *   el resto de esta división queda en INT-SDIA
      * - En otro caso, significa que la base de cálculo es desconocida
      *   y por tanto la rutina termina con INT-CMSG = 'INTPBTIINV'
           IF INT-PBTI = 'D'
      *JSS     MOVE 0 TO INT-S30D , INT-SMES , INT-S360 , INT-SANO
               MOVE 0 TO INT-S30D , INT-S360
               MOVE INT-SEPD TO INT-SDIA
           ELSE
           IF INT-PBTI = 'T' OR 'M'
      *JSS     MOVE 0 TO INT-SANO , INT-SMES , INT-S360
               MOVE 0 TO INT-S360
               DIVIDE 30 INTO INT-SEPD GIVING INT-S30D
                   REMAINDER INT-SDIA
           ELSE
           IF INT-PBTI = 'X' OR 'A'
      *JSS     MOVE 0 TO INT-S30D , INT-SMES , INT-SANO
               MOVE 0 TO INT-S30D
               DIVIDE 360 INTO INT-SEPD GIVING INT-S360
                   REMAINDER INT-SDIA
           ELSE
               MOVE 'INTPBTIINV' TO INT-CMSG.
       FIN-OBT-SEPA.
           EXIT.

      * Descripcion     : Preparacion Tasas Formula Unica
      * Observaciones   :
       OBT-TINT SECTION.
       INI-OBT-TINT.
      * Inicializa variables para seteo de código y mensaje de error.
           MOVE SPACES TO INT-CMSG INT-MENS.

      * Esta SECTION permite setear en la variable INT-MCTI la modalidad
      * de cálculo de la tasa de interés: S (simple) o C (compuesta).
      * Las bases de cálculo pueden ser diaria, mensuales (30 días) o
      * anuales (360 días) y la modalidad de cálculo de la tasa va a
      * depender de la cantidad de días del período a calcular y su base
      * de cálculo, es decir si se está calculando un período mayor a su
      * base de cálculo se habla de que la modalidad de cálculo es sobre
      * el período base y si el período de cálculo es menor o igual a la
      * base de cálculo entonces se habla que la modalidad de cálculo es
      * bajo el período base, por ejemplo:
      * - Si la base de cálculo es anual (INT-PBTI = 'X' o 'A') y si el
      *   período de cálculo de intereses, expresado en días es mayor que 
      *   360 días, entonces su modalidad de cálculo es sobre el período
      *   base, es decir mueve INT-SPBT a INT-MCTI.  
      * - Sino, es decir el período de cálculo de interés es menor o igual
      *   a 360 días, entonces la modalidad de cálculo es bajo el período
      *   base, mueve INT-BPBT a INT-MCTI.
           IF INT-PBTI = 'D'
               MOVE INT-SPBT TO INT-MCTI
           ELSE
           IF INT-PBTI = 'T' OR 'M'
               IF INT-SEPD > 30
                   MOVE INT-SPBT TO INT-MCTI
               ELSE
                   MOVE INT-BPBT TO INT-MCTI
           ELSE
           IF INT-PBTI = 'X' OR 'A'
               IF INT-SEPD > 360
                   MOVE INT-SPBT TO INT-MCTI
               ELSE
                   MOVE INT-BPBT TO INT-MCTI
           ELSE
               MOVE 'INTPBTIINV' TO INT-CMSG
               GO TO FIN-OBT-TINT.

           MOVE INT-TINT TO INT-TINC.

      *Si es tasa variable
      *      IF ( INT-CVTI > SPACES ) AND
      *         ( INT-IBTV NOT = INT-IBTV-NADA-MAS )
      * Si la ICG que cubre el período de cálculo es tasa variable, es
      * decir INT-CVTI > SPACES, busca en CAM/TAB la tasa base. 
      * La key de esta tabla se compone de los siguientes campos:
      *   INT-FINT (Fecha de base cálculo)     
      *   INT-CVTI (Código Valor Tasa Interés) X(03)
      *   INT-PBTI (Base de cálculo)           X(02)
      *   INT-VCAM (Moneda de la operación)    X(06)
      *   entonces la key de la tabla CAM/TAB es:
      *   INT-FINT + INT-FINT + 'I' + INT-CVTI + INT-PBTI + INT-VCAM
      * Obtenida la tasa base se la suma a la variable INT-TINC la cual
      * contiene el spread de la ICG
           IF ( INT-CVTI > SPACES )
                MOVE INT-FINT TO CAM-FEC-FCAM IN CAM(1)
                MOVE INT-FINT TO CAM-FEC-FCAM IN CAM(2)

                MOVE 'I'      TO CAM-MSC-TVAL IN CAM(1)
                MOVE INT-CVTI TO CAM-COD-CVAL IN CAM(1)
                MOVE INT-PBTI TO CAM-COD-TCAM IN CAM(1)

                MOVE INT-VCAM TO CAM-COD-VCAM IN CAM(2)

                MOVE FIO-GET-KEY TO FIO-CMND
                PERFORM TAB-FIO-CAM
                IF ( NOT FIO-STAT-OKS ) OR
                   ( CAM-MSC-STAT IN CAM NOT = 'S' )
                    PERFORM GNS-CHK-BTV
                    IF INT-CMSG > SPACES
                        GO TO FIN-OBT-TINT
                    ELSE
                        ADD CAM-SGV-VCAM IN CAM TO INT-TINC
                ELSE
                    ADD CAM-SGV-VCAM IN CAM TO INT-TINC.
      *     IF INT-CVTI > SPACES AND INT-IBTV = INT-IBTV-UNA-VEZ
      *         ADD CAM-SGV-VCAM IN CAM TO INT-TINT
      *         MOVE INT-IBTV-NADA-MAS TO INT-IBTV.

      * Si la tasa es anticipada (INT-BCTI = 'A') entonces :
      *  - INT-TINC = (INT-TINC*100)/(100 - INT-TINC)
      * Si es vencida queda igual.
           IF INT-BCTI = 'A'
               COMPUTE INT-TINC ROUNDED =
                   ( INT-TINC * 100 ) / ( 100 - INT-TINC ).

      * Luego se setea como es la tasa diaria (INT-TDIA), la tasa mensual
      * (INT-T30D) y la tasa anual (INT-T360) dependiendo de la base de
      * cálculo (D,M,A,T,X) y el modo de cálculo (S:simple,C:compuesta)
           IF INT-PBTI = 'D'
               COMPUTE INT-TDIA = INT-TINC / 100
               COMPUTE INT-T30D = INT-TINC * 30 / 100
               COMPUTE INT-T360 = INT-TINC * 360 / 100
           ELSE
           IF INT-PBTI = 'T' OR 'M'
               COMPUTE INT-T30D = INT-TINC / 100
               COMPUTE INT-T360 = INT-TINC * 12 / 100
               IF INT-MCTI = 'D'
                   COMPUTE INT-TDIA =
                       ( 1 + ( INT-TINC / 100 ) )**( 1 / 30 ) - 1
               ELSE
                   COMPUTE INT-TDIA = INT-TINC / 3000
           ELSE
           IF INT-PBTI = 'X' OR 'A'
               COMPUTE INT-T30D = INT-TINC / 1200
               COMPUTE INT-T360 = INT-TINC / 100
               IF INT-MCTI = 'D'
                   COMPUTE INT-TDIA =
                       ( 1 + ( INT-TINC / 100 ) )**( 1 / 360 ) - 1
               ELSE
                   COMPUTE INT-TDIA = INT-TINC / 36000
           ELSE
               MOVE 'INTPBTIINV' TO INT-CMSG.
       FIN-OBT-TINT.
           EXIT.

       GNS-CHK-BTV SECTION.
       INI-GNS-CHK-BTV.
           MOVE 'N' TO INT-IETB.
           IF NOT FIO-STAT-OKS
               MOVE 'CAMBTSANXABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2
           ELSE
               MOVE 'CAMBTSANVABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2.
           IF ( CAM-FEC-FCAM IN CAM(1) < INT-FPRO ) OR
              ( CAM-FEC-FCAM IN CAM(1) = INT-FPRO AND
                INT-HPRO > '120000' )
               GO TO FIN-GNS-CHK-BTV.
           IF INT-FBTV NOT > ZEROES
               GO TO FIN-GNS-CHK-BTV.
      *
           MOVE INT-DBTV TO CAM-NUM-DCAM IN CAM(1)
                            CAM-NUM-DCAM IN CAM(2).
           MOVE INT-MBTV TO CAM-NUM-MCAM IN CAM(1)
                            CAM-NUM-MCAM IN CAM(2).
           MOVE INT-SBTV TO CAM-NUM-SCAM IN CAM(1)
                            CAM-NUM-SCAM IN CAM(2).
           MOVE INT-ABTV TO CAM-NUM-ACAM IN CAM(1)
                            CAM-NUM-ACAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               MOVE 'CAMBTSANXABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2
           ELSE
           IF CAM-MSC-STAT IN CAM NOT = 'S'
               MOVE 'CAMBTSANVABT'      TO INT-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO INT-MEN2
           ELSE
               MOVE SPACES              TO INT-CMSG
               MOVE SPACES              TO INT-MENS.
       FIN-GNS-CHK-BTV.
           EXIT.

      * Descripcion     : Obtencion Tasa del Periodo
      * Observaciones   :
      *      - Modificar el calculo para Interes Compuesto considerando
      *        la Base de Calculo para Bajo y Sobre Periodo Expresion Ta
       OBT-TIPE SECTION.
      * Esta SECTION retorna el valor de la tasa para un período de días,
      * las fórmulas aquí usadas dependen si la tasa es simple o compuesta
      * y si la base de cálculo es diaria, mensual o anual.
       INI-OBT-TIPE.
           MOVE SPACES TO INT-CMSG INT-MENS.
           IF INT-MCTI = 'S'
               COMPUTE INT-TIPE ROUNDED =
                   INT-TDIA * INT-SDIA + INT-T30D * INT-S30D +
                   INT-T360 * INT-S360
           ELSE
           IF INT-MCTI = 'C'
               IF INT-PBTI = 'D'
                   COMPUTE INT-TIPE ROUNDED =
                       ( INT-TDIA + 1 ) ** INT-SDIA - 1
               ELSE
               IF INT-PBTI = 'T' OR 'M'
                   COMPUTE INT-TIPE ROUNDED =
                       ( INT-T30D + 1 ) ** INT-S30D *
                       ( INT-SDIA * INT-TDIA + 1 ) - 1
               ELSE
               IF INT-PBTI = 'X' OR 'A'
                   COMPUTE INT-TIPE ROUNDED =
                       ( INT-T360 + 1 ) ** INT-S360 *
                       ( INT-SDIA * INT-TDIA + 1 ) - 1
               ELSE
                   MOVE 'INTPBTIINV' TO INT-CMSG
                   GO TO FIN-OBT-TIPE
           ELSE
           IF INT-MCTI = 'D'
               COMPUTE INT-TIPE ROUNDED =
                   ( INT-TDIA + 1 ) ** INT-SEPD - 1
           ELSE
               MOVE 'INTMCTIINV' TO INT-CMSG
               GO TO FIN-OBT-TIPE.
       FIN-OBT-TIPE.
           EXIT.
      *
      * Rutina para obtener tasa de interes
       COL-BUS-TIN SECTION.
       INI-COL-BUS-TIN.
           MOVE SPACES TO INT-TIPT.
           MOVE 0 TO INT-SIND INT-IMAX.
           MOVE INT-IOPC IN INT-VARI TO ICG-CIC-IOPC IN ICG.
           MOVE INT-IDLC IN INT-VARI TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES       TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES       TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'        TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES       TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-COL-BUS-TIN.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-CIC-IOPC IN ICG = INT-IOPC IN INT-VARI AND
                    ICG-NUM-ICAN IN ICG = INT-IDLC IN INT-VARI AND
                    ICG-CIC-IOPV IN ICG = SPACES       AND
                    ICG-NUM-IVCT IN ICG = ZEROES       AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               IF INT-IDLC = 0
                   IF INT-TIPT NOT > SPACES
                       MOVE 'INTRICGNEX' TO INT-CMSG
                       MOVE INT-IOPC     TO INT-MEN2
                       GO TO FIN-COL-BUS-TIN
                   ELSE
                       GO TO FIN-COL-BUS-TIN
               ELSE
      * En caso de no encontrar ICG para un documento mayor que ZERO 
      * (INT-IDLC > 0), mueve ceros al INT-IDLC y vuelve a leerla, si
      * no la encuentra sale con INT-CMSG = 'INTRICGNEX' y termina.
                   IF INT-TIPT NOT > SPACES
                       MOVE 0 TO INT-IDLC
                       GO TO INI-COL-BUS-TIN
                   ELSE
                       GO TO FIN-COL-BUS-TIN.

      *Compara fechas de ICG con fechas 1 y 2
      * Con estas sentencias, se busca la máxima ICG tal que su fecha de
      * inicio sea menor o igual a la Fecha desde donde se quiera obtener
      * la tasa de interés, por ejemplo:
      *                        FUNO           FDOS
      *                         |              |
      *                         v              v
      *  FICG1   FICG2   FICG3   FICG4   FICG5   FICG6
      *    +-------+-------+-------+-------+-------+
      *    |       ||     ||      ||      ||       |
      *    ---->---||-->--||--->--||--->--||--->---| 
      *
      *    MAX(FICG1, FICG2, FICG3, FICG4) = FICG3
      *  Obtenida la ICG máxima se setean, en las variables INT-TIPT y
      *  INT-TIPX, el tipo de tasa de esa ICG. Además, se obtiene el
      *  spread o la tasa en caso:
      *   a) Mueve spread si es tasa variable
      *   b) Mueve la tasa si es tasa fija
           MOVE INT-DUNO TO INT-DAU1.
           MOVE INT-MUNO TO INT-MAU1.
           MOVE INT-SUNO TO INT-SAU1.
           MOVE INT-AUNO TO INT-AAU1.
           MOVE INT-DDOS TO INT-DAU2.
           MOVE INT-MDOS TO INT-MAU2.
           MOVE INT-SDOS TO INT-SAU2.
           MOVE INT-ADOS TO INT-AAU2.
           IF ICG-FEC-FINI IN ICG NOT > INT-FAU1
               MOVE ICG-COD-TINT IN ICG TO INT-TIPT INT-TIPX
               MOVE ICG-NUM-DINI IN ICG TO INT-DDMA
               MOVE ICG-NUM-MINI IN ICG TO INT-MDMA
               MOVE ICG-NUM-SINI IN ICG TO INT-SDMA
               MOVE ICG-NUM-AINI IN ICG TO INT-ADMA
               IF ICG-COD-CVTI IN ICG > SPACES
                   MOVE ICG-SGV-SPRD IN ICG TO INT-TINT INT-TINX
                   GO TO NXT-COL-BUS-TIN
               ELSE
                   MOVE ICG-SGV-TINT IN ICG TO INT-TINT INT-TINX
                   GO TO NXT-COL-BUS-TIN.

      * - Si la máxima ICG es menor o igual a FUNO, (en el ejemplo: FICG3)
      *   y su tipo de tasa está en blanco (ICG-COD-TINT menor o igual a
      *   SPACES) sale con INT-CMSG = 'INTRICGF1NEX'
           IF INT-TIPT NOT > SPACES
               MOVE 'INTRICGF1NEX' TO INT-CMSG
               MOVE INT-IOPC       TO INT-MEN2
               GO TO FIN-COL-BUS-TIN.

      * - Si la lectura de la siguiente ICG a la FICG3, (en el ejemplo : FICG4)
      *   y su fecha de inicio es mayor o igual a la FDOS (Fecha hasta la
      *   cual se quiere obtener la tasa) termina de leer ICG.
      *   Si no es así, es decir es menor que la FDOS, carga en un vector
      *   todas las ICG que sean menor a la FDOS. En el vector se guardan:
      *   la fecha de inicio, el tipo de tasa y la tasa o spread de cada
      *   ICG. (En el ejemplo se cargarían la FICG4 y la FICG5).
           IF ICG-FEC-FINI IN ICG NOT < INT-FAU2
               GO TO FIN-COL-BUS-TIN.
           ADD 1 TO INT-SIND.
      *
      * La rutina sólo puede guardar hasta 20 ICGs, si hay más termina
      * con el mensaje de error INT-CMSG = 'INTRICGOVFLW'.
           IF INT-SIND > INT-LMAX
               MOVE 'INTRICGOVFLW' TO INT-CMSG
               MOVE INT-IOPC     TO INT-MEN2
               GO TO FIN-COL-BUS-TIN.
           IF ICG-COD-CVTI IN ICG > SPACES
               MOVE ICG-SGV-SPRD IN ICG TO INT-VTIN(INT-SIND)
           ELSE
               MOVE ICG-SGV-TINT IN ICG TO INT-VTIN(INT-SIND).
           MOVE ICG-COD-TINT IN ICG TO INT-CTIN(INT-SIND).
           MOVE ICG-FEC-FINI IN ICG TO INT-FTIN(INT-SIND).
           MOVE SPACES              TO INT-ITIN(INT-SIND).
       NXT-COL-BUS-TIN.
           MOVE INT-SIND TO INT-IMAX.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-COL-BUS-TIN.
       FIN-COL-BUS-TIN.
      * La rutina termina esta SECTION agregando una ocurrencia al vector;
      * donde guarda el tipo de tasa y la tasa o spread de la máxima ICG
      * tal que su fecha de inicio sea menor o igual a FUNO, en el ejemplo
      * FICG3.
           IF INT-CMSG > SPACES
               GO TO EXT-COL-BUS-TIN.
           ADD 1 TO INT-IMAX.
           MOVE INT-TIPT TO INT-CTIN(INT-IMAX).
           MOVE INT-TINT TO INT-VTIN(INT-IMAX).
           MOVE 0        TO INT-FTIN(INT-IMAX).
           MOVE SPACES   TO INT-ITIN(INT-IMAX).
       EXT-COL-BUS-TIN.
           EXIT.

