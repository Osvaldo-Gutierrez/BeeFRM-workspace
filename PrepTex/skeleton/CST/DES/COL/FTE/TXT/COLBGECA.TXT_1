      **************************************************************************
      *DAD ini  14-APR-1992 / 28-NOV-1991 / 08-NOV-1991 / 23-AGO-1991
      *
      *    Proceso para Eliminar Entidades Anexas (RCV - ICG - CYA - ROV) y CAN
      *
      *    INPUT   :  Los registros  COLBRRCV, COLBRICG, COLBRCYA, COLBRROV y
      *                              COLBRCAN.
      *               Las  fios      COLBFRCV, COLBFICG, COLBFCYA, COLBFROV y
      *                              COLBFCAN.
      *               La  variable  en COLWGECA : ECA-ECAN IN ECA-VARI.
      *
      *    OUTPUT  :  Las variables en COLWGECA : ECA-CMSG IN ECA-VARI
      *                                           ECA-MEN2 IN ECA-VARI
      *                                           ECA-INDI IN ECA-VARI
      *                                           ECA-VOPC IN ECA-VARI.
      *
      *    PERFORM :  COL-ELI-CAN.
      *
      *DAD fin  14-APR-1992 / 28-NOV-1991 / 08-NOV-1991 / 23-AGO-1991
      **************************************************************************
      *DAD ini  14-APR-1992 / 28-NOV-1991 / 08-NOV-1991 / 23-AGO-1991
      * Proceso para Eliminar Entidades Anexas ( RCV - ICG - CYA - ROV ) y CAN
       COL-ELI-CAN SECTION.
       INI-COL-ELI-CAN.
           MOVE ZEROES TO ECA-INDI IN ECA-VARI
           MOVE SPACES TO ECA-CMSG IN ECA-VARI
                          ECA-VOPC IN ECA-VARI
                          ECA-MEN2 IN ECA-VARI.
      *-------------------------------------------------------------------------
      * Proceso para Eliminar Entidad Anexa RCV
       INI-DEL-ENT-RCV-CAN.
           MOVE CAN-CIC-IOPC IN CAN TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN TO RCV-NUM-ICAN IN RCV.
           MOVE SPACES              TO RCV-KEY-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS AND
                    RCV-KEY-ICAN IN RCV = CAN-KEY-ICAN IN CAN )
               MOVE 'CAN    NORCV'      TO ECA-CMSG IN ECA-VARI
               MOVE CAN-KEY-ICAN IN CAN TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS
               MOVE 'RCV    NOGKU'      TO ECA-CMSG IN ECA-VARI
               MOVE RCV-KEY-IRCV IN RCV TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS
               MOVE 'RCV    NODEL'      TO ECA-CMSG IN ECA-VARI
               MOVE RCV-KEY-IRCV IN RCV TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
      *-------------------------------------------------------------------------
      * Proceso para Eliminar Entidad Anexa ICG
       INI-DEL-ENT-ICG-CAN.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPC IN ICG.
           MOVE CAN-NUM-ICAN IN CAN TO ICG-NUM-ICAN IN ICG.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-CIC-IOPC IN ICG = CAN-CIC-IOPC IN CAN AND
                    ICG-NUM-ICAN IN ICG = CAN-NUM-ICAN IN CAN AND
                    ICG-CIC-IOPV IN ICG = CAN-CIC-IOPC IN CAN AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               GO TO INI-DEL-ENT-CYA-CAN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT FIO-STAT-OKS
               MOVE 'ICG    NOGKU'      TO ECA-CMSG IN ECA-VARI
               MOVE ICG-KEY-IICG IN ICG TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT FIO-STAT-OKS
               MOVE 'ICG    NODEL'      TO ECA-CMSG IN ECA-VARI
               MOVE ICG-KEY-IICG IN ICG TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
      *-------------------------------------------------------------------------
      * Proceso para Eliminar Entidad Anexa CYA
       INI-DEL-ENT-CYA-CAN.
           MOVE CAN-CIC-IOPC IN CAN TO CYA-CIC-IOPC IN CYA.
           MOVE CAN-NUM-ICAN IN CAN TO CYA-NUM-ICAN IN CYA.
           MOVE ZEROS               TO CYA-NUM-ICYA IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-DEL-ENT-CYA-CAN.
           PERFORM COL-FIO-CYA.
           IF NOT ( FIO-STAT-OKS AND
                    CYA-KEY-ICAN IN CYA = CAN-KEY-ICAN IN CAN )
               GO TO INI-DEL-ENT-ROV-CAN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS
               MOVE 'CYA    NOGKU'      TO ECA-CMSG IN ECA-VARI
               MOVE CYA-KEY-ICYA IN CYA TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS
               MOVE 'CYA    NODEL'      TO ECA-CMSG IN ECA-VARI
               MOVE CYA-KEY-ICYA IN CYA TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-DEL-ENT-CYA-CAN.
      *-------------------------------------------------------------------------
      * Proceso para Eliminar Entidad Anexa ROV
       INI-DEL-ENT-ROV-CAN.
           MOVE CAN-CIC-IOPC IN CAN TO ROV-CIC-IOPV IN ROV.
           MOVE CAN-NUM-ICAN IN CAN TO ROV-NUM-IVCT IN ROV.
           MOVE 'ROV-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
       LUP-DEL-ENT-ROV-CAN.
           PERFORM COL-FIO-ROV.
           IF NOT ( FIO-STAT-OKS AND
                    ROV-KEY-IVCT IN ROV = CAN-KEY-ICAN IN CAN )
               GO TO INI-DEL-ENT-CAN-CAN.
           ADD 1 TO ECA-INDI IN ECA-VARI.
           MOVE ROV-CIC-IOPC IN ROV TO ECA-IOPC IN ECA-VARI(ECA-INDI).
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT FIO-STAT-OKS
               MOVE 'ROV    NOGKU'      TO ECA-CMSG IN ECA-VARI
               MOVE ROV-KEY-IROV IN ROV TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT FIO-STAT-OKS
               MOVE 'ROV    NODEL'      TO ECA-CMSG IN ECA-VARI
               MOVE ROV-KEY-IROV IN ROV TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-DEL-ENT-ROV-CAN.
      *-------------------------------------------------------------------------
      * Proceso para Eliminar CAN (Se realiza cuando indicador = 0)
       INI-DEL-ENT-CAN-CAN.
           IF ECA-ECAN IN ECA-VARI = 1
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NOGKU'      TO ECA-CMSG IN ECA-VARI
               MOVE CAN-KEY-ICAN IN CAN TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NODEL'      TO ECA-CMSG IN ECA-VARI
               MOVE CAN-KEY-ICAN IN CAN TO ECA-MEN2 IN ECA-VARI
               GO TO FIN-COL-ELI-CAN.
      *-------------------------------------------------------------------------
       FIN-COL-ELI-CAN.
           EXIT.
      *DAD fin  14-APR-1992 / 28-NOV-1991 / 08-NOV-1991 / 23-AGO-1991
      **************************************************************************
