*%! Codigo adicional para archivo
*% IF WSS
      *<<<< WSS
       COPY TABBRUSR.
       COPY TABBROFI.
       COPY COLBROPC.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       COPY GNSWVPPH.
       01  WSS-VARI.
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-DTER                     PIC 9(02).
              05 WSS-NUM-MTER                     PIC 9(02).
              05 WSS-NUM-STER                     PIC 9(02).
              05 WSS-NUM-ATER                     PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                     PIC 9(02).
              05 WSS-NUM-AFEC                     PIC 9(02).
              05 WSS-NUM-MFEC                     PIC 9(02).
              05 WSS-NUM-DFEC                     PIC 9(02).
           03 WSS-FEC-FFET.
              05 WSS-NUM-SFET                     PIC 9(02).
              05 WSS-NUM-AFET                     PIC 9(02).
              05 WSS-NUM-MFET                     PIC 9(02).
              05 WSS-NUM-DFET                     PIC 9(02).
           03 WSS-FEC-FREG.
              05 WSS-NUM-SREG                     PIC 9(02).
              05 WSS-NUM-AREG                     PIC 9(02).
              05 WSS-NUM-MREG                     PIC 9(02).
              05 WSS-NUM-DREG                     PIC 9(02).
           03 WSS-COD-OFOC                        PIC X(03).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-COD-COOC.
              05 WSS-COD-TOOC                     PIC X(03).
              05 WSS-COD-AOOC                     PIC X(03).
           03 WSS-GLS-TIAU.
              05 WSS-GLS-TIO                      PIC X(05).
              05 WSS-GLS-AUX                      PIC X(05).
           03 WSS-UBFD.
              05 WSS-TAB-UBFD                     PIC X(03).
              05 WSS-COD-UBFD.
                 07 WSS-GLS-UFD3                  PIC X(03).
                 07 WSS-GLS-UFD9                  PIC X(09).
      *>>>>
*% END
*% IF WSS_DTC
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *>>>>
*% END
*% IF CH_OUF_OPC_COD_OFOC
      *<<<< CH_OUF_OPC_COD_OFOC
           MOVE ZEROES TO PAGE-COUNTER.
      *>>>>
*% END
*% IF CH_OUF_CUF_FEC_FCUF
      *<<<< CH_OUF_CUF_FEC_FCUF
      *>>>>
*% END
*% IF PH_OUF
      *<<<< PH_OUF
      * Omite impresion de la primera pagina al controlar cortes con CH
       COPY GNSBGPPH.

      * Mediante BUS-OFI se Obtiene Nombre de la Oficina de Tabla OFI
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.

      * Fecha Cambio Ubicacion Fisica
           MOVE CUF-FEC-FCUF IN SRT      TO WSS-FEC-FREG IN WSS-VARI.
           MOVE WSS-NUM-DREG IN WSS-VARI TO WSS-NUM-DCUF IN WSS-OUF.
           MOVE WSS-NUM-MREG IN WSS-VARI TO WSS-NUM-MCUF IN WSS-OUF.
           MOVE WSS-NUM-SREG IN WSS-VARI TO WSS-NUM-SCUF IN WSS-OUF.
           MOVE WSS-NUM-AREG IN WSS-VARI TO WSS-NUM-ACUF IN WSS-OUF.
      *>>>>
*% END
*% IF INI_INP
      *<<<< INI_INP
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMAAAA ) : '
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFOC IN WSS-VARI.
           DISPLAY WSS-COD-OFOC IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB'         TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-END.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-OUF
                                            WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-OUF
                                            WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-OUF
                                            WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-OUF
                                            WSS-NUM-AFEC IN WSS-VARI.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DTER IN WSS-OUF
                                            WSS-NUM-DFET IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MTER IN WSS-OUF
                                            WSS-NUM-MFET IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-STER IN WSS-OUF
                                            WSS-NUM-SFET IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-ATER IN WSS-OUF
                                            WSS-NUM-AFET IN WSS-VARI.

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFOC IN WSS-VARI
               MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
      *>>>>
*% END
*% IF INI_FST_INP
      *<<<< INI_FST_INP
      * Inicio de Ciclo Proceso para Registro CUF
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CUF-FEC-FCUF IN CUF.
           MOVE 'CUF-FEC-FCUF' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CUF.
           IF NOT FIO-STAT-OKS OR
              CUF-FEC-FCUF IN CUF > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS EN RANGO DE FECHAS DESDE : '
                       WSS-FEC-FINI IN WSS-VARI ' A '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-INP-SRT.
           GO TO LUP-INP-SRT.
      *>>>>
*% END
*% IF LUP_INP
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro CUF
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.

      * Completa formacion y generacion del registro SRT
           PERFORM BUS-SRT-REG.

           GO TO NXT-INP-SRT.
      *>>>>
*% END
*% IF INI_NXT_INP
      *<<<< INI_NXT_INP
           MOVE 'CUF-FEC-FCUF' TO FIO-AKEY.
      *>>>>
*% END
*% IF FIN_NXT_INP
      *<<<< FIN_NXT_INP
           IF CUF-FEC-FCUF IN CUF > WSS-FEC-FFET IN WSS-VARI
               GO TO FIN-INP-SRT.
      *>>>>
*% END
*% IF FST_OUT
      *<<<< FST_OUT
      * Busca glosas de campos en Lineas de Titulos pues con CH no estan en
      * la primera pagina
      * Mediante BUS-OFI se Obtiene Nombre de la Oficina de Tabla OFI
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.

      * Fecha Cambio Ubicacion Fisica
           MOVE CUF-FEC-FCUF IN SRT      TO WSS-FEC-FREG IN WSS-VARI.
           MOVE WSS-NUM-DREG IN WSS-VARI TO WSS-NUM-DCUF IN WSS-OUF.
           MOVE WSS-NUM-MREG IN WSS-VARI TO WSS-NUM-MCUF IN WSS-OUF.
           MOVE WSS-NUM-SREG IN WSS-VARI TO WSS-NUM-SCUF IN WSS-OUF.
           MOVE WSS-NUM-AREG IN WSS-VARI TO WSS-NUM-ACUF IN WSS-OUF.
      *>>>>
*% END
*% IF LUP_OUT
      *<<<< LUP_OUT
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
           PERFORM GEN-LIN-OUT.
      *>>>>
*% END
*% IF EOF
      *<<<< EOF
      **************************************************************************
      * Seleccion del registro para Proceso mediante Oficina, Ejecutivo
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

      * Busca la Operacion de Credito del Cambio Ubicacion Fisica
           MOVE CUF-CIC-IOPC IN CUF TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CUF    OPCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CUF-CIC-IOPC IN CUF
               GO TO FIN-SEL-REG-PRO.

      * Proceso de Seleccion a partir del registro OPC
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFOC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   GO TO FIN-SEL-REG-PRO.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      **************************************************************************
      * Completa formacion y generacion del registro SRT
       BUS-SRT-REG SECTION.
       INI-BUS-SRT-REG.

      * Completa Llenado de Campos y Genera el Registro SRT
           COPY COLRMOUF.
           RELEASE SRT.
       FIN-BUS-SRT-REG.
           EXIT.
      **************************************************************************
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
       GEN-LIN-OUT SECTION.
       INI-GEN-LIN-OUT.
      * Se redefine campo COOC e inicializa glosa de trabajo
           MOVE OPC-COD-COOC IN SRT TO WSS-COD-COOC IN WSS-VARI.
           MOVE SPACES              TO WSS-GLS-TIAU IN WSS-VARI.

      * Mediante BUS-TAB busca la glosa del Tipo de Operacion Credito en la
      * tabla TIO
           IF WSS-COD-TOOC IN WSS-VARI > SPACES
               MOVE 'TAB'                    TO TAB-COD-SIST
               MOVE 'TIO'                    TO TAB-COD-TTAB IN TAB
               MOVE WSS-COD-TOOC IN WSS-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-TIO IN WSS-VARI.

      * Mediante BUS-TAB busca la glosa de la Clase de Operacion Credito en la
      * tabla AUX
           IF OPC-COD-COOC IN SRT > SPACES
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-COOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-AUX IN WSS-VARI.

      * Se concatenan las glosas de tablas TIO y AUX para desplegarlas juntas
           MOVE WSS-GLS-TIAU IN WSS-VARI TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO WSS-GLS-TIAU IN WSS-OUF.

      * Mediante BUS-TAB Busca Tipo de Moneda de Tabla VLR
           IF OPC-COD-VCOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-VCOC IN WSS-OUF
           ELSE
               MOVE SPACES TO WSS-GLS-VCOC IN WSS-OUF.

      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro RDC
           PERFORM BUS-REG-CLI.

      * Busca glosa de codigo Ubicacion Fisica Anterior
           PERFORM BUS-GLS-UFAD.

      * Busca glosa de codigo Ubicacion Fisica Actual
           PERFORM BUS-GLS-UBFD.
       FIN-GEN-LIN-OUT.
           EXIT.
      **************************************************************************
      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro RDC
       BUS-REG-CLI SECTION.
       INI-BUS-REG-CLI.
      * Busca CIC del Cliente con el CIC de Operaciones de Creditos en RDC
      * RDC-NUM-IDLC > 0 ====> DEU.IND.
      * RDC-NUM-IDLC = 0 ====> DEU.DIR.
           MOVE CUF-CAI-IOPC IN SRT TO RDC-CAI-IOPC IN RDC.
           MOVE CUF-IIC-IOPC IN SRT TO RDC-IIC-IOPC IN RDC.
      *    MOVE                     TO RDC-NUM-IDLC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC'      TO FIO-AKEY.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    RDCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RDC-KEY-IDLC IN RDC.
      *        GO TO FIN-BUS-REG-CLI.

      *CIC-CLI Busca Datos del Cliente Mediante su CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS > SPACES
               MOVE SPACES TO DBC-NUM-ICLI IN DBC
                              DBC-VRF-ICLI IN DBC
                              DBC-IND-ICLI IN DBC
                              WSS-GLS-AUXI IN WSS-OUF
                              DBC-GLS-ICLI IN DBC
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC
               IF DBC-GLS-ICLI IN DBC > SPACES
                   MOVE '-'    TO WSS-GLS-AUXI IN WSS-OUF
               ELSE
                   MOVE SPACES TO WSS-GLS-AUXI IN WSS-OUF.
       FIN-BUS-REG-CLI.
           EXIT.
      **************************************************************************
      * Busca glosa de codigo Ubicacion Fisica Anterior
       BUS-GLS-UFAD SECTION.
       INI-BUS-GLS-UFAD.
           MOVE CUF-COD-UFAD IN SRT TO WSS-UBFD IN WSS-VARI.
           IF WSS-TAB-UBFD IN WSS-VARI NOT > SPACES
               MOVE SPACES TO WSS-COD-IUFA IN WSS-OUF
                              WSS-GLS-UFAD IN WSS-OUF
           ELSE
           IF WSS-TAB-UBFD IN WSS-VARI = 'OFI'
               MOVE WSS-COD-UBFD IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               IF MSG-COD-MENS = 'COD    NEX'
                   MOVE SPACES TO WSS-COD-IUFA IN WSS-OUF
                                  WSS-GLS-UFAD IN WSS-OUF
               ELSE
                   MOVE WSS-TAB-UBFD IN WSS-VARI TO WSS-COD-IUFA
                                                              IN WSS-OUF
                   MOVE OFI-GLS-DESC IN OFI      TO WSS-GLS-UFAD
                                                              IN WSS-OUF
           ELSE
               MOVE 'TAB'                    TO TAB-COD-SIST
               MOVE WSS-TAB-UBFD IN WSS-VARI TO TAB-COD-TTAB IN TAB
               MOVE WSS-COD-UBFD IN WSS-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE WSS-TAB-UBFD IN WSS-VARI TO WSS-COD-IUFA IN WSS-OUF
               MOVE TAB-GLS-DCOR IN TAB      TO WSS-GLS-UFAD IN WSS-OUF.
       FIN-BUS-GLS-UFAD.
           EXIT.
      **************************************************************************
      * Busca glosa de codigo Ubicacion Fisica Actual
       BUS-GLS-UBFD SECTION.
       INI-BUS-GLS-UBFD.
           MOVE CUF-COD-UBFD IN SRT TO WSS-UBFD IN WSS-VARI.
           IF WSS-TAB-UBFD IN WSS-VARI NOT > SPACES
               MOVE SPACES TO WSS-COD-IUBF IN WSS-OUF
                              WSS-GLS-UBFD IN WSS-OUF
           ELSE
           IF WSS-TAB-UBFD IN WSS-VARI = 'OFI'
               MOVE WSS-COD-UBFD IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               IF MSG-COD-MENS = 'COD    NEX'
                   MOVE SPACES TO WSS-COD-IUBF IN WSS-OUF
                                  WSS-GLS-UBFD IN WSS-OUF
               ELSE
                   MOVE WSS-TAB-UBFD IN WSS-VARI TO WSS-COD-IUBF
                                                              IN WSS-OUF
                   MOVE OFI-GLS-DESC IN OFI      TO WSS-GLS-UBFD
                                                              IN WSS-OUF
           ELSE
               MOVE 'TAB'                    TO TAB-COD-SIST
               MOVE WSS-TAB-UBFD IN WSS-VARI TO TAB-COD-TTAB IN TAB
               MOVE WSS-COD-UBFD IN WSS-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE WSS-TAB-UBFD IN WSS-VARI TO WSS-COD-IUBF IN WSS-OUF
               MOVE TAB-GLS-DCOR IN TAB      TO WSS-GLS-UBFD IN WSS-OUF.
       FIN-BUS-GLS-UBFD.
           EXIT.
      **************************************************************************

       COPY TABBFUSR.
       COPY TABBFOFI.
       COPY COLBFOPC.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY TABBBUSR.
       COPY TABBBOFI.
       COPY TABBVUSR.
       COPY TABBVOFI.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY SGCBGCLI.
      *>>>>
*% END
