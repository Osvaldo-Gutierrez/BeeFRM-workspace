      **************************************************************************
       PRO-RCV SECTION.
       INI-PRO-RCV.
           MOVE ZEROES TO WSS-VAL-SMO1.
           MOVE ZEROES TO WSS-VAL-SMO2.
      *LEER RCV.
           MOVE CAN-CAI-IOPC IN CAN TO RCV-CAI-IOPC IN RCV.
           MOVE CAN-IIC-IOPC IN CAN TO RCV-IIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN TO RCV-NUM-ICAN IN RCV.
           MOVE CAN-CIC-IOPC IN CAN TO RCV-CIC-IOPV IN RCV.
      *OJO
           MOVE VEN-NUM-IVEN IN VEN TO RCV-NUM-IVCT IN RCV.
      *
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF RCV-NUM-IVCT IN RCV > ZEROES
               MOVE RCV-KEY-IVCT IN RCV TO VEN-KEY-IVEN IN VEN
               PERFORM PRO-VEN
               MOVE VEN-NUM-IDLC IN VEN TO WSS-NUM-IDLC
      *         MOVE RCV-NUM-IDLC IN RCV TO WSS-NUM-IDLC
               ADD  RCV-SGV-CAPI IN RCV TO WSS-SGV-CAPD(WSS-NUM-IDLC)
               ADD  RCV-SGV-FINA IN RCV TO WSS-SGV-FINA(WSS-NUM-IDLC)
      *DAD ini  28-ABR-1992   (Memo.13-03-92 pto.5) / 22-OCT-1991
               IF SCR-CMND = 'REV' AND
                  ( CAN-MSC-FCAN IN CAN NOT = 'I' AND
                    CAN-MSC-FCAN IN CAN NOT = 'P' )
      *DAD fin  28-ABR-1992
                   SUBTRACT 1 FROM WSS-NUM-VCVG IN WSS-VARI
                   IF RCV-IND-SCTB = 'VCD'
                       SUBTRACT 1 FROM WSS-NUM-VCCV IN WSS-VARI
                   ELSE
                       IF RCV-MSC-MCAN IN RCV = 'V'
                           SUBTRACT 1 FROM WSS-NUM-VCCM IN WSS-VARI
                       ELSE
                           SUBTRACT 1 FROM WSS-NUM-VCCO IN WSS-VARI
               ELSE
      *DAD fin  22-OCT-1991
               IF VEN-VAL-SFIN IN VEN NOT > ZEROES
                   ADD 1 TO WSS-NUM-VCVG IN WSS-VARI
      *             ADD 1 TO CAN-NUM-TVCC IN CAN
                   IF RCV-IND-SCTB = 'VCD'
                       ADD 1 TO WSS-NUM-VCCV IN WSS-VARI
                   ELSE
                       IF RCV-MSC-MCAN IN RCV = 'V'
                           ADD 1 TO WSS-NUM-VCCM IN WSS-VARI
                       ELSE
                           ADD 1 TO WSS-NUM-VCCO IN WSS-VARI
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE ZEROES TO WSS-NUM-IDLC.
           ADD  RCV-SGV-CAPI IN RCV TO WSS-SGV-CAPD(500).
           ADD  RCV-SGV-FINA IN RCV TO WSS-SGV-FINA(500).
           PERFORM PRO-OPE.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-RCV.

           IF WSS-NUM-IDLC > ZEROES
               MOVE CAN-CIC-IOPC IN CAN TO DLC-CIC-IOPC IN DLC
               PERFORM PRO-DOC VARYING WSS-NUM-IDLC FROM 1 BY 1
                               UNTIL   WSS-NUM-IDLC > 499.
      *
      *Despues de procesar Vencimiento cancelado, Operacion y Documentos
      *genera siguiente vencimiento para creditos con EVC ( si corresponde ),
      *es decir cuando cancelo totalmente el vencimiento, aun queda saldo
      *del credito y el comando no es reversa
               IF ( VEN-VAL-SFIN IN VEN NOT > ZEROES ) AND
                  ( OPC-VAL-SCRE IN OPC > ZEROES )     AND
                  ( SCR-CMND NOT = 'REV' )
                   IF OPC-IND-VSTR IN OPC = 'S'
                       PERFORM GEN-SIG-VEN-EVC
                   ELSE
                       PERFORM GEN-SIG-VEN-VEN
               ELSE
      *Si se reversa una morosa se debe generar el vencimiento siguiente
      *con VEN-NUM-IVEN = 2
               IF ( SCR-CMND = 'REV' AND VEN-IND-SCTB IN VEN = 'MOR' 
      *DAD ini  23-DEC-1991   (Memo.05-09-91 Pto.1)
                    AND OPC-IND-VSTR IN OPC = 'S' )
      *DAD fin  23-DEC-1991
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
                   MOVE SPACES TO MSG-COD-MENS
                   PERFORM ACT-VEN-SHFT-REV
                   IF MSG-COD-MENS NOT > SPACES
      *DAD fin  20-OCT-1992
                   MOVE  OPC-CIC-IOPC IN OPC TO     VEN-CIC-IOPC IN VEN
                   ADD 1 VEN-NUM-IVCT IN VEN GIVING VEN-NUM-IVEN IN VEN
                   MOVE OPC-NUM-DCOL IN OPC TO VEN-DTBS
                   MOVE OPC-NUM-MCOL IN OPC TO VEN-MTBS
                   MOVE OPC-NUM-SCOL IN OPC TO VEN-STBS
                   MOVE OPC-NUM-ACOL IN OPC TO VEN-ATBS
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM COL-OBT-VEN
                   IF NOT VEN-STAT-OKS
                       MOVE SPACES TO MSG-COD-MENS
                       GO TO FIN-PRO-RCV
                   ELSE
                       MOVE 2 TO VEN-NUM-IVEN IN VEN
                       MOVE VEN TO VEN-BKP
                       MOVE FIO-GET-KEY-UPD TO FIO-CMND
                       PERFORM COL-FIO-VEN
                       IF NOT FIO-STAT-OKS
                           MOVE VEN-BKP TO VEN
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
                           PERFORM PUT-VEN-IND-SCTB
      *DAD fin  20-OCT-1992
                           MOVE FIO-PUT TO FIO-CMND
                           PERFORM COL-FIO-VEN
                           MOVE SPACES TO MSG-COD-MENS
                       ELSE
                           MOVE VEN-BKP TO VEN
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
                           PERFORM PUT-VEN-IND-SCTB
      *DAD fin  20-OCT-1992
                           MOVE FIO-MOD TO FIO-CMND
                           PERFORM COL-FIO-VEN
                           MOVE SPACES TO MSG-COD-MENS.
       FIN-PRO-RCV.
           EXIT.
      *-------------------------------------------------------------------------
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
       ACT-VEN-SHFT-REV SECTION.
       INI-ACT-VEN-SHFT-REV.
      * Lee fisicamente el Ultimo VEN
           MOVE 999 TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN AND
                    VEN-NUM-IVEN IN VEN NOT = 1 )
               GO TO FIN-ACT-VEN-SHFT-REV.
       LUP-ACT-VEN-SHFT-REV.
      * Elimina el VEN tomado de UPD previamente Respaldado
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENSHFTNOUPD' TO MSG-COD-MENS
               GO TO FIN-ACT-VEN-SHFT-REV.
           MOVE VEN-NUM-IVEN IN VEN TO WSS-AUX-IVEN.
           MOVE VEN TO VEN-BKP.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENSHFTNODEL' TO MSG-COD-MENS
               GO TO FIN-ACT-VEN-SHFT-REV.
      * Graba el VEN Respaldado luego de Actualizar
           MOVE VEN-BKP TO VEN.
           ADD 1 TO WSS-AUX-IVEN
           MOVE WSS-AUX-IVEN TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENSHFTNOPUT' TO MSG-COD-MENS
               GO TO FIN-ACT-VEN-SHFT-REV.
           MOVE VEN-BKP TO VEN.
      * Lee fisicamente el Anterior VEN
           MOVE FIO-GET-PRV TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN AND
                    VEN-NUM-IVEN IN VEN NOT = 1 )
               GO TO FIN-ACT-VEN-SHFT-REV.
           GO TO LUP-ACT-VEN-SHFT-REV.
       FIN-ACT-VEN-SHFT-REV.
           EXIT.
      *DAD fin  20-OCT-1992
      *-------------------------------------------------------------------------
      **************************************************************************
       PRO-VEN SECTION.
       INI-PRO-VEN.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               PERFORM PRO-VEN-NSTR
           ELSE
               PERFORM PRO-VEN-SSTR.
       FIN-PRO-VEN.
           EXIT.
      *-------------------------------------------------------------------------
       PRO-VEN-NSTR SECTION.
       INI-PRO-VEN-NSTR.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    NEX' TO MSG-COD-MENS
               GO TO FIN-PRO-VEN-NSTR.
           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FAMT.
      *
           IF SCR-CMND = 'REV'
               MOVE RCV-SGV-CAPI IN RCV TO WSS-VAL-SMO1
               MOVE RCV-SGV-FINA IN RCV TO WSS-VAL-SMO2
           ELSE
      *DAD ini  29-ABR-1993   (Memo.02-10-92)
      *         MOVE VEN-VAL-SCAP IN VEN TO WSS-VAL-SMO1
      *         MOVE VEN-VAL-SFIN IN VEN TO WSS-VAL-SMO2.
               MOVE RCV-SGV-CAPI IN RCV TO WSS-VAL-SMO1
               MOVE RCV-SGV-FINA IN RCV TO WSS-VAL-SMO2.
      *DAD fin  29-ABR-1993
           PERFORM ACT-SFIN-SCAP-VEN.
       FIN-PRO-VEN-NSTR.
           EXIT.
      *-------------------------------------------------------------------------
       PRO-VEN-SSTR SECTION.
       INI-PRO-VEN-SSTR.
      *Si el comando es Reversa pide a traves de GET-VEN-REA el vencimiento
      *correspondiente a la cancelacion a reversar
           IF SCR-CMND = 'REV'
               PERFORM GET-VEN-REA
               IF NOT FIO-STAT-OKS
                   MOVE VEN-STAT TO MSG-COD-MENS
                   MOVE VEN-MEN2 TO FRM-MENS
                   GO TO FIN-PRO-VEN-SSTR
               ELSE
      *            Respalda el vencimiento que se reversa
                   MOVE VEN TO VEN-BKP
      *            Actualiza Estructuras y Documentos
                   PERFORM ACT-REVC-RDLC.
      *
           MOVE 1 TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    NEX' TO MSG-COD-MENS
               GO TO FIN-PRO-VEN-SSTR.

      *DAD ini  20-OCT-1992   (Memo.08-10-92)
           MOVE SPACES TO WSS-IND-SCTB
                          WSS-IND-SCBZ.
           MOVE VEN-IND-SCTB IN VEN TO WSS-IND-SCTB.
           MOVE VEN-IND-SCBZ IN VEN TO WSS-IND-SCBZ.
      *DAD fin  20-OCT-1992
      *
           IF SCR-CMND = 'REV'
               MOVE VEN-BKP TO VEN
               MOVE 1 TO VEN-NUM-IVEN IN VEN.
           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FAMT.
      *
           IF SCR-CMND = 'REV'
               MOVE RCV-SGV-CAPI IN RCV TO WSS-VAL-SMO1
               MOVE RCV-SGV-FINA IN RCV TO WSS-VAL-SMO2
           ELSE
      *DAD ini  29-ABR-1993   (Memo.02-10-92)
      *         MOVE VEN-VAL-SCAP IN VEN TO WSS-VAL-SMO1
      *         MOVE VEN-VAL-SFIN IN VEN TO WSS-VAL-SMO2.
               MOVE RCV-SGV-CAPI IN RCV TO WSS-VAL-SMO1
               MOVE RCV-SGV-FINA IN RCV TO WSS-VAL-SMO2.
      *DAD fin  29-ABR-1993
           PERFORM ACT-SFIN-SCAP-VEN.
       FIN-PRO-VEN-SSTR.
           EXIT.
      *-------------------------------------------------------------------------
       ACT-SFIN-SCAP-VEN SECTION.
       INI-ACT-SFIN-SCAP-VEN.
      *Rebaja SCAP y SFIN del vencimiento que esta cancelando
      *
           IF RCV-SGV-CAPI IN RCV > VEN-VAL-SCAP IN VEN
               MOVE ZEROES TO VEN-VAL-SCAP IN VEN
           ELSE
               SUBTRACT RCV-SGV-CAPI IN RCV FROM VEN-VAL-SCAP IN VEN.
      *
           IF RCV-SGV-FINA IN RCV > VEN-VAL-SFIN IN VEN
               MOVE ZEROES TO VEN-VAL-SFIN IN VEN
           ELSE
               SUBTRACT RCV-SGV-FINA IN RCV FROM VEN-VAL-SFIN IN VEN.
      *
      *No se pueden pagar parcialidades de VEN que fuerom estimados,
      *por lo tanto :
           IF SCR-CMND NOT = 'REV'
               IF RCV-MSC-STAT IN RCV = 'C' OR 'F'
                   MOVE ZEROES TO VEN-VAL-SCAP IN VEN
                   MOVE ZEROES TO VEN-VAL-SFIN IN VEN
               ELSE
                   NEXT SENTENCE
           ELSE
               IF RCV-MSC-STAT IN RCV = 'C'
                   MOVE ZEROES TO VEN-VAL-SCAP IN VEN
               ELSE
               IF RCV-MSC-STAT IN RCV = 'F'
                   MOVE ZEROES TO VEN-VAL-SFIN IN VEN.
      *
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    BCK' TO MSG-COD-MENS.
       FIN-ACT-SFIN-SCAP-VEN.
           EXIT.
      **************************************************************************
       GEN-SIG-VEN-EVC SECTION.
       INI-GEN-SIG-VEN-EVC.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE VEN-VAL-SCAP IN VEN TO WSS-VAL-VENC.
           MOVE VEN-NUM-IEVC IN VEN TO WSS-NUM-IEVC.
      *Lee fisicamente el VEN=1 ( el que se cancelo )
           MOVE 1 TO VEN-NUM-IVEN IN VEN.
      *LRH ini 9-ABR-1994
      *     MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM COL-FIO-VEN.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN AND
                    1                   = VEN-NUM-IVEN IN VEN )
                   MOVE 'VEN    NEX' TO MSG-COD-MENS
                   GO TO FIN-GEN-SIG-VEN-EVC.
      *LRH fin 9-ABR-1994
           ADD 1 VEN-NUM-IVCT IN VEN GIVING WSS-AUX-IVEN.
      *DAD ini  31-MAY-1993
      **DAD ini  20-OCT-1992   (Memo.08-10-92)
      *     MOVE SPACES TO WSS-IND-SCTB
      *                    WSS-IND-SCBZ.
      **DAD fin  20-OCT-1992
      *DAD fin  31-MAY-1993
      *Busca si existe un VEN siguiente para eliminarlo ( ya que el
      *que se cancela puede estar moroso y en ese caso existe uno o mas
      *vencimientos fisicos adicionales )
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF FIO-STAT-OKS AND
              OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN
               MOVE FIO-GET-KEY-UPD TO FIO-CMND
               PERFORM COL-FIO-VEN
      *DAD ini  31-MAY-1993
               IF FIO-STAT-OKS
      *Respalda el VEN leido antes de eliminarlo
                   MOVE VEN TO VEN-BKP
                   MOVE FIO-DEL TO FIO-CMND
                   PERFORM COL-FIO-VEN
                   IF FIO-STAT-OKS
                       MOVE 'S' TO WSS-IND-EXIS
                   ELSE
                       MOVE 'VEN    NODEL' TO MSG-COD-MENS
                       GO TO FIN-GEN-SIG-VEN-EVC
               ELSE
                   MOVE 'VEN    NOGKU' TO MSG-COD-MENS
                   GO TO FIN-GEN-SIG-VEN-EVC
           ELSE
               MOVE 'N' TO WSS-IND-EXIS.
      **DAD ini  20-OCT-1992   (Memo.08-10-92)
      *         IF VEN-NUM-IVCT IN VEN = WSS-AUX-IVEN
      *             MOVE VEN-IND-SCTB IN VEN TO WSS-IND-SCTB
      *             MOVE VEN-IND-SCBZ IN VEN TO WSS-IND-SCBZ
      *             MOVE FIO-DEL TO FIO-CMND
      *             PERFORM COL-FIO-VEN
      *         ELSE
      **DAD fin  20-OCT-1992
      *             MOVE FIO-DEL TO FIO-CMND
      *             PERFORM COL-FIO-VEN.
      *DAD fin  31-MAY-1993
      *
      *Pide a la rutina de expansion generar el siguiente vencimiento
      *al cancelado ( Asume la FCOL para poder generarlo con mayor
      *probabilidad )
           MOVE OPC-CIC-IOPC IN OPC      TO     VEN-CIC-IOPC IN VEN.
      *JSS ADD 1 VEN-NUM-IVCT IN CAN-FLD GIVING VEN-NUM-IVEN IN VEN.
           MOVE WSS-AUX-IVEN             TO     VEN-NUM-IVEN IN VEN.
           MOVE OPC-NUM-DCOL IN OPC TO VEN-DTBS.
           MOVE OPC-NUM-MCOL IN OPC TO VEN-MTBS.
           MOVE OPC-NUM-SCOL IN OPC TO VEN-STBS.
           MOVE OPC-NUM-ACOL IN OPC TO VEN-ATBS.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF NOT VEN-STAT-OKS
               MOVE VEN-STAT TO MSG-COD-MENS
               MOVE VEN-MEN2 TO FRM-MEN2
               GO TO FIN-GEN-SIG-VEN-EVC.
      *
      *DAD ini  31-MAY-1993
      *Respalda el nuevo VEN generado cuando no existia este VEN
           IF WSS-IND-EXIS = 'N'
               MOVE VEN TO VEN-BKP.
           MOVE SPACES              TO MSG-COD-MENS.
           MOVE VEN-VAL-CAPI IN VEN TO WSS-VAL-CAPI-GENE.
      *Lee el 1er VEN fisico ( el cancelado )
           MOVE  1  TO VEN-NUM-IVEN IN VEN
                       VEN-NUM-IVEN IN VEN-BKP.
      *     MOVE VEN TO VEN-BKP.
      *DAD fin  31-MAY-1993
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENIVENNEX=1' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-EVC.
      *Regraba el VEN respaldado con VEN-NUM-IVEN = 1
           MOVE VEN-BKP TO VEN.
      *DAD ini  31-MAY-1993
      **DAD ini  20-OCT-1992   (Memo.08-10-92)
      *     PERFORM PUT-VEN-IND-SCTB.
      *     MOVE SPACES TO MSG-COD-MENS
      **DAD fin  20-OCT-1992
      *DAD fin  31-MAY-1993
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
      *DAD ini  31-MAY-1993
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    NOMOD' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-EVC.
      *DAD fin  31-MAY-1993
       CON-GEN-SIG-VEN-EVC.
      *Proyeccion Estructura => actualizar EVC y DLC
           PERFORM ACT-REVC-RDLC.
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
           IF MSG-COD-MENS NOT > SPACES
               PERFORM ACT-VEN-SHFT.
      *DAD fin  20-OCT-1992
       FIN-GEN-SIG-VEN-EVC.
           EXIT.
      *-------------------------------------------------------------------------
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
       PUT-VEN-IND-SCTB SECTION.
       INI-PUT-VEN-IND-SCTB.
           IF WSS-IND-SCTB > SPACES
               MOVE WSS-IND-SCTB TO VEN-IND-SCTB IN VEN.
           IF WSS-IND-SCBZ > SPACES
               MOVE WSS-IND-SCBZ TO VEN-IND-SCBZ IN VEN.
       FIN-PUT-VEN-IND-SCTB.
           EXIT.
      *DAD fin  20-OCT-1992
      *-------------------------------------------------------------------------
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
       ACT-VEN-SHFT SECTION.
       INI-ACT-VEN-SHFT.
           MOVE 1 TO WSS-AUX-IVEN
                     VEN-NUM-IVEN IN VEN.
      * Lee fisicamente el Primer VEN
      *LRH ini 9-ABR-1994
      *     MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM COL-FIO-VEN.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN AND
                    VEN-NUM-IVEN IN VEN = 1 )
               GO TO FIN-ACT-VEN-SHFT.
      *LRH fin 9-ABR-1994
       LUP-ACT-VEN-SHFT.
      * Lee fisicamente el Siguiente VEN
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN )
               GO TO FIN-ACT-VEN-SHFT.
      * Elimina el VEN tomado de UPD previamente Respaldado
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENSHFTNOUPD' TO MSG-COD-MENS
               GO TO FIN-ACT-VEN-SHFT.
           MOVE VEN TO VEN-BKP.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENSHFTNODEL' TO MSG-COD-MENS
               GO TO FIN-ACT-VEN-SHFT.
      * Graba el VEN Respaldado luego de Actualizar
           MOVE VEN-BKP TO VEN.
           ADD 1 TO WSS-AUX-IVEN.
           MOVE WSS-AUX-IVEN TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENSHFTNOPUT' TO MSG-COD-MENS
               GO TO FIN-ACT-VEN-SHFT.
           MOVE VEN-BKP TO VEN.
           GO TO LUP-ACT-VEN-SHFT.
       FIN-ACT-VEN-SHFT.
           EXIT.
      *DAD fin  20-OCT-1992
      *-------------------------------------------------------------------------
       ACT-REVC-RDLC SECTION.
       INI-ACVT-REVC.
           MOVE 1 TO VEN-I.
      *Primer LUP para actualizar estructuras
       LU1-ACT-REVC-RDLC.
           IF VEN-I > VEN-EMAX
               GO TO CON-ACT-REVC-RDLC.
           MOVE VEN-CIC-IOPC IN VEN TO EVC-CIC-IOPC IN EVC.
           MOVE EVC-IEVC(VEN-I)  TO EVC-NUM-IEVC IN EVC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    EVCNX' TO MSG-COD-MENS VEN-STAT
               GO TO FIN-ACT-REVC-RDLC.
           MOVE EVC-DPVC(VEN-I) TO EVC-NUM-DPVC IN EVC.
           MOVE EVC-MPVC(VEN-I) TO EVC-NUM-MPVC IN EVC.
           MOVE EVC-SPVC(VEN-I) TO EVC-NUM-SPVC IN EVC.
           MOVE EVC-APVC(VEN-I) TO EVC-NUM-APVC IN EVC.
           MOVE EVC-VCAN(VEN-I) TO EVC-NUM-VCAN IN EVC.
           MOVE EVC-IVCT(VEN-I) TO EVC-NUM-IVCT IN EVC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    EVCNX' TO MSG-COD-MENS VEN-STAT
               GO TO FIN-ACT-REVC-RDLC.
           ADD 1 TO VEN-I.
           GO TO LU1-ACT-REVC-RDLC.
       CON-ACT-REVC-RDLC.
      *
           MOVE ZEROES TO VEN-I.
      *Segundo LUP para actualizar documentos
       LUP-ACT-REVC-RDLC.
           ADD 1 TO VEN-I.
           IF VEN-I > 10
               GO TO FIN-ACT-REVC-RDLC.
           IF DLC-TDOC(VEN-I) NOT > SPACES
               GO TO LUP-ACT-REVC-RDLC.
           MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-I            TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               GO TO LUP-ACT-REVC-RDLC.
           MOVE DLC-SINT(VEN-I) TO DLC-VAL-SINT IN DLC.
           MOVE DLC-FAMT(VEN-I) TO DLC-FEC-FAMT IN DLC.
           MOVE DLC-SCPA(VEN-I) TO DLC-VAL-SCPA IN DLC.
      *DAD ini  31-MAY-1993
      *Cuando Exista el VEN y SOLO se genere para proyectar las estructuras
           IF WSS-IND-EXIS = 'S'
               ADD WSS-VAL-CAPI-GENE        TO   DLC-VAL-SCPA IN DLC
               SUBTRACT VEN-VAL-CAPI IN VEN FROM DLC-VAL-SCPA IN DLC.
           MOVE 'N' TO WSS-IND-EXIS.
      *DAD fin  31-MAY-1993
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           GO TO LUP-ACT-REVC-RDLC.
       FIN-ACT-REVC-RDLC.
           EXIT.
      *
       GEN-SIG-VEN-VEN SECTION.
       INI-GEN-SIG-VEN-VEN.
           MOVE SPACES TO MSG-COD-MENS.
      *Solo para cancelacion especifica
           IF RCV-NUM-IVCT IN RCV NOT > ZEROES
               GO TO FIN-GEN-SIG-VEN-VEN.
      *lee el vencimiento siguiente si existe
           MOVE RCV-KEY-IVCT IN RCV TO VEN-KEY-IVEN IN VEN.
           ADD 1 TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF FIO-STAT-OKS
               GO TO FIN-GEN-SIG-VEN-VEN.
      *Solo para tasa variable
           MOVE OPC-CIC-IOPC IN OPC TO ICG-CIC-IOPC IN ICG.
           MOVE ZEROES              TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES              TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-CAI-IOPC IN ICG = OPC-CAI-IOPC IN OPC AND
                    ICG-IIC-IOPC IN ICG = OPC-IIC-IOPC IN OPC AND
                    ICG-NUM-ICAN IN ICG = ZEROES AND
                    ICG-CIC-IOPV IN ICG = SPACES AND
                    ICG-NUM-IVCT IN ICG = ZEROES AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               GO TO FIN-GEN-SIG-VEN-VEN.
           IF ICG-COD-CVTI IN ICG NOT > SPACES
               GO TO FIN-GEN-SIG-VEN-VEN.
      *lee el vencimiento anterior para obtener diferencia de dias
           MOVE RCV-KEY-IVCT IN RCV TO VEN-KEY-IVEN IN VEN.
           IF VEN-NUM-IVEN IN VEN > 1
               SUBTRACT 1 FROM VEN-NUM-IVEN IN VEN
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-VEN
               IF FIO-STAT-OKS
                   MOVE VEN-NUM-DVEN IN VEN TO FEC-DEC1
                   MOVE VEN-NUM-MVEN IN VEN TO FEC-MEC1
                   MOVE VEN-NUM-SVEN IN VEN TO FEC-SEC1
                   MOVE VEN-NUM-AVEN IN VEN TO FEC-AEC1
               ELSE
                   MOVE RCV-KEY-IVCT IN RCV TO VEN-KEY-IVEN IN VEN
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM COL-FIO-VEN
                   IF FIO-STAT-OKS
                       MOVE 30 TO FEC-NDIA
                       GO TO CON-GEN-SIG-VEN-VEN
                   ELSE
                       MOVE 'GENSVENNORCV' TO MSG-COD-MENS
                       GO TO FIN-GEN-SIG-VEN-VEN
           ELSE
               MOVE OPC-NUM-DCOL IN OPC TO FEC-DEC1
               MOVE OPC-NUM-MCOL IN OPC TO FEC-MEC1
               MOVE OPC-NUM-SCOL IN OPC TO FEC-SEC1
               MOVE OPC-NUM-ACOL IN OPC TO FEC-AEC1.
           MOVE RCV-KEY-IVCT IN RCV TO VEN-KEY-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'GENSVENNORCV' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-VEN.
           MOVE VEN-NUM-DVEN IN VEN TO FEC-DEC2.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-MEC2.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-SEC2.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'GENSVEN-DIA' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-VEN.
       CON-GEN-SIG-VEN-VEN.
           MOVE VEN-NUM-DVEN IN VEN TO FEC-ITM1.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-ITM2.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-ITM3.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-SUM-DIA TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'GENSVEN+DIA' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-VEN.
           IF OPC-IND-AFER IN OPC = 'S'
               MOVE FEC-FORM-FEC TO FEC-FORM
               MOVE FEC-FBLK-N   TO FEC-FBLK
               MOVE FEC-CHOY-N   TO FEC-CHOY
               MOVE FEC-VHBL-S   TO FEC-VHBL
               MOVE FEC-VAL-FEC  TO FEC-CMND
               PERFORM PRO-FEC
               IF FEC-STAT-FER
                   MOVE FEC-SIG-HBL  TO FEC-CMND
      *            PERFORM PRO-FEC
                   PERFORM CAL-FEC
                   IF FEC-STAT-OKS
                       NEXT SENTENCE
                   ELSE
                       MOVE 'GENSVENSHBL' TO MSG-COD-MENS
                       GO TO FIN-GEN-SIG-VEN-VEN
               ELSE
               IF NOT FEC-STAT-OKS
                   MOVE 'GENSVENVHBL' TO MSG-COD-MENS
                   GO TO FIN-GEN-SIG-VEN-VEN.
           MOVE FEC-ITM1 TO VEN-NUM-DVEN IN VEN.
           MOVE FEC-ITM2 TO VEN-NUM-MVEN IN VEN.
           MOVE FEC-ITM3 TO VEN-NUM-SVEN IN VEN.
           MOVE FEC-ITM4 TO VEN-NUM-AVEN IN VEN.
           MOVE VEN-FEC-FVEN IN VEN TO VEN-FEC-FIMV IN VEN.
           IF VEN-VAL-CAPI IN VEN > OPC-VAL-SCRE IN OPC
               MOVE OPC-VAL-SCRE IN OPC TO VEN-VAL-CAPI IN VEN
               MOVE 0                   TO VEN-VAL-FINA IN VEN
               MOVE 'N'                 TO VEN-IND-CALC IN VEN.
           ADD 1                    TO VEN-NUM-IVEN IN VEN.
           MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN.
           MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN.
           MOVE ZEROES              TO VEN-VAL-IDNI IN VEN.
           MOVE SPACES              TO VEN-MSC-TACC IN VEN.
           MOVE SPACES              TO VEN-MSC-STAT IN VEN.
           MOVE SPACES              TO VEN-COD-OTRN IN VEN.
           MOVE SPACES              TO VEN-COD-ATRN IN VEN.
           MOVE SPACES              TO VEN-IND-SCTB IN VEN.
           MOVE VEN-NUM-IVEN IN VEN TO VEN-NUM-IVCT IN VEN.
           MOVE SPACES              TO VEN-IND-SCBZ IN VEN.
           MOVE ZEROES              TO VEN-VAL-MTSC IN VEN.
           MOVE SPACES              TO VEN-IND-MTSC IN VEN.
           MOVE SPACES              TO VEN-GLS-DISP IN VEN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'GENSVENNOPUT' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-VEN.
      *
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NOGKU' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-VEN.
           ADD 1 TO OPC-NUM-VCVG IN OPC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NOMOD' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-VEN.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLC    NOGKU' TO MSG-COD-MENS
               GO TO FIN-GEN-SIG-VEN-VEN.
           MOVE VEN-FEC-FVEN IN VEN TO DLC-FEC-FMAV IN DLC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NOMOD' TO MSG-COD-MENS.
       FIN-GEN-SIG-VEN-VEN.
           EXIT.
      *-------------------------------------------------------------------------
       PRO-OPE SECTION.
       INI-PRO-OPE.
           MOVE CAN-CIC-IOPC IN CAN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-OPE.

      *     SUBTRACT WSS-NUM-VCVG IN WSS-VARI FROM OPC-NUM-VCVG IN OPC.
           SUBTRACT CAN-NUM-TVCC IN CAN FROM OPC-NUM-VCVG IN OPC.
      *DAD ini  22-OCT-1991
           IF OPC-NUM-TVCC IN OPC > ZEROES
               ADD CAN-NUM-TVCC IN CAN TO OPC-NUM-TVCC IN OPC
           ELSE
               ADD WSS-NUM-VCCM IN WSS-VARI TO OPC-NUM-VCCM IN OPC
               ADD WSS-NUM-VCCV IN WSS-VARI TO OPC-NUM-VCCV IN OPC
               ADD WSS-NUM-VCCO IN WSS-VARI TO OPC-NUM-VCCO IN OPC.
      *DAD fin  22-OCT-1991
           MOVE CAN-CIC-IOPC IN CAN TO VEN-CIC-IOPC IN VEN.
      *JSS 6-APR-1991 19:58:27
           IF NOT ( CAN-MSC-FCAN IN CAN = 'T' AND
                    CAN-MSC-ECAN IN CAN = 'T' )
              ADD CAN-NUM-TVCC IN CAN TO OPC-NUM-IVEN IN OPC
              IF OPC-NUM-IVEN IN OPC = ZEROES
                  MOVE 1 TO OPC-NUM-IVEN IN OPC.
      *
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           IF NOT FIO-STAT-OKS
               MOVE 'TOC    BCK' TO MSG-COD-MENS
               GO TO FIN-PRO-OPE.
           IF TOC-MSC-OPEC = 'D'
      * Parametrizar ?        ^
               IF WSS-SGV-FINA(500) > OPC-VAL-SCRE IN OPC
                   MOVE ZEROES TO OPC-VAL-SCRE IN OPC
               ELSE
                   SUBTRACT WSS-SGV-FINA(500) FROM OPC-VAL-SCRE IN OPC
           ELSE
      *DAD ini  26-FEB-1993   (Memo.08-01-93)
      * Al Ajustar Valor CAPITAL y FINAL de Cuota Intermedia o Final
      * al Cancelar se debe ajustar el Numero de Vencimientos Vigentes
               IF WSS-SGV-CAPD(500) NOT < OPC-VAL-SCRE IN OPC
                   MOVE ZEROES TO OPC-VAL-SCRE IN OPC
                   MOVE ZEROES TO OPC-NUM-VCVG IN OPC
      *DAD fin  26-FEB-1993
               ELSE
                   SUBTRACT WSS-SGV-CAPD(500) FROM OPC-VAL-SCRE IN OPC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    BCK' TO MSG-COD-MENS.
       FIN-PRO-OPE.
           EXIT.
      **************************************************************************
       PRO-DOC SECTION.
       INI-PRO-DOC.
           IF WSS-SGV-CAPD(WSS-NUM-IDLC) = ZEROES
               PERFORM PRO-DVEG-DLC
               GO TO FIN-PRO-DOC.
           MOVE WSS-NUM-IDLC TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 500 TO WSS-NUM-IDLC
               GO TO FIN-PRO-DOC.
           IF TOC-MSC-OPEC = 'D'
               IF WSS-SGV-FINA(WSS-NUM-IDLC) > DLC-VAL-SDLC IN DLC
                   MOVE ZEROES TO DLC-VAL-SDLC IN DLC
               ELSE
                   SUBTRACT WSS-SGV-FINA(WSS-NUM-IDLC)
                       FROM DLC-VAL-SDLC IN DLC
           ELSE
               IF WSS-SGV-CAPD(WSS-NUM-IDLC) > DLC-VAL-SDLC IN DLC
                   MOVE ZEROES TO DLC-VAL-SDLC IN DLC
               ELSE
                   SUBTRACT WSS-SGV-CAPD(WSS-NUM-IDLC)
                       FROM DLC-VAL-SDLC IN DLC.
      *
      *Para REVersa de EVC SCPA y FAMT se actualizan en ACT-REVC-RDLC
           IF ( OPC-IND-VSTR IN OPC = 'S' ) AND ( SCR-CMND = 'REV' )
               GO TO SIG-PRO-DOC.
      *Para operaciones con EVC donde el valor estimado es el CAPITAL
      *y para creditos con VEN se actualiza SCPA
           IF ( ( OPC-IND-VSTR IN OPC = 'S' ) AND
                ( RCV-MSC-STAT IN RCV = 'C' ) ) OR
                ( OPC-IND-VSTR IN OPC NOT = 'S' )
               IF TOC-MSC-OPEC = 'D'
                   IF WSS-SGV-FINA(WSS-NUM-IDLC) > DLC-VAL-SCPA IN DLC
                       MOVE ZEROES TO DLC-VAL-SCPA IN DLC
                   ELSE
                       SUBTRACT WSS-SGV-FINA(WSS-NUM-IDLC)
                           FROM DLC-VAL-SCPA IN DLC
               ELSE
                   IF WSS-SGV-CAPD(WSS-NUM-IDLC) > DLC-VAL-SCPA IN DLC
                       MOVE ZEROES TO DLC-VAL-SCPA IN DLC
                   ELSE
                       SUBTRACT WSS-SGV-CAPD(WSS-NUM-IDLC)
                           FROM DLC-VAL-SCPA IN DLC.
      *Para operaciones con VEN se actualiza FAMT
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               MOVE WSS-FEC-FAMT TO DLC-FEC-FAMT IN DLC.
      *
       SIG-PRO-DOC.
      *
      *JSS 6-APR-1991 22:00:22
      *Si el comando es REVERSA de un *TE y situacion contable no vigente
           IF SCR-CMND = 'REV'
               IF ( CAN-MSC-FCAN IN CAN = 'T' ) AND
                  ( CAN-MSC-ECAN IN CAN = 'E' ) AND
                  ( VEN-IND-SCTB IN VEN > SPACES )
                   PERFORM PRO-REV-RDT-DLC
               ELSE
                   NEXT SENTENCE
           ELSE
      *Si el comando es CAN o CVC y se cancelo totalmente la cuota y
      *situacion contable no vigente
           IF ( SCR-CMND = 'CAN' OR SCR-CMND = 'CVC' ) AND
              ( VEN-VAL-SFIN IN VEN NOT > ZEROES )     AND
              ( VEN-IND-SCTB IN VEN > SPACES )
               PERFORM PRO-RDT-DLC.
      *
           IF NOT ( SCR-CMND = 'CAN' OR
                    SCR-CMND = 'CVC')
               GO TO CON-PRO-DOC.
      *Respalda fecha de devengo del documento en la cancelacion
           MOVE DLC-FEC-FDEV IN DLC TO CAN-FEC-FDVA IN CAN.
      *ALD/28-FEB-1990
           IF CAN-MSC-CANC IN CAN = 'CTT'
               MOVE DLC-FEC-FMAV IN DLC TO DLC-FEC-FDEV IN DLC
           ELSE
           IF ( CAN-MSC-FCAN IN CAN = 'T' OR
                CAN-MSC-FCAN IN CAN = 'I' ) AND
                OPC-NUM-VCOR IN OPC = 1
               MOVE CAN-FEC-FCAN IN CAN TO DLC-FEC-FDEV IN DLC
           ELSE
      *DAD ini  22-OCT-1991
           IF CAN-MSC-FCAN IN CAN = 'T' OR
              CAN-MSC-FCAN IN CAN = 'I'
               MOVE VEN-NUM-DVEN IN VEN TO DLC-NUM-DDEV IN DLC
               MOVE VEN-NUM-MVEN IN VEN TO DLC-NUM-MDEV IN DLC
               MOVE VEN-NUM-SVEN IN VEN TO DLC-NUM-SDEV IN DLC
               MOVE VEN-NUM-AVEN IN VEN TO DLC-NUM-ADEV IN DLC.
      *DAD fin  22-OCT-1991
       CON-PRO-DOC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 500 TO WSS-NUM-IDLC.
       FIN-PRO-DOC.
           EXIT.
      *-------------------------------------------------------------------------
       PRO-DVEG-DLC SECTION.
       INI-PRO-DVEG-DLC.
           MOVE WSS-NUM-IDLC TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 500 TO WSS-NUM-IDLC
               GO TO FIN-PRO-DVEG-DLC.
           IF NOT ( SCR-CMND = 'CAN' OR
                    SCR-CMND = 'CVC')
               GO TO CON-PRO-DVEG-DLC.
           MOVE DLC-FEC-FDEV IN DLC TO CAN-FEC-FDVA IN CAN.
      *ALD/28-FEB-1990
           IF CAN-MSC-CANC IN CAN = 'CTT'
               MOVE DLC-FEC-FMAV IN DLC TO DLC-FEC-FDEV IN DLC
           ELSE
           IF ( CAN-MSC-FCAN IN CAN = 'T' OR
                CAN-MSC-FCAN IN CAN = 'I' ) AND
                OPC-NUM-VCOR IN OPC = 1
               MOVE CAN-FEC-FCAN IN CAN TO DLC-FEC-FDEV IN DLC
           ELSE
           IF CAN-MSC-FCAN IN CAN = 'T' OR
              CAN-MSC-FCAN IN CAN = 'I'
      *DAD ini  22-OCT-1991
               MOVE VEN-NUM-DVEN IN VEN TO DLC-NUM-DDEV IN DLC
               MOVE VEN-NUM-MVEN IN VEN TO DLC-NUM-MDEV IN DLC
               MOVE VEN-NUM-SVEN IN VEN TO DLC-NUM-SDEV IN DLC
               MOVE VEN-NUM-AVEN IN VEN TO DLC-NUM-ADEV IN DLC.
      *DAD fin  22-OCT-1991
       CON-PRO-DVEG-DLC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
       FIN-PRO-DVEG-DLC.
           EXIT.
      *-------------------------------------------------------------------------
       PRO-RDT-DLC SECTION.
       INI-PRO-RDT-DLC.
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           MOVE 'COL'               TO MSC-COD-SIST FIO-SIST.
           MOVE 'OPEC'              TO MSC-COD-TMSC IN MSC.
           MOVE TOC-MSC-OPEC IN TOC TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           MOVE VEN-CIC-IOPC IN VEN TO RDT-CIC-IOPC IN RDT.
           MOVE VEN-NUM-IDLC IN VEN TO RDT-NUM-IDLC IN RDT.
           MOVE VEN-IND-SCTB IN VEN TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-RDT-DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-RDT-DLC.
           IF MSC-CIC-CMSC IN MSC = 'D'           
               SUBTRACT WSS-VAL-SMO2 FROM RDT-VAL-SRDT IN RDT 
           ELSE
               SUBTRACT WSS-VAL-SMO1 FROM RDT-VAL-SRDT IN RDT.
           IF RDT-VAL-SRDT IN RDT NOT > ZEROES
               MOVE FIO-DEL TO FIO-CMND
               PERFORM COL-FIO-RDT
           ELSE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-RDT.
           MOVE VEN-CIC-IOPC IN VEN TO RDT-CIC-IOPC IN RDT.
           MOVE VEN-NUM-IDLC IN VEN TO RDT-NUM-IDLC IN RDT.
           MOVE SPACES              TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = RDT-CIC-IOPC IN RDT AND
              VEN-NUM-IDLC IN VEN = RDT-NUM-IDLC IN RDT
               GO TO FIN-PRO-RDT-DLC.
           MOVE SPACES TO DLC-IND-SCTB IN DLC.
       FIN-PRO-RDT-DLC.
           EXIT.
      *-------------------------------------------------------------------------
       PRO-REV-RDT-DLC SECTION.
       INI-PRO-REV-RDT-DLC.
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           MOVE 'COL' TO MSC-COD-SIST FIO-SIST.
           MOVE 'OPEC' TO MSC-COD-TMSC IN MSC.
           MOVE TOC-MSC-OPEC IN TOC TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           MOVE VEN-CIC-IOPC IN VEN TO RDT-CIC-IOPC IN RDT.
           MOVE VEN-NUM-IDLC IN VEN TO RDT-NUM-IDLC IN RDT.
           MOVE VEN-IND-SCTB IN VEN TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF FIO-STAT-OKS
               GO TO CON-PRO-REV-RDT-DLC.
           IF WSS-VAL-SMO1 < ZEROES
               SUBTRACT WSS-VAL-SMO1 FROM ZEROES GIVING
                        WSS-VAL-SMO1.
           IF WSS-VAL-SMO2 < ZEROES
               SUBTRACT WSS-VAL-SMO2 FROM ZEROES GIVING
                        WSS-VAL-SMO2.
           IF MSC-CIC-CMSC IN MSC = 'D'           
               MOVE WSS-VAL-SMO2 TO RDT-VAL-SRDT IN RDT 
           ELSE
               MOVE WSS-VAL-SMO1 TO RDT-VAL-SRDT IN RDT.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           MOVE 'MUL' TO DLC-IND-SCTB IN DLC.
           GO TO FIN-PRO-REV-RDT-DLC.
       CON-PRO-REV-RDT-DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-REV-RDT-DLC.
           IF MSC-CIC-CMSC IN MSC = 'D'           
               SUBTRACT WSS-VAL-SMO2 FROM RDT-VAL-SRDT IN RDT 
           ELSE
               SUBTRACT WSS-VAL-SMO1 FROM RDT-VAL-SRDT IN RDT.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-RDT.
       FIN-PRO-REV-RDT-DLC.
           EXIT.
      **************************************************************************
