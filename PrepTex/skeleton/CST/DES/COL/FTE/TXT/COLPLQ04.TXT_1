*%! Codigo adicional para archivo
*% IF RW
      *<<<< RW
       01  WSS-Q4A-RED     REDEFINES     WSS-Q4A.
           03  FILLER                             PIC X(19).
           03  WSS-Q4A-RED-ELE.
               05  WSS-Q4A-ELE OCCURS 7.
                   07  WSS-SCCI                   PIC 9(08).
                   07  WSS-PROM                   PIC 9(08).

       COPY COLRWQ4B.
       01  WSS-Q4B-RED     REDEFINES     WSS-Q4B.
           03  WSS-Q4B-RED-ELE.
               05  WSS-Q4B-ELE OCCURS 6.
                   07  WSS-SCCI                   PIC 9(08).
                   07  WSS-PROM                   PIC 9(08).
           03  WSS-VAL-TSCC                       PIC 9(11).
           03  WSS-VAL-TPRO                       PIC 9(11).
      *>>>>
*% END
*% IF WSS
      *<<<< WSS
       COPY TABBRCAM.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       01  WSS-VARI.
           03 WSS-FEC-FINF.
              05 WSS-NUM-DINF                     PIC 9(02).
              05 WSS-NUM-MINF                     PIC 9(02).
              05 WSS-NUM-SINF                     PIC 9(02).
              05 WSS-NUM-AINF                     PIC 9(02).
           03 WSS-FEC-FIN1.
              05 WSS-NUM-SIN1                     PIC 9(02).
              05 WSS-NUM-AIN1                     PIC 9(02).
              05 WSS-NUM-MIN1                     PIC 9(02).
              05 WSS-NUM-DIN1                     PIC 9(02).
           03 WSS-FEC-FCAM.
              05 WSS-NUM-SCAM                     PIC 9(02).
              05 WSS-NUM-ACAM                     PIC 9(02).
              05 WSS-NUM-MCAM                     PIC 9(02).
              05 WSS-NUM-DCAM                     PIC 9(02).
           03 WSS-FEC-FCA1.
              05 WSS-NUM-DCA1                     PIC 9(02).
              05 WSS-NUM-MCA1                     PIC 9(02).
              05 WSS-NUM-SCA1                     PIC 9(02).
              05 WSS-NUM-ACA1                     PIC 9(02).
           03 WSS-CIC-VCAM                        PIC X(06).
           03 WSS-COD-VCAM                        PIC X(06).
           03 WSS-COD-MENS                        PIC X(12).
           03 WSS-COD-PESO                        PIC X(06).
           03 WSS-COD-GODS.
              05 WSS-COD-OD12.
                 07 WSS-COD-ODS1                  PIC X(01).
                 07 WSS-COD-ODS2                  PIC X(01).
              05 WSS-COD-ODS3                     PIC X(01).
           03 WSS-VEC-ODSE  OCCURS 99  INDEXED BY  WSS-NUM-ODSE.
              05 WSS-COD-ODSE                     PIC X(03).
              05 WSS-CIC-ODSE                     PIC X(03).
           03 WSS-ODSE                            PIC 9(02).
           03 WSS-TODS                            PIC 9(02).
           03 WSS-IND-ODSE                        PIC 9(01).
           03 WSS-IND                             PIC 9(02).
           03 WSS-SUB                             PIC 9(02).
           03 WSS-NUM-HABL         VALUE 0        PIC 9(02).
           03 WSS-NUM-SCCR         VALUE 0        PIC 9(06).
           03 WSS-NUM-SCCP         VALUE 0        PIC 9(06).
           03 WSS-VEC.
              05 WSS-ELE OCCURS 13.
                 07 WSS-SCCI                      PIC 9(08).
                 07 WSS-PROM                      PIC 9(08).
              05 WSS-ELD OCCURS 13.
                 07 WSS-MTOD                      PIC 9(11)V9(04).
                 07 WSS-PTOD                      PIC 9(11)V9(04).
      *>>>>
*% END
*% IF WSS_DTC
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *>>>>
*% END
*% IF RT
      *<<<< RT
       COPY COLRTQ4B.
      *>>>>
*% END
*% IF INI
      *<<<< INI
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      * Captura Fecha del Informe
           DISPLAY 'INGRESE FECHA DEL INFORME (DDMMSSAA) '.
           ACCEPT WSS-FEC-FINF IN WSS-VARI.
           DISPLAY 'FECHA DEL INFORME : ' WSS-FEC-FINF IN WSS-VARI.
      
           DISPLAY 'FIN CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
        
      *VAL-FEC Valida Fecha de Informe
           DISPLAY WSS-FEC-FINF IN WSS-VARI.
           MOVE WSS-FEC-FINF IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

           MOVE WSS-NUM-DINF IN WSS-VARI TO WSS-NUM-DACT IN WSS-Q4A
                                            WSS-NUM-DIN1 IN WSS-VARI.
           MOVE WSS-NUM-MINF IN WSS-VARI TO WSS-NUM-MACT IN WSS-Q4A
                                            WSS-NUM-MIN1 IN WSS-VARI.
           MOVE WSS-NUM-SINF IN WSS-VARI TO WSS-NUM-SACT IN WSS-Q4A
                                            WSS-NUM-SIN1 IN WSS-VARI.
           MOVE WSS-NUM-AINF IN WSS-VARI TO WSS-NUM-AACT IN WSS-Q4A
                                            WSS-NUM-AIN1 IN WSS-VARI.

           MOVE WSS-FEC-FINF IN WSS-VARI TO WSS-FEC-FCA1 IN WSS-VARI.
           MOVE WSS-FEC-FIN1 IN WSS-VARI TO WSS-FEC-FCAM IN WSS-VARI.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Se obtienen los codigos de monedas, mediante el CIC de dichas monedas
      * y sus valores de cambio en pesos a la fecha del informe
           MOVE '0999  '       TO WSS-CIC-VCAM IN WSS-VARI.
           MOVE 'CIC0999  NEX' TO WSS-COD-MENS IN WSS-VARI.
           PERFORM BUS-COD-VLR.
           MOVE WSS-COD-VCAM IN WSS-VARI TO WSS-COD-PESO IN WSS-VARI.

           MOVE '101301'       TO WSS-CIC-VCAM IN WSS-VARI.
           MOVE 'CIC101301NEX' TO WSS-COD-MENS IN WSS-VARI.
           PERFORM BUS-COD-VLR.
           PERFORM CON-VAL-PESOS.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VDOL IN WSS-Q4A.

           MOVE '0998  '       TO WSS-CIC-VCAM IN WSS-VARI.
           MOVE 'CIC0998  NEX' TO WSS-COD-MENS IN WSS-VARI.
           PERFORM BUS-COD-VLR.
           PERFORM CON-VAL-PESOS.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VUEF IN WSS-Q4A.
           
      * Calcula el numero de dias habiles del periodo del mes a listar
           PERFORM CAL-DIA-HBL.

      * Almacena los Codigos y CIC de Origen y Destino Sectorial de tabla ODS
           PERFORM GET-ODS.

      * Inicializacion de las Tablas de Acumulacion del Proceso
           MOVE ZEROES TO WSS-VEC.
      *>>>>
*% END
*% IF LUP_INP
      *<<<< LUP_INP
           ADD 1 TO WSS-NUM-SCCR IN WSS-VARI.
           IF SCC-FEC-FSCC IN SCC > WSS-FEC-FIN1 IN WSS-VARI
               GO TO FIN-INP-SRT.
      *    IF SCC-NUM-DSCC IN SCC > WSS-NUM-DINF IN WSS-VARI
      *        GO TO NXT-INP-SRT.
      *>>>>
*% END
*% IF RM
      *<<<< RM
      * Para moneda distinta de pesos se tranforma a pesos con el valor de
      * cambio en pesos para la moneda a la fecha del informe
           IF SCC-COD-VCSC IN SCC NOT = WSS-COD-PESO IN WSS-VARI
               MOVE SCC-COD-VCSC IN SCC TO WSS-COD-VCAM IN WSS-VARI
               PERFORM CON-VAL-PESOS
               MULTIPLY CAM-SGV-VCAM IN CAM BY SCC-VAL-SCCI IN SCC
                                        GIVING SCC-VAL-SCCI IN SRT.
      *>>>>
*% END
*% IF LUP_OUT
      *<<<< LUP_OUT
      * Busca en Tabla el CIC de Origen y Destino Sectorial con el Codigo de ODS
           PERFORM BUS-CIC-ODSE.
           IF WSS-IND-ODSE IN WSS-VARI = 1
               DISPLAY 'ERROR :SCC-COD-ODSE 0 INVALIDO ' SCC-KEY-ISCC
               GO NXT-OUT-SRT.

      * Asigna Indice de tabla Acumulacion proceso segun CIC de ODS
           IF WSS-COD-ODS1 = '1'
               IF WSS-COD-OD12 = '11'
                   MOVE 2 TO WSS-IND
               ELSE
               IF WSS-COD-OD12 = '12'
                   MOVE 3 TO WSS-IND
               ELSE
               DISPLAY 'ERROR:SCC-COD-ODSE 1 INVALIDO ' WSS-COD-GODS
                       '  ' SCC-KEY-ISCC
               GO TO NXT-OUT-SRT
           ELSE
           IF WSS-COD-ODS1 = '2'
               IF WSS-COD-GODS = '211'
                   MOVE 6 TO WSS-IND
               ELSE
               IF WSS-COD-GODS = '212'
                   MOVE 7 TO WSS-IND
               ELSE
               IF WSS-COD-GODS = '221'
                   MOVE 9 TO WSS-IND
               ELSE
               IF WSS-COD-GODS = '222'
                   MOVE 10 TO WSS-IND
               ELSE
               IF WSS-COD-GODS = '223'
                   MOVE 11 TO WSS-IND
               ELSE
               IF WSS-COD-GODS = '229'
                   MOVE 12 TO WSS-IND
               ELSE
               DISPLAY 'ERROR:SCC-COD-ODSE 2 INVALIDO ' WSS-COD-GODS
                       '  ' SCC-KEY-ISCC
               GO TO NXT-OUT-SRT
           ELSE
           IF WSS-COD-ODS1 = '3'
               MOVE 13 TO WSS-IND
           ELSE
               DISPLAY 'ERROR:SCC-COD-ODSE 3 INVALIDO ' WSS-COD-GODS
                       '  ' SCC-KEY-ISCC
               GO TO NXT-OUT-SRT.

           ADD 1 TO WSS-NUM-SCCP IN WSS-VARI.

      * Acumula el saldo a fecha proceso en tabla segun indice 
           IF SCC-FEC-FSCC IN SCC = WSS-FEC-FIN1 IN WSS-VARI
               ADD SCC-VAL-SCCI IN SRT TO WSS-MTOD IN WSS-VEC(WSS-IND).

      * Acumula saldo para calcular promedio en tabla segun Indice 
           ADD SCC-VAL-SCCI IN SRT TO WSS-PTOD IN WSS-VEC(WSS-IND).
           GO TO NXT-OUT-SRT.
      *>>>>
*% END
*% IF FIN_OUT
      *<<<< FIN_OUT
      * Totaliza montos acumulados del saldo y del promedio en tabla
           ADD WSS-MTOD IN WSS-ELD(2)
               WSS-MTOD IN WSS-ELD(3)  GIVING WSS-MTOD IN WSS-ELD(1).
           ADD WSS-PTOD IN WSS-ELD(2)
               WSS-PTOD IN WSS-ELD(3)  GIVING WSS-PTOD IN WSS-ELD(1).
           ADD WSS-MTOD IN WSS-ELD(6)
               WSS-MTOD IN WSS-ELD(7)  GIVING WSS-MTOD IN WSS-ELD(5).
           ADD WSS-PTOD IN WSS-ELD(6)
               WSS-PTOD IN WSS-ELD(7)  GIVING WSS-PTOD IN WSS-ELD(5).
           ADD WSS-MTOD IN WSS-ELD(9)
               WSS-MTOD IN WSS-ELD(10) 
               WSS-MTOD IN WSS-ELD(11)
               WSS-MTOD IN WSS-ELD(12) GIVING WSS-MTOD IN WSS-ELD(8).
           ADD WSS-PTOD IN WSS-ELD(9)
               WSS-PTOD IN WSS-ELD(10) 
               WSS-PTOD IN WSS-ELD(11)
               WSS-PTOD IN WSS-ELD(12) GIVING WSS-PTOD IN WSS-ELD(8).
           ADD WSS-MTOD IN WSS-ELD(5)
               WSS-MTOD IN WSS-ELD(8)  GIVING WSS-MTOD IN WSS-ELD(4).
           ADD WSS-PTOD IN WSS-ELD(5)
               WSS-PTOD IN WSS-ELD(8)  GIVING WSS-PTOD IN WSS-ELD(4).
           MOVE 1 TO WSS-IND.
       LUP-CAR.
      * Transforma en (M$) los montos de saldos acumulados en tabla
           DIVIDE WSS-MTOD IN WSS-ELD(WSS-IND) BY 1000
               GIVING WSS-SCCI IN WSS-ELE(WSS-IND) ROUNDED.

      *     DIVIDE WSS-SCCI IN WSS-ELE(WSS-IND) BY 
      *                                         WSS-NUM-HABL IN WSS-VARI
      *         GIVING WSS-PROM IN WSS-ELE(WSS-IND) ROUNDED.
      * Calcula promedio en (M$) con montos acumulados para promedio en tabla
           COMPUTE WSS-PROM IN WSS-ELE(WSS-IND) ROUNDED =
                            WSS-PTOD IN WSS-ELD(WSS-IND) /
                            WSS-NUM-HABL IN WSS-VARI / 1000.

      * Traspasa a reporte los valores en (M$) en tabla
           IF WSS-IND < 8
               MOVE WSS-ELE(WSS-IND) TO WSS-Q4A-ELE(WSS-IND)
           ELSE
               COMPUTE WSS-SUB = WSS-IND - 7
               MOVE WSS-ELE(WSS-IND) TO WSS-Q4B-ELE(WSS-SUB).

           ADD 1 TO WSS-IND.
           IF WSS-IND < 14
               GO TO LUP-CAR.          

      * Totaliza totales parciales de tabla en totales del reporte
           ADD WSS-SCCI IN WSS-ELE(1)
               WSS-SCCI IN WSS-ELE(4)
               WSS-SCCI IN WSS-ELE(13) GIVING WSS-VAL-TSCC IN WSS-Q4B.
           ADD WSS-PROM IN WSS-ELE(1)
               WSS-PROM IN WSS-ELE(4)
               WSS-PROM IN WSS-ELE(13) GIVING WSS-VAL-TPRO IN WSS-Q4B.

      * Display Totales de Control y generacion del reporte
           DISPLAY 'TOTAL REG. SCC LEIDOS : ' WSS-NUM-SCCR IN WSS-VARI.
           DISPLAY 'TOTAL REG. PROCESADOS : ' WSS-NUM-SCCP IN WSS-VARI.
           GENERATE DL-PPG.
           GENERATE DL-Q4B.
      *>>>>
*% END
*% IF EOF
      *<<<< EOF
       COPY GNSBPFEC
      **************************************************************************
      * Obtiene el codigo de la moneda, mediante el CIC de la moneda
       BUS-COD-VLR SECTION.
       INI-BUS-COD-VLR.
           MOVE 'TAB'        TO FIO-SIST.
           MOVE 'VLR'        TO TAB-CIC-TTAB IN TAB.
           MOVE WSS-CIC-VCAM TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'        TO MSG-COD-SIST
               MOVE WSS-COD-MENS TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-END.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-VCAM IN WSS-VARI.
       FIN-BUS-COD-VLR.
           EXIT.
      **************************************************************************
      * Calcula el numero de dias habiles del periodo del mes a listar
       CAL-DIA-HBL SECTION.
       INI-CAL-DIA-HBL.
           MOVE WSS-FEC-FINF IN WSS-VARI TO FEC-FECH.
           MOVE '01'                     TO FEC-ITM1.
           PERFORM PRO-FEC.
           IF FEC-STAT-HBL
               MOVE 1 TO WSS-NUM-HABL IN WSS-VARI
           ELSE
               MOVE 0 TO WSS-NUM-HABL IN WSS-VARI.
       LUP-SIG-HBL.
           MOVE FEC-FORM-DMA TO FEC-FORM.
           MOVE FEC-SIG-HBL  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF FEC-ITM1 NOT > WSS-NUM-DINF IN WSS-VARI AND
              FEC-ITM2 = WSS-NUM-MINF IN WSS-VARI
               ADD 1 TO WSS-NUM-HABL IN WSS-VARI
               GO TO LUP-SIG-HBL.
           DISPLAY 'NRO DE DIAS HABILES : ' WSS-NUM-HABL.
       FIN-CAL-DIA-HBL.
           EXIT.
      **************************************************************************
      * Almacena los Codigos y CIC de Origen y Destino Sectorial de tabla ODS
       GET-ODS SECTION.
       INI-GET-ODS.
           MOVE ZEROES TO WSS-ODSE.
           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'ODS'  TO TAB-COD-TABL IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-GET-ODS.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR
              TAB-COD-TTAB IN TAB NOT = 'ODS'
               MOVE WSS-ODSE IN WSS-VARI TO WSS-TODS IN WSS-VARI
               GO TO FIN-GET-ODS.
           IF TAB-COD-CTAB IN TAB NOT > SPACES
               GO TO NXT-GET-ODS.
           ADD 1 TO WSS-ODSE.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-ODSE(WSS-ODSE).
           MOVE TAB-CIC-CTAB IN TAB TO WSS-CIC-ODSE(WSS-ODSE).
       NXT-GET-ODS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-GET-ODS.
       FIN-GET-ODS.
           EXIT.
      **************************************************************************
      * Busca en Tabla el CIC de Origen y Destino Sectorial con el Codigo de ODS
       BUS-CIC-ODSE SECTION.
       INI-BUS-CIC-ODSE.
           MOVE  1  TO WSS-ODSE     IN WSS-VARI
                       WSS-IND-ODSE IN WSS-VARI.
           SET WSS-NUM-ODSE TO 1.
           SEARCH WSS-VEC-ODSE VARYING WSS-ODSE 
                  WHEN ( WSS-ODSE > WSS-TODS OR 
                     WSS-COD-ODSE(WSS-NUM-ODSE) ) = SCC-COD-ODSE IN SRT
                         GO TO FND-OKS.
           GO TO FIN-BUS-CIC-ODSE.
       FND-OKS.
           IF WSS-ODSE NOT > WSS-TODS
               MOVE WSS-CIC-ODSE(WSS-ODSE) TO WSS-COD-GODS IN WSS-VARI
               MOVE  0  TO WSS-IND-ODSE IN WSS-VARI.
       FIN-BUS-CIC-ODSE.
           EXIT.
      **************************************************************************
      * Obtiene valor de cambio en pesos para moneda a la fecha del informe
       CON-VAL-PESOS SECTION.
       INI-CON-VAL-PESOS.
           MOVE WSS-FEC-FCAM IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FCAM IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-VCAM IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                       WSS-FEC-FCA1 ' PARA : ' WSS-COD-VCAM IN WSS-VARI
               PERFORM GNS-PRO-END.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY 'IND. VIGENCIA NO VALIDO A ESTA FECHA : '
                       WSS-FEC-FCA1 ' PARA : ' WSS-COD-VCAM IN WSS-VARI
               PERFORM GNS-PRO-END.
       FIN-CON-VAL-PESOS.
           EXIT.
      **************************************************************************

       COPY TABBFCAM.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
      *>>>>
*% END
