       IDENTIFICATION DIVISION.                                         
      *                                                                 
       PROGRAM-ID.     GARPBAD3.                                         
       AUTHOR.         G. URETA Q.                                      
      * REMARKS.     CONVERTIDO A GENESIS POR F. LIZANA B. JUL 1991
       DATE-WRITTEN.   27-AUG-1987.                                     
       DATE-COMPILED.                                                   
      *         PROGRAMA COBOL + DATACOM/DB                             
      *         QUE TIENE POR OBJETIVO CALCULAR                         
      *         LOS MONTOS REALES Y NO REALES                           
      *         DE LAS GARANTIAS.                                       
      *                                                                 
      *         SE USAN LAS TABLAS :                                    
      *             + INPUT                                             
      *               - D01 (VSAM)                                      
      *                                                                 
      *             + INPUT-OUTPUT                                      
      *               - D03 (DATACOM)                                   
      *                                                                 
      * ENVIRONMENT DIVISION.                                            
      * INPUT-OUTPUT SECTION.                                            
                                                                        
      * FILE-CONTROL.                                                    
                                                                        
      *     SELECT D01                  ASSIGN       TO CYDV001          
      *                                 ORGANIZATION IS INDEXED          
      *                                 ACCESS       IS DYNAMIC          
      *                                 RECORD KEY   IS D01-CLAVE        
      *                                 FILE STATUS  IS FIO-STAT.          
                                                                        
      *                                                                 
       DATA DIVISION.                                                   
      * FILE SECTION.                                                    
                                                                        
      * FD  D01                                                          
      *     LABEL RECORDS OMITTED.                                    
                                                                        
      * COPY GARBRD01.                                               
                                                                               
      *                                                                 
       WORKING-STORAGE SECTION.                                         
                                                                        
       77  CLAVE-ANT                       PIC X(10).                   
       77  CLAVE-SIG                       PIC X(10).                   
       77  SUMA-TOTAL-REA                  PIC S9(11)V9(2) VALUE +0.    
       77  SUMA-TOTAL-NRE                  PIC S9(11)V9(2) VALUE +0.    
       77  CONT-D03-LEIDOS                 PIC 9(05) VALUE 0.           
       77  CONT-D03-GRABADOS               PIC 9(05) VALUE 0.           
       77  PRIM-LECT                       PIC 9(01) VALUE 0.           
                                                                        
       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWGIFD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRERR.
       COPY GNSWGHOY.
       COPY GNSWGSYS.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-D03-REQA==.
                                                                        
       COPY GARBRD03.
       COPY GARBRD01.                                               
                                                                        
      *------------------------------------------------------------*    
      *                                                            *    
      *                   PROGRAMA PRINCIPAL                       *    
      *                                                            *    
      *------------------------------------------------------------*    
                                                                        
       PROCEDURE DIVISION.                                              
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           PERFORM INI.
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN.
                                                                        
           PERFORM PROCESO-PRINC UNTIL NOT FIO-STAT-OKS.
       FIN-MAIN.
           DISPLAY ' '.                                                 
           DISPLAY 'GARPBAD3 : '.                                        
           DISPLAY ' '.                                                 
           DISPLAY 'FIN NORMAL .... '.                                  
           DISPLAY ' '.                                                 
           DISPLAY ' '.                                                 
           DISPLAY 'DEUDORES  LEIDOS      : ' CONT-D03-LEIDOS.          
           DISPLAY ' '.                                                 
           DISPLAY 'REGISTROS ACTUALIZADOS: ' CONT-D03-GRABADOS.        
           DISPLAY ' '.                                                 
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-D03.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-D01.
       COPY GNSBGGBK.

       INI SECTION.
       INI-INI.
           MOVE 'GARPBAD3' TO FIO-PROG.
           MOVE ERR-TERR-LGC IN ERR-VARI TO ERR-COD-TERR IN ERR.
           MOVE 'GARPBAD3'               TO ERR-COD-ATRN IN ERR.
           MOVE ERR-TTRN-BAT IN ERR-VARI TO ERR-MSC-TTRN IN ERR.
           MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR.
           MOVE 'M' TO ERR-MSC-TACC IN ERR.
                                                                        
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               GO TO FIN-INI.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-D01.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-D01.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               GO TO FIN-INI.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-D03.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-D03.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               GO TO FIN-INI.

           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-D03.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR
               MOVE 'D03    NEX'    TO ERR-COD-MENS IN ERR
               MOVE SPACES          TO ERR-COD-CMPO IN ERR
               MOVE SPACES          TO ADR-REQA
               PERFORM PRO-ERR
               GO TO FIN-INI.

           ADD 1               TO CONT-D03-LEIDOS.
           MOVE GAR-ID-SUP-DDR TO CLAVE-ANT CLAVE-SIG.               

           MOVE '2'    TO D01-TIPO-REG.                                 
           MOVE SPACES TO D01-RUT-D.                                    
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-D01.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR
               MOVE 'D01    NEX'    TO ERR-COD-MENS IN ERR
               MOVE SPACES          TO ERR-COD-CMPO IN ERR
               MOVE SPACES          TO ADR-REQA
               PERFORM PRO-ERR
               GO TO FIN-INI.

           PERFORM LEE-SEC-D01.                                         
           IF (D01-TIPO-REG NOT = '2') OR (NOT FIO-STAT-OKS)
               MOVE FIO-STAT-NEX TO FIO-STAT
               MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR
               MOVE 'REG TIPO 2 NEX' TO ERR-COD-MENS IN ERR
               MOVE SPACES           TO ERR-COD-CMPO IN ERR
               MOVE SPACES           TO ADR-REQA
           ELSE
               MOVE ERR-SVRT-REC IN ERR-VARI TO ERR-IND-SVRT IN ERR.
       FIN-INI.
           EXIT.

                                                                        
       PROCESO-PRINC SECTION.                                           
      *----------------------                                           
       INI-PROCESO-PRINC.                                               
                                                                        
           IF CLAVE-ANT = GAR-ID-SUP-DDR                                
              PERFORM VEO-SI-LEE-D01                                    
              PERFORM SUMA-POR-TIPO                                     
              MOVE FIO-GET-NXT TO FIO-CMND
              PERFORM GAR-FIO-D03
              IF NOT FIO-STAT-OKS
                 MOVE '..........' TO CLAVE-SIG                         
                 PERFORM MOVER-SUMA-TOTAL                               
              ELSE                                                      
                 MOVE GAR-ID-SUP-DDR TO CLAVE-SIG                       
           ELSE                                                         
              PERFORM MOVER-SUMA-TOTAL.                                 
       FIN-PROCESO-PRINC.                                               
           EXIT.                                                        
                                                                        
       VEO-SI-LEE-D01 SECTION.                                          
      *-----------------------                                          
       INI-VEO-SI-LEE-D01.
                                                                        
           IF PRIM-LECT = 0                                             
               IF D01-RUT-D < GAR-ID-SUP-DDR                                
                   PERFORM LEE-SEC-D01 UNTIL
                      (D01-RUT-D NOT < GAR-ID-SUP-DDR) OR
                      (NOT FIO-STAT-OKS)
                   MOVE 1 TO PRIM-LECT.                                       
                                                                        
       FIN-VEO-SI-LEE-D01.
           EXIT.                                                        
                                                                        
                                                                        
       LEE-SEC-D01 SECTION.                                             
      *--------------------                                             
       INI-LEE-SEC-D01.                                                 
                                                                        
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-D01.
                                                                        
       FIN-LEE-SEC-D01.                                                 
           EXIT.                                                        
                                                                        
                                                                        
       SUMA-POR-TIPO SECTION.                                           
      *----------------------                                           
       INI-SUMAR-POR-TIPO.                                              
                                                                        
           IF GAR-TIP-REA-NRE = 'R'                                     
              COMPUTE SUMA-TOTAL-REA = SUMA-TOTAL-REA + GAR-VAL         
           ELSE                                                         
              COMPUTE SUMA-TOTAL-NRE = SUMA-TOTAL-NRE + GAR-VAL.        
                                                                        
       FIN-SUMAR-POR-TIPO.                                              
           EXIT.                                                        
                                                                        
                                                                        
       MOVER-SUMA-TOTAL SECTION.                                        
      *-------------------------                                        
       INI-MOVER-SUMA-TOTAL.                                            
                                                                        
           MOVE CLAVE-ANT TO GAR-CLV-D03-1.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-D03.
           IF NOT FIO-STAT-OKS
              DISPLAY 'C-ANT.: ' CLAVE-ANT                              
              DISPLAY 'C-SIG.: ' CLAVE-SIG                              
              DISPLAY 'ID-DDR: ' GAR-ID-SUP-DDR                         
              DISPLAY 'R. COD: ' FIO-STAT
              DISPLAY '****   E R R O R   G R A V E ....*****'          
              DISPLAY '****   A B O R T A           ....*****'          
               MOVE 'D03    NEX'    TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-D03-1' TO ERR-COD-CMPO IN ERR
               MOVE ADR-D03-REQA    TO ADR-REQA
               MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR
               PERFORM PRO-ERR.

           PERFORM MOVER-TOT-REG UNTIL (CLAVE-ANT NOT = GAR-ID-SUP-DDR) 
                   OR NOT FIO-STAT-OKS.
           MOVE 0 TO SUMA-TOTAL-REA SUMA-TOTAL-NRE.                     
           MOVE CLAVE-SIG TO CLAVE-ANT GAR-CLV-D03-1.                       
           MOVE 0 TO PRIM-LECT.                                         
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-D03.
           IF FIO-STAT-OKS
              ADD 1       TO CONT-D03-LEIDOS.                           
       FIN-MOVER-SUMA-TOTAL.                                            
           EXIT.                                                        
                                                                        
       MOVER-TOT-REG SECTION.                                           
      *----------------------                                           
       INI-MOVER-TOT-REG.                                               
                                                                        
           MOVE SUMA-TOTAL-REA TO GAR-VAL-GAR-REA.                      
           MOVE SUMA-TOTAL-NRE TO GAR-VAL-GAR-NRE.                      
           IF D01-RUT-D = GAR-ID-SUP-DDR                                
           THEN  MOVE D01-IND-MAYOR-DEUDOR TO GAR-IND-MAY-DDR.          
      ******  IF GAR-IND-MAY-DDR = ' '                                  
      ******     MOVE D01-EXCEDIDO TO GAR-IND-MAY-DDR.                  

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-D03.
           IF NOT FIO-STAT-OKS
              DISPLAY 'D03-RET-COD: ' FIO-STAT
              DISPLAY 'D03-CLAVE : ' GAR-CLV-D03-1 ' C-ANT: ' CLAVE-ANT     
               MOVE 'D03    MOD'    TO ERR-COD-MENS IN ERR
               MOVE 'GAR-CLV-D03-1' TO ERR-COD-CMPO IN ERR
               MOVE ADR-D03-REQA    TO ADR-REQA
               MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR
               PERFORM PRO-ERR.

           ADD 1             TO CONT-D03-GRABADOS.                    
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-D03.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-D03.
       FIN-MOVER-TOT-REG.                                               
           EXIT.                                                        
                                                                        
       COPY GARBFD03.
       COPY GARBFD01.

       COPY GNSBFTAB.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
      *COPY GNSBFERR.
       COPY GNSBGERR REPLACING ==FIO-STAT IN FIO-VARI== BY ==FIO-STAT==.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBGHOY.
       COPY GNSBGSYS.

