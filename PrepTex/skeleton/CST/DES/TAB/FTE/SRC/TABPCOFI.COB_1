       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      TABPCOFI.
       AUTHOR.          JULIO SEPULVEDA.

      * Programa carga Usuarios de Tablas

       ENVIRONMENT DIVISION.
      *====================

       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGIFD.
      *==============
       COPY TABBROFI.
      *    Registro de Oficinas
      *    --------------------
      * 
      *    Nombre Registro : OFI
      *    Clave(s)        : OFI-COD-OFIC(UU), OFI-STP-ITRN(UU),
      *                      OFI-CIC-OFIC(UU), OFI-EXT-TABL(UU)
      *    Largo           : 297
      *    Bloqueo         : 3
      *    Observaciones   : 
      *
       01  OFI-NEW.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OFI-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  OFI-KEY-IREG.
      *
      *KEY     Codigo Oficina ( LLave Primaria )
               05  OFI-COD-OFIC                         PIC X(03).
      *
      *        Time Stamp Transaccion
               05  OFI-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  OFI-FEC-FTRN.
                       09  OFI-NUM-STRN                 PIC 9(02).
                       09  OFI-NUM-ATRN                 PIC 9(02).
                       09  OFI-NUM-MTRN                 PIC 9(02).
                       09  OFI-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  OFI-HRA-HRTR.
                       09  OFI-NUM-HHTR                 PIC 9(02).
                       09  OFI-NUM-MMTR                 PIC 9(02).
                       09  OFI-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  OFI-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  OFI-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  OFI-IND-VIGE  REDEFINES  OFI-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  OFI-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  OFI-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  OFI-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  OFI-COD-ATRN                             PIC X(12).
      *
      *KEY Codigo Oficina ( Interno Computacional )
           03  OFI-CIC-OFIC                             PIC X(03).
      *
      *    Codigo Oficina Externo
           03  OFI-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  OFI-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  OFI-EXT-CTAB                         PIC X(05).
      *
      *    Abreviacion Oficina
           03  OFI-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion Oficina
           03  OFI-GLS-DESC                             PIC X(38).
      *
      *    Direccion Oficina
           03  OFI-GLS-DIRE                             PIC X(38).
      * 
      *    Codigo Comuna Oficina
           03  OFI-COD-COMU                             PIC X(04).
      *    
      *    Telex Oficina
           03  OFI-GLS-TLEX                             PIC X(38).
      *    
      *    Codigo Plaza Oficina
           03  OFI-COD-PLZA                             PIC X(04).
      *    
      *    Fonos Oficina
           03  OFI-GLS-FONO.
      *    
      *        Fono 1 Oficina
               05  OFI-GLS-FON1                         PIC X(10).
      *    
      *        Fono 2 Oficina
               05  OFI-GLS-FON2                         PIC X(10).
      *    
      *    Anexos Oficina
           03  OFI-GLS-ANXO.
      *    
      *        Anexo 1 Oficina
               05  OFI-GLS-ANX1                         PIC X(04).
      *    
      *        Anexo 2 Oficina
               05  OFI-GLS-ANX2                         PIC X(04).
      *    
      *    Oficina Regional
           03  OFI-COD-OREG                             PIC X(03).
      *    
      *    Oficina Centralizacion Operativa
           03  OFI-COD-OFCO                             PIC X(03).
      *    
      *    Clasificacion Operativa Oficina
           03  OFI-IND-COPE                             PIC X(01).
      *    
      *    Clasificacion Comercial Oficina
           03  OFI-IND-CCOM                             PIC X(01).
      *    
      *    Cuenta Contable Oficina
           03  OFI-NUM-CCON                             PIC X(12).
      *    
      *    Digito Verificador Cuenta Contable
           03  OFI-VRF-CCON                             PIC X(01).
      *    
      *    Cuenta Corriente Oficina
           03  OFI-NUM-CCTE                             PIC X(12).
      *    
      *    Digito Verificador Cuenta Corriente
           03  OFI-VRF-CCTE                             PIC X(01).
      *    
      *    Codigo Jefe Oficina
           03  OFI-COD-JFOF                             PIC X(12).
      *    
      *    Fecha desde Jefe Oficina (SSAAMMDD)
           03  OFI-FEC-FJFO.
               05  OFI-NUM-SJFO                         PIC 9(02).
               05  OFI-NUM-AJFO                         PIC 9(02).
               05  OFI-NUM-MJFO                         PIC 9(02).
               05  OFI-NUM-DJFO                         PIC 9(02).
      *    
      *    Codigo Jefe Operaciones Oficina
           03  OFI-COD-JOOF                             PIC X(12).
      *    
      *    Fecha desde Jefe Operaciones Oficina (SSAAMMDD)
           03  OFI-FEC-FJOO.
               05  OFI-NUM-SJOO                         PIC 9(02).
               05  OFI-NUM-AJOO                         PIC 9(02).
               05  OFI-NUM-MJOO                         PIC 9(02).
               05  OFI-NUM-DJOO                         PIC 9(02).
      *    
      *    Fecha Apertura Oficina (SSAAMMDD)
           03  OFI-FEC-FAPE.
               05  OFI-NUM-SAPE                         PIC 9(02).
               05  OFI-NUM-AAPE                         PIC 9(02).
               05  OFI-NUM-MAPE                         PIC 9(02).
               05  OFI-NUM-DAPE                         PIC 9(02).
      *    
      *    Fecha Cierre Oficina (SSAAMMDD)
           03  OFI-FEC-FCIE.
               05  OFI-NUM-SCIE                         PIC 9(02).
               05  OFI-NUM-ACIE                         PIC 9(02).
               05  OFI-NUM-MCIE                         PIC 9(02).
               05  OFI-NUM-DCIE                         PIC 9(02).
      *    
      *    Disponible
           03  OFI-GLS-DISP                             PIC X(05).

      *==============

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-NEW-REQA==.

      *========================================================

        01  WSS-VARI.
            03 WSS-NDBI-OLD                      PIC 9(03).
            03 WSS-NDBI-NEW                      PIC 9(03).
            03 WSS-SIST                          PIC X(03).
            03 WSS-RPRO                  VALUE 0 PIC 9(05).
            03 WSS-RMAL                  VALUE 0 PIC 9(05).

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           PERFORM INI.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
       LUP-MAIN.
           IF NOT FIO-STAT-OKS
               GO TO MSG-MAIN.
           PERFORM PRO-OFI.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
           GO TO LUP-MAIN.
       MSG-MAIN.
           DISPLAY 'REGISTROS PROCESADOS  : ' WSS-RPRO.
           DISPLAY 'REGISTROS RECHAZADOS  : ' WSS-RMAL.
       FIN-MAIN.
           STOP RUN.

       INI SECTION.
       INI-INI.
           MOVE 'TABPCOFI' TO FIO-PROG.
           PERFORM BUS-FSIS.

           DISPLAY 'INGRESE SISTEMA : '.
           ACCEPT FIO-SIST.
           DISPLAY 'SISTEMA : ' FIO-SIST.

           DISPLAY 'INGRESE NRO DE BASE DE DATOS ANTIGUA : '.
           ACCEPT WSS-NDBI-OLD.
           DISPLAY 'NRO DE BASE DE DATOS ANTIGUA : ' WSS-NDBI-OLD.

           DISPLAY 'INGRESE NRO DE BASE DE DATOS NUEVA   : '.
           ACCEPT WSS-NDBI-NEW.
           DISPLAY 'NRO DE BASE DE DATOS NUEVA   : ' WSS-NDBI-NEW. 
       FIN-INI.
           EXIT.

       PRO-OFI SECTION.
       INI-PRO-OFI.
           MOVE SPACES              TO OFI-GLS-FLAG IN OFI-NEW.
           MOVE OFI-COD-OFIC IN OFI TO OFI-COD-OFIC IN OFI-NEW.
           MOVE OFI-FEC-FUMO IN OFI TO OFI-FEC-FTRN IN OFI-NEW.
           MOVE ZEROES              TO OFI-HRA-HRTR IN OFI-NEW.
           MOVE SPACES              TO OFI-MSC-TACC IN OFI-NEW.
           MOVE 'S'                 TO OFI-IND-VIGE IN OFI-NEW.
           MOVE SPACES              TO OFI-MSC-TTRN IN OFI-NEW.
           MOVE SPACES              TO OFI-COD-TTRN IN OFI-NEW.
           MOVE SPACES              TO OFI-COD-ATRN IN OFI-NEW.
           MOVE OFI-CIC-OFIC IN OFI TO OFI-CIC-OFIC IN OFI-NEW.
           MOVE 'OFI'               TO OFI-EXT-TTAB IN OFI-NEW.
           MOVE OFI-GLS-CEXT IN OFI TO OFI-EXT-CTAB IN OFI-NEW.
           MOVE OFI-GLS-ABRV IN OFI TO OFI-GLS-ABRV IN OFI-NEW.
           MOVE OFI-GLS-DESC IN OFI TO OFI-GLS-DESC IN OFI-NEW.
      *
           MOVE OFI-GLS-DIRE IN OFI TO OFI-GLS-DIRE IN OFI-NEW.
           MOVE OFI-COD-COMU IN OFI TO OFI-COD-COMU IN OFI-NEW.
           MOVE OFI-GLS-TLEX IN OFI TO OFI-GLS-TLEX IN OFI-NEW.
           MOVE OFI-COD-PLZA IN OFI TO OFI-COD-PLZA IN OFI-NEW.
           MOVE OFI-GLS-FON1 IN OFI TO OFI-GLS-FON1 IN OFI-NEW.
           MOVE OFI-GLS-FON2 IN OFI TO OFI-GLS-FON2 IN OFI-NEW.
           MOVE OFI-GLS-ANX1 IN OFI TO OFI-GLS-ANX1 IN OFI-NEW.
           MOVE OFI-GLS-ANX2 IN OFI TO OFI-GLS-ANX2 IN OFI-NEW.
           MOVE OFI-COD-OREG IN OFI TO OFI-COD-OREG IN OFI-NEW.
           MOVE OFI-COD-OFCO IN OFI TO OFI-COD-OFCO IN OFI-NEW.
           MOVE OFI-IND-COPE IN OFI TO OFI-IND-COPE IN OFI-NEW.
           MOVE OFI-IND-CCOM IN OFI TO OFI-IND-CCOM IN OFI-NEW.
           MOVE OFI-COD-CCON IN OFI TO OFI-NUM-CCON IN OFI-NEW.
           MOVE OFI-VRF-CCON IN OFI TO OFI-VRF-CCON IN OFI-NEW.
           MOVE OFI-COD-CCTE IN OFI TO OFI-NUM-CCTE IN OFI-NEW.
           MOVE OFI-VRF-CCTE IN OFI TO OFI-VRF-CCTE IN OFI-NEW.
           MOVE OFI-COD-JFOF IN OFI TO OFI-COD-JFOF IN OFI-NEW.
           MOVE OFI-FEC-FJFO IN OFI TO OFI-FEC-FJFO IN OFI-NEW.
           MOVE OFI-COD-JOOF IN OFI TO OFI-COD-JOOF IN OFI-NEW.
           MOVE OFI-FEC-FJOO IN OFI TO OFI-FEC-FJOO IN OFI-NEW.
           MOVE OFI-FEC-FAPE IN OFI TO OFI-FEC-FAPE IN OFI-NEW.
           MOVE OFI-FEC-FCIE IN OFI TO OFI-FEC-FCIE IN OFI-NEW.
           MOVE SPACES              TO OFI-GLS-DISP IN OFI-NEW.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM TAB-FIO-OFI-NEW.
           ADD 1 TO WSS-RPRO.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-RMAL
               DISPLAY 'FIO-STAT : ' FIO-STAT ', FIO-MENS : ' FIO-MENS
               DISPLAY 'OFI     : ' OFI
               DISPLAY 'OFI-NEW : ' OFI-NEW
               DISPLAY '----------------------------------------------'.
       FIN-PRO-OFI.
           EXIT.

      *==============
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.

      * Modulo FIO para registro OFI en DATACOM
       TAB-FIO-OFI-NEW SECTION.
       INI-TAB-FIO-OFI-NEW.
           MOVE ADR-OFI-REQA TO ADR-REQA.
           MOVE 'OFI' TO ADR-TABL IN ADR-REQA.
      *JSS MOVE IFD-GSIS( 2 ) TO ADR-DBID IN ADR-REQA.
           MOVE WSS-NDBI-NEW TO ADR-DBID IN ADR-REQA.
      *
           MOVE 'OFI00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OFI-COD-OFIC' OR FIO-AKEY NOT > SPACES )
               MOVE OFI-COD-OFIC IN OFI-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'OFI00' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'OFI-STP-ITRN'
               MOVE OFI-STP-ITRN IN OFI-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'OFI01' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'OFI-CIC-OFIC'
               MOVE OFI-CIC-OFIC IN OFI-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'OFI02' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'OFI-EXT-TABL'
               MOVE OFI-EXT-TABL IN OFI-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'OFI03' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En TAB-FIO-OFI-NEW, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
           MOVE OFI-NEW TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OFI-NEW
           ELSE
               MOVE 'TABOFI' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE ADR-REQA TO ADR-OFI-REQA.
       FIN-TAB-FIO-OFI-NEW.
           EXIT.

      * Modulo FIO para registro OFI en DATACOM
       TAB-FIO-OFI SECTION.
       INI-TAB-FIO-OFI.
           MOVE ADR-OFI-REQA TO ADR-REQA.
           MOVE 'OFI' TO ADR-TABL IN ADR-REQA.
      *JSS MOVE IFD-GSIS( 2 ) TO ADR-DBID IN ADR-REQA.
           MOVE WSS-NDBI-OLD TO ADR-DBID IN ADR-REQA.
      *
           MOVE 'OFI00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON
               MOVE OFI-COD-OFIC IN OFI TO ADR-VKEY IN ADR-REQA
               MOVE 'OFI00' TO ADR-DKEY IN ADR-REQA
           ELSE
               STRING 'Nombre Llave : ' FIO-AKEY
                      ' invalida en TAB-FIO-OFI'
                      DELIMITED BY SIZE INTO FIO-MEN1
               MOVE SPACES TO FIO-MEN2
               GO TO PRG-ABT.
           MOVE OFI TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OFI
           ELSE
               MOVE 'TABOFI' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE ADR-REQA TO ADR-OFI-REQA.
       FIN-TAB-FIO-OFI.
           EXIT.
