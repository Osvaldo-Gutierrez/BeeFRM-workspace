       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      TABPCUSR.
       AUTHOR.          JULIO SEPULVEDA.

      * Programa carga Usuarios de Tablas

       ENVIRONMENT DIVISION.
      *====================

       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGIFD.
      *==============
       COPY TABBRUSR.
      *    Registro de Usuarios
      *    --------------------
      * 
      *    Nombre Registro : USR
      *    Clave(s)        : USR-COD-TABL(UU), USR-COD-USER(UU), 
      *                      USR-STP-ITRN(UU), USR-CIC-TABL(UU), 
      *                      USR-EXT-TABL(UU), USR-GLS-ABRV(DC), 
      *                      USR-IDC-IUSR(UU) 
      *    Largo           : 194
      *    Bloqueo         : 2
      *    Observaciones   : 
      *
       01  USR-NEW. 
      *
      *    Filler Compatibilidad VSAM/MVS
           03  USR-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  USR-KEY-IREG.
      *
      *KEY     Tipo + Codigo ( LLave Primaria )
               05  USR-COD-TABL.
      *
      *            Codigo Tipo Tabla
                   07  USR-COD-TTAB                     PIC X(03).
      *
      *            Codigo Usuario
                   07  USR-COD-USER                     PIC X(12).
      *
      *        Time Stamp Transaccion
               05  USR-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  USR-FEC-FTRN.
                       09  USR-NUM-STRN                 PIC 9(02).
                       09  USR-NUM-ATRN                 PIC 9(02).
                       09  USR-NUM-MTRN                 PIC 9(02).
                       09  USR-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  USR-HRA-HRTR.
                       09  USR-NUM-HHTR                 PIC 9(02).
                       09  USR-NUM-MMTR                 PIC 9(02).
                       09  USR-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  USR-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  USR-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  USR-IND-VIGE  REDEFINES  USR-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  USR-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  USR-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  USR-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  USR-COD-ATRN                             PIC X(12).
      *
      *KEY Tipo + Codigo ( Interno Computacional )
           03  USR-CIC-TABL.
      *
      *        Codigo Tipo Tabla
               05  USR-CIC-TTAB                         PIC X(03).
      *
      *        Codigo Usuario
               05  USR-CIC-USER                         PIC X(12).
      *
      *    Codigo Externo
           03  USR-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  USR-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  USR-EXT-CTAB                         PIC X(05).
      *
      *    Descripcion Corta
           03  USR-GLS-DCOR                             PIC X(12).
      *
      *    Abreviacion Descripcion
           03  USR-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion del Codigo
           03  USR-GLS-DESC                             PIC X(40).
      *
      *    Identificador Usuario 
           03  USR-IDC-IUSR.
      *
      *        Rut Usuario
               05  USR-NUM-IUSR                         PIC X(08).
      *
      *        Indicador Rut Usuario
               05  USR-IND-IUSR                         PIC X(01).
      *
      *        Glosa Rut Usuario
               05  USR-GLS-IUSR                         PIC X(03).
      *
      *    Digito Verificador Rut Usuario
           03  USR-VRF-IUSR                             PIC X(01).
      *
      *    Ubicacion Usuario
           03  USR-COD-UBIC.
      *
      *        Tipo Ubicacion
               05  USR-IND-UBIC                         PIC X(01).
      *
      *        Codigo Ubicacion
               05  USR-COD-OFIC                         PIC X(03).
      *
      *    Categoria Usuario
           03  USR-COD-CUSR                             PIC X(08).
      *
      *    Disponible
           03  USR-GLS-DISP                             PIC X(35).
      *
      *    Clave Soundex
           03  USR-SNX-TABL.
               05  USR-SNX-TTAB                         PIC X(03).
               05  USR-SNX-USER                         PIC X(03).


      *==============

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-NEW-REQA==.

      *========================================================

        01  WSS-VARI.
            03 WSS-NDBI-OLD                      PIC 9(03).
            03 WSS-NDBI-NEW                      PIC 9(03).
            03 WSS-SIST                          PIC X(03).
            03 WSS-RPRO                  VALUE 0 PIC 9(05).
            03 WSS-RMAL                  VALUE 0 PIC 9(05).

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           PERFORM INI.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM TAB-FIO-USR.
       LUP-MAIN.
           IF NOT FIO-STAT-OKS
               GO TO MSG-MAIN.
           PERFORM PRO-USR
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM TAB-FIO-USR.
           GO TO LUP-MAIN.
       MSG-MAIN.
           DISPLAY 'REGISTROS PROCESADOS  : ' WSS-RPRO.
           DISPLAY 'REGISTROS RECHAZADOS  : ' WSS-RMAL.
       FIN-MAIN.
           STOP RUN.

       INI SECTION.
       INI-INI.
           MOVE 'TABPCUSR' TO FIO-PROG.
           PERFORM BUS-FSIS.

           DISPLAY 'INGRESE SISTEMA : '.
           ACCEPT FIO-SIST.
           DISPLAY 'SISTEMA : ' FIO-SIST.

           DISPLAY 'INGRESE NRO DE BASE DE DATOS ANTIGUA : '.
           ACCEPT WSS-NDBI-OLD.
           DISPLAY 'NRO DE BASE DE DATOS ANTIGUA : ' WSS-NDBI-OLD.

           DISPLAY 'INGRESE NRO DE BASE DE DATOS NUEVA   : '.
           ACCEPT WSS-NDBI-NEW.
           DISPLAY 'NRO DE BASE DE DATOS NUEVA   : ' WSS-NDBI-NEW. 
       FIN-INI.
           EXIT.

       PRO-USR SECTION.
       INI-PRO-USR.
      *JSS MOVE TAB TO USR.
           MOVE SPACES              TO USR-GLS-FLAG IN USR-NEW.
           MOVE USR-COD-TABL IN USR TO USR-COD-TABL IN USR-NEW.
           MOVE USR-FEC-FUMO IN USR TO USR-FEC-FTRN IN USR-NEW.
           MOVE ZEROES              TO USR-HRA-HRTR IN USR-NEW.
           MOVE SPACES              TO USR-MSC-TACC IN USR-NEW.
           MOVE USR-IND-VIGE IN USR TO USR-IND-VIGE IN USR-NEW.
           MOVE SPACES              TO USR-MSC-TTRN IN USR-NEW.
           MOVE SPACES              TO USR-COD-TTRN IN USR-NEW.
           MOVE USR-COD-RESP IN USR TO USR-COD-ATRN IN USR-NEW.
           MOVE USR-CIC-TABL IN USR TO USR-CIC-TABL IN USR-NEW.
           MOVE USR-GLS-DESC IN USR TO USR-GLS-DCOR IN USR-NEW.
           MOVE USR-GLS-ABRV IN USR TO USR-GLS-ABRV IN USR-NEW.
           MOVE USR-GLS-DESC IN USR TO USR-GLS-DESC IN USR-NEW.
      *    MOVE SPACES              TO USR-GLS-DISP IN USR-NEW.
           MOVE USR-SNX-TABL IN USR TO USR-SNX-TABL IN USR-NEW.
           MOVE USR-COD-RUTU IN USR TO USR-NUM-IUSR IN USR-NEW.
           MOVE USR-IND-RUTU IN USR TO USR-IND-IUSR IN USR-NEW.
           MOVE USR-GLS-RUTU IN USR TO USR-GLS-IUSR IN USR-NEW.
           MOVE USR-VRF-RUTU IN USR TO USR-VRF-IUSR IN USR-NEW.
           MOVE USR-IND-UBIC IN USR TO USR-IND-UBIC IN USR-NEW.
           MOVE USR-COD-OFIC IN USR TO USR-COD-OFIC IN USR-NEW.
           MOVE USR-COD-TUSR IN USR TO USR-COD-TTAB IN USR-NEW.
           MOVE USR-COD-CUSR IN USR TO USR-COD-CUSR IN USR-NEW.
           MOVE SPACES              TO USR-GLS-DISP IN USR-NEW.
           MOVE SPACES              TO USR-EXT-CTAB IN USR-NEW.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM TAB-FIO-USR-NEW.
           ADD 1 TO WSS-RPRO.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-RMAL
               DISPLAY 'FIO-STAT : ' FIO-STAT ', FIO-MENS : ' FIO-MENS
               DISPLAY 'USR     : ' USR
               DISPLAY 'USR-NEW : ' USR-NEW
               DISPLAY '----------------------------------------------'.
       FIN-PRO-USR.
           EXIT.

      *==============
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.

      * Modulo FIO para registro USR en DATACOM
       TAB-FIO-USR SECTION.
       INI-TAB-FIO-USR.
      *JSS
           MOVE 'USR' TO USR-COD-TTAB IN USR.
      *
           MOVE ADR-USR-REQA TO ADR-REQA.
           MOVE 'USR' TO ADR-TABL IN ADR-REQA.
      *JSS MOVE IFD-GSIS( 2 ) TO ADR-DBID IN ADR-REQA.
           MOVE WSS-NDBI-OLD TO ADR-DBID IN ADR-REQA.
      *
           MOVE 'USR00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON
               MOVE USR-COD-TABL IN USR TO ADR-VKEY IN ADR-REQA
               MOVE 'USR00' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-GLS-ABRV'
               MOVE USR-GLS-ABRV IN USR TO ADR-VKEY IN ADR-REQA
               MOVE 'USR01' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-IDC-USER'
               MOVE USR-IDC-USER IN USR TO ADR-VKEY IN ADR-REQA
               MOVE 'USR02' TO ADR-DKEY IN ADR-REQA
           ELSE
               STRING 'Nombre Llave : ' FIO-AKEY
                      ' invalida en TAB-FIO-USR'
                      DELIMITED BY SIZE INTO FIO-MEN1
               MOVE SPACES TO FIO-MEN2
               GO TO PRG-ABT.
      *JSS
           IF FIO-CMND = FIO-PUT OR FIO-MOD
               MOVE USR-COD-TTAB IN USR TO USR-CIC-TTAB IN USR
                                           USR-SNX-TTAB IN USR.
      *
           MOVE USR TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO USR
           ELSE
               MOVE 'TABUSR' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE ADR-REQA TO ADR-USR-REQA.
       FIN-TAB-FIO-USR.
           EXIT.


      * Modulo FIO para registro USR en DATACOM
       TAB-FIO-USR-NEW SECTION.
       INI-TAB-FIO-USR-NEW.
           MOVE ADR-USR-NEW-REQA TO ADR-REQA.
           MOVE 'USR' TO ADR-TABL IN ADR-REQA.
      *JSS MOVE IFD-GSIS( 2 ) TO ADR-DBID IN ADR-REQA.
           MOVE WSS-NDBI-NEW  TO ADR-DBID IN ADR-REQA.
      *
           MOVE 'USR00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'USR-COD-TABL' OR FIO-AKEY NOT > SPACES )
               MOVE USR-COD-TABL IN USR-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'USR00' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-COD-USER'
               MOVE USR-COD-USER IN USR-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'USR01' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-STP-ITRN'
               MOVE USR-STP-ITRN IN USR-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'USR02' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-CIC-TABL'
               MOVE USR-CIC-TABL IN USR-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'USR03' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-EXT-TABL'
               MOVE USR-EXT-TABL IN USR-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'USR04' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-GLS-ABRV'
               MOVE USR-GLS-ABRV IN USR-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'USR05' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'USR-IDC-IUSR'
               MOVE USR-IDC-IUSR IN USR-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'USR06' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En TAB-FIO-USR-NEW, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
      *JSS
           IF FIO-CMND = FIO-PUT OR FIO-MOD
               MOVE USR-COD-TTAB IN USR-NEW TO USR-CIC-TTAB IN USR-NEW
                                           USR-EXT-TTAB IN USR-NEW
                                           USR-SNX-TTAB IN USR-NEW.
      *
      *JSS MOVE USR TO FIO-DFLD.
           MOVE USR-NEW TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
      *JSS     MOVE FIO-DFLD TO USR
               MOVE FIO-DFLD TO USR-NEW
           ELSE
               MOVE 'TABUSR' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE ADR-REQA TO ADR-USR-NEW-REQA.
       FIN-TAB-FIO-USR-NEW.
           EXIT.
