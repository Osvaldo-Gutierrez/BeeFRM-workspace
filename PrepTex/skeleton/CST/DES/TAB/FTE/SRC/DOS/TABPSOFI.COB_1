      *$ASCII
       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      TABPSOFI.
       AUTHOR.          CONSIST.
      * 
      *Traspasa una tabla del Archivo Usuario a un archivo secuencial
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
           SELECT TABOFS ASSIGN TO DA-S-TABOFS
                  ORGANIZATION   IS SEQUENTIAL
                  ACCESS MODE    IS SEQUENTIAL
                  FILE STATUS    IS FIO-STAT.
      *
       DATA DIVISION.
      *=============
       FILE SECTION.
       FD TABOFS 
           LABEL RECORDS STANDARD
           RECORD CONTAINS 297.
      *
      *    Registro de Oficinas
      *    --------------------
      * 
      *    Nombre Registro : OFI
      *    Clave(s)        : OFI-COD-OFIC(UU), OFI-CIC-OFIC(UU),
      *                      OFI-EXT-TABL(DC)
      *    Largo           : 297
      *    Bloqueo         : 3
      *    Observaciones   : 
      *
       01  OFS.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OFI-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  OFI-KEY-IREG.
      *
      *KEY     Codigo Oficina ( LLave Primaria )
               05  OFI-COD-OFIC                         PIC X(03).
      *
      *        Time Stamp Transaccion
               05  OFI-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  OFI-FEC-FTRN.
                       09  OFI-NUM-STRN                 PIC 9(02).
                       09  OFI-NUM-ATRN                 PIC 9(02).
                       09  OFI-NUM-MTRN                 PIC 9(02).
                       09  OFI-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  OFI-HRA-HRTR.
                       09  OFI-NUM-HHTR                 PIC 9(02).
                       09  OFI-NUM-MMTR                 PIC 9(02).
                       09  OFI-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  OFI-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  OFI-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  OFI-IND-VIGE  REDEFINES  OFI-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  OFI-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  OFI-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  OFI-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  OFI-COD-ATRN                             PIC X(12).
      *
      *KEY Codigo Oficina ( Interno Computacional )
           03  OFI-CIC-OFIC                             PIC X(03).
      *
      *    Codigo Oficina Externo
           03  OFI-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  OFI-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  OFI-EXT-CTAB                         PIC X(05).
      *
      *    Abreviacion Oficina
           03  OFI-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion Oficina
           03  OFI-GLS-DESC                             PIC X(38).
      *
      *    Direccion Oficina
           03  OFI-GLS-DIRE                             PIC X(38).
      * 
      *    Codigo Comuna Oficina                        TAB COM
           03  OFI-COD-COMU                             PIC X(04).
      *    
      *    Telex Oficina
           03  OFI-GLS-TLEX                             PIC X(38).
      *    
      *    Codigo Plaza Oficina                         TAB PZA
           03  OFI-COD-PLZA                             PIC X(04).
      *    
      *    Fonos Oficina
           03  OFI-GLS-FONO.
      *    
      *        Fono 1 Oficina
               05  OFI-GLS-FON1                         PIC X(10).
      *    
      *        Fono 2 Oficina
               05  OFI-GLS-FON2                         PIC X(10).
      *    
      *    Anexos Oficina
           03  OFI-GLS-ANXO.
      *    
      *        Anexo 1 Oficina
               05  OFI-GLS-ANX1                         PIC X(04).
      *    
      *        Anexo 2 Oficina
               05  OFI-GLS-ANX2                         PIC X(04).
      *    
      *    Oficina Regional                             TAB OFI
           03  OFI-COD-OREG                             PIC X(03).
      *    
      *    Oficina Centralizacion Operativa             TAB OFI
           03  OFI-COD-OFCO                             PIC X(03).
      *    
      *    Clasificacion Operativa Oficina              TAB COPE
           03  OFI-IND-COPE                             PIC X(01).
      *    
      *    Clasificacion Comercial Oficina              TAB CCOM
           03  OFI-IND-CCOM                             PIC X(01).
      *    
      *    Cuenta Contable Oficina
           03  OFI-NUM-CCON                             PIC X(12).
      *    
      *    Digito Verificador Cuenta Contable
           03  OFI-VRF-CCON                             PIC X(01).
      *    
      *    Cuenta Corriente Oficina
           03  OFI-NUM-CCTE                             PIC X(12).
      *    
      *    Digito Verificador Cuenta Corriente
           03  OFI-VRF-CCTE                             PIC X(01).
      *    
      *    Codigo Jefe Oficina                          TAB USR
           03  OFI-COD-JFOF                             PIC X(12).
      *    
      *    Fecha desde Jefe Oficina (SSAAMMDD)
           03  OFI-FEC-FJFO.
               05  OFI-NUM-SJFO                         PIC 9(02).
               05  OFI-NUM-AJFO                         PIC 9(02).
               05  OFI-NUM-MJFO                         PIC 9(02).
               05  OFI-NUM-DJFO                         PIC 9(02).
      *    
      *    Codigo Jefe Operaciones Oficina              TAB USR
           03  OFI-COD-JOOF                             PIC X(12).
      *    
      *    Fecha desde Jefe Operaciones Oficina (SSAAMMDD)
           03  OFI-FEC-FJOO.
               05  OFI-NUM-SJOO                         PIC 9(02).
               05  OFI-NUM-AJOO                         PIC 9(02).
               05  OFI-NUM-MJOO                         PIC 9(02).
               05  OFI-NUM-DJOO                         PIC 9(02).
      *    
      *    Fecha Apertura Oficina (SSAAMMDD)
           03  OFI-FEC-FAPE.
               05  OFI-NUM-SAPE                         PIC 9(02).
               05  OFI-NUM-AAPE                         PIC 9(02).
               05  OFI-NUM-MAPE                         PIC 9(02).
               05  OFI-NUM-DAPE                         PIC 9(02).
      *    
      *    Fecha Cierre Oficina (SSAAMMDD)
           03  OFI-FEC-FCIE.
               05  OFI-NUM-SCIE                         PIC 9(02).
               05  OFI-NUM-ACIE                         PIC 9(02).
               05  OFI-NUM-MCIE                         PIC 9(02).
               05  OFI-NUM-DCIE                         PIC 9(02).
      *    
      *    Disponible
           03  OFI-GLS-DISP                             PIC X(05).

      *
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS
      * WORKING PARA RUTINAS.
       COPY GNSWGSYS.
      * COPY GNSWGFEC.
      * COPY GNSWGHOY.
       COPY GNSWCFIO.
       COPY GNSWVFIO.

       COPY TABBROFI.

       01  WSS-VARI.
           03 WSS-ROFI.
              05 WSS-CHR-ROFI  OCCURS 297   PIC X(01).
           03  WSS-I           VALUE ZEROES PIC 9(06).
           03  WSS-J           VALUE ZEROES PIC 9(03).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           PERFORM OPN-FILE.
           PERFORM PRO-FILE.
           PERFORM CLO-FILE.
           STOP RUN.
       FIN-MAIN.
           EXIT.

       OPN-FILE SECTION.
      *------------------
       INI-OPE-FILE.
           OPEN OUTPUT TABOFS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO OUTPUT TABOFS FS ' 
                        FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
           MOVE FIO-INP TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO INPUT  TABOFI FS '
                       FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
       FIN-OPN-FILE.
           EXIT.

       PRO-FILE SECTION.
      *-----------------
       INI-PRO-FILE.
           MOVE ZEROES      TO WSS-I.
           MOVE FIO-GET-FST TO FIO-CMND.
       LUP-PRO-FILE.
           PERFORM TAB-FIO-OFI.
           IF NOT FIO-STAT-OKS
               IF WSS-I = 0
                   DISPLAY 'NO EXISTEN REGISTROS'
                   GO TO FIN-PRO-FILE
               ELSE
                   GO TO FIN-PRO-FILE.
           PERFORM OFI-TRS-OFS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-FILE.
       FIN-PRO-FILE.
           EXIT.

       OFI-TRS-OFS SECTION.
       INI-OFI-TRS-OFS.
      *GRABA OFS DESDE OFI.
           ADD  1   TO WSS-I.
      *
      *    MOVE CORR OFI TO OFS.
           MOVE OFI TO OFS.

           MOVE OFS TO WSS-ROFI.
           PERFORM LIMPIE-REG-OFI VARYING WSS-J FROM 1 BY 1 UNTIL
                                          WSS-J > 297.
           MOVE WSS-ROFI TO OFS.

           WRITE OFS.
       FIN-OFI-TRS-OFS.
           EXIT.

       CLO-FILE SECTION.
      *------------------
       INI-CLO-FILE.
           DISPLAY 'NRO. REG. TRASPASADOS :' WSS-I.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
      *
           CLOSE TABOFS.
      *
       FIN-CLO-FILE.
           EXIT.

       LIMPIE-REG-OFI SECTION.
       INI-LIMPIE-REG-OFI.
           IF WSS-CHR-ROFI( WSS-J ) NOT > SPACES
               MOVE ' ' TO WSS-CHR-ROFI( WSS-J ).
       FIN-LIMPIE-REG-OFI.
           EXIT.

      * RUTINAS
       COPY GNSBBSYS.
      * COPY GNSBGEND.
       COPY TABBFOFI.
