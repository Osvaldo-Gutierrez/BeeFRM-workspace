      *$ASCII
       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      TABPSUSR.
       AUTHOR.          CONSIST.
      * 
      *Traspasa una tabla del Archivo Usuario a un archivo secuencial
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
           SELECT TABUSS ASSIGN TO DA-S-TABUSS
                  ORGANIZATION   IS SEQUENTIAL
                  ACCESS MODE    IS SEQUENTIAL
                  FILE STATUS    IS FIO-STAT.
      *
       DATA DIVISION.
      *=============
       FILE SECTION.
       FD TABUSS 
           LABEL RECORDS STANDARD
           RECORD CONTAINS 194.

      *    Registro de Usuarios
      *    --------------------
      * 
      *    Nombre Registro : USR
      *    Clave(s)        : USR-COD-TABL(UU), USR-COD-USER(UU), 
      *                      USR-STP-ITRN(UU), USR-CIC-TABL(UU), 
      *                      USR-EXT-TABL(UU), USR-GLS-ABRV(DC), 
      *                      USR-IDC-IUSR(UU) 
      *    Largo           : 194
      *    Bloqueo         : 2
      *    Observaciones   : 
      *
       01  USS.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  USR-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  USR-KEY-IREG.
      *
      *KEY     Tipo + Codigo ( LLave Primaria )
               05  USR-COD-TABL.
      *
      *            Codigo Tipo Tabla
                   07  USR-COD-TTAB                     PIC X(03).
      *
      *            Codigo Usuario
                   07  USR-COD-USER                     PIC X(12).
      *
      *        Time Stamp Transaccion
               05  USR-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  USR-FEC-FTRN.
                       09  USR-NUM-STRN                 PIC 9(02).
                       09  USR-NUM-ATRN                 PIC 9(02).
                       09  USR-NUM-MTRN                 PIC 9(02).
                       09  USR-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  USR-HRA-HRTR.
                       09  USR-NUM-HHTR                 PIC 9(02).
                       09  USR-NUM-MMTR                 PIC 9(02).
                       09  USR-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  USR-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  USR-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  USR-IND-VIGE  REDEFINES  USR-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  USR-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  USR-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  USR-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  USR-COD-ATRN                             PIC X(12).
      *
      *KEY Tipo + Codigo ( Interno Computacional )
           03  USR-CIC-TABL.
      *
      *        Codigo Tipo Tabla
               05  USR-CIC-TTAB                         PIC X(03).
      *
      *        Codigo Usuario
               05  USR-CIC-USER                         PIC X(12).
      *
      *    Codigo Externo
           03  USR-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  USR-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  USR-EXT-CTAB                         PIC X(05).
      *
      *    Descripcion Corta
           03  USR-GLS-DCOR                             PIC X(12).
      *
      *    Abreviacion Descripcion
           03  USR-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion del Codigo
           03  USR-GLS-DESC                             PIC X(40).
      *
      *    Identificador Usuario 
           03  USR-IDC-IUSR.
      *
      *        Rut Usuario
               05  USR-NUM-IUSR                         PIC X(08).
      *
      *        Indicador Rut Usuario
               05  USR-IND-IUSR                         PIC X(01).
      *
      *        Glosa Rut Usuario
               05  USR-GLS-IUSR                         PIC X(03).
      *
      *    Digito Verificador Rut Usuario
           03  USR-VRF-IUSR                             PIC X(01).
      *
      *    Ubicacion Usuario
           03  USR-COD-UBIC.
      *
      *        Tipo Ubicacion
               05  USR-IND-UBIC                         PIC X(01).
      *
      *        Codigo Ubicacion
               05  USR-COD-OFIC                         PIC X(03).
      *
      *    Categoria Usuario
           03  USR-COD-CUSR                             PIC X(08).
      *
      *    Codigo oficina del usuario
           03  USR-COD-OFUS                             PIC X(03).
      *
      *    Codigo area de gestion
           03  USR-COD-AGES                             PIC X(03).
      *
      *    Disponible
           03  USR-GLS-DISP                             PIC X(29).
      *
      *    Clave Soundex
           03  USR-SNX-TABL.
               05  USR-SNX-TTAB                         PIC X(03).
               05  USR-SNX-USER                         PIC X(03).
      *
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS
      * WORKING PARA RUTINAS.
       COPY GNSWGSYS.
      * COPY GNSWGFEC.
      * COPY GNSWGHOY.
       COPY GNSWCFIO.
       COPY GNSWVFIO.

       COPY TABBRUSR.

       01  WSS-VARI.
           03 WSS-RUSR.
              05 WSS-CHR-RUSR  OCCURS 194   PIC X(01).
           03  WSS-I           VALUE ZEROES PIC 9(06).
           03  WSS-J           VALUE ZEROES PIC 9(03).
           03  WSS-COD-TTAB                 PIC X(03).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           DISPLAY 'INGRESE TABLA A SELECCIONAR : '.
           ACCEPT WSS-COD-TTAB IN WSS-VARI.
           IF WSS-COD-TTAB IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-TTAB IN WSS-VARI.

           PERFORM OPN-FILE.
           PERFORM PRO-FILE.
           PERFORM CLO-FILE.
           STOP RUN.
       FIN-MAIN.
           EXIT.

       OPN-FILE SECTION.
      *------------------
       INI-OPE-FILE.
           OPEN OUTPUT TABUSS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO OUTPUT TABUSS FS ' 
                        FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
           MOVE FIO-INP TO FIO-CMND.
           PERFORM TAB-FIO-USR.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO INPUT  TABUSR FS '
                       FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
       FIN-OPN-FILE.
           EXIT.

       PRO-FILE SECTION.
      *-----------------
       INI-PRO-FILE.
           MOVE ZEROES       TO WSS-I.

           IF WSS-COD-TTAB IN WSS-VARI = '*'
               MOVE FIO-GET-FST TO FIO-CMND
               GO TO LUP-PRO-FILE.

           MOVE WSS-COD-TTAB IN WSS-VARI TO USR-COD-TTAB IN USR.
           MOVE SPACES                   TO USR-COD-USER IN USR.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       LUP-PRO-FILE.
           PERFORM TAB-FIO-USR.
           IF NOT FIO-STAT-OKS
               IF WSS-I = 0
                   DISPLAY 'NO EXISTEN REGISTROS'
                   GO TO FIN-PRO-FILE
               ELSE
                   GO TO FIN-PRO-FILE
           ELSE
               IF WSS-COD-TTAB IN WSS-VARI = '*' OR
                  WSS-COD-TTAB IN WSS-VARI = USR-COD-TTAB IN USR
                   NEXT SENTENCE
               ELSE
                   IF WSS-I = 0
                       DISPLAY 'NO EXISTE TABLA : ' WSS-COD-TTAB
                       GO TO FIN-PRO-FILE
                   ELSE
                       GO TO FIN-PRO-FILE.
           PERFORM USR-TRS-USS.
       NXT-PRO-FILE.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-FILE.
       FIN-PRO-FILE.
           EXIT.

       USR-TRS-USS SECTION.
       INI-USR-TRS-USS.
      *GRABA USS DESDE USR.
           ADD  1   TO WSS-I.
      *
      *    MOVE CORR USR TO USS.
           MOVE USR TO USS.

           MOVE USS TO WSS-RUSR.
           PERFORM LIMPIE-REG-USR VARYING WSS-J FROM 1 BY 1 UNTIL
                                          WSS-J > 194.
           MOVE WSS-RUSR TO USS.

           WRITE USS.
       FIN-USR-TRS-USS.
           EXIT.

       CLO-FILE SECTION.
      *------------------
       INI-CLO-FILE.
           DISPLAY 'NRO. REG. TRASPASADOS :' WSS-I.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM TAB-FIO-USR.
      *
           CLOSE TABUSS.
      *
       FIN-CLO-FILE.
           EXIT.

       LIMPIE-REG-USR SECTION.
       INI-LIMPIE-REG-USR.
           IF WSS-CHR-RUSR( WSS-J ) NOT > SPACES
               MOVE ' ' TO WSS-CHR-RUSR( WSS-J ).
       FIN-LIMPIE-REG-USR.
           EXIT.

      * RUTINAS
       COPY GNSBBSYS.
      * COPY GNSBGEND.
       COPY TABBFUSR.
