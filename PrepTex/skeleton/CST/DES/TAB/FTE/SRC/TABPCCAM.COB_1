       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      TABPCCAM.
       AUTHOR.          JULIO SEPULVEDA.

      * Programa carga Valores de Cambios de Tablas

       ENVIRONMENT DIVISION.
      *====================

       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGIFD.
      *==============
       COPY TABBRCAM.
      *    Registro de Valores de Cambios
      *    ------------------------------
      * 
      *    Nombre Registro : CAM
      *    Clave(s)        : CAM-KEY-CAMB(UU), CAM-STP-ITRN(UU)
      *    Largo           : 75
      *    Bloqueo         : 1
      *    Observaciones   : 
      *
       01  CAM-NEW.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  CAM-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  CAM-KEY-IREG.
      *
      *KEY     Fecha 1 + Fecha 2 + Valor 1 + Valor 2 ( LLave Primaria )
               05  CAM-KEY-CAMB.
      *
      *            Fecha de Cambio
                   07  CAM-FEC-CAMB.
      *
      *                Fecha 1 + Fecha 2
                       09  CAM-FEC-FCAM OCCURS 2 TIMES.
                           11  CAM-NUM-SCAM             PIC 9(02).
                           11  CAM-NUM-ACAM             PIC 9(02).
                           11  CAM-NUM-MCAM             PIC 9(02).
                           11  CAM-NUM-DCAM             PIC 9(02).
      *
      *            Codigo Valor de Cambio
                   07  CAM-COD-CAMB.
      *
      *                Valor 1 + Valor 2
                       09  CAM-COD-VCAM OCCURS 2 TIMES.
      *
      *                    Tipo Valor                   TAB TVAL
                           11  CAM-MSC-TVAL             PIC X(01).
      *
      *                    Codigo Valor
                           11  CAM-COD-CVAL             PIC X(03).
      *
      *                    Tipo Cambio
                           11  CAM-COD-TCAM             PIC X(02).
      *
      *        Time Stamp Transaccion
               05  CAM-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  CAM-FEC-FTRN.
                       09  CAM-NUM-STRN                 PIC 9(02).
                       09  CAM-NUM-ATRN                 PIC 9(02).
                       09  CAM-NUM-MTRN                 PIC 9(02).
                       09  CAM-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  CAM-HRA-HRTR.
                       09  CAM-NUM-HHTR                 PIC 9(02).
                       09  CAM-NUM-MMTR                 PIC 9(02).
                       09  CAM-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  CAM-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  CAM-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  CAM-IND-VIGE  REDEFINES  CAM-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  CAM-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  CAM-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  CAM-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  CAM-COD-ATRN                             PIC X(12).
      *
      *    Valor de Cambio
           03  CAM-SGV-VCAM                  COMP-3     PIC S9(11)V9(4).
      *
      *    Disponible 
           03  CAM-GLS-DISP                             PIC X(06).
      *
      *    Variables de Cambio
       01  CAM-VARI.
      *
      *    Codigo Sistema
           03  CAM-COD-SIST                             PIC X(03).
      *
      *    Moneda Nacional
           03  CAM-COD-MNAC                             PIC X(06).
      *
      *    Porcentaje
           03  CAM-COD-PORC                             PIC X(06).
      *
      *    Subindices de Valores de Cambio
           03  CAM-SIND.
               05  CAM-I                COMP         PIC 9(04).
               05  CAM-IMAX             COMP         PIC 9(04).
      *
      *    Vector de Valores de Cambio
           03  CAM-TAB-ICAM.
               05  CAM-VEC-ICAM OCCURS 10.
                   07  CAM-COD-VCAM                  PIC X(06).
                   07  CAM-SGV-VCAM     COMP-3       PIC S9(11)V9(4).

      *==============

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-NEW-REQA==.

      *========================================================

        01  WSS-VARI.
            03 WSS-NDBI-OLD                      PIC 9(03).
            03 WSS-NDBI-NEW                      PIC 9(03).
            03 WSS-SIST                          PIC X(03).
            03 WSS-RPRO                  VALUE 0 PIC 9(05).
            03 WSS-RMAL                  VALUE 0 PIC 9(05).

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           PERFORM INI.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
       LUP-MAIN.
           IF NOT FIO-STAT-OKS
               GO TO MSG-MAIN.
           PERFORM PRO-CAM.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           GO TO LUP-MAIN.
       MSG-MAIN.
           DISPLAY 'REGISTROS PROCESADOS  : ' WSS-RPRO.
           DISPLAY 'REGISTROS RECHAZADOS  : ' WSS-RMAL.
       FIN-MAIN.
           STOP RUN.

       INI SECTION.
       INI-INI.
           MOVE 'TABPCCAM' TO FIO-PROG.
           PERFORM BUS-FSIS.

           DISPLAY 'INGRESE SISTEMA : '.
           ACCEPT FIO-SIST.
           DISPLAY 'SISTEMA : ' FIO-SIST.

           DISPLAY 'INGRESE NRO DE BASE DE DATOS ANTIGUA : '.
           ACCEPT WSS-NDBI-OLD.
           DISPLAY 'NRO DE BASE DE DATOS ANTIGUA : ' WSS-NDBI-OLD.

           DISPLAY 'INGRESE NRO DE BASE DE DATOS NUEVA   : '.
           ACCEPT WSS-NDBI-NEW.
           DISPLAY 'NRO DE BASE DE DATOS NUEVA   : ' WSS-NDBI-NEW. 
       FIN-INI.
           EXIT.

       PRO-CAM SECTION.
       INI-PRO-CAM.
           MOVE SPACES                 TO CAM-GLS-FLAG IN CAM-NEW.
           MOVE CAM-FEC-FCAM IN CAM(1) TO CAM-FEC-FCAM IN CAM-NEW(1).
           MOVE CAM-FEC-FCAM IN CAM(2) TO CAM-FEC-FCAM IN CAM-NEW(2).
           MOVE CAM-COD-VCAM IN CAM(1) TO CAM-COD-VCAM IN CAM-NEW(1).
           MOVE CAM-COD-VCAM IN CAM(2) TO CAM-COD-VCAM IN CAM-NEW(2).
           MOVE CAM-FEC-FUMO IN CAM    TO CAM-FEC-FTRN IN CAM-NEW.
           MOVE ZEROES                 TO CAM-HRA-HRTR IN CAM-NEW.
           MOVE SPACES                 TO CAM-MSC-TACC IN CAM-NEW.
           MOVE CAM-IND-VIGE IN CAM    TO CAM-IND-VIGE IN CAM-NEW.
           MOVE SPACES                 TO CAM-MSC-TTRN IN CAM-NEW.
           MOVE SPACES                 TO CAM-COD-TTRN IN CAM-NEW.
           MOVE SPACES                 TO CAM-COD-ATRN IN CAM-NEW.
           MOVE CAM-SGV-VCAM IN CAM    TO CAM-SGV-VCAM IN CAM-NEW.
           MOVE SPACES                 TO CAM-GLS-DISP IN CAM-NEW.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM TAB-FIO-CAM-NEW.
           ADD 1 TO WSS-RPRO.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-RMAL
               DISPLAY 'FIO-STAT : ' FIO-STAT ', FIO-MENS : ' FIO-MENS
               DISPLAY 'CAM     : ' CAM
               DISPLAY 'CAM-NEW : ' CAM-NEW
               DISPLAY '----------------------------------------------'.
       FIN-PRO-CAM.
           EXIT.

      *==============
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.

      * Modulo FIO para registro CAM en DATACOM
       TAB-FIO-CAM-NEW SECTION.
       INI-TAB-FIO-CAM-NEW.
           MOVE ADR-CAM-NEW-REQA TO ADR-REQA.
           MOVE 'CAM' TO ADR-TABL IN ADR-REQA.
      *JSS MOVE IFD-GSIS( 2 ) TO ADR-DBID IN ADR-REQA.
           MOVE WSS-NDBI-NEW  TO ADR-DBID IN ADR-REQA.
      *
           MOVE 'CAM00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'CAM-KEY-CAMB' OR FIO-AKEY NOT > SPACES )
               MOVE CAM-KEY-CAMB IN CAM-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'CAM00' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'CAM-STP-ITRN'
               MOVE CAM-STP-ITRN IN CAM-NEW TO ADR-VKEY IN ADR-REQA
               MOVE 'CAM01' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En TAB-FIO-CAM-NEW, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
           MOVE CAM-NEW TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO CAM-NEW
           ELSE
               MOVE 'TABCAM' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE ADR-REQA TO ADR-CAM-NEW-REQA.
       FIN-TAB-FIO-CAM-NEW.
           EXIT.

      * Modulo FIO para registro CAM en DATACOM
       TAB-FIO-CAM SECTION.
       INI-TAB-FIO-CAM.
           MOVE ADR-CAM-REQA TO ADR-REQA.
           MOVE 'CAM' TO ADR-TABL IN ADR-REQA.
      *JSS MOVE IFD-GSIS( 2 ) TO ADR-DBID IN ADR-REQA.
           MOVE WSS-NDBI-OLD  TO ADR-DBID IN ADR-REQA.
      *
           MOVE 'CAM00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON
               MOVE CAM-KEY-CAMB IN CAM TO ADR-VKEY IN ADR-REQA
               MOVE 'CAM00' TO ADR-DKEY IN ADR-REQA
           ELSE
               STRING 'Nombre Llave : ' FIO-AKEY
                      ' invalida en TAB-FIO-CAM'
                      DELIMITED BY SIZE INTO FIO-MEN1
               MOVE SPACES TO FIO-MEN2
               GO TO PRG-ABT.
           MOVE CAM TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO CAM
           ELSE
               MOVE 'TABCAM' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE ADR-REQA TO ADR-CAM-REQA.
       FIN-TAB-FIO-CAM.
           EXIT.
