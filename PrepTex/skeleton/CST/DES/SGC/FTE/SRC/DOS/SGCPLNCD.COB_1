       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      SGCPLNCD.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    19-Nov-92 07:27 PM.

      * Programa Listador de Reporte NCD con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTNCD         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTNCD         ASSIGN TO        UT-S-RPTNCD.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTNCD         ASSIGN TO        UT-S-RPTNCD.
      *<<<< IOS
      *DOS SELECT RPTNCP         ASSIGN TO SYS012-UR-1403-S.
      *MVS SELECT RPTNCP         ASSIGN TO        UT-S-RPTNCP.
           SELECT RPTNCP         ASSIGN TO SYS012-UR-1403-S.
      *DOS SELECT RPTDDC         ASSIGN TO SYS013-UR-1403-S.
      *MVS SELECT RPTDDC         ASSIGN TO        UT-S-RPTDDC.
           SELECT RPTDDC         ASSIGN TO SYS013-UR-1403-S.
      *>>>>

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY SGCRRNCD.
       FD  RPTNCD LABEL RECORDS OMITTED
           REPORT IS RPT-NCD
           .
      *<<<< FLS
       FD  RPTNCP LABEL RECORDS OMITTED
           REPORT IS RPT-NCP.
       01  PASO1.
           03 FILLER                              PIC X(01).
       FD  RPTDDC LABEL RECORDS OMITTED
           REPORT IS RPT-DDC.
       01  PASO.
           03 FILLER                              PIC X(01).
      *>>>>

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY SGCBRDBC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY SGCRWNCD.
      *<<<< RW
       COPY SGCRWNCP.
       COPY SGCRWDDC.
      *>>>>
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY TABWGLOC.
       COPY SGCBRRCD.
       COPY SGCBRDDC.
       COPY GNSBRMSC.
       COPY GNSWGCPT.
       COPY GNSWCNUM.
       COPY GNSWVNUM.
       01  WSS-VARI.
           03 WSS-IND-TICL                        PIC X(01).
           03 WSS-IND-ALFA                        PIC 9(01).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-I                               PIC 9(02).
           03 WSS-J                               PIC 9(02).
           03 WSS-GLS-DIRE.
              05 WSS-GLS-DIR1  OCCURS 60          PIC X(01).
           03 WSS-NUM-DIRE.
              05 WSS-NUM-DIR1  OCCURS 6           PIC 9(01).
           03 WSS-IND-POKS           VALUE 0      PIC 9(06).
           03 WSS-OLD.
              05 WSS-GLS-DOLD                     PIC X(45).
              05 WSS-GLS-FOLD                     PIC X(78).
              05 WSS-NUM-COLD                     PIC 9(06).
           03 WSS-IND-DOLD                        PIC 9(01).
           03 WSS-NEW.
              05 WSS-GLS-DNEW                     PIC X(45).
              05 WSS-GLS-FNEW                     PIC X(78).
              05 WSS-NUM-CNEW                     PIC 9(06).
           03 WSS-IND-DNEW                        PIC 9(01).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY SGCRFNCD.
           .
       COPY SGCRTNCD.
      *<<<< RT
       COPY SGCRFNCP.
           .
       COPY SGCRTNCP.
       COPY SGCRFDDC.
           .
       COPY SGCRTDDC.
      *>>>>

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'SGCPLNCD' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
      *<<<< INI
           DISPLAY 'INICIO DE CAPTURA DE PARAMETROS'.

      *Captura Tipo de Cliente
           DISPLAY 'TIPO DE CLIENTE (*=TODOS) : '.
           ACCEPT  WSS-IND-TICL IN WSS-VARI.
           DISPLAY WSS-IND-TICL IN WSS-VARI.

           DISPLAY 'FIN    DE CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO DE VALIDACION DE PARAMETROS'.

      *VAL-MSC Valida indicador ( miscelaneo ) en TABLAS
           IF WSS-IND-TICL IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-IND-TICL IN WSS-VARI.
           IF WSS-IND-TICL IN WSS-VARI NOT = '*'
               DISPLAY WSS-IND-TICL IN WSS-VARI
               MOVE 'SGC'                    TO MSC-COD-SIST
               MOVE 'TICL'                   TO MSC-COD-TMSC IN MSC
               MOVE WSS-IND-TICL IN WSS-VARI TO MSC-COD-CMSC IN MSC
               PERFORM VAL-MSC
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN DE VALIDACION DE PARAMETROS'.

      * Efectua la Apertura del Archivo SAM de Salida
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM SGC-FIO-DDC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO DDC DE OUTPUT' FIO-STAT
               PERFORM GNS-PRO-END.
      *>>>>
           SORT GNSSRT
       COPY SGCRSNCD.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro DBC
      *
      * Seleccion del Registro para Proceso Mediante Tipo Cliente
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.

      * Formacion y Generacion del Registro SRT al Procesar Varios Registros
           PERFORM BUS-SRT-REG.
           GO TO NXT-INP-SRT.
      *>>>>
       MOV-INP-SRT.
       COPY SGCRMNCD.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTNCD.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-NCD.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
           OPEN OUTPUT RPTNCP.
           INITIATE RPT-NCP.
           OPEN OUTPUT RPTDDC.
           INITIATE RPT-DDC.
           MOVE SRT TO WSS-OLD IN WSS-VARI.
           MOVE  0  TO WSS-IND-DOLD IN WSS-VARI.
           GO TO NXT-OUT-SRT.
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
           PERFORM GEN-LIN-OUT.
           GO TO NXT-OUT-SRT.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-NCD.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      *<<<< FIN_OUT
           MOVE WSS-OLD IN WSS-VARI TO SRT.

           MOVE '-' TO WSS-GLS-AUX1 IN WSS-NCD
                       WSS-GLS-AUX1 IN WSS-NCP
                       WSS-GLS-AUX2 IN WSS-DDC.

           IF DBC-GLS-ICLI IN SRT > SPACES
               MOVE '-' TO WSS-GLS-AUX2 IN WSS-NCD
                           WSS-GLS-AUX2 IN WSS-NCP
                           WSS-GLS-AUX2 IN WSS-DDC
           ELSE
               MOVE SPACES TO WSS-GLS-AUX2 IN WSS-NCD
                              WSS-GLS-AUX2 IN WSS-NCP
                              WSS-GLS-AUX2 IN WSS-DDC.

           IF WSS-IND-DOLD IN WSS-VARI = 1
               GENERATE DL-DDC
               GENERATE DL-RAY
           ELSE
           IF WSS-IND-DNEW IN WSS-VARI = 2
               IF SRT-NUM-DIRE IN SRT > 99
                   GENERATE DL-NCD
                   GENERATE DL-SEP
               ELSE
                   GENERATE DL-NCP
                   GENERATE DL-GUI.
      *>>>>
           TERMINATE RPT-NCD.
      *<<<< FIN_TMT
      * Efectua el Cierre del Archivo SAM y Display de Contadores
      *     DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-IND-RBAD IN WSS-VARI.
      *     DISPLAY 'REGISTROS DDC GRABADOS : ' WSS-IND-POKS IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SGC-FIO-DDC.
           TERMINATE RPT-NCP.
           CLOSE RPTNCP.
           TERMINATE RPT-DDC.
           CLOSE RPTDDC.
      *>>>>
           CLOSE RPTNCD.
       EXT-OUT-SRT.
           EXIT.

       COPY SGCBFDBC.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      **************************************************************************
      * Seleccion del Registro para Proceso Mediante Tipo Cliente
      * Listado
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

           IF WSS-IND-TICL IN WSS-VARI NOT = '*'
               IF WSS-IND-TICL IN WSS-VARI NOT = DBC-IND-TICL IN DBC
                   GO TO FIN-SEL-REG-PRO
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE '*' TO DBC-IND-TICL IN DBC.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      **************************************************************************
      * Formacion y Generacion del Registro SRT al Procesar Varios Registros
       BUS-SRT-REG SECTION.
       INI-BUS-SRT-REG.
      * Complementa Formacion del Registro SRT
      *
      * Compacta Nombre Cliente para el Registro SRT
           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.

      * Mediante CIC del Cliente en DBC obtener TODAS sus Direcciones en RCD
           MOVE DBC-CIC-ICLI IN DBC TO RCD-CIC-ICLI IN RCD.
           MOVE ZEROES              TO RCD-NUM-DIRE IN RCD.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-BUS-DIRE.
           PERFORM SGC-FIO-RCD.
           IF NOT FIO-STAT-OKS OR
              DBC-CIC-ICLI IN DBC NOT = RCD-CIC-ICLI IN RCD
               GO TO FIN-BUS-SRT-REG.

      * Procesa Direccion del Cliente que corresponda al Domicilio Solamente
           IF RCD-MSC-TDIR IN RCD NOT = 'D'
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-BUS-DIRE.

      * Compacta Direccion del Cliente para el Registro SRT
           MOVE RCD-GLS-DIRE IN RCD TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO RCD-GLS-DIRE IN RCD.

      * De Direccion del Cliente extraer Numero de la Calle para el Registro SRT
           IF RCD-GLS-DIRE IN RCD > SPACES
               PERFORM OBT-NRO-CAYE
           ELSE
               MOVE ZEROES TO SRT-NUM-DIRE IN SRT.

      * Mediante BUS-TAB Busca Comuna Obteniendo Codigos de Tabla COM

      * BUSQUEDA LOCALIDAD
           IF RCD-GLS-DIRE IN RCD > SPACES
               MOVE RCD-COD-LUGA IN RCD TO LOC-LUGA
               PERFORM TAB-BUS-LOC
               MOVE LOC-DESC TO SRT-GLS-COMU IN SRT
           ELSE
               MOVE '*' TO RCD-GLS-DIRE IN RCD
               MOVE '*' TO SRT-GLS-COMU IN SRT
               MOVE '*' TO RCD-GLS-FONO IN RCD.

           MOVE FIO-GET-NXT TO FIO-CMND.

      * Completa Llenado de Campos y Genera el Registro SRT
           COPY SGCRMNCD.
           RELEASE SRT.
           GO TO LUP-BUS-DIRE.
       FIN-BUS-SRT-REG.
           EXIT.
      **************************************************************************
      * De Direccion del Cliente extraer Numero de la Calle para el Registro SRT
       OBT-NRO-CAYE SECTION.
       INI-OBT-NRO-CAYE.
           MOVE  1     TO WSS-I IN WSS-VARI
                          WSS-J IN WSS-VARI.
           MOVE  0     TO WSS-IND-ALFA IN WSS-VARI.
           MOVE SPACES TO WSS-NUM-DIRE IN WSS-VARI.
           MOVE RCD-GLS-DIRE IN RCD TO WSS-GLS-DIRE IN WSS-VARI.
       LUP-OBT-NRO-CAYE.
      * Escanear Direccion del Cliente para extraer Numero de la Calle
           IF WSS-GLS-DIR1 IN WSS-VARI(WSS-I) NUMERIC AND
              WSS-IND-ALFA IN WSS-VARI = 0
               GO TO NXT-OBT-NRO-CAYE
           ELSE
           IF WSS-GLS-DIR1 IN WSS-VARI(WSS-I) = SPACES AND
              WSS-IND-ALFA IN WSS-VARI = 0
               GO TO NXT-OBT-NRO-CAYE
           ELSE
           IF WSS-GLS-DIR1 IN WSS-VARI(WSS-I) NOT NUMERIC AND
              WSS-IND-ALFA IN WSS-VARI = 0
               MOVE 1 TO WSS-IND-ALFA IN WSS-VARI
               GO TO NXT-OBT-NRO-CAYE.

      * Almacena digitos del Numero de la Calle de la Direccion del Cliente
           IF WSS-GLS-DIR1 IN WSS-VARI(WSS-I) NUMERIC
               MOVE WSS-GLS-DIR1 IN WSS-VARI(WSS-I)
                                 TO WSS-NUM-DIR1 IN WSS-VARI(WSS-J)
               ADD 1 TO WSS-J IN WSS-VARI
               IF WSS-J IN WSS-VARI > 6
                   DISPLAY 'NUMERO DE CALLE SUPERIOR A 6 DIGITOS : '
                           WSS-NUM-DIRE IN WSS-VARI ' DE '
                           DBC-NUM-ICLI IN DBC
                   MOVE 60 TO WSS-I IN WSS-VARI
                   GO TO NXT-OBT-NRO-CAYE.

           IF WSS-GLS-DIR1 IN WSS-VARI(WSS-I) NOT NUMERIC AND
              WSS-J IN WSS-VARI > 1
               MOVE 60 TO WSS-I IN WSS-VARI.
       NXT-OBT-NRO-CAYE.
           ADD 1 TO WSS-I IN WSS-VARI.
           IF WSS-I IN WSS-VARI NOT > 60
               GO TO LUP-OBT-NRO-CAYE.

      * Procesa digitos del Numero de la Calle Almacenados
           IF WSS-J IN WSS-VARI = 1
               MOVE ZEROES TO SRT-NUM-DIRE IN SRT
               GO TO FIN-OBT-NRO-CAYE.

      *VAL-NUM Validacion numerica de digitos del Numero de la Calle Almacenados
           MOVE WSS-NUM-DIRE IN WSS-VARI TO NUM-NUME.
           MOVE NUM-ZERO-S               TO NUM-ZERO.
           MOVE NUM-SGNO-N               TO NUM-SGNO.
           MOVE 6                        TO NUM-NENT.
           MOVE 0                        TO NUM-NDCM.
           CALL 'GNSPVNUM' USING NUM-VARI.
           IF NUM-STAT-OKS
               MOVE NUM-NN9N TO SRT-NUM-DIRE IN SRT
           ELSE
               MOVE ZEROES TO SRT-NUM-DIRE IN SRT.
       FIN-OBT-NRO-CAYE.
           EXIT.
      **************************************************************************
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
       GEN-LIN-OUT SECTION.
       INI-GEN-LIN-OUT.
           MOVE '-' TO WSS-GLS-AUX1 IN WSS-NCD
                       WSS-GLS-AUX1 IN WSS-NCP
                       WSS-GLS-AUX1 IN WSS-DDC.

           IF SRT-NUM-DIRE IN SRT = WSS-NUM-COLD IN WSS-VARI
               IF RCD-GLS-DIRE IN SRT = WSS-GLS-DOLD IN WSS-VARI
                   MOVE 1 TO WSS-IND-DNEW IN WSS-VARI
               ELSE
                   MOVE 2 TO WSS-IND-DNEW IN WSS-VARI
           ELSE
               MOVE 0 TO WSS-IND-DNEW IN WSS-VARI.

           IF WSS-IND-DOLD IN WSS-VARI = 0 AND
              WSS-IND-DNEW IN WSS-VARI = 0
               NEXT SENTENCE
           ELSE
           IF (( WSS-IND-DOLD IN WSS-VARI = 0 AND
                 WSS-IND-DNEW IN WSS-VARI = 2 ) OR
               ( WSS-IND-DOLD IN WSS-VARI = 2 AND
                 WSS-IND-DNEW IN WSS-VARI = 0 ) OR
               ( WSS-IND-DOLD IN WSS-VARI = 2 AND
                 WSS-IND-DNEW IN WSS-VARI = 2 ))
               PERFORM PROCESO-NCD
           ELSE
               PERFORM PROCESO-DDC.

           MOVE WSS-IND-DNEW IN WSS-VARI TO WSS-IND-DOLD IN WSS-VARI.
           MOVE SRT TO WSS-OLD IN WSS-VARI.
           MOVE SPACES TO WSS-GLS-AUX1 IN WSS-NCD
                          WSS-GLS-AUX2 IN WSS-NCD
                          WSS-GLS-AUX1 IN WSS-NCP
                          WSS-GLS-AUX2 IN WSS-NCP
                          WSS-GLS-AUX1 IN WSS-DDC
                          WSS-GLS-AUX2 IN WSS-DDC.
       FIN-GEN-LIN-OUT.
           EXIT.
      **************************************************************************
       PROCESO-DDC SECTION.
       INI-PROCESO-DDC.
           MOVE '*' TO WSS-GLS-MARK IN WSS-NCD
                       WSS-GLS-MARK IN WSS-NCP.
           MOVE SRT TO WSS-NEW IN WSS-VARI.
           MOVE WSS-OLD IN WSS-VARI TO SRT.

           IF DBC-GLS-ICLI IN SRT > SPACES
               MOVE '-' TO WSS-GLS-AUX2 IN WSS-DDC
                           WSS-GLS-AUX2 IN WSS-NCP
                           WSS-GLS-AUX2 IN WSS-NCD
           ELSE
               MOVE SPACES TO WSS-GLS-AUX2 IN WSS-DDC
                              WSS-GLS-AUX2 IN WSS-NCP
                              WSS-GLS-AUX2 IN WSS-NCD.
           GENERATE DL-DDC.

           IF SRT-NUM-DIRE IN SRT > 99
               GENERATE DL-NCD
           ELSE
               GENERATE DL-NCP.

           MOVE SRT TO DDC.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM SGC-FIO-DDC.

           IF (( WSS-IND-DOLD IN WSS-VARI = 1 AND
                 WSS-IND-DNEW IN WSS-VARI = 0 ) OR
               ( WSS-IND-DOLD IN WSS-VARI = 1 AND
                 WSS-IND-DNEW IN WSS-VARI = 2 ))
               GENERATE DL-RAY.

           IF WSS-IND-DNEW IN WSS-VARI = 0
               IF SRT-NUM-DIRE IN SRT > 99
                   GENERATE DL-SEP
               ELSE
                   GENERATE DL-GUI.

           MOVE WSS-NEW IN WSS-VARI TO SRT.
       FIN-PROCESO-DDC.
           EXIT.
      **************************************************************************
       PROCESO-NCD SECTION.
       INI-PROCESO-NCD.
           MOVE ' ' TO WSS-GLS-MARK IN WSS-NCD
                       WSS-GLS-MARK IN WSS-NCP.
           MOVE SRT TO WSS-NEW IN WSS-VARI.
           MOVE WSS-OLD IN WSS-VARI TO SRT.

           IF DBC-GLS-ICLI IN SRT > SPACES
               MOVE '-' TO WSS-GLS-AUX2 IN WSS-NCD
                           WSS-GLS-AUX2 IN WSS-NCP
           ELSE
               MOVE SPACES TO WSS-GLS-AUX2 IN WSS-NCD
                              WSS-GLS-AUX2 IN WSS-NCP.

           IF SRT-NUM-DIRE IN SRT > 99
               GENERATE DL-NCD
           ELSE
               GENERATE DL-NCP.

           IF WSS-IND-DOLD IN WSS-VARI = 2 AND
              WSS-IND-DNEW IN WSS-VARI = 0
               IF SRT-NUM-DIRE IN SRT > 99
                   GENERATE DL-SEP
               ELSE
                   GENERATE DL-GUI.

           MOVE WSS-NEW IN WSS-VARI TO SRT.
       FIN-PROCESO-NCD.
           EXIT.
      **************************************************************************

       COPY TABBBLOC.
       COPY SGCBFRCD.
       COPY SGCBFDDC.
       COPY GNSBFMSC.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBVMSC.
       COPY GNSBGCPT.
      *>>>>
