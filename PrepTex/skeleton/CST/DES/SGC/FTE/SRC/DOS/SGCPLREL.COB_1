       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      SGCPLREL.
       AUTHOR.          CONSIST.
      * Programa Listador de Reporte REL con Sort.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *DOS SELECT RPTREL         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTREL         ASSIGN TO        UT-S-RPTREL.
           SELECT RPTREL         ASSIGN TO SYS011-UR-1403-S.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY SGCRRREL.
       FD  RPTREL LABEL RECORDS OMITTED
           REPORT IS RPT-REL
           .
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-REC-REQA==.
       COPY SGCBRREC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGIFD.
       COPY SGCRWREL.
       COPY GNSWGSYS.
      *<<<<
       COPY SGCBRDBC.
       COPY TABBROFI.
       COPY TABBRUSR.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWVPPH.
       COPY GNSBRTAB.
       COPY GNSWGCPT.
       COPY SGCWVDBC.
           03  WSS-GLS-SEL1                   PIC X(22).
           03  WSS-GLS-SEL2                   PIC X(22).
           03  WSS-COD-TREL                   PIC X(02).
           03  WSS-IND-VIGE                   PIC X(01).
           03  WSS-CIC-ICLI                   PIC X(12).
           03  WSS-IND-SWTR       VALUE 0     PIC 9(01).
               88  POR-RELACION   VALUE 1.
           03  WSS-IND-SWIC       VALUE 0     PIC 9(01).
               88  POR-CLIENTE    VALUE 1.
      *>>>>
      *<<<<
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
      *>>>>
       REPORT SECTION.
      *--------------
       COPY SGCRFREL.
           .
       COPY SGCRTREL.
       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-REL-SRT-NUM-ICLI SECTION.
           USE BEFORE REPORTING CH-REL-SRT-NUM-ICLI.
       INI-CH-REL-SRT-NUM-ICLI.
       FIN-CH-REL-SRT-NUM-ICLI.
           EXIT.
       DEC-CH-REL-DBC-COD-EJEC SECTION.
           USE BEFORE REPORTING CH-REL-DBC-COD-EJEC.
       INI-CH-REL-DBC-COD-EJEC.
      *<<<<
      *BUS-TAB busca glosa de codigo en tabla
           MOVE DBC-COD-EJEC IN SRT TO USR-COD-USER IN USR.
           PERFORM BUS-USR.
           MOVE USR-GLS-DESC IN USR TO WSS-GLS-EJEC IN WSS-REL.
      *>>>>
       FIN-CH-REL-DBC-COD-EJEC.
           EXIT.
       DEC-PH-REL SECTION.
           USE BEFORE REPORTING PH-REL.
       INI-PH-REL.
      *<<<<
       COPY GNSBGPPH.
      *>>>>
       FIN-PH-REL.
           EXIT.
       END DECLARATIVES.
       MAIN SECTION.
      *------------
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           MOVE 'SGCPLREL' TO FIO-PROG.
           PERFORM BUS-FSIS.
           SORT GNSSRT
       COPY SGCRSREL.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           GOBACK.
       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<<
           DISPLAY 'Inicio de Captura de Parametros'.
           PERFORM GET-PAR.
           MOVE WSS-GLS-SEL1 TO WSS-GLS-TOEC IN WSS-REL.
           MOVE SPACES TO REC-KEY-IREC IN REC.
           IF WSS-CIC-SEL1 = 'C'
               MOVE DBC-CIC-ICLI IN DBC TO WSS-CIC-ICLI IN WSS-VARI
               MOVE DBC-CIC-ICLI IN DBC TO REC-CIC-ICLI IN REC
               MOVE 1 TO WSS-IND-SWIC IN WSS-VARI.
           IF WSS-CIC-SEL1 = 'O'
               MOVE WSS-GLS-SEL2 TO WSS-GLS-OFIC IN WSS-REL.
      *Captura Codigo de Relacion
           DISPLAY 'CODIGO DE RELACION    ( T => TODOS ) : '.
           ACCEPT  WSS-COD-TREL IN WSS-VARI.
           DISPLAY WSS-COD-TREL IN WSS-VARI.
      *Captura Indicador de Vigencia
           DISPLAY 'INDICADOR DE VIGENCIA ( T => TODAS ) : '.
           ACCEPT  WSS-IND-VIGE IN WSS-VARI.
           DISPLAY WSS-IND-VIGE IN WSS-VARI.
           DISPLAY 'Termino Captura de Parametros'.
           DISPLAY 'Inicio de Validacion de Parametros'.
           DISPLAY WSS-COD-TREL IN WSS-VARI.
           IF NOT ( WSS-COD-TREL     = 'T' OR
                    WSS-COD-TREL NOT > SPACES )
               MOVE WSS-COD-TREL IN WSS-VARI TO REC-COD-TREC IN REC-VARI
               MOVE SPACES                   TO REC-COD-AREC IN REC-VARI
      *VAL-TAB Validacion de codigo en tabla : Codigo de Relacion
               MOVE 'SGC' TO TAB-COD-SIST
               MOVE 'REC' TO TAB-COD-TTAB IN TAB
               MOVE REC-COD-CTCA IN REC-VARI TO TAB-COD-CTAB IN TAB
               PERFORM VAL-TAB
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-MENS IN MSG
                   GOBACK
               ELSE
                   MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-RELA IN WSS-REL
                   MOVE 1 TO WSS-IND-SWTR IN WSS-VARI
                   MOVE WSS-COD-TREL IN WSS-VARI TO REC-COD-TREC IN REC
           ELSE
               MOVE SPACES TO WSS-GLS-RELA IN WSS-REL.
           DISPLAY WSS-IND-VIGE IN WSS-VARI.
           IF NOT ( WSS-IND-VIGE     = 'T' OR
                    WSS-IND-VIGE NOT > SPACES )
      *VAL-MSC Valida indicador ( miscelaneo ) de tabla
               MOVE 'TAB' TO MSC-COD-SIST
               MOVE 'VIGE' TO MSC-COD-TMSC IN MSC
               MOVE WSS-IND-VIGE IN WSS-VARI TO MSC-COD-CMSC IN MSC
               PERFORM VAL-MSC
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   GOBACK
               ELSE
                   MOVE MSC-GLS-DESC IN MSC TO WSS-GLS-VIGE IN WSS-REL
           ELSE
               MOVE SPACES TO WSS-GLS-VIGE IN WSS-REL.
           DISPLAY 'Fin de Validacion de Parametros'.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-REC.
       FST-INP-SRT.
      *<<<<
           IF POR-CLIENTE
               MOVE FIO-GET-NLS TO FIO-CMND
               PERFORM SGC-FIO-REC
               GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM SGC-FIO-REC.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<<
           IF NOT ( ( WSS-COD-TREL = REC-COD-TREC IN REC OR
                      NOT POR-RELACION ) AND
                    ( WSS-CIC-ICLI = REC-CIC-ICLI IN REC OR
                      NOT POR-CLIENTE ) )
               GO TO NXT-INP-SRT.
           IF NOT ( WSS-IND-VIGE     = 'T' OR
                    WSS-IND-VIGE NOT > SPACES )
               IF REC-IND-VIGE IN REC NOT = WSS-IND-VIGE IN WSS-VARI
                   GO TO NXT-INP-SRT.
      *CIC-CLI Busca Cliente por medio de CIC
           MOVE REC-CIC-IREL IN REC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO SRT-GLS-NOMR IN SRT.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE SPACES              TO SRT-NUM-IREL IN SRT
               MOVE SPACES              TO SRT-VRF-IREL IN SRT
               MOVE SPACES              TO SRT-IND-IREL IN SRT
               MOVE SPACES              TO SRT-GLS-IREL IN SRT
           ELSE
               MOVE DBC-NUM-ICLI IN DBC TO SRT-NUM-IREL IN SRT
               MOVE DBC-VRF-ICLI IN DBC TO SRT-VRF-IREL IN SRT
               MOVE DBC-IND-ICLI IN DBC TO SRT-IND-IREL IN SRT
               MOVE DBC-GLS-ICLI IN DBC TO SRT-GLS-IREL IN SRT.
      *CIC-CLI Busca Cliente por medio de CIC
           MOVE REC-CIC-ICLI IN REC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO SRT-GLS-NOMC IN SRT.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE SPACES              TO SRT-NUM-ICLI IN SRT
               MOVE SPACES              TO SRT-VRF-ICLI IN SRT
               MOVE SPACES              TO SRT-IND-ICLI IN SRT
               MOVE SPACES              TO SRT-GLS-ICLI IN SRT
           ELSE
               MOVE DBC-NUM-ICLI IN DBC TO SRT-NUM-ICLI IN SRT
               MOVE DBC-VRF-ICLI IN DBC TO SRT-VRF-ICLI IN SRT
               MOVE DBC-IND-ICLI IN DBC TO SRT-IND-ICLI IN SRT
               MOVE DBC-GLS-ICLI IN DBC TO SRT-GLS-ICLI IN SRT.
           IF WSS-CIC-SEL1 = 'O'
               IF WSS-COD-OFIC NOT = DBC-COD-OFIC IN DBC
                   GO TO NXT-INP-SRT
               ELSE
                   NEXT SENTENCE
           ELSE
           IF WSS-CIC-SEL1 = 'E'
               IF WSS-COD-EJEC NOT = DBC-COD-EJEC IN DBC
                   GO TO NXT-INP-SRT.
      *BUS-TAB busca glosa de codigo en tabla
           MOVE REC-COD-TREC IN REC TO REC-COD-TREC IN REC-VARI.
           MOVE SPACES              TO REC-COD-AREC IN REC-VARI.
           MOVE 'SGC' TO TAB-COD-SIST.
           MOVE 'REC' TO TAB-COD-TTAB IN TAB.
           MOVE REC-COD-CTCA IN REC-VARI TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DCOR IN TAB TO SRT-GLS-TREC IN SRT.
      *BUS-TAB busca glosa de codigo en tabla
           IF REC-COD-AREC IN REC > SPACES
               MOVE REC-COD-AREC IN REC TO REC-COD-AREC IN REC-VARI
               MOVE 'SGC' TO TAB-COD-SIST
               MOVE 'REC' TO TAB-COD-TTAB IN TAB
               MOVE REC-COD-CTCA IN REC-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO SRT-GLS-AREC IN SRT
           ELSE
               MOVE SPACES TO SRT-GLS-AREC IN SRT.
           IF REC-FEC-FINI IN REC NOT > ZEROES
               MOVE SPACES TO REC-FEC-FINI IN REC.
           IF REC-FEC-FTER IN REC NOT > ZEROES
               MOVE SPACES TO REC-FEC-FTER IN REC.
           IF REC-FEC-FVAL IN REC NOT > ZEROES
               MOVE SPACES TO REC-FEC-FVAL IN REC.
      *>>>>
       MOV-INP-SRT.
       COPY SGCRMREL.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM SGC-FIO-REC.
      *<<<<
           IF POR-CLIENTE
               IF ( WSS-CIC-ICLI NOT = REC-CIC-ICLI IN REC ) OR
                  ( POR-RELACION AND
                    WSS-COD-TREL NOT = REC-COD-TREC IN REC )
                   GO TO FIN-INP-SRT.
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SGC-FIO-REC.
       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           OPEN OUTPUT RPTREL.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-REL.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<<
           IF WSS-CIC-SEL1 NOT = 'O'
      *BUS-OFI busca oficina en tablas
               MOVE DBC-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIC IN WSS-REL.
      *BUS-TAB busca glosa de codigo en tabla
           IF REC-COD-VCAM IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE REC-COD-VCAM IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VCAM IN WSS-REL
           ELSE
               MOVE SPACES TO WSS-GLS-VCAM IN WSS-REL.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-REL.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-REL.
           CLOSE RPTREL.
       COPY SGCBFREC.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
      *<<<<
       COPY GNSBBTAB.
       COPY GNSBBMSC.
       COPY GNSBFMSC.
       COPY GNSBVMSC.
       COPY GNSBGCPT.
       COPY GNSBVTAB.
       COPY GNSBFTAB.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY TABBBUSR.
       COPY TABBVUSR.
       COPY TABBFUSR.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY TABBFOFI.
       COPY SGCBGCLI.
       COPY SGCBFDBC.
       COPY SGCBPDBC.
      *>>>>
