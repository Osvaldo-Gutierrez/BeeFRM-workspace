       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DEUPLCTT.
       AUTHOR.          CONSIST.
      * PROGRAMA LISTADOR DE REPORTE CTT CON SORT.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *DOS SELECT RPTCTT         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTCTT         ASSIGN TO        UT-S-RPTCTT.
           SELECT RPTCTT         ASSIGN TO SYS011-UR-1403-S.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY DEURRCTT.
       FD  RPTCTT LABEL RECORDS OMITTED
           REPORT IS RPT-CTT
           .
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-FIA-REQA==.
       COPY DEUBRFIA.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGIFD.
       COPY DEURWCTT.
       COPY GNSWGSYS.
      *<<<< WSS
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY TABBROFI.
       COPY TABBRUSR.
       COPY SGCBRDBC.
       COPY DEUBRESC.
       COPY DEUBRAMF.
       COPY GNSWGCPT.
       01  WSS-VARI.
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-DTER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
           03  WSS-FEC-FCMP.
               05  WSS-NUM-SCMP                      PIC 9(02).
               05  WSS-NUM-ACMP                      PIC 9(02).
               05  WSS-NUM-MCMP                      PIC 9(02).
               05  WSS-NUM-DCMP                      PIC 9(02).
           03  WSS-FEC-FFEC.
               05  WSS-NUM-SFEC                      PIC 9(02).
               05  WSS-NUM-AFEC                      PIC 9(02).
               05  WSS-NUM-MFEC                      PIC 9(02).
               05  WSS-NUM-DFEC                      PIC 9(02).
           03  WSS-COD-OFIP             VALUE SPACES PIC X(03).
           03  WSS-IND-TFEC             VALUE SPACES PIC X(01).
           03  WSS-CIC-TFEC             VALUE SPACES PIC X(01).
           03  WSS-IPPH                 VALUE 0      PIC 9(01).
               88  PRIMER-PAGE-HEADING  VALUE 0.
      *DEAD
           03  WSS-COD-TOAX.
               05  WSS-COD-TOOA                      PIC X(03).
               05  WSS-COD-AOOA                      PIC X(03).
      *
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ESC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AMF-REQA==.
      *>>>>
       REPORT SECTION.
      *--------------
       COPY DEURFCTT.
           .
       COPY DEURTCTT.
       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-CTT-ESC-COD-OFEF SECTION.
           USE BEFORE REPORTING CH-CTT-ESC-COD-OFEF.
       INI-CH-CTT-ESC-COD-OFEF.
      *<<<< CH_CTT_ESC_COD_OFEF
      *BUS-OFI busca oficina en tablas
      *DEAD  Cambia FIA-COD-OFFS y GLS-OFFS  por  ESC-COD-OFEF y GLS-OFE
F.
           MOVE ESC-COD-OFEF IN SRT TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFEF IN WSS-CTT.
           MOVE ESC-COD-OFEF IN SRT TO WSS-COD-OFIP IN WSS-CTT.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIP IN WSS-CTT.
      *
      *>>>>
       FIN-CH-CTT-ESC-COD-OFEF.
           EXIT.
       DEC-PH-CTT SECTION.
           USE BEFORE REPORTING PH-CTT.
       INI-PH-CTT.
      *<<<< PH_CTT
           IF PRIMER-PAGE-HEADING
               MOVE 1 TO PRINT-SWITCH
               MOVE 0 TO PAGE-COUNTER
               MOVE 1 TO WSS-IPPH.
      *>>>>
       FIN-PH-CTT.
           EXIT.
       END DECLARATIVES.
       MAIN SECTION.
      *------------
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           MOVE 'DEUPLCTT' TO FIO-PROG.
           PERFORM BUS-FSIS.
           SORT GNSSRT
       COPY DEURSCTT.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           GOBACK.
       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           DISPLAY 'Inicio de Captura de Parametros'.
      *Captura Tipo de Fecha
           DISPLAY 'TIPO FECHA (C:COMPUTACIONAL,R:REAL) : '.
           ACCEPT  WSS-IND-TFEC IN WSS-VARI.
           DISPLAY WSS-IND-TFEC IN WSS-VARI.
      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO (DDMMAAAA) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO (DDMMAAAA) : '.
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFIP IN WSS-VARI.
           DISPLAY WSS-COD-OFIP IN WSS-VARI.
           DISPLAY 'Fin    de Captura    de Parametros'.
           DISPLAY 'Inicio de Validacion de Parametros'.
      *VAL-MSC Valida miscelaneo de tabla : Tipo de Fecha
           DISPLAY WSS-IND-TFEC.
           MOVE 'TAB' TO MSC-COD-SIST.
           MOVE 'TFEC' TO MSC-COD-TMSC IN MSC.
           MOVE WSS-IND-TFEC IN WSS-VARI TO MSC-COD-CMSC IN MSC.
           PERFORM VAL-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC IN MSG
               GOBACK
           ELSE
               MOVE MSC-GLS-DESC IN MSC TO WSS-GLS-TFEC IN WSS-CTT
               MOVE MSC-CIC-CMSC IN MSC TO WSS-CIC-TFEC IN WSS-VARI.
      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N  TO FEC-FBLK.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               GOBACK.
      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N  TO FEC-FBLK.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               GOBACK.
      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB' TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               GOBACK.
      *Mover Fechas a impresion
           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-CTT.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-CTT.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-CTT.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-CTT.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DTER IN WSS-CTT.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MTER IN WSS-CTT.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-STER IN WSS-CTT.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-ATER IN WSS-CTT.
           IF WSS-COD-OFIP IN WSS-VARI > SPACES
      *VAL-OFI Valida Codigo de Oficina en TABLAS
               DISPLAY WSS-COD-OFIP IN WSS-VARI
               MOVE WSS-COD-OFIP IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS NOT = 'COD    NEX'
                   MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIP IN WSS-CTT
                   MOVE WSS-COD-OFIP IN WSS-VARI TO
                                                 WSS-COD-OFIP IN WSS-CTT
               ELSE
                   DISPLAY MSG-GLS-MENS
                   GOBACK.
           DISPLAY 'Fin de Validacion de Parametros'.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
           MOVE FIA-CIC-IESC IN FIA TO ESC-CIC-IESC IN ESC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DEU-FIO-ESC.
           IF WSS-CIC-TFEC IN WSS-VARI = 'R'
               MOVE ESC-FEC-FCTT IN ESC TO WSS-FEC-FCMP IN WSS-VARI
           ELSE
               MOVE ESC-FEC-FINP IN ESC TO WSS-FEC-FCMP IN WSS-VARI.
           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DFEC.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MFEC.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SFEC.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AFEC.
           IF WSS-FEC-FFEC IN WSS-VARI > WSS-FEC-FCMP IN WSS-VARI
               GO TO NXT-INP-SRT.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.
           IF WSS-FEC-FCMP IN WSS-VARI > WSS-FEC-FFEC IN WSS-VARI
               GO TO NXT-INP-SRT.
      *DEAD  Cambia FIA-COD-OFFS  por  ESC-COD-OFEF.
           IF NOT (WSS-COD-OFIP IN WSS-VARI = SPACES OR
                   WSS-COD-OFIP IN WSS-VARI = ESC-COD-OFEF IN ESC)
               GO TO NXT-INP-SRT.
      *
      *>>>>
       MOV-INP-SRT.
       COPY DEURMCTT.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           OPEN OUTPUT RPTCTT.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-CTT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
      *BUS-OFI busca oficina en tablas
      *DEAD  Cambia FIA-COD-OFFS y GLS-OFFS  por  ESC-COD-OFEF y GLS-OFE
F.
           MOVE ESC-COD-OFEF IN SRT TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFEF IN WSS-CTT.
           MOVE ESC-COD-OFEF IN SRT TO WSS-COD-OFIP IN WSS-CTT.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIP IN WSS-CTT.
      *
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *Busqueda de glosas de Fianza
      *BUS-MSC busca glosa de miscelaneo en tabla
           IF FIA-MSC-TVLF IN SRT > SPACES
               MOVE 'TAB' TO MSC-COD-SIST
               MOVE 'TVAL' TO MSC-COD-TMSC IN MSC
               MOVE FIA-MSC-TVLF IN SRT TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DESC IN MSC TO WSS-GLS-TVLF IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-TVLF IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF FIA-COD-TOFS IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'TIO' TO TAB-COD-TTAB IN TAB
               MOVE FIA-COD-TOFS IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-TOFS IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-TOFS IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF ESC-COD-ABOC IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'ABG' TO TAB-COD-TTAB IN TAB
               MOVE ESC-COD-ABOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-ABOC IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-ABOC IN WSS-CTT.
      *BUS-USR busca usuario en tablas
           IF ESC-COD-RCTT IN SRT > SPACES
               MOVE ESC-COD-RCTT IN SRT TO USR-COD-USER IN USR
               PERFORM BUS-USR
               MOVE USR-GLS-DESC IN USR TO WSS-GLS-EJEC IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-EJEC IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF ESC-COD-NOTC IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'NOT' TO TAB-COD-TTAB IN TAB
               MOVE ESC-COD-NOTC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-NOTC IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-NOTC IN WSS-CTT.
      *CIC-CLI Busca Cliente por medio de CIC
           MOVE FIA-CIC-ICLI IN SRT TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE SPACES              TO WSS-NUM-RUTD IN WSS-CTT
               MOVE SPACES              TO WSS-VRF-RUTD IN WSS-CTT
               MOVE SPACES              TO WSS-IND-RUTD IN WSS-CTT
               MOVE SPACES              TO WSS-GLS-RUTD IN WSS-CTT
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO WSS-GLS-NOMD IN WSS-CTT
               MOVE DBC-NUM-ICLI IN DBC TO WSS-NUM-RUTD IN WSS-CTT
               MOVE DBC-VRF-ICLI IN DBC TO WSS-VRF-RUTD IN WSS-CTT
               MOVE DBC-IND-ICLI IN DBC TO WSS-IND-RUTD IN WSS-CTT
               MOVE DBC-GLS-ICLI IN DBC TO WSS-GLS-RUTD IN WSS-CTT.
      *CIC-CLI Busca Cliente por medio de CIC
           MOVE FIA-CIC-ICFS IN SRT TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE SPACES              TO WSS-NUM-RUTF IN WSS-CTT
               MOVE SPACES              TO WSS-VRF-RUTF IN WSS-CTT
               MOVE SPACES              TO WSS-IND-RUTF IN WSS-CTT
               MOVE SPACES              TO WSS-GLS-RUTF IN WSS-CTT
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO WSS-GLS-NOMF IN WSS-CTT
               MOVE DBC-NUM-ICLI IN DBC TO WSS-NUM-RUTF IN WSS-CTT
               MOVE DBC-VRF-ICLI IN DBC TO WSS-VRF-RUTF IN WSS-CTT
               MOVE DBC-IND-ICLI IN DBC TO WSS-IND-RUTF IN WSS-CTT
               MOVE DBC-GLS-ICLI IN DBC TO WSS-GLS-RUTF IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF FIA-COD-VCLF IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE FIA-COD-VCLF IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-VCLF IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-VCLF IN WSS-CTT.
      *Fin busqueda de glosas de Fianza
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-CTT.
      *<<<< FIN_GEN_OUT
           MOVE FIA-CIC-IESC IN SRT TO AMF-CIC-IESC IN AMF.
           MOVE FIA-NUM-IFIA IN SRT TO AMF-NUM-IFIA IN AMF.
           MOVE 0                   TO AMF-NUM-IAMF IN AMF.
           MOVE FIO-GET-NLS TO FIO-CMND.
       GET-NXT-AMF.
           PERFORM DEU-FIO-AMF.
           IF NOT ( FIO-STAT-OKS AND
                    AMF-CIC-IESC IN AMF = FIA-CIC-IESC IN SRT AND
                    AMF-NUM-IFIA IN AMF = FIA-NUM-IFIA IN SRT)
               GENERATE DL-FIN
               GO TO NXT-OUT-SRT.
      *Busqueda de glosas de Ambito
      *BUS-MSC busca glosa de miscelaneo en tabla
           IF AMF-MSC-TVOA IN AMF > SPACES
               MOVE 'TAB' TO MSC-COD-SIST
               MOVE 'TVAL' TO MSC-COD-TMSC IN MSC
               MOVE AMF-MSC-TVOA IN AMF TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DESC IN MSC TO WSS-GLS-TVOA IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-TVOA IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF AMF-COD-TOOA IN AMF > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'TIO' TO TAB-COD-TTAB IN TAB
               MOVE AMF-COD-TOOA IN AMF TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-TOOA IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-TOOA IN WSS-CTT.
      *BUS-OFI busca oficina en tablas
           IF AMF-COD-OFOA IN AMF > SPACES
               MOVE AMF-COD-OFOA IN AMF TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFOA IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-OFOA IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF AMF-COD-VCOA IN AMF > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE AMF-COD-VCOA IN AMF TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-VCOA IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-VCOA IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF AMF-COD-AOOA IN AMF > SPACES
               MOVE 'COL' TO TAB-COD-SIST
               MOVE 'AUX' TO TAB-COD-TTAB IN TAB
      *DEAD
               MOVE AMF-COD-TOOA IN AMF      TO WSS-COD-TOOA IN WSS-VARI
               MOVE AMF-COD-AOOA IN AMF      TO WSS-COD-AOOA IN WSS-VARI
               MOVE WSS-COD-TOAX IN WSS-VARI TO TAB-COD-CTAB IN TAB
      *
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-AOOA IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-AOOA IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF AMF-COD-CDOA IN AMF > SPACES
               MOVE 'COL' TO TAB-COD-SIST
               MOVE 'DES' TO TAB-COD-TTAB IN TAB
               MOVE AMF-COD-CDOA IN AMF TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-CDOA IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-CDOA IN WSS-CTT.
      *BUS-TAB busca glosa de codigo en tabla
           IF AMF-COD-VCLA IN AMF > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE AMF-COD-VCLA IN AMF TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-VCLA IN WSS-CTT
           ELSE
               MOVE SPACES TO WSS-GLS-VCLA IN WSS-CTT.
      *Fin busqueda de glosas de Ambito
           GENERATE DL-AMF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO GET-NXT-AMF.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-CTT.
           CLOSE RPTCTT.
       COPY DEUBFFIA.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
      *<<<< EOF
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSIOPRM.
       COPY GNSBBMSC.
       COPY GNSBVMSC.
       COPY GNSBFMSC.
       COPY GNSIOMSL.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSIOMSJ.
       COPY TABBBUSR.
       COPY TABBFUSR.
       COPY TABBBOFI.
       COPY TABBFOFI.
       COPY TABBVOFI.
       COPY SGCBFDBC.
       COPY SGCBGCLI.
       COPY DEUBFESC.
       COPY DEUBFAMF.
       COPY GNSBGCPT.
      *>>>>
