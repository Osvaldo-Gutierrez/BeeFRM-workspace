       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DEUPLALZ.
       AUTHOR.          CONSIST.
      * PROGRAMA LISTADOR DE REPORTE ALZ CON SORT.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *DOS SELECT RPTALZ         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTALZ         ASSIGN TO        UT-S-RPTALZ.
           SELECT RPTALZ         ASSIGN TO SYS011-UR-1403-S.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY DEURRALZ.
       FD  RPTALZ LABEL RECORDS OMITTED
           REPORT IS RPT-ALZ
           .
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-FIA-REQA==.
       COPY DEUBRFIA.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGIFD.
       COPY DEURWALZ.
       COPY GNSWGSYS.
      *<<<< WSS
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY TABBRUSR.
       COPY TABBROFI.
       COPY SGCBRDBC.
       COPY DEUBRESC.
       COPY DEUBRAMF.
       COPY GNSWGCPT.
       01  WSS-VARI.
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-DTER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).
           03  WSS-FEC-FCMP.
               05  WSS-NUM-SCMP                      PIC 9(02).
               05  WSS-NUM-ACMP                      PIC 9(02).
               05  WSS-NUM-MCMP                      PIC 9(02).
               05  WSS-NUM-DCMP                      PIC 9(02).
           03  WSS-FEC-FFEC.
               05  WSS-NUM-SFEC                      PIC 9(02).
               05  WSS-NUM-AFEC                      PIC 9(02).
               05  WSS-NUM-MFEC                      PIC 9(02).
               05  WSS-NUM-DFEC                      PIC 9(02).
           03  WSS-IND-EFIA             VALUE SPACES PIC X(01).
           03  WSS-CIC-EFIA             VALUE SPACES PIC X(01).
           03  WSS-IND-TFEC             VALUE SPACES PIC X(01).
           03  WSS-CIC-TFEC             VALUE SPACES PIC X(01).
           03  WSS-COD-OFIP             VALUE SPACES PIC X(03).
           03  WSS-IPPH                 VALUE 0      PIC 9(01).
               88  PRIMER-PAGE-HEADING  VALUE 0.
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ESC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AMF-REQA==.
      *>>>>
       REPORT SECTION.
      *--------------
       COPY DEURFALZ.
           .
       COPY DEURTALZ.
       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-ALZ-ESC-COD-OFEF SECTION.
           USE BEFORE REPORTING CH-ALZ-ESC-COD-OFEF.
       INI-CH-ALZ-ESC-COD-OFEF.
      *<<<< CH_ALZ_ESC_COD_OFEF
      *BUS-OFI busca oficina en tablas
      *DEAD  Cambia FIA-COD-OFFS y GLS-OFFS  por  ESC-COD-OFEF y GLS-OFE
F.
           MOVE ESC-COD-OFEF IN SRT TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFEF IN WSS-ALZ.
           MOVE ESC-COD-OFEF IN SRT TO WSS-COD-OFIP IN WSS-ALZ.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIP IN WSS-ALZ.
      *
      *>>>>
       FIN-CH-ALZ-ESC-COD-OFEF.
           EXIT.
       DEC-PH-ALZ SECTION.
           USE BEFORE REPORTING PH-ALZ.
       INI-PH-ALZ.
      *<<<< PH_ALZ
           IF PRIMER-PAGE-HEADING
               MOVE 1 TO PRINT-SWITCH
               MOVE 0 TO PAGE-COUNTER
               MOVE 1 TO WSS-IPPH.
      *>>>>
       FIN-PH-ALZ.
           EXIT.
       END DECLARATIVES.
       MAIN SECTION.
      *------------
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           MOVE 'DEUPLALZ' TO FIO-PROG.
           PERFORM BUS-FSIS.
           SORT GNSSRT
       COPY DEURSALZ.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           GOBACK.
       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           DISPLAY 'Inicio de Captura de Parametros'.
      *Captura Estado de Fianza
           DISPLAY 'ESTADO FIANZA (T:ALZADAS,S:SOLICITADAS, '.
           DISPLAY '                         A:AUTORIZADAS) : '.
           ACCEPT  WSS-IND-EFIA IN WSS-VARI.
           DISPLAY WSS-IND-EFIA IN WSS-VARI.
      *Captura Tipo de Fecha
           DISPLAY 'TIPO FECHA (C:COMPUTACIONAL,R:REAL) : '.
           ACCEPT  WSS-IND-TFEC IN WSS-VARI.
           DISPLAY WSS-IND-TFEC IN WSS-VARI.
      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMAAAA ) : '
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFIP IN WSS-VARI.
           DISPLAY WSS-COD-OFIP IN WSS-VARI.
           DISPLAY 'Fin    de Captura    de Parametros'.
           DISPLAY 'Inicio de Validacion de Parametros'.
      *VAL-MSC Valida miscelaneo de tabla : Estado de Fianza
           DISPLAY WSS-IND-EFIA IN WSS-VARI.
           MOVE 'DEU' TO MSC-COD-SIST.
           MOVE 'EFIA' TO MSC-COD-TMSC IN MSC.
           MOVE WSS-IND-EFIA IN WSS-VARI TO MSC-COD-CMSC IN MSC.
           PERFORM VAL-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC IN MSG
               GOBACK
           ELSE
               MOVE MSC-GLS-DESC IN MSC TO WSS-GLS-TITU IN WSS-ALZ
               MOVE MSC-CIC-CMSC IN MSC TO WSS-CIC-EFIA IN WSS-VARI.
           IF WSS-CIC-EFIA IN WSS-VARI = 'T'
      *VAL-MSC Valida miscelaneo de tabla : Tipo de Fecha
               DISPLAY WSS-IND-TFEC IN WSS-VARI
               MOVE 'TAB' TO MSC-COD-SIST
               MOVE 'TFEC' TO MSC-COD-TMSC IN MSC
               MOVE WSS-IND-TFEC IN WSS-VARI TO MSC-COD-CMSC IN MSC
               PERFORM VAL-MSC
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   GOBACK
               ELSE
                   MOVE MSC-GLS-DESC IN MSC TO WSS-GLS-TFEC IN WSS-ALZ
                   MOVE MSC-CIC-CMSC IN MSC TO WSS-CIC-TFEC IN WSS-VARI.
      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               GOBACK.
      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               GOBACK.
      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB' TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               GOBACK.
           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-ALZ.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-ALZ.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-ALZ.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-ALZ.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DTER IN WSS-ALZ.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MTER IN WSS-ALZ.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-STER IN WSS-ALZ.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-ATER IN WSS-ALZ.
      *DEAD  IF WSS-COD-OFIP IN WSS-VARI NOT = SPACES
           IF WSS-COD-OFIP IN WSS-VARI > SPACES
      *
      *VAL-OFI Valida codigo de Oficina en TABLAS
               DISPLAY WSS-COD-OFIP IN WSS-VARI
               MOVE WSS-COD-OFIP IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS NOT = 'COD    NEX'
                   MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIP IN WSS-ALZ
                   MOVE WSS-COD-OFIP IN WSS-VARI TO
                                                 WSS-COD-OFIP IN WSS-ALZ
               ELSE
                   DISPLAY MSG-GLS-MENS
                   GOBACK.
           DISPLAY 'Fin de Validacion de Parametros'.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
           IF WSS-CIC-EFIA IN WSS-VARI  = 'T' AND
              FIA-FEC-FALZ IN FIA       > ZEROES
               IF WSS-CIC-TFEC IN WSS-VARI = 'R'
                   MOVE FIA-FEC-FALZ IN FIA TO WSS-FEC-FCMP IN WSS-VARI
               ELSE
                   MOVE FIA-FEC-FOUT IN FIA TO WSS-FEC-FCMP IN WSS-VARI
           ELSE
           IF WSS-CIC-EFIA IN WSS-VARI = 'A' AND
              FIA-FEC-FAUT IN FIA      > ZEROES AND
              FIA-FEC-FALZ IN FIA  NOT > ZEROES
               MOVE FIA-FEC-FAUT IN FIA TO WSS-FEC-FCMP IN WSS-VARI
           ELSE
           IF WSS-CIC-EFIA IN WSS-VARI = 'S' AND
              FIA-FEC-FSOL IN FIA      > ZEROES AND
              FIA-FEC-FAUT IN FIA  NOT > ZEROES AND
              FIA-FEC-FALZ IN FIA  NOT > ZEROES
               MOVE FIA-FEC-FSOL IN FIA TO WSS-FEC-FCMP IN WSS-VARI
           ELSE
               GO TO NXT-INP-SRT.
           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.
           IF WSS-FEC-FFEC IN WSS-VARI > WSS-FEC-FCMP IN WSS-VARI
               GO TO NXT-INP-SRT.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.
           IF WSS-FEC-FCMP IN WSS-VARI > WSS-FEC-FFEC IN WSS-VARI
               GO TO NXT-INP-SRT.
           MOVE FIA-CIC-IESC IN FIA TO ESC-CIC-IESC IN ESC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DEU-FIO-ESC.
      *DEAD  Cambia FIA-COD-OFFS  por  ESC-COD-OFEF.
           IF NOT (WSS-COD-OFIP IN WSS-VARI = SPACES OR
                   WSS-COD-OFIP IN WSS-VARI = ESC-COD-OFEF IN ESC)
               GO TO NXT-INP-SRT.
      *
      *>>>>
       MOV-INP-SRT.
       COPY DEURMALZ.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DEU-FIO-FIA.
       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           OPEN OUTPUT RPTALZ.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-ALZ.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
      *BUS-OFI busca oficina en tablas
      *DEAD  Cambia FIA-COD-OFFS y GLS-OFFS  por  ESC-COD-OFEF y GLS-OFE
F.
           MOVE ESC-COD-OFEF IN SRT TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFEF IN WSS-ALZ.
           MOVE ESC-COD-OFEF IN SRT TO WSS-COD-OFIP IN WSS-ALZ.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIP IN WSS-ALZ.
      *
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *Busqueda de glosas de Fianza
      *BUS-TAB busca glosa de codigo en tabla
           IF FIA-COD-VCLF IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE FIA-COD-VCLF IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-VCLF IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-VCLF IN WSS-ALZ.
      *BUS-TAB busca glosa de codigo en tabla
           IF ESC-COD-NOTC IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'NOT' TO TAB-COD-TTAB IN TAB
               MOVE ESC-COD-NOTC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-NOTC IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-NOTC IN WSS-ALZ.
      *BUS-USR busca usuario en tablas
           IF ESC-COD-RCTT IN SRT > SPACES
               MOVE ESC-COD-RCTT IN SRT TO USR-COD-USER IN USR
               PERFORM BUS-USR
               MOVE USR-GLS-DESC IN USR TO WSS-GLS-EJEC IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-EJEC IN WSS-ALZ.
      *BUS-TAB busca glosa de codigo en tabla
           IF ESC-COD-ABOC IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'ABG' TO TAB-COD-TTAB IN TAB
               MOVE ESC-COD-ABOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-ABOC IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-ABOC IN WSS-ALZ.
      *CIC-CLI Busca Cliente por medio de CIC
           MOVE FIA-CIC-ICLI IN SRT TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE SPACES              TO WSS-NUM-RUTD IN WSS-ALZ
               MOVE SPACES              TO WSS-VRF-RUTD IN WSS-ALZ
               MOVE SPACES              TO WSS-IND-RUTD IN WSS-ALZ
               MOVE SPACES              TO WSS-GLS-RUTD IN WSS-ALZ
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO WSS-GLS-NOMD IN WSS-ALZ
               MOVE DBC-NUM-ICLI IN DBC TO WSS-NUM-RUTD IN WSS-ALZ
               MOVE DBC-VRF-ICLI IN DBC TO WSS-VRF-RUTD IN WSS-ALZ
               MOVE DBC-IND-ICLI IN DBC TO WSS-IND-RUTD IN WSS-ALZ
               MOVE DBC-GLS-ICLI IN DBC TO WSS-GLS-RUTD IN WSS-ALZ.
      *CIC-CLI Busca Cliente por medio de CIC
           MOVE FIA-CIC-ICFS IN SRT TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE SPACES              TO WSS-NUM-RUTF IN WSS-ALZ
               MOVE SPACES              TO WSS-VRF-RUTF IN WSS-ALZ
               MOVE SPACES              TO WSS-IND-RUTF IN WSS-ALZ
               MOVE SPACES              TO WSS-GLS-RUTF IN WSS-ALZ
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO WSS-GLS-NOMF IN WSS-ALZ
               MOVE DBC-NUM-ICLI IN DBC TO WSS-NUM-RUTF IN WSS-ALZ
               MOVE DBC-VRF-ICLI IN DBC TO WSS-VRF-RUTF IN WSS-ALZ
               MOVE DBC-IND-ICLI IN DBC TO WSS-IND-RUTF IN WSS-ALZ
               MOVE DBC-GLS-ICLI IN DBC TO WSS-GLS-RUTF IN WSS-ALZ.
      *BUS-MSC busca glosa de miscelaneo en tabla
           IF FIA-MSC-TVLF IN SRT > SPACES
               MOVE 'TAB' TO MSC-COD-SIST
               MOVE 'TVAL' TO MSC-COD-TMSC IN MSC
               MOVE FIA-MSC-TVLF IN SRT TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DESC IN MSC TO WSS-GLS-TVLF IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-TVLF IN WSS-ALZ.
      *BUS-TAB busca glosa de codigo en tabla
           IF FIA-COD-TOFS IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'TIO' TO TAB-COD-TTAB IN TAB
               MOVE FIA-COD-TOFS IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-TOFS IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-TOFS IN WSS-ALZ.
      *BUS-USR busca usuario en tablas
           IF FIA-COD-RAUT IN SRT > SPACES
               MOVE FIA-COD-RAUT IN SRT TO USR-COD-USER IN USR
               PERFORM BUS-USR
               MOVE USR-GLS-DESC IN USR TO WSS-GLS-RAUT IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-RAUT IN WSS-ALZ.
      *DEAD Cambia Busqueda en T. USR (BUS-USR)  por  T. ABG (BUS-TAB).
      *BUS-TAB busca glosa de codigo en tabla
           IF FIA-COD-ABOA IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'ABG' TO TAB-COD-TTAB IN TAB
               MOVE FIA-COD-ABOA IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-ABOA IN WSS-ALZ
           ELSE
               MOVE SPACES TO WSS-GLS-ABOA IN WSS-ALZ.
      *
      *Fin busqueda de glosas de Fianza
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-ALZ.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-ALZ.
           CLOSE RPTALZ.
       COPY DEUBFFIA.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
      *<<<< EOF
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSIOPRM.
       COPY GNSBBMSC.
       COPY GNSBVMSC.
       COPY GNSBFMSC.
       COPY GNSIOMSL.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSIOMSJ.
       COPY TABBBUSR.
       COPY TABBFUSR.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY TABBFOFI.
       COPY SGCBFDBC.
       COPY SGCBGCLI.
       COPY DEUBFESC.
       COPY DEUBFAMF.
       COPY GNSBGCPT.
      *>>>>
