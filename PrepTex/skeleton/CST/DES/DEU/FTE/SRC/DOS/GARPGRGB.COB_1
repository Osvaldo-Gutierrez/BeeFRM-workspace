       IDENTIFICATION DIVISION.
      *------------------------
       PROGRAM-ID.    GARPGRGB.
       AUTHOR.        GERARDO URETA Q.
       DATE-WRITTEN.  AUG,19 1988.
       DATE-COMPILED.

      *****************************************************************
      * SUBRUTINA QUE OBTIENE UN RESUMEN DE LAS GARANTIAS DE UN DEUDOR*
      *                                                               *
      * ESTAS SON DEVUELTAS EN UN REGISTRO CON 100 OCURRENCIAS        *
      *****************************************************************
      *
      * MODIFICACIONES:
      * - SE ENTREGAN CONVERTIDOS A PESOS LOS MONTOS :
      *   VALOR COMERCIAL, VALOR LIQUIDACION Y VALOR AJUSTADO SUPER.
      *   ===  SENC 10/05/89  ===
      *
      * CONVERTIDO POR CONSIST A BASE DE DATOS NUEVO SIST GARANTIAS
      *   20/ABR/92
      ****************************************************************
       ENVIRONMENT DIVISION.
      *---------------------

       DATA DIVISION.
      *--------------

       WORKING-STORAGE SECTION.
      *------------------------

       77  I                        PIC 9(03)   VALUE 0.
       77  J                        PIC 9(03)   VALUE 0.

       01  VAR-CALCULOS.
           05  MONTO-ENT            PIC S9(14)      COMP-3  VALUE +0.
           05  VAL-CML-PESOS        PIC S9(14)      COMP-3  VALUE +0.
           05  VAL-LIQ-PESOS        PIC S9(14)      COMP-3  VALUE +0.
           05  VAL-AJT-PESOS        PIC S9(14)      COMP-3  VALUE +0.

       01  FECHA-W.
           03  MES-W                PIC 9(02).
           03  FILLER               PIC X(01) VALUE '/'.
           03  DIA-W                PIC 9(02).
           03  FILLER               PIC X(01) VALUE '/'.
           03  AGNO-W               PIC 9(02).

       01  FECHA.
           03  SIGLO                PIC 9(02) VALUE 19.
           03  AGNO                 PIC 9(02).
           03  MES                  PIC 9(02).
           03  DIA                  PIC 9(02).

       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.

       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.

       COPY GNSBRTAB.
       COPY TABBRCAM.
       COPY GARBRTAG.
       COPY GARBRGDD.
       COPY GARBRGDG.
       COPY GARBRGVT.
       COPY SGCBRDBC.

       EJECT

       01    WSS-VARI.
             03 WSS-COD-PESO                    PIC X(06).

       01  WOR-PARAMETROS.
           03 GRGA-ID-W                  PIC X(12).
           03 GRGA-STATUS-W              PIC X(12).
           03 GRGA-RESUMEN-W  OCCURS 100 TIMES.
              05 GRGA-KEY-IRGA-W.
                 07 GRGA-IND-REAL-W      PIC X(02).
                 07 GRGA-COD-COGA-W.
                    09 GRGA-COD-TOGA-W   PIC X(03).
                    09 GRGA-COD-AOGA-W   PIC X(03).
                 07 GRGA-IND-CGAR-W      PIC X(01).
                 07 GRGA-COD-VCGA-W.
                    09 GRGA-MSC-TVGA-W   PIC X(01).
                    09 GRGA-COD-CVGA-W   PIC X(03).
                    09 GRGA-COD-TCGA-W   PIC X(02).
                 07 GRGA-COD-OFGA-W      PIC X(03).
              05 GRGA-VAL-COME-W         PIC 9(11)V9(4) COMP-3.
              05 GRGA-VAL-LIQU-W         PIC 9(11)V9(4) COMP-3.
              05 GRGA-VAL-POND-W         PIC 9(11)V9(4) COMP-3.

       LINKAGE SECTION.
      *----------------

       01  LNK-PARAMETROS.
           03 GRGA-ID-L                  PIC X(12).
           03 GRGA-STATUS-L              PIC X(12).
           03 GRGA-RESUMEN-L  OCCURS 100 TIMES.
              05 GRGA-KEY-IRGA-L.
                 07 GRGA-IND-REAL-L      PIC X(02).
                 07 GRGA-COD-COGA-L.
                    09 GRGA-COD-TOGA-L   PIC X(03).
                    09 GRGA-COD-AOGA-L   PIC X(03).
                 07 GRGA-IND-CGAR-L      PIC X(01).
                 07 GRGA-COD-VCGA-L.
                    09 GRGA-MSC-TVGA-L   PIC X(01).
                    09 GRGA-COD-CVGA-L   PIC X(03).
                    09 GRGA-COD-TCGA-L   PIC X(02).
                 07 GRGA-COD-OFGA-L      PIC X(03).
              05 GRGA-VAL-COME-L         PIC 9(11)V9(4) COMP-3.
              05 GRGA-VAL-LIQU-L         PIC 9(11)V9(4) COMP-3.
              05 GRGA-VAL-POND-L         PIC 9(11)V9(4) COMP-3.

       EJECT
       PROCEDURE DIVISION USING LNK-PARAMETROS.
      *------------------

       COPY GNSBGEDB.
           MOVE 'GARPGRGB' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           PERFORM OBT-COD-MNAC.

           MOVE LNK-PARAMETROS TO WOR-PARAMETROS.

           MOVE SPACES         TO GRGA-STATUS-W.

           PERFORM INICIALIZAR VARYING J FROM 1 BY 1 UNTIL J > 100.

           MOVE CURRENT-DATE  TO FECHA-W.
           MOVE DIA-W         TO DIA.
           MOVE MES-W         TO MES.
           MOVE AGNO-W        TO AGNO.


           MOVE GRGA-ID-W      TO DBC-IDC-ICLI.
           MOVE 'DBC-IDC-ICLI' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF FIO-STAT-OKS
               MOVE DBC-CIC-ICLI           TO GDD-CIC-ICLI
           ELSE
               MOVE 'NOTFOUND DBC' TO GRGA-STATUS-W
               PERFORM RETORNO.

           MOVE SPACES       TO GAR-CLV-GDD-1 IN GDD.
           MOVE DBC-CIC-ICLI TO GDD-CIC-ICLI.
           MOVE FIO-GET-NLS  TO FIO-CMND.
           PERFORM GAR-FIO-GDD.
           IF NOT FIO-STAT-OKS OR
              GDD-CIC-ICLI IN GDD NOT = DBC-CIC-ICLI IN DBC
              MOVE 'NOTFOUND GDD' TO GRGA-STATUS-W
              PERFORM RETORNO.

           MOVE 0 TO I.

           PERFORM GENERO-INFORME UNTIL NOT FIO-STAT-OKS OR
                      GDD-CIC-ICLI IN GDD NOT = DBC-CIC-ICLI IN DBC OR
                      GRGA-STATUS-W NOT = SPACES.

           PERFORM RETORNO.

       INICIALIZAR SECTION.
      *--------------------

           MOVE SPACES TO GRGA-KEY-IRGA-W(J).
           MOVE ZEROES TO GRGA-VAL-COME-W(J)
                          GRGA-VAL-LIQU-W(J)
                          GRGA-VAL-POND-W(J).

       FIN-INICIALIZAR.
           EXIT.

       COMPARO SECTION.
      *----------------

      *    PARRAFO NULO.

       FIN-COMPARO.
           EXIT.

       GENERO-INFORME SECTION.
      *-----------------------


           MOVE +0  TO    VAL-CML-PESOS
                          VAL-LIQ-PESOS
                          VAL-AJT-PESOS.

           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GDG.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GDG.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
              GO TO LEO-SGTE-GDD.

           IF GAR-IND-CPL NOT = 'S' OR
              GAR-ES1 < '10' OR GAR-ES1 > '19'
              GO TO LEO-SGTE-GDD.
           IF (GAR-TIP-OPE IN GDG = '952' AND
              (GAR-STP-OPE IN GDG > '079' AND < '084')) OR

              (GAR-TIP-OPE IN GDG = '954' AND
               GAR-STP-OPE IN GDG = '090')
              GO TO SIGO-PROC.

           IF GAR-FEC-VTO < FECHA
              GO TO LEO-SGTE-GDD.
       SIGO-PROC.
           MOVE GAR-NUM-SIS IN GDG TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GDG TO GAR-COD-CRR IN GVT.
           MOVE GAR-COD-TSN IN GDG TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               GO TO LEO-SGTE-GDD.

           MOVE GAR-TIP-OPE IN GDG TO GAR-TIP-OPE IN TAG.
           MOVE GAR-STP-OPE IN GDG TO GAR-STP-OPE IN TAG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
               MOVE 'NOTFOUND TAG' TO GRGA-STATUS-W
           ELSE
              IF I > 0
                 PERFORM COMPARO VARYING J FROM 1 BY 1
                         UNTIL J > I OR J > 100 OR
                        (GRGA-COD-TOGA-W(J) = GAR-TIP-OPE IN GDG AND
                         GRGA-COD-AOGA-W(J) = GAR-STP-OPE IN GDG AND
                         GRGA-IND-CGAR-W(J) = GAR-COD-LIM IN GDG AND
                         GRGA-COD-VCGA-W(J) = GAR-VCB     IN GDG AND
                         GRGA-COD-OFGA-W(J) = GAR-COD-OFI IN GDG)
                 PERFORM REVISO-1
              ELSE
                 ADD 1 TO I

                 PERFORM CONVERTIR-A-PESOS

                 MOVE GAR-TIP-OPE IN GDG TO GRGA-COD-TOGA-W(I)
                 MOVE GAR-STP-OPE IN GDG TO GRGA-COD-AOGA-W(I)
                 MOVE GAR-COD-LIM IN GDG TO GRGA-IND-CGAR-W(I)
                 MOVE GAR-VCB     IN GDG TO GRGA-COD-VCGA-W(I)
                 MOVE GAR-COD-OFI IN GDG TO GRGA-COD-OFGA-W(I)

                 MOVE   VAL-CML-PESOS       TO GRGA-VAL-COME-W(I)
                 MOVE   VAL-LIQ-PESOS       TO GRGA-VAL-LIQU-W(I)
                 MOVE   VAL-AJT-PESOS       TO GRGA-VAL-POND-W(I)

                 IF GAR-TIP-REA-NRE IN TAG = 'N'
                 THEN MOVE 'NR'         TO GRGA-IND-REAL-W (I)
                 ELSE MOVE GAR-TIP-REA-NRE IN TAG
                                        TO GRGA-IND-REAL-W(I).

       LEO-SGTE-GDD.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GDD.

       FIN-GENERO-INFORME.
           EXIT.

       REVISO-1 SECTION.
           IF J NOT > I

              PERFORM CONVERTIR-A-PESOS

              COMPUTE GRGA-VAL-COME-W(J) = GRGA-VAL-COME-W(J) +
                                        VAL-CML-PESOS
              COMPUTE GRGA-VAL-LIQU-W(J) = GRGA-VAL-LIQU-W(J) +
                                        VAL-LIQ-PESOS
              COMPUTE GRGA-VAL-POND-W(J) = GRGA-VAL-POND-W(J) +
                                        VAL-AJT-PESOS
           ELSE
              IF J NOT > 100

                 PERFORM CONVERTIR-A-PESOS

                 ADD 1 TO I
                 MOVE GAR-TIP-OPE IN GDG TO GRGA-COD-TOGA-W(I)
                 MOVE GAR-STP-OPE IN GDG TO GRGA-COD-AOGA-W(I)
                 MOVE GAR-COD-LIM IN GDG TO GRGA-IND-CGAR-W(I)
                 MOVE GAR-VCB     IN GDG TO GRGA-COD-VCGA-W(I)
                 MOVE GAR-COD-OFI IN GDG TO GRGA-COD-OFGA-W(I)
                 MOVE   VAL-CML-PESOS       TO GRGA-VAL-COME-W(I)
                 MOVE   VAL-LIQ-PESOS       TO GRGA-VAL-LIQU-W(I)
                 MOVE   VAL-AJT-PESOS       TO GRGA-VAL-POND-W(I)

                 IF GAR-TIP-REA-NRE IN TAG = 'N'
                    MOVE 'NR'  TO GRGA-IND-REAL-W(I)
                 ELSE
                    MOVE GAR-TIP-REA-NRE IN TAG TO GRGA-IND-REAL-W(I)
              ELSE
                 MOVE 'SOBRE 100 RG' TO GRGA-STATUS-W.

       FIN-REVISO-1.
           EXIT.

       CONVERTIR-A-PESOS SECTION.
      *...........................

           IF GAR-VCB  IN GDG  = WSS-COD-PESO IN WSS-VARI
           THEN  MOVE GAR-VAL-CML-TSN IN  GVT  TO   VAL-CML-PESOS
                 MOVE GAR-VAL-LIQ-TSN IN  GVT  TO   VAL-LIQ-PESOS
                 MOVE GAR-VAL-AJT-SUP IN  GVT  TO   VAL-AJT-PESOS
                 GO TO FIN-CONVERTIR-A-PESOS.

           MOVE GAR-FA-RVL   IN GVT      TO FEC-FANO.
           MOVE FEC-SVLD                 TO CAM-NUM-SCAM IN CAM(1).
           MOVE FEC-AVLD                 TO CAM-NUM-ACAM IN CAM(1).
           MOVE GAR-FM-RVL   IN GVT      TO CAM-NUM-MCAM IN CAM(1).
           MOVE GAR-FD-RVL   IN GVT      TO CAM-NUM-DCAM IN CAM(1).
           MOVE CAM-FEC-FCAM IN CAM(1)   TO CAM-FEC-FCAM IN CAM(2).
           MOVE GAR-VCB      IN GDG      TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT (
              FIO-STAT-OKS AND CAM-IND-VIGE IN CAM = 'S'
              )
               MOVE 'NOTFOUND CAM'       TO GRGA-STATUS-W
               GOBACK.

           COMPUTE   VAL-CML-PESOS ROUNDED =
                         GAR-VAL-CML-TSN IN GVT *  CAM-SGV-VCAM IN CAM.

           COMPUTE   VAL-LIQ-PESOS ROUNDED =
                         GAR-VAL-LIQ-TSN IN GVT *  CAM-SGV-VCAM IN CAM.

           COMPUTE   VAL-AJT-PESOS ROUNDED =
                         GAR-VAL-AJT-SUP IN GVT *  CAM-SGV-VCAM IN CAM.


       FIN-CONVERTIR-A-PESOS.
           EXIT.

       RETORNO SECTION.
      *----------------

           MOVE WOR-PARAMETROS TO LNK-PARAMETROS.
           GOBACK.

       FIN-RETORNO.
           EXIT.

       COPY TABBFCAM.

       COPY GARBFTAG.
       COPY GARBFGDD.
       COPY GARBFGDG.
       COPY GARBFGVT.

       COPY SGCBFDBC.

       COPY GNSBFTAB.
       COPY GNSBTABT.
       COPY GNSBBSYS.

       COPY GNSBGDTC.
       COPY GNSBGIDD.

       OBT-COD-MNAC SECTION.
       INI-OBT-COD-MNAC.
      * Obtiene Codigo Valor de Cambio de Pesos Mediante CIC de la Moned
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'NOTFOUND VLR' TO GRGA-STATUS-W
               PERFORM RETORNO.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
       FIN-OBT-COD-MNAC.
           EXIT.


