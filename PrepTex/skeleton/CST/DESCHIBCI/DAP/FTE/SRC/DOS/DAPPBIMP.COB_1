       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPBIMP.
       AUTHOR.          BEE.
       DATE-WRITTEN.      15-APR-1993 11:58:09

      * Programa Actualizador de entidad IMP

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVNUM.
       COPY GNSWCNUM.
       COPY GNSWGFEC.
       COPY GNSWGHOY.

       COPY GNSWGCIC.
       COPY GNSWCVRF.
       COPY GNSWVVRF.

      * Entidades de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CIC-REQA==.
       COPY GNSBRCIC.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRTAB.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSBRMSG.

      * Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-OPD-REQA==.
       COPY DAPBROPD.
       COPY DAPBRRCC.
       COPY DAPBRCAD.
       COPY DAPBRVEN.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-IMP-REQA==.
       COPY DAPBRIMP.

       01  WSS-VARI.
           03  WSS-VAL-CAPT   VALUE ZEROES    PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
           03 OKS-IMP-OPD                             PIC X(01).
           03 OKS-PLZO-CTB                            PIC X(01).
           03 WSS-GLS-SECC                            PIC X(40).
           03 WSS-GLS-MEN1                            PIC X(80).
           03 WSS-GLS-MEN2                            PIC X(80).
           03 DSP-FIO-STAT          VALUE 'N'         PIC X(01).
              88 DESPLIEGA-FIO-STAT VALUE 'S'.
           03  WSS-GLS-DATA.
               05  WSS-GLS-DATA-FILL                 PIC X(01).
               05  WSS-GLS-PZO-DAP                   PIC X(01).
           03  WSS-I                                 PIC 9(02).
           03  WSS-CIC-IOPD                          PIC X(12).
           03  WSS-IMP-IOPD                          PIC X(12).
           03  WSS-FEC-FINP.
               05  WSS-NUM-DINP                      PIC 9(02).
               05  WSS-NUM-MINP                      PIC 9(02).
               05  WSS-NUM-SINP                      PIC 9(02).
               05  WSS-NUM-AINP                      PIC 9(02).
           03  WSS-FEC-FPRO.
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-DPRO                      PIC 9(02).
           03  WSS-VEC-PLZO.
               05  WSS-NUM-PLZO     OCCURS 6 TIMES   PIC 9(04).
               05  WSS-COD-PLZO     OCCURS 6 TIMES   PIC X(12).
           03  WSS-TOT-PLZO VALUE ZEROES             PIC 9(02).
      *TOTAL DE REGISTROS EN ARCHIVO DE IMPRESION
           03  WSS-TOT-REAI                          PIC 9(04).
      *TOTAL DE REGISTROS PROCESADOS DESDE ARCHIVO DE IMPRESION
           03  WSS-TOT-RPAI                          PIC 9(04).
      *TOTAL DE REGISTROS PROCESADOS DESDE ARCHIVO DE IMPRESION
      * CON CUSTODIA ELECTRONICA
           03  WSS-TOT-REGP-CUST-E                   PIC 9(04).
      *TOTAL DE REGISTROS NO PROCESADOS DESDE ARCHIVO DE IMPRESION
      * CON CUSTODIA ELECTRONICA
           03  WSS-TOT-REGR-CUST-E                   PIC 9(04).
      *TOTAL DE REGISTROS MALOS EN ARCHIVO DE IMPRESION
           03  WSS-TOT-RMAI                          PIC 9(04).
      *TOTAL DE REGS. ELIMINADOS EN PROCESO IMP (OPD E IMP)
           03  WSS-ELI-IMP                           PIC 9(04).
      *TOTAL DE REGS. ELIMINADOS EN PROCESO OPD (OPD)
           03  WSS-ELI-OPD                           PIC 9(04).
      * INDICADOR DE DESPLIEGUE PARAMETRICO
           03  WSS-SWI-DISP  VALUE 'S'               PIC X(01).
      * VARIABLES PROCESO LIC
           03  WSS-STAT-ACT         VALUE ZEROES     PIC 9(01).
           03  SCR-SON-S                             PIC X(01).
           03 SCR-COD-CUST-E            PIC X(01).

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPBIMP' TO FIO-PROG.
           PERFORM BUS-FSIS.
       INI-INP-SRT.
      *<<<< INI_INP
           MOVE ZEROES TO WSS-TOT-REAI
                          WSS-TOT-RPAI
                          WSS-TOT-RMAI
                          WSS-ELI-IMP
                          WSS-ELI-OPD
                          WSS-TOT-REGR-CUST-E
                          WSS-TOT-REGP-CUST-E.
           DISPLAY ' '.
           DISPLAY ' INICIO CAPTURA PARAMETROS '.
           DISPLAY ' '.
           DISPLAY 'CAPTURA PLAZO CONTABLE'.
           MOVE ZEROES TO WSS-TOT-PLZO.
           MOVE 'TAB' TO FIO-SIST.
           MOVE 'PZO' TO TAB-COD-TTAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           PERFORM BUS-PLZO UNTIL
                 NOT ( FIO-STAT-OKS AND
                       TAB-COD-TTAB IN TAB = 'PZO' ).

           DISPLAY ' '.
           ACCEPT WSS-FEC-FINP IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO (DDMMSSAA) : '
                              WSS-FEC-FINP IN WSS-VARI.
           DISPLAY ' '.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FINP IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N  TO FEC-FBLK.
           MOVE FEC-CHOY-N  TO FEC-CHOY.
           MOVE FEC-VHBL-N  TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' PROCESO CANCELADO ' FEC-STAT
               DISPLAY ' ERROR VALIDACION EN F. PROCESO '
                                                FEC-MENS
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DINP TO WSS-NUM-DPRO.
           MOVE WSS-NUM-MINP TO WSS-NUM-MPRO.
           MOVE WSS-NUM-SINP TO WSS-NUM-SPRO.
           MOVE WSS-NUM-AINP TO WSS-NUM-APRO.

      *Obtiene valor indicador de afirmativo
           MOVE 'GNS' TO TAB-COD-SIST.
           MOVE 'SON' TO TAB-CIC-TTAB IN TAB.
           MOVE 'S'   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' VALIDACION INDICADOR S '
                                     MSG-COD-MENS
               PERFORM GNS-PRO-ABT
           ELSE
               MOVE TAB-COD-CTAB IN TAB TO SCR-SON-S
               DISPLAY ' '
               DISPLAY 'IND. AFIRMATIVO ' TAB-COD-CTAB IN TAB
               DISPLAY ' '.


      * OBTIENE CODIGO DE CUSTODIA ELECTRONICA
           MOVE 'DAP' TO TAB-COD-SIST.
           MOVE 'CUS' TO TAB-CIC-TTAB IN TAB.
           MOVE 'E'   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' CODIGO DE CUSTODIA ELECTRONICA NO ENCONTRADO'
                                     MSG-COD-MENS
               PERFORM GNS-PRO-ABT
           ELSE
               MOVE TAB-COD-CTAB IN TAB TO SCR-COD-CUST-E.

           DISPLAY ' FIN CAPTURA PARAMETROS '.
           DISPLAY ' '.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-IMP.

           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM DAP-FIO-IMP.
           IF NOT FIO-STAT-OKS
               DISPLAY ' FIN DE PROCESO  '
               DISPLAY ' NO EXISTEN REG EN TABLA IMP PARA PROCESAR '
               PERFORM FIN-PRO-END
           ELSE
               MOVE FIO-GET-KEY-UPD TO FIO-CMND
               PERFORM DAP-FIO-IMP
               IF NOT FIO-STAT-OKS
                   DISPLAY ' PROCESO CANCELADO  '
                   DISPLAY ' NO EXISTE REG. IMP YA SELECCIONADO: '
                             IMP-CIC-IOPD IN IMP ' FS: ' FIO-STAT
                   PERFORM GNS-PRO-ABT.

       LUP-MAIN.
           IF NOT FIO-STAT-OKS
                GO TO FIN-MAIN.
           ADD 1 TO WSS-TOT-REAI.

      *isp 05-11-1997 CUSTODIA ELECTRONICA
           IF IMP-COD-CUST IN IMP = SCR-COD-CUST-E
                 IF IMP-COD-STAT = 'G'
                       ADD 1 TO WSS-TOT-REGR-CUST-E
                       MOVE FIO-ULK-REC TO FIO-CMND
                       PERFORM DAP-FIO-IMP
                       GO TO NXT-IMP
                 ELSE
                 IF IMP-COD-STAT = 'A'
                       PERFORM PRO-CUST-E
                 ELSE
                       DISPLAY ' '
                       DISPLAY 'ENTIDAD IMP EN CUSTODIA ELECTRONICA '
                       DISPLAY 'PERO CON ESTADO DISTINTO A (G Y A)'
                       DISPLAY IMP-CIC-IOPD IN IMP
                       DISPLAY 'NO SE PROCESA Y SIGUE CICLO '
                       DISPLAY ' '
                       ADD 1 TO WSS-TOT-REGR-CUST-E
                       MOVE FIO-ULK-REC TO FIO-CMND
                       PERFORM DAP-FIO-IMP
                       GO TO NXT-IMP.


           IF ( IMP-CAI-ISER IN IMP > ZEROES OR
                IMP-IIC-ISER IN IMP > ZEROES )
                  IF IMP-COD-STAT IN IMP = 'G' OR 'W'
                      PERFORM IMP-OPD
                      IF OKS-IMP-OPD = 'S'
                          PERFORM ELI-IMP-OKS
                          IF NOT FIO-STAT-OKS
                             ADD 1 TO WSS-TOT-RMAI
                             DISPLAY ' '
                             DISPLAY ' PROCESO CANCELADO '
                             DISPLAY
                             ' ERROR AL ELIMINAR REGISTRO IMP: '
                                            IMP-CIC-IOPD IN IMP
                             PERFORM GNS-PRO-ABT
                          ELSE
                             ADD 1 TO WSS-TOT-RPAI
      *                       DISPLAY ' '
      *                       DISPLAY IMP-CIC-IOPD IN IMP
      *                          ' REGISTRO IMP ELIMINADO(IMPRESO)'
                      ELSE
                         ADD 1 TO WSS-TOT-RMAI
                         MOVE FIO-ULK-REC TO FIO-CMND
                         PERFORM DAP-FIO-IMP
                  ELSE
                      DISPLAY ' '
                      DISPLAY 'ERROR SERIAL NO VACIO ' IMP-CIC-ISER
                              ' OPERACION ' IMP-CIC-IOPD IN IMP
                              ' Y ESTADO NO G NI W ' IMP-COD-STAT
                      PERFORM DEL-IMP-OPD
                      ADD 1 TO WSS-TOT-RMAI
                      MOVE FIO-ULK-REC TO FIO-CMND
                      PERFORM DAP-FIO-IMP
                      DISPLAY IMP-CIC-IOPD ' ' IMP-COD-STAT IN IMP
                      DISPLAY ' : ESTADO INVALIDO PARA IMP '
           ELSE
               ADD 1 TO WSS-TOT-RMAI
               MOVE FIO-ULK-REC TO FIO-CMND
               PERFORM DAP-FIO-IMP
               DISPLAY ' '
               DISPLAY ' REGISTRO IMP SIN SERIAL : '
                                     IMP-CIC-IOPD IN IMP.

       NXT-IMP.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-IMP.
           IF FIO-STAT-OKS
               MOVE FIO-GET-KEY-UPD TO FIO-CMND
               PERFORM DAP-FIO-IMP
               IF NOT FIO-STAT-OKS
                   DISPLAY ' '
                   DISPLAY ' PROCESO CANCELADO  '
                   DISPLAY ' NO EXISTE REG IMP YA SELECCIONADO '
                            IMP-CIC-IOPD IN IMP ' FS: ' FIO-STAT
                   PERFORM GNS-PRO-ABT.
           GO TO LUP-MAIN.

       FIN-MAIN.

      *Elimina OPD con FCMP = FPRO y OPD-COD-STAT not = 'G'
      *Proceso PRO-DEL-OPD por FCMP
           DISPLAY 'FECHA INICIO DE BUSQUEDA OPD '
                   WSS-NUM-DPRO
                   WSS-NUM-MPRO
                   WSS-NUM-SPRO
                   WSS-NUM-APRO.

           MOVE WSS-NUM-DPRO TO OPD-NUM-DCMP IN OPD.
           MOVE WSS-NUM-MPRO TO OPD-NUM-MCMP IN OPD.
           MOVE WSS-NUM-SPRO TO OPD-NUM-SCMP IN OPD.
           MOVE WSS-NUM-APRO TO OPD-NUM-ACMP IN OPD.
           MOVE FIO-GET-NLS TO FIO-CMND.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           PERFORM DAP-FIO-OPD.
           IF FIO-STAT-OKS
               MOVE ADR-OPD-REQA TO AUX-OPD-REQA.

      *     DISPLAY 'PRIMERA LECTURA OPD CON LLAVES ' OPD-CIC-IOPD
      *             ' ' OPD-FEC-FCMP 'STATUS ' FIO-STAT.

           PERFORM PRO-DEL-OPD UNTIL
               ( NOT FIO-STAT-OKS  ) OR
               ( OPD-FEC-FCMP IN OPD > WSS-FEC-FPRO ).

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           PERFORM FIN-PRO-END.

       ELI-IMP-OKS SECTION.
       INI-ELI-IMP-OKS.
           IF IMP-COD-CUST IN IMP = SCR-COD-CUST-E
                MOVE FIO-ULK-REC TO FIO-CMND
           ELSE
                MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-IMP.
       FIN-ELI-IMP-OKS.
           EXIT.

       PRO-CUST-E SECTION.
       INI-PRO-CUST-E.
           MOVE IMP-CIC-IOPD IN IMP TO OPD-CIC-IOPD IN OPD
           MOVE FIO-GET-KEY-UPD TO FIO-CMND
           PERFORM DAP-FIO-OPD
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'NO EXISTE OPERACION PARA ENTIDAD DE '
               DISPLAY 'IMPRESION(IMP) EN CUSTODIA ELECTRONICA : '
               IMP-CIC-IOPD IN IMP
               GO TO FIN-PRO-CUST-E
           ELSE
               IF OPD-COD-STAT IN OPD NOT = 'A'
                   DISPLAY ' '
                   DISPLAY 'OPERACION EN CUSTODIA ELECTRONICA '
                   DISPLAY 'CON ESTADO INVALIDO '
                   DISPLAY 'OPD/STAT : ' OPD-CIC-IOPD IN OPD '-'
                           OPD-COD-STAT IN OPD
                   MOVE FIO-ULK-REC TO FIO-CMND
                   PERFORM DAP-FIO-OPD
                   GO TO FIN-PRO-CUST-E
               ELSE
                   MOVE '0712' TO CIC-CAI-ICIC IN CIC
                   MOVE CIC-GET TO CIC-CMND
                   PERFORM PRO-CIC
                   IF CIC-STAT NOT = CIC-STAT-OKS
                       DISPLAY ' '
                       DISPLAY 'ERROR AL ASIGNAR CIC DE IMPRESION '
                       DISPLAY 'PARA DOC EN CUSTODIA ELECTRONICA'
                       DISPLAY CIC-MENS
                       MOVE FIO-ULK-REC TO FIO-CMND
                       PERFORM DAP-FIO-OPD
                       GO TO FIN-PRO-CUST-E
                   ELSE
                       MOVE CIC-CIC-ICIC TO IMP-CIC-ISER IN IMP
                       MOVE 'G' TO IMP-COD-STAT IN IMP
                       MOVE FIO-MOD TO FIO-CMND
                       PERFORM DAP-FIO-IMP
                       MOVE FIO-GET-KEY-UPD TO FIO-CMND
                       PERFORM DAP-FIO-OPD
                       MOVE IMP-CIC-ISER IN IMP TO OPD-CIC-ISER IN OPD
                       MOVE 'G' TO OPD-COD-STAT IN OPD
                       MOVE FIO-MOD TO FIO-CMND
                       PERFORM DAP-FIO-OPD
                       ADD 1 TO WSS-TOT-REGP-CUST-E.
       FIN-PRO-CUST-E.
           EXIT.

       IMP-OPD SECTION.
       INI-IMP-OPD.
           MOVE 'S' TO OKS-IMP-OPD.
           MOVE IMP-CIC-IOPD IN IMP TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               MOVE 'N' TO OKS-IMP-OPD
               DISPLAY ' '
               DISPLAY
                ' OPERACION A IMPRIMIR NO ESTA EN BASE DE DATOS '
                                            OPD-CIC-IOPD IN OPD
               GO TO FIN-IMP-OPD.

           IF OPD-COD-STAT IN OPD NOT = 'G'
      * PARA BORRAR IMP DE OPERACIONES LIQUIDADAS ( CUST.ELECTR )
              IF OPD-COD-STAT IN OPD = 'L'
                  MOVE FIO-ULK-REC TO FIO-CMND
                  PERFORM DAP-FIO-OPD
                  GO TO FIN-IMP-OPD
              ELSE
                  MOVE 'N' TO OKS-IMP-OPD
                  MOVE FIO-ULK-REC TO FIO-CMND
                  PERFORM DAP-FIO-OPD
                  DISPLAY ' '
                  DISPLAY OPD-CIC-IOPD IN OPD ' '
                       OPD-COD-STAT IN OPD
                  ' : OPD CON ESTADO INVALIDO EN BASE DE DATOS '
                  GO TO FIN-IMP-OPD.

           IF IMP-CIC-ISER IN IMP NOT = OPD-CIC-ISER IN OPD
               MOVE 'N' TO OKS-IMP-OPD
               DISPLAY ' '
               DISPLAY OPD-CIC-IOPD IN OPD ' : OPD CON SERIAL '
                       OPD-CIC-ISER IN OPD ' DISTINTO A '
               DISPLAY IMP-CIC-IOPD IN IMP ' : IMP CON SERIAL '
                       IMP-CIC-ISER IN IMP
               DISPLAY ' '
               GO TO FIN-IMP-OPD.

           PERFORM PLZO-CTB.
           IF OKS-PLZO-CTB = 'N'
              MOVE 'N' TO OKS-IMP-OPD
              MOVE FIO-ULK-REC TO FIO-CMND
              PERFORM DAP-FIO-OPD
              GO TO FIN-IMP-OPD.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               MOVE 'N' TO OKS-IMP-OPD
               DISPLAY ' '
               DISPLAY  ' PROCESO CANCELADO '
               DISPLAY  ' ERROR AL MODIFICAR OPD: '
                          OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-IMP-OPD.
           EXIT.

       PLZO-CTB SECTION.
       INI-PLZO-CTB.
           MOVE 'S' TO OKS-PLZO-CTB.
           MOVE OPD-NUM-DREA IN OPD TO FEC-DEC1.
           MOVE OPD-NUM-MREA IN OPD TO FEC-MEC1.
           MOVE OPD-NUM-SREA IN OPD TO FEC-SEC1.
           MOVE OPD-NUM-AREA IN OPD TO FEC-AEC1.

           MOVE OPD-NUM-DTER IN OPD TO FEC-DEC2.
           MOVE OPD-NUM-MTER IN OPD TO FEC-MEC2.
           MOVE OPD-NUM-STER IN OPD TO FEC-SEC2.
           MOVE OPD-NUM-ATER IN OPD TO FEC-AEC2.

           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR EN DIFERENCIA DIAS PERIODO'
               DISPLAY ' CALCULO PLAZO CONTABLE'
                         FEC-STAT ' : ' FEC-MENS
               DISPLAY ' OPD-CIC-IOPD:' OPD-CIC-IOPD
               DISPLAY ' OPD-FEC-FTER:' OPD-FEC-FTER
               DISPLAY ' OPD-FEC-FREA:' OPD-FEC-FREA
      *        PERFORM GNS-PRO-ABT
               MOVE SPACES TO OPD-COD-PLZO IN OPD
           ELSE
               PERFORM PLZO-VEC VARYING WSS-I FROM 1 BY 1 UNTIL
                     WSS-I > WSS-TOT-PLZO OR
                     FEC-NDIA NOT > WSS-NUM-PLZO(WSS-I).

      *               IF WSS-SWI-DISP = 'S'
      *             DISPLAY ' OPERACION ' OPD-CIC-IOPD IN OPD
      *             DISPLAY ' PLZO OPD  ' FEC-NDIA

           IF FEC-STAT-OKS
               IF WSS-I > WSS-TOT-PLZO
                   DISPLAY ' '
                   DISPLAY ' PROCESO CANCELADO '
                   DISPLAY OPD-CIC-IOPD IN OPD
                   DISPLAY ' OPERACION FUERA DE RANGO DE '
                           'PLAZOS CONTABLES'
                   PERFORM GNS-PRO-ABT
                   MOVE SPACES TO OPD-COD-PLZO IN OPD
               ELSE
                   MOVE WSS-COD-PLZO IN WSS-VEC-PLZO(WSS-I) TO
                        OPD-COD-PLZO IN OPD
      * ISP: PONER COMO CODIGO DE TABLA Y NO FIJO
                   MOVE 'VIG' TO OPD-COD-SCTB IN OPD.
       FIN-PLZO-CTB.
           EXIT.

       PLZO-VEC SECTION.
       INI-PLZO-VEC.
       FIN-PLZO-VEC.
           EXIT.

       BUS-PLZO SECTION.
       INI-BUS-PLZO.
           MOVE TAB-GLS-DATA IN TAB TO WSS-GLS-DATA
           IF NOT ( TAB-IND-VIGE IN TAB = 'S' AND
                    WSS-GLS-PZO-DAP = 'S' )
                MOVE FIO-GET-NXT TO FIO-CMND
                PERFORM GNS-FIO-TAB
           ELSE
                MOVE TAB-GLS-ABRV IN TAB TO NUM-NUME
                MOVE NUM-ZERO-N TO NUM-ZERO
                MOVE NUM-SGNO-N TO NUM-SGNO
                MOVE 5 TO NUM-NENT
                MOVE 0 TO NUM-NDCM
                CALL 'GNSPVNUM' USING NUM-VARI
                IF NUM-STAT-OKS
                    ADD 1 TO WSS-TOT-PLZO
      *                    DISPLAY ' PLAZO CONTABLE CORRELATIVO : '
      *                         WSS-TOT-PLZO
      *              DISPLAY ' VALOR EN DIAS HASTA : ' NUM-NN9N
      *                      ' CODIGO ASOCIADO : ' TAB-COD-CTAB
                    MOVE NUM-NN9N TO WSS-NUM-PLZO IN
                                     WSS-VEC-PLZO(WSS-TOT-PLZO)
                    MOVE TAB-COD-CTAB IN TAB TO
                                     WSS-COD-PLZO IN
                                     WSS-VEC-PLZO(WSS-TOT-PLZO)
                    MOVE FIO-GET-NXT TO FIO-CMND
                    PERFORM GNS-FIO-TAB
                ELSE
                    DISPLAY ' '
                    DISPLAY ' PROCESO CANCELADO  '
                    DISPLAY ' PARAMETRO DE PLAZO CONTABLE NO NUMERICO'
                    PERFORM GNS-PRO-ABT.
       FIN-BUS-PLZO.
           EXIT.

       FIN-PRO-END SECTION.
       INI-FIN-PRO-END.
           DISPLAY ' '.
           DISPLAY ' ESTADISTICAS PROGRAMA DAPPBIMP '.
           DISPLAY ' ============ ======== ======== '.
           DISPLAY ' '.
           DISPLAY ' REGISTROS IMP LEIDOS                   : '
                                                 WSS-TOT-REAI.
           DISPLAY ' REGISTROS IMP PROCESADOS OK            : '
                                                 WSS-TOT-RPAI.
           DISPLAY ' REGISTROS IMP PROCESADOS CON CUST-E    : '
                                                 WSS-TOT-REGP-CUST-E.
           DISPLAY ' REGISTROS IMP NO PROCESADOS            : '
                                                 WSS-TOT-RMAI.
           DISPLAY ' REGISTROS IMP NO PROCESADOS CON CUST-E : '
                                                 WSS-TOT-REGR-CUST-E.
           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY ' REG. IMP ELIMINADOS EN PROCESO IMP NORMAL  : '
                                                 WSS-ELI-IMP.
           DISPLAY ' REG. OPD ELIMINADOS POR NO CONCRETADOS     : '
                                                 WSS-ELI-OPD.
           DISPLAY '  '.
           PERFORM GNS-PRO-END.
       FIN-FIN-PRO-END.
           EXIT.

       PRO-DEL-OPD SECTION.
       INI-PRO-DEL-OPD.
           MOVE FIO-ULK-REC TO FIO-CMND.
           PERFORM DAP-FIO-OPD.

           MOVE OPD-CIC-IOPD IN OPD TO WSS-CIC-IOPD IN WSS-VARI.

      *Proceso PRO-DEL-OPD por FCMP
      *Elimina OPD con FCMP = FPRO y OPD-COD-STAT not = 'G'
      *    Operaciones liquidadas
           IF OPD-COD-STAT IN OPD = 'L'
      *            DISPLAY 'PRO-DEL-OPD ESTADO L ' OPD-CIC-IOPD
                  GO TO END-PRO-OPD.

      *    Operaciones renovables
           IF OPD-COD-STAT IN OPD = 'G' AND
              OPD-FEC-FREA IN OPD NOT = OPD-FEC-FPCO IN OPD
      *            DISPLAY 'PRO-DEL-OPD OPERACIONES RENOVABLES '
      *            OPD-CIC-IOPD
                  GO TO END-PRO-OPD.

      *    Operaciones vigentes ingresadas via ODB u ODL
           IF OPD-COD-STAT IN OPD = 'G'
               IF OPD-IND-IDIF IN OPD = 'S'
      *            DISPLAY 'PRO-DEL-OPD CALCULO PLZO CTBL '
      *                    OPD-CIC-IOPD
                  PERFORM IDIF-CTBL
                  GO TO END-PRO-OPD
               ELSE
      *            DISPLAY 'PRO-DEL-OPD OPERACION CAPTADA OK'
      *                    OPD-CIC-IOPD
                  GO TO END-PRO-OPD.

       ELI-OPD.
           MOVE WSS-CIC-IOPD IN WSS-VARI TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN UPDATE A OPD '
                        OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT
              PERFORM GNS-PRO-ABT.

           ADD 1 TO WSS-ELI-OPD IN WSS-VARI.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR AL ELIMINAR OPD '
                        OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT
              PERFORM GNS-PRO-ABT
           ELSE
      *        DISPLAY 'PRO-DEL-OPD ELIMINACION ENTIDADES '
      *                'RELACIONADAS ' OPD-CIC-IOPD
              PERFORM PRO-DEL-RCC
              PERFORM PRO-DEL-CAD
              PERFORM PRO-DEL-VEN
              PERFORM PRO-DEL-IMP
              PERFORM DSP-INF-ELI.

       END-PRO-OPD.
           MOVE AUX-OPD-REQA   TO ADR-OPD-REQA.
           MOVE FIO-KDEF-S     TO FIO-KDEF.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF FIO-STAT-OKS
               MOVE ADR-OPD-REQA TO AUX-OPD-REQA.
       FIN-PRO-DEL-OPD.
           EXIT.

       DSP-INF-ELI SECTION.
       INI-DSP-INF-ELI.
           DISPLAY ' ' .
           DISPLAY ' OPERACION ELIMINADA ( OPD NO CONCRETADA ) '.
           DISPLAY ' NRO    : ' OPD-CIC-IOPD IN OPD.
           DISPLAY ' SER    : ' OPD-CIC-ISER IN OPD.
           DISPLAY ' ESTADO : ' OPD-COD-STAT IN OPD.
           DISPLAY ' TIPO   : ' OPD-COD-VCAM IN OPD
                                OPD-COD-COPD IN OPD.
           DISPLAY ' OFIC   : ' OPD-COD-OFIC IN OPD.
           DISPLAY ' EJEC   : ' OPD-COD-EJEC IN OPD.
           DISPLAY ' F.CAP  : ' OPD-NUM-DREA IN OPD
                                OPD-NUM-MREA IN OPD
                                OPD-NUM-SREA IN OPD
                                OPD-NUM-AREA IN OPD.

           MOVE OPD-VAL-CAPT IN OPD TO WSS-VAL-CAPT.
           DISPLAY ' MONTO  : ' WSS-VAL-CAPT.
           MOVE OPD-SGV-TINT IN OPD TO WSS-VAL-CAPT.
           DISPLAY ' TASA   : ' WSS-VAL-CAPT.
       FIN-DSP-INF-ELI.
           EXIT.

       PRO-DEL-RCC SECTION.
       INI-PRO-DEL-RCC.
       LUP-DEL-RCC.
           MOVE SPACES TO RCC-KEY-IRCC IN RCC.
           MOVE WSS-CIC-IOPD TO RCC-CIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF FIO-STAT-OKS
               MOVE FIO-END-BRW TO FIO-CMND
               PERFORM DAP-FIO-RCC.

           IF NOT ( FIO-STAT-OKS AND
              WSS-CIC-IOPD = RCC-CIC-IOPD IN RCC )
               GO TO FIN-PRO-DEL-RCC.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-DEL-RCC.

           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF FIO-STAT-OKS
               GO TO LUP-DEL-RCC
           ELSE
               MOVE 'DAP'        TO MSG-COD-SIST
               MOVE 'RCC    DEL' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC.

       FIN-PRO-DEL-RCC.
           EXIT.

       PRO-DEL-CAD SECTION.
       INI-PRO-DEL-CAD.
           MOVE SPACES TO CAD-KEY-ICAD IN CAD.
       LUP-DEL-CAD.
           MOVE WSS-CIC-IOPD TO CAD-CIC-IOPD IN CAD.
           MOVE ZEROES TO CAD-NUM-ILIQ IN CAD.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-CAD.
           IF FIO-STAT-OKS
               MOVE FIO-END-BRW TO FIO-CMND
               PERFORM DAP-FIO-CAD.

           IF NOT ( FIO-STAT-OKS AND
                    WSS-CIC-IOPD = CAD-CIC-IOPD IN CAD AND
                    CAD-NUM-ILIQ IN CAD = ZEROES )
               GO TO FIN-PRO-DEL-CAD.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-CAD.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-DEL-CAD.

           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-CAD.
           IF FIO-STAT-OKS
               GO TO LUP-DEL-CAD
           ELSE
               MOVE 'DAP'        TO MSG-COD-SIST
               MOVE 'CAD    DEL' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC.

       FIN-PRO-DEL-CAD.
           EXIT.

       PRO-DEL-VEN SECTION.
       INI-PRO-DEL-VEN.
           MOVE SPACES TO VEN-KEY-IVEN IN VEN.
       LUP-DEL-VEN.
           MOVE WSS-CIC-IOPD TO VEN-CIC-IOPD IN VEN.
           MOVE ZEROES TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF FIO-STAT-OKS
               MOVE FIO-END-BRW TO FIO-CMND
               PERFORM DAP-FIO-VEN.

           IF NOT ( FIO-STAT-OKS AND
                    WSS-CIC-IOPD = VEN-CIC-IOPD IN VEN )
               GO TO FIN-PRO-DEL-VEN.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-DEL-VEN.

           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF FIO-STAT-OKS
               GO TO LUP-DEL-VEN
           ELSE
               MOVE 'DAP'        TO MSG-COD-SIST
               MOVE 'VEN    DEL' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC.

       FIN-PRO-DEL-VEN.
           EXIT.

       PRO-DEL-IMP SECTION.
       INI-PRO-DEL-IMP.
           MOVE WSS-CIC-IOPD TO IMP-CIC-IOPD IN IMP.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-IMP.
           IF FIO-STAT-OKS
               MOVE FIO-DEL TO FIO-CMND
               PERFORM DAP-FIO-IMP.
       FIN-PRO-DEL-IMP.
           EXIT.

       IDIF-CTBL SECTION.
       INI-IDIF-CTBL.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN UPDATE A OPD '
              DISPLAY ' CALCULO PZO CONTABLE: '
                        OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT
              PERFORM GNS-PRO-ABT.

           PERFORM PLZO-CTB.
           IF OKS-PLZO-CTB = 'N'
              MOVE 'N' TO OKS-IMP-OPD
              MOVE FIO-ULK-REC TO FIO-CMND
              PERFORM DAP-FIO-OPD
              GO TO FIN-IDIF-CTBL.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               MOVE 'N' TO OKS-IMP-OPD
               DISPLAY ' '
               DISPLAY  ' PROCESO CANCELADO '
               DISPLAY  ' ERROR AL MODIFICAR OPD: '
                          OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-IDIF-CTBL.
           EXIT.

       DEL-IMP-OPD SECTION.
       INI-DEL-IMP-OPD.
           MOVE FIO-ULK-REC TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           MOVE IMP-CIC-IOPD IN IMP TO WSS-IMP-IOPD IN WSS-VARI.

       ELI-OPD.
           MOVE WSS-IMP-IOPD IN WSS-VARI TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN UPDATE A OPD: '
                        OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT
              PERFORM GNS-PRO-ABT.

           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR AL ELIMINAR OPD: '
                        OPD-CIC-IOPD IN OPD ' FS: ' FIO-STAT

              PERFORM GNS-PRO-ABT.
      *     ELSE
      *        DISPLAY ' '
      *        DISPLAY ' OPERACION ELIMINADA ( IMP NO CONCRETADA ) '
      *                OPD-CIC-IOPD IN OPD.

       ELI-IMP.
           MOVE FIO-ULK-REC TO FIO-CMND.
           PERFORM DAP-FIO-IMP.

           MOVE WSS-IMP-IOPD IN WSS-VARI TO IMP-CIC-IOPD IN IMP.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM DAP-FIO-IMP.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN UPDATE A IMP: '
                        IMP-CIC-IOPD IN IMP ' FS: ' FIO-STAT
              PERFORM GNS-PRO-ABT.

           ADD 1 TO WSS-ELI-IMP IN WSS-VARI.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM DAP-FIO-IMP.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR AL ELIMINAR IMP '
                        IMP-CIC-IOPD IN IMP ' FS: ' FIO-STAT
              PERFORM GNS-PRO-ABT.
      *     ELSE
      *        DISPLAY ' '
      *        DISPLAY ' REG IMP ELIMINADO ( IMP NO CONCRETADA ) '
      *                IMP-CIC-IOPD IN IMP.
           PERFORM PRO-DEL-RCC.
           PERFORM PRO-DEL-CAD.
           PERFORM PRO-DEL-VEN.
           PERFORM DSP-INF-ELI.

       FIN-DEL-IMP-OPD.
           EXIT.

      * Entidades de DAP
       COPY DAPBFOPD.
       COPY DAPBFRCC.
       COPY DAPBFCAD.
       COPY DAPBFVEN.
       COPY DAPBFIMP.
      * Entidades de GNS
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBBSYS.
       COPY GNSBVTAB.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBBCIC.
       COPY GNSBFCIC.
       COPY GNSBGEND.

      *<<<< EOF
      *Entidades  de GNS
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.


