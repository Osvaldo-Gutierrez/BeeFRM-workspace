000001 IDENTIFICATION DIVISION.
000002 PROGRAM-ID. DAPP0095.
000003 AUTHOR. FCO. HERMOSILLA L.
000004 DATE-WRITTEN. 2/3/83.
000005 DATE-COMPILED.
000006*REMARKS. BANCO DE CREDITO E INVERSIONES
000007*         SISTEMA DE DEPOSITOS A PLAZO.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
000008******************************************************************
000009*                                                                *
000010*    OBJETIVOS: LISTAR REGISTROS SELECCIONADOS POR FECHA VENCI-  *
000011*               MIENTO Y SUCURSAL SACADOS DESDE EL ARCHIVO DE    *
000012*               CARTERA VENCIDA.                                 *
000013*               ESTE PROGRAMA REEMPLAZA A OTRO DE IGUAL NOMBRE   *
000014*               QUE FUE DESARROLLADO EN SEPTIEMBRE DE 1983 CON   *
000015*               EL MISMO FIN.                                    *
000016*                                                                *
000017*    ARCHIVOS                                                    *
000018*      ENTRADA: FDIM01  MAESTRO CARTERA VENCIDA (DISCO-VSAM).    *
000019*               FDIM02  MAESTRO OFICINAS (DISCO-SAM).            *
000020*               FDIC01  CODIGOS DE OPERACION EN TARJETAS.        *
000021*                                                                *
000022*      SALIDA : FCOR01  IMRESORA DEL INFORME.                    *
000023*                                                                *
000024*      OTROS  : F-SORT  ARCHIVO DE CLASIFICACION.                *
000025*               TARJETA PARAMETROS DE FECHAS Y SUCURSALES.       *
000026*                                                                *
000027******************************************************************
000028*               M A N T E N C I O N E S                          *
000029******************************************************************
000030*    28/03/85  F. HERMOSILLA L.                                  *
000031*              - NORMALIZAR COLUMNA 'CAPITAL FINAL' DEL INFORME, *
000032*                QUE EN CIERTOS CASOS NO TOTALIZABA.             *
000033*                                                                *
000034******************************************************************
000030*    06/01/94  CECILIA PIZARRO.                                  *
000031*              - AGRANDA TABLA OFICINAS CONVIERTE A OS           *
000033*                                                                *
000034******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
000035 ENVIRONMENT DIVISION.
000036 CONFIGURATION SECTION.
000037 SOURCE-COMPUTER. IBM-370-3031.
000038 OBJECT-COMPUTER. IBM-370-3031.
000039 SPECIAL-NAMES.
000040     C01 IS CANAL-1
000041     DECIMAL-POINT IS COMMA.
000042 INPUT-OUTPUT SECTION.
000043 FILE-CONTROL.
000044     SELECT FDIM01 ASSIGN TO  AS-FDIM01
000045        ORGANIZATION IS SEQUENTIAL
000046        ACCESS IS SEQUENTIAL
000047        FILE STATUS IS EB-STT-FDIM01.
000048     SELECT FDIM02 ASSIGN TO FDIM02.
000049     SELECT FDIC01 ASSIGN TO FDIC01.
000050     SELECT FDOR01 ASSIGN TO FCOR01.
000051     SELECT F-SORT ASSIGN TO SORTWK1.
000052     EJECT
000053*
000054*
000055 DATA DIVISION.
000056 FILE SECTION.
000057*
000058 FD  FDIM01
000059     LABEL RECORDS ARE STANDARD
000060     DATA RECORDS ARE  CARTERA-DAC
000061                       CARTERA-RENOV
000062                       CARTERA-DAT-DAR.
000063*
000064*
000065*                 REGISTRO PARA DOCUMENTOS A PLAZO FIJO NO
000066*                 RENOVABLES Y D.A.C.
000067*                                                      LARGO: 94
000068 01  CARTERA-DAC.
000069     05 FECHA-VENC.
000070        10 CDAC-FV-ANO           PIC 9(02).
000071        10 CDAC-FV-MES           PIC 9(02).
000072        10 CDAC-FV-DIA           PIC 9(02).
000073     05 SUBKEY.
000074        10 CDAC-CODOFIC          PIC 9(03).
000075        10 CDAC-CODOPER          PIC 9(03).
000076        10 CDAC-NUMDOCTO         PIC 9(06).
000077        10 CDAC-RUT.
000078           15 CDAC-RUT-BASE      PIC 9(08).
000079           15 CDAC-RUT-DV        PIC X(01).
000080     05 FILLER                   PIC X(67).
000081*
000082*
000083*                 REGISTRO PARA DOCUMENTOS RENOVABLES
000084*                                                      LARGO: 99
000085 01  CARTERA-RENOV               PIC X(99).
000086*
000087*
000088*                 REGISTRO PARA DOCUMENTOS  D.A.T. Y D.A.R.
000089*                                                      LARGO: 92
000090 01  CARTERA-DAT-DAR             PIC X(92).
000091*
000092 FD  FDIM02
000093     LABEL RECORD STANDARD
000094     RECORD CONTAINS 79 CHARACTERS
000095     BLOCK CONTAINS 105 RECORDS.
000096 01  FDIM02-OFICINAS.
000097     05 FILLER          PIC X.
000098     05 RFO-TIPR        PIC X.
000099     05 FILLER          PIC XXX.
000100     05 RFO-CODOF       PIC XXX.
000101     05 RFO-NOMOF       PIC X(30).
000102     05 FILLER          PIC X(10).
000103     05 RFO-RUT         PIC X(9).
000104     05 RFO-DIR         PIC X(22).
000105*
000106*
000107 FD  FDIC01
000108     RECORD CONTAINS 80 CHARACTERS
000109     LABEL  RECORD IS OMITTED
000110     DATA   RECORD IS REG-TARJ.
000111 01  REG-TARJ.
000112     05  COD-OPER                    PIC 9(03).
000113     05  DESCRIP-OPER                PIC X(38).
000114     05  FILLER                      PIC X(39).
000115*
000116 SD  F-SORT
000117     LABEL RECORD IS STANDARD
000118     RECORD CONTAINS 99 CHARACTERS.
000119*
000120*
000121*                 REGISTRO PARA DOCUMENTOS A PLAZO FIJO NO
000122*                 RENOVABLES Y D.A.C.
000123*                                                      LARGO: 94
000124 01  RF-CARTERA-DAC.
000125     05 RFCDAC-KEY.
000126        10 RFCDAC-FECHA-VENC.
000127           15 RFCDAC-FV-ANO      PIC 9(02).
000128           15 RFCDAC-FV-MES      PIC 9(02).
000129           15 RFCDAC-FV-DIA      PIC 9(02).
000130        10 RFCDAC-SUBKEY.
000131           15 RFCDAC-CODOFIC     PIC 9(03).
000132           15 RFCDAC-CODOPER     PIC 9(03).
000133           15 RFCDAC-NUMDOCTO    PIC 9(06).
000134           15 RFCDAC-RUT.
000135              20 RFCDAC-RUT-BASE PIC 9(08).
000136              20 RFCDAC-RUT-DV   PIC X(01).
000137     05 RFCDAC-NOM-BENEF         PIC X(29).
000138     05 RFCDAC-FECHA-CAPT.
000139        10 RFCDAC-FC-ANO         PIC 9(02).
000140        10 RFCDAC-FC-MES         PIC 9(02).
000141        10 RFCDAC-FC-DIA         PIC 9(02).
000142     05 RFCDAC-PLAZO             PIC 9(03)       COMP-3.
000143     05 RFCDAC-TASA              PIC 9(02)V9(02) COMP-3.
000144     05 RFCDAC-IND-TASESP        PIC X(03).
000145     05 RFCDAC-CAPITAL           PIC 9(11)V9(02) COMP-3.
000146     05 RFCDAC-INTERES           PIC 9(10)V9(02) COMP-3.
000147     05 RFCDAC-VALOR-A-PAGAR     PIC 9(11)V9(02) COMP-3.
000148     05 RFCDAC-COD-CAPTADOR      PIC X(03).
000149     05 FILLER                   PIC X(05).
000150*
000151*
000152*                 REGISTRO PARA DOCUMENTOS RENOVABLES
000153*                                                      LARGO: 99
000154 01  RF-CARTERA-RENOV.
000155     05 RFCREN-KEY.
000156        10 RFCREN-FECHA-VENC.
000157           15 RFCREN-FV-ANO      PIC 9(02).
000158           15 RFCREN-FV-MES      PIC 9(02).
000159           15 RFCREN-FV-DIA      PIC 9(02).
000160        10 RFCREN-SUBKEY.
000161           15 RFCREN-CODOFIC     PIC 9(03).
000162           15 RFCREN-CODOPER     PIC 9(03).
000163           15 RFCREN-NUMDOCTO    PIC 9(06).
000164           15 RFCREN-RUT.
000165              20 RFCREN-RUT-BASE PIC 9(08).
000166              20 RFCREN-RUT-DV   PIC X(01).
000167     05 RFCREN-NOM-BENEF         PIC X(29).
000168     05 RFCREN-CAPT-ULTRENOV.
000169        10 RFCREN-FECHA-C-UR.
000170           15 RFCREN-FC-UR-ANO   PIC 9(02).
000171           15 RFCREN-FC-UR-MES   PIC 9(02).
000172           15 RFCREN-FC-UR-DIA   PIC 9(02).
000173        10 RFCREN-C-UR-TASA      PIC 9(02)V9(02) COMP-3.
000174        10 RFCREN-C-UR-IND-TASESP
000175                                 PIC X(03).
000176        10 RFCREN-C-UR-CAPITAL   PIC 9(11)V9(02) COMP-3.
000177     05 RFCREN-CAPT-PENRENOV.
000178        10 RFCREN-FECHA-C-PR.
000179           15 RFCREN-FC-PR-ANO   PIC 9(02).
000180           15 RFCREN-FC-PR-MES   PIC 9(02).
000181           15 RFCREN-FC-PR-DIA   PIC 9(02).
000182        10 RFCREN-C-PR-TASA      PIC 9(02)V9(02) COMP-3.
000183        10 RFCREN-C-PR-IND-TASESP
000184                                 PIC X(03).
000185        10 RFCREN-C-PR-CAPITAL   PIC 9(11)V9(02) COMP-3.
000186     05 RFCREN-PLAZO             PIC 9(03)       COMP-3.
000187     05 RFCREN-COD-CAPTADOR      PIC X(03).
000188*
000189*
000190*                 REGISTRO PARA DOCUMENTOS  D.A.T. Y D.A.R.
000191*                                                      LARGO: 92
000192 01  RF-CARTERA-DAT-DAR.
000193     05 RFCDAT-KEY.
000194        10 RFCDAT-FECHA-VENC.
000195           15 RFCDAT-FV-ANO      PIC 9(02).
000196           15 RFCDAT-FV-MES      PIC 9(02).
000197           15 RFCDAT-FV-DIA      PIC 9(02).
000198        10 RFCDAT-SUBKEY.
000199           15 RFCDAT-CODOFIC     PIC 9(03).
000200           15 RFCDAT-CODOPER     PIC 9(03).
000201           15 RFCDAT-NUMDOCTO    PIC 9(06).
000202           15 RFCDAT-RUT.
000203              20 RFCDAT-RUT-BASE PIC 9(08).
000204              20 RFCDAT-RUT-DV   PIC X(01).
000205     05 RFCDAT-NOM-BENEF         PIC X(29).
000206     05 RFCDAT-FECHA-CAPT.
000207        10 RFCDAT-FC-ANO         PIC 9(02).
000208        10 RFCDAT-FC-MES         PIC 9(02).
000209        10 RFCDAT-FC-DIA         PIC 9(02).
000210     05 RFCDAT-TASA              PIC 9(02)V9(02) COMP-3.
000211     05 RFCDAT-IND-TASESP        PIC X(03).
000212     05 RFCDAT-CAPITAL           PIC 9(11)V9(02) COMP-3.
000213     05 RFCDAT-COD-CAPTADOR      PIC X(03).
000214     05 RFCDAT-REAJ-PAGADOS      PIC 9(11)V9(02) COMP-3.
000215     05 RFCDAT-INTERESES-PAGADOS PIC 9(11)V9(02) COMP-3.
000216     05 FILLER                   PIC X(7).
000217*
000218 FD  FDOR01
000219     LABEL RECORD IS OMITTED.
000220 01  FDOR01-IMPR        PIC X(132).
000221     EJECT
000222*
000223*
000224 WORKING-STORAGE SECTION.
000225*
000226 77  EX-CONTALIN        PIC S9(3)     COMP-3 VALUE +99.
000227     88 LARGO-PAG VALUE +60 THRU +99.
000228 77  EX-CONTAPAG        PIC S9(5)     COMP-3 VALUE +0.
000229 77  EB-EOF-FDIC01      PIC S9        COMP-3 VALUE +0.
000230 77  EB-STT-FDIM01      PIC XX.
000231 77  EB-CONTAOPER       PIC S99       COMP-3 VALUE +0.
000232 77  EB-AREA-AUX        PIC X(41).
000233 77  EE-DOCVEN-LEIDOS   PIC S9(7)     COMP-3 VALUE +0.
000234 77  EE-DOCVEN-SORTEADOS PIC S9(7)     COMP-3 VALUE +0.
000235 77  EF-EOF-SORT        PIC S9        COMP-3 VALUE +0.
000236 77  EF-OFI-RESPALDO    PIC S9(3)     COMP-3 VALUE +0.
000237 77  EF-RUT-RESPALDO    PIC X(9).
000238 77  EF-OPE-RESPALDO    PIC S9(3)     COMP-3 VALUE +0.
000239 77  EF-CAPITAL         PIC S9(12)V99 COMP-3 VALUE +0.
000240 77  EF-CAPITAL-BCO     PIC S9(12)V99 COMP-3 VALUE +0.
000241 77  EF-REAJUSTE        PIC S9(12)V99 COMP-3 VALUE +0.
000242 77  EF-REAJUSTE-BCO    PIC S9(12)V99 COMP-3 VALUE +0.
000243 77  EF-INTERES         PIC S9(12)V99 COMP-3 VALUE +0.
000244 77  EF-INTERES-BCO     PIC S9(12)V99 COMP-3 VALUE +0.
000245 77  EF-CAPITFINAL      PIC S9(12)V99 COMP-3 VALUE +0.
000246 77  EF-CAPITFINAL-BCO  PIC S9(12)V99 COMP-3 VALUE +0.
000247 77  EF-CALCULO1        PIC S9(12)V99 COMP-3 VALUE +0.
000248 77  EF-CALCULO2        PIC S9(12)V99 COMP-3 VALUE +0.
000249 77  EF-DOCVEN-IMPRESOS PIC S9(6)     COMP-3 VALUE +0.
000250 77  EF-CONTAOPER       PIC S9(6)     COMP-3 VALUE +0.
000251 77  EF-CONTAOPER-BCO   PIC S9(6)     COMP-3 VALUE +0.
000252*
000253 01  SB-TABLA-OFICINAS.
000254     05 TAB-OFI  OCCURS 0 TO 350 TIMES
000255                 DEPENDING ON CTD-NUMOFI
000256                 ASCENDING KEY TABOF-CODOF
000257                 INDEXED BY TABOF-I.
000258        10 TABOF-CODOF    PIC X(3).
000259        10 TABOF-NOMOF    PIC X(30).
000260        10 TABOF-RUT.
000261           15 TABOF-RUTB  PIC X(8).
000262           15 TABOF-RUTDV PIC X.
000263        10 TABOF-DIR      PIC X(22).
000264*
000265 01  AREA-TRABAJO-TABLA.
000266     05 CTD-NUMOFI        PIC S9(3)   VALUE +0 COMP-3.
000267     05 EOF-OFICINAS      PIC S9      VALUE +0 COMP-3.
000268*
000269 01  SB-PARAMETROS.
000270     05 FECHA-INICIAL.
000271        10 AA-INI         PIC 9(02).
000272        10 MM-INI         PIC 9(02).
000273        10 DD-INI         PIC 9(02).
000274     05 FECHA-FINAL.
000275        10 AA-FIN         PIC 9(02).
000276        10 MM-FIN         PIC 9(02).
000277        10 DD-FIN         PIC 9(02).
000278     05 RANGO-SUCURSALES.
000279        10 SUC-INICIAL    PIC 9(3).
000280        10 SUC-FINAL      PIC 9(3).
000281     05 FILLER            PIC X(62).
000282*
000283 01  SB-TABLA-CODOPER.
000284     05 TBL-CODOPER  OCCURS 0 TO 30 TIMES
000285                     DEPENDING ON EB-CONTAOPER
000286                     ASCENDING KEY CODOPER-TBL
000287                     INDEXED BY COPX1 COPX2.
000288        10 CODOPER-TBL    PIC 9(3).
000289        10 NOMOPER-TBL    PIC X(38).
000290*
000291******************************************************************
000292*          TITULOS Y LINEAS DE TOTALES DE CONTROL                *
000293******************************************************************
000294*                                                                *
000295 01  SD-IDENTIFICACION.
000296     05 FILLER          PIC X(44)   VALUE
000297     'BANCO DE CREDITOS E INVERSIONES            '.
000298     05 FILLER          PIC X(44)   VALUE
000299     '          PGM: DAPP0095                     '.
000300     05 FILLER          PIC X(15)   VALUE 'FECHA PROCESO: '.
000301     05 FECHA-IDENT     PIC X(8).
000302     05 FILLER          PIC X(11)   VALUE '       PAG.'.
000303     05 NRO-PAG         PIC ZZZ9.
000304     05 FILLER          PIC X(6)    VALUE SPACES.
000305*
000306 01  SD-IDENT-TOTALES.
000307     05 FILLER          PIC X(59)   VALUE SPACES.
000308     05 FILLER          PIC X(20)   VALUE ' TOTALES DE CONTROL '.
000309     05 FILLER          PIC X(40)   VALUE
000310        '=======REGISTROS==========NUMERO========'.
000311     05 FILLER          PIC X(14)   VALUE SPACES.
000312*
000313 01  SD-LINEA-TOT.
000314     05 FILLER          PIC X(80)   VALUE SPACES.
000315     05 TEXTO           PIC X(24).
000316     05 FILLER          PIC XX      VALUE SPACES.
000317     05 VALOR           PIC Z(6)9.
000318     05 FILLER          PIC X(20)   VALUE SPACES.
000319*
000320******************************************************************
000321*              TITULOS Y LINEAS DEL INFORME                      *
000322******************************************************************
000323*                                                                *
000324 01  SF-T1.
000325     05 FILLER            PIC X(44)   VALUE
000326     'BANCO DE CREDITO E INVERSIONES              '.
000327     05 FILLER            PIC X(29)   VALUE
000328     '              FECHA PROCESO: '.
000329     05 FECHA-PROC-T1     PIC X(8)B(7).
000330     05 FILLER            PIC X(40)   VALUE
000331     '         PGM: DAPP0095             PAG. '.
000332     05 NRO-PAG-T1        PIC ZZZZ.
000333*
000334 01  SF-T1A.
000335     05 FILLER            PIC X(10)   VALUE 'OFICINA : '.
000336     05 DESCRIP-OFIC-T1A  PIC X(30).
000337     05 FILLER            PIC X(92)   VALUE SPACES.
000338*
000339 01  SF-T2.
000340     05 FILLER            PIC X(10)   VALUE 'CLIENTE : '.
000341     05 NOM-CLIENTE-T2    PIC X(30).
000342     05 FILLER            PIC X(92)   VALUE SPACES.
000343*
000344 01  SF-T3.
000345     05 FILLER            PIC X(10)   VALUE 'R.U.T.  : '.
000346     05 NRO-RUT-T3        PIC 9(8).
000347     05 FILLER            PIC X       VALUE '-'.
000348     05 DV-RUT-T3         PIC X.
000349     05 FILLER            PIC X(112)  VALUE SPACES.
000350*
000351 01  SF-T4.
000352     05 FILLER            PIC X(44)   VALUE
000353     '                                  I N F O R '.
000354     05 FILLER            PIC X(44)   VALUE
000355     'M E  D E  O P E R A C I O N E S  P O R  C L '.
000356     05 FILLER            PIC X(44)   VALUE
000357     'I E N T E                                   '.
000358*
000359 01  SF-S1.
000360     05 FILLER            PIC X(34)   VALUE SPACES.
000361     05 FILLER            PIC X(63)   VALUE ALL '*'.
000362     05 FILLER            PIC X(35)   VALUE SPACES.
000363*
000364 01  SF-T5.
000365     05 FILLER            PIC X(59)   VALUE SPACES.
000366     05 FILLER            PIC X(11)   VALUE 'A N O  1 9 '.
000367     05 AGNO-T5           PIC 99.
000368     05 FILLER            PIC X(60)   VALUE SPACES.
000369*
000370 01  SF-S2.
000371     05 FILLER            PIC X(59)   VALUE SPACES.
000372     05 FILLER            PIC X(14)   VALUE ALL '*'.
000373     05 FILLER            PIC X(59)   VALUE SPACES.
000374*
000375 01  SF-S3.
000376     05 FILLER            PIC X(132)  VALUE ALL '='.
000377*
000378 01  SF-T6.
000379     05 FILLER            PIC X(44)   VALUE
000380     '  T I P O  D E  O P E R A C I O N      NUMER'.
000381     05 FILLER            PIC X(44)   VALUE
000382     'O    FECHA    FECHA    C A P I T A L     REA'.
000383     05 FILLER            PIC X(44)   VALUE
000384     'JUSTE    TASA  I N T E R E S   C A P I T A L'.
000385*
000386 01  SF-T7.
000387     05 FILLER            PIC X(44)   VALUE
000388     '                                       DOCTO'.
000389     05 FILLER            PIC X(44)   VALUE
000390     '.   CAPTAC.  VENCTO.   I N I C I A L        '.
000391     05 FILLER            PIC X(44)   VALUE
000392     '                                 F I N A L  '.
000393*
000394 01  SF-T8.
000395     05 DESCRIP-OPE-T8    PIC X(38).
000396     05 FILLER            PIC X       VALUE ':'.
000397     05 FILLER            PIC X(93)   VALUE SPACES.
000398*
000399 01  SF-LD.
000400     05 FILLER            PIC X(8)    VALUE SPACES.
000401     05 FILLER            PIC X(30)   VALUE SPACES.
000402     05 NUM-DOCTO-LD      PIC B9(6).
000403     05 FECHA-CAP-LD.
000404        10 AA-CAP-LD      PIC BB99.
000405        10 FILLER         PIC X       VALUE '/'.
000406        10 MM-CAP-LD      PIC 99.
000407        10 FILLER         PIC X       VALUE '/'.
000408        10 DD-CAP-LD      PIC 99.
000409     05 FECHA-VEN-LD.
000410        10 AA-VEN-LD      PIC B99.
000411        10 FILLER         PIC X       VALUE '/'.
000412        10 MM-VEN-LD      PIC 99.
000413        10 FILLER         PIC X       VALUE '/'.
000414        10 DD-VEN-LD      PIC 99.
000415     05 CAPITAL-LD        PIC BBZZZ.ZZZ.ZZ9,99.
000416     05 REAJUSTE-LD       PIC BBZZ.ZZZ.ZZ9,99.
000417     05 TASA-LD           PIC BZ9,99.
000418     05 INTERES-LD        PIC BBZZ.ZZZ.ZZ9,99.
000419     05 CAPIT-TOT-LD      PIC BBZZZ.ZZZ.ZZ9,99.
000420*
000421 01  SF-LT1.
000422     05 GLOSA-LT1       PIC X(17)   VALUE 'TOTALES CLIENTE: '.
000423     05 CANT-OPERAC-LT1 PIC ZZ.ZZZ.
000424     05 FILLER          PIC X(12)   VALUE ' OPERACIONES'.
000425     05 CAPITAL-LT1     PIC B(27)ZZZ.ZZZ.ZZZ.ZZ9,99.
000426     05 INTERES-LT1     PIC B(18)ZZZ.ZZZ.ZZZ.ZZ9,99.
000427     05 FILLER          PIC X(16)   VALUE SPACES.
000428*
000429 01  SF-LT2.
000430     05 FILLER          PIC X(77)   VALUE SPACES.
000431     05 REAJUSTE-LT2    PIC ZZZ.ZZZ.ZZZ.ZZ9,99.
000432     05 CAPIT-TOT-LT2   PIC B(19)ZZZ.ZZZ.ZZZ.ZZ9,99.
      *
       77  LIT-79             PIC 9(2)    VALUE 79.
       01  LEGA-DATE-BLOCK.
           05 COBOL-II-DATE.
              10 COBOL-II-YEAR
                              PIC XX.
              10 COBOL-II-MONTH
                              PIC XX.
              10 COBOL-II-DAY PIC XX.
           05 OSVS-CURRENT-DATE.
              10 CURRENT-DATE-MONTH
                              PIC XX.
              10 FILLER       PIC X       VALUE '/'.
              10 CURRENT-DATE-DAY
                              PIC XX.
              10 FILLER       PIC X       VALUE '/'.
              10 CURRENT-DATE-YEAR
                              PIC XX.

           COPY RLOVR001.
       77  Y2K-AA-INI         PIC 9(2).
       77  Y2K-AA-FIN         PIC 9(2).
       77  Y2K-LIT-79         PIC 9(2).
       77  Y2K-FECHA-VENC     PIC 9(6).
       77  Y2K-FECHA-INICIAL  PIC 9(6).
       77  Y2K-FECHA-FINAL    PIC 9(6).
000433*
000434     EJECT
000435*
000436*            MODULO DE CONTROL
000437*
000438 PROCEDURE DIVISION.
000439 A-CONTROL SECTION.
000440*
000441 AA-INICIALIZACION.
000442     PERFORM B-INICIALIZAR.
000443*
000444 AB-PROCESAMIENTO.
000445     PERFORM C-PROCESAR.
000446*
000447 AC-TERMINACION.
000448     PERFORM D-TERMINAR.
000449*
000450 AZZ-FIN-DE-PROCESO.
000451     STOP RUN.
000452     EJECT
000453*
000454*            MODULO DE INICIALIZACION
000455*
000456 B-INICIALIZAR SECTION.
000457*
000458 BA-CONTROL.
000459     PERFORM BB-ACEPTAR-VALIDAR-PARAMETROS
000460     PERFORM BC-PROCESAR-CODIGOS-OPERACION
000461     PERFORM BF-PROCESAR-MAE-OFICINAS
000462     PERFORM BG-ABRIR-ARCH-FECHAR
000463     GO TO BZZ-FIN-DE-SECCION.
000464*
000465 BB-ACEPTAR-VALIDAR-PARAMETROS.
000466     ACCEPT SB-PARAMETROS FROM SYSIPT.
000467     IF FECHA-INICIAL IS NUMERIC THEN
000468********IF AA-INI NOT < LIT-79           AND
              ADD AA-INI ROLLDIFF-1-YY GIVING Y2K-AA-INI
              ADD LIT-79 ROLLDIFF-1-YY GIVING Y2K-LIT-79
              IF Y2K-AA-INI NOT < Y2K-LIT-79 AND
000469           (MM-INI NOT < 1 AND NOT > 12) AND
000470           (DD-INI NOT < 1 AND NOT > 31) THEN
000471           NEXT SENTENCE
000472        ELSE
000473           DISPLAY '01. FECHA INICIAL INVALIDA. PGM DAPP0095 CANCE
000474-          'DO----TARJETA PARAMETROS: ' SB-PARAMETROS UPON CONSOLE
000475           DISPLAY '01. FECHA INICIAL INVALIDA. PGM DAPP0095 CANCE
000476-          'DO----TARJETA PARAMETROS: ' SB-PARAMETROS
000477           STOP RUN
000478     ELSE
000479        DISPLAY '03. FECHA INIC. NO NUMERIC. PGM DAPP0095 CANCELAD
000480-       '---TARJETA PARAMETROS: ' SB-PARAMETROS UPON CONSOLE
000481        DISPLAY '03. FECHA INIC. NO NUMERIC. PGM DAPP0095 CANCELAD
000482-       '---TARJETA PARAMETROS: ' SB-PARAMETROS
000483        STOP RUN.
000484     IF FECHA-FINAL IS NUMERIC THEN
000485********IF AA-FIN NOT < LIT-79           AND
              ADD AA-FIN ROLLDIFF-1-YY GIVING Y2K-AA-FIN
              ADD LIT-79 ROLLDIFF-1-YY GIVING Y2K-LIT-79
              IF Y2K-AA-FIN NOT < Y2K-LIT-79 AND
000486           (MM-FIN NOT < 1 AND NOT > 12) AND
000487           (DD-FIN NOT < 1 AND NOT > 31) THEN
000488           NEXT SENTENCE
000489        ELSE
000490           DISPLAY '05. FECHA FINAL   INVALIDA. PGM DAPP0095 CANCE
000491-          'DO----TARJETA PARAMETROS: ' SB-PARAMETROS UPON CONSOLE
000492           DISPLAY '05. FECHA FINAL   INVALIDA. PGM DAPP0095 CANCE
000493-          'DO----TARJETA PARAMETROS: ' SB-PARAMETROS
000494           STOP RUN
000495     ELSE
000496        DISPLAY '07. FECHA FIN NO NUMERICA . PGM DAPP0095 CANCELA
000497-       'DO----TARJETA PARAMETROS: ' SB-PARAMETROS UPON CONSOLE
000498        DISPLAY '07. FECHA FIN NO NUMERICA . PGM DAPP0095 CANCELA
000499-       'DO----TARJETA PARAMETROS: ' SB-PARAMETROS
000500        STOP RUN.
000501     IF SUC-INICIAL NOT < 0 AND NOT > 100 THEN
000502        NEXT SENTENCE
000503     ELSE
000504        DISPLAY '09. SUCURS. INICIAL FUERA DE RANGO. PGM. DAPP0095
000505-       'ANCELADO----TARJETA PARAMETROS: ' SB-PARAMETROS UPON
000506        CONSOLE
000507        DISPLAY '09. SUCURS. INICIAL FUERA DE RANGO. PGM. DAPP0095
000508-       'ANCELADO----TARJETA PARAMETROS: ' SB-PARAMETROS
000509        STOP RUN.
000510     IF SUC-FINAL NOT < 0 AND NOT > 100 THEN
000511        NEXT SENTENCE
000512     ELSE
000513        DISPLAY '11. SUCURS. FINAL   FUERA DE RANGO. PGM. DAPP0095
000514-       'ANCELADO----TARJETA PARAMETROS: ' SB-PARAMETROS UPON
000515        CONSOLE
000516        DISPLAY '11. SUCURS. FINAL   FUERA DE RANGO. PGM. DAPP0095
000517-       'ANCELADO----TARJETA PARAMETROS: ' SB-PARAMETROS
000518        STOP RUN.
000519     DISPLAY 'PARAMETROS INGRESADOS:'
000520     DISPLAY '======================'
000521     DISPLAY 'FECHA INICIAL .......:' FECHA-INICIAL
000522     DISPLAY 'FECHA FINAL .........:' FECHA-FINAL
000523     DISPLAY 'NRO.SUCURSAL INICIAL :' SUC-INICIAL
000524     DISPLAY 'NRO.SUCURSAL FINAL ..:' SUC-FINAL.
000525*
000526 BC-PROCESAR-CODIGOS-OPERACION.
000527     OPEN INPUT FDIC01
000528     PERFORM BD-CARGAR-TABLA-CODOPER
000529        UNTIL EB-EOF-FDIC01 = 1
000530     PERFORM BE-ORDENAR-TABLA-ASCENDENTE
000531        VARYING COPX1 FROM 1 BY 1
000532        UNTIL COPX1 = EB-CONTAOPER
000533        AFTER COPX2 FROM 1 BY 1
000534        UNTIL COPX2 > EB-CONTAOPER
000535     CLOSE FDIC01.
000536*
000537 BD-CARGAR-TABLA-CODOPER.
000538     READ FDIC01 AT END MOVE +1 TO EB-EOF-FDIC01.
000539     IF EB-CONTAOPER < 30 THEN
000540        ADD 1 TO EB-CONTAOPER
000541        SET COPX1 TO EB-CONTAOPER
000542        MOVE COD-OPER TO CODOPER-TBL (COPX1)
000543        MOVE DESCRIP-OPER TO NOMOPER-TBL (COPX1)
000544     ELSE
000545        DISPLAY '13. TABLA DE OPERACIONES EXEDIDA. PGM. DAPP0095 C
000546-       'CELADO----OPERACIONES EN TABLA: ' EB-CONTAOPER
000547        CLOSE FDIC01
000548        STOP RUN.
000549*
000550 BE-ORDENAR-TABLA-ASCENDENTE.
000551     IF COPX2 = 1 THEN
000552        SET COPX2 TO COPX1
000553        SET COPX2 UP BY 1.
000554     IF CODOPER-TBL (COPX1) > CODOPER-TBL (COPX2) THEN
000555        MOVE TBL-CODOPER (COPX1) TO EB-AREA-AUX
000556        MOVE TBL-CODOPER (COPX2) TO TBL-CODOPER (COPX1)
000557        MOVE EB-AREA-AUX TO TBL-CODOPER (COPX2).
000558*
000559 BF-PROCESAR-MAE-OFICINAS.
000560     OPEN INPUT FDIM02
000561     PERFORM B1-LEER-CARGAR-TABLA-OFICINAS
000562        UNTIL EOF-OFICINAS = 1
000563     CLOSE FDIM02
000564     GO TO B3-FIN-RUTINA-TABOFI.
000565*
000566 B1-LEER-CARGAR-TABLA-OFICINAS.
000567     READ FDIM02 AT END MOVE 1 TO EOF-OFICINAS.
000568     IF EOF-OFICINAS = 0 AND RFO-TIPR = '2' THEN
000569        IF CTD-NUMOFI < 350 THEN
000570           ADD 1 TO CTD-NUMOFI
000571           SET TABOF-I TO CTD-NUMOFI
000572           MOVE RFO-CODOF TO TABOF-CODOF (TABOF-I)
000573           MOVE RFO-NOMOF TO TABOF-NOMOF (TABOF-I)
000574           MOVE RFO-RUT   TO TABOF-RUT   (TABOF-I)
000575           MOVE RFO-DIR   TO TABOF-DIR   (TABOF-I)
000576        ELSE
000577           DISPLAY 'TABLA OFICINAS EXEDIDA. PGM. CANCELADO '
000578           CLOSE FDIM02
000579           PERFORM B2-DESPLEGAR-TABLA-OFICINAS VARYING TABOF-I
000580              FROM 1 BY 1 UNTIL TABOF-I > CTD-NUMOFI
000581           STOP RUN.
000582*
000583 B2-DESPLEGAR-TABLA-OFICINAS.
000584     DISPLAY 'TABLA OFICINAS: ' TAB-OFI (TABOF-I).
000585*
000586 B3-FIN-RUTINA-TABOFI.
000587     EXIT.
000588*
000589 BG-ABRIR-ARCH-FECHAR.
000590     OPEN INPUT FDIM01.
000591     IF EB-STT-FDIM01 > '10' THEN
000592        DISPLAY '15. ERROR STATUS MAE.DOC.VENC. (OPEN). PGM. DOPZ9
000593-       '5 CANCELADO----STATUS: ' EB-STT-FDIM01
000594        CLOSE FDIM01
000595        STOP RUN.
000596     OPEN OUTPUT FDOR01
           ACCEPT COBOL-II-DATE FROM DATE
           MOVE COBOL-II-YEAR TO CURRENT-DATE-YEAR
           MOVE COBOL-II-MONTH TO CURRENT-DATE-MONTH
           MOVE COBOL-II-DAY TO CURRENT-DATE-DAY
           MOVE OSVS-CURRENT-DATE TO FECHA-PROC-T1
           ACCEPT COBOL-II-DATE FROM DATE
           MOVE COBOL-II-YEAR TO CURRENT-DATE-YEAR
           MOVE COBOL-II-MONTH TO CURRENT-DATE-MONTH
           MOVE COBOL-II-DAY TO CURRENT-DATE-DAY
           MOVE OSVS-CURRENT-DATE TO FECHA-IDENT.
000600*
000601 BZZ-FIN-DE-SECCION.
000602     EXIT.
000603     EJECT
000604*
000605*            MODULO DE PROCESO PRINCIPAL
000606*
000607 C-PROCESAR SECTION.
000608*
000609 CA-CONTROL.
000610     SORT F-SORT ON ASCENDING KEY  RFCDAC-CODOFIC IN F-SORT
000611                                   RFCDAC-RUT     IN F-SORT
000612                                   RFCDAC-CODOPER IN F-SORT
000613        INPUT  PROCEDURE E-SELECCION-REGISTROS
000614        OUTPUT PROCEDURE F-LISTAR-REGISTROS
000615     GO TO CZZ-FIN-DE-SECCION.
000616*
000617 CZZ-FIN-DE-SECCION.
000618     EXIT.
000619     EJECT
000620*
000621*            MODULO DE TERMINACION
000622*
000623 D-TERMINAR SECTION.
000624*
000625 DA-CONTROL.
000626     PERFORM DB-TITULOS-TOTALES
000627     PERFORM DC-TOTALIZAR
000628     PERFORM DE-CERRAR-ARCHIVOS
000629     GO TO DZZ-FIN-DE-SECCION.
000630*
000631 DB-TITULOS-TOTALES.
000632     ADD 1 TO EX-CONTAPAG
000633     MOVE EX-CONTAPAG TO NRO-PAG
000634     WRITE FDOR01-IMPR FROM SD-IDENTIFICACION AFTER CANAL-1
000635     WRITE FDOR01-IMPR FROM SD-IDENT-TOTALES  AFTER 10.
000636*
000637 DC-TOTALIZAR.
000638     MOVE EE-DOCVEN-LEIDOS TO VALOR
000639     MOVE 'DOCTOS.VENC.LEIDOS .....' TO TEXTO
000640     PERFORM DD-IMPR
000641     MOVE EE-DOCVEN-SORTEADOS TO VALOR
000642     MOVE 'DOCTOS.VENC.SORTEADOS ..' TO TEXTO
000643     PERFORM DD-IMPR
000644     MOVE EF-DOCVEN-IMPRESOS TO VALOR
000645     MOVE 'DOCTOS.VENC.LISTADOS ...' TO TEXTO
000646     PERFORM DD-IMPR.
000647*
000648 DD-IMPR.
000649     WRITE FDOR01-IMPR FROM SD-LINEA-TOT AFTER 2.
000650*
000651 DE-CERRAR-ARCHIVOS.
000652     CLOSE FDIM01
000653           FDOR01.
000654*
000655 DZZ-FIN-DE-SECCION.
000656     EXIT.
000657     EJECT
000658*
000659*            MODULO DE SELECCION DE REGISTROS
000660*
000661 E-SELECCION-REGISTROS SECTION.
000662*
000663 EA-CONTROL.
000664     PERFORM EB-LEER-SELECCIONAR
000665        UNTIL EB-STT-FDIM01 = '10'
000666     GO TO EZZ-FIN-DE-SECCION.
000667*
000668 EB-LEER-SELECCIONAR.
000669     READ FDIM01.
000670     IF EB-STT-FDIM01 = '00' THEN
000671        ADD 1 TO EE-DOCVEN-LEIDOS
000672********IF FECHA-VENC IN FDIM01 NOT < FECHA-INICIAL AND
000673********************************NOT > FECHA-FINAL   THEN
              MOVE FECHA-VENC IN FDIM01 TO Y2K-FECHA-VENC
              ADD ROLLDIFF-1-YYMMDD TO Y2K-FECHA-VENC
              MOVE FECHA-INICIAL TO Y2K-FECHA-INICIAL
              ADD ROLLDIFF-1-YYMMDD TO Y2K-FECHA-INICIAL
              MOVE FECHA-FINAL TO Y2K-FECHA-FINAL
              ADD ROLLDIFF-1-YYMMDD TO Y2K-FECHA-FINAL
              IF Y2K-FECHA-VENC NOT < Y2K-FECHA-INICIAL AND
                 NOT > Y2K-FECHA-FINAL THEN
000674           IF CDAC-CODOFIC IN FDIM01 NOT < SUC-INICIAL   AND
000675                                     NOT > SUC-FINAL     THEN
000676              PERFORM EC-SELECCION
000677           ELSE
000678              NEXT SENTENCE
000679        ELSE
000680           NEXT SENTENCE
000681     ELSE
000682        IF EB-STT-FDIM01 > '10' THEN
000683           DISPLAY '17. ERROR STATUS MAE.DOC.VEN. (READ). PGM. DOP
000684-          'Z95 CANCELADO----STATUS: ' EB-STT-FDIM01 UPON CONSOLE
000685           DISPLAY '17. ERROR STATUS MAE.DOC.VEN. (READ). PGM. DOP
000686-          'Z95 CANCELADO----STATUS: ' EB-STT-FDIM01
000687           CLOSE FDIM01
000688                 FDOR01
000689           STOP RUN.
000690*
000691 EC-SELECCION.
000692     IF CDAC-CODOPER < 40 THEN
000693        RELEASE RF-CARTERA-DAC FROM CARTERA-DAC
000694     ELSE
000695        IF CDAC-CODOPER < 50 THEN
000696           RELEASE RF-CARTERA-DAC FROM CARTERA-RENOV
000697        ELSE
000698           RELEASE RF-CARTERA-DAC FROM CARTERA-DAT-DAR.
000699     ADD 1 TO EE-DOCVEN-SORTEADOS.
000700*
000701 EZZ-FIN-DE-SECCION.
000702     EXIT.
000703     EJECT
000704*
000705*            MODULO DE EMISION DEL INFORME
000706*
000707 F-LISTAR-REGISTROS SECTION.
000708*
000709 FA-CONTROL.
000710     PERFORM FB-LEER-SORT
000711     PERFORM FC-CTROL-X-OFICINA
000712        UNTIL EF-EOF-SORT = 1
000713     PERFORM FL-TOTALES-FINALES
000714     GO TO FZZ-FIN-DE-SECCION.
000715*
000716 FB-LEER-SORT.
000717     RETURN F-SORT AT END MOVE +1 TO EF-EOF-SORT.
000718*
000719 FC-CTROL-X-OFICINA.
000720     MOVE RFCDAC-CODOFIC TO EF-OFI-RESPALDO
000721     SEARCH ALL TAB-OFI
000722        AT END
000723           MOVE 'SIN CODIGO OFICINA EN TABLA' TO DESCRIP-OFIC-T1A
                 WHEN TABOF-CODOF (TABOF-I) = RFCDAC-CODOFIC
000725           MOVE TABOF-NOMOF (TABOF-I) TO DESCRIP-OFIC-T1A.
000726     PERFORM FD-CTROL-X-RUT
000727        UNTIL EF-EOF-SORT = 1
000728           OR RFCDAC-CODOFIC NOT = EF-OFI-RESPALDO.
000729*
000730 FD-CTROL-X-RUT.
000731     MOVE RFCDAC-RUT         TO EF-RUT-RESPALDO
000732     MOVE RFCDAC-RUT-BASE    TO NRO-RUT-T3
000733     MOVE RFCDAC-RUT-DV      TO DV-RUT-T3
000734     MOVE RFCDAC-NOM-BENEF   TO NOM-CLIENTE-T2
000735     PERFORM FE-CTROL-X-OPERACION
000736        UNTIL EF-EOF-SORT = 1
000737           OR RFCDAC-CODOFIC NOT = EF-OFI-RESPALDO
000738           OR RFCDAC-RUT     NOT = EF-RUT-RESPALDO
000739     PERFORM FK-TOTALES-X-RUT.
000740*
000741 FE-CTROL-X-OPERACION.
000742     MOVE RFCDAC-CODOPER TO EF-OPE-RESPALDO
000743     SEARCH ALL TBL-CODOPER
000744        AT END
000745           MOVE 'SIN CODIGO OPERACION EN TABLA' TO DESCRIP-OPE-T8
                 WHEN CODOPER-TBL (COPX1) = RFCDAC-CODOPER
000747           MOVE NOMOPER-TBL (COPX1) TO DESCRIP-OPE-T8.
000748     IF LARGO-PAG THEN
000749        PERFORM FJ-TITULOS.
000750     WRITE FDOR01-IMPR FROM SF-T8 AFTER 2
000751     ADD 2 TO EX-CONTALIN
000752     PERFORM FF-GENERAR-INFORME
000753        UNTIL EF-EOF-SORT = 1
000754           OR RFCDAC-CODOFIC NOT = EF-OFI-RESPALDO
000755           OR RFCDAC-RUT     NOT = EF-RUT-RESPALDO
000756           OR RFCDAC-CODOPER NOT = EF-OPE-RESPALDO
000757     MOVE SPACES TO FDOR01-IMPR
000758     WRITE FDOR01-IMPR AFTER 1
000759     ADD 1 TO EX-CONTALIN.
000760*
000761 FF-GENERAR-INFORME.
000762     IF LARGO-PAG THEN
000763        PERFORM FJ-TITULOS.
000764     IF RFCDAC-CODOPER < 40 THEN
000765        PERFORM FG-FORMATEAR-DAC
000766     ELSE
000767        IF RFCDAC-CODOPER < 50 THEN
000768           PERFORM FH-FORMATEAR-RENOVABLES
000769        ELSE
000770           PERFORM FI-FORMATEAR-DAT-DAR.
000771     WRITE FDOR01-IMPR FROM SF-LD AFTER 1
000772     ADD 1 TO EF-DOCVEN-IMPRESOS
000773              EX-CONTALIN
000774              EF-CONTAOPER
000775     PERFORM FB-LEER-SORT.
000776*
000777 FG-FORMATEAR-DAC.
000778     MOVE RFCDAC-NUMDOCTO      TO NUM-DOCTO-LD
000779     MOVE RFCDAC-FC-ANO        TO AA-CAP-LD
000780     MOVE RFCDAC-FC-MES        TO MM-CAP-LD
000781     MOVE RFCDAC-FC-DIA        TO DD-CAP-LD
000782     MOVE RFCDAC-FV-ANO        TO AA-VEN-LD
000783     MOVE RFCDAC-FV-MES        TO MM-VEN-LD
000784     MOVE RFCDAC-FV-DIA        TO DD-VEN-LD
000785     MOVE RFCDAC-CAPITAL       TO CAPITAL-LD
000786     MOVE ZEROES               TO REAJUSTE-LD
000787     MOVE RFCDAC-TASA          TO TASA-LD
000788     MOVE RFCDAC-INTERES       TO INTERES-LD
000789     COMPUTE RFCDAC-VALOR-A-PAGAR = RFCDAC-CAPITAL +
000790                                    RFCDAC-INTERES
000791     MOVE RFCDAC-VALOR-A-PAGAR TO CAPIT-TOT-LD
000792     ADD RFCDAC-CAPITAL        TO EF-CAPITAL
000793     ADD RFCDAC-INTERES        TO EF-INTERES
000794     ADD RFCDAC-VALOR-A-PAGAR  TO EF-CAPITFINAL.
000795*
000796 FH-FORMATEAR-RENOVABLES.
000797     MOVE RFCREN-NUMDOCTO      TO NUM-DOCTO-LD
000798     MOVE RFCREN-FC-UR-ANO     TO AA-CAP-LD
000799     MOVE RFCREN-FC-UR-MES     TO MM-CAP-LD
000800     MOVE RFCREN-FC-UR-DIA     TO DD-CAP-LD
000801     MOVE RFCREN-FV-ANO        TO AA-VEN-LD
000802     MOVE RFCREN-FV-MES        TO MM-VEN-LD
000803     MOVE RFCREN-FV-DIA        TO DD-VEN-LD
000804     MOVE RFCREN-C-UR-CAPITAL  TO CAPITAL-LD
000805     MOVE ZEROES               TO REAJUSTE-LD
000806     MOVE RFCREN-C-UR-TASA     TO TASA-LD
000807     ADD RFCREN-C-UR-CAPITAL   TO EF-CAPITAL
000808     COMPUTE EF-CALCULO1 ROUNDED = (RFCREN-C-UR-CAPITAL / 100) *
000809                                    RFCREN-C-UR-TASA
000810     MOVE EF-CALCULO1          TO INTERES-LD
000811     ADD EF-CALCULO1           TO EF-INTERES
000812     COMPUTE EF-CALCULO2 = RFCREN-C-UR-CAPITAL +
000813                           EF-CALCULO1
000814     MOVE EF-CALCULO2          TO CAPIT-TOT-LD
000815     ADD  EF-CALCULO2          TO EF-CAPITFINAL.
000816*
000817 FI-FORMATEAR-DAT-DAR.
000818     MOVE RFCDAT-NUMDOCTO      TO NUM-DOCTO-LD
000819     MOVE RFCDAT-FC-ANO        TO AA-CAP-LD
000820     MOVE RFCDAT-FC-MES        TO MM-CAP-LD
000821     MOVE RFCDAT-FC-DIA        TO DD-CAP-LD
000822     MOVE RFCDAT-FV-ANO        TO AA-VEN-LD
000823     MOVE RFCDAT-FV-MES        TO MM-VEN-LD
000824     MOVE RFCDAT-FV-DIA        TO DD-VEN-LD
000825     MOVE RFCDAT-CAPITAL       TO CAPITAL-LD
000826     MOVE RFCDAT-REAJ-PAGADOS  TO REAJUSTE-LD
000827     MOVE RFCDAT-TASA          TO TASA-LD
000828     MOVE RFCDAT-INTERESES-PAGADOS TO INTERES-LD
000829     ADD RFCDAT-CAPITAL        TO EF-CAPITAL
000830     ADD RFCDAT-INTERESES-PAGADOS TO EF-INTERES
000831     COMPUTE EF-CALCULO2 = RFCDAT-CAPITAL +
000832                           RFCDAT-REAJ-PAGADOS +
000833                           RFCDAT-INTERESES-PAGADOS
000834     MOVE EF-CALCULO2          TO CAPIT-TOT-LD
000835     ADD  EF-CALCULO2          TO EF-CAPITFINAL.
000836*
000837 FJ-TITULOS.
000838     ADD 1 TO EX-CONTAPAG
000839     MOVE EX-CONTAPAG TO NRO-PAG-T1
000840     WRITE FDOR01-IMPR FROM SF-T1  AFTER CANAL-1
000841     WRITE FDOR01-IMPR FROM SF-T1A AFTER 1
000842     WRITE FDOR01-IMPR FROM SF-T2  AFTER 1
000843     WRITE FDOR01-IMPR FROM SF-T3  AFTER 1
000844     WRITE FDOR01-IMPR FROM SF-T4  AFTER 3
000845     WRITE FDOR01-IMPR FROM SF-S1  AFTER 1
000846     WRITE FDOR01-IMPR FROM SF-T5  AFTER 1
000847     WRITE FDOR01-IMPR FROM SF-S2  AFTER 1
000848     WRITE FDOR01-IMPR FROM SF-S3  AFTER 3
000849     WRITE FDOR01-IMPR FROM SF-T6  AFTER 1
000850     WRITE FDOR01-IMPR FROM SF-T7  AFTER 1
000851     WRITE FDOR01-IMPR FROM SF-S3  AFTER 1
000852     MOVE 16 TO EX-CONTALIN.
000853*
000854 FK-TOTALES-X-RUT.
000855     MOVE 'TOTALES CLIENTE: ' TO GLOSA-LT1
000856     MOVE EF-CONTAOPER        TO CANT-OPERAC-LT1
000857     MOVE EF-CAPITAL          TO CAPITAL-LT1
000858     MOVE EF-INTERES          TO INTERES-LT1
000859     MOVE EF-REAJUSTE         TO REAJUSTE-LT2
000860     MOVE EF-CAPITFINAL       TO CAPIT-TOT-LT2
000861     ADD  EF-CONTAOPER        TO EF-CONTAOPER-BCO
000862     ADD  EF-CAPITAL          TO EF-CAPITAL-BCO
000863     ADD  EF-INTERES          TO EF-INTERES-BCO
000864     ADD  EF-REAJUSTE         TO EF-REAJUSTE-BCO
000865     ADD  EF-CAPITFINAL       TO EF-CAPITFINAL-BCO
000866     MOVE +0                  TO EF-CONTAOPER
000867                                 EF-CAPITAL
000868                                 EF-INTERES
000869                                 EF-REAJUSTE
000870                                 EF-CAPITFINAL
000871     WRITE FDOR01-IMPR FROM SF-LT1 AFTER 2
000872     WRITE FDOR01-IMPR FROM SF-LT2 AFTER 1
000873     MOVE 99 TO EX-CONTALIN.
000874*
000875 FL-TOTALES-FINALES.
000876     MOVE 'TOTALES BANCO  : ' TO GLOSA-LT1
000877     MOVE EF-CONTAOPER-BCO    TO CANT-OPERAC-LT1
000878     MOVE EF-CAPITAL-BCO      TO CAPITAL-LT1
000879     MOVE EF-INTERES-BCO      TO INTERES-LT1
000880     MOVE EF-REAJUSTE-BCO     TO REAJUSTE-LT2
000881     MOVE EF-CAPITFINAL-BCO   TO CAPIT-TOT-LT2
000882     MOVE +0                  TO EF-CONTAOPER-BCO
000883                                 EF-CAPITAL-BCO
000884                                 EF-INTERES-BCO
000885                                 EF-REAJUSTE-BCO
000886                                 EF-CAPITFINAL-BCO
000887     WRITE FDOR01-IMPR FROM SF-LT1 AFTER 2
000888     WRITE FDOR01-IMPR FROM SF-LT2 AFTER 1
000889     MOVE 3 TO EX-CONTALIN.
000890*
000891 FZZ-FIN-DE-SECCION.
000892     EXIT.
000893     EJECT
