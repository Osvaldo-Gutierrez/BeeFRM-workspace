000000 IDENTIFICATION DIVISION.
000000 PROGRAM-ID. DAPPLCLI.
000000 AUTHOR. P CONCHA L.
000000 DATE-WRITTEN. NOV/93.
000000 DATE-COMPILED.
000000*COMPILED.     14.07.98.
000000*REMARKS. BANCO DE CREDITO E INVERSIONES
000000*         SISTEMA DE DEPOSITOS A PLAZO.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
000000******************************************************************
000000*                                                                *
000000*    OBJETIVOS: EMITIR INFORME DE OPERACIONES POR CLIENTE        *
000000*               DESDE EL ARCHIVO IPA.                            *
000000*                                                                *
000000*    ARCHIVOS                                                    *
000000*      ENTRADA: DAPDSEL DATOS DE SELECCION PARA EMISION DE LISTADO
000000*               DAPBRIPA  ARCHIVO INTERESES PAGADOS
000000*               TABLA TAB: BD 371
000000*               TABLA TAB: BD 150
000000*               TABLA OFI: BD 150
000000*                                                                *
000000*      SALIDA : DAPLCLI IMRESORA DEL INFORME.                    *
000000*                                                                *
000000*      OTROS  : FSORT  ARCHIVO DE CLASIFICACION.                 *
000000*                                                                *
000000******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
000000 ENVIRONMENT DIVISION.
000000 CONFIGURATION SECTION.
000000 SOURCE-COMPUTER. IBM-370-3031.
000000 OBJECT-COMPUTER. IBM-370-3031.
000000 SPECIAL-NAMES.
000000     C01 IS CANAL-1
000000     DECIMAL-POINT IS COMMA.
000000 INPUT-OUTPUT SECTION.
000000 FILE-CONTROL.
000000     SELECT DAPDSEL    ASSIGN UT-S-DAPDSEL.
000000     SELECT DAPBRIPA   ASSIGN DAPBRIPA
000000        ORGANIZATION IS INDEXED
000000        ACCESS MODE  IS SEQUENTIAL
000000        RECORD KEY   IS IPA-KEY-IPAV
000000        FILE STATUS  IS FS-DAPBRIPA.
000000     SELECT DAPLCLI    ASSIGN UT-S-DAPLCLI.
000000     SELECT FSORT      ASSIGN UT-S-SORTWK1.
000000     EJECT
000000*
000000 DATA DIVISION.
000000 FILE SECTION.
000000*
000000 FD  DAPDSEL
000000     RECORDING MODE IS F
000000     BLOCK CONTAINS 0 RECORDS
000000     LABEL RECORD IS STANDARD.
000000 01  REG-DAPDSEL.
000000     05 DAPDSEL-RUT.
000000        10 DAPDSEL-RUT-NUM             PIC X(08).
000000        10 DAPDSEL-RUT-VRF             PIC X(01).
000000     05 FILLER                         PIC X(01).
000000     05 DAPDSEL-FIN.
000000        10 DAPDSEL-FIN-DIA             PIC X(02).
000000        10 DAPDSEL-FIN-MES             PIC X(02).
000000        10 DAPDSEL-FIN-SIG             PIC X(02).
000000        10 DAPDSEL-FIN-ANO             PIC X(02).
000000     05 FILLER                         PIC X(01).
000000     05 DAPDSEL-FFI.
000000        10 DAPDSEL-FFI-DIA             PIC X(02).
000000        10 DAPDSEL-FFI-MES             PIC X(02).
000000        10 DAPDSEL-FFI-SIG             PIC X(02).
000000        10 DAPDSEL-FFI-ANO             PIC X(02).
000000     05 FILLER                         PIC X(01).
000000     05 DAPDSEL-OFI                    PIC X(03).
000000     05 FILLER                         PIC X(49).
000000*
000000 FD  DAPBRIPA
000000     RECORD CONTAINS 395 CHARACTERS
000000     LABEL RECORD IS STANDARD.
      *    Interfaz DAP - Certificado de Intereses Pagados
      *    -----------------------------------------------
      *
      *    En esta interfaz se entrega la informacion  necesaria
      *    de las liquidaciones de OPERACIONES efectuadas  en el
      *    sistema DAP durante un rango  de  fechas determinado.
      *    Dicha  informacion  sera utilizada por el sub-sistema
      *    de Certificado de Intereses Pagados, a fin de generar
      *    y enviar a los clientes un reporte de  los  intereses
      *    pagados por el Banco a ellos,  producto  de  sus DAP,
      *    cuentas de ahorro u otros instrumentos  de  inversion
      *    que le hayan generado intereses. Esta informacion se-
      *    ra usada por los clientes para sus respectivas decla-
      *    raciones de impuestos.
      *
      *    Nombre Registro : IPA
      *    Clave(s)        : IPA-KEY-IPAV(UU)
      *    Largo           : 395
      *    Bloqueo         : 2
      *    Observaciones   : VSAM/KSDS
      *
       01  IPA.
      *
      *    LLAVE OPD SERIAL + FVEN
           03  IPA-KEY-IPAV.
      *
      *        Cod. Int. Comp. Serial CDP
               05  IPA-CIC-ISER.
      *
      *            Centro Asignacion Identificacion
                   07  IPA-CAI-ISER                     PIC X(04).
      *
      *            Ident. Interna Computacional
                   07  IPA-IIC-ISER                     PIC X(08).
      *
      *        Fecha Vencimiento
               05  IPA-FEC-FVEN.
                   07  IPA-NUM-SVEN                     PIC 9(02).
                   07  IPA-NUM-AVEN                     PIC 9(02).
                   07  IPA-NUM-MVEN                     PIC 9(02).
                   07  IPA-NUM-DVEN                     PIC 9(02).
      *
      *    Codigo Clase Operacion DAP
           03  IPA-COD-COPD.
      *
      *        Codigo Tipo Operacion
               05  IPA-COD-TOPD                         PIC X(03).
      *
      *        Codigo Auxiliar Operacion
               05  IPA-COD-AOPD                         PIC X(03).
      *
      *    Monto Captacion
           03  IPA-VAL-CAPT             COMP-3          PIC 9(11)V9(04).
      *
      *    Monto Intereses Periodo
           03  IPA-VAL-INTS             COMP-3          PIC 9(11)V9(04).
      *
      *    Monto Captacion Inicial
           03  IPA-VAL-CAIN             COMP-3          PIC 9(11)V9(04).
      *
      *    Monto Liquidado
           03  IPA-VAL-MLIQ             COMP-3          PIC 9(11)V9(04).
      *
      *    Oficina de Liquidacion
           03  IPA-COD-OFLI                             PIC 9(03).
      *
      *    Fecha Liquidacion
           03  IPA-FEC-FLIQ.
               05  IPA-NUM-SLIQ                         PIC 9(02).
               05  IPA-NUM-ALIQ                         PIC 9(02).
               05  IPA-NUM-MLIQ                         PIC 9(02).
               05  IPA-NUM-DLIQ                         PIC 9(02).
      *
      *    Plazo en Dias
           03  IPA-NUM-PLZO                             PIC 9(04).
      *
      *    Codigo Moneda
           03  IPA-NUM-TVAL                             PIC 9(04).
      *
      *    Valor Tasa
           03  IPA-SGV-TINT             COMP-3          PIC S9(03)V9(4).
      *
      *    Fecha Captacion
           03  IPA-FEC-FCAP.
               05  IPA-NUM-SCAP                         PIC 9(02).
               05  IPA-NUM-ACAP                         PIC 9(02).
               05  IPA-NUM-MCAP                         PIC 9(02).
               05  IPA-NUM-DCAP                         PIC 9(02).
      *
      *    Primer Beneficiario
           03  IPA-CLI1.
      *
      *        RUT Cliente
               05  IPA-NUM-CLI1                         PIC X(08).
      *
      *        Digito Verificador del Rut
               05  IPA-VRF-CLI1                         PIC X(01).
      *
      *        Nombre Cliente Generico (Persona o Empresa)
               05  IPA-GLS-NOM1.
                   07  IPA-GLS-APA1                     PIC X(25).
                   07  IPA-GLS-AMA1                     PIC X(25).
                   07  IPA-GLS-NOB1                     PIC X(25).
      *
      *        Descripcion de la Direccion
               05  IPA-GLS-DIR1                         PIC X(60).
      *
      *        Codigo de Ciudad
               05  IPA-COD-CIU1                         PIC 9(04).
      *
      *    Segundo Beneficiario
           03  IPA-CLI2.
      *
      *        RUT Cliente
               05  IPA-NUM-CLI2                         PIC X(08).
      *
      *        Digito Verificador del Rut
               05  IPA-VRF-CLI2                         PIC X(01).
      *
      *        Nombre Cliente Generico (Persona o Empresa)
               05  IPA-GLS-NOM2.
                   07  IPA-GLS-APA2                     PIC X(25).
                   07  IPA-GLS-AMA2                     PIC X(25).
                   07  IPA-GLS-NOB2                     PIC X(25).
      *
      *        Descripcion de la Direccion
               05  IPA-GLS-DIR2                         PIC X(60).
      *
      *        Codigo de Ciudad
               05  IPA-COD-CIU2                         PIC 9(04).
      *
      *    Disponible
           03  IPA-GLS-DISP                             PIC X(10).
000000*
000000 SD  FSORT.
000000 01  REG-FSORT.
000000     05 FSORT-COD-OFIC                PIC X(03).
000000     05 FSORT-IPA.
000000        10 FSORT-KEY-IPAV.
000000           15 FSORT-CIC-ISER.
000000              20 FSORT-CAI-ISER       PIC X(04).
000000              20 FSORT-IIC-ISER       PIC X(08).
000000           15 FSORT-FEC-FVEN.
000000              20 FSORT-NUM-SVEN       PIC 9(02).
000000              20 FSORT-NUM-AVEN       PIC 9(02).
000000              20 FSORT-NUM-MVEN       PIC 9(02).
000000              20 FSORT-NUM-DVEN       PIC 9(02).
000000        10 FSORT-COD-COPD.
000000           15 FSORT-COD-TOPD          PIC X(03).
000000           15 FSORT-COD-AOPD          PIC X(03).
000000        10 FSORT-VAL-CAPT             PIC 9(11)V9(04) COMP-3.
000000        10 FSORT-VAL-INTS             PIC 9(11)V9(04) COMP-3.
000000        10 FSORT-VAL-CAIN             PIC 9(11)V9(04) COMP-3.
000000        10 FSORT-VAL-MLIQ             PIC 9(11)V9(04) COMP-3.
000000        10 FSORT-COD-OFLI             PIC 9(03).
000000        10 FSORT-FEC-FLIQ.
000000           15 FSORT-NUM-SLIQ          PIC 9(02).
000000           15 FSORT-NUM-ALIQ          PIC 9(02).
000000           15 FSORT-NUM-MLIQ          PIC 9(02).
000000           15 FSORT-NUM-DLIQ          PIC 9(02).
000000        10 FSORT-NUM-PLZO             PIC 9(04).
000000        10 FSORT-NUM-TVAL             PIC 9(04).
000000        10 FSORT-SGV-TINT             PIC S9(03)V9(4) COMP-3.
000000        10 FSORT-FEC-FCAP.
000000           15 FSORT-NUM-SCAP          PIC 9(02).
000000           15 FSORT-NUM-ACAP          PIC 9(02).
000000           15 FSORT-NUM-MCAP          PIC 9(02).
000000           15 FSORT-NUM-DCAP          PIC 9(02).
000000        10 FSORT-ICLI.
000000           15 FSORT-NUM-ICLI          PIC X(08).
000000           15 FSORT-VRF-ICLI          PIC X(01).
000000           15 FSORT-GLS-NOMC.
000000              20 FSORT-GLS-APAT       PIC X(25).
000000              20 FSORT-GLS-AMAT       PIC X(25).
000000              20 FSORT-GLS-NOMB       PIC X(25).
000000           15 FSORT-GLS-DIRE          PIC X(60).
000000           15 FSORT-COD-CIUD          PIC 9(04).
000000     05 FSORT-FEC-FFIN.
000000        10 FSORT-NUM-SFIN             PIC 9(02).
000000        10 FSORT-NUM-AFIN             PIC 9(02).
000000        10 FSORT-NUM-MFIN             PIC 9(02).
000000        10 FSORT-NUM-DFIN             PIC 9(02).
000000     05 FSORT-FEC-FFFI.
000000        10 FSORT-NUM-SFFI             PIC 9(02).
000000        10 FSORT-NUM-AFFI             PIC 9(02).
000000        10 FSORT-NUM-MFFI             PIC 9(02).
000000        10 FSORT-NUM-DFFI             PIC 9(02).
000000*
000000 FD  DAPLCLI
000000     RECORDING MODE IS F
000000     BLOCK CONTAINS 0 RECORDS
000000     LABEL RECORD IS STANDARD.
000000 01  REG-DAPLCLI        PIC X(132).
000000*
000000     EJECT
000000*
000000 WORKING-STORAGE SECTION.
000000*
000000     COPY BIBBAUD1.
000000*
000000 01  WE-EXTERNAS.
000000     05 WE-FECHA-DATE.
000000        10 WE-AA-DATE            PIC 9(02).
000000        10 WE-MM-DATE            PIC 9(02).
000000        10 WE-DD-DATE            PIC 9(02).
000000*
000000 01  FILE-STATUS-INDICADORES.
000000     05 SW-CANCEL          PIC 9(01)     VALUE 0.
000000     05 SW-PROC-REG        PIC 9(01)     VALUE 0.
000000     05 FS-DAPDSEL         PIC 9(01)     VALUE 0.
000000     05 FS-DAPBRIPA        PIC 9(02)     VALUE 0.
000000     05 FS-FSORT           PIC 9(01)     VALUE 0.
000000*
000000 01  CONTADORES.
000000     05 CNT-DAPBRIPA-L           PIC 9(08) VALUE 0.
000000     05 CNT-LINE-DAPLCLI         PIC 9(03) VALUE 99.
000000        88 SALTO-PAG-DAPLCLI               VALUE 60 THRU 99.
000000     05 CNT-PAGE-DAPLCLI         PIC 9(05) VALUE 0.
000000     05 CNT-DAPDSEL-L            PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-G              PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-L              PIC 9(08) VALUE 0.
000000     05 CNT-DAPLCLI-I            PIC 9(08) VALUE 0.
000000*
000000 01  W-REGISTROS-DE-TRABAJO.
000000     05 WS-VALOR-PESOS      PIC S9(15)       VALUE +0.
000000     05 WR-COD-OFIC         PIC X(03) VALUE SPACES.
000000     05 WR-GLS-OFIC         PIC X(19) VALUE SPACES.
000000     05 WR-NUM-ICLI         PIC X(08) VALUE SPACES.
000000     05 WR-COD-VCAM         PIC X(06) VALUE SPACES.
000000     05 WR-GLS-VCAM         PIC X(20) VALUE SPACES.
000000     05 WR-ABR-VCAM         PIC X(05) VALUE SPACES.
000000     05 WR-COD-COPD.
000000        10 WR-COD-TOPD      PIC X(03) VALUE SPACES.
000000        10 WR-COD-AOPD      PIC X(03) VALUE SPACES.
000000     05 WR-GLS-COPD         PIC X(24) VALUE SPACES.
000000     05 WR-GLS-TOPD         PIC X(24) VALUE SPACES.
000000     05 WR-GLS-NOMC         PIC X(75) VALUE SPACES.
000000     05 WR-VAL-VVUF         PIC S9(7)V9(04) COMP-3 VALUE +0.
000000     05 WR-CLIENTE          PIC X(01) VALUE 'N'.
000000     05 WS-VAL-INTS         PIC S9(13)V9(04) COMP-3.
000000     05 WS-VAL-FINA         PIC S9(13)V9(04) COMP-3.
000000     05 WS-MENSAJE          PIC X(50) VALUE SPACES.
000000*
000000     EJECT
000000 01  TABLA-SELECC.
000000     05 TB-CNT-SELECC                 PIC 9(08)      VALUE 0.
000000     05 TB-SELECC   OCCURS 0 TO 100 TIMES
000000                    DEPENDING ON TB-CNT-SELECC
000000                    INDEXED BY IX-SELECC.
000000        10 TB-SELECC-RUT.
000000           15 TB-SELECC-RUT-NUM       PIC X(08).
000000           15 TB-SELECC-RUT-VRF       PIC X(01).
000000        10 FILLER                     PIC X(01).
000000        10 TB-SELECC-FIN.
000000           15 TB-SELECC-FIN-SIG       PIC X(02).
000000           15 TB-SELECC-FIN-ANO       PIC X(02).
000000           15 TB-SELECC-FIN-MES       PIC X(02).
000000           15 TB-SELECC-FIN-DIA       PIC X(02).
000000        10 FILLER                     PIC X(01).
000000        10 TB-SELECC-FFI.
000000           15 TB-SELECC-FFI-SIG       PIC X(02).
000000           15 TB-SELECC-FFI-ANO       PIC X(02).
000000           15 TB-SELECC-FFI-MES       PIC X(02).
000000           15 TB-SELECC-FFI-DIA       PIC X(02).
000000        10 FILLER                     PIC X(01).
000000        10 TB-SELECC-OFI              PIC X(03).
000000*
000000     EJECT
000000*
000000* LINEAS DE IMPRESION
000000*
000000 01  TIT-01.
000000     05 FILLER                   PIC X(30)  VALUE
000000        'BANCO CREDITO INVERSIONES     '.
000000     05 FILLER                   PIC X(82)  VALUE SPACES.
000000     05 FILLER                   PIC X(10)  VALUE 'DAPPLCLI  '.
000000     05 TIT-01-FEC-FPRO.
000000        10 TIT-01-NUM-DPRO       PIC 9(02).
000000        10 FILLER                PIC X(01) VALUE SPACES.
000000        10 TIT-01-NUM-MPRO       PIC 9(02).
000000        10 FILLER                PIC X(01) VALUE SPACES.
000000        10 TIT-01-NUM-SPRO       PIC 9(02).
000000        10 TIT-01-NUM-APRO       PIC 9(02).
000000*
000000 01  TIT-02.
000000     05 TIT-02-RPT-DSIS          PIC X(30)  VALUE
000000        'DEPOSITOS A PLAZO'.
000000     05 FILLER                   PIC X(17)  VALUE SPACES.
000000     05 FILLER                   PIC X(27)  VALUE SPACES.
000000     05 FILLER                   PIC X(10)  VALUE SPACES.
000000     05 FILLER                   PIC X(38)  VALUE SPACES.
000000     05 FILLER                   PIC X(07)  VALUE 'PAGINA '.
000000     05 TIT-02-NUM-PAGE          PIC ZZZ.
000000*
000000 01  TIT-03.
000000     05 FILLER                   PIC X(10)  VALUE 'OFICINA : '.
000000     05 TIT-03-COD-OFIC          PIC X(03).
000000     05 FILLER                   PIC X(01)  VALUE SPACES.
000000     05 TIT-03-GLS-OFIC          PIC X(29).
000000*
000000 01  TIT-04.
000000     05 FILLER                   PIC X(50)  VALUE
000000        'INFORME DE OPERACIONES POR CLIENTE                '.
000000     05 FILLER                   PIC X(23)  VALUE
000000        '(VENCIMIENTOS ENTRE EL '.
000000     05 TIT-04-FEC-FFIN.
000000        10 TIT-04-NUM-DFIN       PIC X(02).
000000        10 FILLER                PIC X(01)  VALUE '-'.
000000        10 TIT-04-NUM-MFIN       PIC X(02).
000000        10 FILLER                PIC X(01)  VALUE '-'.
000000        10 TIT-04-NUM-SFIN       PIC X(02).
000000        10 TIT-04-NUM-AFIN       PIC X(02).
000000     05 FILLER                   PIC X(06)  VALUE ' Y EL '.
000000     05 TIT-04-FEC-FFFI.
000000        10 TIT-04-NUM-DFFI       PIC X(02).
000000        10 FILLER                PIC X(01)  VALUE '-'.
000000        10 TIT-04-NUM-MFFI       PIC X(02).
000000        10 FILLER                PIC X(01)  VALUE '-'.
000000        10 TIT-04-NUM-SFFI       PIC X(02).
000000        10 TIT-04-NUM-AFFI       PIC X(02).
000000     05 FILLER                   PIC X(01)  VALUE
000000        ')'.
000000*
000000 01  TIT-05.
000000     05 FILLER                   PIC X(10)  VALUE 'CLIENTE : '.
000000     05 TIT-05-GLS-NOMC          PIC X(40).
000000     05 FILLER                   PIC X(05)  VALUE SPACES.
000000     05 FILLER                   PIC X(09)  VALUE 'RUT    : '.
000000     05 TIT-05-NUM-ICLI          PIC X(08).
000000     05 FILLER                   PIC X(01)  VALUE '-'.
000000     05 TIT-05-VRF-ICLI          PIC X(01).
000000*
000000 01  TIT-06.
000000     05 FILLER                   PIC X(132) VALUE ALL '-'.
000000*
000000 01  TIT-07.
000000     05 FILLER                   PIC X(12) VALUE '   NUMERO   '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(10) VALUE '   FECHA  '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(10) VALUE '   FECHA  '.
000000     05 FILLER                   PIC X(06) VALUE SPACES.
000000     05 FILLER                   PIC X(18) VALUE
000000        '  C A P I T A L   '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(09) VALUE
000000        '   TASA  '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(18) VALUE
000000        '   I N T E R E S  '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(18) VALUE
000000        '  C A P I T A L   '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(06) VALUE ' COD. '.
000000*
000000 01  TIT-08.
000000     05 FILLER                   PIC X(12) VALUE '  DOCUMENTO '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(10) VALUE ' CAPTACION'.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(10) VALUE '  VENCIM. '.
000000     05 FILLER                   PIC X(06) VALUE SPACES.
000000     05 FILLER                   PIC X(18) VALUE
000000        '  I N I C I A L   '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(09) VALUE
000000        ' INTERES'.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(18) VALUE SPACES.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(18) VALUE
000000        '      F I N A L   '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(06) VALUE ' OPER '.
000000*
000000 01  DET-01.
000000     05 FILLER                   PIC X(09)  VALUE 'MONEDA : '.
000000     05 DET-01-ABR-VCAM          PIC X(14).
000000     05 FILLER                   PIC X(04)  VALUE SPACES.
000000     05 FILLER                   PIC X(18)  VALUE
000000        'CLASE OPERACION : '.
000000     05 DET-01-GLS-TOPD          PIC X(19).
000000*
000000 01  DET-02.
000000     05 DET-02-CIC-ISER.
000000        10 DET-02-CAI-ISER       PIC X(04).
000000        10 DET-02-IIC-ISER       PIC X(08).
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 DET-02-FEC-FREA.
000000        10 DET-02-NUM-DREA       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 DET-02-NUM-MREA       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 DET-02-NUM-SREA       PIC X(02).
000000        10 DET-02-NUM-AREA       PIC X(02).
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 DET-02-FEC-FVEN.
000000        10 DET-02-NUM-DVEN       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 DET-02-NUM-MVEN       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 DET-02-NUM-SVEN       PIC X(02).
000000        10 DET-02-NUM-AVEN       PIC X(02).
000000     05 FILLER                   PIC X(06) VALUE SPACES.
000000     05 DET-02-VAL-CAPT          PIC Z.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 DET-02-SGV-TINT          PIC ---9,9999.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 DET-02-VAL-INTS          PIC Z.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 DET-02-VAL-FINA          PIC Z.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 DET-02-COD-COPD          PIC X(06).
000000*
000000 01  TOT-01.
000000     05 TOT-01-TIP-TOT           PIC X(13) VALUE SPACES.
000000     05 TOT-01-TOT-OPE           PIC ZZZZZZZ.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 TOT-01-LIT-TOT           PIC X(15) VALUE SPACES.
000000     05 FILLER                   PIC X(09) VALUE SPACES.
000000     05 TOT-01-VAL-CAPT          PIC Z.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(09) VALUE SPACES.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 TOT-01-VAL-INTS          PIC Z.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 TOT-01-VAL-FINA          PIC Z.ZZZ.ZZZ.ZZ9,9999.
000000*
000000     EJECT
000000* AREA DE COMUNICACION CON RUTINA RUTUVALF
000000 01  RUTWVALF.
000000     05 RUTWVALF-FECHA.
000000        10 RUTWVALF-FECHA-D        PIC 9(02).
000000        10 RUTWVALF-FECHA-M        PIC 9(02).
000000        10 RUTWVALF-FECHA-A.
000000           15 RUTWVALF-FECHA-AM    PIC 9(02).
000000           15 RUTWVALF-FECHA-AA    PIC 9(02).
000000     05 RUTWVALF-CODRET            PIC 9(01)  VALUE 0.
000000*
000000     EJECT
000000     COPY RUTWTOFI.
000000     EJECT
000000     COPY RUTWTTAB.
000000     EJECT
000000     COPY RUTWCMNN.
000000     EJECT
000000******************************************************************
000000*      USER INFORMATION BLOCK                                    *
000000******************************************************************
000000*
000000 01  UIB.
000000     05 FILLER                 PIC X(32) VALUE 'DAPPLCLI'.
000000*
000000     EJECT
000000******************************************************************
000000*                   WORK AREA                                    *
000000******************************************************************
000000*
000000 01  REG-TAB.
000000*    COPYDD TAB-R-TAB.TAB-E-TAB00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO TAB
           02  TAB-E-TAB00.
      *                             REGISTRO GENERICO DE TABLAS
               04  TAB.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  TAB-GLS-FLAG PICTURE X(1).
      *                             CLAVE PRIMARIA REGISTRO
                   06  TAB-KEY-IREG.
      *                             KEY     TIPO + CODIGO ( LLAVE PRIMAR
                       08  TAB-COD-TABL.
      *                             CODIGO TIPO TABLA
                       10  TAB-COD-TTAB
                                    PICTURE X(3).
      *                             CODIGO TABLA
                       10  TAB-COD-CTAB
                                    PICTURE X(12).
      *                             TIME STAMP TRANSACCION
                       08  TAB-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  TAB-FEC-FTRN.
                       12  TAB-NUM-STRN
                                    PICTURE 9(2).
                       12  TAB-NUM-ATRN
                                    PICTURE 9(2).
                       12  TAB-NUM-MTRN
                                    PICTURE 9(2).
                       12  TAB-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  TAB-HRA-HRTR.
                       12  TAB-NUM-HHTR
                                    PICTURE 9(2).
                       12  TAB-NUM-MMTR
                                    PICTURE 9(2).
                       12  TAB-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  TAB-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  TAB-MSC-STAT PICTURE X(1).
      *                             INDICADOR VIGENCIA
                   06  TAB-IND-VIGE
                             REDEFINES TAB-MSC-STAT
                                    PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  TAB-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  TAB-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  TAB-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  TAB-COD-ATRN PICTURE X(12).
      *                             KEY TIPO + CODIGO ( INTERNO COMPUTAC
                   06  TAB-CIC-TABL.
      *                             CODIGO TIPO TABLA
                       08  TAB-CIC-TTAB
                                    PICTURE X(3).
      *                             CODIGO TABLA
                       08  TAB-CIC-CTAB
                                    PICTURE X(12).
      *                             CODIGO EXTERNO
                   06  TAB-EXT-TABL.
      *                             CODIGO TIPO TABLA
                       08  TAB-EXT-TTAB
                                    PICTURE X(3).
      *                             CODIGO TABLA
                       08  TAB-EXT-CTAB
                                    PICTURE X(5).
      *                             DESCRIPCION CORTA
                   06  TAB-GLS-DCOR PICTURE X(12).
      *                             ABREVIACION DESCRIPCION
                   06  TAB-GLS-ABRV PICTURE X(5).
      *                             DESCRIPCION DEL CODIGO
                   06  TAB-GLS-DESC PICTURE X(40).
      *                             DATOS ANEXOS
                   06  TAB-GLS-DATA PICTURE X(60).
      *                             CLAVE SOUNDEX
                   06  TAB-SNX-TABL.
                       08  TAB-SNX-TTAB
                                    PICTURE X(3).
                       08  TAB-SNX-CTAB
                                    PICTURE X(3).
               04  TAB-REDEF REDEFINES TAB
                                    PICTURE X(194).
000000     EJECT
000000*
000000 01  ACUMULADORES.
000000     05 ACM-ICLI.
000000        10 ACM-ICLI-NUME         PIC S9(11)       COMP-3 VALUE 0.
000000     05 ACM-VCAM.
000000        10 ACM-VCAM-NUME         PIC S9(11)       COMP-3 VALUE 0.
000000        10 ACM-VCAM-VAL-CAPT     PIC S9(13)V9(04) COMP-3 VALUE 0.
000000        10 ACM-VCAM-VAL-INTS     PIC S9(13)V9(04) COMP-3 VALUE 0.
000000        10 ACM-VCAM-VAL-FINA     PIC S9(13)V9(04) COMP-3 VALUE 0.
000000     05 ACM-COPD.
000000        10 ACM-COPD-NUME         PIC S9(11)       COMP-3 VALUE 0.
000000        10 ACM-COPD-VAL-CAPT     PIC S9(13)V9(04) COMP-3 VALUE 0.
000000        10 ACM-COPD-VAL-INTS     PIC S9(13)V9(04) COMP-3 VALUE 0.
000000        10 ACM-COPD-VAL-FINA     PIC S9(13)V9(04) COMP-3 VALUE 0.
000000*
           COPY RLOVR001.
000000     EJECT
000000*
000000*            MODULO DE CONTROL
000000*
000000 PROCEDURE DIVISION.
000000*
000000 A-CONTROL SECTION.
000000     ENTRY 'DBMSCBL'.
000000     COPY BIBBAUD2.
000000*
000000 AA-INICIALIZACION.
000000     PERFORM B-INICIALIZAR.
000000*
000000 AB-PROCESAMIENTO.
000000     PERFORM C-PROCESAR.
000000*
000000 AC-TERMINACION.
000000     PERFORM D-TERMINAR.
000000*
000000 AZZ-FIN-DE-PROCESO.
000000     GOBACK.
000000     EJECT
000000*
000000*            MODULO DE INICIALIZACION
000000*
000000 B-INICIALIZAR SECTION.
000000*
000000 BA-CONTROL.
000000     PERFORM BF-PROC-DAPDSEL
000000     PERFORM BG-ABRIR-ARCH-FECHAR
000000     PERFORM BZ-VERIFICA
000000     GO TO BZZ-FIN-SECC.
000000*
000000 BF-PROC-DAPDSEL.
000000* CARGA DE TABLA DE SELECCION
000000     OPEN INPUT DAPDSEL
000000     PERFORM BF1-LEER-DAPDSEL
000000     PERFORM BF2-CARGA-DAPDSEL UNTIL
000000        FS-DAPDSEL = 1
000000     IF TB-CNT-SELECC = 0
000000        DISPLAY '* NO HAY REGISTROS DE SELECCION VALIDOS'
000000        MOVE 1 TO SW-CANCEL.
000000     CLOSE DAPDSEL.
000000*
000000 BF1-LEER-DAPDSEL.
000000     READ DAPDSEL END MOVE 1 TO FS-DAPDSEL.
000000     IF FS-DAPDSEL = 0
000000        ADD 1 TO CNT-DAPDSEL-L.
000000*
000000 BF2-CARGA-DAPDSEL.
000000     MOVE 1            TO SW-PROC-REG
000000* VALIDA RUT
000000     IF DAPDSEL-RUT-NUM NOT NUMERIC
000000        MOVE 0         TO SW-PROC-REG
000000        DISPLAY '* RUT           NO NUMERICO'.
000000* VALIDA FECHA INICIAL
000000     MOVE DAPDSEL-FIN-DIA   TO RUTWVALF-FECHA-D
000000     MOVE DAPDSEL-FIN-MES   TO RUTWVALF-FECHA-M
000000     MOVE DAPDSEL-FIN-SIG   TO RUTWVALF-FECHA-AM
000000     MOVE DAPDSEL-FIN-ANO   TO RUTWVALF-FECHA-AA
000000     CALL 'RUTUVALF' USING RUTWVALF-FECHA RUTWVALF-CODRET
000000     IF RUTWVALF-CODRET NOT = 0
000000        MOVE 0         TO SW-PROC-REG
000000        DISPLAY '* FECHA INICIAL INVALIDA'.
000000* VALIDA FECHA FINAL
000000     MOVE DAPDSEL-FFI-DIA   TO RUTWVALF-FECHA-D
000000     MOVE DAPDSEL-FFI-MES   TO RUTWVALF-FECHA-M
000000     MOVE DAPDSEL-FFI-SIG   TO RUTWVALF-FECHA-AM
000000     MOVE DAPDSEL-FFI-ANO   TO RUTWVALF-FECHA-AA
000000     CALL 'RUTUVALF' USING RUTWVALF-FECHA RUTWVALF-CODRET
000000     IF RUTWVALF-CODRET NOT = 0
000000        MOVE 0         TO SW-PROC-REG
000000        DISPLAY '* FECHA FINAL   INVALIDA'.
000000* VALIDA OFICINA DESTINO
000000     MOVE DAPDSEL-OFI                 TO RUTWTOFI-COD-OFIC
000000     CALL 'RUTUTOFI' USING RUTWTOFI
000000     EVALUATE RUTWTOFI-RETURN-CODE
000000        WHEN SPACES
000000           CONTINUE
000000        WHEN OTHER
000000           MOVE 0         TO SW-PROC-REG
000000           DISPLAY '* OFICINA       INVALIDA'
000000     END-EVALUATE.
000000     IF SW-PROC-REG = 0
000000        DISPLAY '** REGISTRO DE SELECCION IGNORADO: '
000000                REG-DAPDSEL
000000        DISPLAY ' '
000000     ELSE
000000        DISPLAY '** REGISTRO DE SELECCION ACEPTADO: '
000000                REG-DAPDSEL
000000        DISPLAY ' '
000000        PERFORM BF3-CARGA-DAPDSEL.
000000     PERFORM BF1-LEER-DAPDSEL.
000000*
000000 BF3-CARGA-DAPDSEL.
000000     IF TB-CNT-SELECC < 101
000000        ADD 1                TO TB-CNT-SELECC
000000        SET IX-SELECC        TO TB-CNT-SELECC
000000        MOVE SPACES          TO TB-SELECC         (IX-SELECC)
000000        MOVE DAPDSEL-RUT     TO TB-SELECC-RUT     (IX-SELECC)
000000        MOVE DAPDSEL-FIN-DIA TO TB-SELECC-FIN-DIA (IX-SELECC)
000000        MOVE DAPDSEL-FIN-MES TO TB-SELECC-FIN-MES (IX-SELECC)
000000        MOVE DAPDSEL-FIN-SIG TO TB-SELECC-FIN-SIG (IX-SELECC)
000000        MOVE DAPDSEL-FIN-ANO TO TB-SELECC-FIN-ANO (IX-SELECC)
000000        MOVE DAPDSEL-FFI-DIA TO TB-SELECC-FFI-DIA (IX-SELECC)
000000        MOVE DAPDSEL-FFI-MES TO TB-SELECC-FFI-MES (IX-SELECC)
000000        MOVE DAPDSEL-FFI-SIG TO TB-SELECC-FFI-SIG (IX-SELECC)
000000        MOVE DAPDSEL-FFI-ANO TO TB-SELECC-FFI-ANO (IX-SELECC)
000000        MOVE DAPDSEL-OFI     TO TB-SELECC-OFI     (IX-SELECC)
000000     ELSE
000000        DISPLAY '*** TABLA DE SELECCION EXCEDIDA DE 100.'
000000        MOVE 1 TO SW-CANCEL.
000000*
000000 BG-ABRIR-ARCH-FECHAR.
000000     OPEN INPUT DAPBRIPA
000000*    IF FS-DAPBRIPA > 10
000000     IF FS-DAPBRIPA > 10 AND FS-DAPBRIPA NOT = 35
000000        DISPLAY '*** ERROR OPEN DAPBRIPA. FS=' FS-DAPBRIPA
000000        MOVE 1 TO SW-CANCEL.
000000     OPEN OUTPUT DAPLCLI.
000000* ACCEPTAR FECHA
000000     ACCEPT WE-FECHA-DATE FROM DATE
000000     MOVE WE-DD-DATE TO TIT-01-NUM-DPRO
000000     MOVE WE-MM-DATE TO TIT-01-NUM-MPRO
000000     MOVE WE-AA-DATE TO TIT-01-NUM-APRO
000000*****IF WE-AA-DATE NOT < 80
000000     IF WE-AA-DATE < ROLL-YY
000000        MOVE 20      TO TIT-01-NUM-SPRO
000000     ELSE
000000        MOVE 19      TO TIT-01-NUM-SPRO.
000000*
000000 BZ-VERIFICA.
000000     IF SW-CANCEL = 1
000000        DISPLAY  '*** ERRORES EN INICIALIZACION ***'
000000        PERFORM D-TERMINAR.
000000*
000000 BZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE PROCESO PRINCIPAL
000000*
000000 C-PROCESAR SECTION.
000000*
000000 CA-CONTROL.
000000     SORT FSORT
000000        ASCENDING KEY
000000           FSORT-COD-OFIC
000000           FSORT-NUM-ICLI
000000           FSORT-NUM-TVAL
000000           FSORT-COD-COPD
000000           FSORT-CIC-ISER
000000           FSORT-FEC-FVEN
000000        INPUT  PROCEDURE E-SELECCION-REGISTROS
000000        OUTPUT PROCEDURE F-LISTAR-REGISTROS
000000     IF SORT-RETURN NOT = 0
000000        DISPLAY '*** HUBO PROBLEMAS EN EL SORT ***'
000000        DISPLAY '*** HUBO PROBLEMAS EN EL SORT ***'
000000        MOVE 1 TO SW-CANCEL
000000        PERFORM D-TERMINAR.
000000     GO TO CZZ-FIN-SECC.
000000*
000000 CZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE TERMINACION
000000*
000000 D-TERMINAR SECTION.
000000*
000000 DA-CONTROL.
000000     CLOSE DAPBRIPA
000000           DAPLCLI.
000000     DISPLAY 'REGS. LEIDOS   DAPDSEL...........' CNT-DAPDSEL-L
000000     DISPLAY 'REGS. CARGAD   DAPDSEL (TABLA)...' TB-CNT-SELECC
000000     DISPLAY 'REGS. LEIDOS   DAPBRIPA..........' CNT-DAPBRIPA-L
000000     DISPLAY 'REGS. GRABAD   FSORT.............' CNT-FSORT-G
000000     DISPLAY 'REGS. LEIDOS   FSORT.............' CNT-FSORT-L
000000     DISPLAY 'REGS. IMPRES   INFORME OPERAC....' CNT-DAPLCLI-I
000000     DISPLAY ' '
000000     IF SW-CANCEL = 0
000000        DISPLAY '*** TERMINO NORMAL ***'
000000     ELSE
000000        MOVE 100 TO RETURN-CODE
000000        DISPLAY '*** PROCESO CANCELADO ***'
000000        GOBACK.
000000     GO TO DZZ-FIN-SECC.
000000*
000000 DZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE SELECCION DE REGISTROS
000000*
000000 E-SELECCION-REGISTROS SECTION.
000000*
000000 EA-LEER-DAPBRIPA.

           IF FS-DAPBRIPA = 35
              GO TO EZZ-FIN-SECC
           END-IF.

000000     READ DAPBRIPA RECORD
000000     IF FS-DAPBRIPA = 00
000000        ADD 1 TO CNT-DAPBRIPA-L
000000     ELSE
000000        IF FS-DAPBRIPA = 10
000000           NEXT SENTENCE
000000        ELSE
000000           DISPLAY '*** ERROR READ DAPBRIPA. FS=' FS-DAPBRIPA
000000           MOVE 16 TO SORT-RETURN.
000000*
000000 EB-CONTROL.
000000     PERFORM EC-SELECCIONA
000000        UNTIL FS-DAPBRIPA = 10
000000     GO TO EZZ-FIN-SECC.
000000*
000000 EC-SELECCIONA.
000000     MOVE 'N'               TO WR-CLIENTE
000000     PERFORM ED-BUSCA-CLIENTE
000000     IF WR-CLIENTE = 'N'
000000        NEXT SENTENCE
000000     ELSE
000000        IF IPA-FEC-FVEN < TB-SELECC-FIN (IX-SELECC) OR
000000           IPA-FEC-FVEN > TB-SELECC-FFI (IX-SELECC)
000000              NEXT SENTENCE
000000        ELSE
000000           PERFORM EE-GRABA-FSORT.
000000     PERFORM EA-LEER-DAPBRIPA.
000000*
000000 ED-BUSCA-CLIENTE.
000000     SET IX-SELECC          TO 1
000000     SEARCH TB-SELECC
000000        AT END
000000           MOVE 'N'         TO WR-CLIENTE
000000        WHEN
000000           TB-SELECC-RUT-NUM (IX-SELECC) = IPA-NUM-CLI1
000000              MOVE '1'      TO WR-CLIENTE
000000        WHEN
000000           TB-SELECC-RUT-NUM (IX-SELECC) = IPA-NUM-CLI2
000000              MOVE '2'      TO WR-CLIENTE.
000000*
000000 EE-GRABA-FSORT.
000000     MOVE TB-SELECC-OFI (IX-SELECC)   TO FSORT-COD-OFIC
000000     MOVE IPA-KEY-IPAV                TO FSORT-KEY-IPAV
000000     MOVE IPA-COD-COPD                TO FSORT-COD-COPD
000000     MOVE IPA-VAL-CAPT                TO FSORT-VAL-CAPT
000000     MOVE IPA-VAL-INTS                TO FSORT-VAL-INTS
000000     MOVE IPA-VAL-CAIN                TO FSORT-VAL-CAIN
000000     MOVE IPA-VAL-MLIQ                TO FSORT-VAL-MLIQ
000000     MOVE IPA-COD-OFLI                TO FSORT-COD-OFLI
000000     MOVE IPA-FEC-FLIQ                TO FSORT-FEC-FLIQ
000000     MOVE IPA-NUM-PLZO                TO FSORT-NUM-PLZO
000000     MOVE IPA-NUM-TVAL                TO FSORT-NUM-TVAL
000000     MOVE IPA-SGV-TINT                TO FSORT-SGV-TINT
000000     MOVE IPA-FEC-FCAP                TO FSORT-FEC-FCAP
000000     IF WR-CLIENTE = '1'
000000        MOVE IPA-CLI1                 TO FSORT-ICLI
000000     ELSE
000000        MOVE IPA-CLI2                 TO FSORT-ICLI.
000000     MOVE TB-SELECC-FIN (IX-SELECC)   TO FSORT-FEC-FFIN
000000     MOVE TB-SELECC-FFI (IX-SELECC)   TO FSORT-FEC-FFFI
000000     RELEASE REG-FSORT
000000     ADD 1                            TO CNT-FSORT-G.
000000*
000000 EZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE EMISION DEL INFORME
000000*
000000 F-LISTAR-REGISTROS SECTION.
000000*
000000 FA-LEER-FSORT.
000000     RETURN FSORT AT END MOVE 1 TO FS-FSORT.
000000     IF FS-FSORT = 0
000000        ADD 1 TO CNT-FSORT-L.
000000*
000000 FB-CONTROL.
000000     PERFORM FC-CTRL-X-OFIC UNTIL
000000        FS-FSORT = 1
000000     GO TO FZZ-FIN-SECC.
000000*
000000 FC-CTRL-X-OFIC.
000000* OFICINA
000000     MOVE 99                TO CNT-LINE-DAPLCLI
000000     MOVE 0                 TO CNT-PAGE-DAPLCLI
000000     MOVE FSORT-COD-OFIC    TO WR-COD-OFIC
000000     PERFORM FC1-BUSCA-GLOSA-OFIC
000000     MOVE WR-COD-OFIC       TO TIT-03-COD-OFIC
000000     MOVE WR-GLS-OFIC       TO TIT-03-GLS-OFIC
000000     PERFORM FD-CTRL-X-ICLI UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FS-FSORT = 1.
000000*
000000 FC1-BUSCA-GLOSA-OFIC.
000000     MOVE WR-COD-OFIC                 TO RUTWTOFI-COD-OFIC
000000     CALL 'RUTUTOFI' USING RUTWTOFI
000000     EVALUATE RUTWTOFI-RETURN-CODE
000000        WHEN SPACES
000000           MOVE RUTWTOFI-GLS-DESC     TO WR-GLS-OFIC
000000        WHEN '14'
000000           MOVE ALL '?'               TO WR-GLS-OFIC
000000        WHEN OTHER
000000           DISPLAY 'ERROR EN ACCESO TABLA OFI (RUTUTOFI). '
000000                   'COD-RET = ' RUTWTOFI-RETURN-CODE
000000           DISPLAY 'CLAVE   = ' RUTWTOFI-COD-OFIC
000000           MOVE 20 TO SORT-RETURN
000000     END-EVALUATE.
000000*
000000 FD-CTRL-X-ICLI.
000000     MOVE 99                  TO CNT-LINE-DAPLCLI
000000     MOVE 0                   TO CNT-PAGE-DAPLCLI
000000     MOVE +0                  TO ACM-ICLI-NUME
000000     MOVE FSORT-NUM-SFIN      TO TIT-04-NUM-SFIN
000000     MOVE FSORT-NUM-AFIN      TO TIT-04-NUM-AFIN
000000     MOVE FSORT-NUM-MFIN      TO TIT-04-NUM-MFIN
000000     MOVE FSORT-NUM-DFIN      TO TIT-04-NUM-DFIN
000000     MOVE FSORT-NUM-SFFI      TO TIT-04-NUM-SFFI
000000     MOVE FSORT-NUM-AFFI      TO TIT-04-NUM-AFFI
000000     MOVE FSORT-NUM-MFFI      TO TIT-04-NUM-MFFI
000000     MOVE FSORT-NUM-DFFI      TO TIT-04-NUM-DFFI
000000     MOVE FSORT-NUM-ICLI      TO WR-NUM-ICLI
000000     MOVE FSORT-NUM-ICLI      TO TIT-05-NUM-ICLI
000000     MOVE FSORT-VRF-ICLI      TO TIT-05-VRF-ICLI
000000     PERFORM FD1-COMPACTA-NOMBRE
000000     MOVE WR-GLS-NOMC         TO TIT-05-GLS-NOMC
000000     PERFORM FE-CTRL-X-VCAM UNTIL
000000        FSORT-NUM-ICLI NOT = WR-NUM-ICLI OR
000000           FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000              FS-FSORT = 1
000000     ADD 1                    TO CNT-DAPLCLI-I
000000     PERFORM FD9-TOTALES-X-ICLI.
000000*
000000 FD1-COMPACTA-NOMBRE.
000000     MOVE FSORT-GLS-APAT      TO RUTWCMNN-INP-APAT
000000     MOVE FSORT-GLS-AMAT      TO RUTWCMNN-INP-AMAT
000000     MOVE FSORT-GLS-NOMB      TO RUTWCMNN-INP-NOMB
000000     CALL 'RUTUCMNN' USING RUTWCMNN
000000     MOVE RUTWCMNN-OUT-NOMB TO WR-GLS-NOMC.
000000*
000000 FD9-TOTALES-X-ICLI.
000000* TOTALIZAR POR ICLI
000000     IF SALTO-PAG-DAPLCLI
000000        PERFORM FV-TITULOS.
000000     MOVE SPACES                 TO TOT-01
000000     MOVE 'TOTAL CLIENTE'        TO TOT-01-TIP-TOT
000000     MOVE ACM-ICLI-NUME          TO TOT-01-TOT-OPE
000000     MOVE 'OPERACION(ES) :'      TO TOT-01-LIT-TOT
000000     WRITE REG-DAPLCLI FROM TOT-01     AFTER 2
000000     ADD 2                       TO CNT-LINE-DAPLCLI.
000000*
000000 FE-CTRL-X-VCAM.
000000* MONEDA
000000     MOVE +0                TO ACM-VCAM-VAL-CAPT
000000                               ACM-VCAM-VAL-INTS
000000                               ACM-VCAM-VAL-FINA
000000                               ACM-VCAM-NUME
000000     MOVE FSORT-NUM-TVAL    TO WR-COD-VCAM
000000     PERFORM FE1-BUSCA-GLOSA-VCAM
000000     MOVE WR-ABR-VCAM       TO DET-01-ABR-VCAM
000000     PERFORM FF-CTRL-X-COPD UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-NUM-ICLI NOT = WR-NUM-ICLI OR
000000              FSORT-NUM-TVAL NOT = WR-COD-VCAM OR
000000                 FS-FSORT = 1
000000     PERFORM FE9-TOTALES-VCAM.
000000*
000000 FE1-BUSCA-GLOSA-VCAM.
000000* BUSCA GLOSA MONEDA
000000     MOVE +150                TO RUTWTTAB-BASE
000000     MOVE 'VLR'               TO RUTWTTAB-TABLA
000000     MOVE WR-COD-VCAM         TO RUTWTTAB-CLAVE
000000     CALL 'RUTUTTAB' USING RUTWTTAB
000000     EVALUATE RUTWTTAB-RETURN-CODE
000000        WHEN SPACES
000000           MOVE RUTWTTAB-REGISTRO-TAB TO REG-TAB
000000           MOVE TAB-GLS-DESC          TO WR-GLS-VCAM
000000           MOVE TAB-GLS-ABRV          TO WR-ABR-VCAM
000000        WHEN '14'
000000           MOVE ALL '?'               TO WR-GLS-VCAM
000000           MOVE ALL '?'               TO WR-ABR-VCAM
000000        WHEN OTHER
000000           DISPLAY 'ERROR EN ACCESO TABLA TAB (RUTUTTAB). '
000000                   'COD-RET = ' RUTWTTAB-RETURN-CODE
000000           DISPLAY 'BASE  = ' RUTWTTAB-BASE ' '
000000                   'TABLA = ' RUTWTTAB-TABLA ' '
000000                   'CLAVE = ' RUTWTTAB-CLAVE
000000           MOVE 20 TO SORT-RETURN
000000     END-EVALUATE.
000000*
000000 FE9-TOTALES-VCAM.
000000* ACUMULA TOTALES POR CLASE EN TOTALES POR CLIENTE
000000     ADD ACM-VCAM-NUME           TO ACM-ICLI-NUME
000000* TOTALIZAR POR VCAM
000000     IF SALTO-PAG-DAPLCLI
000000        PERFORM FV-TITULOS.
000000     MOVE SPACES                 TO TOT-01
000000     MOVE 'TOTAL MONEDA '        TO TOT-01-TIP-TOT
000000     MOVE ACM-VCAM-NUME          TO TOT-01-TOT-OPE
000000     MOVE 'OPERACION(ES) :'      TO TOT-01-LIT-TOT
000000     MOVE ACM-VCAM-VAL-CAPT      TO TOT-01-VAL-CAPT
000000     MOVE ACM-VCAM-VAL-INTS      TO TOT-01-VAL-INTS
000000     MOVE ACM-VCAM-VAL-FINA      TO TOT-01-VAL-FINA
000000     WRITE REG-DAPLCLI FROM TOT-01     AFTER 2
000000     ADD 2                       TO CNT-LINE-DAPLCLI.
000000*
000000 FF-CTRL-X-COPD.
000000* CLASE OPERACION
000000     MOVE +0                TO ACM-COPD-VAL-CAPT
000000                               ACM-COPD-VAL-INTS
000000                               ACM-COPD-VAL-FINA
000000                               ACM-COPD-NUME
000000     MOVE FSORT-COD-COPD    TO WR-COD-COPD
000000     PERFORM FF1-BUSCA-GLOSA-COPD
000000     MOVE WR-GLS-TOPD       TO DET-01-GLS-TOPD
000000     IF SALTO-PAG-DAPLCLI
000000        PERFORM FV-TITULOS.
000000     WRITE REG-DAPLCLI FROM DET-01     AFTER 2
000000     ADD 2                       TO CNT-LINE-DAPLCLI
000000     PERFORM FG-PROCESA UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-NUM-ICLI NOT = WR-NUM-ICLI OR
000000              FSORT-NUM-TVAL NOT = WR-COD-VCAM OR
000000                 FSORT-COD-COPD NOT = WR-COD-COPD OR
000000                    FS-FSORT = 1
000000     PERFORM FE9-TOTALES-COPD.
000000*
000000 FF1-BUSCA-GLOSA-COPD.
000000* BUSCA GLOSA CODIGO OPERACION EN TABLA TIO
000000     MOVE +150                TO RUTWTTAB-BASE
000000     MOVE 'TIO'               TO RUTWTTAB-TABLA
000000     MOVE WR-COD-TOPD         TO RUTWTTAB-CLAVE
000000     CALL 'RUTUTTAB' USING RUTWTTAB
000000     EVALUATE RUTWTTAB-RETURN-CODE
000000        WHEN SPACES
000000           MOVE RUTWTTAB-REGISTRO-TAB TO REG-TAB
000000           MOVE TAB-GLS-DESC          TO WR-GLS-TOPD
000000        WHEN '14'
000000           MOVE ALL '?'               TO WR-GLS-TOPD
000000        WHEN OTHER
000000           DISPLAY 'ERROR EN ACCESO TABLA TAB (RUTUTTAB). '
000000                   'COD-RET = ' RUTWTTAB-RETURN-CODE
000000           DISPLAY 'BASE  = ' RUTWTTAB-BASE ' '
000000                   'TABLA = ' RUTWTTAB-TABLA ' '
000000                   'CLAVE = ' RUTWTTAB-CLAVE
000000           MOVE 20 TO SORT-RETURN
000000     END-EVALUATE.
000000*
000000 FE9-TOTALES-COPD.
000000* ACUMULA TOTALES POR CLASE EN TOTALES POR MONEDA
000000     ADD ACM-COPD-VAL-CAPT       TO ACM-VCAM-VAL-CAPT
000000     ADD ACM-COPD-VAL-INTS       TO ACM-VCAM-VAL-INTS
000000     ADD ACM-COPD-VAL-FINA       TO ACM-VCAM-VAL-FINA
000000     ADD ACM-COPD-NUME           TO ACM-VCAM-NUME.
000000*
000000 FG-PROCESA.
000000     IF SALTO-PAG-DAPLCLI
000000        PERFORM FV-TITULOS.
000000     MOVE FSORT-CIC-ISER      TO DET-02-CIC-ISER
000000     MOVE FSORT-NUM-SCAP      TO DET-02-NUM-SREA
000000     MOVE FSORT-NUM-ACAP      TO DET-02-NUM-AREA
000000     MOVE FSORT-NUM-MCAP      TO DET-02-NUM-MREA
000000     MOVE FSORT-NUM-DCAP      TO DET-02-NUM-DREA
000000     MOVE FSORT-NUM-SVEN      TO DET-02-NUM-SVEN
000000     MOVE FSORT-NUM-AVEN      TO DET-02-NUM-AVEN
000000     MOVE FSORT-NUM-MVEN      TO DET-02-NUM-MVEN
000000     MOVE FSORT-NUM-DVEN      TO DET-02-NUM-DVEN
000000     MOVE FSORT-VAL-CAPT      TO DET-02-VAL-CAPT
000000     MOVE FSORT-SGV-TINT      TO DET-02-SGV-TINT
000000     MOVE FSORT-VAL-INTS      TO DET-02-VAL-INTS
000000     COMPUTE WS-VAL-FINA =
000000        FSORT-VAL-CAPT + FSORT-VAL-INTS
000000     MOVE WS-VAL-FINA         TO DET-02-VAL-FINA
000000     MOVE FSORT-COD-COPD      TO DET-02-COD-COPD
000000     ADD FSORT-VAL-CAPT       TO ACM-COPD-VAL-CAPT
000000     ADD FSORT-VAL-INTS       TO ACM-COPD-VAL-INTS
000000     ADD WS-VAL-FINA          TO ACM-COPD-VAL-FINA
000000     ADD 1                    TO ACM-COPD-NUME
000000     WRITE REG-DAPLCLI FROM DET-02 AFTER 1
000000     ADD 1                    TO CNT-LINE-DAPLCLI
000000     PERFORM FA-LEER-FSORT.
000000*
000000 FV-TITULOS.
000000     ADD 1                       TO CNT-PAGE-DAPLCLI
000000     MOVE CNT-PAGE-DAPLCLI       TO TIT-02-NUM-PAGE
000000     WRITE REG-DAPLCLI FROM TIT-01 AFTER CANAL-1
000000     WRITE REG-DAPLCLI FROM TIT-02 AFTER 1
000000     WRITE REG-DAPLCLI FROM TIT-03 AFTER 1
000000     WRITE REG-DAPLCLI FROM TIT-04 AFTER 1
000000     WRITE REG-DAPLCLI FROM TIT-05 AFTER 1
000000     WRITE REG-DAPLCLI FROM TIT-06 AFTER 2
000000     WRITE REG-DAPLCLI FROM TIT-07 AFTER 1
000000     WRITE REG-DAPLCLI FROM TIT-08 AFTER 1
000000     WRITE REG-DAPLCLI FROM TIT-06 AFTER 1
000000     MOVE 10                     TO CNT-LINE-DAPLCLI.
000000*
000000 FZZ-FIN-SECC.
000000     EXIT.
000000     EJECT

