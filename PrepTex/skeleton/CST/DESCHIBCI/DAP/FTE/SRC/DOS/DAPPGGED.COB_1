       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      DAPPGGED.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    19-May-92 12:53 PM.

      * Programa Generador de Regs. de OPD's para cargar en las bases
      * de datos del SIC (Gestion) sin Sort - Diario.
      * ==>> 12-APR-1993 08:26:05
      *      MOVE ZEROES ( FIJO ) A MVA-VAL-VAR-3 PARA REGISTROS
      *      ELIMINADOS EN PRO-ELI SECTION.
      *  CECILIA PIZARRO 5.05.93
      *  - SE ASIGNA FECHA DE PROCESO A FECHA DE TRANSACCION.
      *  JORGE FERRADA  18.11.93
      *  - SE ASIGNA OPD-COD-EATE A CAMPOS MOP-COD-EJE-1 Y
      *    MVA-COD-VAR-1

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY DAPBROPD.

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
      * COPY GNSWVIDD.
       COPY GNSBRTAB.
       COPY GNSBRMSG.

       COPY GNSWGSYS.
      *Entidades  de GNS
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSBRMSC.
       COPY GNSWGPES.
      *Entidades  de TAB
       COPY TABBRCAM.
      *Entidades de DAP
       COPY DAPBRRCC.
       COPY DAPBRLIQ.
       COPY DAPBRVEN.
       COPY DAPBRMRC.
       COPY DAPBRMOP.
       COPY DAPBRMVA.

       01  WSS-VARI.
           03 WSS-IND-MCLI        VALUE SPACES    PIC X(01).
           03 WSS-STAT-RCC        VALUE ZEROES    PIC 9(01).
           03 WSS-VAL-CAPT        VALUE ZEROES    PIC S9(13)V9(04).
           03 WSS-VAL-VAR2        VALUE ZEROES    PIC S9(13)V9(04).
           03 WSS-NUM-DIAS        VALUE ZEROES    PIC 9(05).
           03 WSS-LEI-OPD         VALUE ZEROES    PIC 9(05).
           03 WSS-PRO-ING         VALUE ZEROES    PIC 9(05).
           03 WSS-PRO-MOD         VALUE ZEROES    PIC 9(05).
           03 WSS-LEI-VEN         VALUE ZEROES    PIC 9(05).
           03 WSS-LEI-RCC         VALUE ZEROES    PIC 9(05).
           03 WSS-LEI-LIQ         VALUE ZEROES    PIC 9(05).
           03 WSS-PRO-ELI         VALUE ZEROES    PIC 9(05).
           03 WSS-PRO-VEN         VALUE ZEROES    PIC 9(05).
           03 WSS-GRA-MRC         VALUE ZEROES    PIC 9(05).
           03 WSS-GRA-MOP         VALUE ZEROES    PIC 9(05).
           03 WSS-GRA-MVA         VALUE ZEROES    PIC 9(05).
      *FECHA INICIAL DE RANGO DE BUSQUEDA BUSQUEDA ( FECHA PROCESO + 1 )
           03  WSS-FEC-FINI.
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-DINI                      PIC 9(02).
      *FECHA FINAL DE RANGO DE BUSQUEDA ( FINI + TODOS
      *DIAS NO HABILES CONSECUTIVOS
           03  WSS-FEC-FFIN.
               05  WSS-NUM-SFIN                      PIC 9(02).
               05  WSS-NUM-AFIN                      PIC 9(02).
               05  WSS-NUM-MFIN                      PIC 9(02).
               05  WSS-NUM-DFIN                      PIC 9(02).

      *FECHA INGRESADA COMO PARAMETRO ( DDMMSSAA )
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).

      *FECHA INGRESADA COMO PARAMETRO ( SSAAMMDD )
           03  WSS-FEC-FTRA.
               05  WSS-NUM-STRA                      PIC 9(02).
               05  WSS-NUM-ATRA                      PIC 9(02).
               05  WSS-NUM-MTRA                      PIC 9(02).
               05  WSS-NUM-DTRA                      PIC 9(02).

           03 WSS-FEC-FPA1.
              05  WSS-NUM-DPA1                    PIC 9(02).
              05  WSS-NUM-MPA1                    PIC 9(02).
              05  WSS-NUM-SPA1                    PIC 9(02).
              05  WSS-NUM-APA1                    PIC 9(02).
           03 WSS-FEC-FPA2.
              05  WSS-NUM-DPA2                    PIC 9(02).
              05  WSS-NUM-MPA2                    PIC 9(02).
              05  WSS-NUM-SPA2                    PIC 9(02).
              05  WSS-NUM-APA2                    PIC 9(02).
      *Entidades de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *Entidades de DAP
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LIQ-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.


       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'DAPPGGED' TO FIO-PROG.
           PERFORM BUS-FSIS.

      * Captura parametros
           PERFORM ACC-FPRO.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-MRC.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'ERROR FIO-UPD MRC, FIO-STAT : ' FIO-STAT
               PERFORM GNS-PRO-ABT.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-MOP.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'ERROR FIO-UPD MVO, FIO-STAT : ' FIO-STAT
               PERFORM GNS-PRO-ABT.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM DAP-FIO-MVA.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'PROCESO CANCELADO '
               DISPLAY 'ERROR FIO-UPD MVA, FIO-STAT : ' FIO-STAT
               PERFORM GNS-PRO-ABT.

      * Obtiene Fecha Hoy
           PERFORM GET-FHOY.

       LUP-MAIN.
           IF NOT FIO-STAT-OKS
                GO TO FIN-MAIN.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO OPD-NUM-DCMP IN OPD.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO OPD-NUM-MCMP IN OPD.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO OPD-NUM-SCMP IN OPD.
           MOVE WSS-NUM-APRO IN WSS-VARI TO OPD-NUM-ACMP IN OPD.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           PERFORM PRO-OPD UNTIL
               ( NOT FIO-STAT-OKS  ) OR
               ( OPD-FEC-FCMP IN OPD > WSS-FEC-FTRA ).

           MOVE WSS-NUM-DPRO IN WSS-VARI TO LIQ-NUM-DCMP IN LIQ.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO LIQ-NUM-MCMP IN LIQ.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO LIQ-NUM-SCMP IN LIQ.
           MOVE WSS-NUM-APRO IN WSS-VARI TO LIQ-NUM-ACMP IN LIQ.
           MOVE 'LIQ-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
           PERFORM PRO-LIQ UNTIL
               ( NOT FIO-STAT-OKS  ) OR
               ( LIQ-FEC-FCMP IN LIQ > WSS-FEC-FFIN ).

       FIN-MAIN.

           DISPLAY '     '.
           DISPLAY 'TOTALES DE CONTROL DAPPGGED '.
           DISPLAY '--------------------------- '.
           DISPLAY '     '.
           DISPLAY 'TOTAL REGISTROS LEIDOS OPD       : ' WSS-LEI-OPD.
           DISPLAY 'TOTAL REGISTROS PROCESADOS ING   : ' WSS-PRO-ING.
           DISPLAY 'TOTAL REGISTROS PROCESADOS MOD   : ' WSS-PRO-MOD.
           DISPLAY '     '.
           DISPLAY 'TOTAL REGISTROS LEIDOS LIQ       : ' WSS-LEI-LIQ.
           DISPLAY 'TOTAL REGISTROS PROCESADOS ELI   : ' WSS-PRO-ELI.
           DISPLAY '     '.
           DISPLAY '     '.
           DISPLAY 'TOTAL REGISTROS GRABADOS MRC     : ' WSS-GRA-MRC.
           DISPLAY 'TOTAL REGISTROS GRABADOS MOP     : ' WSS-GRA-MOP.
           DISPLAY 'TOTAL REGISTROS GRABADOS MVA     : ' WSS-GRA-MVA.
           DISPLAY '     '.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-MRC.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-MOP.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM DAP-FIO-MVA.
           PERFORM GNS-PRO-END.

       COPY DAPBFOPD.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.

      *Entidades  de GNS
       COPY GNSBFMSC.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBGCAM.
       COPY GNSBGPES.
      *Entidades  de DAP
       COPY DAPBFRCC.
       COPY DAPBFVEN.
       COPY DAPBFLIQ.
       COPY DAPBFMRC.
       COPY DAPBFMOP.
       COPY DAPBFMVA.
      *Entidades  de TAB
       COPY TABBFCAM.
       PRO-OPD SECTION.
       INI-PRO-OPD.
      *     DISPLAY 'PRO-OPD ' OPD-CIC-IOPD IN OPD.
           ADD 1 TO WSS-LEI-OPD.

      * PROCESA OPD VIGENTES PARA 'C'reacion (Captaciones del dia)
           IF OPD-COD-STAT IN OPD = 'G' AND
              OPD-CIC-IOPD IN OPD = OPD-CIC-ORIG IN OPD
              PERFORM PRO-ING
              ADD 1 TO WSS-PRO-ING.

      * PROCESA OPD VIGENTES PARA 'M'odificacion (Renovaciones del dia)
           IF OPD-COD-STAT IN OPD = 'G' AND
              OPD-COD-RENO IN OPD = 'S'
              IF OPD-CAI-GREN IN OPD > SPACES OR
                 OPD-IIC-GREN IN OPD > ZEROES
                 PERFORM PRO-MOD
                 ADD 1 TO WSS-PRO-MOD.

       NXT-PRO-OPD.
           MOVE 'OPD-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
       FIN-PRO-OPD.
           EXIT.

       PRO-LIQ SECTION.
       INI-PRO-LIQ.
      *     DISPLAY 'PRO-LIQ ' LIQ-CIC-IOPD IN LIQ.

           ADD 1 TO WSS-LEI-LIQ IN WSS-VARI.

           IF NOT ( LIQ-COD-LIQU IN LIQ = 'LT' OR 'LP' OR 'RC' )
              GO TO NXT-PRO-LIQ.

           MOVE LIQ-CIC-IOPD IN LIQ TO OPD-CIC-IOPD IN OPD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-OPD.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' OPERACION NO ESTA EN BASE DE DATOS'
                          OPD-CIC-IOPD IN OPD  ' ' FIO-STAT
               PERFORM GNS-PRO-ABT.

      * Todas las Liquidaciones Anticipadas del dia
           IF LIQ-COD-LIQU IN LIQ = 'RC' AND
              LIQ-FEC-FCMP IN LIQ = WSS-FEC-FTRA IN WSS-VARI AND
              LIQ-FEC-FCMP IN LIQ = LIQ-FEC-FTRN IN LIQ
              ADD 1 TO WSS-PRO-ELI IN WSS-VARI
              PERFORM PRO-ELI.

      * Todas las Liquidaciones Anticipadas Futuras
           IF LIQ-COD-LIQU IN LIQ = 'RC' AND
              LIQ-FEC-FCMP IN LIQ NOT = WSS-FEC-FTRA IN WSS-VARI
              ADD 1 TO WSS-PRO-ELI IN WSS-VARI
              PERFORM PRO-ELI.

      * Liquidaciones Totales para Fijos (Ultimo Vcto.)
           IF LIQ-COD-LIQU IN LIQ = 'LT' AND
              OPD-COD-RENO IN OPD = 'N'  AND
              LIQ-FEC-FCMP IN LIQ NOT = WSS-FEC-FTRA IN WSS-VARI
              ADD 1 TO WSS-PRO-ELI IN WSS-VARI
              PERFORM PRO-ELI.

      * Liquidaciones Totales para Renovables (Comando RDL Total)
           IF LIQ-COD-LIQU IN LIQ = 'LT' AND
              OPD-COD-RENO IN OPD = 'S'  AND
              LIQ-FEC-FCMP IN LIQ = WSS-FEC-FTRA IN WSS-VARI
              ADD 1 TO WSS-PRO-ELI IN WSS-VARI
              PERFORM PRO-ELI.

      * Liquidaciones de Fijos con Retiros (Excepto el ultimo)
           IF LIQ-COD-LIQU IN LIQ = 'LP' AND
              OPD-COD-RENO IN OPD = 'N'  AND
              LIQ-FEC-FCMP IN LIQ NOT = WSS-FEC-FTRA IN WSS-VARI
              ADD 1 TO WSS-PRO-MOD IN WSS-VARI
              PERFORM PRO-MOD.

       NXT-PRO-LIQ.
           MOVE 'LIQ-FEC-FCMP' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-LIQ.
       FIN-PRO-LIQ.
           EXIT.

       PRO-ING SECTION.
       INI-PRO-ING.
      * Genera archivo de operaciones (GESRMOP)
      *    Cod. Operacion Movimiento
           MOVE 'C' TO MOP-COD-OPER IN MOP.

      *    Indicador Tipo de Accion
           MOVE 'I' TO MOP-MSC-TACC IN MOP.

      *    Indicador Status
           MOVE 'A' TO MOP-MSC-STAT IN MOP.

      *    Indicador Tipo Origen Transaccion
           MOVE 'B' TO MOP-MSC-TTRN IN MOP.

      *    Codigo Tipo Origen Transaccion
           MOVE SPACES TO MOP-COD-TTRN IN MOP.

      *    Codigo Status
           MOVE 'VIG' TO MOP-COD-STAT IN MOP.

      *    Ejecutivo
           MOVE OPD-COD-EJEC IN OPD TO MOP-COD-EJOP IN MOP.

      *    Fecha Inicio Operacion
           MOVE OPD-FEC-FREA IN OPD TO MOP-FEC-FIOP IN MOP.

      *    Fecha Termino Operacion
           MOVE OPD-FEC-FTER IN OPD TO MOP-FEC-FTOP IN MOP.

      *    Cod. Tipo de Tasa
           MOVE OPD-COD-TINT IN OPD TO MOP-TIP-TAS IN MOP.

      *    Tasa
           MOVE OPD-SGV-TINT IN OPD TO MOP-TAS IN MOP.

      *    Codigo 1
           MOVE OPD-COD-TCDP IN OPD TO MOP-COD-1 IN MOP.

      *    Monto 1
           MOVE OPD-VAL-CAPT IN OPD TO MOP-VAL-1 IN MOP.

           PERFORM GEN-RMOP.

      * Genera archivo de datos variables de las operaciones (GESRMVA)
      *    Cod. Operacion Movimiento
           MOVE 'C' TO MVA-COD-OPER IN MVA.

      *    Fecha Ultimo Movimiento
           MOVE OPD-FEC-FTRN IN OPD TO MVA-FEC-FULM IN MVA.

      *    Monto 1
           MOVE OPD-VAL-CAPT IN OPD TO MVA-VAL-VAR-1 IN MVA.

      *    Monto 2
           ADD 1 TO WSS-LEI-VEN IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-VAR2 IN WSS-VARI.
           MOVE OPD-CIC-IOPD IN OPD TO VEN-CIC-IOPD IN VEN.
           MOVE 1                   TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT FIO-STAT-OKS
              DISPLAY ' ERROR NO EXISTE VENCIMIENTO '
                                          VEN-KEY-IVEN IN VEN
           ELSE
              IF VEN-COD-STAT IN VEN  = 'N'
                  MOVE OPD-CIC-GREN IN OPD TO
                       VEN-CIC-IOPD IN VEN
                  MOVE FIO-GET-KEY TO FIO-CMND
                  PERFORM DAP-FIO-VEN
                  IF NOT FIO-STAT-OKS
                       DISPLAY ' ERROR NO EXISTE VENCIMIENTO '
                               'QUE RENUEVA' VEN-KEY-IVEN IN VEN

                  ELSE
                       COMPUTE WSS-VAL-VAR2 IN WSS-VARI =
                           VEN-VAL-FINA IN VEN - VEN-VAL-CAPI IN VEN
              ELSE
                 COMPUTE WSS-VAL-VAR2 IN WSS-VARI =
                      VEN-VAL-FINA IN VEN - VEN-VAL-CAPI IN VEN.

           MOVE WSS-VAL-VAR2 IN WSS-VARI TO MVA-VAL-VAR-2 IN MVA.

      *    Monto 3
           MOVE ZEROES TO MVA-VAL-VAR-3 IN MVA.

      *    Tasa 1
           MOVE OPD-SGV-TINT IN OPD TO MVA-TAS-VAR-1 IN MVA.

      *    Tasa 2
           MOVE OPD-SGV-SPRD IN OPD TO MVA-TAS-VAR-2 IN MVA.

      *    Cantidad 1
           MOVE ZEROES TO WSS-NUM-DIAS IN WSS-VARI.
           MOVE ZEROES TO WSS-FEC-FPA1 IN WSS-VARI.
           MOVE ZEROES TO WSS-FEC-FPA2 IN WSS-VARI.
           MOVE OPD-NUM-DREA IN OPD TO WSS-NUM-DPA1 IN WSS-VARI.
           MOVE OPD-NUM-MREA IN OPD TO WSS-NUM-MPA1 IN WSS-VARI.
           MOVE OPD-NUM-SREA IN OPD TO WSS-NUM-SPA1 IN WSS-VARI.
           MOVE OPD-NUM-AREA IN OPD TO WSS-NUM-APA1 IN WSS-VARI.

           MOVE OPD-NUM-DTER IN OPD TO WSS-NUM-DPA2 IN WSS-VARI.
           MOVE OPD-NUM-MTER IN OPD TO WSS-NUM-MPA2 IN WSS-VARI.
           MOVE OPD-NUM-STER IN OPD TO WSS-NUM-SPA2 IN WSS-VARI.
           MOVE OPD-NUM-ATER IN OPD TO WSS-NUM-APA2 IN WSS-VARI.

           MOVE WSS-FEC-FPA1 IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FPA2 IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA TO FEC-CMND.
           PERFORM CAL-FEC.
           IF FEC-STAT-OKS
               MOVE FEC-NDIA TO WSS-NUM-DIAS IN WSS-VARI
           ELSE
               DISPLAY 'ERROR EN RUTINA DIF. DIAS '
                                                FEC-MENS.
           MOVE WSS-NUM-DIAS IN WSS-VARI TO MVA-CTD-VAR-1 IN MVA.

      *    Fecha 1
           MOVE OPD-FEC-FREA IN OPD TO MVA-FEC-FVA1 IN MVA.

      *    Fecha 2
           MOVE OPD-FEC-FPVC IN OPD TO MVA-FEC-FVA2 IN MVA.

      *    Fecha Ult. Act. Registro Var.
           MOVE ZEROES TO MVA-FEC-FUAC IN MVA.

      *    Estado del Registro Var
           MOVE 'V' TO MVA-COD-EST IN MVA.

           PERFORM GEN-RMVA.

      * Genera archivo relacion cliente-operacion (GESRMRC)
       PRO-MRC.
           MOVE OPD-CAI-IOPD IN OPD TO RCC-CAI-IOPD IN RCC.
           MOVE OPD-IIC-IOPD IN OPD TO RCC-IIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' NO EXISTE RCC PARA OPERACION '
                        OPD-CIC-IOPD IN OPD ' ' FIO-STAT
              PERFORM GNS-PRO-ABT.

       LUP-PRO-MRC.
      *    Codigo de Operacion
           MOVE 'C' TO MRC-COD-OPER IN MRC.

      *    Indicador Tipo de Accion
           MOVE 'I' TO MRC-MSC-TACC IN MRC.

      *    Indicador Status
           MOVE 'A' TO MRC-MSC-STAT IN MRC.

      *    Indicador Tipo Origen Transaccion
           MOVE 'B' TO MRC-MSC-TTRN IN MRC.

      *    Codigo Status
           MOVE 'VIG' TO MRC-COD-STAT IN MRC.

      *    Numero de Direccion
           MOVE RCC-NUM-DIRE IN RCC TO MRC-NUM-DIRE IN MRC.

      *    Fecha Inicio Operacion
           MOVE OPD-FEC-FREA IN OPD TO MRC-FEC-FIRL IN MRC.

      *    Fecha Termino Operacion
           MOVE ZEROES              TO MRC-FEC-FTRL IN MRC.

           PERFORM GEN-RMRC.

       NXT-PRO-MRC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF FIO-STAT-OKS AND
              OPD-CAI-IOPD IN OPD = RCC-CAI-IOPD IN RCC AND
              OPD-IIC-IOPD IN OPD = RCC-IIC-IOPD IN RCC
              GO TO LUP-PRO-MRC.

       FIN-PRO-ING.
           EXIT.

       PRO-MOD SECTION.
       INI-PRO-MOD.
      * Genera archivo de datos variables de las operaciones (GESRMVA)
      *    Cod. Operacion Movimiento
           MOVE 'M' TO MVA-COD-OPER IN MVA.

      *    Fecha Ultimo Movimiento
           MOVE OPD-FEC-FTRN IN OPD TO MVA-FEC-FULM IN MVA.

      *    Monto 1
           MOVE OPD-VAL-CAPT IN OPD TO MVA-VAL-VAR-1 IN MVA.

      *    Monto 2
           ADD 1 TO WSS-LEI-VEN IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-VAR2 IN WSS-VARI.
           MOVE OPD-CIC-IOPD IN OPD TO VEN-CIC-IOPD IN VEN.
           MOVE 1                   TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-VEN.
           IF NOT FIO-STAT-OKS
              DISPLAY ' ERROR NO EXISTE VENCIMIENTO '
                                          VEN-KEY-IVEN IN VEN
              MOVE ZEROES TO MVA-VAL-VAR-2 IN MVA
              MOVE ZEROES TO MVA-VAL-VAR-3 IN MVA
           ELSE
              IF VEN-COD-STAT IN VEN  = 'N'
                  MOVE OPD-CIC-GREN IN OPD TO
                       VEN-CIC-IOPD IN VEN
                  MOVE FIO-GET-KEY TO FIO-CMND
                  PERFORM DAP-FIO-VEN
                  IF NOT FIO-STAT-OKS
                       DISPLAY ' ERROR NO EXISTE VENCIMIENTO '
                               'QUE RENUEVA' VEN-KEY-IVEN IN VEN
                  ELSE
                       COMPUTE WSS-VAL-VAR2 IN WSS-VARI =
                           VEN-VAL-FINA IN VEN - VEN-VAL-CAPI IN VEN
              ELSE
                 COMPUTE WSS-VAL-VAR2 IN WSS-VARI =
                      VEN-VAL-FINA IN VEN - VEN-VAL-CAPI IN VEN.

              MOVE WSS-VAL-VAR2 IN WSS-VARI TO
                                           MVA-VAL-VAR-2 IN MVA.
      *    Monto 3
              MOVE VEN-VAL-CAPI IN VEN TO MVA-VAL-VAR-3 IN MVA.

      *    Tasa 1
           MOVE OPD-SGV-TINT IN OPD TO MVA-TAS-VAR-1 IN MVA.

      *    Tasa 2
           MOVE OPD-SGV-SPRD IN OPD TO MVA-TAS-VAR-2 IN MVA.

      *    Cantidad 1
           MOVE ZEROES TO WSS-NUM-DIAS IN WSS-VARI.
           MOVE ZEROES TO WSS-FEC-FPA1 IN WSS-VARI.
           MOVE ZEROES TO WSS-FEC-FPA2 IN WSS-VARI.
           MOVE OPD-NUM-DREA IN OPD TO WSS-NUM-DPA1 IN WSS-VARI.
           MOVE OPD-NUM-MREA IN OPD TO WSS-NUM-MPA1 IN WSS-VARI.
           MOVE OPD-NUM-SREA IN OPD TO WSS-NUM-SPA1 IN WSS-VARI.
           MOVE OPD-NUM-AREA IN OPD TO WSS-NUM-APA1 IN WSS-VARI.

           MOVE OPD-NUM-DTER IN OPD TO WSS-NUM-DPA2 IN WSS-VARI.
           MOVE OPD-NUM-MTER IN OPD TO WSS-NUM-MPA2 IN WSS-VARI.
           MOVE OPD-NUM-STER IN OPD TO WSS-NUM-SPA2 IN WSS-VARI.
           MOVE OPD-NUM-ATER IN OPD TO WSS-NUM-APA2 IN WSS-VARI.

           MOVE WSS-FEC-FPA1 IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FPA2 IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA TO FEC-CMND.
           PERFORM CAL-FEC.
           IF FEC-STAT-OKS
               MOVE FEC-NDIA TO WSS-NUM-DIAS IN WSS-VARI
           ELSE
               DISPLAY 'ERROR EN RUTINA DIF. DIAS '
                                                FEC-MENS.
           MOVE WSS-NUM-DIAS IN WSS-VARI TO MVA-CTD-VAR-1 IN MVA.

      *    Fecha 1
           MOVE OPD-FEC-FREA IN OPD TO MVA-FEC-FVA1 IN MVA.

      *    Fecha 2
           MOVE OPD-FEC-FPVC IN OPD TO MVA-FEC-FVA2 IN MVA.

      *    Fecha Ult. Act. Registro Var.
           MOVE FEC-SHOY TO MVA-NUM-SUAC IN MVA.
           MOVE FEC-AHOY TO MVA-NUM-AUAC IN MVA.
           MOVE FEC-MHOY TO MVA-NUM-MUAC IN MVA.
           MOVE FEC-DHOY TO MVA-NUM-DUAC IN MVA.

      *    Estado del Registro Var
           MOVE 'V' TO MVA-COD-EST IN MVA.

           PERFORM GEN-RMVA.

       FIN-PRO-MOD.
           EXIT.

       PRO-ELI SECTION.
       INI-PRO-ELI.
      * Genera archivo de operaciones (GESRMOP)
      *    Cod. Operacion Movimiento
           MOVE 'E' TO MOP-COD-OPER IN MOP.

      *    Indicador Tipo de Accion
           MOVE 'E' TO MOP-MSC-TACC IN MOP.

      *    Indicador Status
           MOVE 'E' TO MOP-MSC-STAT IN MOP.

      *    Indicador Tipo Origen Transaccion
           MOVE 'B' TO MOP-MSC-TTRN IN MOP.

      *    Codigo Tipo Origen Transaccion
           MOVE SPACES TO MOP-COD-TTRN IN MOP.

      *    Codigo Status
           MOVE 'ELI' TO MOP-COD-STAT IN MOP.

      *    Ejecutivo
           MOVE SPACES TO MOP-COD-EJOP IN MOP.

      *    Fecha Inicio Operacion
           MOVE ZEROES TO MOP-FEC-FIOP IN MOP.

      *    Fecha Termino Operacion
           MOVE OPD-FEC-FTER IN OPD TO MOP-FEC-FTOP IN MOP.

      *    Cod. Tipo de Tasa
           MOVE SPACES TO MOP-TIP-TAS IN MOP.

      *    Tasa
           MOVE ZEROES TO MOP-TAS IN MOP.

      *    Codigo 1
           MOVE SPACES TO MOP-COD-1 IN MOP.

      *    Monto 1
           MOVE ZEROES TO MOP-VAL-1 IN MOP.

           PERFORM GEN-RMOP.

      * Genera archivo de datos variables de las operaciones (GESRMVA)
      *    Cod. Operacion Movimiento
           MOVE 'E' TO MVA-COD-OPER IN MVA.

      *    Fecha Ultimo Movimiento
           MOVE OPD-FEC-FTRN IN OPD TO MVA-FEC-FULM IN MVA.

      *    Monto 1
           MOVE ZEROES TO MVA-VAL-VAR-1 IN MVA.

      *    Monto 2
           MOVE ZEROES TO MVA-VAL-VAR-2 IN MVA.

      *    Monto 3
           MOVE ZEROES TO MVA-VAL-VAR-3 IN MVA.

      *    Tasa 1
           MOVE ZEROES TO MVA-TAS-VAR-1 IN MVA.

      *    Tasa 2
           MOVE ZEROES TO MVA-TAS-VAR-2 IN MVA.

      *    Cantidad 1
           MOVE ZEROES TO MVA-CTD-VAR-1 IN MVA.

      *    Fecha 1
           MOVE ZEROES TO MVA-FEC-FVA1 IN MVA.

      *    Fecha 2
           MOVE ZEROES TO MVA-FEC-FVA2 IN MVA.

      *    Fecha Ult. Act. Registro Var.
           MOVE FEC-SHOY TO MVA-NUM-SUAC IN MVA.
           MOVE FEC-AHOY TO MVA-NUM-AUAC IN MVA.
           MOVE FEC-MHOY TO MVA-NUM-MUAC IN MVA.
           MOVE FEC-DHOY TO MVA-NUM-DUAC IN MVA.

      *    Estado del Registro Var
           MOVE 'E' TO MVA-COD-EST IN MVA.
           PERFORM GEN-RMVA.

      * Genera archivo relacion cliente-operacion (GESRMRC)
       PRO-ELI-MRC.
           MOVE OPD-CAI-IOPD IN OPD TO RCC-CAI-IOPD IN RCC.
           MOVE OPD-IIC-IOPD IN OPD TO RCC-IIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF NOT FIO-STAT-OKS
              GO TO FIN-PRO-ELI.

       LUP-PRO-ELI-MRC.
      *    Codigo de Operacion
           MOVE 'E' TO MRC-COD-OPER IN MRC.

      *    Indicador Tipo de Accion
           MOVE 'E' TO MRC-MSC-TACC IN MRC.

      *    Indicador Status
           MOVE 'E' TO MRC-MSC-STAT IN MRC.

      *    Indicador Tipo Origen Transaccion
           MOVE 'B' TO MRC-MSC-TTRN IN MRC.

      *    Codigo Status
           MOVE 'ELI' TO MRC-COD-STAT IN MRC.

      *    Numero de Direccion
           MOVE ZEROES TO MRC-NUM-DIRE IN MRC.

      *    Fecha Inicio Relacion
           MOVE ZEROES TO MRC-FEC-FIRL IN MRC.

      *    Fecha Termino Relacion
           MOVE FEC-SHOY TO MRC-NUM-STRL IN MRC.
           MOVE FEC-AHOY TO MRC-NUM-ATRL IN MRC.
           MOVE FEC-MHOY TO MRC-NUM-MTRL IN MRC.
           MOVE FEC-DHOY TO MRC-NUM-DTRL IN MRC.

           PERFORM GEN-RMRC.

       NXT-PRO-ELI-MRC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF FIO-STAT-OKS AND
              OPD-CAI-IOPD IN OPD = RCC-CAI-IOPD IN RCC AND
              OPD-IIC-IOPD IN OPD = RCC-IIC-IOPD IN RCC
              GO TO LUP-PRO-ELI-MRC.
       FIN-PRO-ELI.
           EXIT.

       GEN-RMOP SECTION.
       INI-GEN-RMOP.
           PERFORM GET-CUR-RCC.
           IF WSS-STAT-RCC IN WSS-VARI = 0
               PERFORM GET-NXT-RCC.

      *    Cod. Int. Comp. Operacion
           MOVE OPD-CIC-IOPD IN OPD TO MOP-CIC-IOPE IN MOP.

      *    Fecha de Movimiento
           MOVE WSS-NUM-SPRO TO MOP-NUM-STRN IN MOP.
           MOVE WSS-NUM-APRO TO MOP-NUM-ATRN IN MOP.
           MOVE WSS-NUM-MPRO TO MOP-NUM-MTRN IN MOP.
           MOVE WSS-NUM-DPRO TO MOP-NUM-DTRN IN MOP.

      *    Hora de Movimiento
           MOVE ZEROES TO MOP-HRA-HRTR IN MOP.

      *    Autor de la Transaccion
           MOVE 'SISTDAP' TO MOP-COD-ATRN IN MOP.

      *    Valor de Cambio Operacion (Moneda)
           MOVE OPD-COD-VCAM IN OPD TO MOP-COD-VCOP IN MOP.
      * FHER 09-06-93
           IF OPD-COD-VCAM IN OPD = '1013  '
              MOVE '01'             TO MOP-COD-TCOP IN MOP.

      *    Codigo Clase de la Operacion
           MOVE OPD-COD-TOPD IN OPD TO MOP-COD-TOPR IN MOP.

      *    Codigo Auxiliar de la Operacion
           MOVE OPD-COD-AOPD IN OPD TO MOP-COD-AOPR IN MOP.

      *    Codigo Oficina
           MOVE OPD-COD-OFIC IN OPD TO MOP-COD-OFOP IN MOP.

      *    Motivo Termino Operacion
           MOVE SPACES TO MOP-COD-MOTT IN MOP.

      *    Fecha Reapertura Operacion
           MOVE ZEROES TO MOP-FEC-FROP IN MOP.

      *    Ind. Multiples Clientes
           MOVE SPACES TO MOP-IND-MCLI IN MOP.
           IF WSS-IND-MCLI IN WSS-VARI = 'M'
              MOVE 'MC' TO MOP-IND-MCLI IN MOP

      *    Producto de Marketing
           MOVE SPACES TO MOP-COD-PDT IN MOP.

      *    Funcion (para costo funcion)
           MOVE SPACES TO MOP-COD-FCN IN MOP.

      *    Codigo Plazo operacion
           MOVE OPD-COD-PLZO TO MOP-COD-PZO IN MOP.

      *    Area de gestion 1
           MOVE SPACES TO MOP-COD-AGE-1 IN MOP.

      *    Cod. Operacion Relacionada 1
           MOVE SPACES TO MOP-COP-REL-1 IN MOP.

      *    Cod. Operacion Relacionada 2
           MOVE SPACES TO MOP-COP-REL-2 IN MOP.

      *    Codigo 2
           MOVE SPACES TO MOP-COD-2 IN MOP.

      *    Codigo 3
           MOVE SPACES TO MOP-COD-3 IN MOP.

      *    Indicador 1
           MOVE SPACES TO MOP-IND-1 IN MOP.

      *    Fecha 1
           MOVE ZEROES TO MOP-FEC-FUNO IN MOP.

      *    Ejecutivo 1
           MOVE OPD-COD-EATE IN OPD TO MOP-COD-EJE-1 IN MOP.

      *    Disponible
           MOVE SPACES TO MOP-FILLER-2 IN MOP.

      *    Estado Actualiz. del Mvto.
           MOVE SPACES TO MOP-COD-EST-ACT IN MOP.

      *    DISPLAY '        '.
      *    DISPLAY 'MOP ==> ' MOP-CIC-IOPE IN MOP ' '
      *                       MOP-COD-OPER IN MOP ' '.
      *    DISPLAY '        '.

           MOVE FIO-PUT TO FIO-CMND.
      *  Archivo Intereses y Reajustes Devengados
           PERFORM DAP-FIO-MOP.
           IF NOT FIO-STAT-OKS
              DISPLAY 'PROCESO CANCELADO '
              DISPLAY 'ERROR FIO-PUT MOP, FIO-STAT : ' FIO-STAT
              PERFORM GNS-PRO-ABT
           ELSE
              ADD 1 TO WSS-GRA-MOP.
       FIN-GEN-RMOP.
           EXIT.

       GEN-RMRC SECTION.
       INI-GEN-RMRC.
      *    Cod. Int. Comp. Cliente
           MOVE RCC-CIC-ICLI IN RCC TO MRC-CIC-ICLI IN MRC.

      *    Cod. Int. Comp. Operacion
           MOVE RCC-CIC-IOPD IN RCC TO MRC-CIC-IOPE IN MRC.

      *    Fecha de Movimiento
           MOVE WSS-NUM-SPRO TO MRC-NUM-STRN IN MRC.
           MOVE WSS-NUM-APRO TO MRC-NUM-ATRN IN MRC.
           MOVE WSS-NUM-MPRO TO MRC-NUM-MTRN IN MRC.
           MOVE WSS-NUM-DPRO TO MRC-NUM-DTRN IN MRC.

      *    Hora de Movimiento
           MOVE ZEROES TO MRC-HRA-HTRN IN MRC.

      *    Autor de la Transaccion
           MOVE 'SISTDAP' TO MRC-COD-ATRN IN MRC.

      *    Cod. Relacion Cliente Operacion
           MOVE RCC-COD-RCOP IN RCC TO MRC-COD-RCOP IN MRC.

      *    Porcentaje Relacion Cliente Operacion
           MOVE RCC-VAL-PRCO IN RCC TO MRC-VAL-PRCO IN MRC.

      *    Codigo Tipo de Operacion
           MOVE OPD-COD-TOPD IN OPD TO MRC-COD-TOPR IN MRC.

      *    Codigo Auxiliar de la Operacion
           MOVE OPD-COD-AOPD IN OPD TO MRC-COD-AOPR IN MRC.

      *    Disponible
           MOVE SPACES TO MRC-GLS-DISP IN MRC.

      *    Estado Actualiz. del Mvto.
           MOVE SPACES TO MRC-COD-EST-ACT IN MRC.

      *    DISPLAY '        '.
      *    DISPLAY 'MRC ==> ' MRC-COD-OPER IN MRC ' '
      *                       MRC-CIC-ICLI IN MRC ' '
      *                       MRC-CIC-IOPE IN MRC ' '
      *                       MRC-FEC-FTRN IN MRC.
      *    DISPLAY '        '.
           MOVE FIO-PUT TO FIO-CMND.
      *  Archivo Relacion Cliente Operacion
           PERFORM DAP-FIO-MRC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'PROCESO CANCELADO '
              DISPLAY 'ERROR FIO-PUT MRC, FIO-STAT : ' FIO-STAT
              PERFORM GNS-PRO-ABT
           ELSE
              ADD 1 TO WSS-GRA-MRC.
       FIN-GEN-RMRC.
           EXIT.

       GEN-RMVA SECTION.
       INI-GEN-RMVA.
      *    Cod. Int. Comp. Operacion
           MOVE OPD-CIC-IOPD IN OPD TO MVA-CIC-IOPE IN MVA.

      *    Fecha de Vigencia
           MOVE WSS-NUM-SPRO TO MVA-NUM-SVIG IN MVA.
           MOVE WSS-NUM-APRO TO MVA-NUM-AVIG IN MVA.
           MOVE WSS-NUM-MPRO TO MVA-NUM-MVIG IN MVA.
           MOVE WSS-NUM-DPRO TO MVA-NUM-DVIG IN MVA.

      *    Numero Secuencia
           MOVE '0' TO MVA-NUM-SEC IN MVA.

      *    Monto 4
           MOVE ZEROES TO MVA-VAL-VAR-4 IN MVA.

      *    Monto 5
           MOVE ZEROES TO MVA-VAL-VAR-5 IN MVA.

      *    Monto 6
           MOVE ZEROES TO MVA-VAL-VAR-6 IN MVA.

      *    Cantidad 2
           MOVE ZEROES TO MVA-CTD-VAR-2 IN MVA.

      *    Cantiadad 3
           MOVE ZEROES TO MVA-CTD-VAR-3 IN MVA.

      *    Cantiadad 4
           MOVE ZEROES TO MVA-CTD-VAR-4 IN MVA.

      *    Codigo 1
           MOVE OPD-COD-EATE IN OPD TO MVA-COD-VAR-1 IN MVA.

      *    Codigo 2
           MOVE SPACES TO MVA-COD-VAR-2 IN MVA.

      *    Disponible
           MOVE SPACES TO MVA-GLS-DISP IN MVA.

      *    Estado Actualiz. del Mvto.
           MOVE SPACES TO MVA-COD-EST-ACT IN MVA.

      *    DISPLAY '        '.
      *    DISPLAY 'MVA ==> ' MVA-CIC-IOPE IN MVA ' '
      *                       MVA-COD-OPER IN MVA ' '
      *                       MVA-FEC-FVIG IN MVA ' '
      *                       MVA-NUM-SEC  IN MVA.
      *    DISPLAY '        '.
           MOVE FIO-PUT TO FIO-CMND.
      *  Archivo Intereses y Reajustes Devengados
           PERFORM DAP-FIO-MVA.
           IF NOT FIO-STAT-OKS
              DISPLAY 'PROCESO CANCELADO '
              DISPLAY 'ERROR FIO-PUT MVA, FIO-STAT : ' FIO-STAT
              PERFORM GNS-PRO-ABT
           ELSE
              ADD 1 TO WSS-GRA-MVA.
       FIN-GEN-RMVA.
           EXIT.

       GET-CUR-RCC SECTION.
       INI-GET-CUR-RCC.
           MOVE 0  TO WSS-STAT-RCC IN WSS-VARI.
           MOVE OPD-CAI-IOPD IN OPD TO RCC-CAI-IOPD IN RCC.
           MOVE OPD-IIC-IOPD IN OPD TO RCC-IIC-IOPD IN RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' NO EXISTE RCC PARA OPERACION '
                        OPD-CIC-IOPD IN OPD ' ' FIO-STAT
              PERFORM GNS-PRO-ABT.
       FIN-GET-CUR-RCC.
           EXIT.

       GET-NXT-RCC SECTION.
       INI-GET-NXT-RCC.
           MOVE 'RCC-KEY-IOPD' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DAP-FIO-RCC.
           IF FIO-STAT-OKS AND
              OPD-CAI-IOPD IN OPD = RCC-CAI-IOPD IN RCC AND
              OPD-IIC-IOPD IN OPD = RCC-IIC-IOPD IN RCC
              MOVE 'M'    TO WSS-IND-MCLI IN WSS-VARI
           ELSE
              MOVE SPACES TO WSS-IND-MCLI IN WSS-VARI.
       FIN-GET-NXT-RCC.
           EXIT.

       ACC-FPRO SECTION.
       INI-ACC-FPRO.
      * ISP: ACEPTA COMO PARAMETRO LA FECHA DE PROCESO,
      *      [ FECHA DE PROCESO + 1 = FECHA DE INICIO,
      *      FECHA DE TERMINO ! DONDE FECHA DE TERMINO ES EL PROXIMO DIA
      *      HABIL A FECHA DE PROCESO

           DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA) : '.
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.

      *VAL-FEC Valida Fecha de proceso
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR VALIDACION EN F. PROCESO '
                                            WSS-FEC-FPRO
               PERFORM GNS-PRO-ABT
           ELSE
               MOVE WSS-NUM-DPRO TO WSS-NUM-DTRA
               MOVE WSS-NUM-MPRO TO WSS-NUM-MTRA
               MOVE WSS-NUM-SPRO TO WSS-NUM-STRA
               MOVE WSS-NUM-APRO TO WSS-NUM-ATRA
      * ISP: OBTIENE FECHA DE MANANA ( YA ESTA CARGADO FEC-FECH )
               MOVE FEC-SUM-DIA  TO FEC-CMND
               MOVE 1            TO FEC-NDIA
               MOVE FEC-FORM-FEC TO FEC-FORM
               PERFORM CAL-FEC
               IF NOT FEC-STAT-OKS
                  DISPLAY ' '
                  DISPLAY ' PROCESO CANCELADO '
                  DISPLAY ' ERROR EN OBTENCION FECHA MANANA '
                  PERFORM GNS-PRO-ABT
               ELSE
                    MOVE FEC-ITM1    TO WSS-NUM-DINI
                    MOVE FEC-ITM2    TO WSS-NUM-MINI
                    MOVE FEC-ITM3    TO WSS-NUM-SINI
                    MOVE FEC-ITM4    TO WSS-NUM-AINI.

       CIC-ACC-FPRO.
      * ISP: YA ESTA CARGADO FEC-FECH
                    MOVE FEC-FORM-FEC TO FEC-FORM
                    MOVE FEC-FBLK-N  TO FEC-FBLK
                    MOVE FEC-CHOY-N  TO FEC-CHOY
                    MOVE FEC-VHBL-S  TO FEC-VHBL
                    MOVE FEC-VAL-FEC TO FEC-CMND
                    PERFORM PRO-FEC
                    IF NOT FEC-STAT-HBL
                        MOVE FEC-SUM-DIA  TO FEC-CMND
                        MOVE 1            TO FEC-NDIA
                        MOVE FEC-FORM-FEC TO FEC-FORM
                        PERFORM CAL-FEC
                        IF NOT FEC-STAT-OKS
                           DISPLAY ' '
                           DISPLAY ' PROCESO CANCELADO '
                           DISPLAY ' ERROR EN OBTENCION FECHA MANANA '
                           PERFORM GNS-PRO-ABT
                        ELSE
                           GO TO CIC-ACC-FPRO
                    ELSE
                        MOVE FEC-ITM1    TO WSS-NUM-DFIN
                        MOVE FEC-ITM2    TO WSS-NUM-MFIN
                        MOVE FEC-ITM3    TO WSS-NUM-SFIN
                        MOVE FEC-ITM4    TO WSS-NUM-AFIN.

           MOVE WSS-NUM-DTRA TO WSS-NUM-DPA1 IN WSS-VARI.
           MOVE WSS-NUM-MTRA TO WSS-NUM-MPA1 IN WSS-VARI.
           MOVE WSS-NUM-STRA TO WSS-NUM-SPA1 IN WSS-VARI.
           MOVE WSS-NUM-ATRA TO WSS-NUM-APA1 IN WSS-VARI.
      *
           MOVE WSS-NUM-DFIN TO WSS-NUM-DPA2 IN WSS-VARI.
           MOVE WSS-NUM-MFIN TO WSS-NUM-MPA2 IN WSS-VARI.
           MOVE WSS-NUM-SFIN TO WSS-NUM-SPA2 IN WSS-VARI.
           MOVE WSS-NUM-AFIN TO WSS-NUM-APA2 IN WSS-VARI.

           DISPLAY 'FECHA DE PROCESO           : ' WSS-FEC-FPRO.
           DISPLAY 'FECHA INICIO SELECCION     : ' WSS-FEC-FPA1.
           DISPLAY 'FECHA TERMINO DE SELECCION : ' WSS-FEC-FPA2.
       FIN-ACC-FPRO.
           EXIT.
