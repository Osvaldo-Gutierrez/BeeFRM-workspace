000000 IDENTIFICATION DIVISION.
000000*=======================
000000 PROGRAM-ID.      DAPPLVSC.
000000 AUTHOR.          P CONCHA L.
000000 DATE-WRITTEN.    30-NOV-93.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
000000******************************************************************
000000*  SISTEMA    : DEPOSITOS A PLAZO
000000*  SUB-SISTEMA: INFORMES DAP
000000*  ANALISTA   : CECILIA PIZARRO.
000000*  OBJETIVO :
000000*           - GENERAR INFORME VARIACIONES DE SALDOS DE CARTERA
000000*  ARCHIVOS :
000000*  INP      :
000000*             DAPTGRU  TABLA DE GRUPOS DE OPERACIONES
000000*             DAPDVSO  ARCHIVO DE ACUMULACION DE SALDOS DIARIOS
000000*             TABLA TAB: BD 150
000000*             TABLA OFI: BD 150
000000*  OUT      :
000000*             DAPLVSC  INFORME VARIACIONES DE SALDOS DE CARTERA
000000*  WRK      :
000000*             FSORT    ARCHIVO PARA ORDENAMIENTO
000000*
000000******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
000000     EJECT
000000 ENVIRONMENT DIVISION.
000000*====================
000000 CONFIGURATION SECTION.
000000*---------------------
000000 SPECIAL-NAMES.
000000      C01 IS CANAL-1
000000      DECIMAL-POINT IS COMMA.
000000 INPUT-OUTPUT SECTION.
000000*--------------------
000000 FILE-CONTROL.
000000     SELECT DAPTGRU        ASSIGN TO        UT-S-DAPTGRU.
000000     SELECT DAPDVSO        ASSIGN TO        DAPDVSO
000000        ORGANIZATION IS INDEXED
000000        ACCESS       IS DYNAMIC
000000        RECORD KEY   IS DAPDVSO-KEY-IVSO
000000        FILE STATUS  IS FS-DAPDVSO.
000000     SELECT FSORT          ASSIGN TO        UT-S-SORTWK1.
000000     SELECT DAPLVSC        ASSIGN TO        UT-S-DAPLVSC.
000000     EJECT
000000 DATA DIVISION.
000000*=============
000000 FILE SECTION.
000000*------------
000000*
000000 FD  DAPTGRU
000000     RECORDING MODE IS F
000000     BLOCK CONTAINS 0 RECORDS
000000     LABEL RECORDS STANDARD.
000000 01  REG-DAPTGRU.
000000     05 DAPTGRU-COD-VCAM PIC X(06).
000000     05 FILLER           PIC X(01).
000000     05 DAPTGRU-COD-PLZO PIC X(02).
000000     05 FILLER           PIC X(01).
000000     05 DAPTGRU-COD-GRUP PIC X(01).
000000     05 FILLER           PIC X(01).
000000     05 DAPTGRU-GLS-GRUP PIC X(45).
000000     05 FILLER           PIC X(23).
000000*
      *
       FD  DAPDVSO
           LABEL RECORDS STANDARD.
      *    VARIACION SALDOS POR OFICINA
      *    ----------------------------
      *
      *    ESTE REGISTRO PERMITE ALMACENAR LOS DATOS BASICOS PARA LA
      *    GENERACION DEL INFORME DE VARIACION DE SALDOS POR OFICINA.
      *
      *    NOMBRE REGISTRO : DAPDVSO
      *    CLAVE(S)        : DAPDVSO-KEY-IVSO(UU)
      *    LARGO           : 50
      *    BLOQUEO         : 1
      *    OBSERVACIONES   :
      *
       01  REG-DAPDVSO.
      *
      *    IDENTIFICACION DAPDVSO
           05 DAPDVSO-KEY-IVSO.
      *
      *       FECHA PROCESO
              10 DAPDVSO-FEC-FPRO.
                 15 DAPDVSO-NUM-SPRO       PIC 9(02).
                 15 DAPDVSO-NUM-APRO       PIC 9(02).
                 15 DAPDVSO-NUM-MPRO       PIC 9(02).
                 15 DAPDVSO-NUM-DPRO       PIC 9(02).
      *
      *       CODIGO VALOR DE CAMBIO
              10 DAPDVSO-COD-VCAM.
      *
      *          MISCELANEO TIPO VALOR TAB TVAL
                 15 DAPDVSO-MSC-TVAL       PIC X(01).
      *
      *          CODIGO VALOR
                 15 DAPDVSO-COD-CVAL       PIC X(03).
      *
      *          CODIGO TIPO CAMBIO
                 15 DAPDVSO-COD-TCAM       PIC X(02).
      *
      *       CODIGO DE PLAZO OPERACION
              10 DAPDVSO-COD-PLZO          PIC X(02).
      *
      *       CODIGO OFICINA OPERACION DAP TAB OFI
              10 DAPDVSO-COD-OFIC          PIC X(03).
      *
      *    SALDO CAPTACION A LA FECHA
           05 DAPDVSO-VAL-SCAP             PIC S9(11)V9(04) COMP-3.
      *
      *    CANTIDAD DE DOCUMENTOS
           05 DAPDVSO-CAN-OPER             PIC S9(11) COMP-3.
      *
      *    GLOSA DISPONIBLE
           05 DAPDVSO-GLS-DISP             PIC X(17).
      *
000000*
000000 SD  FSORT.
000000 01  REG-FSORT.
000000     05 FSORT-KEY.
000000        10 FSORT-COD-VCAM        PIC X(06).
000000        10 FSORT-COD-GRUP        PIC X(01).
000000        10 FSORT-COD-OFIC        PIC X(03).
000000     05 FSORT-COD-TIPO           PIC X(01).
000000     05 FSORT-VAL-SCAP           PIC S9(13)V9(04) COMP-3.
000000     05 FSORT-CAN-OPER           PIC S9(11)       COMP-3.
000000     05 FSORT-GLS-GRUP           PIC X(45).
000000*
000000 FD  DAPLVSC
000000     RECORDING MODE IS F
000000     BLOCK CONTAINS 0 RECORDS
000000     LABEL RECORDS STANDARD.
000000 01  REG-DAPLVSC       PIC X(132).
000000*
000000     EJECT
000000 WORKING-STORAGE SECTION.
000000*-----------------------
000000*
000000     COPY BIBBAUD1.
000000*
000000 01  W-CONTADORES.
000000     05 CNT-DAPTGRU-L            PIC 9(08) VALUE 0.
000000     05 CNT-DAPDVSO-L            PIC 9(08) VALUE 0.
000000     05 CNT-DAPDVSO-S            PIC 9(08) VALUE 0.
000000     05 CNT-DAPDVSO-I            PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-G              PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-L              PIC 9(08) VALUE 0.
000000     05 CNT-LINE-DAPLVSC         PIC 9(03) VALUE 0.
000000        88 SALTO-PAG-DAPLVSC               VALUE 60 THRU 99.
000000     05 CNT-PAGE-DAPLVSC         PIC 9(04) VALUE 0.
000000*
000000 01  WE-EXTERNAS.
000000     05 WE-FECHA-DATE.
000000        10 WE-AA-DATE            PIC 9(02).
000000        10 WE-MM-DATE            PIC 9(02).
000000        10 WE-DD-DATE            PIC 9(02).
000000*
000000 01  F-FILE-STATUS.
000000     05 FS-DAPTGRU               PIC 9(01) VALUE 0.
000000     05 FS-DAPDVSO               PIC 9(02) VALUE 0.
000000     05 FS-FSORT                 PIC 9(01) VALUE 0.
000000*
000000 01  W-VARIABLES.
000000     05 SW-CANCEL                PIC 9(01) VALUE 0.
000000*
000000 01  W-PARAMETROS.
000000* FECHA DE PROCESO
000000     05 WP-FEC-FPRI.
000000        10 WP-NUM-DPRI           PIC X(02).
000000        10 WP-NUM-MPRI           PIC X(02).
000000        10 WP-NUM-SPRI           PIC X(02).
000000        10 WP-NUM-APRI           PIC X(02).
000000* FECHA ANTERIOR
000000     05 WP-FEC-FFAN.
000000        10 WP-NUM-DFAN           PIC X(02).
000000        10 WP-NUM-MFAN           PIC X(02).
000000        10 WP-NUM-SFAN           PIC X(02).
000000        10 WP-NUM-AFAN           PIC X(02).
000000*
000000 01  W-REGISTROS-DE-TRABAJO.
000000     05 WR-COD-TIPO              PIC X(01) VALUE SPACES.
000000     05 WR-COD-OFIC              PIC X(03) VALUE SPACES.
000000     05 WR-GLS-OFIC              PIC X(24) VALUE SPACES.
000000     05 WR-COD-VCAM              PIC X(06) VALUE SPACES.
000000     05 WR-GLS-VCAM              PIC X(20) VALUE SPACES.
000000     05 WR-COD-GRUP              PIC X(01) VALUE SPACES.
000000     05 WR-GLS-GRUP              PIC X(45) VALUE SPACES.
000000     05 WS-FEC-FFAN.
000000        10 WS-NUM-SFAN           PIC 9(02).
000000        10 WS-NUM-AFAN           PIC 9(02).
000000        10 WS-NUM-MFAN           PIC 9(02).
000000        10 WS-NUM-DFAN           PIC 9(02).
000000     05 WS-FEC-FFAC.
000000        10 WS-NUM-SFAC           PIC 9(02).
000000        10 WS-NUM-AFAC           PIC 9(02).
000000        10 WS-NUM-MFAC           PIC 9(02).
000000        10 WS-NUM-DFAC           PIC 9(02).
000000*
000000 01  ACM-GRUP.
000000     05 ACM-GRUP-ANTE-VAL-SCAP PIC S9(13)V9(4)  COMP-3 VALUE +0.
000000     05 ACM-GRUP-ANTE-CAN-OPER PIC S9(11)       COMP-3 VALUE +0.
000000     05 ACM-GRUP-ACTU-VAL-SCAP PIC S9(13)V9(4)  COMP-3 VALUE +0.
000000     05 ACM-GRUP-ACTU-CAN-OPER PIC S9(11)       COMP-3 VALUE +0.
000000     05 ACM-GRUP-VARI-VAL-SCAP PIC S9(11)V9(4)  COMP-3 VALUE +0.
000000*
000000     EJECT
000000*
000000* TABLA DE GRUPOS
000000*
000000 01  TABLA-GRUPOS.
000000     05 TB-CNT-GRUPOS                   PIC 9(08) VALUE IS 0.
000000     05 TB-BUS-GRUPOS.
000000        10 TB-BUS-GRUPOS-COD-VCAM       PIC X(06).
000000        10 TB-BUS-GRUPOS-COD-PLZO       PIC X(02).
000000     05 TAB-GRUPOS     OCCURS 0 TO 50 TIMES
000000                       DEPENDING ON TB-CNT-GRUPOS
000000                       INDEXED BY IX-GRUPOS.
000000        10 TB-GRUPOS.
000000           15 TB-GRUPOS-KEY.
000000              20 TB-GRUPOS-KEY-COD-VCAM PIC X(06).
000000              20 TB-GRUPOS-KEY-COD-PLZO PIC X(02).
000000           15 TB-GRUPOS-COD-GRUP        PIC X(01).
000000           15 TB-GRUPOS-GLS-GRUP        PIC X(45).
000000*
000000     EJECT
000000*
000000* TABLA DE ACUMULACION Y CALCULO
000000*
000000 01  TABLA-ACMCAL.
000000     05 TB-CNT-ACMCAL  PIC 9(08) VALUE IS 0.
000000     05 TAB-ACMCAL     OCCURS 0 TO 5000 TIMES
000000                       DEPENDING ON TB-CNT-ACMCAL
000000                       INDEXED BY IX-ACMCAL.
000000        10 TB-ACMCAL.
000000           15 TB-ACMCAL-KEY.
000000              20 TB-ACMCAL-KEY-COD-VCAM PIC X(06).
000000              20 TB-ACMCAL-KEY-COD-GRUP PIC X(01).
000000              20 TB-ACMCAL-KEY-COD-OFIC PIC X(03).
000000           15 TB-ACMCAL-ANTE-VAL-SCAP   PIC S9(13)V9(04) COMP-3.
000000           15 TB-ACMCAL-ANTE-CAN-OPER   PIC S9(11)       COMP-3.
000000           15 TB-ACMCAL-ACTU-VAL-SCAP   PIC S9(13)V9(04) COMP-3.
000000           15 TB-ACMCAL-ACTU-CAN-OPER   PIC S9(11)       COMP-3.
000000           15 TB-ACMCAL-VARI-VAL-SCAP   PIC S9(11)V9(4)  COMP-3.
000000           15 TB-ACMCAL-VARI-PRC-SCAP   PIC S9(05)V9(4)  COMP-3.
000000           15 TB-ACMCAL-VARI-CAN-OPER   PIC S9(11)       COMP-3.
000000           15 TB-ACMCAL-GLS-GRUP        PIC X(45).
000000*
000000     EJECT
000000******************************************************************
000000*                   WORK AREA                                    *
000000******************************************************************
000000*
000000 01  REG-TAB.
000000*    COPYDD TAB-R-TAB.TAB-E-TAB00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO TAB
           02  TAB-E-TAB00.
      *                             REGISTRO GENERICO DE TABLAS
               04  TAB.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  TAB-GLS-FLAG PICTURE X(1).
      *                             CLAVE PRIMARIA REGISTRO
                   06  TAB-KEY-IREG.
      *                             KEY     TIPO + CODIGO ( LLAVE PRIMAR
                       08  TAB-COD-TABL.
      *                             CODIGO TIPO TABLA
                       10  TAB-COD-TTAB
                                    PICTURE X(3).
      *                             CODIGO TABLA
                       10  TAB-COD-CTAB
                                    PICTURE X(12).
      *                             TIME STAMP TRANSACCION
                       08  TAB-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  TAB-FEC-FTRN.
                       12  TAB-NUM-STRN
                                    PICTURE 9(2).
                       12  TAB-NUM-ATRN
                                    PICTURE 9(2).
                       12  TAB-NUM-MTRN
                                    PICTURE 9(2).
                       12  TAB-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  TAB-HRA-HRTR.
                       12  TAB-NUM-HHTR
                                    PICTURE 9(2).
                       12  TAB-NUM-MMTR
                                    PICTURE 9(2).
                       12  TAB-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  TAB-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  TAB-MSC-STAT PICTURE X(1).
      *                             INDICADOR VIGENCIA
                   06  TAB-IND-VIGE
                             REDEFINES TAB-MSC-STAT
                                    PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  TAB-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  TAB-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  TAB-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  TAB-COD-ATRN PICTURE X(12).
      *                             KEY TIPO + CODIGO ( INTERNO COMPUTAC
                   06  TAB-CIC-TABL.
      *                             CODIGO TIPO TABLA
                       08  TAB-CIC-TTAB
                                    PICTURE X(3).
      *                             CODIGO TABLA
                       08  TAB-CIC-CTAB
                                    PICTURE X(12).
      *                             CODIGO EXTERNO
                   06  TAB-EXT-TABL.
      *                             CODIGO TIPO TABLA
                       08  TAB-EXT-TTAB
                                    PICTURE X(3).
      *                             CODIGO TABLA
                       08  TAB-EXT-CTAB
                                    PICTURE X(5).
      *                             DESCRIPCION CORTA
                   06  TAB-GLS-DCOR PICTURE X(12).
      *                             ABREVIACION DESCRIPCION
                   06  TAB-GLS-ABRV PICTURE X(5).
      *                             DESCRIPCION DEL CODIGO
                   06  TAB-GLS-DESC PICTURE X(40).
      *                             DATOS ANEXOS
                   06  TAB-GLS-DATA PICTURE X(60).
      *                             CLAVE SOUNDEX
                   06  TAB-SNX-TABL.
                       08  TAB-SNX-TTAB
                                    PICTURE X(3).
                       08  TAB-SNX-CTAB
                                    PICTURE X(3).
               04  TAB-REDEF REDEFINES TAB
                                    PICTURE X(194).
000000*
000000     EJECT
000000*
000000* LINEAS DE IMPRESION
000000*
000000 01  TIT-01.
000000     05 FILLER                   PIC X(30)  VALUE
000000        'BANCO CREDITO INVERSIONES     '.
000000     05 FILLER                   PIC X(82)  VALUE SPACES.
000000     05 FILLER                   PIC X(10)  VALUE 'DAPPLVSC  '.
000000     05 TIT-01-FEC-FPRO.
000000        10 TIT-01-NUM-DPRO       PIC 9(02).
000000        10 FILLER                PIC X(01) VALUE SPACES.
000000        10 TIT-01-NUM-MPRO       PIC 9(02).
000000        10 FILLER                PIC X(01) VALUE SPACES.
000000        10 TIT-01-NUM-SPRO       PIC 9(02).
000000        10 TIT-01-NUM-APRO       PIC 9(02).
000000*
000000 01  TIT-02.
000000     05 TIT-02-RPT-DSIS          PIC X(30)  VALUE
000000        'DEPOSITOS A PLAZO'.
000000     05 FILLER                   PIC X(17)  VALUE SPACES.
000000     05 FILLER                   PIC X(37)  VALUE
000000        'VARIACION SALDOS CARTERA D.A.P'.
000000     05 FILLER                   PIC X(38)  VALUE SPACES.
000000     05 FILLER                   PIC X(07)  VALUE 'PAGINA '.
000000     05 TIT-02-NUM-PAGE          PIC ZZZ.
000000*
000000 01  TIT-03.
000000     05 FILLER                   PIC X(09)  VALUE 'MONEDA : '.
000000     05 TIT-03-GLS-VCAM          PIC X(20).
000000     05 FILLER                   PIC X(32)  VALUE SPACES.
000000     05 FILLER                   PIC X(18)  VALUE
000000        'CLASE OPERACION : '.
000000     05 TIT-03-GLS-GRUP          PIC X(45).
000000*
000000 01  TIT-04.
000000     05 FILLER                   PIC X(132) VALUE ALL '-'.
000000*
000000 01  TIT-05.
000000     05 FILLER                   PIC X(24) VALUE 'OFICINA'.
000000     05 FILLER                   PIC X(21) VALUE
000000        '       SALDO ANTERIOR'.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 FILLER                   PIC X(08) VALUE 'NRO. DE '.
000000     05 FILLER                   PIC X(01) VALUE SPACES.
000000     05 FILLER                   PIC X(21) VALUE
000000        '       SALDO   ACTUAL'.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 FILLER                   PIC X(08) VALUE 'NRO. DE '.
000000     05 FILLER                   PIC X(03) VALUE SPACES.
000000     05 FILLER                   PIC X(42) VALUE
000000        '<-------------  VARIACION  -------------->'.
000000*
000000 01  TIT-06.
000000     05 FILLER                   PIC X(24) VALUE SPACES.
000000     05 FILLER                   PIC X(11) VALUE
000000        '       AL  '.
000000     05 TIT-06-FEC-FFAN.
000000        10 TIT-06-NUM-DFAN       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 TIT-06-NUM-MFAN       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 TIT-06-NUM-SFAN       PIC X(02).
000000        10 TIT-06-NUM-AFAN       PIC X(02).
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 FILLER                   PIC X(08) VALUE 'DEPOSITO'.
000000     05 FILLER                   PIC X(01) VALUE SPACES.
000000     05 FILLER                   PIC X(11) VALUE
000000        '       AL  '.
000000     05 TIT-06-FEC-FFAC.
000000        10 TIT-06-NUM-DFAC       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 TIT-06-NUM-MFAC       PIC X(02).
000000        10 FILLER                PIC X(01) VALUE '-'.
000000        10 TIT-06-NUM-SFAC       PIC X(02).
000000        10 TIT-06-NUM-AFAC       PIC X(02).
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 FILLER                   PIC X(08) VALUE 'DEPOSITO'.
000000     05 FILLER                   PIC X(03) VALUE SPACES.
000000     05 FILLER                   PIC X(20) VALUE
000000        '         MONTOS     '.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 FILLER                   PIC X(08) VALUE '    %   '.
000000     05 FILLER                   PIC X(01) VALUE SPACES.
000000     05 FILLER                   PIC X(09) VALUE '   DEPOS.'.
000000*
000000 01  DET-01.
000000     05 DET-01-GLS-OFIC          PIC X(24).
000000     05 DET-01-ANTE-VAL-SCAP     PIC ZZZZ.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 DET-01-ANTE-CAN-OPER     PIC ZZZZ.ZZ9.
000000     05 FILLER                   PIC X(01) VALUE SPACES.
000000     05 DET-01-ACTU-VAL-SCAP     PIC ZZZZ.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 DET-01-ACTU-CAN-OPER     PIC ZZZZ.ZZ9.
000000     05 FILLER                   PIC X(03) VALUE SPACES.
000000     05 DET-01-VARI-VAL-SCAP     PIC ---.---.---.--9,9999.
000000     05 FILLER                   PIC X(04) VALUE SPACES.
000000     05 DET-01-VARI-PRC-SCAP     PIC ----9,99.
000000     05 FILLER                   PIC X(01) VALUE SPACES.
000000     05 DET-01-VARI-CAN-OPER     PIC -----.--9.
000000*
000000 01  TOT-01.
000000     05 FILLER                   PIC X(24) VALUE SPACES.
000000     05 FILLER                   PIC X(21) VALUE ALL '-'.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 FILLER                   PIC X(08) VALUE ALL '-'.
000000     05 FILLER                   PIC X(01) VALUE SPACES.
000000     05 FILLER                   PIC X(21) VALUE ALL '-'.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 FILLER                   PIC X(08) VALUE ALL '-'.
000000     05 FILLER                   PIC X(03) VALUE SPACES.
000000     05 FILLER                   PIC X(20) VALUE ALL '-'.
000000*
000000 01  TOT-02.
000000     05 FILLER                   PIC X(24) VALUE
000000        'TOTAL OPERACION'.
000000     05 TOT-02-ANTE-VAL-SCAP     PIC ZZZZ.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 TOT-02-ANTE-CAN-OPER     PIC ZZZZ.ZZ9.
000000     05 FILLER                   PIC X(01) VALUE SPACES.
000000     05 TOT-02-ACTU-VAL-SCAP     PIC ZZZZ.ZZZ.ZZZ.ZZ9,9999.
000000     05 FILLER                   PIC X(02) VALUE SPACES.
000000     05 TOT-02-ACTU-CAN-OPER     PIC ZZZZ.ZZ9.
000000     05 FILLER                   PIC X(03) VALUE SPACES.
000000     05 TOT-02-VARI-VAL-SCAP     PIC ---.---.---.--9,9999.
000000*
000000     EJECT
000000* AREA DE COMUNICACION CON RUTINA RUTUVALF
000000 01  RUTWVALF.
000000     05 RUTWVALF-FECHA.
000000        10 RUTWVALF-FECHA-D      PIC 9(02).
000000        10 RUTWVALF-FECHA-M      PIC 9(02).
000000        10 RUTWVALF-FECHA-A.
000000           15 RUTWVALF-FECHA-AM  PIC 9(02).
000000           15 RUTWVALF-FECHA-AA  PIC 9(02).
000000     05 RUTWVALF-CODRET          PIC 9(01)  VALUE 0.
000000     EJECT
000000     COPY RTNWCALE.
000000     EJECT
000000     COPY RUTWTOFI.
000000     EJECT
000000     COPY RUTWTTAB.
000000     EJECT
           COPY RLOVR001.
000000*
000000* MODULO DE CONTROL
000000*
000000 PROCEDURE DIVISION.
000000*
000000 A-CONTROL SECTION.
000000     ENTRY 'DBMSCBL'.
000000     COPY BIBBAUD2.
000000*
000000 AA-INICIALIZACION.
000000     PERFORM B-INICIALIZAR.
000000*
000000 AB-PROCESAMIENTO.
000000     PERFORM C-PROCESAR.
000000*
000000 AC-TERMINACION.
000000     PERFORM D-TERMINAR.
000000*
000000 AZZ-FIN-PROC.
000000     GOBACK.
000000     EJECT
000000*
000000*
000000*
000000 B-INICIALIZAR SECTION.
000000*
000000 BA-CONTROL-PROCESO.
000000     PERFORM BB-ACCEPT-TARJETA
000000     PERFORM BC-PROC-CALENDARIO
000000     PERFORM BD-PROC-FECHA
000000     PERFORM BE-PROC-DAPTGRU
000000     PERFORM BM-ABRE-ARCH
000000     PERFORM BZ-VERIFICA
000000     GO TO BZZ-FIN-SECC.
000000*
000000 BB-ACCEPT-TARJETA.
000000     DISPLAY ' '.
000000     DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
000000     DISPLAY ' '.
000000     DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA)   : '.
000000     ACCEPT WP-FEC-FPRI
000000     DISPLAY WP-FEC-FPRI
000000*
000000     DISPLAY ' '.
000000     DISPLAY 'INGRESE FECHA ANTERIOR   (DDMMSSAA)/* : '.
000000     ACCEPT WP-FEC-FFAN
000000     DISPLAY WP-FEC-FFAN
000000*
000000     DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
000000     DISPLAY ' '.
000000     DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
000000*
000000* VALIDA FECHA DE PROCESO
000000     DISPLAY WP-FEC-FPRI
000000     MOVE WP-NUM-DPRI TO RUTWVALF-FECHA-D
000000     MOVE WP-NUM-MPRI TO RUTWVALF-FECHA-M
000000     MOVE WP-NUM-SPRI TO RUTWVALF-FECHA-AM
000000     MOVE WP-NUM-APRI TO RUTWVALF-FECHA-AA
000000     CALL 'RUTUVALF' USING RUTWVALF-FECHA RUTWVALF-CODRET
000000     IF RUTWVALF-CODRET NOT = 0
000000           DISPLAY ' '
000000        DISPLAY
000000           '* FECHA PROCESO INVALIDA. ' WP-FEC-FPRI
000000        MOVE 1 TO SW-CANCEL.
000000*
000000* VALIDA FECHA ANTERIOR
000000     IF WP-FEC-FFAN > SPACES AND
000000        WP-FEC-FFAN NOT = '*'
000000        DISPLAY WP-FEC-FFAN
000000        MOVE WP-NUM-DFAN TO RUTWVALF-FECHA-D
000000        MOVE WP-NUM-MFAN TO RUTWVALF-FECHA-M
000000        MOVE WP-NUM-SFAN TO RUTWVALF-FECHA-AM
000000        MOVE WP-NUM-AFAN TO RUTWVALF-FECHA-AA
000000        CALL 'RUTUVALF' USING RUTWVALF-FECHA RUTWVALF-CODRET
000000        IF RUTWVALF-CODRET NOT = 0
000000           DISPLAY ' '
000000           DISPLAY
000000              '* FECHA ANTERIOR INVALIDA. ' WP-FEC-FFAN
000000           MOVE 1 TO SW-CANCEL.
000000*
000000 BC-PROC-CALENDARIO.
000000     MOVE 'CARGAR' TO CALEAREA-FUNCION
000000     MOVE ALL '0'  TO CALEAREA-FECHA-1
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-FUNCION = 'CANCEL'
000000           DISPLAY ' '
000000        DISPLAY
000000           'ERROR EN CARGA CALENDARIO FS= ' CALEAREA-COD-RET
000000        MOVE 1 TO SW-CANCEL.
000000*
000000 BD-PROC-FECHA.
000000     IF WP-FEC-FFAN = '*'
000000        PERFORM BD1-CALCULA-FECHA-ANTERIOR.
000000     PERFORM BD2-VERIFICA-FECHA-ANTERIOR
000000     PERFORM BD3-CARGA-FECHAS.
000000     DISPLAY 'FIN VALIDACION PARAMETROS'.
000000*
000000 BD1-CALCULA-FECHA-ANTERIOR.
000000*CALCULA FECHA DIA HABIL ANTERIOR A FECHA PROCESO
000000     MOVE 'BDHA'                 TO CALEAREA-FUNCION
000000     MOVE WP-NUM-DPRI            TO CALEAREA-FECHA-1-D
000000     MOVE WP-NUM-MPRI            TO CALEAREA-FECHA-1-M
000000     MOVE WP-NUM-SPRI            TO CALEAREA-FECHA-1-AM
000000     MOVE WP-NUM-APRI            TO CALEAREA-FECHA-1-AA
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-COD-RET > 0
000000        DISPLAY ' '
000000        DISPLAY
000000           '* ERROR EN FECHA DE PROCESO.   ' RTNWCALE
000000        MOVE 1                   TO SW-CANCEL
000000     ELSE
000000        MOVE CALEAREA-FECHA-2-D  TO WP-NUM-DFAN
000000        MOVE CALEAREA-FECHA-2-M  TO WP-NUM-MFAN
000000        MOVE CALEAREA-FECHA-2-AM TO WP-NUM-SFAN
000000        MOVE CALEAREA-FECHA-2-AA TO WP-NUM-AFAN
000000        DISPLAY ' '
000000        DISPLAY 'FECHA ANTERIOR CALCULADA   : '
000000           CALEAREA-FECHA-2.
000000*
000000 BD2-VERIFICA-FECHA-ANTERIOR.
000000*VERIFICA QUE LA FECHA ANTERIOR INGRESADA POR PARAMETROS SEA HABIL
000000     MOVE 'INFO'                 TO CALEAREA-FUNCION
000000     MOVE WP-NUM-DFAN            TO CALEAREA-FECHA-1-D
000000     MOVE WP-NUM-MFAN            TO CALEAREA-FECHA-1-M
000000     MOVE WP-NUM-SFAN            TO CALEAREA-FECHA-1-AM
000000     MOVE WP-NUM-AFAN            TO CALEAREA-FECHA-1-AA
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-COD-RET > 0
000000        DISPLAY ' '
000000        DISPLAY
000000           '* ERROR EN FECHA ANTERIOR.   ' RTNWCALE
000000        MOVE 1                   TO SW-CANCEL
000000     ELSE
000000        IF CALEAREA-TIP-DIA = 'F'
000000           DISPLAY ' '
000000           DISPLAY '* FECHA ANTERIOR NO ES HABIL. '
000000           MOVE 1 TO SW-CANCEL.
000000*VERIFICA QUE FECHA ANTERIOR SEA MENOR QUE FECHA DE PROCESO
000000     MOVE 'CDCEF'                TO CALEAREA-FUNCION
000000     MOVE WP-NUM-DPRI            TO CALEAREA-FECHA-1-D
000000     MOVE WP-NUM-MPRI            TO CALEAREA-FECHA-1-M
000000     MOVE WP-NUM-SPRI            TO CALEAREA-FECHA-1-AM
000000     MOVE WP-NUM-APRI            TO CALEAREA-FECHA-1-AA
000000     MOVE WP-NUM-DFAN            TO CALEAREA-FECHA-2-D
000000     MOVE WP-NUM-MFAN            TO CALEAREA-FECHA-2-M
000000     MOVE WP-NUM-SFAN            TO CALEAREA-FECHA-2-AM
000000     MOVE WP-NUM-AFAN            TO CALEAREA-FECHA-2-AA
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-COD-RET > 0
000000        DISPLAY ' '
000000        DISPLAY
000000           '* ERROR EN FECHAS            ' RTNWCALE
000000        MOVE 1                   TO SW-CANCEL
000000     ELSE
000000        IF CALEAREA-NUM-DIA NOT > 0
000000           DISPLAY ' '
000000           DISPLAY '* FECHA ANTERIOR NO MENOR QUE FECHA PROCESO.'
000000           MOVE 1 TO SW-CANCEL.
000000*
000000 BD3-CARGA-FECHAS.
000000* ACEPTAR FECHA DEL COMPUTADOR
000000     ACCEPT WE-FECHA-DATE FROM DATE
000000     MOVE WE-DD-DATE             TO TIT-01-NUM-DPRO
000000     MOVE WE-MM-DATE             TO TIT-01-NUM-MPRO
000000     MOVE WE-AA-DATE             TO TIT-01-NUM-APRO
000000*****IF WE-AA-DATE NOT < 80
000000     IF WE-AA-DATE < ROLL-YY
000000        MOVE 20                  TO TIT-01-NUM-SPRO
000000     ELSE
000000        MOVE 19                  TO TIT-01-NUM-SPRO.
000000* PONE FECHA DE PROCESO (PARAMETRO) COMO ACTUAL
000000     MOVE WP-NUM-DPRI            TO WS-NUM-DFAC
000000                                    TIT-06-NUM-DFAC
000000     MOVE WP-NUM-MPRI            TO WS-NUM-MFAC
000000                                    TIT-06-NUM-MFAC
000000     MOVE WP-NUM-SPRI            TO WS-NUM-SFAC
000000                                    TIT-06-NUM-SFAC
000000     MOVE WP-NUM-APRI            TO WS-NUM-AFAC
000000                                    TIT-06-NUM-AFAC.
000000* PONE FECHA ANTERIOR (PARAMETRO O CALCULADA) COMO ANTERIOR
000000     MOVE WP-NUM-DFAN            TO WS-NUM-DFAN
000000                                    TIT-06-NUM-DFAN
000000     MOVE WP-NUM-MFAN            TO WS-NUM-MFAN
000000                                    TIT-06-NUM-MFAN
000000     MOVE WP-NUM-SFAN            TO WS-NUM-SFAN
000000                                    TIT-06-NUM-SFAN
000000     MOVE WP-NUM-AFAN            TO WS-NUM-AFAN
000000                                    TIT-06-NUM-AFAN.
000000*
000000 BE-PROC-DAPTGRU.
000000     OPEN INPUT DAPTGRU
000000     PERFORM BE1-LEER-DAPTGRU
000000     PERFORM BE2-CARGA-DAPTGRU UNTIL
000000        FS-DAPTGRU = 1
000000     CLOSE DAPTGRU.
000000*
000000 BE1-LEER-DAPTGRU.
000000     READ DAPTGRU AT END MOVE 1 TO FS-DAPTGRU.
000000     IF FS-DAPTGRU = 0
000000        ADD 1 TO CNT-DAPTGRU-L.
000000*
000000 BE2-CARGA-DAPTGRU.
000000     IF TB-CNT-GRUPOS < 50
000000        ADD 1                 TO TB-CNT-GRUPOS
000000        SET IX-GRUPOS         TO TB-CNT-GRUPOS
000000        MOVE DAPTGRU-COD-VCAM TO
000000           TB-GRUPOS-KEY-COD-VCAM (IX-GRUPOS)
000000        MOVE DAPTGRU-COD-PLZO TO
000000           TB-GRUPOS-KEY-COD-PLZO (IX-GRUPOS)
000000        MOVE DAPTGRU-COD-GRUP TO
000000           TB-GRUPOS-COD-GRUP     (IX-GRUPOS)
000000        MOVE DAPTGRU-GLS-GRUP TO
000000           TB-GRUPOS-GLS-GRUP     (IX-GRUPOS)
000000     ELSE
000000        DISPLAY '*** TABLA DE GRUPOS EXCEDIDA DE 50.'
000000        MOVE 1                TO SW-CANCEL.
000000     PERFORM BE1-LEER-DAPTGRU.
000000*
000000 BM-ABRE-ARCH.
000000     OPEN INPUT  DAPDVSO
000000          OUTPUT DAPLVSC.
000000     IF FS-DAPDVSO > 0
000000        DISPLAY '*** ERROR OPEN DAPDVSO. FS=' FS-DAPDVSO
000000        MOVE 1 TO SW-CANCEL.
000000*
000000 BZ-VERIFICA.
000000     IF SW-CANCEL = 1
000000        DISPLAY '*** ERRORES EN INICIALIZACION ***'
000000        PERFORM D-TERMINAR.
000000*
000000 BZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 C-PROCESAR SECTION.
000000*
000000 CA-CONTROL.
000000     SORT FSORT
000000        ASCENDING KEY
000000           FSORT-COD-VCAM
000000           FSORT-COD-GRUP
000000           FSORT-COD-OFIC
000000           FSORT-COD-TIPO
000000        INPUT  PROCEDURE  E-ENTRADA-SORT
000000        OUTPUT PROCEDURE  F-SALIDA-SORT.
000000*
000000     IF SORT-RETURN NOT = 0
000000        DISPLAY '**  ####################### **'
000000        DISPLAY '**  HUBO ERROR EN SORT      **'
000000        DISPLAY '**  ####################### **'
000000        MOVE 1 TO SW-CANCEL
000000        PERFORM D-TERMINAR.
000000*
000000     IF TB-CNT-ACMCAL = 0
000000        DISPLAY ' '
000000        DISPLAY '*** NO HAY INFORMACION DE SALDOS DE LAS FECHAS '
000000                'SOLICITADAS POR PARAMETROS.'
000000     ELSE
000000        PERFORM G-IMPRIME.
000000*
000000 CZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 D-TERMINAR  SECTION.
000000*
000000 DA-TOTALES.
000000     CLOSE DAPDVSO DAPLVSC
000000     DISPLAY ' '
000000     DISPLAY '+---------------------------------------------+'
000000     DISPLAY '+ ESTADISTICAS DE PROCESO.  PROGRAMA DAPPLVSC +'
000000     DISPLAY '+---------------------------------------------+'
000000     DISPLAY ' '
000000     DISPLAY 'REG DAPTGRU LEIDOS (GRUPOS).........' CNT-DAPTGRU-L
000000     DISPLAY 'REG DAPTGRU CARGAD (GRUPOS).........' TB-CNT-GRUPOS
000000     DISPLAY 'REG DAPDVSO LEIDOS..................' CNT-DAPDVSO-L
000000     DISPLAY 'REG DAPDVSO SELECC..................' CNT-DAPDVSO-S
000000     DISPLAY 'REG DAPDVSO IGNORA..................' CNT-DAPDVSO-I
000000     DISPLAY 'REG FSORT   GRABAD..................' CNT-FSORT-G
000000     DISPLAY 'REG FSORT   LEIDOS..................' CNT-FSORT-L
000000     DISPLAY ' '
000000     DISPLAY '***********************************************'
000000     DISPLAY ' '
000000     IF SW-CANCEL    = 1
000000        DISPLAY ' HAY MENSAJES PARA EL EXPLOTADOR'
000000        DISPLAY ' '
000000        DISPLAY '************************************************'
000000        DISPLAY '************************************************'
000000        DISPLAY '****                                        ****'
000000        DISPLAY '****      TERMINO ERRONEO DEL PROCESO       ****'
000000        DISPLAY '****                                        ****'
000000        DISPLAY '************************************************'
000000        DISPLAY '************************************************'
000000        MOVE 100 TO RETURN-CODE
000000        GOBACK
000000     ELSE
000000        DISPLAY ' NO HAY MENSAJES PARA EL EXPLOTADOR'
000000        DISPLAY ' '
000000        DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL ***'.
000000*
000000 DZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 E-ENTRADA-SORT SECTION.
000000*
000000 EA-CONTROL.
000000* SELECCIONA LOS REGISTROS DAPDVSO CON FECHA ANTERIOR
000000     MOVE LOW-VALUE   TO DAPDVSO-KEY-IVSO
000000     MOVE WS-FEC-FFAN TO DAPDVSO-FEC-FPRO
000000     MOVE 'A'         TO WR-COD-TIPO
000000     START DAPDVSO KEY = DAPDVSO-FEC-FPRO
000000     IF FS-DAPDVSO = 0
000000        PERFORM EB-LEER-DAPDVSO
000000        PERFORM ED-SELECCIONA UNTIL
000000           DAPDVSO-FEC-FPRO NOT = WS-FEC-FFAN OR
000000              FS-DAPDVSO = 10
000000     ELSE
000000        DISPLAY ' '
000000        DISPLAY '*** NO HAY INFORMACION REGISTRADA CON FECHA '
000000           WS-NUM-DFAN
000000           WS-NUM-MFAN
000000           WS-NUM-SFAN
000000           WS-NUM-AFAN
000000        DISPLAY '    SE ASUME CERO PARA LOS VALORES ASOCIADOS'
000000                ' A ESTA.'.
000000* SELECCIONA LOS REGISTROS DAPDVSO CON FECHA ACTUAL
000000     MOVE LOW-VALUE   TO DAPDVSO-KEY-IVSO
000000     MOVE WS-FEC-FFAC TO DAPDVSO-FEC-FPRO
000000     MOVE 'B'         TO WR-COD-TIPO
000000     START DAPDVSO KEY = DAPDVSO-FEC-FPRO
000000     IF FS-DAPDVSO = 0
000000        PERFORM EB-LEER-DAPDVSO
000000        PERFORM ED-SELECCIONA UNTIL
000000           DAPDVSO-FEC-FPRO NOT = WS-FEC-FFAC OR
000000              FS-DAPDVSO = 10
000000     ELSE
000000        DISPLAY ' '
000000        DISPLAY '*** NO HAY INFORMACION REGISTRADA CON FECHA '
000000           WS-NUM-DFAC
000000           WS-NUM-MFAC
000000           WS-NUM-SFAC
000000           WS-NUM-AFAC
000000        DISPLAY '    SE ASUME CERO PARA LOS VALORES ASOCIADOS'
000000                ' A ESTA.'.
000000     GO TO EZZ-FIN-SECC.
000000*
000000 EB-LEER-DAPDVSO.
000000     READ DAPDVSO NEXT RECORD
000000     IF FS-DAPDVSO = 0
000000        ADD 1 TO CNT-DAPDVSO-L.
000000*
000000 ED-SELECCIONA.
000000* SELECCION Y ASIGNACION DE GRUPOS
000000     MOVE SPACES                 TO WR-COD-GRUP
000000     MOVE DAPDVSO-COD-VCAM       TO TB-BUS-GRUPOS-COD-VCAM
000000     MOVE DAPDVSO-COD-PLZO       TO TB-BUS-GRUPOS-COD-PLZO
000000     SET IX-GRUPOS               TO 1
000000     SEARCH TAB-GRUPOS
000000        WHEN
000000           TB-GRUPOS-KEY (IX-GRUPOS) = TB-BUS-GRUPOS
000000              MOVE TB-GRUPOS-COD-GRUP (IX-GRUPOS) TO
000000                 WR-COD-GRUP
000000              MOVE TB-GRUPOS-GLS-GRUP (IX-GRUPOS) TO
000000                 WR-GLS-GRUP.
000000*
000000     IF WR-COD-GRUP = SPACES
000000        ADD 1 TO CNT-DAPDVSO-I
000000     ELSE
000000        ADD 1        TO CNT-DAPDVSO-S
000000        PERFORM EK-GRABA-FSORT.
000000     PERFORM EB-LEER-DAPDVSO.
000000*
000000 EK-GRABA-FSORT.
000000     MOVE DAPDVSO-COD-VCAM TO FSORT-COD-VCAM
000000     MOVE WR-COD-GRUP      TO FSORT-COD-GRUP
000000     MOVE DAPDVSO-COD-OFIC TO FSORT-COD-OFIC
000000     MOVE WR-COD-TIPO      TO FSORT-COD-TIPO
000000     MOVE DAPDVSO-VAL-SCAP TO FSORT-VAL-SCAP
000000     MOVE DAPDVSO-CAN-OPER TO FSORT-CAN-OPER
000000     MOVE WR-GLS-GRUP      TO FSORT-GLS-GRUP
000000     RELEASE REG-FSORT
000000     ADD 1                 TO CNT-FSORT-G.
000000*
000000 EZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 F-SALIDA-SORT SECTION.
000000*
000000 FA-LEER-FSORT.
000000     RETURN FSORT AT END MOVE 1 TO FS-FSORT.
000000     IF FS-FSORT = 0
000000        ADD 1 TO CNT-FSORT-L.
000000*
000000 FB-CTRL-PROC.
000000     PERFORM FC-PROCESA UNTIL
000000        FS-FSORT = 1
000000     GO TO FZZ-FIN-SECC.
000000*
000000 FC-PROCESA.
000000     PERFORM FD-BUSCA-ACMCAL
000000     PERFORM FA-LEER-FSORT.
000000*
000000 FD-BUSCA-ACMCAL.
000000     SET IX-ACMCAL TO 1
000000     SEARCH TAB-ACMCAL
000000        AT END
000000           PERFORM FE-CREA-ACMCAL
000000        WHEN
000000           TB-ACMCAL-KEY (IX-ACMCAL) = FSORT-KEY
000000              PERFORM FF-ACTU-ACMCAL.
000000*
000000 FE-CREA-ACMCAL.
000000     IF TB-CNT-ACMCAL < 5000
000000        ADD 1            TO TB-CNT-ACMCAL
000000        SET IX-ACMCAL    TO TB-CNT-ACMCAL
000000        MOVE FSORT-KEY   TO TB-ACMCAL-KEY           (IX-ACMCAL)
000000        MOVE +0          TO TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL)
000000                            TB-ACMCAL-ANTE-CAN-OPER (IX-ACMCAL)
000000                            TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL)
000000                            TB-ACMCAL-ACTU-CAN-OPER (IX-ACMCAL)
000000                            TB-ACMCAL-VARI-VAL-SCAP (IX-ACMCAL)
000000                            TB-ACMCAL-VARI-PRC-SCAP (IX-ACMCAL)
000000                            TB-ACMCAL-VARI-CAN-OPER (IX-ACMCAL)
000000        MOVE FSORT-GLS-GRUP TO
000000                            TB-ACMCAL-GLS-GRUP      (IX-ACMCAL)
000000        PERFORM FF-ACTU-ACMCAL
000000     ELSE
000000        DISPLAY '*** TABLA ACMCAL EXCEDIDA DE 5000.'
000000        MOVE 16 TO SORT-RETURN.
000000*
000000 FF-ACTU-ACMCAL.
000000     IF FSORT-COD-TIPO = 'A'
000000* TIPO 'A' CORRESPONDEN A FECHA ENTERIOR
000000        ADD FSORT-VAL-SCAP       TO
000000           TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL)
000000        ADD FSORT-CAN-OPER       TO
000000           TB-ACMCAL-ANTE-CAN-OPER (IX-ACMCAL)
000000     ELSE
000000        IF FSORT-COD-TIPO = 'B'
000000* TIPO 'B' CORRESPONDEN A FECHA ACTUAL
000000           ADD FSORT-VAL-SCAP    TO
000000              TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL)
000000           ADD FSORT-CAN-OPER    TO
000000              TB-ACMCAL-ACTU-CAN-OPER (IX-ACMCAL).
000000*
000000 FZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 G-IMPRIME SECTION.
000000*
000000 GA-CONTROL.
000000* RECORRE SECUENCIALMENTE LA TABLA DE ACUMULACION, CALCULANDO
000000* LAS VARIACIONES CORRESPONDIENTES E IMPRIMIENDO EL INFORME.
000000* CORTE DE CONTROL POR MONEDA Y GRUPO
000000     SET IX-ACMCAL       TO 1
000000     PERFORM GC-CTRL-X-VCAM UNTIL
000000        IX-ACMCAL > TB-CNT-ACMCAL.
000000     GO TO GZZ-FIN-SECC.
000000*
000000 GC-CTRL-X-VCAM.
000000*  MONEDA
000000     MOVE 99                                 TO CNT-LINE-DAPLVSC
000000     MOVE 0                                  TO CNT-PAGE-DAPLVSC
000000     MOVE TB-ACMCAL-KEY-COD-VCAM (IX-ACMCAL) TO WR-COD-VCAM
000000     PERFORM GC1-BUSCA-GLOSA-VCAM
000000     MOVE WR-GLS-VCAM                        TO TIT-03-GLS-VCAM
000000     PERFORM GD-CTRL-X-GRUP UNTIL
000000        TB-ACMCAL-KEY-COD-VCAM (IX-ACMCAL) NOT = WR-COD-VCAM OR
000000           IX-ACMCAL > TB-CNT-ACMCAL.
000000*
000000 GC1-BUSCA-GLOSA-VCAM.
000000*  BUSCA GLOSA MONEDA
000000     MOVE +150                TO RUTWTTAB-BASE
000000     MOVE 'VLR'               TO RUTWTTAB-TABLA
000000     MOVE WR-COD-VCAM         TO RUTWTTAB-CLAVE
000000     CALL 'RUTUTTAB' USING RUTWTTAB
000000     EVALUATE RUTWTTAB-RETURN-CODE
000000        WHEN SPACES
000000           MOVE RUTWTTAB-REGISTRO-TAB TO REG-TAB
000000           MOVE TAB-GLS-DESC          TO WR-GLS-VCAM
000000        WHEN '14'
000000           MOVE ALL '?'               TO WR-GLS-VCAM
000000        WHEN OTHER
000000           DISPLAY 'ERROR EN ACCESO TABLA TAB (RUTUTTAB). '
000000                   'COD-RET = ' RUTWTTAB-RETURN-CODE
000000           DISPLAY 'BASE  = ' RUTWTTAB-BASE ' '
000000                   'TABLA = ' RUTWTTAB-TABLA ' '
000000                   'CLAVE = ' RUTWTTAB-CLAVE
000000           MOVE 20 TO SORT-RETURN
000000     END-EVALUATE.
000000*
000000 GD-CTRL-X-GRUP.
000000*  GRUPO
000000     MOVE 99                                  TO CNT-LINE-DAPLVSC
000000     MOVE 0                                   TO CNT-PAGE-DAPLVSC
000000     INITIALIZE ACM-GRUP
000000     MOVE TB-ACMCAL-KEY-COD-GRUP (IX-ACMCAL)  TO WR-COD-GRUP
000000     MOVE TB-ACMCAL-GLS-GRUP     (IX-ACMCAL)  TO TIT-03-GLS-GRUP
000000     PERFORM GE-IMPRIME UNTIL
000000        TB-ACMCAL-KEY-COD-VCAM (IX-ACMCAL) NOT = WR-COD-VCAM OR
000000           TB-ACMCAL-KEY-COD-GRUP (IX-ACMCAL) NOT = WR-COD-GRUP OR
000000              IX-ACMCAL > TB-CNT-ACMCAL
000000     PERFORM GD9-TOTALES-GRUP.
000000*
000000 GD9-TOTALES-GRUP.
000000* TOTALIZAR POR GRUPO
000000     IF SALTO-PAG-DAPLVSC
000000        PERFORM GV-TITULOS.
000000     MOVE ACM-GRUP-ANTE-VAL-SCAP      TO TOT-02-ANTE-VAL-SCAP
000000     MOVE ACM-GRUP-ANTE-CAN-OPER      TO TOT-02-ANTE-CAN-OPER
000000     MOVE ACM-GRUP-ACTU-VAL-SCAP      TO TOT-02-ACTU-VAL-SCAP
000000     MOVE ACM-GRUP-ACTU-CAN-OPER      TO TOT-02-ACTU-CAN-OPER
000000     MOVE ACM-GRUP-VARI-VAL-SCAP      TO TOT-02-VARI-VAL-SCAP
000000     WRITE REG-DAPLVSC FROM TOT-01     AFTER 1
000000     WRITE REG-DAPLVSC FROM TOT-02     AFTER 1
000000     ADD 2                            TO CNT-LINE-DAPLVSC.
000000*
000000 GE-IMPRIME.
000000* * GENERA LINEA DETALLE (ACUMULADO DE OFICINA)
000000     MOVE TB-ACMCAL-KEY-COD-OFIC (IX-ACMCAL)  TO WR-COD-OFIC
000000     PERFORM GE1-BUSCA-GLOSA-OFIC
000000     PERFORM GE2-CALCULA-VARIACION
000000     IF SALTO-PAG-DAPLVSC
000000        PERFORM GV-TITULOS.
000000     MOVE WR-GLS-OFIC                         TO
000000        DET-01-GLS-OFIC
000000     MOVE TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL) TO
000000        DET-01-ANTE-VAL-SCAP
000000     MOVE TB-ACMCAL-ANTE-CAN-OPER (IX-ACMCAL) TO
000000        DET-01-ANTE-CAN-OPER
000000     MOVE TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL) TO
000000        DET-01-ACTU-VAL-SCAP
000000     MOVE TB-ACMCAL-ACTU-CAN-OPER (IX-ACMCAL) TO
000000        DET-01-ACTU-CAN-OPER
000000     MOVE TB-ACMCAL-VARI-VAL-SCAP (IX-ACMCAL) TO
000000        DET-01-VARI-VAL-SCAP
000000     MOVE TB-ACMCAL-VARI-PRC-SCAP (IX-ACMCAL) TO
000000        DET-01-VARI-PRC-SCAP
000000     MOVE TB-ACMCAL-VARI-CAN-OPER (IX-ACMCAL) TO
000000        DET-01-VARI-CAN-OPER
000000     WRITE REG-DAPLVSC FROM DET-01     AFTER 1
000000     ADD 1                                    TO CNT-LINE-DAPLVSC.
000000* ACUMULA VALORES POR OFICINA A VALORES POR GRUPO PARA TOTAL
000000     ADD TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL)  TO
000000        ACM-GRUP-ANTE-VAL-SCAP
000000     ADD TB-ACMCAL-ANTE-CAN-OPER (IX-ACMCAL)  TO
000000        ACM-GRUP-ANTE-CAN-OPER
000000     ADD TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL)  TO
000000        ACM-GRUP-ACTU-VAL-SCAP
000000     ADD TB-ACMCAL-ACTU-CAN-OPER (IX-ACMCAL)  TO
000000        ACM-GRUP-ACTU-CAN-OPER
000000     ADD TB-ACMCAL-VARI-VAL-SCAP (IX-ACMCAL)  TO
000000        ACM-GRUP-VARI-VAL-SCAP.
000000     SET IX-ACMCAL UP BY 1.
000000*
000000 GE1-BUSCA-GLOSA-OFIC.
000000     MOVE WR-COD-OFIC     TO RUTWTOFI-COD-OFIC
000000     CALL 'RUTUTOFI' USING RUTWTOFI
000000     EVALUATE RUTWTOFI-RETURN-CODE
000000        WHEN SPACES
000000           MOVE RUTWTOFI-GLS-DESC     TO WR-GLS-OFIC
000000        WHEN '14'
000000           MOVE ALL '?'               TO WR-GLS-OFIC
000000        WHEN OTHER
000000           DISPLAY 'ERROR EN ACCESO TABLA OFI (RUTUTOFI). '
000000                   'COD-RET = ' RUTWTOFI-RETURN-CODE
000000           DISPLAY 'CLAVE   = ' RUTWTOFI-COD-OFIC
000000           MOVE 20 TO SORT-RETURN
000000     END-EVALUATE.
000000*
000000 GE2-CALCULA-VARIACION.
000000* SE CALCULA LA VARIACION EN MONTOS,
000000*               PORCENTAJE DE VARIACION DE MONTOS Y
000000*               VARIACION EN CANTIDAD DE DOCUMENTOS
000000* VARIACION EN MONTOS
000000     SUBTRACT TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL) FROM
000000        TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL) GIVING
000000           TB-ACMCAL-VARI-VAL-SCAP (IX-ACMCAL)
000000* PORCENTAJE DE VARIACION DE MONTOS
000000     IF TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL) = +0 OR
000000        TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL) = +0
000000           PERFORM GE3-CALCULA-VARIACION-ESP
000000     ELSE
000000        COMPUTE TB-ACMCAL-VARI-PRC-SCAP (IX-ACMCAL) ROUNDED =
000000           (((TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL) -
000000              TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL)) /
000000                TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL)) * 100).
000000* VARIACION EN CANTIDAD DE DOCUMENTOS
000000     SUBTRACT TB-ACMCAL-ANTE-CAN-OPER (IX-ACMCAL) FROM
000000        TB-ACMCAL-ACTU-CAN-OPER (IX-ACMCAL) GIVING
000000           TB-ACMCAL-VARI-CAN-OPER (IX-ACMCAL).
000000*
000000 GE3-CALCULA-VARIACION-ESP.
000000* CALCULA VARIACIONES ESPECIALES.
000000     MOVE +0              TO TB-ACMCAL-VARI-PRC-SCAP (IX-ACMCAL)
000000     IF TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL) > +0 AND
000000        TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL) = +0
000000           MOVE -100      TO TB-ACMCAL-VARI-PRC-SCAP (IX-ACMCAL)
000000     ELSE
000000        IF TB-ACMCAL-ANTE-VAL-SCAP (IX-ACMCAL) = +0 AND
000000           TB-ACMCAL-ACTU-VAL-SCAP (IX-ACMCAL) > +0
000000               MOVE +100  TO TB-ACMCAL-VARI-PRC-SCAP (IX-ACMCAL).
000000*
000000 GV-TITULOS.
000000     ADD 1                       TO CNT-PAGE-DAPLVSC
000000     MOVE CNT-PAGE-DAPLVSC       TO TIT-02-NUM-PAGE
000000     WRITE REG-DAPLVSC FROM TIT-01 AFTER CANAL-1
000000     WRITE REG-DAPLVSC FROM TIT-02 AFTER 1
000000     WRITE REG-DAPLVSC FROM TIT-03 AFTER 1
000000     WRITE REG-DAPLVSC FROM TIT-04 AFTER 2
000000     WRITE REG-DAPLVSC FROM TIT-05 AFTER 1
000000     WRITE REG-DAPLVSC FROM TIT-06 AFTER 1
000000     WRITE REG-DAPLVSC FROM TIT-04 AFTER 1
000000     MOVE 08                     TO CNT-LINE-DAPLVSC.
000000*
000000 GZZ-FIN-SECC.
000000     EXIT.
000000     EJECT

