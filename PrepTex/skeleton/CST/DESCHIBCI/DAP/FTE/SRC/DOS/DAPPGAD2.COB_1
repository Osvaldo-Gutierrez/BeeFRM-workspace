       IDENTIFICATION DIVISION.
       PROGRAM-ID. DAPPGAD2.
       AUTHOR.     JORGE FERRADA.
      *REMARKS.    BANCO DE CREDITO E INVERSIONES
      *            SISTEMA DE DEPOSITOS A PLAZO INFORME ADM.DATOS
      *
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ***************************************************************
      *       EL OBJETIVO ES GENERAR UNA COPIA DEL ARCHIVO IPA EN
      *       FORMATO DISPLAY PARA ADMINISTRACION DE DATOS
      *
      *    ARCHIVOS
      *      ENTRADA:
      *                DAPMIPA
      *                TABLA OPD DE DAP
      *
      *      SALIDA :  ADMMIPA  (DISCO SAM)
      *
      ***************************************************************
      **                     MANTENCIONES                          **
      ***************************************************************
      *CECILIA PIZARRO 27.01.93                                     *
      *                SE MODIFICA EL MES A PROCESAR Y SELECC       *
      *                TANTO LOS   VENCIMIENTOS COMO LIQUIDACIONES  *
      *                                                             *
      * 06-04-94 JORGE FERRADA G.                                   *
      *          SE MODIFICA PARA QUE EL MES Y ANO DE PROCESO SE    *
      *          INCORPOREN POR PARAMETRO.                          *
      ***************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-370-4341.
       OBJECT-COMPUTER. IBM-370-4341.
       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *--------------
      *
           SELECT  DAPMIPA ASSIGN      TO DAPMIPA
                   ORGANIZATION        IS INDEXED
                   ACCESS              IS SEQUENTIAL
                   RECORD KEY          IS IPA-KEY-IPAV IN IPA
                   FILE STATUS         IS  FS-IPA.
           SELECT  ADMMIPA ASSIGN      TO ADMMIPA
                   FILE STATUS         IS  FS-IPA2.
           EJECT
      *
      *
       DATA DIVISION.
       FILE SECTION.
      *
       FD   DAPMIPA
            RECORD CONTAINS 395 CHARACTERS
            LABEL RECORD ARE STANDARD.
      *    Interfaz DAP - Certificado de Intereses Pagados
      *    -----------------------------------------------
      *
      *    En esta interfaz se entrega la informacion  necesaria
      *    de las liquidaciones de OPERACIONES efectuadas  en el
      *    sistema DAP durante un rango  de  fechas determinado.
      *    Dicha  informacion  sera utilizada por el sub-sistema
      *    de Certificado de Intereses Pagados, a fin de generar
      *    y enviar a los clientes un reporte de  los  intereses
      *    pagados por el Banco a ellos,  producto  de  sus DAP,
      *    cuentas de ahorro u otros instrumentos  de  inversion
      *    que le hayan generado intereses. Esta informacion se-
      *    ra usada por los clientes para sus respectivas decla-
      *    raciones de impuestos.
      *
      *    Nombre Registro : IPA
      *    Clave(s)        : IPA-KEY-IPAV(UU)
      *    Largo           : 395
      *    Bloqueo         : 2
      *    Observaciones   : VSAM/KSDS
      *
       01  IPA.
      *
      *    LLAVE OPD SERIAL + FVEN
           03  IPA-KEY-IPAV.
      *
      *        Cod. Int. Comp. Serial CDP
               05  IPA-CIC-ISER.
      *
      *            Centro Asignacion Identificacion
                   07  IPA-CAI-ISER                     PIC X(04).
      *
      *            Ident. Interna Computacional
                   07  IPA-IIC-ISER                     PIC X(08).
      *
      *        Fecha Vencimiento
               05  IPA-FEC-FVEN.
                   07  IPA-NUM-SVEN                     PIC 9(02).
                   07  IPA-NUM-AVEN                     PIC 9(02).
                   07  IPA-NUM-MVEN                     PIC 9(02).
                   07  IPA-NUM-DVEN                     PIC 9(02).
      *
      *    Codigo Clase Operacion DAP
           03  IPA-COD-COPD.
      *
      *        Codigo Tipo Operacion
               05  IPA-COD-TOPD                         PIC X(03).
      *
      *        Codigo Auxiliar Operacion
               05  IPA-COD-AOPD                         PIC X(03).
      *
      *    Monto Captacion
           03  IPA-VAL-CAPT             COMP-3          PIC 9(11)V9(04).
      *
      *    Monto Intereses Periodo
           03  IPA-VAL-INTS             COMP-3          PIC 9(11)V9(04).
      *
      *    Monto Captacion Inicial
           03  IPA-VAL-CAIN             COMP-3          PIC 9(11)V9(04).
      *
      *    Monto Liquidado
           03  IPA-VAL-MLIQ             COMP-3          PIC 9(11)V9(04).
      *
      *    Oficina de Liquidacion
           03  IPA-COD-OFLI                             PIC 9(03).
      *
      *    Fecha Liquidacion
           03  IPA-FEC-FLIQ.
               05  IPA-NUM-SLIQ                         PIC 9(02).
               05  IPA-NUM-ALIQ                         PIC 9(02).
               05  IPA-NUM-MLIQ                         PIC 9(02).
               05  IPA-NUM-DLIQ                         PIC 9(02).
      *
      *    Plazo en Dias
           03  IPA-NUM-PLZO                             PIC 9(04).
      *
      *    Codigo Moneda
           03  IPA-NUM-TVAL                             PIC 9(04).
      *
      *    Valor Tasa
           03  IPA-SGV-TINT             COMP-3          PIC S9(03)V9(4).
      *
      *    Fecha Captacion
           03  IPA-FEC-FCAP.
               05  IPA-NUM-SCAP                         PIC 9(02).
               05  IPA-NUM-ACAP                         PIC 9(02).
               05  IPA-NUM-MCAP                         PIC 9(02).
               05  IPA-NUM-DCAP                         PIC 9(02).
      *
      *    Primer Beneficiario
           03  IPA-CLI1.
      *
      *        RUT Cliente
               05  IPA-NUM-CLI1                         PIC X(08).
      *
      *        Digito Verificador del Rut
               05  IPA-VRF-CLI1                         PIC X(01).
      *
      *        Nombre Cliente Generico (Persona o Empresa)
               05  IPA-GLS-NOM1.
                   07  IPA-GLS-APA1                     PIC X(25).
                   07  IPA-GLS-AMA1                     PIC X(25).
                   07  IPA-GLS-NOB1                     PIC X(25).
      *
      *        Descripcion de la Direccion
               05  IPA-GLS-DIR1                         PIC X(60).
      *
      *        Codigo de Ciudad
               05  IPA-COD-CIU1                         PIC 9(04).
      *
      *    Segundo Beneficiario
           03  IPA-CLI2.
      *
      *        RUT Cliente
               05  IPA-NUM-CLI2                         PIC X(08).
      *
      *        Digito Verificador del Rut
               05  IPA-VRF-CLI2                         PIC X(01).
      *
      *        Nombre Cliente Generico (Persona o Empresa)
               05  IPA-GLS-NOM2.
                   07  IPA-GLS-APA2                     PIC X(25).
                   07  IPA-GLS-AMA2                     PIC X(25).
                   07  IPA-GLS-NOB2                     PIC X(25).
      *
      *        Descripcion de la Direccion
               05  IPA-GLS-DIR2                         PIC X(60).
      *
      *        Codigo de Ciudad
               05  IPA-COD-CIU2                         PIC 9(04).
      *
      *    Disponible
           03  IPA-GLS-DISP                             PIC X(10).
      *
       FD   ADMMIPA
            BLOCK CONTAINS 0 RECORDS
            LABEL RECORD ARE STANDARD.
      *
       01  REG-IPA.
      *
      *    LLAVE OPD SERIAL + FVEN
           03  IPA-KEY-IPAV.
      *
      *        Cod. Int. Comp. Serial CDP
               05  IPA-CIC-ISER.
      *
      *            Centro Asignacion Identificacion
                   07  IPA-CAI-ISER                     PIC X(04).
      *
      *            Ident. Interna Computacional
                   07  IPA-IIC-ISER                     PIC X(08).
      *
      *        Fecha Vencimiento
               05  IPA-FEC-FVEN.
                   07  IPA-NUM-SVEN                     PIC 9(02).
                   07  IPA-NUM-AVEN                     PIC 9(02).
                   07  IPA-NUM-MVEN                     PIC 9(02).
                   07  IPA-NUM-DVEN                     PIC 9(02).
      *
      *    Codigo Clase Operacion DAP
           03  IPA-COD-COPD.
      *
      *        Codigo Tipo Operacion
               05  IPA-COD-TOPD                         PIC X(03).
      *
      *        Codigo Auxiliar Operacion
               05  IPA-COD-AOPD                         PIC X(03).
      *
      *    Monto Captacion
           03  IPA-VAL-CAPT                             PIC 9(11)V9(04).
      *
      *    Monto Intereses Periodo
           03  IPA-VAL-INTS                             PIC 9(11)V9(04).
      *
      *    Monto Captacion Inicial
           03  IPA-VAL-CAIN                             PIC 9(11)V9(04).
      *
      *    Monto Liquidado
           03  IPA-VAL-MLIQ                             PIC 9(11)V9(04).
      *
      *    Oficina de Liquidacion
           03  IPA-COD-OFLI                             PIC 9(03).
      *
      *    Fecha Liquidacion
           03  IPA-FEC-FLIQ.
               05  IPA-NUM-SLIQ                         PIC 9(02).
               05  IPA-NUM-ALIQ                         PIC 9(02).
               05  IPA-NUM-MLIQ                         PIC 9(02).
               05  IPA-NUM-DLIQ                         PIC 9(02).
      *
      *    Plazo en Dias
           03  IPA-NUM-PLZO                             PIC 9(04).
      *
      *    Codigo Moneda
           03  IPA-NUM-TVAL                             PIC 9(04).
      *
      *    Valor Tasa
           03  IPA-SGV-TINT                             PIC S9(03)V9(4).
      *
      *    Fecha Captacion
           03  IPA-FEC-FCAP.
               05  IPA-NUM-SCAP                         PIC 9(02).
               05  IPA-NUM-ACAP                         PIC 9(02).
               05  IPA-NUM-MCAP                         PIC 9(02).
               05  IPA-NUM-DCAP                         PIC 9(02).
      *
      *    Primer Beneficiario
           03  IPA-CLI1.
      *
      *        RUT Cliente
               05  IPA-NUM-CLI1                         PIC X(08).
      *
      *        Digito Verificador del Rut
               05  IPA-VRF-CLI1                         PIC X(01).
      *
      *        Nombre Cliente Generico (Persona o Empresa)
               05  IPA-GLS-NOM1.
                   07  IPA-GLS-APA1                     PIC X(25).
                   07  IPA-GLS-AMA1                     PIC X(25).
                   07  IPA-GLS-NOB1                     PIC X(25).
      *
      *        Descripcion de la Direccion
               05  IPA-GLS-DIR1                         PIC X(60).
      *
      *        Codigo de Ciudad
               05  IPA-COD-CIU1                         PIC 9(04).
      *
      *    Segundo Beneficiario
           03  IPA-CLI2.
      *
      *        RUT Cliente
               05  IPA-NUM-CLI2                         PIC X(08).
      *
      *        Digito Verificador del Rut
               05  IPA-VRF-CLI2                         PIC X(01).
      *
      *        Nombre Cliente Generico (Persona o Empresa)
               05  IPA-GLS-NOM2.
                   07  IPA-GLS-APA2                     PIC X(25).
                   07  IPA-GLS-AMA2                     PIC X(25).
                   07  IPA-GLS-NOB2                     PIC X(25).
      *
      *        Descripcion de la Direccion
               05  IPA-GLS-DIR2                         PIC X(60).
      *
      *        Codigo de Ciudad
               05  IPA-COD-CIU2                         PIC 9(04).
      *
      *    CLAVE OPD
           03  IPA-CIC-IOPD                             PIC X(12).
      *
           EJECT
       WORKING-STORAGE SECTION.
      *-----------------------
      *
           COPY BIBBAUD1.
      *
       01  PARAMETROS.
           02 PA-ANO                        PIC  9(2) VALUE 0.
           02 FILLER                        PIC  X.
           02 PA-MES                        PIC  9(2) VALUE 0.
      *
       01  FILE-STATUS.
           02 FS-IPA                        PIC  9(2) VALUE 0.
           02 FS-IPA2                       PIC  9(2) VALUE 0.
      *
       01  TOTALES-CONTROL.
           02  TOTAL-AIPA                   PIC 9(08) VALUE 0.
           02  TOTAL-AIPA2                  PIC 9(08) VALUE 0.
           02  TOTAL-AOPD                   PIC 9(08) VALUE 0.
      *
       01  VARIABLES-CONTROL.
           02 SW-FINIPA                     PIC X(02) VALUE 'NO'.
           02 SW-ERROR                      PIC X(02) VALUE 'NO'.
      *
      *
      ***************************************************************
      *      USER INFORMATION BLOCK
      ***************************************************************
      *
       01  UIB.
           03 FILLER                   PIC X(32) VALUE 'DAPPGAD2'.
      *
      ***************************************************************
      *      REQUEST AREA
      ***************************************************************
      *
       01  REQ-OPD-AREA.
           03 REQ-OPD-CMD                   PIC X(5).
           03 REQ-OPD-TBLNM                 PIC X(3).
           03 REQ-OPD-KEYNM                 PIC X(5).
           03 REQ-OPD-RETURN-CODE           PIC X(2) VALUE SPACES.
           03 REQ-OPD-INT-RTN-CD            PIC X(1).
           03 REQ-OPD-DBID                  PIC S9(4) COMP  VALUE +370.
           03 REQ-OPD-TBLID                 PIC X(7).
           03 FILLER                        PIC X(25).
           03 REQ-OPD-COUNT-MAX             PIC S9(4) COMP.
           03 REQ-OPD-IO-COUNT              PIC S9(4) COMP.
           03 FILLER                        PIC X(22).
           03 REQ-OPD-KEY-VALUE             PIC X(24).
           03 REQ-OPD-KEY-OPD   REDEFINES REQ-OPD-KEY-VALUE.
              05 REQ-KEY-OPD-IOPD           PIC X(12).
              05 FILLER                     PIC X(12).
      *
      *
           EJECT
      *
      ***************************************************************
      *                   WORK AREA
      ***************************************************************
      *
      *COPY DE TABLA OPD
       01  REG-OPD.
      *    COPYDD DAP-R-OPD.DAP-E-OPD00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO OPD
           02  DAP-E-OPD00.
      *                             OPERACION DEPOSITO A PLAZO
               03  OPD.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  OPD-GLS-FLAG PICTURE X(1).
      *                             IDENTIFICACION OPERACION DAP
                   04  OPD-KEY-IOPD.
      *                             COD. INT. COMP. OPERACION DAP
                       05  OPD-CIC-IOPD.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       06  OPD-CAI-IOPD
                                    PICTURE X(4).
      *                             IDENT. INTERNA COMPUTACIONAL
                       06  OPD-IIC-IOPD
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                   04  OPD-STP-ITRN.
      *                             FECHA TRANSACCION
                       05  OPD-FEC-FTRN
                                    PICTURE 9(8).
      *                             HORA TRANSACCION
                       05  OPD-HRA-HRTR
                                    PICTURE 9(6).
      *                             INDICADOR TIPO ACCION
                   04  OPD-COD-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  OPD-COD-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  OPD-COD-OTRN PICTURE X(4).
      *                             AUTOR TRANSACCION
                   04  OPD-COD-ATRN PICTURE X(12).
      *                             VALOR DE CAMBIO OPERACION DAP
                   04  OPD-COD-VCAM.
      *                             MISCELANEO TIPO VALOR
                       05  OPD-MSC-TVAL
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       05  OPD-COD-CVAL
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       05  OPD-COD-TCAM
                                    PICTURE X(2).
      *                             CODIGO CLASE OPERACION DAP
                   04  OPD-COD-COPD.
      *                             CODIGO TIPO OPERACION
                       05  OPD-COD-TOPD
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR OPERACION
                       05  OPD-COD-AOPD
                                    PICTURE X(3).
      *                             CODIGO OFICINA OPERACION DAP
                   04  OPD-COD-OFIC PICTURE X(3).
      *                             RESPONSABLE MANTENCION OPERACION DAP
                   04  OPD-COD-EJEC PICTURE X(12).
      *                             COD. INT. COMP. SERIAL CDP
                   04  OPD-CIC-ISER.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       05  OPD-CAI-ISER
                                    PICTURE X(4).
      *                             IDENT. INTERNA COMPUTACIONAL
                       05  OPD-IIC-ISER
                                    PICTURE X(8).
      *                             FECHA CAPTACION REAL
                   04  OPD-FEC-FREA.
                       05  OPD-NUM-SREA
                                    PICTURE 9(2).
                       05  OPD-NUM-AREA
                                    PICTURE 9(2).
                       05  OPD-NUM-MREA
                                    PICTURE 9(2).
                       05  OPD-NUM-DREA
                                    PICTURE 9(2).
      *                             FECHA EMISION CONTABLE
                   04  OPD-FEC-FCTB.
                       05  OPD-NUM-SCTB
                                    PICTURE 9(2).
                       05  OPD-NUM-ACTB
                                    PICTURE 9(2).
                       05  OPD-NUM-MCTB
                                    PICTURE 9(2).
                       05  OPD-NUM-DCTB
                                    PICTURE 9(2).
      *                             FECHA INGRESO COMPUTACIONAL
                   04  OPD-FEC-FCMP.
                       05  OPD-NUM-SCMP
                                    PICTURE 9(2).
                       05  OPD-NUM-ACMP
                                    PICTURE 9(2).
                       05  OPD-NUM-MCMP
                                    PICTURE 9(2).
                       05  OPD-NUM-DCMP
                                    PICTURE 9(2).
      *                             MONTO CAPTACION
                   04  OPD-VAL-CAPT PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             SALDO VALOR CAPTACION
                   04  OPD-VAL-SCAP PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             MONTO A PAGAR POR CAJA
                   04  OPD-VAL-CAJA PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             ANALISIS FERIADOS (S/N)
                   04  OPD-COD-AFER PICTURE X(1).
      *                             CONDICION GARANTIA
                   04  OPD-COD-GARA PICTURE X(1).
      *                             CODIGO UBICACION FISICA CDP
                   04  OPD-COD-UBFD.
                       05  OPD-COD-IUBF
                                    PICTURE X(3).
                       05  OPD-COD-CUBF
                                    PICTURE X(3).
                       05  OPD-COD-SUBF
                                    PICTURE X(3).
      *                             NUMERO PROXIMO VENCIMIENTO
                   04  OPD-NUM-IVEN PICTURE 9(3).
      *                             FECHA PROXIMO VENCIMIENTO
                   04  OPD-FEC-FPVC.
                       05  OPD-NUM-SPVC
                                    PICTURE 9(2).
                       05  OPD-NUM-APVC
                                    PICTURE 9(2).
                       05  OPD-NUM-MPVC
                                    PICTURE 9(2).
                       05  OPD-NUM-DPVC
                                    PICTURE 9(2).
      *                             CODIGO TIPO TASA INTERES
                   04  OPD-COD-TINT.
      *                             CODIGO TIPO TASA
                       05  OPD-COD-TIPT.
      *                             BASE CALCULO TASA INTERES
                       06  OPD-COD-BCTI
                                    PICTURE X(1).
      *                             PERIODO BASE TASA INTERES
                       06  OPD-COD-PBTI
                                    PICTURE X(1).
      *                             MODALIDAD CALCULO TASA INTERES
                       06  OPD-COD-MCTI.
      *                             BAJO PERIODO BASE TASA
                       07  OPD-COD-BPBT
                                    PICTURE X(1).
      *                             SOBRE PERIODO BASE TASA
                       07  OPD-COD-SPBT
                                    PICTURE X(1).
      *                             CODIGO VALOR TASA INTERES
                       05  OPD-COD-CVTI
                                    PICTURE X(3).
      *                             TIPO FECHA PERIODO REPACTO
                       05  OPD-COD-TFPR
                                    PICTURE X(1).
      *                             NUMERO PERIODO REPACTO
                       05  OPD-NUM-PREP
                                    PICTURE 9(3).
      *                             UNIDAD TIEMPO OPD-NUM-PREP
                       05  OPD-MSC-PREP
                                    PICTURE X(1).
      *                             VALOR SPREAD
                   04  OPD-SGV-SPRD PICTURE S9(5)V9(4)
                                        COMPUTATIONAL.
      *                             VALOR TASA
                   04  OPD-SGV-TINT PICTURE S9(5)V9(4)
                                        COMPUTATIONAL.
      *                             RETENCION DOCUMENTO
                   04  OPD-NUM-RETD PICTURE 9(3).
      *                             UNIDAD TIEMPO OPD-NUM-RETD
                   04  OPD-IND-RETD PICTURE X(1).
      *                             IND DEVOLUCION DINERO DOC S/FONDOS
                   04  OPD-COD-DEVL PICTURE X(1).
      *                             INDICADOR CUSTODIA
                   04  OPD-COD-CUST PICTURE X(1).
      *                             CONDICION GARANTIA ( EN POD )
                   04  OPD-COD-GPOD PICTURE X(1).
      *                             SEPARACION ENTRE VENCIMIENTOS
                   04  OPD-NUM-SEVE PICTURE 9(4)
                                        COMPUTATIONAL.
      *                             UNIDAD DE TIEMPO OPD-NUM-SEVE
                   04  OPD-IND-SEVE PICTURE X(1).
      *                             DURACION VENCIMIENTOS
                   04  OPD-NUM-DURV PICTURE 9(4)
                                        COMPUTATIONAL.
      *                             UNIDAD DE TIEMPO OPD-NUM-DURV
                   04  OPD-IND-DURV PICTURE X(1).
      *                             NUMERO VENCIMIENTOS NO LIQUIDADOS
                   04  OPD-NUM-VNLQ PICTURE 9(3).
      *                             NUMERO VENCIMIENTOS LIQUIDADOS
                   04  OPD-NUM-VSLQ PICTURE 9(3).
      *                             MODALIDAD DE RETIRO
                   04  OPD-COD-MORE PICTURE X(2).
      *                             MONTO O PORCENTAJE DEL RETIRO
                   04  OPD-SGV-MPRE PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             INDICADOR ORDEN NO PAGO
                   04  OPD-COD-IONP PICTURE X(1).
      *                             SITUACION CONTABLE
                   04  OPD-COD-SCTB PICTURE X(3).
      *                             INDICADOR RENOVABILIDAD
                   04  OPD-COD-RENO PICTURE X(1).
      *                             INDICADOR ENDOSABILIDAD
                   04  OPD-COD-ENDO PICTURE X(1).
      *                             VALOR REAJUSTES DEVENGADOS
                   04  OPD-VAL-READ PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             VALOR INTERESES DEVENGADOS
                   04  OPD-VAL-INTD PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             USUARIO QUE AUTORIZA TASA ESPECIAL
                   04  OPD-COD-EATE PICTURE X(12).
      *                             TASA PERIODO
                   04  OPD-VAL-SINT PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             FECHA DEVENGO ( ULTIMO PAGO TOTAL IN
                   04  OPD-FEC-FDEV.
                       05  OPD-NUM-SDEV
                                    PICTURE 9(2).
                       05  OPD-NUM-ADEV
                                    PICTURE 9(2).
                       05  OPD-NUM-MDEV
                                    PICTURE 9(2).
                       05  OPD-NUM-DDEV
                                    PICTURE 9(2).
      *                             FECHA TERMINO DE LA OPERACION
                   04  OPD-FEC-FTER.
                       05  OPD-NUM-STER
                                    PICTURE 9(2).
                       05  OPD-NUM-ATER
                                    PICTURE 9(2).
                       05  OPD-NUM-MTER
                                    PICTURE 9(2).
                       05  OPD-NUM-DTER
                                    PICTURE 9(2).
      *                             TIPO CERTIF DAP
                   04  OPD-COD-TCDP PICTURE X(3).
      *                             CODIGO RESCATE
                   04  OPD-COD-RESC PICTURE X(1).
      *                             CODIGO PLAZO PAGO RESCATE
                   04  OPD-COD-PLPA PICTURE X(1).
      *                             PLAZO RESCATE
                   04  OPD-NUM-PLRE PICTURE 9(3).
      *                             UNIDAD TIEMPO PLAZO RESCATE
                   04  OPD-IND-PLRE PICTURE X(1).
      *                             PERIODO NEGOCIACION TASA ESPECIAL
                   04  OPD-NUM-PNTE PICTURE 9(3).
      *                             UNIDAD TIEMPO OPD-NUM-PNTE
                   04  OPD-IND-PNTE PICTURE X(1).
      *                             PLAZO CONTABLE
                   04  OPD-COD-PLZO PICTURE X(2).
      *                             RESERVADO SAFE
                   04  OPD-GLS-SAFE PICTURE X(18).
      *                             FECHA DE CAPTACION ORIGINAL
                   04  OPD-FEC-FPCO.
                       05  OPD-NUM-SPCO
                                    PICTURE 9(2).
                       05  OPD-NUM-APCO
                                    PICTURE 9(2).
                       05  OPD-NUM-MPCO
                                    PICTURE 9(2).
                       05  OPD-NUM-DPCO
                                    PICTURE 9(2).
      *                             PERIODO PRIMER VENCIMIENTO
                   04  OPD-NUM-PVEN PICTURE 9(5)
                                        COMPUTATIONAL-3.
      *                             UNIDAD TIEMPO OPD-NUM-PVEN
                   04  OPD-IND-PVEN PICTURE X(1).
      *                             NUMERO DE OPERACION QUE GENERO RENOV
                   04  OPD-CIC-GREN.
                       05  OPD-CAI-GREN
                                    PICTURE X(4).
                       05  OPD-IIC-GREN
                                    PICTURE X(8).
      *                             INDICADOR ABONO AUTOMATICO A CUENTA
                   04  OPD-COD-AACC PICTURE X(1).
      *                             NUMERO DE OPERACION ORIGINAL
                   04  OPD-CIC-ORIG.
                       05  OPD-CAI-ORIG
                                    PICTURE X(4).
                       05  OPD-IIC-ORIG
                                    PICTURE X(8).
      *                             MULTIPLES CLIENTES ?
                   04  OPD-IND-MCLI PICTURE X(1).
      *                             INGRESO NO LINEA ? ( S = ODB, N = OD
                   04  OPD-IND-IDIF PICTURE X(1).
      *                             MONTO CAPTACION EN PESOS
                   04  OPD-VAL-IPES PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             INDICADOR CUSTODIA ORIGINAL
                   04  OPD-COD-CUSO PICTURE X(1).
      *                             GLOSA DISPONIBLE
                   04  OPD-GLS-DISP PICTURE X(9).

      *
      ***************************************************************
      *                 ELEMENT LIST
      ***************************************************************
      *
       01  ELEM-LIST.
           03 ELEM-1                        PIC X(05).
           03 ELEM-SEC                      PIC X(01).
           03 FILLER                        PIC X(05) VALUE SPACES.
           EJECT
      *
      *            MODULO DE CONTROL
      *
       PROCEDURE DIVISION.
       A-CONTROL SECTION.
      *
           ENTRY 'DBMSCBL'.
           COPY BIBBAUD2.
      *
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
      *
       AB-PROCESAMIENTO.
           IF SW-ERROR = 'NO'
              PERFORM C-PROCESAR
           END-IF.
      *
       AC-TERMINACION.
           PERFORM E-TERMINAR.
      *
       AZZ-FIN-DE-PROCESO.
           GOBACK.
           EJECT
      *
      *            MODULO DE INICIALIZACION
      *
       B-INICIALIZAR SECTION.
      *
       B1-CONTROL-PROCESO.
      *-------------------
           ACCEPT PARAMETROS.
           DISPLAY 'EL PARAMETRO INGRESADO ES :  ' PARAMETROS.
           OPEN OUTPUT ADMMIPA
                INPUT  DAPMIPA.
           IF FS-IPA NOT = 0
              DISPLAY 'ERROR AL ABRIR ARCHIVO IPA FS: ' FS-IPA
              MOVE  'SI'                    TO SW-ERROR
           END-IF.
      *
      *
       BZZ-FIN-DE-SECCION.
           EXIT.
           EJECT
      *
      *            MODULO DE PROCESO PRINCIPAL
      *
       C-PROCESAR SECTION.
      *
      *
       CA-CONTROL.
      *-----------
           PERFORM CA1-LEER-IPA.
           PERFORM CB1-PROCESAR UNTIL SW-FINIPA = 'SI'.
           GO TO CZZZ-FIN-SECCION.
      *
       CA1-LEER-IPA.
      *--------------
           READ DAPMIPA AT END MOVE 'SI' TO SW-FINIPA.
           IF  FS-IPA  NOT = 0  AND SW-FINIPA = 'NO'
               DISPLAY '**** ERROR AL LEER ARCHIVO IPA FS:' FS-IPA
               MOVE  'SI'                   TO SW-FINIPA
                                               SW-ERROR
           ELSE
               IF SW-FINIPA = 'NO'
                  ADD  1                    TO TOTAL-AIPA
               END-IF
           END-IF.
      *
       CB1-PROCESAR.
      *-------------

           IF IPA-NUM-AVEN IN IPA = PA-ANO AND
              IPA-NUM-MVEN IN IPA = PA-MES
              PERFORM CB2-GRABA-ARCHIVO
           ELSE
              IF IPA-NUM-ALIQ IN IPA = PA-ANO AND
                 IPA-NUM-MLIQ IN IPA = PA-MES
                 PERFORM CB2-GRABA-ARCHIVO
              END-IF
           END-IF.

           PERFORM CA1-LEER-IPA.

      *
       CB2-GRABA-ARCHIVO.
      *------------------
           PERFORM CBA-LEE-OPD.

           MOVE CORR IPA                    TO REG-IPA.
           MOVE OPD-CIC-IOPD                TO IPA-CIC-IOPD.

           WRITE REG-IPA
           ADD 1                            TO TOTAL-AIPA2.
      *
       CBA-LEE-OPD.
      *-------------
      ** UBICA Y LEE REGISTRO TABLA OPD
           MOVE 'REDKY'                     TO REQ-OPD-CMD.
           MOVE 'OPD'                       TO REQ-OPD-TBLNM.
           MOVE 'OPD01'                     TO REQ-OPD-KEYNM.
           MOVE  IPA-CIC-ISER IN IPA        TO REQ-KEY-OPD-IOPD.
           MOVE 'OPD00'                     TO ELEM-1.
           CALL 'DBNTRY'                    USING UIB
                                                  REQ-OPD-AREA
                                                  OPD
                                                  ELEM-LIST.
           EVALUATE REQ-OPD-RETURN-CODE
           WHEN '  '
              CONTINUE
           WHEN '14'
              DISPLAY 'REGISTRO NO EXISTE EN OPD     '
              DISPLAY 'SERIAL NO ENCONTRADO    =     ' IPA-CIC-ISER
                                                       IN IPA
              MOVE SPACES                   TO OPD-CIC-IOPD
              ADD  1                        TO TOTAL-AOPD
           WHEN  OTHER
              DISPLAY        '**************************'
              DISPLAY        '* ERROR: REDKY TABLA OPD *'
              DISPLAY        '* TABLA          : ' REQ-OPD-TBLNM
              DISPLAY        '* CODIGO RETORNO : ' REQ-OPD-RETURN-CODE
              DISPLAY        '* REQ. AREA      : ' REQ-OPD-AREA
              MOVE 'SI'                     TO SW-ERROR
                                               SW-FINIPA
           END-EVALUATE.
      *
      *
      *
       CZZZ-FIN-SECCION.
             EXIT.
             EJECT
      *
      *            MODULO DE TERMINACION
      *
       E-TERMINAR SECTION.
      *
       EA-CONTROL.
           PERFORM EB-TOTALIZAR
           PERFORM EC-CERRAR-ARCHIVOS
           GO TO DZZ-FIN-DE-SECCION.
      *
       EB-TOTALIZAR.
           DISPLAY '    TOTALES DE CONTROL PGM. DAPPGAUD'
           DISPLAY '    --------------------------------'
           DISPLAY ' '
           DISPLAY ' REGISTROS ARCHIVO IPA LEIDOS                =  '
                    TOTAL-AIPA.
           DISPLAY ' REGISTROS ARCHIVO IPA GRABADOS              =  '
                    TOTAL-AIPA2
           DISPLAY ' REGISTROS OPD NO ENCONTRADOS                =  '
                    TOTAL-AOPD
      *
           IF SW-ERROR    = 'SI'
              DISPLAY ' HAY MENSAJES PARA EL EXPLOTADOR'
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '****                                        **'
              DISPLAY '****      TERMINO ERRONEO DEL PROCESO       **'
              DISPLAY '****                                        **'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              MOVE 100                      TO RETURN-CODE
           ELSE
              DISPLAY ' NO HAY MENSAJES PARA EL EXPLOTADOR'
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL ***'
           END-IF.
           SKIP1
       EC-CERRAR-ARCHIVOS.
           CLOSE DAPMIPA ADMMIPA.
      *
       DZZ-FIN-DE-SECCION.
           EXIT.
           EJECT
