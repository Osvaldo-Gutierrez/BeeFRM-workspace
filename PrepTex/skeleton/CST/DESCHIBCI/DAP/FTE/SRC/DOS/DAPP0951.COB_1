000000 IDENTIFICATION DIVISION.
000000 PROGRAM-ID. DAPP0951.
000000 AUTHOR. FCO. HERMOSILLA L.
000000 DATE-WRITTEN. 2/3/83.
000000 DATE-COMPILED.
000000*REMARKS. BANCO DE CREDITO E INVERSIONES
000000*         SISTEMA DE DEPOSITOS A PLAZO.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
000000******************************************************************
000000*                                                                *
000000*    OBJETIVOS: LISTAR REGISTROS SELECCIONADOS POR FECHA VENCI-  *
000000*               MIENTO Y SUCURSAL SACADOS DESDE EL ARCHIVO DE    *
000000*               CARTERA VENCIDA.                                 *
000000*               ESTE PROGRAMA REEMPLAZA A OTRO DE IGUAL NOMBRE   *
000000*               QUE FUE DESARROLLADO EN SEPTIEMBRE DE 1983 CON   *
000000*               EL MISMO FIN.                                    *
000000*                                                                *
000000*    ARCHIVOS                                                    *
000000*      ENTRADA: FDIM01  MAESTRO CARTERA VENCIDA (DISCO-VSAM).    *
000000*               FDIC01  CODIGOS DE OPERACION EN TARJETAS.        *
000000*                                                                *
000000*      SALIDA : FCOR01  IMRESORA DEL INFORME.                    *
000000*                                                                *
000000*      OTROS  : F-SORT  ARCHIVO DE CLASIFICACION.                *
000000*               TARJETA PARAMETROS DE FECHAS Y SUCURSALES.       *
000000*                                                                *
000000******************************************************************
000000*               M A N T E N C I O N E S                          *
000000******************************************************************
000000*    28/03/85  F. HERMOSILLA L.                                  *
000000*              - NORMALIZAR COLUMNA 'CAPITAL FINAL' DEL INFORME, *
000000*                QUE EN CIERTOS CASOS NO TOTALIZABA.             *
000000*                                                                *
000000******************************************************************
000000*    26/12/86  H. ARAVENA R.                                     *
000000*              - SE CAMBIA FORMATO DE REGISTRO DE ARCHIVO DOCU-  *
000000*                MENTOS VENCIDOS.                                *
000000*                                                                *
000000******************************************************************
000000*    05/04/89  F. HERMOSILLA L.                                  *
000000*              - AMPLIAR A 60 OCURRENCIAS LA CAPACIDAD DE LA     *
000000*                TABLA DE CODIGOS DE OPERACION.                  *
000000*              - AMPLIAR A 250 OCURRENCIAS LA CAPACIDAD DE LA    *
000000*                TABLA DE SUCURSALES.                            *
000000*                                                                *
000000******************************************************************
000000*    06/96     P CONCHA L                                        *
000000*              - CONVERSION A COBOL-2                            *
000000*              - REEMPLAZA A DOPZ951 QUE QUEDA OBSOLETO          *
000000*              - SE INCORPORA USO DE RUTINAS RUTUVALF, RUTUOFIC. *
000000*                                                                *
000000******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
000000 ENVIRONMENT DIVISION.
000000 CONFIGURATION SECTION.
000000 SOURCE-COMPUTER. IBM-370-3031.
000000 OBJECT-COMPUTER. IBM-370-3031.
000000 SPECIAL-NAMES.
000000     C01 IS CANAL-1
000000     DECIMAL-POINT IS COMMA.
000000 INPUT-OUTPUT SECTION.
000000 FILE-CONTROL.
000000     SELECT FDIM01 ASSIGN TO AS-FDIM01
000000        ORGANIZATION IS SEQUENTIAL
000000        ACCESS IS SEQUENTIAL
000000        FILE STATUS IS EB-STT-FDIM01.
000000     SELECT FDIC01 ASSIGN TO UT-S-FDIC01.
000000     SELECT FDOR01 ASSIGN TO UT-S-FCOR01.
000000     SELECT F-SORT ASSIGN TO UT-S-SORTWK1.
000000     EJECT
000000*
000000*
000000 DATA DIVISION.
000000 FILE SECTION.
000000*
000000 FD  FDIM01
000000     LABEL RECORDS IS STANDARD
000000     DATA  RECORD ARE R-DCTOVEN
000000                      R-DCTOREN.
000000
000000 01  R-DCTOVEN.
000000     03 ADVF-CLAVE              PIC X(6).
000000     03 ADVF-STAT1              PIC X.
000000     03 ADVF-STAT2              PIC X.
000000     03 ADVF-MISC               PIC X(3).
000000     03 ADVF-CAPITAL            PIC S9(9)V99 COMP-3.
000000     03 ADVF-INTERES            PIC S9(9)V99 COMP-3.
000000     03 ADVF-CAPITAL-IN         PIC S9(9)V99 COMP-3.
000000     03 ADVF-LIQUIDA            PIC S9(9)V99 COMP-3.
000000     03 ADVF-OFLIQ              PIC X.
000000     03 ADVF-FCHLIQ             PIC X(3).
000000     03 ADVF-PLAZO              PIC S9(3)    COMP-3.
000000     03 ADVF-MONEDA             PIC X.
000000     03 ADVF-FCHVCTO            PIC X(3).
000000     03 ADVF-CODOPER            PIC S9(3)    COMP-3.
000000     03 ADVF-TASA               PIC S9(3)V99 COMP-3.
000000     03 ADVF-FCHCAPT            PIC X(3).
000000     03 ADVF-RUTNRO1            PIC X(5).
000000     03 ADVF-RUTDIG1            PIC X.
000000     03 ADVF-NOMBRE1            PIC X(30).
000000     03 ADVF-DIRECC1            PIC X(30).
000000     03 ADVF-CCIUDAD1           PIC XX.
000000     03 ADVF-NOMBRE2            PIC X(30).
000000     03 ADVF-RUTNRO2            PIC X(5).
000000     03 ADVF-RUTDIG2            PIC X.
000000     03 ADVF-INDTASESP          PIC XX.
000000
000000 01  R-DCTOREN.
000000     03 ADVR-CLAVE              PIC X(6).
000000     03 ADVR-STAT1              PIC X.
000000     03 ADVR-STAT2              PIC X.
000000     03 ADVR-MISC               PIC X(3).
000000     03 ADVR-CAPITAL            PIC S9(9)V99 COMP-3.
000000     03 ADVR-INTERES            PIC S9(9)V99 COMP-3.
000000     03 ADVR-CAPITAL-IN         PIC S9(9)V99 COMP-3.
000000     03 ADVR-LIQUIDA            PIC S9(9)V99 COMP-3.
000000     03 ADVR-OFLIQ              PIC X.
000000     03 ADVR-FCHLIQ             PIC X(3).
000000     03 ADVR-PLAZO              PIC S9(3)    COMP-3.
000000     03 ADVR-MONEDA             PIC X.
000000     03 ADVR-FCHVCTO            PIC X(3).
000000     03 ADVR-CODOPER            PIC S9(3)    COMP-3.
000000     03 ADVR-TASA               PIC S9(3)V99 COMP-3.
000000     03 ADVR-FCHCAPT            PIC X(3).
000000     03 ADVR-RUTNRO1            PIC X(5).
000000     03 ADVR-RUTDIG1            PIC X.
000000     03 ADVR-NOMBRE1            PIC X(30).
000000     03 ADVR-DIRECC1            PIC X(30).
000000     03 ADVR-CCIUDAD1           PIC XX.
000000     03 ADVR-NOMBRE2            PIC X(30).
000000     03 ADVR-RUTNRO2            PIC X(5).
000000     03 ADVR-RUTDIG2            PIC X.
000000     03 ADVR-INDTASESP          PIC XX.
000000     03 ADVR-FECHULT            PIC X(3).
000000     03 ADVR-TASAULT            PIC S9(3)V99  COMP-3.
000000     03 ADVR-TESPULT            PIC XX.
000000     03 ADVR-CAPULT             PIC S9(11)V99 COMP-3.
000000*
000000 FD  FDIC01
000000     RECORDING MODE IS F
000000     LABEL RECORD STANDARD
000000     BLOCK CONTAINS  0  RECORDS.
000000 01  REG-TARJ.
000000     05  COD-OPER                    PIC 9(03).
000000     05  DESCRIP-OPER                PIC X(38).
000000     05  FILLER                      PIC X(39).
000000*
000000 SD  F-SORT.
000000*
000000*
000000*                 REGISTRO PARA DOCUMENTOS A PLAZO FIJO NO
000000*                 RENOVABLES Y D.A.C.
000000 01  R-SORT.
000000     03 SORT-CLAVE.
000000        05 FILLER               PIC 9.
000000        05 SORT-OFICAPT         PIC 9(3).
000000        05 FILLER               PIC 9.
000000        05 SORT-NUMDOCTO        PIC 9(6).
000000        05 SORT-TIPODOCTO       PIC 9.
000000     03 SORT-STAT1              PIC X.
000000     03 SORT-STAT2              PIC X.
000000     03 SORT-MISC               PIC X(3).
000000     03 SORT-CAPITAL            PIC S9(9)V99 COMP-3.
000000     03 SORT-INTERES            PIC S9(9)V99 COMP-3.
000000     03 SORT-CAPITAL-IN         PIC S9(9)V99 COMP-3.
000000     03 SORT-LIQUIDA            PIC S9(9)V99 COMP-3.
000000     03 SORT-OFLIQ              PIC X.
000000     03 SORT-FCHLIQ             PIC 9(6).
000000     03 SORT-PLAZO              PIC S9(3)    COMP-3.
000000     03 SORT-MONEDA             PIC X.
000000     03 SORT-FCHVCTO.
000000        05 SORT-DDVCTO          PIC 99.
000000        05 SORT-MMVCTO          PIC 99.
000000        05 SORT-AAVCTO          PIC 99.
000000     03 SORT-CODOPER            PIC S9(3)    COMP-3.
000000     03 SORT-TASA               PIC S9(3)V99 COMP-3.
000000     03 SORT-FCHCAPT.
000000        05 SORT-DDCAPT          PIC 99.
000000        05 SORT-MMCAPT          PIC 99.
000000        05 SORT-AACAPT          PIC 99.
000000     03 SORT-RUT.
000000        05 SORT-RUTNRO1         PIC 9(10).
000000        05 SORT-RUTDIG1         PIC X.
000000     03 SORT-NOMBRE1            PIC X(30).
000000     03 SORT-DIRECC1            PIC X(30).
000000     03 SORT-CCIUDAD1           PIC XX.
000000     03 SORT-NOMBRE2            PIC X(30).
000000     03 SORT-RUTNRO2            PIC 9(10).
000000     03 SORT-RUTDIG2            PIC X.
000000     03 SORT-INDTASESP          PIC XX.
000000*
000000*
000000 FD  FDOR01
000000     RECORDING MODE IS F
000000     LABEL RECORD STANDARD
000000     BLOCK CONTAINS  0  RECORDS.
000000 01  FDOR01-IMPR        PIC X(132).
000000     EJECT
000000*
000000*
000000 WORKING-STORAGE SECTION.
000000*
      *
       01  BIBBAUD1.
           05 FILLER          PIC X(20) VALUE 'VARIABLES DE AUDIT'.
           05 BIB-MODULO      PIC X(08) VALUE '[MODNAME'.
           05 BIB-PROGRA      PIC X(15) VALUE '[PROGRAMMERNAME'.
           05 BIB-NIVEL       PIC X(05) VALUE '[LVNO'.
           05 BIB-FECHA-CATA  PIC X(08) VALUE '[DATEUPD'.
           05 BIB-MASTER      PIC X(44) VALUE
              '[DATA-SET-NAME-FOR-THE-LIBRARIAN-MASTER-FILE'.
           05 BIB-FECHA-EJEC  PIC 9(06).
           05 BIB-HORA-EJEC   PIC 9(08).
      *
000000*
000000 77  SW-CANCEL          PIC 9(01)     VALUE 0.
000000 77  EX-CONTALIN        PIC S9(3)     COMP-3 VALUE +99.
000000     88 LARGO-PAG VALUE +60 THRU +99.
000000 77  EX-CONTAPAG        PIC S9(5)     COMP-3 VALUE +0.
000000 77  EB-EOF-FDIC01      PIC S9        COMP-3 VALUE +0.
000000 77  EB-STT-FDIM01      PIC XX.
000000 77  EB-CONTAOPER       PIC S99       COMP-3 VALUE +0.
000000 77  EB-AREA-AUX        PIC X(41).
000000 77  EE-DOCVEN-LEIDOS   PIC S9(7)     COMP-3 VALUE +0.
000000 77  EE-DOCVEN-SORTEADOS PIC S9(7)     COMP-3 VALUE +0.
000000 77  EF-EOF-SORT        PIC S9        COMP-3 VALUE +0.
000000 77  EF-OFI-RESPALDO    PIC 9(03)            VALUE 0.
000000 77  EF-RUT-RESPALDO    PIC X(11).
000000 77  EF-OPE-RESPALDO    PIC S9(3)     COMP-3 VALUE +0.
000000 77  EF-CAPITAL         PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-CAPITAL-BCO     PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-REAJUSTE        PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-REAJUSTE-BCO    PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-INTERES         PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-INTERES-BCO     PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-CAPITFINAL      PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-CAPITFINAL-BCO  PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  VALOR-A-PAGAR      PIC S9(12)V99 COMP-3 VALUE +0.
000000 77  EF-DOCVEN-IMPRESOS PIC S9(6)     COMP-3 VALUE +0.
000000 77  EF-CONTAOPER       PIC S9(6)     COMP-3 VALUE +0.
000000 77  EF-CONTAOPER-BCO   PIC S9(6)     COMP-3 VALUE +0.
000000*
000000 01  SB-PARAMETROS.
000000     05 FECHA-INICIAL.
000000        10 AA-INI         PIC 9(02).
000000        10 MM-INI         PIC 9(02).
000000        10 DD-INI         PIC 9(02).
000000     05 FECHA-FINAL.
000000        10 AA-FIN         PIC 9(02).
000000        10 MM-FIN         PIC 9(02).
000000        10 DD-FIN         PIC 9(02).
000000     05 RANGO-SUCURSALES.
000000        10 SUC-INICIAL    PIC 9(3).
000000        10 SUC-FINAL      PIC 9(3).
000000     05 FILLER            PIC X(62).
000000*
000000 01  SB-TABLA-CODOPER.
000000     05 TBL-CODOPER  OCCURS 0 TO 60 TIMES
000000                     DEPENDING ON EB-CONTAOPER
000000                     ASCENDING KEY CODOPER-TBL
000000                     INDEXED BY COPX1 COPX2.
000000        10 CODOPER-TBL    PIC 9(3).
000000        10 NOMOPER-TBL    PIC X(38).
000000*
000000*              TITULOS Y LINEAS DEL INFORME                      *
000000******************************************************************
000000*                                                                *
000000 01  SF-T1.
000000     05 FILLER            PIC X(44)   VALUE
000000     'BANCO DE CREDITO E INVERSIONES              '.
000000     05 FILLER            PIC X(29)   VALUE
000000     '              FECHA PROCESO: '.
000000     05 FECHA-PROC-T1     PIC X(8).
000000     05 FILLER            PIC X(7).
000000     05 FILLER            PIC X(40)   VALUE
000000     '           PGM: DOPZ95             PAG. '.
000000     05 NRO-PAG-T1        PIC ZZZZ.
000000*
000000 01  SF-T1A.
000000     05 FILLER            PIC X(10)   VALUE 'OFICINA : '.
000000     05 DESCRIP-OFIC-T1A  PIC X(30).
000000     05 FILLER            PIC X(92)   VALUE SPACES.
000000*
000000 01  SF-T2.
000000     05 FILLER            PIC X(10)   VALUE 'CLIENTE : '.
000000     05 NOM-CLIENTE-T2    PIC X(30).
000000     05 FILLER            PIC X(92)   VALUE SPACES.
000000*
000000 01  SF-T3.
000000     05 FILLER            PIC X(10)   VALUE 'R.U.T.  : '.
000000     05 NRO-RUT-T3        PIC 9(8).
000000     05 FILLER            PIC X       VALUE '-'.
000000     05 DV-RUT-T3         PIC X.
000000     05 FILLER            PIC X(112)  VALUE SPACES.
000000*
000000 01  SF-T4.
000000     05 FILLER            PIC X(44)   VALUE
000000     '                                  I N F O R '.
000000     05 FILLER            PIC X(44)   VALUE
000000     'M E  D E  O P E R A C I O N E S  P O R  C L '.
000000     05 FILLER            PIC X(44)   VALUE
000000     'I E N T E                                   '.
000000*
000000 01  SF-S1.
000000     05 FILLER            PIC X(34)   VALUE SPACES.
000000     05 FILLER            PIC X(63)   VALUE ALL '*'.
000000     05 FILLER            PIC X(35)   VALUE SPACES.
000000*
000000 01  SF-T5.
000000     05 FILLER            PIC X(59)   VALUE SPACES.
000000     05 FILLER            PIC X(11)   VALUE 'A N O  1 9 '.
000000     05 AGNO-T5           PIC 99.
000000     05 FILLER            PIC X(60)   VALUE SPACES.
000000*
000000 01  SF-S2.
000000     05 FILLER            PIC X(59)   VALUE SPACES.
000000     05 FILLER            PIC X(14)   VALUE ALL '*'.
000000     05 FILLER            PIC X(59)   VALUE SPACES.
000000*
000000 01  SF-S3.
000000     05 FILLER            PIC X(132)  VALUE ALL '='.
000000*
000000 01  SF-T6.
000000     05 FILLER            PIC X(44)   VALUE
000000     '  T I P O  D E  O P E R A C I O N      NUMER'.
000000     05 FILLER            PIC X(44)   VALUE
000000     'O    FECHA    FECHA    C A P I T A L     REA'.
000000     05 FILLER            PIC X(44)   VALUE
000000     'JUSTE    TASA  I N T E R E S   C A P I T A L'.
000000*
000000 01  SF-T7.
000000     05 FILLER            PIC X(44)   VALUE
000000     '                                       DOCTO'.
000000     05 FILLER            PIC X(44)   VALUE
000000     '.   CAPTAC.  VENCTO.   I N I C I A L        '.
000000     05 FILLER            PIC X(44)   VALUE
000000     '                                 F I N A L  '.
000000*
000000 01  SF-T8.
000000     05 DESCRIP-OPE-T8    PIC X(38).
000000     05 FILLER            PIC X       VALUE ':'.
000000     05 FILLER            PIC X(93)   VALUE SPACES.
000000*
000000 01  SF-LD.
000000     05 FILLER            PIC X(8)    VALUE SPACES.
000000     05 FILLER            PIC X(30)   VALUE SPACES.
000000     05 NUM-DOCTO-LD      PIC B9(6).
000000     05 FECHA-CAP-LD.
000000        10 AA-CAP-LD      PIC BB99.
000000        10 FILLER         PIC X       VALUE '/'.
000000        10 MM-CAP-LD      PIC 99.
000000        10 FILLER         PIC X       VALUE '/'.
000000        10 DD-CAP-LD      PIC 99.
000000     05 FECHA-VEN-LD.
000000        10 AA-VEN-LD      PIC B99.
000000        10 FILLER         PIC X       VALUE '/'.
000000        10 MM-VEN-LD      PIC 99.
000000        10 FILLER         PIC X       VALUE '/'.
000000        10 DD-VEN-LD      PIC 99.
000000     05 CAPITAL-LD        PIC BBZZZ.ZZZ.ZZ9,99.
000000     05 REAJUSTE-LD       PIC BBZZ.ZZZ.ZZ9,99.
000000     05 TASA-LD           PIC BZ9,99.
000000     05 INTERES-LD        PIC BBZZ.ZZZ.ZZ9,99.
000000     05 CAPIT-TOT-LD      PIC BBZZZ.ZZZ.ZZ9,99.
000000*
000000 01  SF-LT1.
000000     05 GLOSA-LT1       PIC X(17)   VALUE 'TOTALES CLIENTE: '.
000000     05 CANT-OPERAC-LT1 PIC ZZ.ZZZ.
000000     05 FILLER          PIC X(12)   VALUE ' OPERACIONES'.
000000     05 CAPITAL-LT1     PIC B(27)ZZZ.ZZZ.ZZZ.ZZ9,99.
000000     05 INTERES-LT1     PIC B(18)ZZZ.ZZZ.ZZZ.ZZ9,99.
000000     05 FILLER          PIC X(16)   VALUE SPACES.
000000*
000000 01  SF-LT2.
000000     05 FILLER          PIC X(77)   VALUE SPACES.
000000     05 REAJUSTE-LT2    PIC ZZZ.ZZZ.ZZZ.ZZ9,99.
000000     05 CAPIT-TOT-LT2   PIC B(19)ZZZ.ZZZ.ZZZ.ZZ9,99.
000000*
000000*
000000 01  AREA-TRANSFORMA-BCD-A-DISPLAY.
000000*
000000*
000000******                NUMERO R.U.T. CLIENTE              ********
000000     05 AT-NRORUT-BCD.
000000        10 ATR-NRUT-BCD  PIC X(5).
000000        10 ATR-FILLER    PIC 9       COMP-3 VALUE 0.
000000     05 R-AT-NRUT-ID REDEFINES AT-NRORUT-BCD  PIC 9(10)V9  COMP-3.
000000     05 AT-NRORUT-ED     PIC 9(10).
000000*
000000******                CLAVE MAE. CART. LINEA             ********
000000     05 AT-CLAVE-BCD.
000000        10 ATC-CLAVE-BCD PIC X(6).
000000        10 ATC-FILLER    PIC 9       COMP-3 VALUE 0.
000000     05 R-AT-CLAVE-BCD  REDEFINES AT-CLAVE-BCD PIC 9(12)V9 COMP-3.
000000     05 AT-CLAVE-ED      PIC 9(12).
000000     05 R-AT-CLAVE-ED REDEFINES AT-CLAVE-ED.
000000        10 FILLER        PIC 9.
000000        10 AT-CLOFICAPT  PIC 9(3).
000000        10 FILLER        PIC 9.
000000        10 AT-CLNUMDOCTO PIC 9(6).
000000        10 AT-CLTIPO     PIC 9.
000000*
000000******                CODIGO DE CIUDAD                   ********
000000     05 AT-CODCIUD-BCD.
000000        10 ATC-CODCIUD-BCD PIC X(2).
000000        10 ATC-FILLER      PIC 9       COMP-3 VALUE 0.
000000     05 R-AT-CODCIUD-BCD  REDEFINES AT-CODCIUD-BCD
000000                           PIC 9(4)V9  COMP-3.
000000     05 AT-CODCIUD-ED      PIC 9(4).
000000
000000*
000000******                FECHA  CAPTACION                   ********
000000     05 AT-FCHCAPT-BCD.
000000        10 ATC-FCHCAPT-BCD PIC X(3).
000000        10 ATC-FILLER      PIC 9       COMP-3 VALUE 0.
000000     05 R-AT-FCHCAPT-BCD  REDEFINES AT-FCHCAPT-BCD
000000                           PIC 9(6)V9  COMP-3.
000000     05 AT-FCHCAPT-ED      PIC 9(6).
000000     05 AT-FCHC-DDMMAA     REDEFINES AT-FCHCAPT-ED.
000000        10 AT-FCHC-DD      PIC 99.
000000        10 AT-FCHC-MM      PIC 99.
000000        10 AT-FCHC-AA      PIC 99.
000000*
000000******                FECHA  VENCIMIENTO                 ********
000000     05 AT-FCHVENC-BCD.
000000        10 ATC-FCHVENC-BCD PIC X(3).
000000        10 ATC-FILLER      PIC 9       COMP-3 VALUE 0.
000000     05 R-AT-FCHVENC-BCD  REDEFINES AT-FCHVENC-BCD
000000                           PIC 9(6)V9  COMP-3.
000000     05 AT-FCHVENC-ED      PIC 9(6).
000000     05 AT-FCHV-DDMMAA     REDEFINES AT-FCHVENC-ED.
000000        10 AT-FCHV-DD      PIC 99.
000000        10 AT-FCHV-MM      PIC 99.
000000        10 AT-FCHV-AA      PIC 99.
000000     05 AT-FCHV-AAMMDD.
000000        10 AT-FCHV-AA      PIC 99.
000000        10 AT-FCHV-MM      PIC 99.
000000        10 AT-FCHV-DD      PIC 99.
000000*
000000     EJECT
      ******************************************************************
      *
       01  RTNWOFIC.
      *
      * AREA DE COMUNICACION DE SUBRUTINA "RTNUOFIC"
      *
      * ***  FUNCIONES VALIDAS PARA SUBRUTINA  ***
      *
      * CARGAR : CARGA TABLA DE OFICINAS EN MEMORIA
      * BUSCAR : BUSCA OFICINA POR CODIGO
      * DESPLI : DESPLIEGA CONTENIDO DE TABLA EN MEMORIA (COMPLETA)
      *
           05 OFICAREA-FUNCION     PIC X(06).
           05 OFICAREA-CODOF       PIC 9(03).
           05 OFICAREA-NOMOF       PIC X(30).
           05 OFICAREA-REGIONAL    PIC X(02).
           05 OFICAREA-COD-REG     PIC X(02).
           05 OFICAREA-IND-PROV    PIC X(01).
           05 OFICAREA-RUT.
              10 OFICAREA-RUT-NU      PIC X(08).
              10 OFICAREA-RUT-DV      PIC X(01).
           05 OFICAREA-DIR         PIC X(22).
           05 OFICAREA-COD-RET     PIC 9(02).
      *
      ******************************************************************
000000*
000000     EJECT
000000 01  RUTWVALF.
000000     05 RUTWVALF-FEC.
000000        10 RUTWVALF-FEC-DD    PIC 9(02)   VALUE 0.
000000        10 RUTWVALF-FEC-MM    PIC 9(02)   VALUE 0.
000000        10 RUTWVALF-FEC-SS    PIC 9(02)   VALUE 0.
000000        10 RUTWVALF-FEC-AA    PIC 9(02)   VALUE 0.
000000     05 RUTWVALF-COD-RET      PIC 9(01)   VALUE 0.
      *

           COPY RLOVR001.

       77  Y2K-AT-FCHV-AAMMDD
                         PIC 9(6).
       77  Y2K-FECHA-INICIAL
                         PIC 9(6).
       77  Y2K-FECHA-FINAL
                         PIC 9(6).
000000*
000000     EJECT
000000*
000000*            MODULO DE CONTROL
000000*
000000 PROCEDURE DIVISION.
000000*
000000 A-CONTROL SECTION.
      *
      * DESPLIEGA VARIABLES DE AUDIT
           DISPLAY '***********************************************'.
           DISPLAY '***   IDENTIFICACION DE MODULO EJECUTADO    ***'.
           DISPLAY 'MASTER FILE               = ' BIB-MASTER.
           DISPLAY 'NOMBRE DEL MODULO         = ' BIB-MODULO.
           DISPLAY 'NUMERO DE VERSION         = ' BIB-NIVEL.
           DISPLAY 'FECHA DE CATALOGACION     = ' BIB-FECHA-CATA.
           DISPLAY 'NOMBRE DEL PROGRAMADOR    = ' BIB-PROGRA.
           DISPLAY ' '.
           DISPLAY '***   IDENTIFICACION DE EJECUCION           ***'.
           DISPLAY '*    (FECHA Y HORA DEL COMPUTADOR)            *'.
           ACCEPT BIB-FECHA-EJEC FROM DATE
           ACCEPT BIB-HORA-EJEC  FROM TIME
           DISPLAY 'FECHA EJECUCION           = ' BIB-FECHA-EJEC.
           DISPLAY 'HORA EJECUCION (HHMMSSDD) = ' BIB-HORA-EJEC.
           DISPLAY '***                                         ***'.
           DISPLAY '***********************************************'.
      *
000000*
000000 AA-INICIALIZACION.
000000     PERFORM B-INICIALIZAR.
000000*
000000 AB-PROCESAMIENTO.
000000     PERFORM C-PROCESAR.
000000*
000000 AC-TERMINACION.
000000     PERFORM D-TERMINAR.
000000*
000000 AZZ-FIN-DE-PROCESO.
000000     STOP RUN.
000000     EJECT
000000*
000000*            MODULO DE INICIALIZACION
000000*
000000 B-INICIALIZAR SECTION.
000000*
000000 BA-CONTROL.
000000     PERFORM BB-ACEPTAR-VALIDAR-PARAMETROS
000000     PERFORM BC-PROCESAR-CODIGOS-OPERACION
000000     PERFORM BF-PROC-OFICIN
000000     PERFORM BG-ABRIR-ARCH-FECHAR
000000     PERFORM BZ-VERIFICA
000000     GO TO BZZ-FIN-DE-SECCION.
000000*
000000 BB-ACEPTAR-VALIDAR-PARAMETROS.
000000     ACCEPT SB-PARAMETROS FROM SYSIN.
000000     DISPLAY 'PARAMETROS INGRESADOS= ' SB-PARAMETROS
000000* VALIDA FECHA INICIAL
000000     MOVE DD-INI          TO RUTWVALF-FEC-DD
000000     MOVE MM-INI          TO RUTWVALF-FEC-MM
000000*****MOVE 19              TO RUTWVALF-FEC-SS
000000     MOVE AA-INI          TO RUTWVALF-FEC-AA
           IF RUTWVALF-FEC-AA < ROLL-YY
              MOVE 20           TO RUTWVALF-FEC-SS
           ELSE
              MOVE 19           TO RUTWVALF-FEC-SS
           END-IF
000000     CALL 'RUTUVALF' USING RUTWVALF-FEC RUTWVALF-COD-RET
000000     IF RUTWVALF-COD-RET NOT = 0
000000        DISPLAY 'FECHA INICIAL INVALIDA.'
000000        MOVE 1 TO SW-CANCEL.
000000* VALIDA FECHA FINAL
000000     MOVE DD-FIN          TO RUTWVALF-FEC-DD
000000     MOVE MM-FIN          TO RUTWVALF-FEC-MM
000000*****MOVE 19              TO RUTWVALF-FEC-SS
000000     MOVE AA-FIN          TO RUTWVALF-FEC-AA
           IF RUTWVALF-FEC-AA < ROLL-YY
              MOVE 20           TO RUTWVALF-FEC-SS
           ELSE
              MOVE 19           TO RUTWVALF-FEC-SS
           END-IF
000000     CALL 'RUTUVALF' USING RUTWVALF-FEC RUTWVALF-COD-RET
000000     IF RUTWVALF-COD-RET NOT = 0
000000        DISPLAY 'FECHA FINAL   INVALIDA.'
000000        MOVE 1 TO SW-CANCEL.
000000     IF SUC-INICIAL NOT NUMERIC
000000        DISPLAY '*** SUCURSAL INICIAL NO NUMERICA'
000000        MOVE 1 TO SW-CANCEL.
000000     IF SUC-FINAL   NOT NUMERIC
000000        DISPLAY '*** SUCURSAL FINAL   NO NUMERICA'
000000        MOVE 1 TO SW-CANCEL.
000000*
000000 BC-PROCESAR-CODIGOS-OPERACION.
000000     OPEN INPUT FDIC01
000000     PERFORM BD-CARGAR-TABLA-CODOPER UNTIL
000000        EB-EOF-FDIC01 = 1
000000     PERFORM BE-ORDENAR-TABLA-ASCENDENTE
000000        VARYING COPX1 FROM 1 BY 1 UNTIL
000000           COPX1 = EB-CONTAOPER
000000           AFTER COPX2 FROM 1 BY 1 UNTIL
000000              COPX2 > EB-CONTAOPER
000000     CLOSE FDIC01.
000000*
000000 BD-CARGAR-TABLA-CODOPER.
000000     READ FDIC01 AT END MOVE +1 TO EB-EOF-FDIC01.
000000     IF EB-CONTAOPER < 60
000000        ADD 1 TO EB-CONTAOPER
000000        SET COPX1 TO EB-CONTAOPER
000000        MOVE COD-OPER TO CODOPER-TBL (COPX1)
000000        MOVE DESCRIP-OPER TO NOMOPER-TBL (COPX1)
000000     ELSE
000000        DISPLAY '13. TABLA DE OPERACIONES EXEDIDA. '
000000                EB-CONTAOPER
000000        MOVE 1 TO SW-CANCEL.
000000*
000000 BE-ORDENAR-TABLA-ASCENDENTE.
000000     IF COPX2 = 1
000000        SET COPX2 TO COPX1
000000        SET COPX2 UP BY 1.
000000     IF CODOPER-TBL (COPX1) > CODOPER-TBL (COPX2)
000000        MOVE TBL-CODOPER (COPX1) TO EB-AREA-AUX
000000        MOVE TBL-CODOPER (COPX2) TO TBL-CODOPER (COPX1)
000000        MOVE EB-AREA-AUX TO TBL-CODOPER (COPX2).
000000*
000000 BF-PROC-OFICIN.
000000     MOVE 'CARGAR'     TO OFICAREA-FUNCION
000000     CALL 'RUTUOFIC' USING RTNWOFIC
000000     IF OFICAREA-COD-RET NOT = 0
000000       DISPLAY '** ERROR EN CARGA OFICINAS'
000000       MOVE 1          TO SW-CANCEL.
000000     MOVE 'DESPLI'     TO OFICAREA-FUNCION
000000     CALL 'RUTUOFIC' USING RTNWOFIC.
000000*
000000 BG-ABRIR-ARCH-FECHAR.
000000     OPEN INPUT FDIM01.
000000     IF EB-STT-FDIM01 > '10'
000000        DISPLAY '15. ERROR STATUS MAE.DOC.VENC. (OPEN).'
000000                ' FS= ' EB-STT-FDIM01
000000        MOVE 1 TO SW-CANCEL.
000000     OPEN OUTPUT FDOR01
000000     ACCEPT FECHA-PROC-T1 FROM DATE
000000     MOVE AA-FIN TO AGNO-T5.
000000*
000000 BZ-VERIFICA.
000000     IF SW-CANCEL = 1
000000        DISPLAY '*** ERRORES EN INICIALIZACION ***'
000000        PERFORM D-TERMINAR.
000000*
000000 BZZ-FIN-DE-SECCION.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE PROCESO PRINCIPAL
000000*
000000 C-PROCESAR SECTION.
000000*
000000 CA-CONTROL.
000000     SORT F-SORT ON ASCENDING KEY  SORT-OFICAPT
000000                                   SORT-RUT
000000                                   SORT-CODOPER
000000        INPUT  PROCEDURE E-SELECCION-REGISTROS
000000        OUTPUT PROCEDURE F-LISTAR-REGISTROS
000000     IF SORT-RETURN NOT = 0
000000        DISPLAY '*** HUBO PROBLEMAS EN EL SORT ***'
000000        DISPLAY '*** HUBO PROBLEMAS EN EL SORT ***'
000000        MOVE 1 TO SW-CANCEL
000000        PERFORM D-TERMINAR.
000000*
000000 CZZ-FIN-DE-SECCION.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE TERMINACION
000000*
000000 D-TERMINAR SECTION.
000000*
000000 DA-TOTALES.
000000     CLOSE FDIM01
000000           FDOR01.
000000     DISPLAY ' '
000000     DISPLAY '*** TOTALES DE CONTROL  PGM DAPP0951'
000000     DISPLAY 'DOCTOS VENC LEIDOS.......' EE-DOCVEN-LEIDOS
000000     DISPLAY 'DOCTOS VENC SORTEADOS....' EE-DOCVEN-SORTEADOS
000000     DISPLAY 'DOCTOS VENC LISTADOS.....' EF-DOCVEN-IMPRESOS
000000     DISPLAY ' '
000000     IF SW-CANCEL = 0
000000        DISPLAY '*** TERMINO NORMAL ***'
000000     ELSE
000000        MOVE 100 TO RETURN-CODE
000000        DISPLAY '*** PROCESO CANCELADO ***'
000000        STOP RUN.
000000*
000000 DZZ-FIN-DE-SECCION.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE SELECCION DE REGISTROS
000000*
000000 E-SELECCION-REGISTROS SECTION.
000000*
000000 EA-CONTROL.
000000     PERFORM EB-LEER-SELECCIONAR UNTIL
000000        EB-STT-FDIM01 = '10'
000000     GO TO EZZ-FIN-DE-SECCION.
000000*
000000 EB-LEER-SELECCIONAR.
000000     READ FDIM01.
000000     IF EB-STT-FDIM01 = '00'
000000        ADD 1 TO EE-DOCVEN-LEIDOS
000000        PERFORM EC-TRANSFORMA-RUT-CLAVE-ESTADO
000000********IF AT-FCHV-AAMMDD   NOT < FECHA-INICIAL AND
000000****************************NOT > FECHA-FINAL
              MOVE AT-FCHV-AAMMDD TO Y2K-AT-FCHV-AAMMDD
              ADD ROLLDIFF-1-YYMMDD TO Y2K-AT-FCHV-AAMMDD
              MOVE FECHA-INICIAL TO Y2K-FECHA-INICIAL
              ADD ROLLDIFF-1-YYMMDD TO Y2K-FECHA-INICIAL
              MOVE FECHA-FINAL TO Y2K-FECHA-FINAL
              ADD ROLLDIFF-1-YYMMDD TO Y2K-FECHA-FINAL
              IF Y2K-AT-FCHV-AAMMDD NOT < Y2K-FECHA-INICIAL AND
                 NOT > Y2K-FECHA-FINAL
000000           IF AT-CLOFICAPT  NOT < SUC-INICIAL   AND
000000                            NOT > SUC-FINAL
000000              PERFORM ED-MOVER-OTROS-CAMPOS
000000           ELSE
000000              NEXT SENTENCE
000000        ELSE
000000           NEXT SENTENCE
000000     ELSE
000000        IF EB-STT-FDIM01 > '10'
000000           DISPLAY '17. ERROR STATUS MAE.DOC.VEN. (READ).'
000000                   ' FS= ' EB-STT-FDIM01
000000           MOVE 20 TO SORT-RETURN.
000000*
000000 EC-TRANSFORMA-RUT-CLAVE-ESTADO.
000000     MOVE ADVF-CLAVE     TO ATC-CLAVE-BCD
000000     MOVE R-AT-CLAVE-BCD TO AT-CLAVE-ED
000000*
000000     MOVE ADVF-RUTNRO1   TO ATR-NRUT-BCD
000000     MOVE R-AT-NRUT-ID   TO AT-NRORUT-ED
000000     MOVE AT-NRORUT-ED   TO SORT-RUTNRO1.
000000     MOVE ADVF-RUTDIG1   TO SORT-RUTDIG1.
000000*
000000     MOVE ADVF-FCHCAPT     TO ATC-FCHCAPT-BCD
000000     MOVE R-AT-FCHCAPT-BCD TO AT-FCHCAPT-ED
000000     MOVE AT-FCHC-DDMMAA   TO SORT-FCHCAPT.
000000*
000000     MOVE ADVF-FCHVCTO     TO ATC-FCHVENC-BCD
000000     MOVE R-AT-FCHVENC-BCD TO AT-FCHVENC-ED
000000     MOVE AT-FCHV-DDMMAA   TO SORT-FCHVCTO.
000000     MOVE CORR AT-FCHV-DDMMAA   TO AT-FCHV-AAMMDD.
000000*
000000 ED-MOVER-OTROS-CAMPOS.
000000     MOVE ADVF-CAPITAL     TO SORT-CAPITAL.
000000     MOVE ADVF-TASA        TO SORT-TASA.
000000     MOVE ADVF-PLAZO       TO SORT-PLAZO.
000000     MOVE AT-CLAVE-ED      TO SORT-CLAVE.
000000     MOVE ADVF-STAT1       TO SORT-STAT1.
000000     MOVE ADVF-STAT2       TO SORT-STAT2.
000000     MOVE ADVF-MISC        TO SORT-MISC.
000000     MOVE ADVF-INTERES     TO SORT-INTERES.
000000     MOVE ADVF-MONEDA      TO SORT-MONEDA.
000000     MOVE ADVF-CODOPER     TO SORT-CODOPER.
000000     MOVE ADVF-NOMBRE1     TO SORT-NOMBRE1.
000000     MOVE ADVF-DIRECC1     TO SORT-DIRECC1.
000000     MOVE ADVF-INDTASESP   TO SORT-INDTASESP.
000000*
000000     MOVE ADVF-FCHLIQ      TO ATC-FCHCAPT-BCD.
000000     MOVE R-AT-FCHCAPT-BCD TO AT-FCHCAPT-ED.
000000     MOVE AT-FCHCAPT-ED    TO SORT-FCHLIQ.
000000*
000000     MOVE ADVF-RUTNRO1   TO ATR-NRUT-BCD
000000     MOVE R-AT-NRUT-ID   TO AT-NRORUT-ED
000000     MOVE AT-NRORUT-ED   TO SORT-RUTNRO1.
000000     MOVE ADVF-RUTDIG1   TO SORT-RUTDIG1.
000000*
000000     RELEASE R-SORT.
000000     ADD 1 TO EE-DOCVEN-SORTEADOS.
000000*
000000*
000000 EZZ-FIN-DE-SECCION.
000000     EXIT.
000000     EJECT
000000*
000000*            MODULO DE EMISION DEL INFORME
000000*
000000 F-LISTAR-REGISTROS SECTION.
000000*
000000 FA-CONTROL.
000000     PERFORM FB-LEER-SORT
000000     PERFORM FC-CTROL-X-OFICINA UNTIL
000000        EF-EOF-SORT = 1
000000     PERFORM FL-TOTALES-FINALES
000000     GO TO FZZ-FIN-DE-SECCION.
000000*
000000 FB-LEER-SORT.
000000     RETURN F-SORT AT END MOVE +1 TO EF-EOF-SORT.
000000*
000000 FC-CTROL-X-OFICINA.
000000     MOVE SORT-OFICAPT           TO EF-OFI-RESPALDO
000000* BUSCA OFICINA EN TABLA
000000     MOVE 'BUSCAR'               TO OFICAREA-FUNCION
000000     MOVE EF-OFI-RESPALDO        TO OFICAREA-CODOF
000000     CALL 'RUTUOFIC' USING RTNWOFIC
000000* INICIALIZA VARIABLES DE CODIGO Y NOMBRE DE OFICINA
000000     MOVE OFICAREA-NOMOF         TO DESCRIP-OFIC-T1A
000000     PERFORM FD-CTROL-X-RUT UNTIL
000000        SORT-OFICAPT NOT = EF-OFI-RESPALDO OR
000000        EF-EOF-SORT = 1.
000000*
000000 FD-CTROL-X-RUT.
000000     MOVE SORT-RUT           TO EF-RUT-RESPALDO
000000     MOVE SORT-RUTNRO1       TO NRO-RUT-T3
000000     MOVE SORT-RUTDIG1       TO DV-RUT-T3
000000     MOVE SORT-NOMBRE1       TO NOM-CLIENTE-T2
000000     PERFORM FE-CTROL-X-OPERACION UNTIL
000000        SORT-OFICAPT NOT = EF-OFI-RESPALDO OR
000000        SORT-RUT     NOT = EF-RUT-RESPALDO OR
000000        EF-EOF-SORT = 1
000000     PERFORM FK-TOTALES-X-RUT.
000000*
000000 FE-CTROL-X-OPERACION.
000000     MOVE SORT-CODOPER TO EF-OPE-RESPALDO
000000     SEARCH ALL TBL-CODOPER
000000        AT END
000000           MOVE 'SIN CODIGO OPERACION EN TABLA' TO DESCRIP-OPE-T8
000000        WHEN CODOPER-TBL (COPX1) = SORT-CODOPER
000000           MOVE NOMOPER-TBL (COPX1) TO DESCRIP-OPE-T8.
000000     IF LARGO-PAG
000000        PERFORM FJ-TITULOS.
000000     WRITE FDOR01-IMPR FROM SF-T8 AFTER 2
000000     ADD 2 TO EX-CONTALIN
000000     PERFORM FF-GENERAR-INFORME UNTIL
000000        SORT-OFICAPT NOT = EF-OFI-RESPALDO OR
000000        SORT-RUT     NOT = EF-RUT-RESPALDO OR
000000        SORT-CODOPER NOT = EF-OPE-RESPALDO OR
000000        EF-EOF-SORT = 1
000000     MOVE SPACES TO FDOR01-IMPR
000000     WRITE FDOR01-IMPR AFTER 1
000000     ADD 1 TO EX-CONTALIN.
000000*
000000 FF-GENERAR-INFORME.
000000     IF LARGO-PAG
000000        PERFORM FJ-TITULOS.
000000     PERFORM FH-FORMATEAR-REGISTRO.
000000     WRITE FDOR01-IMPR FROM SF-LD AFTER 1
000000     ADD 1 TO EF-DOCVEN-IMPRESOS
000000              EX-CONTALIN
000000              EF-CONTAOPER
000000     PERFORM FB-LEER-SORT.
000000*
000000 FH-FORMATEAR-REGISTRO.
000000     MOVE SORT-NUMDOCTO      TO NUM-DOCTO-LD
000000     MOVE SORT-AACAPT        TO AA-CAP-LD
000000     MOVE SORT-MMCAPT        TO MM-CAP-LD
000000     MOVE SORT-DDCAPT        TO DD-CAP-LD
000000     MOVE SORT-AAVCTO        TO AA-VEN-LD
000000     MOVE SORT-MMVCTO        TO MM-VEN-LD
000000     MOVE SORT-DDVCTO        TO DD-VEN-LD
000000     MOVE SORT-CAPITAL       TO CAPITAL-LD
000000     MOVE ZEROES               TO REAJUSTE-LD
000000     MOVE SORT-TASA          TO TASA-LD
000000     MOVE SORT-INTERES       TO INTERES-LD
000000     COMPUTE VALOR-A-PAGAR = SORT-CAPITAL +
000000                                    SORT-INTERES
000000     MOVE VALOR-A-PAGAR      TO CAPIT-TOT-LD
000000     ADD SORT-CAPITAL        TO EF-CAPITAL
000000     ADD SORT-INTERES        TO EF-INTERES
000000     ADD VALOR-A-PAGAR       TO EF-CAPITFINAL.
000000*
000000 FJ-TITULOS.
000000     ADD 1 TO EX-CONTAPAG
000000     MOVE EX-CONTAPAG TO NRO-PAG-T1
000000     WRITE FDOR01-IMPR FROM SF-T1  AFTER CANAL-1
000000     WRITE FDOR01-IMPR FROM SF-T1A AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-T2  AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-T3  AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-T4  AFTER 3
000000     WRITE FDOR01-IMPR FROM SF-S1  AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-T5  AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-S2  AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-S3  AFTER 3
000000     WRITE FDOR01-IMPR FROM SF-T6  AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-T7  AFTER 1
000000     WRITE FDOR01-IMPR FROM SF-S3  AFTER 1
000000     MOVE 16 TO EX-CONTALIN.
000000*
000000 FK-TOTALES-X-RUT.
000000     MOVE 'TOTALES CLIENTE: ' TO GLOSA-LT1
000000     MOVE EF-CONTAOPER        TO CANT-OPERAC-LT1
000000     MOVE EF-CAPITAL          TO CAPITAL-LT1
000000     MOVE EF-INTERES          TO INTERES-LT1
000000     MOVE EF-REAJUSTE         TO REAJUSTE-LT2
000000     MOVE EF-CAPITFINAL       TO CAPIT-TOT-LT2
000000     ADD  EF-CONTAOPER        TO EF-CONTAOPER-BCO
000000     ADD  EF-CAPITAL          TO EF-CAPITAL-BCO
000000     ADD  EF-INTERES          TO EF-INTERES-BCO
000000     ADD  EF-REAJUSTE         TO EF-REAJUSTE-BCO
000000     ADD  EF-CAPITFINAL       TO EF-CAPITFINAL-BCO
000000     MOVE +0                  TO EF-CONTAOPER
000000                                 EF-CAPITAL
000000                                 EF-INTERES
000000                                 EF-REAJUSTE
000000                                 EF-CAPITFINAL
000000     WRITE FDOR01-IMPR FROM SF-LT1 AFTER 2
000000     WRITE FDOR01-IMPR FROM SF-LT2 AFTER 1
000000     MOVE 99 TO EX-CONTALIN.
000000*
000000 FL-TOTALES-FINALES.
000000     MOVE 'TOTALES BANCO  : ' TO GLOSA-LT1
000000     MOVE EF-CONTAOPER-BCO    TO CANT-OPERAC-LT1
000000     MOVE EF-CAPITAL-BCO      TO CAPITAL-LT1
000000     MOVE EF-INTERES-BCO      TO INTERES-LT1
000000     MOVE EF-REAJUSTE-BCO     TO REAJUSTE-LT2
000000     MOVE EF-CAPITFINAL-BCO   TO CAPIT-TOT-LT2
000000     MOVE +0                  TO EF-CONTAOPER-BCO
000000                                 EF-CAPITAL-BCO
000000                                 EF-INTERES-BCO
000000                                 EF-REAJUSTE-BCO
000000                                 EF-CAPITFINAL-BCO
000000     WRITE FDOR01-IMPR FROM SF-LT1 AFTER 2
000000     WRITE FDOR01-IMPR FROM SF-LT2 AFTER 1
000000     MOVE 3 TO EX-CONTALIN.
000000*
000000 FZZ-FIN-DE-SECCION.
000000     EXIT.
000000     EJECT

