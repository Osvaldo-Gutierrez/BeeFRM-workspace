000000 IDENTIFICATION DIVISION.
000000*========================
000000 PROGRAM-ID. DAPPLSDO.
000000 AUTHOR.     P CONCHA L.
000000*REMARKS.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
000000******************************************************************
000000*  SISTEMA    : DEPOSITOS A PLAZO
000000*  SUB-SISTEMA: INFORMES DAP
000000*  ANALISTA   : P CONCHA L.
000000*  OBJETIVO :
000000*           - GENERAR INFORME CON SALDOS DIARIOS DE DAP, A PARTIR
000000*             DEL ARCHIVO DE CUADRATURA.
000000*
000000*  INPUT    :
000000*             DAPDCUA
000000*  OUTPUT   :
000000*             INFORME DE SALDOS.
000000*
000000*  FECHA    : SEP/93.
000000*
000000***************************************************************
000000* MANTENCION:  P CONCHA L.   OCT-93
000000*
000000* -SE CAMBIA LA GLOSA (TITULO) PARA LOS DOCUMENTOS NO VIGENTES)
000000* -SE INCORPORA LA IMPRESION DE LA CUENTA CONTABLE EN LISTADO.
000000*
000000***************************************************************
000000* MANTENCION:  P CONCHA L.   09/12/93
000000*
000000* -SE INCORPORA FORMATO COPY DEL ARCHIVO DAPDCUA
000000*
000000***************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
000000 ENVIRONMENT DIVISION.
000000*=====================
000000     SKIP1
000000 CONFIGURATION SECTION.
000000*----------------------
000000 SPECIAL-NAMES.
000000     C01 IS CANAL-1
000000     DECIMAL-POINT IS COMMA.
000000     SKIP1
000000*
000000 INPUT-OUTPUT SECTION.
000000*=====================
000000 FILE-CONTROL.
000000     SELECT DAPDCUA        ASSIGN TO DAPDCUA.
000000     SELECT DAPLSDO        ASSIGN TO DAPLSDO.
000000     SELECT FSORT          ASSIGN TO SORTWK1.
000000
000000 DATA DIVISION.
000000*==============
000000*
000000 FILE SECTION.
000000*=============
000000*
      *
      ******************************************************************
      * ARCHIVO: DAPDCUA    TIPO: SAM         REGISTRO: FIJO
      *
      * ARCHIVO DE DATOS DE SALDOS PARA CUADRATURA
      *
      * LARGO REGISTRO             150 BYTES
      ******************************************************************
      *
      *
       FD  DAPDCUA
           RECORDING MODE IS F
           LABEL RECORD STANDARD
           BLOCK CONTAINS    0 RECORDS.
       01  REG-DAPDCUA.
           05 DAPDCUA-COD-OFIC           PIC X(03).
           05 DAPDCUA-COD-TPCC           PIC X(03).
           05 DAPDCUA-COD-VCAM           PIC X(06).
           05 DAPDCUA-COD-COPD.
              10 DAPDCUA-COD-TOPD        PIC X(03).
              10 DAPDCUA-COD-AOPD        PIC X(03).
           05 DAPDCUA-COD-PLZO           PIC X(02).
           05 DAPDCUA-COD-CTAC           PIC X(09).
           05 DAPDCUA-OPE-SALD           PIC S9(13)V9(02) COMP-3.
           05 DAPDCUA-OPE-NUME           PIC S9(11)       COMP-3.
           05 DAPDCUA-GLS-OFIC           PIC X(19).
           05 DAPDCUA-ABR-VCAM           PIC X(05).
           05 DAPDCUA-GLS-VCAM           PIC X(20).
           05 DAPDCUA-GLS-COPD           PIC X(24).
           05 DAPDCUA-GLS-PLZO           PIC X(10).
           05 DAPDCUA-FEC-REFE.
              10 DAPDCUA-FEC-REFE-DD     PIC 9(02).
              10 DAPDCUA-FEC-REFE-MM     PIC 9(02).
              10 DAPDCUA-FEC-REFE-SS     PIC 9(02).
              10 DAPDCUA-FEC-REFE-AA     PIC 9(02).
           05 DAPDCUA-COD-MMCC           PIC X(02).
           05 DAPDCUA-OPE-CAPT           PIC S9(13)V9(04) COMP-3.
           05 FILLER                     PIC X(10).
      *
000000*
000000 FD  DAPLSDO
000000     RECORDING MODE IS F
000000     RECORD CONTAINS 132 CHARACTERS
000000     BLOCK CONTAINS    0 RECORDS.
000000 01  REG-DAPLSDO          PIC X(132).
000000*
000000 SD  FSORT.
000000 01  REG-FSORT.
000000     05 FSORT-COD-OFIC           PIC X(03).
000000     05 FSORT-COD-TPCC           PIC X(03).
000000     05 FSORT-COD-VCAM           PIC X(06).
000000     05 FSORT-COD-COPD.
000000        10 FSORT-COD-TOPD        PIC X(03).
000000        10 FSORT-COD-AOPD        PIC X(03).
000000     05 FSORT-COD-PLZO           PIC X(02).
000000     05 FSORT-COD-CTAC           PIC X(09).
000000     05 FSORT-OPE-SALD           PIC S9(13)V9(02) COMP-3.
000000     05 FSORT-OPE-NUME           PIC S9(11)       COMP-3.
000000     05 FSORT-GLS-OFIC           PIC X(19).
000000     05 FSORT-ABR-VCAM           PIC X(05).
000000     05 FSORT-GLS-VCAM           PIC X(20).
000000     05 FSORT-GLS-COPD           PIC X(24).
000000     05 FSORT-GLS-PLZO           PIC X(10).
000000     05 FSORT-FEC-REFE.
000000        10 FSORT-FEC-REFE-DD     PIC 9(02).
000000        10 FSORT-FEC-REFE-MM     PIC 9(02).
000000        10 FSORT-FEC-REFE-SS     PIC 9(02).
000000        10 FSORT-FEC-REFE-AA     PIC 9(02).
000000     05 FSORT-COD-MMCC           PIC X(02).
000000     05 FSORT-OPE-CAPT           PIC S9(13)V9(04) COMP-3.
000000     05 FILLER                   PIC X(10).
000000*
000000     EJECT
000000 WORKING-STORAGE SECTION.
000000*
      *
       01  BIBBAUD1.
           05 FILLER          PIC X(20) VALUE 'VARIABLES DE AUDIT'.
           05 BIB-MODULO      PIC X(08) VALUE '[MODNAME'.
           05 BIB-PROGRA      PIC X(15) VALUE '[PROGRAMMERNAME'.
           05 BIB-NIVEL       PIC X(05) VALUE '[LVNO'.
           05 BIB-FECHA-CATA  PIC X(08) VALUE '[DATEUPD'.
           05 BIB-MASTER      PIC X(44) VALUE
              '[DATA-SET-NAME-FOR-THE-LIBRARIAN-MASTER-FILE'.
           05 BIB-FECHA-EJEC  PIC 9(06).
           05 BIB-HORA-EJEC   PIC 9(08).
      *
000000*
000000 01  WE-EXTERNAS.
000000     05 WE-FECHA-DATE.
000000        10 WE-AA-DATE          PIC 9(02).
000000        10 WE-MM-DATE          PIC 9(02).
000000        10 WE-DD-DATE          PIC 9(02).
000000*
000000 01  W-CONTADORES.
000000     03 CNT-LINE-DAPLSDO       PIC 9(3)  VALUE 99.
000000        88 SALTO-PAG-DAPLSDO            VALUE 60 THRU 99.
000000     03 CNT-PAGE-DAPLSDO       PIC 9(3)  VALUE 0.
000000     03 CNT-DAPDCUA-L          PIC 9(08) VALUE 0.
000000     03 CNT-FSORT-G            PIC 9(08) VALUE 0.
000000     03 CNT-FSORT-L            PIC 9(08) VALUE 0.
000000*
000000 01  W-REGISTROS-DE-TRABAJO.
000000     03 SW-CANCEL               PIC 9(01) VALUE 0.
000000     03 FS-DAPDCUA                    PIC 9(01) VALUE 0.
000000     03 FS-FSORT                      PIC 9(01) VALUE 0.
000000     03 X-BANC                        PIC 9(3) VALUE 0.
000000
000000     03 WR-COD-OFIC                   PIC X(03) VALUE SPACES.
000000     03 WR-COD-TPCC                   PIC X(03) VALUE SPACES.
000000     03 WR-COD-VCAM                   PIC X(06) VALUE SPACES.
000000     03 WR-COD-COPD.
000000        05 WR-COD-TOPD                PIC X(03) VALUE SPACES.
000000        05 WR-COD-AOPD                PIC X(03) VALUE SPACES.
000000     03 WR-COD-PLZO                   PIC X(02) VALUE SPACES.
000000*
000000 01  W-TB-BANCO.
000000        10 WS-BANCO-AUX.
000000           15 WS-BANCO-KEY-AUX.
000000              20 WS-COD-TPCC-AUX      PIC X(03).
000000              20 WS-COD-VCAM-AUX      PIC X(06).
000000              20 WS-COD-COPD-AUX      PIC X(06).
000000              20 WS-COD-PLZO-AUX      PIC X(02).
000000           15 WS-GLS-VCAM-AUX         PIC X(20).
000000           15 WS-GLS-COPD-AUX         PIC X(25).
000000           15 WS-GLS-PLZO-AUX         PIC X(10).
000000           15 WS-BANC-NUME-AUX        PIC S9(11)    COMP-3.
000000           15 WS-BANC-SALD-AUX        PIC S9(13)V99 COMP-3.
000000           15 WS-COD-CTAC-AUX         PIC X(09).
000000     05 WB-LARGO-BAN                PIC 9(03)  VALUE 0.
000000     05 WB-TABLA-BANCO  OCCURS 0 TO 500 TIMES
000000                        DEPENDING ON WB-LARGO-BAN
000000                        INDEXED BY I-BANC
000000                                   Y-BANC.
000000        10 WB-BANCO.
000000           15 WB-BANCO-KEY.
000000              20 WB-COD-TPCC           PIC X(03).
000000              20 WB-COD-VCAM           PIC X(06).
000000              20 WB-COD-COPD           PIC X(06).
000000              20 WB-COD-PLZO           PIC X(02).
000000           15 WB-GLS-VCAM              PIC X(20).
000000           15 WB-GLS-COPD              PIC X(25).
000000           15 WB-GLS-PLZO              PIC X(10).
000000           15 WB-BANC-NUME             PIC S9(11)    COMP-3.
000000           15 WB-BANC-SALD             PIC S9(13)V99 COMP-3.
000000           15 WB-COD-CTAC              PIC X(09).
000000*
000000 01  ACUMULADORES.
000000     05 ACM-TPCC.
000000        10 ACM-TPCC-NUME          PIC S9(11)    COMP-3 VALUE 0.
000000        10 ACM-TPCC-SALD          PIC S9(13)V99 COMP-3 VALUE 0.
000000     05 ACM-VCAM.
000000        10 ACM-VCAM-NUME          PIC S9(11)    COMP-3 VALUE 0.
000000        10 ACM-VCAM-SALD          PIC S9(13)V99 COMP-3 VALUE 0.
000000     05 ACM-TOPD.
000000        10 ACM-TOPD-NUME          PIC S9(11)    COMP-3 VALUE 0.
000000        10 ACM-TOPD-SALD          PIC S9(13)V99 COMP-3 VALUE 0.
000000     05 ACM-COPD.
000000        10 ACM-COPD-NUME          PIC S9(11)    COMP-3 VALUE 0.
000000        10 ACM-COPD-SALD          PIC S9(13)V99 COMP-3 VALUE 0.
000000*
000000     EJECT
000000******************************************************************
000000*             TITULOS Y LINEAS DEL LISTADO                       *
000000******************************************************************
000000*                                                                *
000000 01  WR-LINEAS-TITULO.
000000     05 WR-LINEA01.
000000        10 FILLER           PIC X(018) VALUE 'BANCO CREDITO INVE'.
000000        10 FILLER           PIC X(018) VALUE 'RSIONES         PG'.
000000        10 FILLER           PIC X(012) VALUE 'M : DAPPLSDO'.
000000        10 FILLER           PIC X(018) VALUE '                 F'.
000000        10 FILLER           PIC X(005) VALUE 'ECHA:'.
000000        10 WR-LA01-DD       PIC 99.
000000        10 FILLER           PIC X(001) VALUE ALL '/'.
000000        10 WR-LA01-MM       PIC 99.
000000        10 FILLER           PIC X(001) VALUE ALL '/'.
000000        10 WR-LA01-SS       PIC 99.
000000        10 WR-LA01-AA       PIC 99.
000000     05 WR-LINEA02.
000000        10 FILLER           PIC X(017) VALUE 'DEPOSITOS A PLAZO'.
000000        10 FILLER           PIC X(048) VALUE ALL ' '.
000000        10 FILLER           PIC X(007) VALUE 'PAG. :'.
000000        10 WR-LA02-PAG      PIC ZZ9.
000000     05 WR-LINEA03.
000000        10 FILLER           PIC X(009) VALUE 'OFICINA:'.
000000        10 WR-LA03-OFICINA  PIC X(030).
000000     05 WR-LINEA04.
000000        10 FILLER           PIC X(023) VALUE ALL ' '.
000000        10 FILLER           PIC X(018) VALUE 'TOTALES CARTERA DA'.
000000        10 FILLER           PIC X(008) VALUE 'P  AL :'.
000000        10 WR-LA04-DD       PIC 99.
000000        10 FILLER           PIC X(001) VALUE ALL '/'.
000000        10 WR-LA04-MM       PIC 99.
000000        10 FILLER           PIC X(001) VALUE ALL '/'.
000000        10 WR-LA04-SS       PIC 99.
000000        10 WR-LA04-AA       PIC 99.
000000        10 FILLER           PIC X(005) VALUE SPACES.
000000        10 WR-LA04-TIPO     PIC X(020) VALUE SPACES.
000000     05 WR-LINEA05.
000000        10 FILLER           PIC X(023) VALUE ALL ' '.
000000        10 FILLER           PIC X(036) VALUE ALL '='.
000000     05 WR-LINEA06.
000000        10 FILLER           PIC X(097) VALUE ALL '-'.
000000     05 WR-LINEA07.
000000        10 FILLER           PIC X(018) VALUE ' O P E R A C I O N'.
000000        10 FILLER           PIC X(018) VALUE ' E S'.
000000        10 FILLER           PIC X(005) VALUE 'PLAZO'.
000000        10 FILLER           PIC X(017) VALUE ALL ' '.
000000        10 FILLER           PIC X(005) VALUE 'SALDO'.
000000        10 FILLER           PIC X(015) VALUE '          CANT.'.
000000        10 FILLER           PIC X(008) VALUE ALL SPACES.
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 FILLER           PIC X(009) VALUE '  CUENTA '.
000000     05 WR-LINEA08.
000000        10 FILLER           PIC X(057) VALUE ALL ' '.
000000        10 FILLER           PIC X(007) VALUE 'CAPITAL'.
000000        10 FILLER           PIC X(014) VALUE '        DOCTOS'.
000000        10 FILLER           PIC X(001) VALUE ALL '.'.
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 FILLER           PIC X(005) VALUE ALL ' '.
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 FILLER           PIC X(009) VALUE 'CONTABLE '.
000000     05 WR-SPACES           PIC X(132) VALUE SPACES.
000000
000000*------------>   IDEM A WR-LINEA06
000000     05 WR-LINEA09.
000000        10 FILLER           PIC X(007) VALUE 'MONEDA:'.
000000        10 WR-LA09-MONEDA   PIC X(020).
000000     05 WR-LINEA10.
000000        10 FILLER           PIC X(022) VALUE ALL '-'.
000000     05 WR-LINEA11.
000000        10 WR-LA11-COD-COPD PIC X(006).
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 WR-LA11-GLS-COPD PIC X(024).
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 WR-LA11-COD-PLZO PIC X(002).
000000        10 FILLER           PIC X(001) VALUE ALL ' '.
000000        10 WR-LA11-GLS-PLZO PIC X(010).
000000        10 FILLER           PIC X(003) VALUE ALL ' '.
000000        10 WR-LA11-SALD-CAP PIC ZZZ.ZZZ.ZZZ.ZZ9,99.
000000        10 FILLER           PIC X(004) VALUE ALL ' '.
000000        10 WR-LA11-NUM-DOCT PIC ZZZ.ZZ9.
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 WR-LA11-COD-MON  PIC X(005).
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 WR-LA11-COD-CTAC PIC X(009).
000000     05 WR-LINEA12.
000000        10 FILLER           PIC X(016) VALUE 'TOTAL OPERACION:'.
000000        10 WR-LA12-TOPD     PIC X(003).
000000        10 FILLER           PIC X(028) VALUE ALL ' '.
000000        10 WR-LA12-SALD-CAP PIC ZZ.ZZZ.ZZZ.ZZZ.ZZ9,99.
000000        10 FILLER           PIC X(004) VALUE ALL ' '.
000000        10 WR-LA12-NUM-DOCT PIC ZZZ.ZZ9.
000000     05 WR-LINEA13.
000000        10 FILLER           PIC X(013) VALUE 'TOTAL MONEDA:'.
000000        10 WR-LA13-MONEDA   PIC X(020).
000000        10 FILLER           PIC X(013) VALUE ALL ' '.
000000        10 WR-LA13-SALD-CAP PIC ZZZ.ZZZ.ZZZ.ZZZ.ZZ9,99.
000000        10 FILLER           PIC X(002) VALUE ALL ' '.
000000        10 WR-LA13-NUM-DOCT PIC Z.ZZZ.ZZ9.
000000     05 WR-LINEA14.
000000        10 FILLER           PIC X(014) VALUE 'TOTAL OFICINA:'.
000000        10 WR-LA14-OFICINA  PIC X(019).
000000        10 FILLER           PIC X(037) VALUE ALL ' '.
000000        10 WR-LA14-NUM-DOCT PIC Z.ZZZ.ZZ9.
000000*-----------------------------------------------------------------
      *
           COPY RLOVR001.
000000     EJECT
000000*
000000* MODULO DE CONTROL
000000*
000000 PROCEDURE DIVISION.
000000*
000000 A-CONTROL SECTION.
      *
      * DESPLIEGA VARIABLES DE AUDIT
           DISPLAY '***********************************************'.
           DISPLAY '***   IDENTIFICACION DE MODULO EJECUTADO    ***'.
           DISPLAY 'MASTER FILE               = ' BIB-MASTER.
           DISPLAY 'NOMBRE DEL MODULO         = ' BIB-MODULO.
           DISPLAY 'NUMERO DE VERSION         = ' BIB-NIVEL.
           DISPLAY 'FECHA DE CATALOGACION     = ' BIB-FECHA-CATA.
           DISPLAY 'NOMBRE DEL PROGRAMADOR    = ' BIB-PROGRA.
           DISPLAY ' '.
           DISPLAY '***   IDENTIFICACION DE EJECUCION           ***'.
           DISPLAY '*    (FECHA Y HORA DEL COMPUTADOR)            *'.
           ACCEPT BIB-FECHA-EJEC FROM DATE
           ACCEPT BIB-HORA-EJEC  FROM TIME
           DISPLAY 'FECHA EJECUCION           = ' BIB-FECHA-EJEC.
           DISPLAY 'HORA EJECUCION (HHMMSSDD) = ' BIB-HORA-EJEC.
           DISPLAY '***                                         ***'.
           DISPLAY '***********************************************'.
      *
000000*
000000 AA-INICIALIZACION.
000000     PERFORM B-INICIALIZAR.
000000*
000000 AB-PROCESAMIENTO.
000000     PERFORM C-PROCESAR.
000000*
000000 AC-TERMINACION.
000000     PERFORM D-TERMINAR.
000000*
000000 AZZ-FIN-PROC.
000000     STOP RUN.
000000     EJECT
000000*
000000*
000000*
000000 B-INICIALIZAR SECTION.
000000*
000000 BA-CONTROL-PROCESO.
000000     PERFORM BB-ACCEPT
000000     PERFORM BD-ABRE-ARCH
000000     PERFORM BZ-VERIFICA
000000     GO TO BZZ-FIN-SECC.
000000*
000000 BB-ACCEPT.
000000* ACCEPTAR FECHA
000000     ACCEPT WE-FECHA-DATE FROM DATE
000000     MOVE WE-DD-DATE TO WR-LA01-DD
000000     MOVE WE-MM-DATE TO WR-LA01-MM
000000     MOVE WE-AA-DATE TO WR-LA01-AA
000000*****IF WE-AA-DATE NOT < 80
000000     IF WE-AA-DATE < ROLL-YY
000000        MOVE 20      TO WR-LA01-SS
000000     ELSE
000000        MOVE 19      TO WR-LA01-SS.
000000*
000000 BD-ABRE-ARCH.
000000     OPEN INPUT  DAPDCUA
000000          OUTPUT DAPLSDO.
000000*
000000 BZ-VERIFICA.
000000     IF SW-CANCEL = 1
000000        DISPLAY '*** ERRORES EN INICIALIZACION ***'
000000        PERFORM D-TERMINAR.
000000*
000000 BZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 C-PROCESAR SECTION.
000000*
000000 CA-CONTROL.
000000     SORT FSORT
000000        ON ASCENDING KEY
000000           FSORT-COD-OFIC
000000           FSORT-COD-TPCC
000000           FSORT-COD-VCAM
000000           FSORT-COD-COPD
000000           FSORT-COD-PLZO
000000              INPUT  PROCEDURE E-ENTRADA-SORT
000000              OUTPUT PROCEDURE F-SALIDA-SORT.
000000*
000000     IF SORT-RETURN NOT = 0
000000        DISPLAY '**  ####################### **'
000000        DISPLAY '**  HUBO ERROR EN SORT      **'
000000        DISPLAY '**  ####################### **'
000000        MOVE 1 TO SW-CANCEL
000000        PERFORM D-TERMINAR.
000000*
000000     PERFORM G-IMPRIME-TOTALES-BANCO.
000000*
000000 CZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 D-TERMINAR SECTION.
000000*
000000 DA-TOTALES.
000000     CLOSE DAPDCUA DAPLSDO
000000     DISPLAY '*** TOTALES DE CONTROL ***'
000000     DISPLAY ' '
000000     DISPLAY 'REG. CUADRATURA LEIDOS...........' CNT-DAPDCUA-L
000000     DISPLAY 'REG. GRABADOS AL SORT............' CNT-FSORT-G
000000     DISPLAY 'REG. LEIDOS DEL SORT.............' CNT-FSORT-L
000000     DISPLAY ' '
000000     IF SW-CANCEL = 0
000000        DISPLAY '*** TERMINO NORMAL ***'
000000     ELSE
000000        MOVE 100 TO RETURN-CODE
000000        DISPLAY '*** PROCESO CANCELADO ***'
000000        DISPLAY '*** PROCESO CANCELADO ***'
000000        DISPLAY '*** PROCESO CANCELADO ***'
000000        STOP RUN.
000000*
000000 DZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 E-ENTRADA-SORT SECTION.
000000*
000000 EA-LEER-DAPDCUA.
000000     READ DAPDCUA AT END MOVE 1 TO FS-DAPDCUA.
000000     IF FS-DAPDCUA = 0
000000        ADD 1 TO CNT-DAPDCUA-L.
000000*
000000 EB-CTRL-PROC.
000000     PERFORM EC-GRABA-FSORT UNTIL
000000        FS-DAPDCUA = 1
000000     GO TO EZZ-FIN-SECC.
000000*
000000 EC-GRABA-FSORT.
000000     MOVE DAPDCUA-COD-OFIC    TO FSORT-COD-OFIC
000000     MOVE DAPDCUA-COD-TPCC    TO FSORT-COD-TPCC
000000     MOVE DAPDCUA-COD-VCAM    TO FSORT-COD-VCAM
000000     MOVE DAPDCUA-COD-COPD    TO FSORT-COD-COPD
000000     MOVE DAPDCUA-COD-PLZO    TO FSORT-COD-PLZO
000000     MOVE DAPDCUA-COD-CTAC    TO FSORT-COD-CTAC
000000     MOVE DAPDCUA-OPE-SALD    TO FSORT-OPE-SALD
000000     MOVE DAPDCUA-OPE-NUME    TO FSORT-OPE-NUME
000000     MOVE DAPDCUA-GLS-OFIC    TO FSORT-GLS-OFIC
000000     MOVE DAPDCUA-ABR-VCAM    TO FSORT-ABR-VCAM
000000     MOVE DAPDCUA-GLS-VCAM    TO FSORT-GLS-VCAM
000000     MOVE DAPDCUA-GLS-COPD    TO FSORT-GLS-COPD
000000     MOVE DAPDCUA-GLS-PLZO    TO FSORT-GLS-PLZO
000000     MOVE DAPDCUA-FEC-REFE    TO FSORT-FEC-REFE
000000     MOVE DAPDCUA-COD-MMCC    TO FSORT-COD-MMCC
000000     RELEASE REG-FSORT
000000     ADD 1                    TO CNT-FSORT-G
000000     PERFORM EA-LEER-DAPDCUA.
000000*
000000 EZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 F-SALIDA-SORT SECTION.
000000*
000000 FA-LEE-SORT.
000000     RETURN FSORT AT END MOVE 1 TO FS-FSORT.
000000     IF FS-FSORT = 0
000000        ADD 1 TO CNT-FSORT-L.
000000*
000000 FB-CTRL-PROC.
000000     MOVE FSORT-FEC-REFE-DD  TO WR-LA04-DD
000000     MOVE FSORT-FEC-REFE-MM  TO WR-LA04-MM
000000     MOVE FSORT-FEC-REFE-AA  TO WR-LA04-AA
000000     MOVE FSORT-FEC-REFE-SS  TO WR-LA04-SS
000000     INITIALIZE ACM-TOPD
000000     PERFORM FC-CTRL-X-OFIC UNTIL
000000        FS-FSORT = 1
000000     GO TO FZZ-FIN-SECC.
000000*
000000 FC-CTRL-X-OFIC.
000000     MOVE 99                TO CNT-LINE-DAPLSDO
000000     MOVE 0                 TO CNT-PAGE-DAPLSDO
000000     MOVE FSORT-COD-OFIC    TO WR-COD-OFIC
000000     MOVE FSORT-GLS-OFIC    TO WR-LA03-OFICINA
000000                               WR-LA14-OFICINA
000000     PERFORM FD-CTRL-X-TPCC UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FS-FSORT = 1.
000000*
000000 FD-CTRL-X-TPCC.
000000     MOVE 99                     TO CNT-LINE-DAPLSDO
000000     MOVE 0                      TO CNT-PAGE-DAPLSDO
000000     INITIALIZE ACM-TPCC
000000     MOVE FSORT-COD-TPCC         TO WR-COD-TPCC
000000     IF WR-COD-TPCC = 'CAP'
000000        MOVE '(VIGENTES)         '     TO WR-LA04-TIPO.
000000     IF WR-COD-TPCC = 'DVE'
000000        MOVE '(VENC. NO COBRADOS)'     TO WR-LA04-TIPO.
000000     PERFORM FE-CTRL-X-VCAM UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-COD-TPCC NOT = WR-COD-TPCC OR
000000              FS-FSORT = 1
000000     PERFORM FD9-TOTALES-TPCC.
000000*
000000 FD9-TOTALES-TPCC.
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM FV-TITULOS.
000000     MOVE ACM-TPCC-NUME     TO WR-LA14-NUM-DOCT
000000     WRITE REG-DAPLSDO FROM WR-LINEA14 AFTER 3
000000     ADD 3                  TO CNT-LINE-DAPLSDO.
000000*
000000 FE-CTRL-X-VCAM.
000000     INITIALIZE ACM-VCAM
000000     MOVE FSORT-COD-VCAM    TO WR-COD-VCAM
000000     MOVE FSORT-GLS-VCAM    TO WR-LA09-MONEDA
000000                               WR-LA13-MONEDA
000000     MOVE FSORT-ABR-VCAM    TO WR-LA11-COD-MON
000000     PERFORM FE1-GRABAR-LINEA-VCAM
000000     PERFORM FF-CTRL-X-COPD UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-COD-TPCC NOT = WR-COD-TPCC OR
000000              FSORT-COD-VCAM NOT = WR-COD-VCAM OR
000000                 FS-FSORT = 1
000000     PERFORM FE9-TOTALES-VCAM.
000000*
000000 FE1-GRABAR-LINEA-VCAM.
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM FV-TITULOS.
000000     WRITE REG-DAPLSDO FROM WR-LINEA09 AFTER 2
000000     WRITE REG-DAPLSDO FROM WR-LINEA10 AFTER 1
000000     ADD 3                  TO CNT-LINE-DAPLSDO.
000000*
000000 FE9-TOTALES-VCAM.
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM FV-TITULOS.
000000     MOVE ACM-VCAM-NUME     TO WR-LA13-NUM-DOCT
000000     MOVE ACM-VCAM-SALD     TO WR-LA13-SALD-CAP
000000     WRITE REG-DAPLSDO FROM WR-LINEA13 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-SPACES  AFTER 1
000000     ADD 2                       TO CNT-LINE-DAPLSDO.
000000*
000000 FF-CTRL-X-COPD.
000000     MOVE FSORT-COD-COPD         TO WR-COD-COPD
000000                                    WR-LA11-COD-COPD
000000                                    WR-LA12-TOPD
000000     MOVE FSORT-GLS-COPD         TO WR-LA11-GLS-COPD
000000     PERFORM FG-CTRL-X-PLZO UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-COD-TPCC NOT = WR-COD-TPCC OR
000000              FSORT-COD-VCAM NOT = WR-COD-VCAM OR
000000                 FSORT-COD-COPD NOT = WR-COD-COPD OR
000000                    FS-FSORT = 1
000000     PERFORM FF9-TOTALES-COPD.
000000*
000000 FF9-TOTALES-COPD.
000000     IF FSORT-COD-TOPD NOT = WR-COD-TOPD OR
000000        FSORT-COD-VCAM NOT = WR-COD-VCAM OR
000000           FSORT-COD-TPCC NOT = WR-COD-TPCC OR
000000              FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000                 FS-FSORT = 1
000000                    PERFORM FF91-TOTALES-TOPD
000000                    INITIALIZE ACM-TOPD.
000000*
000000 FF91-TOTALES-TOPD.
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM FV-TITULOS.
000000     MOVE ACM-TOPD-NUME          TO WR-LA12-NUM-DOCT
000000     MOVE ACM-TOPD-SALD          TO WR-LA12-SALD-CAP
000000     WRITE REG-DAPLSDO FROM WR-LINEA06 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA12 AFTER 1
000000     ADD 2                       TO CNT-LINE-DAPLSDO.
000000*
000000 FG-CTRL-X-PLZO.
000000     INITIALIZE ACM-COPD
000000     MOVE FSORT-COD-PLZO         TO WR-COD-PLZO
000000     MOVE FSORT-COD-PLZO         TO WR-LA11-COD-PLZO
000000     MOVE FSORT-GLS-PLZO         TO WR-LA11-GLS-PLZO
000000     MOVE FSORT-COD-CTAC         TO WR-LA11-COD-CTAC
000000     PERFORM FJ-ACUMULA-COPD UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-COD-TPCC NOT = WR-COD-TPCC OR
000000              FSORT-COD-VCAM NOT = WR-COD-VCAM OR
000000                 FSORT-COD-COPD NOT = WR-COD-COPD OR
000000                    FSORT-COD-TOPD NOT = WR-COD-TOPD OR
000000                       FSORT-COD-PLZO NOT = WR-COD-PLZO OR
000000                          FS-FSORT = 1
000000     PERFORM FJ1-IMPRIME-COPD
000000     PERFORM FK-ACUMULA-BANCO.
000000*
000000 FJ-ACUMULA-COPD.
000000*  ACUMULA CANTIDADES Y MONTOS MIENTRAS SEA LA MISMA:
000000*  OFIC, TPCC, VCAM, COPD Y PLZO.
000000     ADD FSORT-OPE-SALD          TO ACM-TPCC-SALD
000000                                    ACM-VCAM-SALD
000000                                    ACM-TOPD-SALD
000000                                    ACM-COPD-SALD
000000     ADD FSORT-OPE-NUME          TO ACM-TPCC-NUME
000000                                    ACM-VCAM-NUME
000000                                    ACM-TOPD-NUME
000000                                    ACM-COPD-NUME
000000     PERFORM FA-LEE-SORT.
000000*
000000 FJ1-IMPRIME-COPD.
000000** GENERA LINEA DETALLE PARA OPERACION.
000000     MOVE ACM-COPD-NUME          TO WR-LA11-NUM-DOCT
000000     MOVE ACM-COPD-SALD          TO WR-LA11-SALD-CAP
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM FV-TITULOS.
000000     WRITE REG-DAPLSDO FROM WR-LINEA11 AFTER 1
000000     ADD 1                       TO CNT-LINE-DAPLSDO.
000000*
000000 FK-ACUMULA-BANCO.
000000     SET I-BANC TO 1
000000     SEARCH WB-TABLA-BANCO
000000        AT END
000000           PERFORM FK1-CREAR-BANCO
000000        WHEN
000000           WB-COD-TPCC (I-BANC) = WR-COD-TPCC AND
000000           WB-COD-VCAM (I-BANC) = WR-COD-VCAM AND
000000           WB-COD-COPD (I-BANC) = WR-COD-COPD AND
000000           WB-COD-PLZO (I-BANC) = WR-COD-PLZO
000000              PERFORM FK2-ACTUA-BANCO.
000000*
000000 FK1-CREAR-BANCO.
000000     ADD 1                TO WB-LARGO-BAN
000000     IF WB-LARGO-BAN > 500
000000        DISPLAY 'ERROR EN TABLA BANCO OVERFLOW > A 500'
000000        MOVE 20 TO SORT-RETURN
000000     ELSE
000000        SET I-BANC            TO WB-LARGO-BAN
000000        MOVE WR-COD-TPCC      TO WB-COD-TPCC  (I-BANC)
000000        MOVE WR-COD-VCAM      TO WB-COD-VCAM  (I-BANC)
000000        MOVE WR-COD-COPD      TO WB-COD-COPD  (I-BANC)
000000        MOVE WR-COD-PLZO      TO WB-COD-PLZO  (I-BANC)
000000        MOVE WR-LA11-GLS-PLZO TO WB-GLS-PLZO  (I-BANC)
000000        MOVE WR-LA11-GLS-COPD TO WB-GLS-COPD  (I-BANC)
000000        MOVE WR-LA09-MONEDA   TO WB-GLS-VCAM  (I-BANC)
000000        MOVE ACM-COPD-SALD    TO WB-BANC-SALD (I-BANC)
000000        MOVE ACM-COPD-NUME    TO WB-BANC-NUME (I-BANC)
000000        MOVE WR-LA11-COD-CTAC TO WB-COD-CTAC  (I-BANC).
000000*
000000 FK2-ACTUA-BANCO.
000000     ADD ACM-COPD-SALD  TO WB-BANC-SALD(I-BANC)
000000     ADD ACM-COPD-NUME  TO WB-BANC-NUME(I-BANC).
000000*
000000 FV-TITULOS.
000000     ADD 1                       TO CNT-PAGE-DAPLSDO
000000     MOVE CNT-PAGE-DAPLSDO       TO WR-LA02-PAG
000000     WRITE REG-DAPLSDO FROM WR-LINEA01 AFTER CANAL-1
000000     WRITE REG-DAPLSDO FROM WR-LINEA02 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA03 AFTER 2
000000     WRITE REG-DAPLSDO FROM WR-LINEA04 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA05 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA06 AFTER 2
000000     WRITE REG-DAPLSDO FROM WR-LINEA07 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA08 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA06 AFTER 1
000000     MOVE 11                     TO CNT-LINE-DAPLSDO.
000000*
000000 FZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 G-IMPRIME-TOTALES-BANCO SECTION.
000000*
000000 GA-CONTROL.
000000     PERFORM GB-ORDENA-TABLA-BCO
000000        VARYING X-BANC  FROM 1 BY 1 UNTIL
000000           X-BANC = WB-LARGO-BAN
000000        AFTER Y-BANC FROM 1 BY 1 UNTIL
000000           Y-BANC > WB-LARGO-BAN
000000     MOVE 'TOTALES BANCO'   TO WR-LA03-OFICINA
000000                               WR-LA14-OFICINA
000000     MOVE SPACES            TO WR-LA11-COD-MON
000000     SET I-BANC             TO 1
000000     PERFORM GC-CTRL-X-TPCC UNTIL
000000        I-BANC > WB-LARGO-BAN
000000     GO TO GZZ-FIN-SECC.
000000*
000000 GB-ORDENA-TABLA-BCO.
000000* ORDENA METODO BURBUJA
000000     IF Y-BANC = 1
000000        SET Y-BANC  TO X-BANC
000000        SET Y-BANC  UP BY 1.
000000     IF WB-BANCO-KEY (X-BANC) > WB-BANCO-KEY (Y-BANC)
000000        MOVE WB-BANCO (Y-BANC)     TO WS-BANCO-AUX
000000        MOVE WB-BANCO (X-BANC)     TO WB-BANCO (Y-BANC)
000000        MOVE WS-BANCO-AUX          TO WB-BANCO (X-BANC).
000000*
000000 GC-CTRL-X-TPCC.
000000     INITIALIZE ACM-TPCC
000000     MOVE 99                           TO CNT-LINE-DAPLSDO
000000     MOVE 0                            TO CNT-PAGE-DAPLSDO
000000     MOVE WB-COD-TPCC (I-BANC)         TO WR-COD-TPCC
000000     IF WR-COD-TPCC = 'CAP'
000000        MOVE '(VIGENTES)'              TO WR-LA04-TIPO.
000000     IF WR-COD-TPCC = 'DVE'
000000        MOVE '(VENC. NO COBRADOS)'     TO WR-LA04-TIPO.
000000     PERFORM GD-CTRL-X-VCAM UNTIL
000000        WB-COD-TPCC (I-BANC) NOT = WR-COD-TPCC OR
000000           I-BANC > WB-LARGO-BAN.
000000     PERFORM GM-TOTALES-TPCC.
000000*
000000 GD-CTRL-X-VCAM.
000000     INITIALIZE ACM-VCAM
000000     MOVE WB-COD-VCAM (I-BANC)   TO WR-COD-VCAM
000000     MOVE WB-GLS-VCAM (I-BANC)   TO WR-LA09-MONEDA
000000                                    WR-LA13-MONEDA
000000     PERFORM GE-PROCESA UNTIL
000000        WB-COD-TPCC (I-BANC) NOT = WR-COD-TPCC OR
000000           WB-COD-VCAM (I-BANC) NOT = WR-COD-VCAM OR
000000              I-BANC > WB-LARGO-BAN
000000     PERFORM GN-TOTALES-VCAM.
000000*
000000 GE-PROCESA.
000000*  ACUMULA CANTIDADES Y MONTOS MIENTRAS SEA LA MISMA MONEDA
000000     ADD WB-BANC-SALD  (I-BANC)  TO ACM-TPCC-SALD
000000                                    ACM-VCAM-SALD
000000     ADD WB-BANC-NUME  (I-BANC)  TO ACM-TPCC-NUME
000000                                    ACM-VCAM-NUME
000000     MOVE WB-COD-COPD  (I-BANC)  TO WR-LA11-COD-COPD
000000     MOVE WB-GLS-COPD  (I-BANC)  TO WR-LA11-GLS-COPD
000000     MOVE WB-COD-PLZO  (I-BANC)  TO WR-LA11-COD-PLZO
000000     MOVE WB-GLS-PLZO  (I-BANC)  TO WR-LA11-GLS-PLZO
000000     MOVE WB-BANC-SALD (I-BANC)  TO WR-LA11-SALD-CAP
000000     MOVE WB-BANC-NUME (I-BANC)  TO WR-LA11-NUM-DOCT
000000     MOVE WB-COD-CTAC  (I-BANC)  TO WR-LA11-COD-CTAC
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM GT-TITULOS.
000000     WRITE REG-DAPLSDO FROM WR-LINEA11 AFTER 1
000000     ADD 1                       TO CNT-LINE-DAPLSDO
000000     SET I-BANC UP BY 1.
000000*
000000 GM-TOTALES-TPCC.
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM GT-TITULOS.
000000     MOVE ACM-TPCC-NUME     TO WR-LA14-NUM-DOCT
000000     WRITE REG-DAPLSDO FROM WR-LINEA14 AFTER 3
000000     ADD 3                  TO CNT-LINE-DAPLSDO.
000000*
000000 GN-TOTALES-VCAM.
000000     IF SALTO-PAG-DAPLSDO
000000        PERFORM GT-TITULOS.
000000     MOVE ACM-VCAM-NUME     TO WR-LA13-NUM-DOCT
000000     MOVE ACM-VCAM-SALD     TO WR-LA13-SALD-CAP
000000     WRITE REG-DAPLSDO FROM WR-LINEA13 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-SPACES  AFTER 1
000000     ADD 2                  TO CNT-LINE-DAPLSDO.
000000*
000000 GT-TITULOS.
000000     ADD 1                  TO CNT-PAGE-DAPLSDO
000000     MOVE CNT-PAGE-DAPLSDO  TO WR-LA02-PAG
000000     WRITE REG-DAPLSDO FROM WR-LINEA01 AFTER CANAL-1
000000     WRITE REG-DAPLSDO FROM WR-LINEA02 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA03 AFTER 2
000000     WRITE REG-DAPLSDO FROM WR-LINEA04 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA05 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA06 AFTER 2
000000     WRITE REG-DAPLSDO FROM WR-LINEA07 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA08 AFTER 1
000000     WRITE REG-DAPLSDO FROM WR-LINEA06 AFTER 1
000000     MOVE 11                TO CNT-LINE-DAPLSDO.
000000*
000000 GZZ-FIN-SECC.
000000     EXIT.
000000     EJECT

