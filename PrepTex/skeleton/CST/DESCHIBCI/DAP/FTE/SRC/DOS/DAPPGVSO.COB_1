000000 IDENTIFICATION DIVISION.
000000*=======================
000000 PROGRAM-ID.      DAPPGVSO.
000000 AUTHOR.          P CONCHA L.
000000 DATE-WRITTEN.    30-NOV-93.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
000000******************************************************************
000000*  SISTEMA    : DEPOSITOS A PLAZO
000000*  SUB-SISTEMA: INFORMES DAP
000000*  ANALISTA   : CECILIA PIZARRO.
000000*  OBJETIVO :
000000*           - GENERAR ARCHIVO ACUMULATIVO DE SALDOS DIARIOS
000000*             POR OFICINA, MONEDA Y PLAZO
000000*           - EFECTUAR AUTOMATICAMENTE LA MANTENCION DEL ARCHIVO
000000*             (CADA PROCESO DE FIN DE MES SE ELIMINAN TODOS LOS
000000*              REGISTROS DE SALDOS DIARIOS DE TRES (3) MESES
000000*              ATRAS, CON EXCEPCION DE LOS DEL ULTIMO DIA HABIL,
000000*              QUE SE CONSERVN PERMANENTEMENTE.)
000000*  ARCHIVOS :
000000*  INP      :
000000*             DAPDCUA  ARCHIVO DE SALDOS CUADRATURA
000000*  I-O      :
000000*             DAPDVSO  ARCHOVO DE SALDOS POR OFICINA
000000*  WRK      :
000000*             FSORT    ARCHIVO PARA ORDENAMIENTO
000000*
000000******************************************************************
000000     EJECT
000000 ENVIRONMENT DIVISION.
000000*====================
000000 CONFIGURATION SECTION.
000000*---------------------
000000 SPECIAL-NAMES.
000000      C01 IS CANAL-1
000000      DECIMAL-POINT IS COMMA.
000000 INPUT-OUTPUT SECTION.
000000*--------------------
000000 FILE-CONTROL.
000000     SELECT DAPDCUA        ASSIGN TO        UT-S-DAPDCUA.
000000     SELECT FSORT          ASSIGN TO        UT-S-SORTWK1.
000000     SELECT DAPDVSO        ASSIGN TO        DAPDVSO
000000        ORGANIZATION IS INDEXED
000000        ACCESS       IS DYNAMIC
000000        RECORD KEY   IS DAPDVSO-KEY-IVSO
000000        FILE STATUS  IS FS-DAPDVSO.
000000     EJECT
000000 DATA DIVISION.
000000*=============
000000 FILE SECTION.
000000*------------
000000*
      *
      ******************************************************************
      * ARCHIVO: DAPDCUA    TIPO: SAM         REGISTRO: FIJO
      *
      * ARCHIVO DE DATOS DE SALDOS PARA CUADRATURA
      *
      * LARGO REGISTRO             150 BYTES
      ******************************************************************
      *
      *
       FD  DAPDCUA
           RECORDING MODE IS F
           LABEL RECORD STANDARD
           BLOCK CONTAINS    0 RECORDS.
       01  REG-DAPDCUA.
           05 DAPDCUA-COD-OFIC           PIC X(03).
           05 DAPDCUA-COD-TPCC           PIC X(03).
           05 DAPDCUA-COD-VCAM           PIC X(06).
           05 DAPDCUA-COD-COPD.
              10 DAPDCUA-COD-TOPD        PIC X(03).
              10 DAPDCUA-COD-AOPD        PIC X(03).
           05 DAPDCUA-COD-PLZO           PIC X(02).
           05 DAPDCUA-COD-CTAC           PIC X(09).
           05 DAPDCUA-OPE-SALD           PIC S9(13)V9(02) COMP-3.
           05 DAPDCUA-OPE-NUME           PIC S9(11)       COMP-3.
           05 DAPDCUA-GLS-OFIC           PIC X(19).
           05 DAPDCUA-ABR-VCAM           PIC X(05).
           05 DAPDCUA-GLS-VCAM           PIC X(20).
           05 DAPDCUA-GLS-COPD           PIC X(24).
           05 DAPDCUA-GLS-PLZO           PIC X(10).
           05 DAPDCUA-FEC-REFE.
              10 DAPDCUA-FEC-REFE-DD     PIC 9(02).
              10 DAPDCUA-FEC-REFE-MM     PIC 9(02).
              10 DAPDCUA-FEC-REFE-SS     PIC 9(02).
              10 DAPDCUA-FEC-REFE-AA     PIC 9(02).
           05 DAPDCUA-COD-MMCC           PIC X(02).
           05 DAPDCUA-OPE-CAPT           PIC S9(13)V9(04) COMP-3.
           05 FILLER                     PIC X(10).
      *
000000*
000000 SD  FSORT.
000000 01  REG-FSORT.
000000     05 FSORT-COD-VCAM           PIC X(6).
000000     05 FSORT-COD-PLZO           PIC X(2).
000000     05 FSORT-COD-OFIC           PIC X(3).
000000     05 FSORT-VAL-SCAP           PIC S9(13)V9(04) COMP-3.
000000     05 FSORT-CAN-OPER           PIC S9(11)       COMP-3.
000000*
      *
       FD  DAPDVSO
           LABEL RECORDS STANDARD.
      *    VARIACION SALDOS POR OFICINA
      *    ----------------------------
      *
      *    ESTE REGISTRO PERMITE ALMACENAR LOS DATOS BASICOS PARA LA
      *    GENERACION DEL INFORME DE VARIACION DE SALDOS POR OFICINA.
      *
      *    NOMBRE REGISTRO : DAPDVSO
      *    CLAVE(S)        : DAPDVSO-KEY-IVSO(UU)
      *    LARGO           : 50
      *    BLOQUEO         : 1
      *    OBSERVACIONES   :
      *
       01  REG-DAPDVSO.
      *
      *    IDENTIFICACION DAPDVSO
           05 DAPDVSO-KEY-IVSO.
      *
      *       FECHA PROCESO
              10 DAPDVSO-FEC-FPRO.
                 15 DAPDVSO-NUM-SPRO       PIC 9(02).
                 15 DAPDVSO-NUM-APRO       PIC 9(02).
                 15 DAPDVSO-NUM-MPRO       PIC 9(02).
                 15 DAPDVSO-NUM-DPRO       PIC 9(02).
      *
      *       CODIGO VALOR DE CAMBIO
              10 DAPDVSO-COD-VCAM.
      *
      *          MISCELANEO TIPO VALOR TAB TVAL
                 15 DAPDVSO-MSC-TVAL       PIC X(01).
      *
      *          CODIGO VALOR
                 15 DAPDVSO-COD-CVAL       PIC X(03).
      *
      *          CODIGO TIPO CAMBIO
                 15 DAPDVSO-COD-TCAM       PIC X(02).
      *
      *       CODIGO DE PLAZO OPERACION
              10 DAPDVSO-COD-PLZO          PIC X(02).
      *
      *       CODIGO OFICINA OPERACION DAP TAB OFI
              10 DAPDVSO-COD-OFIC          PIC X(03).
      *
      *    SALDO CAPTACION A LA FECHA
           05 DAPDVSO-VAL-SCAP             PIC S9(11)V9(04) COMP-3.
      *
      *    CANTIDAD DE DOCUMENTOS
           05 DAPDVSO-CAN-OPER             PIC S9(11) COMP-3.
      *
      *    GLOSA DISPONIBLE
           05 DAPDVSO-GLS-DISP             PIC X(17).
      *
000000*
000000     EJECT
000000 WORKING-STORAGE SECTION.
000000*-----------------------
000000*
      *
       01  BIBBAUD1.
           05 FILLER          PIC X(20) VALUE 'VARIABLES DE AUDIT'.
           05 BIB-MODULO      PIC X(08) VALUE '[MODNAME'.
           05 BIB-PROGRA      PIC X(15) VALUE '[PROGRAMMERNAME'.
           05 BIB-NIVEL       PIC X(05) VALUE '[LVNO'.
           05 BIB-FECHA-CATA  PIC X(08) VALUE '[DATEUPD'.
           05 BIB-MASTER      PIC X(44) VALUE
              '[DATA-SET-NAME-FOR-THE-LIBRARIAN-MASTER-FILE'.
           05 BIB-FECHA-EJEC  PIC 9(06).
           05 BIB-HORA-EJEC   PIC 9(08).
      *
000000*
000000 01  W-CONTADORES.
000000     05 CNT-DAPDCUA-L            PIC 9(08) VALUE 0.
000000     05 CNT-DAPDCUA-S            PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-G              PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-L              PIC 9(08) VALUE 0.
000000     05 CNT-DAPDVSO-G            PIC 9(08) VALUE 0.
000000     05 CNT-DAPDVSO-L            PIC 9(08) VALUE 0.
000000     05 CNT-DAPDVSO-D            PIC 9(08) VALUE 0.
000000*
000000 01  WE-EXTERNAS.
000000     05 WE-FECHA-DATE.
000000        10 WE-AA-DATE            PIC 9(02).
000000        10 WE-MM-DATE            PIC 9(02).
000000        10 WE-DD-DATE            PIC 9(02).
000000*
000000 01  F-FILE-STATUS.
000000     05 FS-DAPDCUA               PIC 9(01) VALUE 0.
000000     05 FS-FSORT                 PIC 9(01) VALUE 0.
000000     05 FS-DAPDVSO               PIC 9(02) VALUE 0.
000000*
000000 01  W-VARIABLES.
000000     05 SW-CANCEL                PIC 9(01) VALUE 0.
000000     05 SW-FIN-MES               PIC 9(01) VALUE 0.
000000*
000000 01  W-PARAMETROS.
000000     05 WP-FEC-FPRI.
000000        10 WP-NUM-DPRI           PIC X(02).
000000        10 WP-NUM-MPRI           PIC X(02).
000000        10 WP-NUM-SPRI           PIC X(02).
000000        10 WP-NUM-APRI           PIC X(02).
000000*
000000 01  W-REGISTROS-DE-TRABAJO.
000000     05 WR-COD-OFIC              PIC X(03) VALUE SPACES.
000000     05 WR-COD-VCAM              PIC X(06) VALUE SPACES.
000000     05 WR-COD-PLZO              PIC X(02) VALUE SPACES.
000000     05 WS-FEC-FFAC.
000000        10 WS-NUM-SFAC           PIC 9(02).
000000        10 WS-NUM-AFAC           PIC 9(02).
000000        10 WS-NUM-MFAC           PIC 9(02).
000000        10 WS-NUM-DFAC           PIC 9(02).
000000     05 WS-FEC-FMIN.
000000        10 WS-NUM-SMIN           PIC 9(02).
000000        10 WS-NUM-AMIN           PIC 9(02).
000000        10 WS-NUM-MMIN           PIC 9(02).
000000        10 WS-NUM-DMIN           PIC 9(02).
000000     05 WS-FEC-FMAX.
000000        10 WS-NUM-SMAX           PIC 9(02).
000000        10 WS-NUM-AMAX           PIC 9(02).
000000        10 WS-NUM-MMAX           PIC 9(02).
000000        10 WS-NUM-DMAX           PIC 9(02).
000000     05 WS-FEC-FNDE.
000000        10 WS-NUM-SNDE           PIC 9(02).
000000        10 WS-NUM-ANDE           PIC 9(02).
000000        10 WS-NUM-MNDE           PIC 9(02).
000000        10 WS-NUM-DNDE           PIC 9(02).
000000*
000000 01  ACUMULADORES.
000000     05 ACM-OFIC.
000000        10 ACM-OFIC-VAL-SCAP    PIC S9(13)V9(4)  COMP-3 VALUE +0.
000000        10 ACM-OFIC-CAN-OPER    PIC S9(11)       COMP-3 VALUE +0.
000000*
000000     EJECT
000000* AREA DE COMUNICACION CON RUTINA RUTUVALF
000000 01  RUTWVALF.
000000     05 RUTWVALF-FECHA.
000000        10 RUTWVALF-FECHA-D      PIC 9(02).
000000        10 RUTWVALF-FECHA-M      PIC 9(02).
000000        10 RUTWVALF-FECHA-A.
000000           15 RUTWVALF-FECHA-AM  PIC 9(02).
000000           15 RUTWVALF-FECHA-AA  PIC 9(02).
000000     05 RUTWVALF-CODRET          PIC 9(01)  VALUE 0.
000000     EJECT
      ******************************************************************
      *
       01  RTNWCALE.
      *
      * AREA DE LLAMADA A SUBRUTINA "RTNUCALE"
      *
      * ***  FUNCIONES VALIDAS PARA SUBRUTINA  ***
      *
      * AVNDC  : AVANZA N DIAS CALENDARIO (N = 0 A 99999)
      * AVNDH  : AVANZA N DIAS HABILES (N = 0 A 99999)
      * BDCA   : BUSCA DIA CALENDARIO ANTERIOR
      * BDCS   : BUSCA DIA CALENDARIO SIGUIENTE
      * BDHA   : BUSCA DIA HABIL ANTERIOR
      * BDHS   : BUSCA DIA HABIL SIGUIENTE
      * BPDCM  : BUSCA PRIMER DIA CALENDARIO MES (ACTUAL)
      * BPDCMA : BUSCA PRIMER DIA CALENDARIO MES ANTERIOR
      * BPDCMS : BUSCA PRIMER DIA CALENDARIO MES SIGUIENTE
      * BPDHM  : BUSCA PRIMER DIA HABIL MES (ACTUAL)
      * BPDHMA : BUSCA PRIMER DIA HABIL MES ANTERIOR
      * BPDHMS : BUSCA PRIMER DIA HABIL MES SIGUIENTE
      * BUDCM  : BUSCA ULTIMO DIA CALENDARIO MES (ACTUAL)
      * BUDCMA : BUSCA ULTIMO DIA CALENDARIO MES ANTERIOR
      * BUDCMS : BUSCA ULTIMO DIA CALENDARIO MES SIGUIENTE
      * BUDHM  : BUSCA ULTIMO DIA HABIL MES (ACTUAL)
      * BUDHMA : BUSCA ULTIMO DIA HABIL MES ANTERIOR
      * BUDHMS : BUSCA ULTIMO DIA HABIL MES SIGUIENTE
      * CARGAR : CARGA ARCHIVO CALENDARIO EN MEMORIA DESDE UNA FECHA
      * CDCEF  : CALCULA DIFERENCIA DE DIAS CALENDARIO ENTRE FECHAS
      * INFO   : ENTREGA INFORMACION ACERCA DE UNA FECHA
      * RENDC  : RETROCEDE N DIAS CALENDARIO (N = 0 A 99999)
      * RENDH  : RETROCEDE N DIAS HABILES (N = 0 A 99999)
      *
           05 CALEAREA-FUNCION              PIC X(06).
           05 CALEAREA-FECHA-1              PIC X(08).
           05 R1-CALEAREA-FECHA-1         REDEFINES CALEAREA-FECHA-1.
              10 CALEAREA-FECHA-1-D         PIC 9(02).
              10 CALEAREA-FECHA-1-M         PIC 9(02).
              10 CALEAREA-FECHA-1-A         PIC 9(04).
              10 R1-CALEAREA-FECHA-1-A    REDEFINES CALEAREA-FECHA-1-A.
                 15 CALEAREA-FECHA-1-AM     PIC 9(02).
                 15 CALEAREA-FECHA-1-AA     PIC 9(02).
           05 CALEAREA-FECHA-2              PIC X(08).
           05 R1-CALEAREA-FECHA-2         REDEFINES CALEAREA-FECHA-2.
              10 CALEAREA-FECHA-2-D         PIC 9(02).
              10 CALEAREA-FECHA-2-M         PIC 9(02).
              10 CALEAREA-FECHA-2-A         PIC 9(04).
              10 R1-CALEAREA-FECHA-2-A    REDEFINES CALEAREA-FECHA-2-A.
                 15 CALEAREA-FECHA-2-AM     PIC 9(02).
                 15 CALEAREA-FECHA-2-AA     PIC 9(02).
           05 CALEAREA-NUM-DIA              PIC S9(8).
           05 CALEAREA-TIP-DIA              PIC X(01).
           05 CALEAREA-INI-DIA              PIC X(02).
           05 CALEAREA-COD-RET              PIC 9(02).
      *
      ******************************************************************
000000     EJECT
000000*
000000* MODULO DE CONTROL
000000*
000000 PROCEDURE DIVISION.
000000*
000000 A-CONTROL SECTION.
      *
      * DESPLIEGA VARIABLES DE AUDIT
           DISPLAY '***********************************************'.
           DISPLAY '***   IDENTIFICACION DE MODULO EJECUTADO    ***'.
           DISPLAY 'MASTER FILE               = ' BIB-MASTER.
           DISPLAY 'NOMBRE DEL MODULO         = ' BIB-MODULO.
           DISPLAY 'NUMERO DE VERSION         = ' BIB-NIVEL.
           DISPLAY 'FECHA DE CATALOGACION     = ' BIB-FECHA-CATA.
           DISPLAY 'NOMBRE DEL PROGRAMADOR    = ' BIB-PROGRA.
           DISPLAY ' '.
           DISPLAY '***   IDENTIFICACION DE EJECUCION           ***'.
           DISPLAY '*    (FECHA Y HORA DEL COMPUTADOR)            *'.
           ACCEPT BIB-FECHA-EJEC FROM DATE
           ACCEPT BIB-HORA-EJEC  FROM TIME
           DISPLAY 'FECHA EJECUCION           = ' BIB-FECHA-EJEC.
           DISPLAY 'HORA EJECUCION (HHMMSSDD) = ' BIB-HORA-EJEC.
           DISPLAY '***                                         ***'.
           DISPLAY '***********************************************'.
      *
000000*
000000 AA-INICIALIZACION.
000000     PERFORM B-INICIALIZAR.
000000*
000000 AB-PROCESAMIENTO.
000000     PERFORM C-PROCESAR.
000000*
000000 AC-TERMINACION.
000000     PERFORM D-TERMINAR.
000000*
000000 AZZ-FIN-PROC.
000000     STOP RUN.
000000     EJECT
000000*
000000*
000000*
000000 B-INICIALIZAR SECTION.
000000*
000000 BA-CONTROL-PROCESO.
000000     PERFORM BB-ACCEPT-TARJETA
000000     PERFORM BC-PROC-CALENDARIO
000000     PERFORM BD-PROC-FECHA
000000     PERFORM BM-ABRE-ARCH
000000     PERFORM BZ-VERIFICA
000000     GO TO BZZ-FIN-SECC.
000000*
000000 BB-ACCEPT-TARJETA.
000000     DISPLAY ' '.
000000     DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
000000     DISPLAY ' '.
000000     DISPLAY 'INGRESE FECHA DE PROCESO (DDMMSSAA) : '.
000000     ACCEPT WP-FEC-FPRI
000000     DISPLAY WP-FEC-FPRI
000000
000000     DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
000000     DISPLAY ' '.
000000     DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
000000*
000000*VAL-FEC VALIDA FECHA DE PROCESO
000000     DISPLAY WP-FEC-FPRI
000000     MOVE WP-NUM-DPRI TO RUTWVALF-FECHA-D
000000     MOVE WP-NUM-MPRI TO RUTWVALF-FECHA-M
000000     MOVE WP-NUM-SPRI TO RUTWVALF-FECHA-AM
000000     MOVE WP-NUM-APRI TO RUTWVALF-FECHA-AA
000000     CALL 'RUTUVALF' USING RUTWVALF-FECHA RUTWVALF-CODRET
000000     IF RUTWVALF-CODRET NOT = 0
000000        DISPLAY
000000           '* FECHA PROCESO INVALIDA. ' WP-FEC-FPRI
000000        MOVE 1 TO SW-CANCEL.
000000*
000000     DISPLAY 'FIN VALIDACION PARAMETROS'.
000000*
000000 BC-PROC-CALENDARIO.
000000     MOVE 'CARGAR' TO CALEAREA-FUNCION
000000     MOVE ALL '0'  TO CALEAREA-FECHA-1
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-FUNCION = 'CANCEL'
000000        DISPLAY
000000           'ERROR EN CARGA CALENDARIO FS= ' CALEAREA-COD-RET
000000        MOVE 1 TO SW-CANCEL.
000000*
000000 BD-PROC-FECHA.
000000* ACEPTAR FECHA DEL COMPUTADOR
000000     ACCEPT WE-FECHA-DATE FROM DATE
000000     MOVE WP-NUM-DPRI TO WS-NUM-DFAC
000000     MOVE WP-NUM-MPRI TO WS-NUM-MFAC
000000     MOVE WP-NUM-SPRI TO WS-NUM-SFAC
000000     MOVE WP-NUM-APRI TO WS-NUM-AFAC.
000000*FECHA ULTIMO DIA HABIL CORRESPONDIENTE A FECHA PROCESO
000000     MOVE 'BUDHM'                TO CALEAREA-FUNCION
000000     MOVE WP-NUM-DPRI            TO CALEAREA-FECHA-1-D
000000     MOVE WP-NUM-MPRI            TO CALEAREA-FECHA-1-M
000000     MOVE WP-NUM-SPRI            TO CALEAREA-FECHA-1-AM
000000     MOVE WP-NUM-APRI            TO CALEAREA-FECHA-1-AA
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-COD-RET > 0
000000        DISPLAY ' '
000000        DISPLAY
000000           '* ERROR EN FECHA DE PROCESO.   ' RTNWCALE
000000        MOVE 1                   TO SW-CANCEL
000000     ELSE
000000        IF CALEAREA-FECHA-1 = CALEAREA-FECHA-2
000000           MOVE 1 TO SW-FIN-MES
000000           PERFORM BD1-DETERMINA-BORRE.
000000*
000000     DISPLAY 'FIN VALIDACION PARAMETROS'.
000000*
000000 BD1-DETERMINA-BORRE.
000000* METODO:
000000* A PARTIR DE LA FECHA DE PROCESO, SE VERIFICA QUE ESTA SEA DEL
000000* PROCESO DE FIN DE MES (ULTIMO DIA HABIL).
000000* SI ES ASI, SE CALCULAN LAS SIGUIENTES FECHAS:
000000* FECHA MAXIMA = ULTIMO DIA CALENDARIO DE MES Y ANO DE PROCESO MEN
000000* OS TRES MESES.  (SENALA FECHA DE TERMINO DE PROCESO DE BORRE)
000000* FECHA MINIMA = PRIMER DIA CALENDARIO DE MES Y ANO DE PROCESO MEN
000000* OS TRES MESES.  (SENALA FECHA DE COMIENZO DE PROCESO DE BORRE)
000000* FECHA NODELE = ULTIMO DIA HABIL      DE MES Y ANO DE PROCESO MEN
000000* OS TRES MESES.  (SENALA FECHA DE LA QUE NO SE EFECTUARA BORRE)
000000*
000000     DISPLAY ' '
000000     DISPLAY '*** PROCESO DE FIN DE MES ***'
000000     MOVE WP-NUM-DPRI            TO CALEAREA-FECHA-2-D
000000     MOVE WP-NUM-MPRI            TO CALEAREA-FECHA-2-M
000000     MOVE WP-NUM-SPRI            TO CALEAREA-FECHA-2-AM
000000     MOVE WP-NUM-APRI            TO CALEAREA-FECHA-2-AA
000000* RETROCEDE TRES MESES EN CALENDARIO
000000     PERFORM BD2-BUSCA-UDCMA 3 TIMES
000000     MOVE CALEAREA-FECHA-2-D     TO WS-NUM-DMAX
000000     MOVE CALEAREA-FECHA-2-M     TO WS-NUM-MMAX
000000     MOVE CALEAREA-FECHA-2-AM    TO WS-NUM-SMAX
000000     MOVE CALEAREA-FECHA-2-AA    TO WS-NUM-AMAX
000000     PERFORM BD3-BUSCA-PDCM
000000     MOVE CALEAREA-FECHA-2-D     TO WS-NUM-DMIN
000000     MOVE CALEAREA-FECHA-2-M     TO WS-NUM-MMIN
000000     MOVE CALEAREA-FECHA-2-AM    TO WS-NUM-SMIN
000000     MOVE CALEAREA-FECHA-2-AA    TO WS-NUM-AMIN
000000     PERFORM BD4-BUSCA-UDHM
000000     MOVE CALEAREA-FECHA-2-D     TO WS-NUM-DNDE
000000     MOVE CALEAREA-FECHA-2-M     TO WS-NUM-MNDE
000000     MOVE CALEAREA-FECHA-2-AM    TO WS-NUM-SNDE
000000     MOVE CALEAREA-FECHA-2-AA    TO WS-NUM-ANDE
000000     DISPLAY '*** SE BORRAN REGISTROS DE SALDOS DIARIOS '
000000     DISPLAY '    ENTRE EL ' WS-FEC-FMIN ' Y EL ' WS-FEC-FMAX
000000     DISPLAY '    EXCEPTO LOS DEL ' WS-FEC-FNDE
000000     DISPLAY ' '.
000000*
000000 BD2-BUSCA-UDCMA.
000000* BUSCA ULTIMO DIA CALENDARIO DE MES ANTERIOR
000000     MOVE 'BUDCMA'               TO CALEAREA-FUNCION
000000     MOVE CALEAREA-FECHA-2       TO CALEAREA-FECHA-1
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-COD-RET > 0
000000        DISPLAY ' '
000000        DISPLAY
000000           '* ERROR EN FECHA UDCMA.        ' RTNWCALE
000000        MOVE 1                   TO SW-CANCEL.
000000*
000000 BD3-BUSCA-PDCM.
000000* BUSCA PRIMER DIA CALENDARIO DE MES
000000     MOVE 'BPDCM'                TO CALEAREA-FUNCION
000000     MOVE CALEAREA-FECHA-2       TO CALEAREA-FECHA-1
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-COD-RET > 0
000000        DISPLAY ' '
000000        DISPLAY
000000           '* ERROR EN FECHA PDCM.         ' RTNWCALE
000000        MOVE 1                   TO SW-CANCEL.
000000*
000000 BD4-BUSCA-UDHM.
000000* BUSCA ULTIMO DIA HABIL DE MES
000000     MOVE 'BUDHM'                TO CALEAREA-FUNCION
000000     MOVE CALEAREA-FECHA-2       TO CALEAREA-FECHA-1
000000     CALL 'RUTUCALE' USING RTNWCALE
000000     IF CALEAREA-COD-RET > 0
000000        DISPLAY ' '
000000        DISPLAY
000000           '* ERROR EN FECHA UDHM.         ' RTNWCALE
000000        MOVE 1                   TO SW-CANCEL.
000000*
000000 BM-ABRE-ARCH.
000000     OPEN INPUT  DAPDCUA
000000          I-O    DAPDVSO.
000000     IF FS-DAPDVSO > 0
000000        DISPLAY '*** ERROR OPEN DAPDVSO. FS=' FS-DAPDVSO
000000        MOVE 1 TO SW-CANCEL.
000000*
000000 BZ-VERIFICA.
000000     IF SW-CANCEL = 1
000000        DISPLAY '*** ERRORES EN INICIALIZACION ***'
000000        PERFORM D-TERMINAR.
000000*
000000 BZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 C-PROCESAR SECTION.
000000*
000000 CA-CONTROL.
000000     SORT FSORT
000000        ASCENDING KEY
000000           FSORT-COD-VCAM
000000           FSORT-COD-PLZO
000000           FSORT-COD-OFIC
000000        INPUT  PROCEDURE  E-ENTRADA-SORT
000000        OUTPUT PROCEDURE  F-SALIDA-SORT.
000000*
000000     IF SORT-RETURN NOT = 0
000000        DISPLAY '**  ####################### **'
000000        DISPLAY '**  HUBO ERROR EN SORT      **'
000000        DISPLAY '**  ####################### **'
000000        MOVE 1 TO SW-CANCEL
000000        GO TO CZZ-FIN-SECC.
000000*
000000     IF SW-FIN-MES = 1
000000        PERFORM G-ELIMINA.
000000*
000000 CZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 D-TERMINAR  SECTION.
000000*
000000 DA-TOTALES.
000000     CLOSE DAPDCUA DAPDVSO
000000     DISPLAY ' '
000000     DISPLAY '+---------------------------------------------+'
000000     DISPLAY '+ ESTADISTICAS DE PROCESO.  PROGRAMA DAPPGVSO +'
000000     DISPLAY '+---------------------------------------------+'
000000     DISPLAY ' '
000000     DISPLAY 'REG DAPDCUA LEIDOS..................' CNT-DAPDCUA-L
000000     DISPLAY 'REG DAPDCUA SELECC..................' CNT-DAPDCUA-S
000000     DISPLAY 'REG FSORT   GRABAD..................' CNT-FSORT-G
000000     DISPLAY 'REG FSORT   LEIDOS..................' CNT-FSORT-L
000000     DISPLAY 'REG DAPDVSO CREADO..................' CNT-DAPDVSO-G
000000     DISPLAY ' '
000000     IF SW-FIN-MES = 1
000000        DISPLAY 'PROCESO MENSUAL DE BORRE'
000000        DISPLAY
000000           'REG DAPDVSO LEIDOS..................' CNT-DAPDVSO-L
000000        DISPLAY
000000           'REG DAPDVSO BORRAD..................' CNT-DAPDVSO-D
000000     DISPLAY ' '.
000000     DISPLAY '***********************************************'
000000     DISPLAY ' '
000000     IF SW-CANCEL    = 1
000000        DISPLAY ' HAY MENSAJES PARA EL EXPLOTADOR'
000000        DISPLAY ' '
000000        DISPLAY '************************************************'
000000        DISPLAY '************************************************'
000000        DISPLAY '****                                        ****'
000000        DISPLAY '****      TERMINO ERRONEO DEL PROCESO       ****'
000000        DISPLAY '****                                        ****'
000000        DISPLAY '************************************************'
000000        DISPLAY '************************************************'
000000        MOVE 100 TO RETURN-CODE
000000        STOP RUN
000000     ELSE
000000        DISPLAY ' NO HAY MENSAJES PARA EL EXPLOTADOR'
000000        DISPLAY ' '
000000        DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL ***'.
000000*
000000 DZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 E-ENTRADA-SORT SECTION.
000000*
000000 EA-LEER-DAPDCUA.
000000     READ DAPDCUA AT END MOVE 1 TO FS-DAPDCUA.
000000     IF FS-DAPDCUA = 0
000000        ADD 1 TO CNT-DAPDCUA-L.
000000*
000000 EC-CTRL-PROC.
000000* SI LA FECHA DEL ARCHIVO DE CUADRATURA NO CORRESPONDE A PROCESO
000000* PROCESO SE CANCELA.
000000     IF DAPDCUA-FEC-REFE-DD = WS-NUM-DFAC AND
000000        DAPDCUA-FEC-REFE-MM = WS-NUM-MFAC AND
000000        DAPDCUA-FEC-REFE-SS = WS-NUM-SFAC AND
000000        DAPDCUA-FEC-REFE-AA = WS-NUM-AFAC
000000           PERFORM ED-SELECCIONA UNTIL
000000              FS-DAPDCUA = 1
000000     ELSE
000000        DISPLAY '*** FECHA DE ARCHIVO DE CUADRATURA NO '
000000                'CORRESPONDE A FECHA DE PROCESO'
000000        MOVE 16 TO SORT-RETURN.
000000     GO TO EZZ-FIN-SECC.
000000*
000000 ED-SELECCIONA.
000000** SELECCIONA REGISTROS PARA CARGARLOS EN EL SORT
000000* SOLO VIGENTES
000000     IF DAPDCUA-COD-TPCC = 'CAP'
000000        ADD 1        TO CNT-DAPDCUA-S
000000        PERFORM EK-GRABA-FSORT.
000000     PERFORM EA-LEER-DAPDCUA.
000000*
000000 EK-GRABA-FSORT.
000000     MOVE DAPDCUA-COD-VCAM TO FSORT-COD-VCAM
000000     MOVE DAPDCUA-COD-PLZO TO FSORT-COD-PLZO
000000     MOVE DAPDCUA-COD-OFIC TO FSORT-COD-OFIC
000000     MOVE DAPDCUA-OPE-CAPT TO FSORT-VAL-SCAP
000000     MOVE DAPDCUA-OPE-NUME TO FSORT-CAN-OPER
000000     RELEASE REG-FSORT
000000     ADD 1             TO CNT-FSORT-G.
000000*
000000 EZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 F-SALIDA-SORT SECTION.
000000*
000000 FA-LEER-FSORT.
000000     RETURN FSORT AT END MOVE 1 TO FS-FSORT.
000000     IF FS-FSORT = 0
000000        ADD 1 TO CNT-FSORT-L.
000000*
000000 FB-CTRL-PROC.
000000     PERFORM FC-CTRL-X-VCAM UNTIL
000000        FS-FSORT = 1
000000     GO TO FZZ-FIN-SECC.
000000*
000000 FC-CTRL-X-VCAM.
000000* MONEDA
000000     MOVE FSORT-COD-VCAM    TO WR-COD-VCAM
000000     PERFORM FD-CTRL-X-PLZO UNTIL
000000        FSORT-COD-VCAM NOT = WR-COD-VCAM OR
000000           FS-FSORT = 1.
000000*
000000 FD-CTRL-X-PLZO.
000000* PLAZO
000000     MOVE FSORT-COD-PLZO    TO WR-COD-PLZO
000000     PERFORM FE-CTRL-X-OFIC UNTIL
000000        FSORT-COD-VCAM NOT = WR-COD-VCAM OR
000000           FSORT-COD-PLZO NOT = WR-COD-PLZO OR
000000              FS-FSORT = 1.
000000*
000000 FE-CTRL-X-OFIC.
000000* OFICINA
000000     INITIALIZE ACM-OFIC
000000     MOVE FSORT-COD-OFIC    TO WR-COD-OFIC
000000     PERFORM FF-ACUMULA UNTIL
000000        FSORT-COD-VCAM NOT = WR-COD-VCAM OR
000000           FSORT-COD-PLZO NOT = WR-COD-PLZO OR
000000              FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000                 FS-FSORT = 1
000000     PERFORM FG-GRABA-DAPDVSO.
000000*
000000 FF-ACUMULA.
000000     ADD FSORT-VAL-SCAP          TO ACM-OFIC-VAL-SCAP
000000     ADD FSORT-CAN-OPER          TO ACM-OFIC-CAN-OPER
000000     PERFORM FA-LEER-FSORT.
000000*
000000 FG-GRABA-DAPDVSO.
000000     MOVE LOW-VALUE              TO REG-DAPDVSO
000000     MOVE WS-FEC-FFAC            TO DAPDVSO-FEC-FPRO
000000     MOVE WR-COD-VCAM            TO DAPDVSO-COD-VCAM
000000     MOVE WR-COD-PLZO            TO DAPDVSO-COD-PLZO
000000     MOVE WR-COD-OFIC            TO DAPDVSO-COD-OFIC
000000     MOVE ACM-OFIC-VAL-SCAP      TO DAPDVSO-VAL-SCAP
000000     MOVE ACM-OFIC-CAN-OPER      TO DAPDVSO-CAN-OPER
000000     WRITE REG-DAPDVSO
000000     IF FS-DAPDVSO = 0
000000        ADD 1                    TO CNT-DAPDVSO-G
000000     ELSE
000000        DISPLAY '*** ERR. GRAB DAPDVSO. FS=' FS-DAPDVSO.
000000*
000000 FZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 G-ELIMINA SECTION.
000000*
000000 GA-CONTROL.
000000     MOVE LOW-VALUE   TO DAPDVSO-KEY-IVSO
000000     MOVE WS-FEC-FMIN TO DAPDVSO-FEC-FPRO
000000     START DAPDVSO KEY NOT < DAPDVSO-FEC-FPRO
000000     IF FS-DAPDVSO = 0
000000        PERFORM GB-LEER-DAPDVSO
000000        PERFORM GC-DELE-DAPDVSO UNTIL
000000           DAPDVSO-FEC-FPRO > WS-FEC-FMAX OR
000000              FS-DAPDVSO = 10.
000000     GO TO GZZ-FIN-SECC.
000000*
000000 GB-LEER-DAPDVSO.
000000     READ DAPDVSO NEXT RECORD
000000     IF FS-DAPDVSO = 0
000000        ADD 1 TO CNT-DAPDVSO-L.
000000*
000000 GC-DELE-DAPDVSO.
000000     IF DAPDVSO-FEC-FPRO < WS-FEC-FMIN OR
000000        DAPDVSO-FEC-FPRO > WS-FEC-FMAX
000000           SUBTRACT 1 FROM CNT-DAPDVSO-L.
000000     IF DAPDVSO-FEC-FPRO < WS-FEC-FMIN OR
000000        DAPDVSO-FEC-FPRO > WS-FEC-FMAX OR
000000        DAPDVSO-FEC-FPRO = WS-FEC-FNDE
000000           NEXT SENTENCE
000000     ELSE
000000        DELETE DAPDVSO RECORD
000000        IF FS-DAPDVSO = 0
000000           ADD 1 TO CNT-DAPDVSO-D.
000000     PERFORM GB-LEER-DAPDVSO.
000000*
000000 GZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
