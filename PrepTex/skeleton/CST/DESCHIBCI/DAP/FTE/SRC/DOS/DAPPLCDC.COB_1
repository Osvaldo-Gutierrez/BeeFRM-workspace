000000 IDENTIFICATION DIVISION.
000000 PROGRAM-ID. DAPPLCDC.
000000 AUTHOR.     P CONCHA L.
000000
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
000000* ***************************************************************
000000*
000000*  OBJETIVO : PAREA SALDOS DAP CON SALDO CONTABLE.
000000*
000000*  INPUT    :   -SDOCMN  : MAESTRO CONTABILIDAD MN
000000*               -SDOCMX  : MAESTRO CONTABILIDAD MX
000000*               -DAPDCUA : ARCHIVO CUADRATURA DAP
000000*               -BD 371 TABLA DCC
000000*
000000*  OUTPUT   :   -DAPLDIF : INFORME DE SALDOS CON DIFERENCIAS.
000000*               -DAPLRCH : INFORME DE RECHAZOS DEL PROCESO.
000000*
000000*  FSORT    :   -FSORT   : ARCHIVO SORT.
000000*
000000* ***************************************************************
000000* MANTENCION:   P CONCHA L    09/12/93
000000* - SE INCORPORA FORMATO COPY DE ARCHIVO DAPDCUA
000000*
000000* ***************************************************************
000000* MANTENCION:   P CONCHA L    20/07/94
000000* - SE MODIFICA PROCESO DE CARGA DE TABLA DE SALDOS CONTABLES
000000*   PARA ELIMINAR ERROR DE DESCUADRATURA PRODUCIDO POR EL
000000*   CAMBIO DE DEPENDENCIA DE UNA OFICINA (MONEDA EXTRANJERA).
000000*
000000* ***************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
000000
000000 ENVIRONMENT DIVISION.
000000*********************
000000 CONFIGURATION SECTION.
000000*=====================
000000 SPECIAL-NAMES.
000000     C01 IS CANAL-1
000000     DECIMAL-POINT IS COMMA.
000000
000000 INPUT-OUTPUT SECTION.
000000*====================
000000 FILE-CONTROL.
000000     SELECT SDOCMN  ASSIGN TO UT-S-SDOCMN.
000000     SELECT SDOCMX  ASSIGN TO SDOCMX
000000        ORGANIZATION   IS INDEXED
000000        ACCESS MODE    IS SEQUENTIAL
000000        RECORD KEY     IS SDOCMX-CLV-CLAV
000000        FILE STATUS    IS FS-SDOCMX.
000000     SELECT DAPDCUA ASSIGN TO UT-S-DAPDCUA.
000000     SELECT DAPLDIF ASSIGN TO UT-S-DAPLDIF.
000000     SELECT DAPLRCH ASSIGN TO UT-S-DAPLRCH.
000000     SELECT FSORT   ASSIGN TO UT-S-SORTWK1.
000000*
000000     EJECT
000000 DATA DIVISION.
000000*
000000 FILE SECTION.
000000*
000000 FD  SDOCMN
000000     RECORDING MODE IS F
000000     RECORD  CONTAINS 550 CHARACTERS
000000     BLOCK   CONTAINS   0  RECORDS
000000     LABEL RECORD  IS STANDARD.
000000 01  SDOCMN-REG.
000000     05 SDOCMN-MON                     PIC 9(2).
000000     05 SDOCMN-LLAVE.
000000        10 SDOCMN-OFI                  PIC 9(3).
000000        10 SDOCMN-CTA                  PIC 9(9).
000000     05 SDOCMN-NOM                     PIC X(50).
000000     05 SDOCMN-SALDO                   PIC S9(13)V99 COMP-3.
000000     05 SDOCMN-RES                     PIC X(478).
000000*
000000 FD  SDOCMX
000000     RECORD CONTAINS 110 CHARACTERS.
000000 01  REG-SDOCMX.
000000     05 SDOCMX-CLV-CLAV.
000000        10 SDOCMX-COD-OFIC         PIC 9(03).
000000        10 SDOCMX-COD-MONE         PIC 9(02).
000000        10 SDOCMX-NUM-CNTA         PIC 9(09).
000000        10 SDOCMX-COD-COAN         PIC 9(06).
000000     05 SDOCMX-GLS-COAN            PIC X(40).
000000     05 SDOCMX-NUM-SALD            PIC S9(13)V9(02) COMP-3.
000000     05 SDOCMX-NUM-DB01            PIC S9(13)V9(02) COMP-3.
000000     05 SDOCMX-NUM-HB01            PIC S9(13)V9(02) COMP-3.
000000     05 SDOCMX-NUM-DB02            PIC S9(13)V9(02) COMP-3.
000000     05 SDOCMX-NUM-HB02            PIC S9(13)V9(02) COMP-3.
000000     05 FILLER                     PIC X(10).
000000*
      *
      ******************************************************************
      * ARCHIVO: DAPDCUA    TIPO: SAM         REGISTRO: FIJO
      *
      * ARCHIVO DE DATOS DE SALDOS PARA CUADRATURA
      *
      * LARGO REGISTRO             150 BYTES
      ******************************************************************
      *
      *
       FD  DAPDCUA
           RECORDING MODE IS F
           LABEL RECORD STANDARD
           BLOCK CONTAINS    0 RECORDS.
       01  REG-DAPDCUA.
           05 DAPDCUA-COD-OFIC           PIC X(03).
           05 DAPDCUA-COD-TPCC           PIC X(03).
           05 DAPDCUA-COD-VCAM           PIC X(06).
           05 DAPDCUA-COD-COPD.
              10 DAPDCUA-COD-TOPD        PIC X(03).
              10 DAPDCUA-COD-AOPD        PIC X(03).
           05 DAPDCUA-COD-PLZO           PIC X(02).
           05 DAPDCUA-COD-CTAC           PIC X(09).
           05 DAPDCUA-OPE-SALD           PIC S9(13)V9(02) COMP-3.
           05 DAPDCUA-OPE-NUME           PIC S9(11)       COMP-3.
           05 DAPDCUA-GLS-OFIC           PIC X(19).
           05 DAPDCUA-ABR-VCAM           PIC X(05).
           05 DAPDCUA-GLS-VCAM           PIC X(20).
           05 DAPDCUA-GLS-COPD           PIC X(24).
           05 DAPDCUA-GLS-PLZO           PIC X(10).
           05 DAPDCUA-FEC-REFE.
              10 DAPDCUA-FEC-REFE-DD     PIC 9(02).
              10 DAPDCUA-FEC-REFE-MM     PIC 9(02).
              10 DAPDCUA-FEC-REFE-SS     PIC 9(02).
              10 DAPDCUA-FEC-REFE-AA     PIC 9(02).
           05 DAPDCUA-COD-MMCC           PIC X(02).
           05 DAPDCUA-OPE-CAPT           PIC S9(13)V9(04) COMP-3.
           05 FILLER                     PIC X(10).
      *
000000*
000000 FD  DAPLDIF
000000     RECORDING MODE IS F
000000     BLOCK CONTAINS 0 RECORDS
000000     LABEL RECORD  IS STANDARD.
000000 01  REG-DAPLDIF.
000000     05 FILLER                         PIC X(132).
000000*
000000 FD  DAPLRCH
000000     RECORDING MODE IS F
000000     BLOCK CONTAINS 0 RECORDS
000000     LABEL RECORD  IS STANDARD.
000000 01  REG-DAPLRCH.
000000     05 FILLER                         PIC X(132).
000000*
000000 SD  FSORT.
000000 01  REG-FSORT.
000000     05 FSORT-LLAVE.
000000        10 FSORT-COD-OFIC      PIC X(3).
000000        10 FSORT-COD-MMCC      PIC X(2).
000000        10 FSORT-COD-CTAC      PIC X(9).
000000     05 FSORT-COD-TPCC         PIC X(3).
000000     05 FSORT-COD-COPD.
000000        10 FSORT-COD-TOPD      PIC X(03).
000000        10 FSORT-COD-AOPD      PIC X(03).
000000     05 FSORT-OPE-SALD         PIC S9(13)V99 COMP-3.
000000     05 FSORT-COD-PLZO         PIC X(02).
000000     05 FSORT-COD-VCAM         PIC X(06).
000000     05 FSORT-FEC-REFE.
000000        10 FSORT-FEC-REFE-DD   PIC X(02).
000000        10 FSORT-FEC-REFE-MM   PIC X(02).
000000        10 FSORT-FEC-REFE-SS   PIC X(02).
000000        10 FSORT-FEC-REFE-AA   PIC X(02).
000000*
000000     EJECT
000000 WORKING-STORAGE SECTION.
000000*
000000     COPY BIBBAUD1.
000000*
000000 01  W-CONTADORES.
000000     05 CNT-SDOCMN-L         PIC 9(08) VALUE 0.
000000     05 CNT-SDOCMN-C         PIC 9(08) VALUE 0.
000000     05 CNT-SDOCMX-L         PIC 9(08) VALUE 0.
000000     05 CNT-SDOCMX-C         PIC 9(08) VALUE 0.
000000     05 CNT-DAPDCUA-L        PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-G          PIC 9(08) VALUE 0.
000000     05 CNT-FSORT-L          PIC 9(08) VALUE 0.
000000     05 CNT-NO-PAREAD        PIC 9(08) VALUE 0.
000000     05 CNT-SI-PAREAD        PIC 9(08) VALUE 0.
000000     05 CNT-PAGE-DAPLDIF     PIC 9(4) VALUE 0.
000000     05 CNT-LINE-DAPLDIF     PIC 9(3) VALUE 99.
000000        88 SALTO-PAG-DAPLDIF          VALUE 60 THRU 99.
000000     05 CNT-PAGE-DAPLRCH     PIC 9(4) VALUE 0.
000000     05 CNT-LINE-DAPLRCH     PIC 9(3) VALUE 99.
000000        88 SALTO-PAG-DAPLRCH          VALUE 60 THRU 99.
000000*
000000 01  F-FILE-STATUS.
000000     05 FS-SDOCMN                  PIC 9(01) VALUE 0.
000000     05 FS-SDOCMX                  PIC 9(02) VALUE 0.
000000     05 FS-DAPDCUA                 PIC 9(01) VALUE 0.
000000     05 FS-FSORT                   PIC 9(01) VALUE 0.
000000*
000000 01  W-VARIABLES.
000000     05 SW-CANCEL               PIC 9(01) VALUE 0.
000000     05 SW-IMPR-DIF             PIC 9(01) VALUE 0.
000000     05 SW-IMPR-RCH             PIC 9(01) VALUE 0.
000000*
000000 01  WE-EXTERNAS.
000000     05 WE-FECHA-DATE.
000000        10 WE-AA-DATE          PIC 9(02).
000000        10 WE-MM-DATE          PIC 9(02).
000000        10 WE-DD-DATE          PIC 9(02).
000000*
000000 01  W-REGISTROS-DE-TRABAJO.
000000     05 WR-COD-OFIC         PIC X(03) VALUE SPACES.
000000     05 WR-COD-COPD         PIC X(06) VALUE SPACES.
000000     05 WR-COD-PLZO         PIC X(02) VALUE SPACES.
000000     05 WR-COD-CTAC         PIC X(09) VALUE SPACES.
000000     05 WR-COD-MMCC         PIC X(02) VALUE SPACES.
000000     05 WR-DIFERENCIA       PIC S9(13)V99   COMP-3  VALUE +0.
000000     05 WR-SALDO-TOT        PIC S9(13)V99   COMP-3  VALUE +0.
000000*
000000     EJECT
000000*
000000 01  LINEA-1.
000000     05 FILLER              PIC X(30) VALUE
000000        'BANCO CREDITO INVERSIONES'.
000000     05 FILLER              PIC X(61) VALUE SPACES.
000000     05 FILLER              PIC X(20) VALUE
000000        'PGM.: DAPPLCDC      '.
000000     05 FILLER              PIC X(7) VALUE 'FECHA: '.
000000     05 LIN1-FECHA.
000000        10 LIN1-FECHA-DD    PIC X(2).
000000        10 FILLER           PIC X    VALUE '/'.
000000        10 LIN1-FECHA-MM    PIC X(2).
000000        10 FILLER           PIC X    VALUE '/'.
000000        10 LIN1-FECHA-SS    PIC X(2).
000000        10 LIN1-FECHA-AA    PIC X(2).
000000*
000000 01  LINEA-2.
000000     05 FILLER              PIC X(30) VALUE
000000        'GERENCIA DE CONTABILIDAD'.
000000     05 FILLER              PIC X(83) VALUE SPACES.
000000     05 FILLER              PIC X(11) VALUE
000000        'PAG. :   '.
000000     05 LIN2-PAG            PIC ZZZ9.
000000*
000000  01 LINEA-4D.
000000     05 FIILER              PIC X(42) VALUE SPACES.
000000     05 FILLER              PIC X(50) VALUE
000000        'CONTROL DE CUADRATURA DAP-CONTABILIDAD'.
000000*
000000  01 LINEA-4R.
000000     05 FIILER              PIC X(42) VALUE SPACES.
000000     05 FILLER              PIC X(50) VALUE
000000        'RECHAZOS PROCESO CUADRATURA DAP-CONTABILIDAD'.
000000*
000000  01 LINEA-5.
000000     05 FIILER              PIC X(49) VALUE SPACES.
000000     05 FILLER              PIC X(18) VALUE
000000        '(FECHA CONTABLE : '.
000000     05 LIN5B-FECHCONT.
000000        10 LIN5B-FECHCONT-DD           PIC X(02).
000000        10 FILLER                      PIC X(01) VALUE '/'.
000000        10 LIN5B-FECHCONT-MM           PIC X(02).
000000        10 FILLER                      PIC X(01) VALUE '/'.
000000        10 LIN5B-FECHCONT-SS           PIC X(02).
000000        10 LIN5B-FECHCONT-AA           PIC X(02).
000000     05 FILLER                         PIC X(01) VALUE ')'.
000000*
000000  01 LINEA-6.
000000     05 FILLER              PIC X(42) VALUE SPACES.
000000     05 FILLER              PIC X(50) VALUE
000000        '=========================================='.
000000*
000000 01  LINEA-8D.
000000     05 FILLER              PIC X(03) VALUE 'SUC'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(02) VALUE 'MM'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(09) VALUE ' CUENTA  '.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(21) VALUE
000000        '  SALDO CONTABLE     '.
000000     05 FILLER              PIC X(03) VALUE SPACES.
000000     05 FILLER              PIC X(21) VALUE
000000        '   SALDO CARTERA     '.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(06) VALUE 'C.OPER'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(02) VALUE 'PZ'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(06) VALUE 'MONEDA'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(21) VALUE
000000        ' SALDO OPERACION     '.
000000     05 FILLER              PIC X(03) VALUE SPACES.
000000     05 FILLER              PIC X(21) VALUE
000000        '      DIFERENCIA     '.
000000*
000000 01  LINEA-8R.
000000     05 FILLER              PIC X(03) VALUE 'SUC'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(02) VALUE 'MM'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(09) VALUE ' CUENTA  '.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(06) VALUE 'C.OPER'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(02) VALUE 'PZ'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(06) VALUE 'MONEDA'.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 FILLER              PIC X(21) VALUE
000000        ' SALDO OPERACION     '.
000000     05 FILLER              PIC X(03) VALUE SPACES.
000000     05 FILLER              PIC X(65) VALUE '        CAUSA'.
000000*
000000 01  LINEA-9.
000000     05 FILLER              PIC X(132) VALUE ALL '-'.
000000*
000000 01  LIN-DETDIF.
000000     05 DETDIF-COD-OFIC     PIC X(03).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETDIF-COD-MMCC     PIC X(02).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETDIF-COD-CTAC     PIC X(09).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETDIF-SALDMTO      PIC ZZZZ.ZZZ.ZZZ.ZZ9,99CR.
000000     05 FILLER              PIC X(03) VALUE SPACES.
000000     05 DETDIF-SALDCRED     PIC ZZZZ.ZZZ.ZZZ.ZZ9,99CR.
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETDIF-COD-COPD     PIC X(06).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETDIF-COD-PLZO     PIC X(02).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETDIF-COD-VCAM     PIC X(06).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETDIF-SALDDET      PIC ZZZZ.ZZZ.ZZZ.ZZ9,99CR.
000000     05 FILLER              PIC X(03) VALUE SPACES.
000000     05 DETDIF-DIFER        PIC ZZZZ.ZZZ.ZZZ.ZZ9,99CR.
000000*
000000 01  LIN-DETRCH.
000000     05 DETRCH-COD-OFIC     PIC X(03).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETRCH-COD-MMCC     PIC X(02).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETRCH-COD-CTAC     PIC X(09).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETRCH-COD-COPD     PIC X(06).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETRCH-COD-PLZO     PIC X(02).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETRCH-COD-VCAM     PIC X(06).
000000     05 FILLER              PIC X(01) VALUE SPACES.
000000     05 DETRCH-OPE-SALD     PIC ZZZZ.ZZZ.ZZZ.ZZ9,99CR.
000000     05 FILLER              PIC X(03) VALUE SPACES.
000000     05 DETRCH-GLOSA        PIC X(65).
000000*
000000     EJECT
000000******************************************************************
000000*      USER INFORMATION BLOCK                                    *
000000******************************************************************
000000*
000000 01  UIB.
000000     05 FILLER                 PIC X(32) VALUE 'DAPPLCDC'.
000000*
000000******************************************************************
000000*      REQUEST AREA                                              *
000000******************************************************************
000000*
000000 01  REQ-AREA-DCC.
000000     05 REQ-COMAN-DCC        PIC X(05).
000000     05 REQ-TBLNM-DCC        PIC X(03)  VALUE 'DCC'.
000000     05 REQ-KEYNM-DCC        PIC X(05)  VALUE 'DCC00'.
000000     05 REQ-RTNCD-DCC        PIC X(02)  VALUE SPACES.
000000     05 REQ-INRCD-DCC        PIC X(01).
000000     05 REQ-DBIDE-DCC        PIC S9(4)  COMP VALUE +371.
000000     05 REQ-TBLID-DCC        PIC X(07).
000000     05 FILLER               PIC X(25).
000000     05 REQ-CTMAX-DCC        PIC S9(4)  COMP.
000000     05 REQ-IOCNT-DCC        PIC S9(4)  COMP.
000000     05 FILLER               PIC X(22).
000000     05 REQ-KYVAL-DCC.
000000        10 REQ-CLAVE-DCC.
000000           15 REQ-CLAVE-DCC-COD   PIC X(03) VALUE 'DCC'.
000000           15 FILLER              PIC X(20).
000000*
000000******************************************************************
000000*                   WORK AREA                                    *
000000******************************************************************
000000*
000000 01  REG-DCC.
000000*    COPYDD TDL-R-DCC.TDL-E-DCC00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO DCC
           02  TDL-E-DCC00.
      *                             DICCIONARIO CUENTAS CONTABLES
               04  DCC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  DCC-GLS-FLAG PICTURE X(1).
      *                             LLAVE PRINCIPAL
                   06  DCC-KEY-IDCC.
      *                             TIPO + CODIGO
                       08  DCC-COD-TABL.
      *                             CODIGO TIPO TABLA
                       10  DCC-COD-TTAB
                                    PICTURE X(3).
      *                             IDENTIFICACION CUENTA CONTABLE
                       10  DCC-COD-CCTB.
      *                             IDENTIFICA TIPO CUENTA CONTABLE
                       12  DCC-COD-TCCT
                                    PICTURE X(3).
      *                             CODIGO VALOR DE CAMBIO
                       12  DCC-COD-VCAM.
      *                             MISCELANEO TIPO VALOR
                       14  DCC-MSC-TVAL
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       14  DCC-COD-CVAL
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       14  DCC-COD-TCAM
                                    PICTURE X(2).
      *                             CODIGO CLASE OPERACION DAP
                       12  DCC-COD-COPD.
      *                             CODIGO TIPO OPERACION
                       14  DCC-COD-TOPD
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR
                       14  DCC-COD-AOPD
                                    PICTURE X(3).
      *                             CODIGO PLAZO
                       12  DCC-COD-PLZO
                                    PICTURE X(2).
      *                             SITUACION CONTABLE
                       12  DCC-COD-SCTB
                                    PICTURE X(3).
      *                             TIME STAMP TRANSACCION
                   06  DCC-STP-ITRN.
      *                             FECHA TRANSACCION
                       08  DCC-FEC-FTRN.
                       10  DCC-NUM-STRN
                                    PICTURE 9(2).
                       10  DCC-NUM-ATRN
                                    PICTURE 9(2).
                       10  DCC-NUM-MTRN
                                    PICTURE 9(2).
                       10  DCC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       08  DCC-HRA-HRTR
                                    PICTURE 9(6).
      *                             INDICADOR TIPO ACCION
                   06  DCC-COD-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  DCC-COD-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  DCC-COD-OTRN PICTURE X(4).
      *                             AUTOR TRANSACCION
                   06  DCC-COD-ATRN PICTURE X(12).
      *                             TIPO + CODIGO ( INTERNO COMPUTACIONA
                   06  DCC-CIC-TABL.
      *                             CODIGO TIPO TABLA
                       08  DCC-CIC-TTAB
                                    PICTURE X(3).
      *                             IDENTIFICACION CUENTA CONTABLE
                       08  DCC-CIC-CCTB.
      *                             IDENTIFICA TIPO CUENTA CONTABLE
                       10  DCC-CIC-TCCT
                                    PICTURE X(3).
      *                             CODIGO VALOR DE CAMBIO
                       10  DCC-CIC-VCAM.
      *                             MISCELANEO TIPO VALOR
                       12  DCC-CIC-TVAL
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       12  DCC-CIC-CVAL
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       12  DCC-CIC-TCAM
                                    PICTURE X(2).
      *                             CODIGO CLASE OPERACION DAP
                       10  DCC-CIC-COPD.
      *                             CODIGO TIPO OPERACION
                       12  DCC-CIC-TOPD
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR
                       12  DCC-CIC-AOPD
                                    PICTURE X(3).
      *                             CODIGO PLAZO
                       10  DCC-CIC-PLZO
                                    PICTURE X(2).
      *                             SITUACION CONTABLE
                       10  DCC-CIC-SCTB
                                    PICTURE X(3).
      *                             CODIGO EXTERNO ( CTA SEGUN SUPER )
                   06  DCC-EXT-TABL.
      *                             CODIGO TIPO TABLA
                       08  DCC-EXT-TTAB
                                    PICTURE X(3).
      *                             CODIGO TABLA
                       08  DCC-EXT-CTAB
                                    PICTURE X(5).
      *                             CODIGO CUENTA SEGUN BANCO
                   06  DCC-GLS-CCSB PICTURE X(12).
      *                             INDICADOR DE VIGENCIA
                   06  DCC-COD-VIGE PICTURE X(1).
      *                             DISPONIBLE
                   06  DCC-GLS-DISP PICTURE X(17).
000000*
000000******************************************************************
000000*                 ELEMENT LIST                                   *
000000******************************************************************
000000*
000000 01  ELM-LIS-DCC.
000000     05 ELEM-DCC             PIC X(05)  VALUE 'DCC00'.
000000     05 ELSE-DCC             PIC X(01).
000000     05 FILLER               PIC X(05)  VALUE SPACES.
000000*
000000*-----------------------------------------------------------------
000000     EJECT
000000*
000000* TABLA DE CUENTAS CONTABLES
000000*
000000 01  TABLA-TABDCC.
000000     05 TB-TABDCC-BUS.
000000        10 TB-TABDCC-BUS-CTAC    PIC X(09) VALUE SPACES.
000000     05 TB-TABDCC-SWI-ENC        PIC 9(01) VALUE IS 0.
000000     05 TB-CNT-TABDCC            PIC 9(08) VALUE IS 0.
000000     05 TB-TABDCC       OCCURS 0 TO 1000 TIMES
000000                        DEPENDING ON TB-CNT-TABDCC
000000                        INDEXED BY IX-TABDCC.
000000        10 REG-TB-TABDCC.
000000           15 TB-TABDCC-CTA        PIC X(09).
000000*
000000     EJECT
000000*
000000* TABLA DE SALDOS CONTABLES
000000*
000000 01  TABLA-TABSDC.
000000     05 TB-TABSDC-BUS.
000000        10 TB-TABSDC-BUS-OFI     PIC 9(03) VALUE 0.
000000        10 TB-TABSDC-BUS-MDA     PIC 9(02) VALUE 0.
000000        10 TB-TABSDC-BUS-CTA     PIC 9(09) VALUE 0.
000000     05 TB-TABSDC-SAL            PIC S9(13)V99 COMP-3.
000000     05 TB-TABSDC-SWI-ENC        PIC 9(01) VALUE IS 0.
000000     05 TB-CNT-TABSDC            PIC 9(08) VALUE IS 0.
000000     05 TB-TABSDC       OCCURS 0 TO 20000 TIMES
000000                        DEPENDING ON TB-CNT-TABSDC
000000                        INDEXED BY IX-TABSDC.
000000        10 REG-TB-TABSDC.
000000           15 TB-TABSDC-KEY.
000000              20 TB-TABSDC-OFI        PIC 9(03).
000000              20 TB-TABSDC-MDA        PIC 9(02).
000000              20 TB-TABSDC-CTA        PIC 9(09).
000000           15 TB-TABSDC-SDO           PIC S9(13)V99 COMP-3.
000000*
000000     EJECT
000000*
000000* TABLA DE DETALLES DE IMPRESION (OPERACIONES)
000000*
000000 01  TABLA-TABDET.
000000     05 TB-CNT-TABDET            PIC 9(08) VALUE IS 0.
000000     05 TB-TABDET       OCCURS 0 TO 500 TIMES
000000                        DEPENDING ON TB-CNT-TABDET
000000                        INDEXED BY IX-TABDET.
000000        10 REG-TB-TABDET.
000000           15 TB-TABDET-COD-COPD         PIC X(06).
000000           15 TB-TABDET-COD-PLZO         PIC X(02).
000000           15 TB-TABDET-COD-VCAM         PIC X(06).
000000           15 TB-TABDET-OPE-SALD         PIC S9(13)V99 COMP-3.
000000*
           COPY RLOVR001.
000000     EJECT
000000*
000000* MODULO DE CONTROL
000000*
000000 PROCEDURE DIVISION.
000000*
000000 A-CONTROL SECTION.
000000     ENTRY 'DBMSCBL'.
000000     COPY BIBBAUD2.
000000*
000000 AA-INICIALIZACION.
000000     PERFORM B-INICIALIZAR.
000000*
000000 AB-PROCESAMIENTO.
000000     PERFORM C-PROCESAR.
000000*
000000 AC-TERMINACION.
000000     PERFORM D-TERMINAR.
000000*
000000 AZZ-FIN-PROC.
000000     GOBACK.
000000     EJECT
000000*
000000*
000000*
000000 B-INICIALIZAR SECTION.
000000*
000000 BA-CONTROL-PROCESO.
000000     PERFORM BB-ACCEPT-FECHA
000000     PERFORM BE-PROC-TABDCC
000000     PERFORM BG-PROC-SDOCMN
000000     PERFORM BH-PROC-SDOCMX
000000     PERFORM BL-ABRE-ARCH
000000     PERFORM BZ-VERIFICA
000000     GO TO BZZ-FIN-SECC.
000000*
000000 BB-ACCEPT-FECHA.
000000* ACCEPTAR FECHA
000000     ACCEPT WE-FECHA-DATE FROM DATE
000000     MOVE WE-DD-DATE TO LIN1-FECHA-DD
000000     MOVE WE-MM-DATE TO LIN1-FECHA-MM
000000     MOVE WE-AA-DATE TO LIN1-FECHA-AA
000000*****IF WE-AA-DATE NOT < 80
000000     IF WE-AA-DATE < ROLL-YY
000000        MOVE 20      TO LIN1-FECHA-SS
000000     ELSE
000000        MOVE 19      TO LIN1-FECHA-SS.
000000*
000000 BE-PROC-TABDCC.
000000* POSICIONA EN TABLA
000000     MOVE 'LOCKY'             TO REQ-COMAN-DCC
000000     MOVE 'DCC'               TO REQ-TBLNM-DCC
000000     MOVE 'DCC00'             TO REQ-KEYNM-DCC
000000     MOVE +371                TO REQ-DBIDE-DCC
000000     MOVE SPACES              TO REQ-KYVAL-DCC
000000     MOVE 'DCC'               TO REQ-CLAVE-DCC-COD
000000     PERFORM BE2-CALL-DCC
000000*
000000     IF REQ-RTNCD-DCC NOT = SPACES
000000        DISPLAY '** ERROR ACCESO (TABLA DCC 371)'
000000        DISPLAY 'REQ ' REQ-AREA-DCC
000000        DISPLAY 'WKA ' REG-DCC
000000     ELSE
000000        IF REQ-CLAVE-DCC-COD = 'DCC'
000000           MOVE 'REDKY'               TO REQ-COMAN-DCC
000000           PERFORM BE2-CALL-DCC
000000           PERFORM BE1-CARGA-TABDCC UNTIL
000000              REQ-CLAVE-DCC-COD NOT = 'DCC' OR
000000                 REQ-RTNCD-DCC NOT = SPACES.
000000     IF TB-CNT-TABDCC = 0
000000        DISPLAY '** NO HAY REGISTROS EN TABLA DCC'
000000        MOVE 1                  TO SW-CANCEL.
000000*
000000 BE1-CARGA-TABDCC.
000000     ADD 1                      TO TB-CNT-TABDCC
000000     IF TB-CNT-TABDCC > 1000
000000        DISPLAY '** TABLA DE TABDCC EXCEDIDA (1000)'
000000        MOVE 1                  TO SW-CANCEL
000000     ELSE
000000        SET IX-TABDCC           TO TB-CNT-TABDCC
000000        MOVE DCC-GLS-CCSB       TO TB-TABDCC-CTA (IX-TABDCC).
000000     MOVE 'REDNX'               TO REQ-COMAN-DCC
000000     PERFORM BE2-CALL-DCC.
000000*
000000 BE2-CALL-DCC.
000000     CALL 'DBNTRY' USING UIB REQ-AREA-DCC REG-DCC ELM-LIS-DCC.
000000*
000000 BG-PROC-SDOCMN.
000000     OPEN INPUT SDOCMN
000000     PERFORM BG1-LEER-SDOCMN
000000     PERFORM BG2-CARGA-SDOCMN UNTIL
000000        FS-SDOCMN = 1 OR
000000           SW-CANCEL = 1
000000     CLOSE      SDOCMN.
000000*
000000 BG1-LEER-SDOCMN.
000000     READ SDOCMN AT END MOVE 1 TO FS-SDOCMN.
000000     IF FS-SDOCMN = 0
000000        ADD 1 TO CNT-SDOCMN-L.
000000*
000000 BG2-CARGA-SDOCMN.
000000     IF SDOCMN-MON = 00
000000        MOVE SDOCMN-CTA       TO TB-TABDCC-BUS-CTAC
000000        PERFORM BI1-BUSCA-TABDCC
000000        IF TB-TABDCC-SWI-ENC = 1
000000           MOVE SDOCMN-OFI    TO TB-TABSDC-BUS-OFI
000000           MOVE SDOCMN-MON    TO TB-TABSDC-BUS-MDA
000000           MOVE SDOCMN-CTA    TO TB-TABSDC-BUS-CTA
000000           MOVE SDOCMN-SALDO  TO TB-TABSDC-SAL
000000           PERFORM BI2-CARGA-TABSDC
000000           ADD 1 TO CNT-SDOCMN-C.
000000     PERFORM BG1-LEER-SDOCMN.
000000*
000000 BH-PROC-SDOCMX.
000000     OPEN INPUT SDOCMX
000000     IF FS-SDOCMX = 0
000000        PERFORM BH1-LEER-SDOCMX
000000        PERFORM BH2-CARGA-SDOCMX UNTIL
000000           FS-SDOCMX = 10 OR
000000              SW-CANCEL = 1
000000     ELSE
000000        DISPLAY
000000           '*** ERROR OPEN SDOCMX.  FS= ' FS-SDOCMX
000000        MOVE 1 TO SW-CANCEL.
000000     CLOSE      SDOCMX.
000000*
000000 BH1-LEER-SDOCMX.
000000     READ SDOCMX RECORD
000000     IF FS-SDOCMX = 0
000000        ADD 1 TO CNT-SDOCMX-L
000000     ELSE
000000        IF FS-SDOCMX = 10
000000           NEXT SENTENCE
000000        ELSE
000000           DISPLAY '*** ERROR READ SDOCMX.  FS= ' FS-SDOCMX
000000           MOVE 1 TO SW-CANCEL.
000000*
000000 BH2-CARGA-SDOCMX.
000000     IF SDOCMX-COD-MONE = 01
000000        IF SDOCMX-COD-COAN < 1000
000000           MOVE SDOCMX-NUM-CNTA     TO TB-TABDCC-BUS-CTAC
000000           PERFORM BI1-BUSCA-TABDCC
000000           IF TB-TABDCC-SWI-ENC = 1
000000              MOVE SDOCMX-COD-COAN  TO TB-TABSDC-BUS-OFI
000000              MOVE SDOCMX-COD-MONE  TO TB-TABSDC-BUS-MDA
000000              MOVE SDOCMX-NUM-CNTA  TO TB-TABSDC-BUS-CTA
000000              MOVE SDOCMX-NUM-SALD  TO TB-TABSDC-SAL
000000              PERFORM BI2-CARGA-TABSDC
000000              IF TB-TABSDC-SWI-ENC = 0
000000                 ADD 1 TO CNT-SDOCMX-C.
000000     PERFORM BH1-LEER-SDOCMX.
000000*
000000 BI1-BUSCA-TABDCC.
000000* BUSCA CUENTA CONTABLE
000000     MOVE 0                   TO TB-TABDCC-SWI-ENC
000000     SET  IX-TABDCC                          TO 1
000000     SEARCH TB-TABDCC
000000        WHEN
000000           TB-TABDCC-CTA (IX-TABDCC) = TB-TABDCC-BUS
000000              MOVE 1                   TO TB-TABDCC-SWI-ENC.
000000*
000000 BI2-CARGA-TABSDC.
000000* SI NO EXISTE EL SALDO CONTABLE, SE CARGA EN TABLA DE MEMORIA
000000* SI YA EXISTE, EL SALDO ES INCREMENTADO.
000000     PERFORM BI3-BUSCA-TABSDC
000000     IF TB-TABSDC-SWI-ENC = 0
000000* REGISTRO A CARGAR NO EXISTE, SALDO SE CARGA
000000        ADD 1                      TO TB-CNT-TABSDC
000000        IF TB-CNT-TABSDC > 20000
000000           DISPLAY '** TABLA DE TABSDC EXCEDIDA (20000)'
000000           MOVE 1                  TO SW-CANCEL
000000        ELSE
000000           SET IX-TABSDC           TO TB-CNT-TABSDC
000000           MOVE TB-TABSDC-BUS      TO TB-TABSDC-KEY (IX-TABSDC)
000000           MOVE TB-TABSDC-SAL      TO TB-TABSDC-SDO (IX-TABSDC)
000000     ELSE
000000* REGISTRO A CARGAR YA EXISTE, SALDO SE INCREMENTA
000000        ADD  TB-TABSDC-SAL         TO TB-TABSDC-SDO (IX-TABSDC).
000000*
000000 BI3-BUSCA-TABSDC.
000000* DETERMINA LA EXISTENCIA DE SALDO CONTABLE PARA OFICINA EN TABLA
000000     SET  IX-TABSDC                          TO 1
000000     SEARCH TB-TABSDC
000000        AT END
000000           MOVE 0                            TO TB-TABSDC-SWI-ENC
000000        WHEN
000000           TB-TABSDC-KEY (IX-TABSDC) = TB-TABSDC-BUS
000000              MOVE 1                         TO TB-TABSDC-SWI-ENC.
000000*
000000 BL-ABRE-ARCH.
000000     OPEN INPUT  DAPDCUA
000000          OUTPUT DAPLDIF DAPLRCH.
000000*
000000 BZ-VERIFICA.
000000     IF SW-CANCEL = 1
000000        DISPLAY '*** ERRORES EN INICIALIZACION ***'
000000        PERFORM D-TERMINAR.
000000*
000000 BZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 C-PROCESAR SECTION.
000000*
000000 CA-CONTROL.
000000     SORT FSORT
000000        ASCENDING KEY
000000           FSORT-COD-OFIC
000000           FSORT-COD-MMCC
000000           FSORT-COD-CTAC
000000           FSORT-COD-COPD
000000           FSORT-COD-PLZO
000000              INPUT  PROCEDURE  E-INGRESA
000000              OUTPUT PROCEDURE  F-PAREA.
000000     IF SORT-RETURN NOT = 0
000000        DISPLAY '*** HUBO PROBLEMAS EN EL SORT ***'
000000        DISPLAY '*** HUBO PROBLEMAS EN EL SORT ***'
000000        MOVE 1 TO SW-CANCEL
000000        PERFORM D-TERMINAR.
000000*
000000 CZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 D-TERMINAR  SECTION.
000000*
000000 DA-TOTALES.
000000     CLOSE DAPDCUA DAPLDIF DAPLRCH
000000     DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA DAPPLCDC'
000000     DISPLAY ' '
000000     DISPLAY 'CUENTAS CONTABLES CARGADAS (DCC).' TB-CNT-TABDCC
000000     DISPLAY 'REG. SDOCMN LEIDOS...............' CNT-SDOCMN-L
000000     DISPLAY 'SALDOS CONTABLES  CARGAD (MN)....' CNT-SDOCMN-C
000000     DISPLAY 'REG. SDOCMX LEIDOS...............' CNT-SDOCMX-L
000000     DISPLAY 'SALDOS CONTABLES  CARGAD (MX)....' CNT-SDOCMX-C
000000     DISPLAY 'SALDOS CONTABLES  CARGAD (TOTAL).' TB-CNT-TABSDC
000000     DISPLAY 'REG. CUADRATURA LEIDOS...........' CNT-DAPDCUA-L
000000     DISPLAY 'REG. GRABADOS AL SORT............' CNT-FSORT-G
000000     DISPLAY 'REG. LEIDOS DEL SORT.............' CNT-FSORT-L
000000     DISPLAY 'TOTAL REG. NO PAREADOS...........' CNT-NO-PAREAD
000000     DISPLAY 'TOTAL REG. SI PAREADOS...........' CNT-SI-PAREAD
000000     DISPLAY ' '
000000     IF SW-CANCEL    = 1
000000        DISPLAY ' HAY MENSAJES PARA EL EXPLOTADOR'
000000        DISPLAY ' '
000000        DISPLAY ' '
000000        DISPLAY ' '
000000        DISPLAY '************************************************'
000000        DISPLAY '************************************************'
000000        DISPLAY '****                                        ****'
000000        DISPLAY '****      TERMINO ERRONEO DEL PROCESO       ****'
000000        DISPLAY '****                                        ****'
000000        DISPLAY '************************************************'
000000        DISPLAY '************************************************'
000000        MOVE 100 TO RETURN-CODE
000000        GOBACK
000000     ELSE
000000        DISPLAY ' NO HAY MENSAJES PARA EL EXPLOTADOR'
000000        DISPLAY ' '
000000        DISPLAY ' '
000000        DISPLAY ' '
000000        DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL ***'
000000     END-IF.
000000*
000000 DZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 E-INGRESA SECTION.
000000*
000000 EA-LEER-DAPDCUA.
000000     READ DAPDCUA AT END MOVE 1 TO FS-DAPDCUA.
000000     IF FS-DAPDCUA = 0
000000        ADD 1 TO CNT-DAPDCUA-L.
000000*
000000 EB-CTRL-PROC.
000000     PERFORM EC-GRABA-FSORT UNTIL
000000        FS-DAPDCUA = 1
000000     GO TO EZZ-FIN-SECC.
000000*
000000 EC-GRABA-FSORT.
000000     MOVE DAPDCUA-COD-OFIC    TO FSORT-COD-OFIC
000000     MOVE DAPDCUA-COD-MMCC    TO FSORT-COD-MMCC
000000     MOVE DAPDCUA-COD-CTAC    TO FSORT-COD-CTAC
000000     MOVE DAPDCUA-COD-TPCC    TO FSORT-COD-TPCC
000000     MOVE DAPDCUA-COD-COPD    TO FSORT-COD-COPD
000000     MOVE DAPDCUA-COD-PLZO    TO FSORT-COD-PLZO
000000     MOVE DAPDCUA-COD-VCAM    TO FSORT-COD-VCAM
000000     MOVE DAPDCUA-OPE-SALD    TO FSORT-OPE-SALD
000000     MOVE DAPDCUA-FEC-REFE    TO FSORT-FEC-REFE
000000     RELEASE REG-FSORT
000000     ADD 1                    TO CNT-FSORT-G
000000     PERFORM EA-LEER-DAPDCUA.
000000*
000000 EZZ-FIN-SECC.
000000     EXIT.
000000     EJECT
000000*
000000*
000000*
000000 F-PAREA SECTION.
000000*
000000 FA-LEER-FSORT.
000000     RETURN FSORT AT END MOVE 1 TO FS-FSORT.
000000     IF FS-FSORT = 0
000000        ADD 1 TO CNT-FSORT-L.
000000*
000000 FC-CTRL-PROC.
000000     MOVE 99                 TO CNT-LINE-DAPLDIF
000000     MOVE 0                  TO CNT-PAGE-DAPLDIF
000000     MOVE 99                 TO CNT-LINE-DAPLRCH
000000     MOVE 0                  TO CNT-PAGE-DAPLRCH
000000     MOVE FSORT-FEC-REFE-DD  TO LIN5B-FECHCONT-DD
000000     MOVE FSORT-FEC-REFE-MM  TO LIN5B-FECHCONT-MM
000000     MOVE FSORT-FEC-REFE-AA  TO LIN5B-FECHCONT-AA
000000     MOVE FSORT-FEC-REFE-SS  TO LIN5B-FECHCONT-SS
000000     DISPLAY ' '
000000     DISPLAY 'SALDOS DE CARTERA AL: ' FSORT-FEC-REFE
000000     DISPLAY ' '
000000     PERFORM FD-CTRL-X-OFIC UNTIL
000000        FS-FSORT = 1
000000     GO TO FZZ-FIN-SECC.
000000*
000000 FD-CTRL-X-OFIC.
000000     MOVE FSORT-COD-OFIC    TO WR-COD-OFIC
000000     MOVE 0                 TO SW-IMPR-DIF
000000                               SW-IMPR-RCH
000000     PERFORM FE-CTRL-X-MMCC UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FS-FSORT = 1
000000     IF SW-IMPR-DIF = 1
000000        MOVE SPACES         TO REG-DAPLDIF
000000        WRITE REG-DAPLDIF  AFTER 3
000000        ADD 3               TO CNT-LINE-DAPLDIF.
000000     IF SW-IMPR-RCH = 1
000000        MOVE SPACES         TO REG-DAPLRCH
000000        WRITE REG-DAPLRCH  AFTER 3
000000        ADD 3               TO CNT-LINE-DAPLRCH.
000000*
000000 FE-CTRL-X-MMCC.
000000     MOVE FSORT-COD-MMCC    TO WR-COD-MMCC
000000     PERFORM FF-CTRL-X-CTAC UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-COD-MMCC NOT = WR-COD-MMCC OR
000000              FS-FSORT = 1.
000000*
000000 FF-CTRL-X-CTAC.
000000     MOVE FSORT-COD-CTAC         TO WR-COD-CTAC
000000     MOVE +0                     TO WR-SALDO-TOT
000000     MOVE 0                      TO TB-CNT-TABDET
000000     PERFORM FG-CARGA-TABDET UNTIL
000000        FSORT-COD-OFIC NOT = WR-COD-OFIC OR
000000           FSORT-COD-MMCC NOT = WR-COD-MMCC OR
000000              FSORT-COD-CTAC NOT = WR-COD-CTAC OR
000000                 FS-FSORT = 1
000000     PERFORM FH-ANALIZA-SALDOS.
000000*
000000 FG-CARGA-TABDET.
000000     ADD 1 TO TB-CNT-TABDET
000000     IF TB-CNT-TABDET > 500
000000        DISPLAY ' '
000000        DISPLAY '*** TABLA TABDET EXCEDIDA ***'
000000        DISPLAY '*** EXCEDE LAS 500 OCURRENCIAS      ***'
000000        DISPLAY ' '
000000        MOVE 16 TO SORT-RETURN
000000     ELSE
000000        SET IX-TABDET       TO TB-CNT-TABDET
000000        MOVE FSORT-COD-COPD TO TB-TABDET-COD-COPD (IX-TABDET)
000000        MOVE FSORT-OPE-SALD TO TB-TABDET-OPE-SALD (IX-TABDET)
000000        MOVE FSORT-COD-PLZO TO TB-TABDET-COD-PLZO (IX-TABDET)
000000        MOVE FSORT-COD-VCAM TO TB-TABDET-COD-VCAM (IX-TABDET)
000000        ADD FSORT-OPE-SALD  TO WR-SALDO-TOT.
000000     PERFORM FA-LEER-FSORT.
000000*
000000 FH-ANALIZA-SALDOS.
000000     MOVE WR-COD-OFIC  TO TB-TABSDC-BUS-OFI
000000     MOVE WR-COD-MMCC  TO TB-TABSDC-BUS-MDA
000000     MOVE WR-COD-CTAC  TO TB-TABSDC-BUS-CTA
000000     PERFORM FI-BUSCA-TABSDC
000000     IF TB-TABSDC-SWI-ENC = 0
000000        PERFORM FJ-IMPRIMIR-RECHAZO
000000     ELSE
000000        IF WR-SALDO-TOT NOT = TB-TABSDC-SAL
000000           ADD 1 TO CNT-NO-PAREAD
000000           PERFORM FK-IMPRIMIR-DETALLE
000000        ELSE
000000           ADD 1 TO CNT-SI-PAREAD.
000000*
000000 FI-BUSCA-TABSDC.
000000* BUSCA SALDO CONTABLE
000000     SET  IX-TABSDC                          TO 1
000000     SEARCH TB-TABSDC
000000        AT END
000000           MOVE 0                            TO TB-TABSDC-SWI-ENC
000000        WHEN
000000           TB-TABSDC-KEY (IX-TABSDC) = TB-TABSDC-BUS
000000              MOVE TB-TABSDC-SDO (IX-TABSDC) TO TB-TABSDC-SAL
000000              MOVE 1                         TO TB-TABSDC-SWI-ENC.
000000*
000000 FJ-IMPRIMIR-RECHAZO.
000000     MOVE 1            TO SW-IMPR-RCH
000000     MOVE WR-COD-OFIC  TO DETRCH-COD-OFIC
000000     MOVE WR-COD-MMCC  TO DETRCH-COD-MMCC
000000     MOVE WR-COD-CTAC  TO DETRCH-COD-CTAC
000000     PERFORM FJ1-IMPRIMIR-RECHAZO
000000        VARYING IX-TABDET FROM 1 BY 1 UNTIL
000000           IX-TABDET > TB-CNT-TABDET
000000     MOVE SPACES       TO REG-DAPLRCH
000000     WRITE REG-DAPLRCH  AFTER 1
000000     ADD 1             TO CNT-LINE-DAPLRCH.
000000*
000000 FJ1-IMPRIMIR-RECHAZO.
000000     IF SALTO-PAG-DAPLRCH
000000        PERFORM FQ-TITULO-RCH.
000000     MOVE TB-TABDET-COD-COPD (IX-TABDET) TO DETRCH-COD-COPD
000000     MOVE TB-TABDET-COD-PLZO (IX-TABDET) TO DETRCH-COD-PLZO
000000     MOVE TB-TABDET-COD-VCAM (IX-TABDET) TO DETRCH-COD-VCAM
000000     MOVE TB-TABDET-OPE-SALD (IX-TABDET) TO DETRCH-OPE-SALD
000000     MOVE '** REGISTRO NO EXISTE EN TABLA DE SALDOS **' TO
000000          DETRCH-GLOSA
000000     WRITE REG-DAPLRCH FROM LIN-DETRCH AFTER 1
000000     ADD 1                               TO CNT-LINE-DAPLRCH.
000000*
000000 FK-IMPRIMIR-DETALLE.
000000     MOVE 1                   TO SW-IMPR-DIF
000000     MOVE SPACES              TO LIN-DETDIF
000000     MOVE WR-COD-OFIC         TO DETDIF-COD-OFIC
000000     MOVE WR-COD-MMCC         TO DETDIF-COD-MMCC
000000     MOVE WR-COD-CTAC         TO DETDIF-COD-CTAC
000000     MOVE TB-TABSDC-SAL       TO DETDIF-SALDMTO
000000     MOVE WR-SALDO-TOT        TO DETDIF-SALDCRED
000000     PERFORM FK1-IMPRIMIR-DETALLE
000000        VARYING IX-TABDET FROM 1 BY 1 UNTIL
000000           IX-TABDET > TB-CNT-TABDET
000000     MOVE SPACES              TO REG-DAPLDIF
000000     WRITE REG-DAPLDIF  AFTER 1
000000     ADD 1                    TO CNT-LINE-DAPLDIF.
000000*
000000 FK1-IMPRIMIR-DETALLE.
000000     IF SALTO-PAG-DAPLDIF
000000        PERFORM FP-TITULO-DIF.
000000     MOVE TB-TABDET-COD-COPD (IX-TABDET) TO DETDIF-COD-COPD
000000     MOVE TB-TABDET-COD-PLZO (IX-TABDET) TO DETDIF-COD-PLZO
000000     MOVE TB-TABDET-COD-VCAM (IX-TABDET) TO DETDIF-COD-VCAM
000000     MOVE TB-TABDET-OPE-SALD (IX-TABDET) TO DETDIF-SALDDET
000000     IF IX-TABDET = TB-CNT-TABDET
000000        COMPUTE WR-DIFERENCIA =
000000           TB-TABSDC-SAL - WR-SALDO-TOT
000000        MOVE WR-DIFERENCIA               TO DETDIF-DIFER.
000000     WRITE REG-DAPLDIF FROM LIN-DETDIF AFTER 1
000000     ADD 1                               TO CNT-LINE-DAPLDIF
000000     MOVE SPACES                         TO LIN-DETDIF.
000000*
000000 FP-TITULO-DIF.
000000     ADD 1                 TO CNT-PAGE-DAPLDIF
000000     MOVE CNT-PAGE-DAPLDIF TO LIN2-PAG
000000     WRITE REG-DAPLDIF FROM LINEA-1  AFTER CANAL-1
000000     WRITE REG-DAPLDIF FROM LINEA-2  AFTER 1
000000     WRITE REG-DAPLDIF FROM LINEA-4D AFTER 2
000000     WRITE REG-DAPLDIF FROM LINEA-5  AFTER 1
000000     WRITE REG-DAPLDIF FROM LINEA-6  AFTER 1
000000     WRITE REG-DAPLDIF FROM LINEA-9  AFTER 2
000000     WRITE REG-DAPLDIF FROM LINEA-8D AFTER 1
000000     WRITE REG-DAPLDIF FROM LINEA-9  AFTER 1
000000     MOVE 10               TO CNT-LINE-DAPLDIF.
000000*
000000 FQ-TITULO-RCH.
000000     ADD 1                 TO CNT-PAGE-DAPLRCH
000000     MOVE CNT-PAGE-DAPLRCH TO LIN2-PAG
000000     WRITE REG-DAPLRCH FROM LINEA-1  AFTER CANAL-1
000000     WRITE REG-DAPLRCH FROM LINEA-2  AFTER 1
000000     WRITE REG-DAPLRCH FROM LINEA-4R AFTER 2
000000     WRITE REG-DAPLRCH FROM LINEA-5  AFTER 1
000000     WRITE REG-DAPLRCH FROM LINEA-6  AFTER 1
000000     WRITE REG-DAPLRCH FROM LINEA-9  AFTER 2
000000     WRITE REG-DAPLRCH FROM LINEA-8R AFTER 1
000000     WRITE REG-DAPLRCH FROM LINEA-9  AFTER 1
000000     MOVE 10               TO CNT-LINE-DAPLRCH.
000000*
000000 FZZ-FIN-SECC.
000000     EXIT.
000000     EJECT

