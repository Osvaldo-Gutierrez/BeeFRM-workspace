      * IMPRIME GARANTIAS DEL DEUDOR, MEDIANTE RPT DGA
       GEN-RDGA SECTION.
       INI-GEN-RDGA.

           GENERATE DL-HDGA.

           MOVE SPACES                   TO GAR-CLV-GDD-1 IN GDD.
           MOVE WSS-CIC-ICLI IN WSS-VARI TO GDD-CIC-ICLI  IN GDD.
           MOVE FIO-GET-NLS TO FIO-CMND.
      *
       GET-NXT-GDD.
           PERFORM GAR-FIO-GDD.
           IF NOT ( FIO-STAT-OKS AND
                    WSS-CIC-ICLI IN WSS-VARI = GDD-CIC-ICLI IN GDD )
               GO TO FIN-GEN-RDGA.

      * BUSCA GARANTIA
           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GDG.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GDG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               DISPLAY 'INCONSISTENCIA ENTRE GDD Y GDG'
               DISPLAY 'GARANTIA NRO ' GAR-CLV-GDG-1 IN GDG
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO GET-NXT-GDD.

      * GARANTIA COMPLETA Y VIGENTE A FECHA DEL INFORME
           IF NOT ( GAR-IND-CPL IN GDG = 'S' AND
                    GAR-FEC-ESC-CNS IN GDG NOT > WSS-AMD-FFOT AND
                    ( GAR-ES1 IN GDG > '09' AND < '20' OR
                      GAR-ES1 IN GDG > '19' AND
                      GAR-FEC-ALZ     IN GDG > WSS-AMD-FFOT )
                  )
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO GET-NXT-GDD.

      * EXAMINA SI GARANTIA TIENE MULTIPLES DEUDORES
           MOVE FIO-END-BRW TO FIO-CMND.
           PERFORM GAR-FIO-GDD.

      *    SE POSICIONA POR LA LLAVE DEL HIJO
           MOVE 'GAR-CLV-GDD-2' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-GDD.

      *    BUSCA SIGUIENTE POR LA LLAVE DEL HIJO
           MOVE 'GAR-CLV-GDD-2' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.

           PERFORM GAR-FIO-GDD.
           IF FIO-STAT-OKS AND
              GAR-CLV-GDG-1  IN GDG = GAR-CLV-GDD-2  IN GDD
                MOVE 'GAR-CLV-GDD-2' TO FIO-AKEY
                MOVE FIO-END-BRW TO FIO-CMND
                PERFORM GAR-FIO-GDD
                MOVE 'S' TO WSS-IND-ODEU IN WSS-DGA
           ELSE
                MOVE 'N' TO WSS-IND-ODEU IN WSS-DGA
                IF FIO-STAT-OKS
                    MOVE 'GAR-CLV-GDD-2' TO FIO-AKEY
                    MOVE FIO-END-BRW TO FIO-CMND
                    PERFORM GAR-FIO-GDD.

      * REPOSICIONA EN GDD
           MOVE WSS-CIC-ICLI IN WSS-VARI TO GDD-CIC-ICLI  IN GDD.
           MOVE GAR-CLV-GDG-1 IN GDG     TO GAR-CLV-GDD-2 IN GDD.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-GDD.

      * BUSCA TASACION VIGENTE A LA FECHA DEL INFORME
           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GVT.
           MOVE ZEROES             TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS OR
              (GAR-NUM-SIS IN GVT NOT = GAR-NUM-SIS IN GDD) OR
              (GAR-COD-CRR IN GVT NOT = GAR-COD-CRR IN GDD)
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO GET-NXT-GDD.

           MOVE '00' TO FIO-STAT.
           MOVE 'N'  TO WSS-CRITER-FIN.
           MOVE GAR-COD-TSN IN GVT TO WSS-COD-TSN.
           PERFORM BUS-TSN-VIG UNTIL NOT FIO-STAT-OKS.
           IF WSS-CRITER-FIN = 'N'
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO GET-NXT-GDD.

           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GVT.
           MOVE WSS-COD-TSN        TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL REPOSICIONAR EN TASACION '
               DISPLAY 'TASACION NRO ' GAR-CLV-GVT-1 IN GVT
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO GET-NXT-GDD.

      * BUSCA ATRIBUTOS DE GARANTIA
           MOVE GAR-TIP-OPE IN GDG TO GAR-TIP-OPE IN TAG.
           MOVE GAR-STP-OPE IN GDG TO GAR-STP-OPE IN TAG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE TAG ' GAR-TIP-OPE IN TAG
                                        GAR-STP-OPE IN TAG
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO GET-NXT-GDD.

      * BUS-TAB BUSCA GLOSA DE TABLA REAL / NO REAL
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'RAL' TO TAB-COD-TTAB IN TAB.
           MOVE GAR-TIP-REA-NRE IN TAG TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-REAL IN WSS-DGA.
      * BUS-MSC BUSCA GLOSA DE MISCELANEO EN TABLA
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'LIM' TO TAB-COD-TTAB IN TAB.
           MOVE GAR-COD-LIM IN GDG TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-CGAR IN WSS-DGA.
      * BUS-TAB BUSCA GLOSA DE CODIGO EN TABLA
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'VLR' TO TAB-COD-TTAB IN TAB.
           MOVE GAR-VCB IN GDG TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VCGA IN WSS-DGA.
      * BUS-OFI BUSCA OFICINA EN TABLAS.
           MOVE GAR-COD-OFI IN GDG TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFGA IN WSS-DGA.

      * BUSCA SEGURO
           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GSE.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GSE.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO GAR-IIV-CIA-SEG IN GSE
                              GAR-VCB-SEG     IN GSE
               MOVE ZEROES TO GAR-VAL-SEG     IN GSE
           ELSE
           IF GAR-IIV-CIA-SEG IN GSE > SPACES
      * BUS-TAB BUSCA GLOSA DE CODIGO EN TABLA
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'CSE' TO TAB-COD-TTAB IN TAB
               MOVE GAR-IIV-CIA-SEG IN GSE TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-SEGG IN WSS-DGA.

      * BUS-TAB BUSCA GLOSA DE CODIGO EN TABLA
           IF FIO-STAT-OKS
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE GAR-VCB-SEG IN GSE TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VCSG IN WSS-DGA.

      * BUS-TAB BUSCA GLOSA DE CODIGO EN TABLA
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'TIO' TO TAB-COD-TTAB IN TAB.
           MOVE GAR-TIP-OPE IN GDG TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-TOGA IN WSS-DGA.

           GENERATE DL-DGA.

      * BUSCA VINCULACION CON COLOCACIONES
           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GES.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GES.
           MOVE ZEROES             TO GAR-COD-ESP IN GES.
           MOVE FIO-GET-NLS TO FIO-CMND.
       GET-NXT-GES.
           PERFORM GAR-FIO-GES.
           IF NOT ( FIO-STAT-OKS AND
                    GAR-ID IN GES = GAR-CLV-GDG-1 IN GDG )
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO GET-NXT-GDD.

      * BUSCA GLOSA TIPO DE MONEDA
           IF GAR-TIP-VCB-CDT IN GES > SPACES AND
              GAR-COD-VCB-CDT IN GES NOT > SPACES
               MOVE 'TAB' TO MSC-COD-SIST
               MOVE 'TVAL' TO MSC-COD-TMSC IN MSC
               MOVE GAR-TIP-VCB-CDT IN GES TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DCOR IN MSC TO WSS-GLS-TVCG IN WSS-DGA.
      * BUS-TAB BUSCA GLOSA DE TIPO OPERACION
           IF GAR-TIP-OPE-CDT IN GES > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'TIO' TO TAB-COD-TTAB IN TAB
               MOVE GAR-TIP-OPE-CDT IN GES TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR TO WSS-GLS-TOCG IN WSS-DGA.
      * BUS-TAB BUSCA GLOSA COD AUXILIAR DE TIPO DE OPERACION
           IF GAR-STP-OPE-CDT IN GES > SPACES
               MOVE 'COL' TO TAB-COD-SIST
               MOVE 'AUX' TO TAB-COD-TTAB IN TAB
               MOVE GAR-STP-OPE-CDT IN GES TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-COCG IN WSS-DGA.
      * BUS-TAB BUSCA GLOSA DE CODIGO EN TABLA
           IF GAR-VCB-CDT IN GES > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE GAR-VCB-CDT IN GES TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VCCG IN WSS-DGA.
      * BUS-TAB BUSCA GLOSA DE CODIGO EN TABLA
           IF GAR-COD-DST-CDT IN GES > SPACES
               MOVE 'COL' TO TAB-COD-SIST
               MOVE 'DES' TO TAB-COD-TTAB IN TAB
               MOVE GAR-COD-DST-CDT IN GES TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-CDCG IN WSS-DGA.
      * BUS-TAB BUSCA GLOSA DE CODIGO EN TABLA
           IF GAR-VCB-LIM-CDT IN GES > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE GAR-VCB-LIM-CDT IN GES TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VCLC IN WSS-DGA.
      * FIN BUSQUEDA DE GLOSAS.

           GENERATE DL-GES.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO GET-NXT-GES.

       FIN-GEN-RDGA.
           EXIT.

       BUS-TSN-VIG SECTION.
       INI-BUS-TSN-VIG.

           IF GAR-FEC-RVL IN GVT NOT > WSS-AMD-FFOT
               MOVE 'S'                TO WSS-CRITER-FIN
               MOVE GAR-COD-TSN IN GVT TO WSS-COD-TSN
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM GAR-FIO-GVT
               IF NOT FIO-STAT-OKS OR
                  (GAR-NUM-SIS IN GVT NOT = GAR-NUM-SIS IN GDG) OR
                  (GAR-COD-CRR IN GVT NOT = GAR-COD-CRR IN GDG)
                   MOVE '23' TO FIO-STAT
               ELSE
                   MOVE '00' TO FIO-STAT
           ELSE
               MOVE '23' TO FIO-STAT.
       FIN-BUS-TSN-VIG.
           EXIT.

