       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.   TABPBTXC.
       AUTHOR.       BEE TEAM.
       DATE-WRITTEN. 19-APR-1999 12:22:15.

      * Programa procesa Tasas Maximas Convencionales

       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY TABWGTXC.
       COPY TABBRTXC.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TXC-REQA==.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       01  PGM-VARI.
           03 PGM-MNNR   VALUE '0999  '  PIC X(06).
           03 PGM-MNRJ   VALUE '0998  '  PIC X(06).
           03 PGM-HVAL   VALUE 999999999 PIC 9(09).
           03 PGM-PLZO                   PIC X(04).
           03 PGM-VCOC                   PIC X(06).
           03 PGM-VALO                   PIC 9(09).
           03 PGM-FECH.
              05 PGM-DECH                PIC 9(02).
              05 PGM-MECH                PIC 9(02).
              05 PGM-SECH                PIC 9(02).
              05 PGM-AECH                PIC 9(02).
           03 PGM-ATX1            COMP-3 PIC S9(11)V9(4).
           03 PGM-ATX2            COMP-3 PIC S9(11)V9(4).
           03 PGM-AUX1                   PIC S9(11)V9(4).
           03 PGM-AUX2                   PIC S9(11).

       LINKAGE SECTION.
      *----------------
       01  LNK-VARI                     PIC X(148).

       PROCEDURE DIVISION USING LNK-VARI.
      *==================================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE LNK-VARI TO TXC-CMMA.
           MOVE SPACES TO TXC-CMSG TXC-MENS.
           MOVE 'TABPBTXC' TO FIO-PROG.
           IF TXC-FEC1 > ZEROES AND TXC-FEC2 NOT > ZEROES
               PERFORM TXC-PRO-FECHA
           ELSE
           IF TXC-FEC1 > ZEROES AND TXC-FEC2 > ZEROES
               PERFORM TXC-PRO-TRAMO
           ELSE
               MOVE 'TXCFEC1=ZERO' TO TXC-CMSG.
       MSG-MAIN.
           MOVE ZEROES TO TXC-MNTO.
           MOVE ZEROES TO TXC-FEC1.
           MOVE ZEROES TO TXC-FEC2.
           MOVE TXC-CMMA TO LNK-VARI.
       FIN-MAIN.
           EXIT PROGRAM.

       TXC-PRO-FECHA SECTION.
       INI-TXC-PRO-FECHA.
           MOVE TXC-DEC1 TO PGM-DECH.
           MOVE TXC-MEC1 TO PGM-MECH.
           MOVE TXC-SEC1 TO PGM-SECH.
           MOVE TXC-AEC1 TO PGM-AECH.
           PERFORM TXC-BUS-VTXC.
           IF TXC-CMSG > SPACES
               GO TO FIN-TXC-PRO-FECHA.
           MOVE TXC-NUM-DTXC IN TXC TO FEC-DEC1.
           MOVE TXC-NUM-MTXC IN TXC TO FEC-MEC1.
           MOVE TXC-NUM-STXC IN TXC TO FEC-SEC1.
           MOVE TXC-NUM-ATXC IN TXC TO FEC-AEC1.
           MOVE TXC-DEC1            TO FEC-DEC2.
           MOVE TXC-MEC1            TO FEC-MEC2.
           MOVE TXC-SEC1            TO FEC-SEC2.
           MOVE TXC-AEC1            TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'TXCDIF DIA' TO TXC-CMSG
               GO TO FIN-TXC-PRO-FECHA.
           IF TXC-COD-BASE IN TXC = 'A'
               COMPUTE TXC-ATXC ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * FEC-NDIA
           ELSE
               COMPUTE TXC-ATXC ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * 12 * FEC-NDIA.
       FIN-TXC-PRO-FECHA.
           EXIT.

       TXC-PRO-TRAMO SECTION.
       INI-TXC-PRO-TRAMO.
           MOVE TXC-DEC1 TO PGM-DECH.
           MOVE TXC-MEC1 TO PGM-MECH.
           MOVE TXC-SEC1 TO PGM-SECH.
           MOVE TXC-AEC1 TO PGM-AECH.
           PERFORM TXC-BUS-VTXC.
           IF TXC-CMSG > SPACES
               GO TO FIN-TXC-PRO-TRAMO.
           MOVE TXC-NUM-DTXC IN TXC TO FEC-DEC1.
           MOVE TXC-NUM-MTXC IN TXC TO FEC-MEC1.
           MOVE TXC-NUM-STXC IN TXC TO FEC-SEC1.
           MOVE TXC-NUM-ATXC IN TXC TO FEC-AEC1.
           MOVE TXC-DEC1            TO FEC-DEC2.
           MOVE TXC-MEC1            TO FEC-MEC2.
           MOVE TXC-SEC1            TO FEC-SEC2.
           MOVE TXC-AEC1            TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'TXCDIF DIA' TO TXC-CMSG
               GO TO FIN-TXC-PRO-TRAMO.
           IF TXC-COD-BASE IN TXC = 'A'
               COMPUTE PGM-ATX1 ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * FEC-NDIA
           ELSE
               COMPUTE PGM-ATX1 ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * 12 * FEC-NDIA.
      *
           MOVE TXC-DEC2 TO PGM-DECH.
           MOVE TXC-MEC2 TO PGM-MECH.
           MOVE TXC-SEC2 TO PGM-SECH.
           MOVE TXC-AEC2 TO PGM-AECH.
           PERFORM TXC-BUS-VTXC.
           IF TXC-CMSG > SPACES
               GO TO FIN-TXC-PRO-TRAMO.
           MOVE TXC-NUM-DTXC IN TXC TO FEC-DEC1.
           MOVE TXC-NUM-MTXC IN TXC TO FEC-MEC1.
           MOVE TXC-NUM-STXC IN TXC TO FEC-SEC1.
           MOVE TXC-NUM-ATXC IN TXC TO FEC-AEC1.
           MOVE TXC-DEC2            TO FEC-DEC2.
           MOVE TXC-MEC2            TO FEC-MEC2.
           MOVE TXC-SEC2            TO FEC-SEC2.
           MOVE TXC-AEC2            TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'TXCDIF DIA' TO TXC-CMSG
               GO TO FIN-TXC-PRO-TRAMO.
           IF TXC-COD-BASE IN TXC = 'A'
               COMPUTE PGM-ATX2 ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * FEC-NDIA
           ELSE
               COMPUTE PGM-ATX2 ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * 12 * FEC-NDIA.
      *
           COMPUTE TXC-ATXC ROUNDED = ( PGM-ATX2 - PGM-ATX1 ) / 360.
       FIN-TXC-PRO-TRAMO.
           EXIT.

       TXC-BUS-VTXC SECTION.
       INI-TXC-BUS-VTXC.
      *Busca el tramo al que pertenece
           MOVE SPACES TO TXC-CMSG TXC-MENS.
           MOVE TXC-MNTO TO PGM-AUX1.
           MOVE TXC-MNTO TO PGM-AUX2.
           SUBTRACT PGM-AUX2 FROM PGM-AUX1.
           IF PGM-AUX1 > 0
               ADD TXC-MNTO 1 GIVING PGM-VALO
           ELSE
               MOVE TXC-MNTO TO PGM-VALO.
      *
           IF PGM-VALO > PGM-HVAL
               MOVE PGM-HVAL TO PGM-VALO.
           IF TXC-VCOC = PGM-MNRJ
               MOVE '*'      TO PGM-PLZO
               MOVE TXC-VCOC TO PGM-VCOC
           ELSE
           IF TXC-VCOC = PGM-MNNR
               MOVE TXC-VCOC TO PGM-VCOC
      *Dado que, por ahora, solo se manejan tramos para M.N. No
      *reajustable para plazos sobre y bajo 90 dias, solo en estos
      *casos se exige obligatoriamente que el programa que
      *llama a la rutina cargue la variable monto (TXC-MNTO).
      *Si en el futuro se manejan tramos para otros casos, entonces
      *esta validacion debe hacerse extensiva a dichos casos.
               IF TXC-MNTO NOT > 0
                   MOVE 'TXCMNTO=ZERO' TO TXC-CMSG
                   GO TO FIN-TXC-BUS-VTXC
               ELSE
               IF TXC-PLZO-B90
                   MOVE 'T$B9' TO PGM-PLZO
               ELSE
                   MOVE 'T$S9' TO PGM-PLZO
      *************************************************************
           ELSE
               MOVE '*'    TO PGM-PLZO
               MOVE 'MEXT' TO PGM-VCOC.
           MOVE PGM-PLZO TO TXC-COD-PLZO IN TXC.
           MOVE PGM-VCOC TO TXC-COD-VCOC IN TXC.
           MOVE PGM-DECH TO TXC-NUM-DTXC IN TXC.
           MOVE PGM-MECH TO TXC-NUM-MTXC IN TXC.
           MOVE PGM-SECH TO TXC-NUM-STXC IN TXC.
           MOVE PGM-AECH TO TXC-NUM-ATXC IN TXC.
           SUBTRACT PGM-VALO FROM PGM-HVAL GIVING
                            TXC-NUM-CMST IN TXC.
           MOVE TXC-KEY-ITXC IN TXC TO TXC-MEN2.
       LUP-TXC-BUS-VTXC.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM TAB-FIO-TXC.
           IF NOT ( FIO-STAT-OKS AND
                    TXC-COD-PLZO IN TXC = PGM-PLZO AND
                    TXC-COD-VCOC IN TXC = PGM-VCOC )
               MOVE 'TXCTASANOFND' TO TXC-CMSG
               GO TO FIN-TXC-BUS-VTXC.
           IF TXC-MSC-STAT IN TXC NOT = 'V'
               MOVE TXC-NUM-DTXC IN TXC TO FEC-ITM1
               MOVE TXC-NUM-MTXC IN TXC TO FEC-ITM2
               MOVE TXC-NUM-STXC IN TXC TO FEC-ITM3
               MOVE TXC-NUM-ATXC IN TXC TO FEC-ITM4
               MOVE FEC-FORM-FEC TO FEC-FORM
               MOVE FEC-RST-DIA  TO FEC-CMND
               MOVE 1            TO FEC-NDIA
               PERFORM CAL-FEC
               IF NOT FEC-STAT-OKS
                   MOVE 'TXCRST DIAER' TO TXC-CMSG
                   GO TO FIN-TXC-BUS-VTXC
               ELSE
                   MOVE FEC-ITM1 TO TXC-NUM-DTXC IN TXC
                   MOVE FEC-ITM2 TO TXC-NUM-MTXC IN TXC
                   MOVE FEC-ITM3 TO TXC-NUM-STXC IN TXC
                   MOVE FEC-ITM4 TO TXC-NUM-ATXC IN TXC
                   GO TO LUP-TXC-BUS-VTXC.
      *
      *Analizar si obtuve un tramo con fecha distinta a la fecha
      *solicitada (PGM-FECH), si obtuve un tramo con fecha distinta
      *entonces encontre el tramo inferior de esa fecha y no necesariame
      *el tramo que le corresponde al monto TXC-MNTO.
      *
           IF TXC-NUM-DTXC IN TXC = PGM-DECH AND
              TXC-NUM-MTXC IN TXC = PGM-MECH AND
              TXC-NUM-STXC IN TXC = PGM-SECH AND
              TXC-NUM-ATXC IN TXC = PGM-AECH
               GO TO CON-TXC-BUS-VTXC.
           IF PGM-VALO NOT > TXC-NUM-MSTR IN TXC
               GO TO CON-TXC-BUS-VTXC.
      *
           SUBTRACT PGM-VALO FROM PGM-HVAL GIVING
                            TXC-NUM-CMST IN TXC.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM TAB-FIO-TXC.
           IF NOT ( FIO-STAT-OKS AND
                    TXC-COD-PLZO IN TXC = PGM-PLZO AND
                    TXC-COD-VCOC IN TXC = PGM-VCOC )
               MOVE 'TXCTASANOFND' TO TXC-CMSG
               GO TO FIN-TXC-BUS-VTXC.
       CON-TXC-BUS-VTXC.
           MOVE TXC-SGV-VTXC IN TXC TO TXC-VTXC.
           MOVE TXC-COD-BASE IN TXC TO TXC-BASE.
           MOVE SPACES              TO TXC-MENS.
       FIN-TXC-BUS-VTXC.
           EXIT.

       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY TABBFTXC.
       COPY GNSBGDTC.

      *--- Para eliminar el acceso a tablas se emulan las rutinas
      *                 GET-MSG Y GNS-FIO-TAB.
      *La rutina GET-MSG se invoca para buscar un mensaje
      *La rutina GNS-FIO-TAB se invoca para verificar si un dia es
      *feriado (por lo tanto nunca es invocada con los comandos
      *FEC-DIF-DIA o FEC-RST-DIA, utilizados en este programa)
       GET-MSG SECTION.
       INI-GET-MSG.
           MOVE MSG-COD-MENS IN MSG TO MSG-GLS-DESC IN MSG.
       FIN-GET-MSG.
           EXIT.

       GNS-FIO-TAB SECTION.
       INI-GNS-FIO-TAB.
           MOVE '23' TO FIO-STAT.
       FIN-GNS-FIO-TAB.
           EXIT.
