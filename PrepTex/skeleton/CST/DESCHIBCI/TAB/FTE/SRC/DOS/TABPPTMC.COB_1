       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.   TABPPTMC.
       AUTHOR.       BEE TEAM.
       DATE-WRITTEN. 19-APR-1999 12:22:15.

      * Programa entrega Tramos Tasas Maximas Convencionales

       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY TABWGTMC.
       COPY TABBRTXC.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TXC-REQA==.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGFRM.
       COPY GNSWGSYS.
       01  PGM-VARI.
           03 PGM-MNNR   VALUE '0999  '  PIC X(06).
           03 PGM-MNRJ   VALUE '0998  '  PIC X(06).
           03 PGM-HVAL   VALUE 999999999 PIC 9(09).
           03 PGM-KBUS.
              05 PGM-PLZO                PIC X(04).
              05 PGM-VCOC                PIC X(06).
              05 PGM-FECH.
                 07 PGM-DECH             PIC 9(02).
                 07 PGM-MECH             PIC 9(02).
                 07 PGM-SECH             PIC 9(02).
                 07 PGM-AECH             PIC 9(02).
           03 PGM-FTXC.
              05 PGM-STXC                PIC 9(02).
              05 PGM-ATXC                PIC 9(02).
              05 PGM-MTXC                PIC 9(02).
              05 PGM-DTXC                PIC 9(02).
           03 PGM-MTRA          VALUE 12 PIC 9(02).
           03 PGM-I                      PIC 9(02).

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                    PIC X(422).
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE DFHCOMMAREA TO TMC-CMMA.
           MOVE SPACES TO TMC-CMSG TMC-MENS.
           MOVE 'TABPPTMC' TO FIO-PROG.
           IF TMC-FECH > ZEROES
               PERFORM BUS-TRAMOS-TMC
           ELSE
               MOVE 'TMCFECH=ZERO' TO TMC-CMSG.
       MSG-MAIN.
           MOVE ZEROES TO TMC-FECH.
           MOVE TMC-CMMA TO DFHCOMMAREA.
       FIN-MAIN.
       COPY GNSBGGBK.

       BUS-TRAMOS-TMC SECTION.
       INI-BUS-TRAMOS-TMC.
           MOVE 0 TO PGM-I.
           MOVE SPACES TO TMC-CMSG TMC-MENS.
           MOVE TMC-DECH TO PGM-DECH.
           MOVE TMC-MECH TO PGM-MECH.
           MOVE TMC-SECH TO PGM-SECH.
           MOVE TMC-AECH TO PGM-AECH.
           IF TMC-VCOC = PGM-MNRJ
               MOVE '*'      TO PGM-PLZO
               MOVE TMC-VCOC TO PGM-VCOC
           ELSE
           IF TMC-VCOC = PGM-MNNR
               MOVE TMC-VCOC TO PGM-VCOC
               IF TMC-PLZO-B90
                   MOVE 'T$B9' TO PGM-PLZO
               ELSE
                   MOVE 'T$S9' TO PGM-PLZO
           ELSE
               MOVE '*'    TO PGM-PLZO
               MOVE 'MEXT' TO PGM-VCOC.
           MOVE PGM-PLZO TO TXC-COD-PLZO IN TXC.
           MOVE PGM-VCOC TO TXC-COD-VCOC IN TXC.
           MOVE PGM-DECH TO TXC-NUM-DTXC IN TXC.
           MOVE PGM-MECH TO TXC-NUM-MTXC IN TXC.
           MOVE PGM-SECH TO TXC-NUM-STXC IN TXC.
           MOVE PGM-AECH TO TXC-NUM-ATXC IN TXC.
           MOVE PGM-HVAL TO TXC-NUM-CMST IN TXC.
           MOVE FIO-GET-LEQ TO FIO-CMND.
       LUP-BUS-TRAMOS-TMC.
           PERFORM TAB-FIO-TXC.
           IF FIO-STAT-OKS AND
              TXC-COD-PLZO IN TXC = PGM-PLZO AND
              TXC-COD-VCOC IN TXC = PGM-VCOC
               IF TXC-MSC-STAT IN TXC = 'V'
                   PERFORM CARGA-TABLA
                   GO TO FIN-BUS-TRAMOS-TMC
               ELSE
      *Para posicionarse en el 1er reg. se mueve 0 TO TXC-NUM-CMST
                   MOVE 0 TO TXC-NUM-CMST IN TXC
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM TAB-FIO-TXC
                   IF FIO-STAT-OKS
                       MOVE FIO-GET-PRV TO FIO-CMND
                       GO TO LUP-BUS-TRAMOS-TMC
                   ELSE
                       MOVE 'TMCTRMONOFND' TO TMC-CMSG
                       MOVE PGM-KBUS       TO TMC-MENS
           ELSE
               MOVE 'TMCTRMONOFND' TO TMC-CMSG
               MOVE PGM-KBUS       TO TMC-MENS.
       FIN-BUS-TRAMOS-TMC.
           ADD 1 TO PGM-I.
           PERFORM LIMPIAR-TABLA VARYING PGM-I FROM PGM-I BY 1
                   UNTIL PGM-I > PGM-MTRA.
       EXT-BUS-TRAMOS-TMC.
           EXIT.

       CARGA-TABLA SECTION.
       INI-CARGA-TABLA.
           MOVE SPACES TO TMC-CMSG TMC-MENS
           MOVE TXC-FEC-FTXC IN TXC TO PGM-FTXC.
           MOVE 0 TO PGM-I.
       LUP-CARGA-TABLA.
           IF TXC-MSC-ANTI IN TXC NOT = 'U'
               GO TO NXT-CARGA-TABLA.
           ADD 1 TO PGM-I.
           IF PGM-I > PGM-MTRA
               MOVE 'TMCTRMOOVFLW' TO TMC-CMSG
               MOVE PGM-KBUS       TO TMC-MENS
               GO TO FIN-CARGA-TABLA.
      *
           MOVE TXC-NUM-MSTR IN TXC TO TMC-MSTR(PGM-I).
           MOVE TXC-SGV-VTXC IN TXC TO TMC-VTMC(PGM-I).
           MOVE TXC-NUM-DTXC IN TXC TO FEC-DEC1.
           MOVE TXC-NUM-MTXC IN TXC TO FEC-MEC1.
           MOVE TXC-NUM-STXC IN TXC TO FEC-SEC1.
           MOVE TXC-NUM-ATXC IN TXC TO FEC-AEC1.
           MOVE PGM-DECH            TO FEC-DEC2.
           MOVE PGM-MECH            TO FEC-MEC2.
           MOVE PGM-SECH            TO FEC-SEC2.
           MOVE PGM-AECH            TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'TMCDIF DIA' TO TMC-CMSG
               GO TO FIN-CARGA-TABLA.
           IF TXC-COD-BASE IN TXC = 'A'
               COMPUTE TMC-ATMC(PGM-I) ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * FEC-NDIA
           ELSE
               COMPUTE TMC-ATMC(PGM-I) ROUNDED = TXC-SGV-ATXC IN TXC +
                   TXC-SGV-VTXC IN TXC * 12 * FEC-NDIA.
           MOVE TXC-COD-BASE IN TXC TO TMC-BASE(PGM-I).
      *
       NXT-CARGA-TABLA.
           MOVE FIO-GET-PRV TO FIO-CMND.
           PERFORM TAB-FIO-TXC.
           IF FIO-STAT-OKS AND
              TXC-COD-PLZO IN TXC = PGM-PLZO AND
              TXC-COD-VCOC IN TXC = PGM-VCOC AND
              TXC-FEC-FTXC IN TXC = PGM-FTXC
               GO TO LUP-CARGA-TABLA.
       FIN-CARGA-TABLA.
           EXIT.

       LIMPIAR-TABLA SECTION.
       INI-LIMPIAR-TABLA.
           MOVE  0     TO TMC-MSTR(PGM-I).
           MOVE +0     TO TMC-VTMC(PGM-I).
           MOVE +0     TO TMC-ATMC(PGM-I).
           MOVE SPACES TO TMC-BASE(PGM-I).
       FIN-LIMPIAR-TABLA.
           EXIT.

       COPY GNSBIABT.
       COPY GNSBGSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY TABBFTXC.
       COPY GNSBGDTC.

      *--- Para eliminar el acceso a tablas se emulan las rutinas
      *                 GET-MSG Y GNS-FIO-TAB.
      *La rutina GET-MSG se invoca para buscar un mensaje
      *La rutina GNS-FIO-TAB se invoca para verificar si un dia es
      *feriado (por lo tanto nunca es invocada con los comandos
      *FEC-DIF-DIA o FEC-RST-DIA, utilizados en este programa)
       GET-MSG SECTION.
       INI-GET-MSG.
           MOVE MSG-COD-MENS IN MSG TO MSG-GLS-DESC IN MSG.
       FIN-GET-MSG.
           EXIT.

       GNS-FIO-TAB SECTION.
       INI-GNS-FIO-TAB.
           MOVE '23' TO FIO-STAT.
       FIN-GNS-FIO-TAB.
           EXIT.
