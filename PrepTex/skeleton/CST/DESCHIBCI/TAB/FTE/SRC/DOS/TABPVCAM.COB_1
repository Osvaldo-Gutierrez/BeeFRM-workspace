       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      TABPVCAM.
       AUTHOR.          BEE-TEAM(rvm).
       DATE-WRITTEN.    28-APR-1997.

      * Programa valida que exista el VALOR DE CAMBIO para una fecha

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
      *
       COPY GNSWGSYS.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGHOY.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY TABBRCAM.
      *
       01  WSS-VARI.
      *
           03  WSS-CAM.
               05  WSS-FEC-FCAM.
                   07  WSS-NUM-SCAM              PIC X(02).
                   07  WSS-NUM-ACAM              PIC X(02).
                   07  WSS-NUM-MCAM              PIC X(02).
                   07  WSS-NUM-DCAM              PIC X(02).
               05  WSS-COD-VCAM1                 PIC X(06).
               05  WSS-COD-VCAM2                 PIC X(06).
      *
           03  WSS-IND-RBAD     VALUE SPACES     PIC X(01).
           03  WSS-IND-CNEX     VALUE SPACES     PIC X(01).
           03  WSS-IND-CVIG     VALUE SPACES     PIC X(01).
           03  WSS-VAL-VCAM     VALUE ZEROES   PIC ---.---.---.---,----.
      *
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'TABPVCAM' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM TAB-FIO-CAM.
           DISPLAY 'STATUS DE APERTURA TABCAM : ' FIO-STAT.

           PERFORM OPEN-FILE.
           PERFORM VAL-CAM.
           PERFORM CLOSE-FILE.

           IF WSS-IND-CNEX IN WSS-VARI = 'S' AND
              WSS-IND-RBAD IN WSS-VARI = 'S'
               DISPLAY ' '
               DISPLAY 'TABPVCAM CANCELADO...'
               PERFORM GNS-PRO-ABT.

           IF WSS-IND-CVIG IN WSS-VARI = 'S' AND
              WSS-IND-RBAD IN WSS-VARI = 'S'
               DISPLAY ' '
               DISPLAY 'TABPVCAM CANCELADO...'
               PERFORM GNS-PRO-ABT.

           DISPLAY ' '.
           DISPLAY 'TABPVCAM TERMINO NORMAL...'.
           STOP RUN.
       FIN-MAIN.
           EXIT.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           DISPLAY 'STATUS DE APERTURA TABTAB : ' FIO-STAT.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           DISPLAY 'STATUS DE APERTURA GNSTAB : ' FIO-STAT.

       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       VAL-CAM SECTION.
       INI-VAL-CAM.
           MOVE ZEROES TO WSS-FEC-FCAM  IN WSS-VARI.
           MOVE SPACES TO WSS-COD-VCAM1 IN WSS-VARI.
           MOVE SPACES TO WSS-COD-VCAM2 IN WSS-VARI.

      *OBTIENE FECHA Y HORA DE PROCESO
           PERFORM GET-FHOY.
           DISPLAY ' '.
           DISPLAY 'FECHA DE PROCESO : ' HOY-FHOY IN HOY-VARI.
           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY 'SI NO EXISTE CAM, CANCELAR ? (S/*) : '.
           ACCEPT  WSS-IND-CNEX IN WSS-VARI.
           DISPLAY WSS-IND-CNEX IN WSS-VARI.

           DISPLAY 'SI VIGENCIA NO VALIDA EN CAM, CANCELAR ? (S/*) : '.
           ACCEPT  WSS-IND-CVIG IN WSS-VARI.
           DISPLAY WSS-IND-CVIG IN WSS-VARI.

           DISPLAY ' '.
           DISPLAY 'INGRESE CAM A VALIDAR (SSAAMMDDXXXXXXZZZZZZ) : '.
       LUP-VAL-CAM.
           MOVE SPACES TO WSS-CAM.
           ACCEPT WSS-CAM.

           IF WSS-CAM IN WSS-VARI = '99999999999999999999' OR
              WSS-CAM IN WSS-VARI NOT > SPACES
               GO TO FIN-VAL-CAM.
      *
           IF WSS-FEC-FCAM IN WSS-VARI = 'FPROC   '
               MOVE HOY-SHOY IN HOY-VARI TO CAM-NUM-SCAM IN CAM(1)
                                            CAM-NUM-SCAM IN CAM(2)
               MOVE HOY-AHOY IN HOY-VARI TO CAM-NUM-ACAM IN CAM(1)
                                            CAM-NUM-ACAM IN CAM(2)
               MOVE HOY-MHOY IN HOY-VARI TO CAM-NUM-MCAM IN CAM(1)
                                            CAM-NUM-MCAM IN CAM(2)
               MOVE HOY-DHOY IN HOY-VARI TO CAM-NUM-DCAM IN CAM(1)
                                            CAM-NUM-DCAM IN CAM(2)
           ELSE
               MOVE WSS-NUM-SCAM IN WSS-VARI TO CAM-NUM-SCAM IN CAM(1)
                                                CAM-NUM-SCAM IN CAM(2)
               MOVE WSS-NUM-ACAM IN WSS-VARI TO CAM-NUM-ACAM IN CAM(1)
                                                CAM-NUM-ACAM IN CAM(2)
               MOVE WSS-NUM-MCAM IN WSS-VARI TO CAM-NUM-MCAM IN CAM(1)
                                                CAM-NUM-MCAM IN CAM(2)
               MOVE WSS-NUM-DCAM IN WSS-VARI TO CAM-NUM-DCAM IN CAM(1)
                                                CAM-NUM-DCAM IN CAM(2).

           MOVE WSS-COD-VCAM1 IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-VCAM2 IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'CAM NO EXISTE AL : '
                       WSS-FEC-FCAM IN WSS-VARI ' DE : '
                       CAM-COD-VCAM IN CAM(1) ' A  : '
                       CAM-COD-VCAM IN CAM(2)
               MOVE 'S' TO WSS-IND-RBAD IN WSS-VARI
           ELSE
               IF CAM-IND-VIGE IN CAM NOT = 'S'
                   DISPLAY 'CAM NO VIG AL : '
                       WSS-FEC-FCAM IN WSS-VARI ' DE : '
                       CAM-COD-VCAM IN CAM(1) ' A  : '
                       CAM-COD-VCAM IN CAM(2)
                   MOVE 'S' TO WSS-IND-RBAD IN WSS-VARI
               ELSE
                   MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VCAM IN WSS-VARI
                   DISPLAY 'CAM PARA : '
                       WSS-FEC-FCAM IN WSS-VARI ' DE : '
                       CAM-COD-VCAM IN CAM(1) ' A  : '
                       CAM-COD-VCAM IN CAM(2) ' ES DE : '
                       WSS-VAL-VCAM IN WSS-VARI.
      *
           GO TO LUP-VAL-CAM.
       FIN-VAL-CAM.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM TAB-FIO-CAM.

           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
       COPY TABBFCAM.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBBSYS.
       COPY GNSBGHOY.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBTABT.
