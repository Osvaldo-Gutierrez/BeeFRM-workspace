       IDENTIFICATION DIVISION.
      ************************
       PROGRAM-ID. SUPPLI03.
      *AUTHOR. DONOSO JARA OSVALDO.
      *DATE-WRITTEN,  12/04/91.
      *DATE-COMPILED, 12/04/91.
      *REMARKS. SISTEMA SUPER BANCOS
      *         ANALISTA NUNEZ SERGIO.
000000*COMPILED SITE 2000          21.09.98
      *---------------------------------------------------------------*
      * Modificacion  1 : Se debe omitir la linea correspondiente a   *
      *                  "hijo menor de..." de la caratula            *
      *                                                 ODJ MAY/91    *
      *
      * Modificacion  2 :                                             *
      *            Programador: David Parraguez Muoz.............    *
      *            Fecha      : 02/07/98                              *
      *            Cambio     : Se inserta siglo en encabezado del    *
      *                         archivo que se graba en la cinta para *
      *                         la super                              *
      *---------------------------------------------------------------*
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 13 DE MAYO DE 1998                                    *
      ******************************************************************
      *
       ENVIRONMENT DIVISION.
      *********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *====================
       FILE-CONTROL.
           SELECT SUPLI03 ASSIGN   TO    UT-S-SUPLI03.

           SELECT SUPVI03 ASSIGN   TO    SUPVI03
                  ORGANIZATION IS INDEXED
                  ACCESS       IS SEQUENTIAL
                  RECORD KEY   IS I03-CLAVE
                  FILE STATUS IS WR-FS-SUPVI03.

           SELECT SUPVCIN ASSIGN   TO     UT-S-SUPVCIN
                  FILE STATUS IS WR-FS-SUPVCIN.

       DATA DIVISION.
      **************
       FILE SECTION.
      *============
       FD  SUPLI03      LABEL RECORD OMITTED.
       01  SUPRI03                   PIC X(80).

       FD  SUPVI03      LABEL RECORD STANDARD.
       COPY SUPBRI03.

       FD  SUPVCIN      LABEL RECORD STANDARD
                        BLOCK CONTAINS 0 RECORDS.
       01  SUPRCIN                   PIC X(62).
       EJECT
       WORKING-STORAGE SECTION.
      *=======================
       SKIP1
       01  W-PARAMETROS.
           05 FILLER                    PIC X(34).
           05 WP-FECHA.
              10 WP-MES                 PIC X(02).
              10 FILLER                 PIC X(01).
              10 WP-ANO                 PIC X(02).
       SKIP1
       01  W-SWITCHES.
           05 WS-TERMINO                PIC 9(01)  VALUE 0.
              88 WS-TERMINO-OK      VALUE 0.
              88 WS-TERMINO-ANORMAL VALUE 1.
       SKIP1
       01  W-INDICES.
           05 WI-I                      PIC 9(03)  VALUE 0.
       SKIP1
       01  W-TABLAS.
           05 WT-DETALLE.
              10 WT-TIP-PERS            PIC 9(07) OCCURS 11 TIMES.
           05 WT-GLS-DETALLE.
              10 FILLER PIC X(45) VALUE 'TOTAL DE PERSONAS INFORMADAS'.
              10 FILLER PIC X(45) VALUE 'PRESIDENTE DEL DIRECTORIO'.
              10 FILLER PIC X(45) VALUE 'VICEPRESIDENTE DEL DIRECTORIO'.
              10 FILLER PIC X(45) VALUE 'DIRECTOR'.
              10 FILLER PIC X(45) VALUE 'DIRECTOR SUPLENTE'.
              10 FILLER PIC X(45) VALUE 'AGENTE DE BANCO EXTRANJERO'.
              10 FILLER PIC X(45) VALUE 'GERENTE GENERAL'.
              10 FILLER PIC X(45) VALUE 'OTRO APODERADO GENERAL'.
              10 FILLER PIC X(45) VALUE 'CONYUGE DE DIRECTOR O APODERADO
      -       ' GENERAL'.
              10 FILLER PIC X(45) VALUE 'HIJO MENOR DE DIRECTOR O OPEDER
      -       'ADO GENERAL'.
              10 FILLER PIC X(45) VALUE 'SOCIEDAD CON PARTICIPACION > 5
      -       '%'.
           05 FILLER REDEFINES WT-GLS-DETALLE OCCURS 11 TIMES.
              10 WT-GLS                 PIC X(45).
           05 WT-MESES.
              10 FILLER PIC X(10) VALUE '  ENERO   '.
              10 FILLER PIC X(10) VALUE ' FEBRERO  '.
              10 FILLER PIC X(10) VALUE '  MARZO   '.
              10 FILLER PIC X(10) VALUE '  ABRIL   '.
              10 FILLER PIC X(10) VALUE '   MAYO   '.
              10 FILLER PIC X(10) VALUE '  JUNIO   '.
              10 FILLER PIC X(10) VALUE '  JULIO   '.
              10 FILLER PIC X(10) VALUE '  AGOSTO  '.
              10 FILLER PIC X(10) VALUE 'SEPTIEMBRE'.
              10 FILLER PIC X(10) VALUE ' OCTUBRE  '.
              10 FILLER PIC X(10) VALUE 'NOVIEMBRE '.
              10 FILLER PIC X(10) VALUE 'DICIEMBRE '.
           05 FILLER REDEFINES WT-MESES OCCURS 12 TIMES.
              10 WT-MES                 PIC X(10).
       SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-I03                  PIC 9(06).
           05 WA-R-I03                  PIC 9(06).
           05 WA-G-I03                  PIC 9(06).
           05 WA-L-CIN                  PIC 9(06).
           05 WA-R-CIN                  PIC 9(06).
           05 WA-G-CIN                  PIC 9(06).
       SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-SECCION                 PIC X(15).
           05 WR-PARRAFO                 PIC X(15).
           05 WR-ARCHIVO                 PIC X(10).
           05 WR-OPERACI                 PIC X(10).
           05 WR-FILE-ST                 PIC X(02).
           05 WR-MENSAJE                 PIC X(35).
           05 WR-FECHA-HOY               PIC X(08).
           05 WR-FS-SUPVI03              PIC 9(02) VALUE 0.
           05 WR-FS-SUPVCIN              PIC 9(02) VALUE 0.
           05 WR-1ER-REG.
              10 WR-CODIGO-INST          PIC X(03) VALUE '016'.
              10 WR-CODIGO-ARCH          PIC X(03) VALUE 'I03'.
      * DPM ini  2-JUL-1998
              10 WR-SIG                  PIC 9(02).
      * DPM fin  2-JUL-1998
              10 WR-ANO                  PIC 9(02).
              10 WR-MES                  PIC 9(02).
              10 FILLER                  PIC X(42) VALUE SPACES.

           05 WR-LINEA01.
              10 FILLER         PIC X(020) VALUE 'INSTITUCION :  BANCO'.
              10 FILLER         PIC X(020) VALUE ' DE CREDITO E INVERS'.
              10 FILLER         PIC X(005) VALUE 'IONES'.
              10 FILLER         PIC X(022) VALUE ALL ' '.
              10 FILLER         PIC X(012) VALUE 'CODIGO : 016'.

           05 WR-LINEA02.
              10 FILLER         PIC X(020) VALUE 'INFORMACION CORRESPO'.
              10 FILLER         PIC X(020) VALUE 'NDIENTE AL MES DE :'.
              10 WR-LA02-MES    PIC X(010).

      **** Y2K-COMIENZO (INTERSOFT)
      *       10 FILLER         PIC X(006) VALUE ' DE 19'.
              10 FILLER         PIC X(004) VALUE ' DE '.
              10 WR-LA02-SIG    PIC X(002).
      **** Y2K-FINAL    (INTERSOFT)

              10 WR-LA02-ANO    PIC X(002).
              10 FILLER         PIC X(020) VALUE '         ARCHIVO: I0'.
              10 FILLER         PIC X(001) VALUE ALL '3'.

           05 WR-LINEA03.
              10 FILLER         PIC X(059) VALUE ALL ' '.
              10 FILLER         PIC X(019) VALUE 'PERSONAS INFORMADAS'.

           05 WR-LINEA04.
              10 FILLER         PIC X(001) VALUE ALL '.'.
              10 FILLER         PIC X(077) VALUE ALL '-'.
              10 FILLER         PIC X(001) VALUE ALL '.'.

           05 WR-LINEA05.
              10 FILLER         PIC X(002) VALUE '|'.
              10 WR-LA05-GLS    PIC X(045).
              10 FILLER         PIC X(018) VALUE '        :  |'.
              10 WR-LA05-NUM    PIC Z.ZZZ.ZZ9.
              10 FILLER         PIC X(005) VALUE '    |'.

           05 WR-LINEA06.
              10 FILLER         PIC X(001) VALUE ALL '|'.
              10 FILLER         PIC X(057) VALUE ALL '-'.
              10 FILLER         PIC X(020) VALUE '|-------------------'.
              10 FILLER         PIC X(001) VALUE ALL '|'.

           05 WR-LINEA07.
              10 FILLER         PIC X(001) VALUE ALL ''''.
              10 FILLER         PIC X(077) VALUE ALL '-'.
              10 FILLER         PIC X(001) VALUE ALL ''''.

           05 WR-LINEA08.
              10 FILLER         PIC X(017) VALUE '   RESPONSABLE :'.
              10 FILLER         PIC X(028) VALUE ALL '.'.

           05 WR-LINEA09.
              10 FILLER         PIC X(017) VALUE '   FONO        :'.
              10 FILLER         PIC X(012) VALUE ALL '.'.
              10 FILLER         PIC X(026) VALUE ALL ' '.
              10 FILLER         PIC X(015) VALUE ALL '.'.

           05 WR-LINEA10.
              10 FILLER         PIC X(058) VALUE ALL ' '.
              10 FILLER         PIC X(007) VALUE 'GERENTE'.

       EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
      *------------------
           PERFORM B-INICIALIZAR.
       AB-PROCESAMIENTO.
      *-----------------
           IF WS-TERMINO-OK
              PERFORM C-PROCESAR
           END-IF.
       AC-TERMINACION.
      *---------------
           PERFORM D-TERMINAR.
       AZ-FIN-DE-PROCESO.
      *------------------
           STOP RUN.
       EJECT
       B-INICIALIZAR SECTION.
      *=======================
       BA-INICIALES.
      *-------------
           INITIALIZE WT-DETALLE W-CONTADORES-Y-ACUMULADORES.
           OPEN INPUT  SUPVI03
                OUTPUT SUPLI03 SUPVCIN.
           IF (WR-FS-SUPVI03 NOT = 0 AND NOT = 97)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BA-INICIALES'      TO WR-PARRAFO
               MOVE 'SUPVI03'           TO WR-ARCHIVO
               MOVE  'OPEN'             TO WR-OPERACI
               MOVE  WR-FS-SUPVI03      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO BZ-FIN-DE-SECCION
           END-IF.

           IF (WR-FS-SUPVCIN NOT = 0)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BA-INICIALES'      TO WR-PARRAFO
               MOVE 'SUPVCIN'           TO WR-ARCHIVO
               MOVE  'OPEN'             TO WR-OPERACI
               MOVE  WR-FS-SUPVCIN      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO BZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       BB-VALIDA.
      *----------
           ACCEPT W-PARAMETROS.
           IF (WP-MES < '01' OR WP-MES > '12' OR
               WP-ANO NOT NUMERIC)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BB-VALIDA'         TO WR-PARRAFO
               MOVE 'SYSIN'             TO WR-ARCHIVO
               MOVE  '** FECHA INVALIDA **' TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO BZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
       EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GENERAL.
      *-------------------
           MOVE WP-MES     TO WR-MES
           MOVE WP-ANO     TO WR-ANO
      * DPM ini  2-JUL-1998
           IF WR-ANO > 50
               MOVE 19 TO WR-SIG
           ELSE
               MOVE 20 TO WR-SIG
           END-IF.
      * DPM fin  2-JUL-1998
           MOVE WR-1ER-REG TO I03.
           PERFORM CE-GRABA-SUPVCIN
           PERFORM CB-LEER-SUPVI03.
           PERFORM CC-GRABAR-CINTA
               UNTIL (WR-FS-SUPVI03 NOT = 0).
           PERFORM CD-LISTA-CARATULA.
           GO TO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-SUPVI03.
      *----------------
           READ SUPVI03 NEXT.
           IF (WR-FS-SUPVI03 NOT = 0 AND NOT = 10)
               MOVE 'C-PROCESAR'        TO WR-SECCION
               MOVE 'CB-LEER-SUPVI03'   TO WR-PARRAFO
               MOVE 'SUPVI03'           TO WR-ARCHIVO
               MOVE  'READ'             TO WR-OPERACI
               MOVE  WR-FS-SUPVI03      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO CZ-FIN-DE-SECCION
           END-IF.
       CC-GRABAR-CINTA.
      *----------------
           ADD 1 TO WA-L-I03
           IF (I03-NUM-REL-INST = 0 OR = 9 OR > 10)
               ADD 1 TO WA-R-I03
           ELSE
               PERFORM CE-GRABA-SUPVCIN
               ADD 1 I03-NUM-REL-INST GIVING WI-I
               ADD 1 TO WT-TIP-PERS(1)
               ADD 1 TO WT-TIP-PERS(WI-I)
           END-IF.
           PERFORM CB-LEER-SUPVI03.
           SKIP1
       CD-LISTA-CARATULA.
      *------------------
           MOVE 1               TO WI-I
           MOVE WT-MES (WR-MES) TO WR-LA02-MES
           MOVE WR-ANO          TO WR-LA02-ANO

      **** Y2K-COMIENZO (INTERSOFT)
           IF WR-ANO NOT < 60
              MOVE '19' TO WR-LA02-SIG
           ELSE
              MOVE '20' TO WR-LA02-SIG
           END-IF.
      **** Y2K-FINAL    (INTERSOFT)

           WRITE SUPRI03 FROM WR-LINEA01 AFTER PAGE
           WRITE SUPRI03 FROM WR-LINEA02 AFTER 2
           WRITE SUPRI03 FROM WR-LINEA03 AFTER 7
           WRITE SUPRI03 FROM WR-LINEA04 AFTER 1
           PERFORM UNTIL (WI-I > 11)
              MOVE WT-GLS(WI-I)      TO WR-LA05-GLS
              MOVE WT-TIP-PERS(WI-I) TO WR-LA05-NUM
              WRITE SUPRI03 FROM WR-LINEA05 AFTER 1
              IF (WI-I < 11)
                  WRITE SUPRI03 FROM WR-LINEA06 AFTER 1
              END-IF
      *---------------------------------------------------------------*
      *   se agrega este if para soportar modificacion 1              *
      *---------------------------------------------------------------*
              IF (WI-I = 9)
                 ADD 1 TO WI-I
              END-IF
              ADD 1 TO WI-I
           END-PERFORM.
           WRITE SUPRI03 FROM WR-LINEA07 AFTER 1
           WRITE SUPRI03 FROM WR-LINEA08 AFTER 7
           WRITE SUPRI03 FROM WR-LINEA09 AFTER 2
           WRITE SUPRI03 FROM WR-LINEA10 AFTER 1.
           SKIP1
       CE-GRABA-SUPVCIN.
      *------------------
           WRITE SUPRCIN FROM I03
           IF (WR-FS-SUPVCIN NOT = 0)
               MOVE 'C-PROCESAR'        TO WR-SECCION
               MOVE 'CE-GRABA-SUPVCIN'  TO WR-PARRAFO
               MOVE 'SUPVCIN'           TO WR-ARCHIVO
               MOVE  'WRITE'            TO WR-OPERACI
               MOVE  WR-FS-SUPVCIN      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO CZ-FIN-DE-SECCION
           ELSE
               ADD 1 TO WA-G-CIN
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
       EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           IF WS-TERMINO-ANORMAL
              DISPLAY 'SUPLI03, NO EMITIDO O INCOMPLETO'
              DISPLAY ' '
              DISPLAY ' '
           ELSE
              DISPLAY 'SUPLI03, SE EMITIO PARA LAS SIGUIENTES OFICINAS'
              DISPLAY ' '
              DISPLAY '090 L'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPLI03 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '          CARATULA DE CUADRATURA I03 '
           DISPLAY ' '
           DISPLAY 'ARCHIVO    LEIDOS    RECHAZADOS   GRABADOS'
           DISPLAY ' '
           DISPLAY 'SUPVI03  ' WA-L-I03 '    ' WA-R-I03 '   ' WA-G-I03
           DISPLAY 'SUPVCIN  ' WA-L-CIN '    ' WA-R-CIN '   ' WA-G-CIN
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY W-PARAMETROS.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-TERMINO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
       EB-TERMINO.
      *-----------
           CLOSE SUPLI03 SUPVI03 SUPVCIN.
           IF WS-TERMINO-ANORMAL
              MOVE 100 TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY ' '
              DISPLAY 'EL ERROR FUE EN LA SECCION          ' WR-SECCION
              DISPLAY '                   PARRAFO          ' WR-PARRAFO
              DISPLAY '                   ARCHIVO          ' WR-ARCHIVO
              DISPLAY '                   OPERACION        ' WR-OPERACI
              DISPLAY '                   FILE STATUS      ' WR-FILE-ST
              DISPLAY '                   MENSAJE DE ERROR ' WR-MENSAJE
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.

       EZ-FIN-DE-SECCION.
      *------------------
           EXIT.


