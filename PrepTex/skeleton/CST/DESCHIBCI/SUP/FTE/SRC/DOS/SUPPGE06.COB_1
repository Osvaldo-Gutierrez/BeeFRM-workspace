       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPGE06.
      *AUTHOR.         PABLO RETAMAL P.
      *                DATE-WRITTEN,  03/06/97.
      *     GENERA ARCHIVO DETALLE VCTOS POR CTA. CTBLE.
      *     GENERA ARCHIVO FLUJOS POR CTA MAYOR
      *
      *

000000*COMPILED SITE 2000          21.09.98
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      C01           IS SALTO
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT ARCHD00 ASSIGN       TO ARCHD00
                  ORGANIZATION         IS INDEXED
                  ACCESS               IS DYNAMIC
                  RECORD KEY           IS D00-CLAVE
                  FILE STATUS          IS WR-FS-ARCHD00.
           SELECT LISTADO ASSIGN       TO LISTADO.
           SELECT FSORT   ASSIGN       TO SORTWK1.
           SELECT ARCHTOT ASSIGN       TO ARCHTOT.

       DATA DIVISION.
      *==============
       FILE SECTION.
      *=============
       FD  ARCHD00.
       COPY CYDV000C.

       FD  LISTADO
           RECORDING MODE F.
       01  LINEA  PIC X(133).


       SD  FSORT.
       01  R-FSORT.
           05 SOR-LLAVE.
              10 SOR-MONEDA                   PIC 9(1).
              10 SOR-FECHA-VCTO.
                 15 SOR-SS-VCTO               PIC 9(2).
                 15 SOR-AA-VCTO               PIC 9(2).
                 15 SOR-MM-VCTO               PIC 9(2).
              10 SOR-CTA-MAYOR.
                 15 SOR-DIG-1                 PIC X(1).
                 15 SOR-RESTO                 PIC X(3).
           05 SOR-CTA-CONTAB                  PIC X(10).
           05 SOR-MONTO                       PIC 9(13).
           05 SOR-INT-DEV                     PIC 9(13).
           05 SOR-REA-DEV                     PIC 9(13).

       FD  ARCHTOT
           BLOCK CONTAINS 0 RECORDS.
       01  R-ARCHTOT                          PIC X(64).

       WORKING-STORAGE SECTION.
      *========================
       01  WK-FEXT.
           05 WK-FEXT-SS       PIC 9(02).
           05 WK-FEXT-AAMMDD   PIC X(06).

       01  W-ARCHTOT.
           05 TOT-LLAVE.
              10 TOT-FECHA-VCTO.
                 15 TOT-SS-VCTO               PIC 9(2).
                 15 TOT-AA-VCTO               PIC 9(2).
                 15 TOT-MM-VCTO               PIC 9(2).
              10 FILLER                       PIC X(1) VALUE '%'.
              10 TOT-CTA-MAYOR                PIC X(4).
           05 FILLER                          PIC X(1) VALUE '%'.
           05 TOT-CTA-CONTAB                  PIC X(10).
           05 FILLER                          PIC X(1) VALUE '%'.
           05 TOT-MONTO                       PIC 9(13).
           05 FILLER                          PIC X(1) VALUE '%'.
           05 TOT-INT-DEV                     PIC 9(13).
           05 FILLER                          PIC X(1) VALUE '%'.
           05 TOT-REA-DEV                     PIC 9(13).
       01  W-FEC-PARAM.
           05 WK-PGM                 PIC X(08).
           05 FILLER                 PIC X(01).
           05 WK-FEC-PROC.
              07 WK-FEC-PROC-SSAAMM.
                 10 WK-FEC-SS           PIC 9(02).
                 10 WK-FEC-AA           PIC 9(02).
                 10 WK-FEC-MM           PIC 9(02).
              07 WK-FEC-DD           PIC 9(02).
           05 FILLER                 PIC X(01).
           05 WK-NUM-REG             PIC 9(09).
           05 FILLER                 PIC X(53).

       01  W-SWITCHES.
           05 WK-EOF-SORT            PIC 9(01) VALUE 0.
              88 WK-FIN-SORT                   VALUE 1.
           05 WS-PROCESO             PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK                 VALUE 0.
              88 WS-PROCESO-NO-OK              VALUE 1.
           05 FF-SORT                PIC 9(09) VALUE ZERO.
           05 WA-R-PRO               PIC 9(09) VALUE ZERO.
           05 WA-R-GRA               PIC 9(09) VALUE ZERO.
           05 WA-R-LEI               PIC 9(09) VALUE ZERO.
           05 J                      PIC 9(09) VALUE ZERO.

       01  W-ACUMUL-DETA                       VALUE ZERO.
           05 W-A-DETA OCCURS 9 TIMES PIC 9(11)V99.

       01  W-ACUMUL-TOTA                       VALUE ZERO.
           05 W-A-TOTA OCCURS 9 TIMES PIC 9(11)V99.

       01  W-REGISTROS-DE-TRABAJO.
           03 W-AA.
              05 W-A1                   PIC X(01) VALUE ZERO.
              05 W-A2                   PIC X(01) VALUE ZERO.
           03 WK-FF                     PIC X(12) VALUE ZERO.
           03 WW REDEFINES
                 WK-FF OCCURS 12 TIMES  PIC X(01).
           03 WX REDEFINES WK-FF.
              05 SIS-WX                 PIC 9(01).
              05 SUC-WX                 PIC X(03).
              05 COD-WX                 PIC 9(03).
                 88 COD-CASTIGA  VALUE 055 080 081 955 956.
                 88 COD-ELIMINA  VALUE 921 303.
              05 FILLER-WX              PIC X(05).
           03 WK-SOR-LLAVE.
              10 WK-MONEDA                   PIC 9(1) VALUE ZEROES.
              10 WK-FECHA-VCTO.
                 15 WK-SS-VCTO               PIC 9(2) VALUE ZEROES.
                 15 WK-AA-VCTO               PIC 9(2) VALUE ZEROES.
                 15 WK-MM-VCTO               PIC 9(2) VALUE ZEROES.
              10 WK-CTA-MAYOR                PIC X(4) VALUE ZEROES.
           03 W-FECHA-PERIODO.
              10 W-FECHA-VCTO-ANT.
                 15 W-SS-VCTO-ANT           PIC 9(2).
                 15 W-AA-VCTO-ANT           PIC 9(2).
                 15 W-MM-VCTO-ANT           PIC 9(2).
              10 W-DD-VCTO-ANT              PIC 9(2) VALUE 01.

           03 W-FL                                VALUE ZERO.
              05 W-NUEVO-SALDO          PIC 9(11)V99.
              05 W-NUEVA-A              PIC 9(03).
              05 W-AMORTIZACION         PIC S9(11)V99.
              05 W-PERIODO              PIC 9(05).
              05 W-PERIODO-POND         PIC 9(08).
              05 W-PERIODO-FRACCION     PIC 9(03)V9(12).
              05 W-PERIODO-ACUM         PIC 9(03)V9(12).
              05 W-ACUMULO              PIC 9(11)V99.
              05 W-DIFER               PIC S9(11)V99.
              05 W-DIFER-TOT           PIC S9(11)V99.
              05 W-SALDO-CNTBLE        PIC S9(11)V99.
              05 W-FECHA-TRABAJO.
                 10 W-TRAB-AA          PIC 9(4).
                 10 W-TRAB-MM          PIC 9(2).
                 10 W-TRAB-DD          PIC 9(2).
              05 W-AMORT               PIC 9(13).
              05 W-TOT-MONTO           PIC 9(13).
              05 W-TOT-INTDEV          PIC 9(13).
              05 W-TOT-READEV          PIC 9(13).
              05 W-NCUOT               PIC 9(3).
              05 W-CUOTAS-PEND-1       PIC 9(3).

           03 W-FL1.
              05 WR-MENSAJES.
                10 WR-SECCION           PIC X(15).
                10 WR-PARRAFO           PIC X(15).
                10 WR-ARCHIVO           PIC X(10).
                10 WR-OPERACI           PIC X(10).
                10 WR-FILE-ST           PIC X(02).
                10 WR-MENSAJE           PIC X(35).
              05 WR-FS-ARCHD00          PIC 9(02).

      **** LISTADO ***
       01  TITULO1.
           03 FILLER                 PIC X(01)  VALUE SPACE.
           03 FILLER                 PIC X(10)  VALUE 'BANCO BCI.'.
           03 FILLER                 PIC X(40)  VALUE SPACE.
           03 FILLER                 PIC X(34)  VALUE
              'FLUJOS  MENSUALES  A  DICIEMBRE   '.
           03 AA-TIT1                PIC 9(02)  VALUE ZERO.
           03 FILLER                 PIC X(30)  VALUE SPACE.
           03 FILLER                 PIC X(10)  VALUE 'PAGINA :  '.
           03 PAG-TIT1               PIC ZZZ9.

       01  RAYA1.
           03 FILLER                 PIC X(51)  VALUE SPACE.
           03 FILLER                 PIC X(32)  VALUE ALL '-'.

       01  BLANCO.
           03 FILLER                 PIC X(133) VALUE SPACE.

       01  TITULO2.
           03 FILLER                 PIC X(07)  VALUE ' AA-MES'.
           03 FILLER                 PIC X(14)  VALUE '   COMERCIALES'.
           03 FILLER                 PIC X(14)  VALUE '       CONSUMO'.
           03 FILLER                 PIC X(14)  VALUE '  HIPOTECARIOS'.
           03 FILLER                 PIC X(14)  VALUE '         COMEX'.
           03 FILLER                 PIC X(14)  VALUE ' INTERBANCARIO'.
           03 FILLER                 PIC X(14)  VALUE ' OTROS PTMOS V'.
           03 FILLER                 PIC X(14)  VALUE ' CARTERA VCDA.'.
           03 FILLER                 PIC X(14)  VALUE '  CONTINGENTES'.
           03 FILLER                 PIC X(14)  VALUE ' OTROS NO ESP.'.

       01  RAYA2.
           03 FILLER                 PIC X(01)  VALUE SPACE.
           03 FILLER                 PIC X(132) VALUE ALL '-'.

       01  DETALLE.
           03 FILLER                 PIC X(01)  VALUE SPACE.
           03 AA-DET                 PIC 9(04)  VALUE ZERO.
           03 MM-DET                 PIC 9(02)  VALUE ZERO.
           03 VAL1-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL2-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL3-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL4-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL5-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL6-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL7-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL8-DET               PIC ZZZZZZZZZZ9,99.
           03 VAL9-DET               PIC ZZZZZZZZZZ9,99.

       01  TOTAL1.
           03 FILLER                 PIC X(04)  VALUE SPACE.
           03 TOT1                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
           03 FILLER                 PIC X(11)  VALUE SPACE.
           03 TOT3                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
           03 FILLER                 PIC X(11)  VALUE SPACE.
           03 TOT5                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
           03 FILLER                 PIC X(11)  VALUE SPACE.
           03 TOT7                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
           03 FILLER                 PIC X(11)  VALUE SPACE.
           03 TOT9                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.

       01  TOTAL2.
           03 FILLER                 PIC X(18)  VALUE SPACE.
           03 TOT2                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
           03 FILLER                 PIC X(11)  VALUE SPACE.
           03 TOT4                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
           03 FILLER                 PIC X(11)  VALUE SPACE.
           03 TOT6                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
           03 FILLER                 PIC X(11)  VALUE SPACE.
           03 TOT8                   PIC ZZ.ZZZ.ZZZ.ZZ9,99.
      **** TABLAS ****

       01  WK-DESCRIPCION-TABLAS.
           03 WK-AAMM-VCTO.
              05 WK-AA-VCTO               PIC 9(2).
              05 WK-MM-VCTO               PIC 9(2).

       01  WK-FECHAS      VALUE ZERO.
           03 WS-SIGLO                    PIC 9(02).
           03 WS-AA                       PIC 9(02).
           03 WS-MM                       PIC 9(02).
           03 WS-DD                       PIC 9(02).
           03 WS-DIAS                     PIC 9(06).
      *------------------------------------------------
      *    WORK RUTINA CALENDARIO
      *------------------------------------------------
           COPY RTNWCALE.

       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.

       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.

       AC-TERMINACION.
           PERFORM D-TERMINAR.

       AZ-FIN-DE-PROCESO.
           STOP RUN.

       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-ABRIR-ARCHIVOS.
           GO BZ-FIN-DE-SECCION.

       BB-ABRIR-ARCHIVOS.
      *------------------
           ACCEPT W-FEC-PARAM.
           OPEN INPUT  ARCHD00
                 OUTPUT LISTADO ARCHTOT.
           IF (WR-FS-ARCHD00 NOT = 0 AND NOT = 97)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'ARCHD00    '           TO WR-ARCHIVO
               MOVE 'OPEN       '           TO WR-OPERACI
               MOVE WR-FS-ARCHD00           TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.

      *-------------------------------------------------
      *    CARGA TABLA CALENDARIO
      *-------------------------------------------------
           MOVE '00000000'                  TO CALEAREA-FECHA-1.
           MOVE 'CARGAR'                    TO CALEAREA-FUNCION.
           CALL 'RUTUCALE' USING RTNWCALE.
           IF CALEAREA-COD-RET NOT = ZERO
              DISPLAY
              'ERROR EN LA CARGA DE ARCHIVO CALENDARIO. CODIGO DE'
              ' RETORNO = ' CALEAREA-COD-RET
              CLOSE  ARCHD00 ARCHTOT
              MOVE 100 TO RETURN-CODE
              STOP RUN
              END-IF.

       BD-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-NO-OK            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
       BZ-FIN-DE-SECCION.  EXIT.

       C-PROCESAR SECTION.
       CA-PROC-PPAL.
           SORT FSORT ON ASCENDING KEY SOR-LLAVE
                                       SOR-CTA-CONTAB
           INPUT  PROCEDURE CA-CARGAR-SORT
           OUTPUT PROCEDURE CA-LISTAR.

       CA-CARGAR-SORT SECTION.
           PERFORM CC-LEER-ARCHD00
           PERFORM CD-GRABAR-SORT
            UNTIL  D00-CLAVE  = HIGH-VALUE  OR
                   WS-PROCESO-NO-OK.

       CA-LISTAR      SECTION.
           PERFORM CC-LEER-FSORT.
           MOVE        SOR-LLAVE  TO    WK-SOR-LLAVE.
           PERFORM CD-LISTAR-SORT UNTIL WK-FIN-SORT.
           PERFORM TOTAL-FECHA-CTA-MAYOR.

       CC-VARIOS SECTION.
       CC-LEER-ARCHD00.
              READ
              ARCHD00 NEXT AT END MOVE HIGH-VALUE TO D00-CLAVE
              END-READ.
              IF D00-CLAVE NOT = HIGH-VALUE
                 ADD 1 TO WA-R-LEI
              END-IF.
              IF WA-R-LEI > WK-NUM-REG
                 MOVE HIGH-VALUE   TO D00-CLAVE
              END-IF.

       CD-GRABAR-SORT.
           IF D00-ESTADO-CREDITO NOT = 4
      ***  PRP. SOLICITADO 23.07.97, RODRIGO RIQUELME
              AND D00-TIPO-CRED-CART-ORIGEN > 1104
              AND D00-TIPO-CRED-CART-ORIGEN < 1249
              MOVE 0                 TO W-NCUOT
              MOVE WK-FEC-PROC       TO W-FECHA-TRABAJO
              MOVE D00-FECHA-DE-EXTINCION  TO WK-FEXT-AAMMDD
              IF D00-FECHA-DE-EXTINCION-AA <  60
                 MOVE   20         TO WK-FEXT-SS
              ELSE
                 MOVE   19         TO WK-FEXT-SS
              END-IF
              IF D00-NRO-CTAS-AMORT-PEND < 2 OR
                 D00-FECHA-PROX-VCTO = D00-FECHA-DE-EXTINCION OR
      ********   D00-FECHA-DE-EXTINCION < WK-FEC-PROC
                 WK-FEXT                < WK-FEC-PROC
                 MOVE 1    TO D00-NRO-CTAS-AMORT-PEND
              ELSE
                 PERFORM CALCULA-DIF-DIAS
                 PERFORM CALCULA-PERIODO
              END-IF
              IF D00-SALDO-EN-CART-VENC > 0
                 COMPUTE D00-SALDO-CONTABLE =
                         D00-SALDO-CONTABLE - D00-SALDO-EN-CART-VENC
                 PERFORM CD-GRABAR-CVCDA
                 END-IF
              IF D00-INT-POR-COBRAR > 0  AND
                 D00-SALDO-EN-CART-VENC = 0 AND
                 D00-SALDO-CONTABLE = 0
                 MOVE 1     TO D00-NRO-CTAS-AMORT-PEND
              END-IF
              COMPUTE W-AMORT ROUNDED =
                      D00-SALDO-CONTABLE / D00-NRO-CTAS-AMORT-PEND
              IF W-AMORT > 0 OR D00-INT-POR-COBRAR > 0
                 PERFORM CALCULAR-VCTOS
                         UNTIL W-NCUOT = D00-NRO-CTAS-AMORT-PEND
              END-IF

           END-IF.

           PERFORM CC-LEER-ARCHD00.

       CD-GRABAR-CVCDA.
      *----------------
           EXIT.
      ***  PRP. SOLICITADO 23.07.97, RODRIGO RIQUELME
      ***  MOVE D00-TIPO-CRED-CART-ORIGEN    TO SOR-CTA-MAYOR
      ***  MOVE ZEROES                       TO SOR-DIG-1
      ***  MOVE ZEROES                       TO SOR-FECHA-VCTO
      ***  MOVE D00-CUENTA-CONTABLE          TO SOR-CTA-CONTAB
      ***  MOVE D00-SALDO-EN-CART-VENC       TO SOR-MONTO
      ***  MOVE D00-INT-POR-COBRAR           TO SOR-INT-DEV
      ***  MOVE D00-REAJ-DEVEN               TO SOR-REA-DEV
      ***  PERFORM CD-RELEASE-SORT.

       CALCULA-DIF-DIAS.
      *---------------
      *-----------------------------------------------------------
      *    MOVIMIENTO DE FECHA DESDE (FECHA1) DE RUTINA CALENDARIO
      *------------------------------------------------------------
           IF D00-FECHA-DE-EXTINCION-AA <  60
              MOVE   20         TO CALEAREA-FECHA-1-AM
           ELSE
              MOVE   19         TO CALEAREA-FECHA-1-AM
           END-IF
           MOVE D00-FECHA-DE-EXTINCION-AA TO CALEAREA-FECHA-1-AA
           MOVE D00-FECHA-DE-EXTINCION-MM TO CALEAREA-FECHA-1-M
           MOVE D00-FECHA-DE-EXTINCION-DD TO CALEAREA-FECHA-1-D
      *-----------------------------------------------------------
      *    MOVIMIENTO DE FECHA HASTA (FECHA2) DE RUTINA CALENDARIO
      *------------------------------------------------------------
           IF D00-FECHA-PROX-VCTO-AA    <  60
              MOVE   20                TO CALEAREA-FECHA-2-AM
           ELSE
              MOVE   19                TO CALEAREA-FECHA-2-AM
           END-IF
           MOVE D00-FECHA-PROX-VCTO-AA TO CALEAREA-FECHA-2-AA
           MOVE D00-FECHA-PROX-VCTO-MM TO CALEAREA-FECHA-2-M
           MOVE D00-FECHA-PROX-VCTO-DD TO CALEAREA-FECHA-2-D
      *-------------------------------------------------------------
      *   MOVIMIENTO DE FUNCION (CDCEF) CALCULO DE DIAS ENTRE FECHAS
      *-------------------------------------------------------------
           MOVE ZERO                   TO CALEAREA-COD-RET
                                          CALEAREA-NUM-DIA
           MOVE 'CDCEF'                TO CALEAREA-FUNCION
           CALL 'RUTUCALE'  USING RTNWCALE
           IF CALEAREA-COD-RET NOT = ZERO
              DISPLAY ' >> ERROR EN CALENDARIO <<<'
              DISPLAY ' >>    DESDE   HASTA    <<<'
              DISPLAY ' >> PROCESO    SIGUE    <<<'
              DISPLAY ' >> CODIGO DE RETORNO   ==>' CALEAREA-COD-RET
              DISPLAY ' >> D00-FECHA-DE-EXTINCION '
                                                 D00-FECHA-DE-EXTINCION
              DISPLAY ' >> D00-FECHA-PROX-VCTO ==>' D00-FECHA-PROX-VCTO
              DISPLAY ' >> DIAS CALCULADOS     ==>' CALEAREA-NUM-DIA
              MOVE 30               TO WS-DIAS
           ELSE
              MOVE CALEAREA-NUM-DIA TO WS-DIAS
              END-IF.

       CALCULA-PERIODO.
      *---------------
           COMPUTE W-CUOTAS-PEND-1 = D00-NRO-CTAS-AMORT-PEND - 1.
           COMPUTE W-PERIODO ROUNDED = WS-DIAS / W-CUOTAS-PEND-1.

      ****  POBLAR FECHA BUSQUEDA
      ***  IF D00-FECHA-PROX-VCTO-AA    <  60
      ***     MOVE   20                TO W-SS-VCTO-ANT
      ***  ELSE
      ***        MOVE   19             TO W-SS-VCTO-ANT
      ***  END-IF
      ***  MOVE D00-FECHA-PROX-VCTO-AA TO W-AA-VCTO-ANT
      ***  MOVE D00-FECHA-PROX-VCTO-MM TO W-MM-VCTO-ANT.

       CALCULAR-VCTOS.
      *---------------
      ***************************************************************
      *  -SI FECHA PROX VCTO = FECHA EXTINCION, SIGNIFICA QUE SOLO
      *   QUEDA UNA CUOTA Y SE ACUMULARA EN EL TRAMO ESPECIFICADO.
      *
      *  -SI SON DISTINTAS, LA PRIMERA CUOTA DEBE TENER LA FECHA DE
      *   PROX. VCTO. EL RESTO SE CALCULARA EN BASE AL PERIODO:
      *
      *             DIF DIAS (FEC.EXTINCION - FEC.PROXVCTO)
      *   PERIODO = ---------------------------------------
      *                     (CUOTAS-PEND - 1)
      *
      ***************************************************************
           IF W-NCUOT = 0
              PERFORM PROCESO-1VCTO
           ELSE
              PERFORM PROCESO-VARIOS-VCTOS
           END-IF.

      **** MOVE SOR-FECHA-VCTO               TO W-FECHA-VCTO-ANT
           MOVE D00-TIPO-CRED-CART-ORIGEN    TO SOR-CTA-MAYOR
           MOVE D00-CUENTA-CONTABLE          TO SOR-CTA-CONTAB
           MOVE W-AMORT                      TO SOR-MONTO
           MOVE ZEROES                       TO SOR-INT-DEV
           MOVE ZEROES                       TO SOR-REA-DEV
           IF W-NCUOT = 0
              MOVE D00-INT-POR-COBRAR        TO SOR-INT-DEV
              MOVE D00-REAJ-DEVEN            TO SOR-REA-DEV
           END-IF.
           PERFORM CD-RELEASE-SORT.
           ADD 1         TO W-NCUOT.

       PROCESO-1VCTO.
      *---------------
           MOVE D00-FECHA-PROX-VCTO-MM       TO SOR-MM-VCTO
           MOVE D00-FECHA-PROX-VCTO-AA       TO SOR-AA-VCTO
           IF D00-FECHA-PROX-VCTO-AA < 60
              MOVE 20      TO SOR-SS-VCTO
           ELSE
              MOVE 19      TO SOR-SS-VCTO
           END-IF
           IF SOR-FECHA-VCTO < WK-FEC-PROC-SSAAMM
              MOVE WK-FEC-SS      TO SOR-SS-VCTO
              MOVE WK-FEC-AA      TO SOR-AA-VCTO
              MOVE WK-FEC-MM      TO SOR-MM-VCTO
           END-IF.

       PROCESO-VARIOS-VCTOS.
      *---------------------
      *-----------------------------------------------------------
      *    MOVIMIENTO DE FECHA DESDE (FECHA1) DE RUTINA CALENDARIO
      *------------------------------------------------------------
      *    MOVE W-SS-VCTO-ANT       TO CALEAREA-FECHA-1-AM
      *    MOVE W-AA-VCTO-ANT       TO CALEAREA-FECHA-1-AA
      *    MOVE W-MM-VCTO-ANT       TO CALEAREA-FECHA-1-M
      *    MOVE W-DD-VCTO-ANT       TO CALEAREA-FECHA-1-D
           IF D00-FECHA-PROX-VCTO-AA    <  60
              MOVE   20                TO CALEAREA-FECHA-1-AM
           ELSE
              MOVE   19                TO CALEAREA-FECHA-1-AM
           END-IF
           MOVE D00-FECHA-PROX-VCTO-AA TO CALEAREA-FECHA-1-AA
           MOVE D00-FECHA-PROX-VCTO-MM TO CALEAREA-FECHA-1-M
           MOVE D00-FECHA-PROX-VCTO-DD TO CALEAREA-FECHA-1-D
      *---------------------------------------------------------------
      *    LIMPIA AREAS PARA RECIBIR NUEVO VENCIMIENTO
      *---------------------------------------------------------------
           MOVE ZERO                TO CALEAREA-FECHA-2-AM
                                       CALEAREA-FECHA-2-AA
                                       CALEAREA-FECHA-2-M
                                       CALEAREA-FECHA-2-D
      *---------------------------------------------------------------
      *    MOVIMIENTO DE FUNCION (AVNDC) AVANCE EN NN DIAS
      *---------------------------------------------------------------
           MOVE ZERO                    TO CALEAREA-COD-RET
           COMPUTE W-PERIODO-POND = W-PERIODO * W-NCUOT
           MOVE W-PERIODO-POND          TO CALEAREA-NUM-DIA
           MOVE 'AVNDC'                 TO CALEAREA-FUNCION
           CALL 'RUTUCALE'  USING RTNWCALE
           IF CALEAREA-COD-RET NOT = ZERO
              DISPLAY ' >> ERROR EN CALENDARIO <<<'
              DISPLAY ' >>  NUEVO VENCIMIENTO  <<<'
              DISPLAY ' >> PROCESO NO CONTINUA <<<'
              DISPLAY ' >> CODIGO DE RETORNO   ==>' CALEAREA-COD-RET
              DISPLAY ' >> CALEAREA-FECHA-1-AM ==>' CALEAREA-FECHA-1-AM
              DISPLAY ' >> CALEAREA-FECHA-1-AA ==>' CALEAREA-FECHA-1-AA
              DISPLAY ' >> CALEAREA-FECHA-1-M  ==>' CALEAREA-FECHA-1-M
              DISPLAY ' >> CALEAREA-FECHA-1-D  ==>' CALEAREA-FECHA-1-D
              DISPLAY ' '
              DISPLAY ' >> CALEAREA-FECHA-2-AM ==>' CALEAREA-FECHA-2-AM
              DISPLAY ' >> CALEAREA-FECHA-2-AA ==>' CALEAREA-FECHA-2-AA
              DISPLAY ' >> CALEAREA-FECHA-2-M  ==>' CALEAREA-FECHA-2-M
              DISPLAY ' >> CALEAREA-FECHA-2-D  ==>' CALEAREA-FECHA-2-D
              DISPLAY ' >> DIAS SUMADOS        ==>' WS-DIAS
              MOVE 100 TO RETURN-CODE
              CLOSE ARCHD00 LISTADO ARCHTOT
              STOP RUN
              END-IF
           MOVE CALEAREA-FECHA-2-AM TO SOR-SS-VCTO
           MOVE CALEAREA-FECHA-2-AA TO SOR-AA-VCTO
           MOVE CALEAREA-FECHA-2-M  TO SOR-MM-VCTO.

           IF SOR-FECHA-VCTO < WK-FEC-PROC-SSAAMM
              MOVE WK-FEC-SS      TO SOR-SS-VCTO
              MOVE WK-FEC-AA      TO SOR-AA-VCTO
              MOVE WK-FEC-MM      TO SOR-MM-VCTO
           END-IF.


      *CALCULAR-VCTOS.
      *---------------
      *****************  MODO PERIODOS FIJOS 30 DIAS
      *    ADD 1    TO W-TRAB-MM
      *    IF W-TRAB-MM > 12
      *       MOVE 01    TO W-TRAB-MM
      *       ADD 1      TO W-TRAB-AA
      *    END-IF.
      *    MOVE W-FECHA-TRABAJO              TO SOR-FECHA-VCTO
      *    MOVE D00-TIPO-CRED-CART-ORIGEN    TO SOR-CTA-MAYOR
      *    MOVE D00-CUENTA-CONTABLE          TO SOR-CTA-CONTAB
      *    MOVE W-AMORT                      TO SOR-MONTO
      *    MOVE ZEROES                       TO SOR-INT-DEV
      *    IF W-NCUOT = 0
      *       MOVE D00-INT-POR-COBRAR        TO SOR-INT-DEV
      *    END-IF.
      *    PERFORM CD-RELEASE-SORT.
      *    ADD 1         TO W-NCUOT.

       CD-RELEASE-SORT.
      *---------------
      ***   SOR-MONEDA = 0   NACIONAL NO REAJ
      ***   SOR-MONEDA = 1   NACIONAL REAJ
      ***   SOR-MONEDA = 2   EXTRANJERA
           IF D00-MONEDA = 999
              MOVE 0                         TO SOR-MONEDA
           ELSE
              IF D00-MONEDA = 994 OR 997 OR 998
                 MOVE 1                      TO SOR-MONEDA
              ELSE
                 MOVE 2                      TO SOR-MONEDA
              END-IF
           END-IF.

           RELEASE R-FSORT
           ADD 1 TO FF-SORT.

       CD-LISTAR-SORT.
      *-------------
           ADD 1        TO WA-R-PRO.
           IF SOR-LLAVE = WK-SOR-LLAVE
              ADD  SOR-MONTO      TO W-TOT-MONTO
              ADD  SOR-INT-DEV    TO W-TOT-INTDEV
              ADD  SOR-REA-DEV    TO W-TOT-READEV
           ELSE
              PERFORM TOTAL-FECHA-CTA-MAYOR
              MOVE  SOR-LLAVE       TO WK-SOR-LLAVE
              MOVE ZEROES           TO W-TOT-MONTO
                                       W-TOT-INTDEV
                                       W-TOT-READEV
              ADD  SOR-MONTO      TO W-TOT-MONTO
              ADD  SOR-INT-DEV    TO W-TOT-INTDEV
              ADD  SOR-REA-DEV    TO W-TOT-READEV
           END-IF.

           PERFORM CC-LEER-FSORT.

       TOTAL-FECHA-CTA-MAYOR.
      *---------------------
           MOVE WK-FECHA-VCTO      TO TOT-FECHA-VCTO
           MOVE WK-CTA-MAYOR       TO TOT-CTA-MAYOR
           MOVE SPACES             TO TOT-CTA-CONTAB
           MOVE W-TOT-MONTO        TO TOT-MONTO
           MOVE W-TOT-INTDEV       TO TOT-INT-DEV
           MOVE W-TOT-READEV       TO TOT-REA-DEV
           ADD 1                   TO WA-R-GRA
           MOVE W-ARCHTOT          TO R-ARCHTOT
           WRITE R-ARCHTOT.

       CC-LEER-FSORT.
      *-------------
           RETURN FSORT
                     AT END MOVE HIGH-VALUE TO SOR-LLAVE
                            MOVE   1        TO WK-EOF-SORT
           END-RETURN.

      **** IF SOR-MONTO NOT > 0
      ****    DISPLAY R-FSORT
      **** END-IF.


       D-TERMINAR SECTION.
      *===================
       DB-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'PROGRAMA NO EMITE INFORME'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPGE06'
           DISPLAY '-----------------------------------------'
           DISPLAY 'REG LEIDOS DE ARCHD00 : ' WA-R-LEI
           DISPLAY 'REG PROCESADOS        : ' WA-R-PRO
           DISPLAY 'REG GRABADOS          : ' WA-R-GRA
           DISPLAY 'REG GRABADOS SORT     : ' FF-SORT
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.

       DC-TERMINO.
      *-----------
           CLOSE ARCHD00 LISTADO ARCHTOT
           IF WS-PROCESO-NO-OK
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
       DZ-FIN-DE-SECCION. EXIT.

