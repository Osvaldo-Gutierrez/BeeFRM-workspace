       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPCI02.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  27/02/91.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Super Bancos                                       *
      *  Subsist. :                                                    *
      *  Analista : Nunez Sergio                                       *
      *  Objetivo : Crear archivo I02 desde la tabla REC 100. Este     *
      *             archivo contiene la clasificacion por grupos       *
      *             economicos de los clientes relacionados con el     *
      *             banco medianta la relacion RB de la tabla.         *
      *  Archivos :                                                    *
      *   Entrada : DBC 100, Datos basicos de clientes                 *
      *             REC 100, Relacion entre clientes                   *
      *             TAB 230, RSC Rut ficticios sobre 100 millones      *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Salida  : SUPVI02, Grupos relacionados (I02)                 *
      *                                                                *
      *   Trabajo : SUPSSOR, Archivo de sort                           *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: Osvaldo Donoso Jara...............     *
      *            Fecha      : 20/08/93                               *
      *            Cambio     : Se cambia picture de REC-COD-AREC de   *
      *                         tabla REC de pic x(03) a pix (05)      *
      *                                                                *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPVI02 ASSIGN TO SUPVI02
                  ORGANIZATION IS INDEXED
                  ACCESS       IS RANDOM
                  RECORD KEY   IS I02-CLAVE
                  FILE STATUS IS WR-FS-SUPVI02.
           SELECT SUPSSOR ASSIGN TO DA-S-SORTWK1.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPVI02
           RECORD CONTAINS 80 CHARACTERS
           LABEL RECORD STANDARD.
      ***************************************************************
      * Grupos Relacionados - Sistema Instituciones                 *
      ***************************************************************
       01  I02.
           05 I02-CLAVE.
              10 I02-RUT-RELACION.
                 15 I02-NUM-RUT                PIC 9(09).
                 15 I02-VRF-RUT                PIC X(01).
           05 I02-NOM-O-RSOCIAL                PIC X(50).
           05 I02-NUM-GRP-ECOM1                PIC 9(04).
           05 I02-NUM-GRP-ECOM2                PIC 9(04).
           05 I02-NUM-GRP-ECOM3                PIC 9(04).
           05 I02-NUM-GRP-ECOM4                PIC 9(04).
           05 I02-NUM-GRP-ECOM5                PIC 9(04).
       SD  SUPSSOR.
       01  SUPRSOR.
           02 SOR-CLAVE.
              03 SOR-CIC-ICLI      PIC X(12).
              03 SOR-GRP-ECOM      PIC 9(05).
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       SKIP1
       01  W-TABLAS.
           05 WT-NOMBRE                 PIC X(75).
           05 WT-N REDEFINES WT-NOMBRE OCCURS 75 PIC X(01).
           05 WT-TABLA-RUT OCCURS 1000 TIMES INDEXED BY WI-IND.
              10 WT-RUT-SUP.
                 15 WT-RUT-SUP-L09          PIC X(09).
                 15 WT-RUT-SUP-VRF          PIC X(01).
              10 WT-RUT-FIC                 PIC X(09).
       SKIP1
       01  W-INDICES.
           05 WI-I                   PIC 9(03) VALUE 0.
           05 WI-K                   PIC 9(03) VALUE 0.
           05 WI-N                   PIC 9(01) VALUE 0.
       SKIP1
       01  W-SWITCHES.
           05 WS-PROCESO             PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0, 2.
              88 WS-MENSAJE-OP       VALUE 2.
              88 WS-PROCESO-ERROR    VALUE 1.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-REC               PIC 9(06) VALUE 0.
           05 WA-R-REC               PIC 9(06) VALUE 0.
           05 WA-G-REC               PIC 9(06) VALUE 0.
           05 WA-L-DBC               PIC 9(06) VALUE 0.
           05 WA-R-DBC               PIC 9(06) VALUE 0.
           05 WA-G-DBC               PIC 9(06) VALUE 0.
           05 WA-L-I02               PIC 9(06) VALUE 0.
           05 WA-R-I02               PIC 9(06) VALUE 0.
           05 WA-G-I02               PIC 9(06) VALUE 0.
           05 WA-L-SOR               PIC 9(06) VALUE 0.
           05 WA-R-SOR               PIC 9(06) VALUE 0.
           05 WA-G-SOR               PIC 9(06) VALUE 0.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA           PIC X(02).
              10 WR-HOY-MM           PIC X(02).
              10 WR-HOY-DD           PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION          PIC X(15).
              10 WR-PARRAFO          PIC X(15).
              10 WR-ARCHIVO          PIC X(10).
              10 WR-OPERACI          PIC X(10).
              10 WR-FILE-ST          PIC X(02).
              10 WR-MENSAJE          PIC X(35).
           05 WR-FS-SUPVI02          PIC 9(02) VALUE 0.
           05 WR-FS-SUPSSOR          PIC 9(02) VALUE 0.
           05 WR-CAR-AUX             PIC X(01).
           05 WR-GRP-ANTE            PIC 9(05).
           05 WR-COD-AREC            PIC 9(05).
           05 WR-RUT-D00-L09.
              15 WR-RUT-D00-L08      PIC X(08) JUST RIGHT.
              15 WR-RUT-D00-VRF      PIC X(01).
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  WB-UIB.
           05 FILLER                 PIC X(32) VALUE 'SUPPCI02'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  WB-REQ-REC-AREA.
           02 WB-REQ-REC-CMD          PIC X(5) VALUE 'REDKX'.
           02 FILLER                  PIC X(3) VALUE 'REC'.
           02 FILLER                  PIC X(5) VALUE 'REC02'.
           02 WB-REQ-REC-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +100.
           02 FILLER                  PIC X(58).
           02 WB-REQ-REC-KEY-VALUE    PIC X(12) VALUE '100000107699'.

       01  WB-REQ-DBC-AREA.
           02 WB-REQ-DBC-CMD          PIC X(5) VALUE 'REDKY'.
           02 FILLER                  PIC X(3) VALUE 'DBC'.
           02 FILLER                  PIC X(5) VALUE 'DBC00'.
           02 WB-REQ-DBC-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +100.
           02 FILLER                  PIC X(58).
           02 WB-REQ-DBC-KEY-VALUE    PIC X(12).

       01  WB-REQ-TAB-AREA.
           02 WB-REQ-TAB-CMD          PIC X(5) VALUE 'REDKG'.
           02 FILLER                  PIC X(3) VALUE 'TAB'.
           02 FILLER                  PIC X(5) VALUE 'TAB00'.
           02 WB-REQ-TAB-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +230.
           02 FILLER                  PIC X(58).
           02 WB-REQ-TAB-KEY-VALUE    PIC X(30) VALUE 'RSC'.
      *
      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
      *    Relaciones entre Clientes
      *    -------------------------
      *
      *    Nombre Registro : REC
      *    Clave(s)        : REC-KEY-IREC(UU), REC-CIC-IREL(DU)
      *    Largo           : 158
      *    Bloqueo         : 1
      *    Observaciones   :
      *
      *    01) Modificacion  21/07/93,  DAD   (Req. BCI 20/07/93)
      *        - Aumentado largo campo REC-COD-AREC de X(03) a X(05).
      *        - Disminuido largo campo Glosa Disponible de X(31) a X(29
      *
       01  REC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  REC-GLS-FLAG                             PIC X(01).
      *
      *KEY Clave Primaria Registro
           03  REC-KEY-IREG.
      *
      *        Identificacion Relacion entre Clientes
               05  REC-KEY-IREC.
      *
      *KEY         Cod. Int. Comp. Cliente
                   07  REC-CIC-ICLI.
      *
      *                Centro Asignacion Identificacion
                       09  REC-CAI-ICLI.
                           11  REC-COD-TCLI             PIC X(01).
                           11  REC-COD-CCLI             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  REC-IIC-ICLI                 PIC X(08).

      *KEY         Codigo Tipo Relacion entre Clientes  SGC REC
                   07  REC-COD-TREC                     PIC X(02).
      *
      *KEY         Cod. Int. Comp. Cliente Relacionado
                   07  REC-CIC-IREL.
      *
      *                Centro Asignacion Identificacion
                       09  REC-CAI-IREL.
                           11  REC-COD-TREL             PIC X(01).
                           11  REC-COD-CREL             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  REC-IIC-IREL                 PIC X(08).
      *
      *KEY         Num Corr segun Tipo Relacion
                   07  REC-NUM-TREC                     PIC 9(02).
      *
      *        Time Stamp Transaccion
               05  REC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  REC-FEC-FTRN.
                       09  REC-NUM-STRN                 PIC 9(02).
                       09  REC-NUM-ATRN                 PIC 9(02).
                       09  REC-NUM-MTRN                 PIC 9(02).
                       09  REC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  REC-HRA-HRTR.
                       09  REC-NUM-HHTR                 PIC 9(02).
                       09  REC-NUM-MMTR                 PIC 9(02).
                       09  REC-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  REC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  REC-MSC-STAT                             PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  REC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  REC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  REC-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  REC-COD-ATRN                             PIC X(12).
      *
      *    Indicador de Vigencia Relacion (S/N)         TAB VIGE
           03  REC-IND-VIGE                             PIC X(01).
      *
      *OPR Fecha Inicio de la Relacion
           03  REC-FEC-FINI.
               05  REC-NUM-SINI                         PIC 9(02).
               05  REC-NUM-AINI                         PIC 9(02).
               05  REC-NUM-MINI                         PIC 9(02).
               05  REC-NUM-DINI                         PIC 9(02).
      *
      *OPR Fecha Inicio Computacional
           03  REC-FEC-FINP.
               05  REC-NUM-SINP                         PIC 9(02).
               05  REC-NUM-AINP                         PIC 9(02).
               05  REC-NUM-MINP                         PIC 9(02).
               05  REC-NUM-DINP                         PIC 9(02).
      *
      *OPR Fecha de Termino de la Relacion
           03  REC-FEC-FTER.
               05  REC-NUM-STER                         PIC 9(02).
               05  REC-NUM-ATER                         PIC 9(02).
               05  REC-NUM-MTER                         PIC 9(02).
               05  REC-NUM-DTER                         PIC 9(02).
      *
      *OPR Fecha de Termino Computacional
           03  REC-FEC-FOUT.
               05  REC-NUM-SOUT                         PIC 9(02).
               05  REC-NUM-AOUT                         PIC 9(02).
               05  REC-NUM-MOUT                         PIC 9(02).
               05  REC-NUM-DOUT                         PIC 9(02).
      *
      *MOD-01  X(03).
      *RCM Codigo Asociado a la Relacion                SGC REC
           03  REC-COD-AREC                             PIC X(05).
      *
      *    Valor Asociado a la Relacion
           03  REC-SGV-AREC        COMP-3               PIC S9(11)V9(4).
      *
      *OPR Codigo de Valor Asociado a la Relacion       TAB VLR
           03  REC-COD-VCAM.
      *
      *        Miscelaneo Tipo Valor                    TAB TVAL
               05  REC-MSC-TVAL                         PIC X(01).
      *
      *        Codigo Valor
               05  REC-COD-CVAL                         PIC X(03).
      *
      *        Codigo Tipo Cambio
               05  REC-COD-TCAM                         PIC X(02).
      *
      *INF Fecha Referencia Valor Asociado a Relacion
           03  REC-FEC-FVAL.
               05  REC-NUM-SVAL                         PIC 9(02).
               05  REC-NUM-AVAL                         PIC 9(02).
               05  REC-NUM-MVAL                         PIC 9(02).
               05  REC-NUM-DVAL                         PIC 9(02).
      *
      *    Fecha de Verificacion
           03  REC-FEC-FVRF.
               05  REC-NUM-SVRF                         PIC 9(02).
               05  REC-NUM-AVRF                         PIC 9(02).
               05  REC-NUM-MVRF                         PIC 9(02).
               05  REC-NUM-DVRF                         PIC 9(02).
      *
      *MOD-01  X(31).
           03  REC-GLS-DISP                             PIC X(29).
      *
       01  REC-VARI.
           03  REC-COD-CTCA.
               05  REC-COD-TREC                         PIC X(02).
      *MOD-01  X(03).
               05  REC-COD-AREC                         PIC X(05).
           03  REC-COD-DCOM             VALUE 'DC'      PIC X(02).
           03  REC-COD-RBCO             VALUE 'RB'      PIC X(02).
           03  REC-COD-FUNC             VALUE 'FU'      PIC X(02).
           03  REC-COD-CONY             VALUE 'CY'      PIC X(02).
           03  REC-COD-CMAT             VALUE 'CM'      PIC X(02).
           03  REC-COD-REPR             VALUE 'RP'      PIC X(02).
           03  REC-COD-MCLI             VALUE 'MC'      PIC X(02).
           03  REC-COD-RLEG             VALUE 'RL'      PIC X(02).
           03  REC-COD-PRES             VALUE 'PP'      PIC X(02).
           03  REC-COD-APOD             VALUE 'AP'      PIC X(02).
           03  REC-COD-EMPL             VALUE 'EM'      PIC X(02).
           03  REC-COD-COMD             VALUE 'CD'      PIC X(02).
           03  REC-COD-PERA             VALUE 'PA'      PIC X(02).
           03  REC-COD-SOCD             VALUE 'SD'      PIC X(02).
           03  REC-COD-OPOD             VALUE 'OP'      PIC X(02).
      *    Datos Basicos del Cliente
      *    -------------------------
      *
      *    Nombre Registro : DBC
      *    Clave(s)        : DBC-KEY-IDBC(UU), DBC-STP-ITRN(UU),DBC-IDC-
      *                      DBC-GLS-NOMC(DC), DBC-SNX-NOMC(DC)
      *    Largo           : 241
      *    Bloqueo         : 1
      *    Observaciones   :
      *
       01  DBC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  DBC-GLS-FLAG                             PIC X(01).
      *
      *KEY Clave Primaria Registro
           03  DBC-KEY-IREG.
      *
      *        Identificacion Datos Basicos Clientes
               05  DBC-KEY-IDBC.
      *
      *            Cod. Int. Comp. Cliente
                   07  DBC-CIC-ICLI.
      *
      *                Centro Asignacion Identificacion
                       09  DBC-CAI-ICLI.
                           11  DBC-COD-TCLI             PIC X(01).
                           11  DBC-COD-CCLI             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  DBC-IIC-ICLI                 PIC X(08).
      *
      *        Time Stamp Transaccion
               05  DBC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  DBC-FEC-FTRN.
                       09  DBC-NUM-STRN                 PIC 9(02).
                       09  DBC-NUM-ATRN                 PIC 9(02).
                       09  DBC-NUM-MTRN                 PIC 9(02).
                       09  DBC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  DBC-HRA-HRTR.
                       09  DBC-NUM-HHTR                 PIC 9(02).
                       09  DBC-NUM-MMTR                 PIC 9(02).
                       09  DBC-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  DBC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  DBC-MSC-STAT                             PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  DBC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  DBC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  DBC-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  DBC-COD-ATRN                             PIC X(12).
      *
      *KEY Rut Cliente + Indicador o Calificador + Extension
           03  DBC-IDC-ICLI.
      *
      *        RUT Cliente
               05  DBC-NUM-ICLI                         PIC X(08).
      *
      *        Indicador Tipo RUT
               05  DBC-IND-ICLI                         PIC X(01).
      *
      *        Extension del RUT
               05  DBC-GLS-ICLI                         PIC X(03).
      *
      *KEY Digito Verificador del Rut
           03  DBC-VRF-ICLI                             PIC X(01).
      *
      *KEY Nombre Cliente Generico (Persona o Empresa)
           03  DBC-GLS-NOMC.
               05  DBC-GLS-APAT                         PIC X(25).
               05  DBC-GLS-AMAT                         PIC X(25).
               05  DBC-GLS-NOMB                         PIC X(25).
      *
      *    Soundex del Nombre de Cliente
           03  DBC-SNX-NOMC.
               05  DBC-SNX-APAT                         PIC X(03).
               05  DBC-SNX-AMAT                         PIC X(03).
               05  DBC-SNX-NOMB                         PIC X(03).
      *
      *    Numero de Direccion de Envio Correspondencia
           03  DBC-NUM-DCOR                             PIC 9(02).
      *
      *OPR Ind Tipo Cliente ( Persona o Empresa )       SGC TICL
           03  DBC-IND-TICL                             PIC X(01).
      *
      *    Tipo Cliente (Potencial,No Cliente,Grupo)    SGC TCL
           03  DBC-COD-TICL                             PIC X(02).
      *
      *    Codigo Clasificacion Comercial               SGC CLC
           03  DBC-COD-CLCM                             PIC X(02).
      *
      *RCM Codigo del Ejecutivo Encargado del Cliente   TAB USR
           03  DBC-COD-EJEC                             PIC X(12).
      *
      *OPR Codigo de Oficina donde opera el Cliente     TAB OFI
           03  DBC-COD-OFIC                             PIC X(03).
      *
      *RCM Indicador si opera en otra Oficina           SGC OOFI
           03  DBC-IND-OOFI                             PIC X(01).
      *
      *EXT Codigo Actividad Economica                   TAB ACT
           03  DBC-COD-ACTI                             PIC X(09).
      *
      *EXT Origen y Destino Sect (Comp.Inst)            TAB ODS
           03  DBC-COD-ODSE                             PIC X(03).
      *
      *INF Fecha Ultima Declaracion Articulo 85
           03  DBC-FEC-FA85.
               05  DBC-NUM-SA85                         PIC 9(02).
               05  DBC-NUM-AA85                         PIC 9(02).
               05  DBC-NUM-MA85                         PIC 9(02).
               05  DBC-NUM-DA85                         PIC 9(02).
      *
      *    Tipo Ultimo Pago Impuesto a la Renta         SGC UPIR
           03  DBC-IND-UPIR                             PIC X(03).
      *
      *    Fecha Ultimo Pago Impuesto a la Renta
           03  DBC-FEC-FUPI.
               05  DBC-NUM-SUPI                         PIC 9(02).
               05  DBC-NUM-AUPI                         PIC 9(02).
               05  DBC-NUM-MUPI                         PIC 9(02).
               05  DBC-NUM-DUPI                         PIC 9(02).
      *
      *INF Indicador de Multas Banco Central            SGC MUBC
           03  DBC-IND-MUBC                             PIC X(01).
      *
      *INF Indicador de Juicios Banco Central           SGC JUBC
           03  DBC-IND-JUBC                             PIC X(01).
      *
      *EXT Codigo Estado Cliente ( como Deudor )        SGC SCLI
           03  DBC-MSC-SCLI                             PIC X(01).
      *
      *    Fecha Estado Cliente
           03  DBC-FEC-FSCL.
               05  DBC-NUM-SSCL                         PIC 9(02).
               05  DBC-NUM-ASCL                         PIC 9(02).
               05  DBC-NUM-MSCL                         PIC 9(02).
               05  DBC-NUM-DSCL                         PIC 9(02).
      *
      *    Codigo de Bloqueo del Cliente                SGC BLO
           03  DBC-COD-BLOC                             PIC X(02).
      *
      *    Numero Total de Direcciones
           03  DBC-NUM-TDIR                             PIC 9(02).
      *
      *    Numero Total de Comentarios
           03  DBC-NUM-TCMT                             PIC 9(03).
      *
      *    Fecha Inicio Operacion
           03  DBC-FEC-FIOP.
               05  DBC-NUM-SIOP                         PIC 9(02).
               05  DBC-NUM-AIOP                         PIC 9(02).
               05  DBC-NUM-MIOP                         PIC 9(02).
               05  DBC-NUM-DIOP                         PIC 9(02).
      *
      *    Fecha Termino Operacion
           03  DBC-FEC-FTOP.
               05  DBC-NUM-STOP                         PIC 9(02).
               05  DBC-NUM-ATOP                         PIC 9(02).
               05  DBC-NUM-MTOP                         PIC 9(02).
               05  DBC-NUM-DTOP                         PIC 9(02).
      *
      *    Numero Operaciones Fecha Termino Nula
           03  DBC-NUM-OFTN                             PIC 9(03).
      *
      *    Fecha Ingreso de Datos a la Base de Datos
           03  DBC-FEC-FINP.
               05  DBC-NUM-SINP                         PIC 9(02).
               05  DBC-NUM-AINP                         PIC 9(02).
               05  DBC-NUM-MINP                         PIC 9(02).
               05  DBC-NUM-DINP                         PIC 9(02).
      *
      *    Disponible
      *    03  DBC-GLS-DISP                             PIC X(00).
      *    Registro Generico de Tablas
      *    ---------------------------
      *
      *    Nombre Registro : TAB
      *    Clave(s)        : TAB-COD-TABL(UU), TAB-STP-ITRN(UU),
      *                      TAB-CIC-TABL(UU), TAB-EXT-TABL(DC)
      *    Largo           : 194
      *    Bloqueo         : 2
      *    Observaciones   :
      *
       01  TAB.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  TAB-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  TAB-KEY-IREG.
      *
      *KEY     Tipo + Codigo ( LLave Primaria )
               05  TAB-COD-TABL.
      *
      *            Codigo Tipo Tabla
                   07  TAB-COD-TTAB                     PIC X(03).
      *
      *            Codigo Tabla
                   07  TAB-COD-CTAB                     PIC X(12).
      *
      *        Time Stamp Transaccion
               05  TAB-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  TAB-FEC-FTRN.
                       09  TAB-NUM-STRN                 PIC 9(02).
                       09  TAB-NUM-ATRN                 PIC 9(02).
                       09  TAB-NUM-MTRN                 PIC 9(02).
                       09  TAB-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  TAB-HRA-HRTR.
                       09  TAB-NUM-HHTR                 PIC 9(02).
                       09  TAB-NUM-MMTR                 PIC 9(02).
                       09  TAB-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  TAB-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  TAB-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  TAB-IND-VIGE  REDEFINES  TAB-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  TAB-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  TAB-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  TAB-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  TAB-COD-ATRN                             PIC X(12).
      *
      *KEY Tipo + Codigo ( Interno Computacional )
           03  TAB-CIC-TABL.
      *
      *        Codigo Tipo Tabla
               05  TAB-CIC-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  TAB-CIC-CTAB                         PIC X(12).
      *
      *    Codigo Externo
           03  TAB-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  TAB-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  TAB-EXT-CTAB                         PIC X(05).
      *
      *    Descripcion Corta
           03  TAB-GLS-DCOR                             PIC X(12).
      *
      *    Abreviacion Descripcion
           03  TAB-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion del Codigo
           03  TAB-GLS-DESC                             PIC X(40).
      *
      *    Datos Anexos
           03  TAB-GLS-DATA.
               05  TAB-GLS-DAT1.
                   07  TAB-COD-DAT1     OCCURS 10       PIC X(01).
               05  TAB-GLS-DAT2.
                   07  TAB-COD-DAT2     OCCURS 10       PIC X(02).
               05  TAB-GLS-DAT3.
                   07  TAB-COD-DAT3     OCCURS 10       PIC X(03).
      *
      *    Clave Soundex
           03  TAB-SNX-TABL.
               05  TAB-SNX-TTAB                         PIC X(03).
               05  TAB-SNX-CTAB                         PIC X(03).
      *
      *    Variables de Tablas
       01  TAB-VARI.
      *
      *    Codigo Sistema
           03  TAB-COD-SIST                             PIC X(03).
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  WB-ELEM-LIS        PIC X(11).
           EJECT
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           EJECT
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              SORT SUPSSOR ON ASCENDING KEY SOR-CLAVE
              INPUT  PROCEDURE IS C-PROCESAR
              OUTPUT PROCEDURE IS D-GRABAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM E-TERMINAR.
           SKIP1
       AZ-FIN-DE-PROCESO.
           GOBACK.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
           PERFORM BA-ABRIR-ARCHIVOS.
           PERFORM BB-CARGA-TABLA-RSC.
           GO BZ-FIN-DE-SECCION.
       BA-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT SUPVI02
           IF WR-FS-SUPVI02 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'     TO WR-SECCION
              MOVE 'BA-ABRIR-ARCHIVOS' TO WR-PARRAFO
              MOVE 'SUPVI02    '       TO WR-ARCHIVO
              MOVE 'OPEN       '       TO WR-OPERACI
              MOVE WR-FS-SUPVI02       TO WR-FILE-ST
              MOVE 1                   TO WS-PROCESO
              GO BZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       BB-CARGA-TABLA-RSC.
      *-------------------
           INITIALIZE W-TABLAS.
           PERFORM XX-LEER-TAB.
           MOVE 'REDNX'                     TO WB-REQ-TAB-CMD
           PERFORM XX-LEER-TAB.
           SET WI-IND                       TO 1
           DISPLAY 'TABLA DE RUT FICTICIOS CARGADA...'
           PERFORM UNTIL (WS-PROCESO-ERROR  OR WB-REQ-TAB-RC NOT = '  '
                       OR TAB-COD-TTAB NOT = 'RSC')
                   MOVE TAB-COD-CTAB        TO WT-RUT-SUP(WI-IND)
                   MOVE TAB-GLS-DATA        TO WT-RUT-FIC(WI-IND)
                   DISPLAY 'RUT SUPER : '      WT-RUT-SUP(WI-IND)
                       ' RUT FICTICIO : '      WT-RUT-FIC(WI-IND)
                   SET WI-IND               UP BY 1
                   PERFORM XX-LEER-TAB
                   IF (WI-IND > 1000)
                       MOVE 'B-INICIALIZAR'      TO WR-SECCION
                       MOVE 'BB-CARGA-TABLA-RSC' TO WR-PARRAFO
                       MOVE 'TAB'                TO WR-ARCHIVO
                       MOVE WB-REQ-TAB-CMD       TO WR-OPERACI
                       MOVE WB-REQ-TAB-RC        TO WR-FILE-ST
                       MOVE 'SE LLENO TABLA INT' TO WR-MENSAJE
                       MOVE 1                    TO WS-PROCESO
                   END-IF
           END-PERFORM.
           SKIP1
       XX-LEER-TAB.
      *------------
           MOVE SPACES                   TO WB-REQ-TAB-RC
           MOVE 'TAB00'                  TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-TAB-AREA TAB WB-ELEM-LIS
           IF (WB-REQ-TAB-RC NOT = '  ' AND NOT = '14')
               MOVE 'B-INICIALIZAR'      TO WR-SECCION
               MOVE 'BB-LEER-TAB'        TO WR-PARRAFO
               MOVE 'TAB'                TO WR-ARCHIVO
               MOVE WB-REQ-TAB-CMD       TO WR-OPERACI
               MOVE WB-REQ-TAB-RC        TO WR-FILE-ST
               MOVE WB-REQ-TAB-KEY-VALUE TO WR-MENSAJE
               MOVE 1                    TO WS-PROCESO
           END-IF.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GRAL.
      *----------------
           PERFORM CB-LEER-REC
           MOVE 'REDNE'        TO WB-REQ-REC-CMD
           PERFORM CC-SELECCIONA-REGS
              UNTIL (WB-REQ-REC-RC   NOT = '  '           OR
                     WS-PROCESO-ERROR)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-REC.
      *------------
           MOVE SPACES TO WB-REQ-REC-RC
           MOVE 'REC00' TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-REC-AREA REC WB-ELEM-LIS
           IF (WB-REQ-REC-RC NOT = '  ' AND NOT = '14')
               MOVE 'C-PROCESAR'        TO WR-SECCION
               MOVE 'CB-LEER-REC'       TO WR-PARRAFO
               MOVE 'REC'               TO WR-ARCHIVO
               MOVE WB-REQ-REC-CMD      TO WR-OPERACI
               MOVE WB-REQ-REC-RC       TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO CZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       CC-SELECCIONA-REGS.
      *-------------------
           ADD 1 TO WA-L-REC
           IF (REC-COD-TREC IN REC = 'RB' AND REC-IND-VIGE IN REC = 'S')
               MOVE REC-COD-AREC IN REC TO WR-COD-AREC
               MOVE WR-COD-AREC         TO SOR-GRP-ECOM
               MOVE REC-CIC-ICLI IN REC TO SOR-CIC-ICLI
               RELEASE SUPRSOR
               ADD 1 TO WA-G-SOR
           END-IF.
           PERFORM CB-LEER-REC.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-GRABAR SECTION.
      *=================
       DA-SALIDA-SORT.
      *---------------
           IF (WS-PROCESO-ERROR)
               GO DZ-FIN-DE-SECCION
           END-IF.
           RETURN SUPSSOR AT END MOVE 1 TO WR-FS-SUPSSOR END-RETURN
           PERFORM DB-PROCESO-SORT
             UNTIL (WR-FS-SUPSSOR NOT = 0 OR WS-PROCESO-ERROR).
           GO DZ-FIN-DE-SECCION.
           SKIP1
       DB-PROCESO-SORT.
      *----------------
           MOVE SOR-CIC-ICLI TO WB-REQ-DBC-KEY-VALUE
           MOVE 0 TO WI-I WR-GRP-ANTE  I02-NUM-GRP-ECOM1
                     I02-NUM-GRP-ECOM2 I02-NUM-GRP-ECOM3
                     I02-NUM-GRP-ECOM4 I02-NUM-GRP-ECOM5
           PERFORM DB-FORMA-REGISTRO
             UNTIL (SOR-CIC-ICLI  NOT = WB-REQ-DBC-KEY-VALUE OR
                    WR-FS-SUPSSOR NOT = 0)
           IF (WI-I > 0)
               PERFORM DC-LEER-DBC
               IF (WB-REQ-DBC-RC = '  ')
                   PERFORM DD-COMPACTA-NOMBRE
                   PERFORM DF-GRABA-REGISTRO
               ELSE
                   MOVE 2 TO WS-PROCESO
                   DISPLAY '** NO SE ENCUENTRA EN DBC ==> ' REC-CIC-ICLI
                   ADD 1 TO WA-R-REC
               END-IF
           END-IF.
           SKIP1
       DB-FORMA-REGISTRO.
      *------------------
           ADD 1 TO WA-L-SOR
           IF (WI-I < 5 AND SOR-GRP-ECOM NOT = WR-GRP-ANTE)
               MOVE SOR-GRP-ECOM TO WR-GRP-ANTE
               ADD 1 TO WI-I
               EVALUATE WI-I
                 WHEN '001' MOVE SOR-GRP-ECOM TO I02-NUM-GRP-ECOM1
                 WHEN '002' MOVE SOR-GRP-ECOM TO I02-NUM-GRP-ECOM2
                 WHEN '003' MOVE SOR-GRP-ECOM TO I02-NUM-GRP-ECOM3
                 WHEN '004' MOVE SOR-GRP-ECOM TO I02-NUM-GRP-ECOM4
                 WHEN '005' MOVE SOR-GRP-ECOM TO I02-NUM-GRP-ECOM5
               END-EVALUATE
           END-IF.
           RETURN SUPSSOR AT END MOVE 1 TO WR-FS-SUPSSOR END-RETURN.
           SKIP1
       DC-LEER-DBC.
      *------------
           MOVE SPACES  TO WB-REQ-DBC-RC
           MOVE 'DBC00' TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-DBC-AREA DBC WB-ELEM-LIS
           IF (WB-REQ-DBC-RC NOT = '  ' AND NOT = '14')
               MOVE 'D-GRABAR'          TO WR-SECCION
               MOVE 'DC-LEER-DBC'       TO WR-PARRAFO
               MOVE 'DBC'               TO WR-ARCHIVO
               MOVE WB-REQ-DBC-CMD      TO WR-OPERACI
               MOVE WB-REQ-DBC-RC       TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO DZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       DD-COMPACTA-NOMBRE.
      *-------------------
           IF (DBC-IND-TICL IN DBC = 'P')
               MOVE DBC-GLS-NOMC IN DBC TO WT-NOMBRE
               MOVE 0                   TO WI-K WI-I WI-N
               PERFORM DE-COMPACTA VARYING WI-I FROM 1 BY 1
                       UNTIL (WI-I > 75)
               MOVE WT-NOMBRE TO DBC-GLS-NOMC IN DBC
           END-IF.
           SKIP1
       DE-COMPACTA.
      *------------
           MOVE WT-N(WI-I) TO WR-CAR-AUX
           MOVE ' '        TO WT-N(WI-I)
           IF (WI-K = 0 AND WR-CAR-AUX = ' ')
               NEXT SENTENCE
           ELSE
           IF (WR-CAR-AUX = ' ')
               IF (WT-N(WI-K) = '/' OR = ' ')
                   NEXT SENTENCE
               ELSE
                   ADD 1 TO WI-K
                   ADD 1 TO WI-N
                   IF (WI-N < 3)
                       MOVE '/' TO WT-N(WI-K)
                   ELSE
                       MOVE ' ' TO WT-N(WI-K)
                   END-IF
               END-IF
           ELSE
               ADD 1 TO WI-K
               MOVE WR-CAR-AUX TO WT-N(WI-K)
           END-IF
           END-IF.
           SKIP1
       DF-GRABA-REGISTRO.
      *------------------
           MOVE DBC-GLS-NOMC IN DBC         TO I02-NOM-O-RSOCIAL

           MOVE DBC-NUM-ICLI                TO WR-RUT-D00-L08
           MOVE DBC-VRF-ICLI                TO WR-RUT-D00-VRF
           SET WI-IND                       TO 1
           SEARCH WT-TABLA-RUT
            AT END
                  MOVE DBC-NUM-ICLI IN DBC  TO I02-NUM-RUT
                  MOVE DBC-VRF-ICLI IN DBC  TO I02-VRF-RUT
            WHEN (WT-RUT-SUP-L09(WI-IND) = SPACES)
                  MOVE DBC-NUM-ICLI IN DBC  TO I02-NUM-RUT
                  MOVE DBC-VRF-ICLI IN DBC  TO I02-VRF-RUT
            WHEN (WT-RUT-FIC(WI-IND) = WR-RUT-D00-L09)
                  DISPLAY 'RUT FICTICIO : ' WR-RUT-D00-L09
                  ' CAMBIADO POR RUT SUPER ' WT-RUT-SUP(WI-IND)
                  MOVE WT-RUT-SUP(WI-IND) TO I02-RUT-RELACION
           END-SEARCH

           WRITE I02.
           IF (WR-FS-SUPVI02 = 22)
               DISPLAY '** REGISTRO DUPLICADO I02 ==> ' I02
               MOVE 2 TO WS-PROCESO
               ADD  1 TO WA-R-REC
           ELSE
           IF (WR-FS-SUPVI02 = 0)
               ADD 1 TO WA-G-I02
           ELSE
               MOVE 'D-GRABAR'          TO WR-SECCION
               MOVE 'DE-GRABA-REGISTRO' TO WR-PARRAFO
               MOVE 'SUPVI02'           TO WR-ARCHIVO
               MOVE 'WRITE'             TO WR-OPERACI
               MOVE WR-FS-SUPVI02       TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO DZ-FIN-DE-SECCION
           END-IF
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       E-TERMINAR SECTION.
      *===================
       EA-ESTADISTICAS.
      *----------------
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'SUPPCI02 NO EMITE LISTADO'
           DISPLAY ' '
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPCI02 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY ' CARGA EN ARCHIVO I02 PERSONAS/GRUPO ECONOMICO'
           DISPLAY ' '
           DISPLAY 'ARCHIVO  LEIDOS  RECHAZADOS   GRABADOS'
           DISPLAY ' '
           DISPLAY 'REC    : ' WA-L-REC '     ' WA-R-REC '    ' WA-G-REC
           DISPLAY 'DBC    : ' WA-L-DBC '     ' WA-R-DBC '    ' WA-G-DBC
           DISPLAY 'SORT   : ' WA-L-SOR '     ' WA-R-SOR '    ' WA-G-SOR
           DISPLAY 'SUPVI02: ' WA-L-I02 '     ' WA-R-I02 '    ' WA-G-I02
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-ERROR OR WS-MENSAJE-OP
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       EC-TERMINO.
      *-----------
           CLOSE SUPVI02.
           IF WS-PROCESO-ERROR
              MOVE 100 TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY ' '
              DISPLAY 'EL ERROR FUE EN LA SECCION          ' WR-SECCION
              DISPLAY '                   PARRAFO          ' WR-PARRAFO
              DISPLAY '                   ARCHIVO          ' WR-ARCHIVO
              DISPLAY '                   OPERACION        ' WR-OPERACI
              DISPLAY '                   FILE STATUS      ' WR-FILE-ST
              DISPLAY '                   MENSAJE DE ERROR ' WR-MENSAJE
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       EZ-FIN-DE-SECCION.
      *------------------
           EXIT.
