       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.     SUPPGP14.
       AUTHOR.         CONSIST.
      *DAD ini
       DATE-WRITTEN.   28-OCT-1994    (Ultima Modificacion).
      *DAD fin

      * GENERA ARCHIVO P14 EN FORMA BATCH A PARTIR DEL D02 SORTEADO.

       ENVIRONMENT DIVISION.
      *=====================
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *DAD ini  30-DEC-1991
      *DOS SELECT  SRTD02 ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT  SRTD02 ASSIGN TO        DA-S-SORTWK1.
           SELECT  SRTD02 ASSIGN TO        DA-S-SORTWK1.
      *DAD fin  30-DEC-1991

       DATA DIVISION.
      *=============
       FILE SECTION.
       SD  SRTD02.
       01  SRT.
           03 SRT-COD-PROD                        PIC X(04).
           03 SRT-COD-LOCA                        PIC X(06).
           03 SRT-IND-TMON                        PIC X(03).
           03 SRT-VAL-SCON                        PIC 9(11)V9(02).
           03 SRT-VAL-SMOR                        PIC 9(11)V9(02).
           03 SRT-VAL-SCVC                        PIC 9(11)V9(02).
           03 SRT-IND-ESTC                        PIC X(01).

       WORKING-STORAGE SECTION.
      *------------------------
       COPY SUPBRD02.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWVIDD.
       COPY GNSWGSYS.
       COPY SUPBRP14.
       COPY GNSBRTAB.
       COPY GNSBRMSC.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       01  WSS-VARI.
           03 WSS-KOLD.
              05 WSS-COD-PROD                          PIC X(04).
              05 WSS-COD-LOCA                          PIC X(06).
              05 WSS-IND-TMON                          PIC X(03).
           03 WSS-KNEW.
              05 WSS-COD-PROD                          PIC X(04).
              05 WSS-COD-LOCA                          PIC X(06).
              05 WSS-IND-TMON                          PIC X(03).
           03 WSS-VAL-SCON         VALUE 0             PIC 9(11)V9(02).
           03 WSS-VAL-SMOR         VALUE 0             PIC 9(11)V9(02).
           03 WSS-VAL-SCVC         VALUE 0             PIC 9(11)V9(02).
      *DAD ini  05-MAR-1993
           03 WSS-VAL-PASO         VALUE 0             PIC 9(11)V9(02).
      *DAD fin  05-MAR-1993
      *DAD ini  28-OCT-1994 / 26-10-89
           03 WSS-NUM-SCON         VALUE 0             PIC 9(06).
           03 WSS-NUM-SMOR         VALUE 0             PIC 9(06).
           03 WSS-NUM-SCVC         VALUE 0             PIC 9(06).
      *DAD fin  28-OCT-1994 / 26-10-89
           03 WSS-IND-ICOL         VALUE SPACES        PIC X(02).
           03 WSS-COD-NIIO.
              05 WSS-COD-SIST      VALUE SPACES        PIC X(01).
              05 FILLER            VALUE SPACES        PIC X(11).
           03 PRE-VARI.
              05 PRE-COD-TCCO      VALUE LOW-VALUES    PIC X(04).
              05 PRE-GLS-TCCO      VALUE LOW-VALUES    PIC X(40).
              05 PRE-COD-MONE      VALUE LOW-VALUES    PIC X(03).
              05 PRE-IND-TMON      VALUE LOW-VALUES    PIC X(03).


       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           MOVE 'SUPPGP14' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           SORT SRTD02
                      ON ASCENDING KEY SRT-COD-PROD IN SRT
                      ON ASCENDING KEY SRT-COD-LOCA IN SRT
                      ON ASCENDING KEY SRT-IND-TMON IN SRT
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
           STOP RUN.
       FIN-MAIN.
           EXIT.

       INP-SRT SECTION.
       INI-INP-SRT.
      *JJV MOVE FIO-INP TO FIO-CMND.
      *    PERFORM GNS-FIO-TAB.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM SUP-FIO-P14.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR DE APERTURA  P14: ABORTO ' FIO-STAT
               STOP RUN.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SUP-FIO-D02.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR DE APERTURA  D02: ABORTO ' FIO-STAT
               STOP RUN.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM SUP-FIO-D02.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ARCHIVO D02 VACIO: FIN DE PROCESO'
               MOVE FIO-CLO TO FIO-CMND
               PERFORM SUP-FIO-D02
               STOP RUN.
      *DAD ini  13/07/90
      * Para saltarce el primer registro del D02 que es de Control
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM SUP-FIO-D02.
      *DAD fin  13/07/90
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
               MOVE FIO-CLO TO FIO-CMND
               PERFORM SUP-FIO-D02
               GO TO FIN-INP-SRT.
      *DAD ini  06/07/90
           IF D02-VAL-SCVD IN D02 > ZEROES OR
              D02-IND-DCAS IN D02 = 'C'
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM SUP-FIO-D02
               GO TO LUP-INP-SRT.
      *DAD fin  06/07/90
      *DAD ini  05/12/89
           IF NOT ( D02-IND-TDOP IN D02 = 'D' AND
      *DAD ini  02-JUL-1991
                    D02-NUM-DDIR IN D02 = 1 )
      *DAD fin  02-JUL-1991
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM SUP-FIO-D02
               GO TO LUP-INP-SRT.
      *DAD fin  05/12/89
           IF D02-COD-NIIO IN D02 = WSS-COD-NIIO IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM SUP-FIO-D02
               GO TO LUP-INP-SRT
           ELSE
               MOVE D02-COD-NIIO IN D02 TO WSS-COD-NIIO IN WSS-VARI.

           IF D02-COD-TCCO IN D02 NOT = PRE-COD-TCCO OR
              PRE-COD-TCCO = LOW-VALUES
      *DAD ini  02/08/89
      *       MOVE D02-COD-TCCO IN D02 TO PRE-COD-TCCO
      *DAD fin  02/08/89
              MOVE 'SUP'               TO TAB-COD-SIST
              MOVE 'PRD'               TO TAB-COD-TTAB IN TAB
              MOVE D02-COD-TCCO IN D02 TO TAB-COD-CTAB IN TAB
      *ISP
              MOVE TAB-COD-SIST TO FIO-SIST
              MOVE FIO-GET-KEY TO FIO-CMND
              PERFORM GNS-FIO-TAB
              IF NOT FIO-STAT-OKS
                  MOVE FIO-GET-NXT TO FIO-CMND
                  PERFORM SUP-FIO-D02
                  GO TO LUP-INP-SRT
              ELSE
      *DAD ini  02/08/89
                  MOVE D02-COD-TCCO IN D02 TO PRE-COD-TCCO
      *DAD fin  02/08/89
                  MOVE TAB-GLS-DATA IN TAB TO PRE-GLS-TCCO.
           MOVE PRE-GLS-TCCO TO WSS-IND-ICOL IN WSS-VARI.

           IF ( WSS-IND-ICOL IN WSS-VARI NOT = 'CE' ) AND
              ( WSS-IND-ICOL IN WSS-VARI NOT = 'LC' )
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM SUP-FIO-D02
               GO TO LUP-INP-SRT.
           MOVE D02-COD-TCCO IN D02 TO SRT-COD-PROD IN SRT.
           MOVE D02-COD-LOCA IN D02 TO SRT-COD-LOCA IN SRT.

           IF D02-COD-MONE IN D02 NOT = PRE-COD-MONE OR
              PRE-COD-MONE = LOW-VALUES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-EXT-TTAB IN TAB
               MOVE D02-COD-MONE IN D02 TO TAB-EXT-CTAB IN TAB
               MOVE 'TAB-EXT-TABL'      TO FIO-AKEY
               PERFORM BUS-TAB
      *DAD ini
               MOVE TAB-COD-DAT3 IN TAB(1) TO PRE-IND-TMON.
      *DAD fin
           MOVE PRE-IND-TMON        TO SRT-IND-TMON IN SRT.
           MOVE D02-VAL-SCON IN D02 TO SRT-VAL-SCON IN SRT.
      *DAD ini  30-DEC-1991 / AVM
      * El Saldo Contable debe INCLUIR los Intereses Devengados
      *     SUBTRACT D02-VAL-IPCO IN D02 FROM SRT-VAL-SCON IN SRT.
      *     SUBTRACT D02-VAL-IPCV IN D02 FROM SRT-VAL-SCON IN SRT.
      *DAD fin  30-DEC-1991
           MOVE D02-VAL-SMOR IN D02 TO SRT-VAL-SMOR IN SRT.
      *DAD INSTRUCCION SIGUIENTE FUE MODIFICADA POR  "SENC"----> "*"
      *    QUE FUE REALIZADA POR EL PROPIO BANCO DE CREDITO
      *    MOVE D02-VAL-SCVC IN D02 TO SRT-VAL-SCVC IN SRT.
      *SENC 9/5/88. SE SUMA SALDO CARTERA VENCIDA VENDIDA A SRT-VAL-SCVC
           MOVE ZEROES              TO SRT-VAL-SCVC IN SRT.
           ADD  D02-VAL-SCVC IN D02 TO SRT-VAL-SCVC IN SRT.
           ADD  D02-VAL-SCVV IN D02 TO SRT-VAL-SCVC IN SRT.
      *
           MOVE D02-IND-ESTC IN D02 TO SRT-IND-ESTC IN SRT.

      *ODJ INSTRUCCION SIGUIENTE FUE MODIFICADA POR  "OADJ"----> "*"
      *    QUE FUE REALIZADA POR EL PROPIO BANCO DE CREDITO
           IF (WSS-COD-SIST  IN WSS-VARI = '5' AND
               D02-COD-TCCO IN D02       = '1115')
               MOVE '1116'              TO SRT-COD-PROD IN SRT.
      *ODJ
           RELEASE SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM SUP-FIO-D02.
           GO TO LUP-INP-SRT.
      *JJV MOVE FIO-CLO TO FIO-CMND.
      *    PERFORM GNS-FIO-TAB.
       FIN-INP-SRT.
           EXIT.

       OUT-SRT SECTION.
       INI-OUT-SRT.
           RETURN SRTD02 AT END
               GO TO CON-OUT-SRT.
           PERFORM PUT-KNW.
           PERFORM ADD-VAL.
       LUP-OUT-SRT.
           MOVE WSS-KNEW TO WSS-KOLD.
           RETURN SRTD02 AT END
               GO TO CON-OUT-SRT.
           PERFORM PUT-KNW.
           IF WSS-KNEW = WSS-KOLD
               PERFORM ADD-VAL
               GO TO LUP-OUT-SRT.
           PERFORM PUT-P14.
           MOVE ZEROES TO WSS-VAL-SCON IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-SMOR IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-SCVC IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-SCON IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-SMOR IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-SCVC IN WSS-VARI.
           MOVE 'SUP'               TO MSC-COD-SIST.
           MOVE 'ESTC'              TO MSC-COD-TMSC IN MSC.
           MOVE SRT-IND-ESTC IN SRT TO MSC-COD-CMSC IN MSC.
           MOVE SPACES              TO MSG-COD-MENS.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS NOT = SPACES
               DISPLAY 'REGISTRO NO PROCESADO:'
               DISPLAY 'CODIGO: *' SRT-IND-ESTC IN SRT '*'
               DISPLAY SRT
               GO TO LUP-OUT-SRT
           ELSE
               PERFORM ADD-VAL-SRT
               GO TO LUP-OUT-SRT.
       CON-OUT-SRT.
           PERFORM PUT-P14.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SUP-FIO-P14.
       FIN-OUT-SRT.
           EXIT.

       PUT-KNW SECTION.
       INI-PUT-KNW.
           MOVE SRT-COD-PROD IN SRT TO WSS-COD-PROD IN WSS-KNEW.
           MOVE SRT-COD-LOCA IN SRT TO WSS-COD-LOCA IN WSS-KNEW.
           MOVE SRT-IND-TMON IN SRT TO WSS-IND-TMON IN WSS-KNEW.
       FIN-PUT-KNW.
           EXIT.

       PUT-P14 SECTION.
       INI-PUT-P14.
           MOVE WSS-COD-PROD IN WSS-KOLD TO P14-COD-PROD.
           MOVE WSS-COD-LOCA IN WSS-KOLD TO P14-COD-LOCA.
           MOVE WSS-IND-TMON IN WSS-KOLD TO P14-IND-TMON.
           MOVE WSS-VAL-SCON IN WSS-VARI TO P14-VAL-SCON.
           MOVE WSS-VAL-SMOR IN WSS-VARI TO P14-VAL-SMOR.
           MOVE WSS-VAL-SCVC IN WSS-VARI TO P14-VAL-SCVC.
           MOVE WSS-NUM-SCON IN WSS-VARI TO P14-NUM-SCON.
           MOVE WSS-NUM-SMOR IN WSS-VARI TO P14-NUM-SMOR.
           MOVE WSS-NUM-SCVC IN WSS-VARI TO P14-NUM-SCVC.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM SUP-FIO-P14.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR DE ESCRITURA  P14 ' FIO-STAT
               DISPLAY P14
           ELSE
               DISPLAY 'REGISTRO GRABADO P14'
               DISPLAY P14.
       FIN-PUT-P14.
           EXIT.

       ADD-VAL SECTION.
       INI-ADD-VAL.
           MOVE 'SUP'               TO MSC-COD-SIST.
           MOVE 'ESTC'              TO MSC-COD-TMSC IN MSC.
           MOVE SRT-IND-ESTC IN SRT TO MSC-COD-CMSC IN MSC.
           MOVE SPACES TO MSG-COD-MENS.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS NOT = SPACES
               DISPLAY 'REGISTRO NO PROCESADO:'
               DISPLAY SRT
           ELSE
               PERFORM ADD-VAL-SRT.
       FIN-ADD-VAL.
           EXIT.

       ADD-VAL-SRT SECTION.
       INI-ADD-VAL-SRT.
           IF MSC-CIC-CMSC IN MSC = '1'
               ADD 1                   TO WSS-NUM-SCON IN WSS-VARI
               ADD SRT-VAL-SCON IN SRT TO WSS-VAL-SCON IN WSS-VARI
           ELSE
           IF MSC-CIC-CMSC IN MSC = '2'
               ADD 1                   TO WSS-NUM-SMOR IN WSS-VARI
      *DAD ini  05-MAR-1993 / 05-FEB-1993
               ADD SRT-VAL-SMOR IN SRT TO WSS-VAL-SMOR IN WSS-VARI
               SUBTRACT SRT-VAL-SMOR IN SRT FROM
                        SRT-VAL-SCON IN SRT GIVING
                        WSS-VAL-PASO IN WSS-VARI
               ADD WSS-VAL-PASO IN WSS-VARI TO WSS-VAL-SCON IN WSS-VARI
      *DAD fin  05-MAR-1993 / 05-FEB-1993
           ELSE
           IF MSC-CIC-CMSC IN MSC = '3'
               ADD 1                   TO WSS-NUM-SCVC IN WSS-VARI
               ADD SRT-VAL-SCVC IN SRT TO WSS-VAL-SCVC IN WSS-VARI
      *DAD ini  05-MAR-1993
               ADD SRT-VAL-SMOR IN SRT TO WSS-VAL-SMOR IN WSS-VARI
               SUBTRACT SRT-VAL-SMOR IN SRT
                        SRT-VAL-SCVC IN SRT FROM
                        SRT-VAL-SCON IN SRT GIVING
                        WSS-VAL-PASO IN WSS-VARI
               ADD WSS-VAL-PASO IN WSS-VARI TO WSS-VAL-SCON IN WSS-VARI
      *DAD fin  05-MAR-1993
           ELSE
               DISPLAY 'ESTADO CLIENTE INVALIDO, REG NO PROC:' SRT.
       FIN-ADD-VAL-SRT.
           EXIT.

       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSBBMSC.
       COPY GNSBFMSC.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY SUPIOD02.
       COPY SUPIOP14.

