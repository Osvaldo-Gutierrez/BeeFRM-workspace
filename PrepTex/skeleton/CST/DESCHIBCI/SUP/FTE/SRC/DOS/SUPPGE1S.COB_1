       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPGE1S.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  20/01/95.
00000 *COMPILED SITE 2000         07.08.98
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Sistema Super Bancos                               *
      *  Subsist. :                                                    *
      *  Analista : Donoso J. Osvaldo                                  *
      *  Objetivo : Genera archivo E01 formato Super                   *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPVE01, Archivo E01 formato banco                 *
      *                                                                *
      *   Salida  : SUPVE1S, Archivo E1S formato super                 *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: David Parraguez Muoz.............     *
      *            Fecha      : 02/07/98                               *
      *            Cambio     : Se inserta siglo en encabezado del     *
      *                         archivo que se graba en la cinta para  *
      *                         la super                               *
      *                                                                *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : .....................................  *
      *                         .....................................  *
      *                         .....................................  *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPVE1S ASSIGN       TO SUPVE1S
                  FILE STATUS          IS WR-FS-SUPVE1S.
           SELECT SUPVE01 ASSIGN       TO SUPVE01
                  FILE STATUS          IS WR-FS-SUPVE01.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPVE01                LABEL RECORD STANDARD
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRE01.
           SKIP1
       FD  SUPVE1S                LABEL RECORD STANDARD
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRE1S.
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           02 WA-L-E01                      PIC 9(06) VALUE 0.
           02 WA-G-E1S                      PIC 9(06) VALUE 0.
           02 WA-NUM-DIMP                   PIC 9(06) VALUE 0.
           SKIP1
       01  W-PARAMETROS.
           02 FILLER                        PIC X(20).
           02 WP-ANO-PER                    PIC X(02).
           02 WP-MES-PER                    PIC X(02).
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(15).
              10 WR-PARRAFO                 PIC X(15).
              10 WR-ARCHIVO                 PIC X(10).
              10 WR-OPERACI                 PIC X(10).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-MENSAJE                 PIC X(35).
           05 WR-FS-SUPVE01                 PIC 9(02) VALUE 0.
           05 WR-FS-SUPVE1S                 PIC 9(02) VALUE 0.
           EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           STOP RUN.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-VALIDAR-PARAME.
           PERFORM BB-ABRIR-ARCHIVOS.
           GO BZ-FIN-DE-SECCION.
       BB-VALIDAR-PARAME.
      *------------------
           ACCEPT W-PARAMETROS.
           IF (WP-ANO-PER NOT NUMERIC OR
               WP-MES-PER NOT NUMERIC OR
               WP-ANO-PER = '00'      OR
               WP-MES-PER > '12'      OR
               WP-MES-PER < '01')
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-VALIDAR-PARAME'     TO WR-PARRAFO
               MOVE 'SYSIN      '           TO WR-ARCHIVO
               MOVE '           '           TO WR-OPERACI
               MOVE WR-FS-SUPVE1S           TO WR-FILE-ST
               MOVE 'ERROR EN PARAMETROS'   TO WR-MENSAJE
               PERFORM BC-DISPLAY-ERROR
           END-IF.
           IF WR-FS-SUPVE01 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVE01    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVE01            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT SUPVE1S
                INPUT  SUPVE01.

           IF WR-FS-SUPVE1S NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVE1S    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVE1S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           IF WR-FS-SUPVE01 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVE01    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVE01            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BC-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-ERROR            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROC-PPAL.
      *-------------
           PERFORM CB-GENERA-REG-CABECERA.
           PERFORM CB-LEER-SUPVE01
           PERFORM CC-GENERA-E1S-SUPER
              UNTIL (WR-FS-SUPVE01 NOT = 0 OR WS-PROCESO-ERROR)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-GENERA-REG-CABECERA.
      *-----------------------
           MOVE SPACES                      TO E1S0.
           MOVE '016'                       TO E1S-COD-INS-FIN
           MOVE 'E01'                       TO E1S-IDE-ARC
           MOVE WP-ANO-PER                  TO E1S-ANO-PER
           MOVE WP-MES-PER                  TO E1S-MES-PER
      * DPM ini  2-JUL-1998
           IF E1S-ANO-PER > 50
               MOVE 19 TO E1S-SIG-PER
           ELSE
               MOVE 20 TO E1S-SIG-PER
           END-IF.
      * DPM fin  2-JUL-1998
           PERFORM XX-GRABA-SUPVE1S.
           SKIP1
       CB-LEER-SUPVE01.
      *----------------
           READ SUPVE01 END-READ.
           IF WR-FS-SUPVE01 = 0
              ADD 1                         TO WA-L-E01
           ELSE
           IF WR-FS-SUPVE01  NOT = 10
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'CB-LEER-SUPVE01 '       TO WR-PARRAFO
              MOVE 'SUPVE01     '           TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-SUPVE01            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       CC-GENERA-E1S-SUPER.
      *--------------------
           MOVE E01-IND-TCRE                TO E1S-IND-TCRE
           MOVE E01-COD-LOCA                TO E1S-COD-LOCA
           MOVE 0                           TO E1S-NUM-OPER
           MOVE 0                           TO E1S-VAL-REMA
           MOVE 0                           TO WA-NUM-DIMP
           PERFORM UNTIL
               (E01-IND-TCRE NOT = E1S-IND-TCRE OR
                E01-COD-LOCA NOT = E1S-COD-LOCA OR
                WR-FS-SUPVE01    = 10           OR
                WS-PROCESO-ERROR)
                ADD 1                       TO E1S-NUM-OPER
                ADD E01-VAL-REMA            TO E1S-VAL-REMA
                ADD E01-NUM-DIMP            TO WA-NUM-DIMP
                PERFORM CB-LEER-SUPVE01
           END-PERFORM.

           COMPUTE E1S-NUM-DIMP ROUNDED = WA-NUM-DIMP / E1S-NUM-OPER

           IF (NOT WS-PROCESO-ERROR)
               PERFORM XX-GRABA-SUPVE1S
           END-IF.
           SKIP1
       XX-GRABA-SUPVE1S.
      *-----------------
           WRITE E1S1 END-WRITE.
           IF WR-FS-SUPVE1S = 0
              ADD 1                         TO WA-G-E1S
           ELSE
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'XX-GRABA-SUPVE1S'       TO WR-PARRAFO
              MOVE 'SUPVE1S     '           TO WR-ARCHIVO
              MOVE 'WRITE      '            TO WR-OPERACI
              MOVE WR-FS-SUPVE1S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'ESTE PROCESO NO EMITE INFORME'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPGE1S FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '    GENERA ARCHIVO E01 EN FORMATO SUPER'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'REG LEIDOS DE  SUPVE01 : ' WA-L-E01
           DISPLAY 'REG GRABADOS   SUPVE1S : ' WA-G-E1S
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SUPVE01 SUPVE1S
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT

