       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPP1003.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  29/05/91.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 13 DE MAYO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Super Bancos                                       *
      *  Subsist. :                                                    *
      *  Analista : Nunez Sergio                                       *
      *  Objetivo : Emite informe "INFORMACION DE LOS EJECUTIVOS DE    *
      *             LA SOCIEDAD", para ser enviado a la Superintendecia*
      *             de Valores y Seguros                               *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : DBC 100, Datos basicos de clientes                 *
      *             REC 100, Relacion entre clientes                   *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Salida  : SUPL1003, Informe "Informacion de los ejec..."     *
      *                                                                *
      *   Trabajo : SUPSSOR, Archivo de sort                           *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: Osvaldo Donoso Jara...............     *
      *            Fecha      : 20/08/93                               *
      *            Cambio     : Se cambia picture de campo REC-COD-AREC*
      *                         de tabla REC de pic x(3) a x(5)        *
      *                                                                *
      ******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPL1003 ASSIGN UT-S-SUPL1003.
           SELECT SUPSSOR  ASSIGN TO DA-S-SORTWK1.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPL1003
           LABEL RECORDS OMITTED.
       01  SUPR1003                PIC X(132).

       SD  SUPSSOR.
       01  SUPRSOR.
           02 SRT-CLAVE.
              03 SRT-COD-CAR-EJEC  PIC X(02).
              03 SRT-APE-PAT-EJEC  PIC X(25).
           02 SRT-APE-MAT-EJEC     PIC X(25).
           02 SRT-NOM-BRE-EJEC     PIC X(25).
           02 SRT-CIC-CLI-EJEC     PIC X(12).
           02 SRT-NUM-RUT-EJEC     PIC 9(08).
           02 SRT-VRF-RUT-EJEC     PIC X(01).
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       SKIP1
       01  W-PARAMETROS.
           05 FILLER                 PIC X(28).
           05 WP-FECHA.
              10 WP-DIA              PIC X(02).
              10 FILLER              PIC X(01).
              10 WP-MES              PIC X(02).
              10 FILLER              PIC X(01).
              10 WP-ANO              PIC X(02).
           05 FILLER                 PIC X(44).
       SKIP1
       01  W-INDICES.
           05 WI-I                   PIC 9(03) VALUE 0.
           05 WI-K                   PIC 9(03) VALUE 0.
           05 WI-N                   PIC 9(01) VALUE 0.
       SKIP1
       01  W-SWITCHES.
           05 WS-PROCESO             PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0, 2.
              88 WS-MENSAJE-OP       VALUE 2.
              88 WS-PROCESO-NO-OK    VALUE 1.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-REC               PIC 9(06) VALUE 0.
           05 WA-R-REC               PIC 9(06) VALUE 0.
           05 WA-G-REC               PIC 9(06) VALUE 0.
           05 WA-L-DBC               PIC 9(06) VALUE 0.
           05 WA-R-DBC               PIC 9(06) VALUE 0.
           05 WA-G-DBC               PIC 9(06) VALUE 0.
           05 WA-L-SOR               PIC 9(06) VALUE 0.
           05 WA-R-SOR               PIC 9(06) VALUE 0.
           05 WA-G-SOR               PIC 9(06) VALUE 0.
           05 WA-PAG                 PIC 9(02) VALUE 0.
           05 WA-LIN                 PIC 9(02) VALUE 60.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA           PIC X(02).
              10 WR-HOY-MM           PIC X(02).
              10 WR-HOY-DD           PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION          PIC X(15).
              10 WR-PARRAFO          PIC X(15).
              10 WR-ARCHIVO          PIC X(10).
              10 WR-OPERACI          PIC X(10).
              10 WR-FILE-ST          PIC X(02).
              10 WR-MENSAJE          PIC X(35).
           05 WR-FS-SUPSSOR          PIC 9(02) VALUE 0.
           05 WR-COD-AREC            PIC 9(05).
              88 WR-COD-AREC-OK      VALUE 1, 2, 3, 4, 6, 7, 50.
           05 WR-RES                 PIC 9(03).
           05 WR-CUO                 PIC 9(03).
           05 WR-PARAM-RUTUFECH.
              10 WR-RUTU-FECH.
      ***********15 WR-RUTU-SIG      PIC 99 VALUE  19.
                 15 WR-RUTU-SIG      PIC 99.
                 15 WR-RUTU-ANO      PIC 99.
                 15 WR-RUTU-MES      PIC 99.
                 15 WR-RUTU-DIA      PIC 99.
              10 WR-RUTU-SN          PIC X.
              10 WR-RUTU-HABANT      PIC 9(08) VALUE 0.
              10 WR-RUTU-HABPOS      PIC 9(08) VALUE 0.
              10 WR-RUTU-CODRET      PIC 9     VALUE 0.
           05 WR-LINEA00        PIC X(02)  VALUE ALL '1 '.
           05 WR-LINEA01.
              10 FILLER         PIC X(001) VALUE ALL '2'.
              10 FILLER         PIC X(022) VALUE ALL ' '.
              10 FILLER         PIC X(020) VALUE 'FORMULARIO No. 1 INF'.
              10 FILLER         PIC X(020) VALUE 'ORMACION DE LOS EJEC'.
              10 FILLER         PIC X(020) VALUE 'UTIVOS DE LA SOCIEDA'.
              10 FILLER         PIC X(001) VALUE ALL 'D'.
           05 WR-LINEA02.
              10 FILLER         PIC X(001) VALUE ALL '2'.
              10 FILLER         PIC X(017) VALUE '  I.- ANTECEDENTE'.
              10 FILLER         PIC X(018) VALUE 'S DE LA SOCIEDAD :'.
           05 WR-LINEA02A.
              10 FILLER         PIC X(001) VALUE ALL '1'.
              10 FILLER         PIC X(108) VALUE ALL ' '.
              10 FILLER         PIC X(011) VALUE 'Pgina No.'.
              10 WR-LA02-PAG    PIC Z9.
              10 FILLER         PIC X(004) VALUE ' de'.
              10 WR-LA02-TOT    PIC Z9.
           05 WR-LINEA03.
              10 FILLER         PIC X(001) VALUE ALL '1'.

      **** Y2K-COMIENZO (INTERSOFT)
      *       10 FILLER         PIC X(071) VALUE ALL ' '.
      *       10 FILLER         PIC X(006) VALUE 'DDMMAA'.
              10 FILLER         PIC X(069) VALUE ALL ' '.
              10 FILLER         PIC X(008) VALUE 'DDMMSSAA'.
      **** Y2K-FINAL    (INTERSOFT)

           05 WR-LINEA04.
              10 FILLER         PIC X(001) VALUE ALL '1'.
              10 FILLER         PIC X(011) VALUE ALL ' '.
              10 WR-LA04-BCO    PIC X(034).
              10 FILLER         PIC X(005) VALUE ALL ' '.
              10 WR-LA04-RUT    PIC X(008).
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA04-VRF    PIC X(001).

      **** Y2K-COMIENZO (INTERSOFT)
      *       10 FILLER         PIC X(011) VALUE ALL ' '.
              10 FILLER         PIC X(009) VALUE ALL ' '.
      **** Y2K-FINAL    (INTERSOFT)

              10 WR-LA04-DIA    PIC X(002).
              10 WR-LA04-MES    PIC X(002).

      **** Y2K-COMIENZO (INTERSOFT)
              10 WR-LA04-SIG    PIC X(002).
      **** Y2K-FINAL    (INTERSOFT)

              10 WR-LA04-ANO    PIC X(002).
              10 FILLER         PIC X(027) VALUE ALL ' '.
              10 WR-LA04-TIP    PIC X(002).
      *---------------------------------------------------------------*
      * LINEA04A,LINEA08A,LINEA09A LOS CAMPOS TIENEN VALUE ALL X'B7'  *
      *---------------------------------------------------------------*
           05 WR-LINEA04A.
              10 FILLER         PIC X(001) VALUE ALL '1'.
              10 FILLER         PIC X(011) VALUE ALL ' '.
              10 WR-LA04-BCOA   PIC X(034) VALUE ALL ''.
              10 FILLER         PIC X(005) VALUE ALL ' '.
              10 WR-LA04-RUTA   PIC X(008) VALUE ALL ''.
              10 FILLER         PIC X(001) VALUE ALL ' '.
              10 WR-LA04-VRFA   PIC X(001) VALUE ALL ''.
              10 FILLER         PIC X(011) VALUE ALL ' '.
              10 WR-LA04-FECA   PIC X(006) VALUE ALL ''.
              10 FILLER         PIC X(027) VALUE ALL ' '.
              10 WR-LA04-TIPA   PIC X(002) VALUE ALL ''.
           05 WR-LINEA05.
              10 FILLER         PIC X(001) VALUE ALL '1'.
              10 FILLER         PIC X(020) VALUE '           I.1  Raz'.
              10 FILLER         PIC X(020) VALUE 'n Social de la Socie'.
              10 FILLER         PIC X(020) VALUE 'dad       I.2 Rut So'.
              10 FILLER         PIC X(020) VALUE 'ciedad     I.3 Fecha'.
              10 FILLER         PIC X(018) VALUE ' Elaboracin Info.'.
              10 FILLER         PIC X(020) VALUE '      I.4 Tipo Socie'.
              10 FILLER         PIC X(003) VALUE 'dad'.
           05 WR-LINEA06.
              10 FILLER         PIC X(001) VALUE ALL '2'.
              10 FILLER         PIC X(018) VALUE '  II.- ANTECEDENTE'.
              10 FILLER         PIC X(020) VALUE 'S DE LOS EJECUTIVOS'.
              10 FILLER         PIC X(001) VALUE ALL ':'.
           05 WR-LINEA07.
              10 FILLER         PIC X(020) VALUE '1           II.1  Ap'.
              10 FILLER         PIC X(014) VALUE 'ellido Paterno'.
              10 FILLER         PIC X(020) VALUE '           II.2  Ape'.
              10 FILLER         PIC X(013) VALUE 'llido Materno'.
              10 FILLER         PIC X(020) VALUE '          II.3  Nomb'.
              10 FILLER         PIC X(003) VALUE 'res'.
              10 FILLER         PIC X(020) VALUE ALL ' '.
              10 FILLER         PIC X(020) VALUE 'II.4 Rut   II.5 Carg'.
              10 FILLER         PIC X(001) VALUE ALL 'o'.
           05 WR-LINEA08.
              10 FILLER         PIC X(012) VALUE '1Ejecutivo'.
              10 WR-LA08-APA    PIC X(025).
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA08-AMA    PIC X(025).
              10 FILLER         PIC X(007) VALUE ALL ' '.
              10 WR-LA08-NOM    PIC X(025).
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA08-RUT    PIC X(008).
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA08-VRF    PIC X(001).
              10 FILLER         PIC X(005) VALUE ALL ' '.
              10 WR-LA08-CAR    PIC X(002).
           05 WR-LINEA08A.
              10 FILLER         PIC X(001) VALUE ALL '1'.
              10 FILLER         PIC X(011) VALUE ALL ' '.
              10 WR-LA08-APAA   PIC X(025) VALUE ALL ''.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA08-AMAA   PIC X(025) VALUE ALL ''.
              10 FILLER         PIC X(007) VALUE ALL ' '.
              10 WR-LA08-NOMA   PIC X(025) VALUE ALL ''.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA08-RUTA   PIC X(008) VALUE ALL ''.
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA08-VRFA   PIC X(001) VALUE ALL ''.
              10 FILLER         PIC X(005) VALUE ALL ' '.
              10 WR-LA08-CARA   PIC X(002) VALUE ALL ''.
           05 WR-LINEA09.
              10 FILLER         PIC X(012) VALUE '1Conyuge'.
              10 WR-LA09-APA    PIC X(025).
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA09-AMA    PIC X(025).
              10 FILLER         PIC X(007) VALUE ALL ' '.
              10 WR-LA09-NOM    PIC X(025).
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA09-RUT    PIC X(008).
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA09-VRF    PIC X(001).
           05 WR-LINEA09A.
              10 FILLER         PIC X(001) VALUE ALL '1'.
              10 FILLER         PIC X(011) VALUE ALL ' '.
              10 WR-LA09-APAA   PIC X(025) VALUE ALL ''.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA09-AMAA   PIC X(025) VALUE ALL ''.
              10 FILLER         PIC X(007) VALUE ALL ' '.
              10 WR-LA09-NOMA   PIC X(025) VALUE ALL ''.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA09-RUTA   PIC X(008) VALUE ALL ''.
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA09-VRFA   PIC X(001) VALUE ALL ''.
           05 WR-LINEA10.
              10 FILLER         PIC X(001) VALUE ALL '2'.
              10 FILLER         PIC X(058) VALUE ALL ' '.
              10 FILLER         PIC X(025) VALUE ALL '-'.
           05 WR-LINEA11.
              10 FILLER         PIC X(001) VALUE ALL '2'.
              10 FILLER         PIC X(063) VALUE ALL ' '.
              10 FILLER         PIC X(015) VALUE 'Gerente General'.
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  WB-UIB.
           05 FILLER                 PIC X(32) VALUE 'SUPP1003'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  WB-REQ-REC-AREA.
           02 WB-REQ-REC-CMD          PIC X(5) VALUE 'REDKX'.
           02 FILLER                  PIC X(3) VALUE 'REC'.
           02 WB-REQ-REC-NKY          PIC X(5) VALUE 'REC02'.
           02 WB-REQ-REC-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +100.
           02 FILLER                  PIC X(58).
           02 WB-REQ-REC-KEY-VALUE.
              03 WB-REQ-REC-KEY-1     PIC X(12) VALUE '100000107699'.
              03 WB-REQ-REC-KEY-2     PIC X(02).
              03 WB-REQ-REC-KEY-3     PIC X(12).
              03 WB-REQ-REC-KEY-4     PIC X(02).

       01  WB-REQ-DBC-AREA.
           02 WB-REQ-DBC-CMD          PIC X(5) VALUE 'REDKY'.
           02 FILLER                  PIC X(3) VALUE 'DBC'.
           02 FILLER                  PIC X(5) VALUE 'DBC00'.
           02 WB-REQ-DBC-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +100.
           02 FILLER                  PIC X(58).
           02 WB-REQ-DBC-KEY-VALUE    PIC X(12).

      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
      *    Relaciones entre Clientes
      *    -------------------------
      *
      *    Nombre Registro : REC
      *    Clave(s)        : REC-KEY-IREC(UU), REC-CIC-IREL(DU)
      *    Largo           : 158
      *    Bloqueo         : 1
      *    Observaciones   :
      *
      *    01) Modificacion  21/07/93,  DAD   (Req. BCI 20/07/93)
      *        - Aumentado largo campo REC-COD-AREC de X(03) a X(05).
      *        - Disminuido largo campo Glosa Disponible de X(31) a X(29
      *
       01  REC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  REC-GLS-FLAG                             PIC X(01).
      *
      *KEY Clave Primaria Registro
           03  REC-KEY-IREG.
      *
      *        Identificacion Relacion entre Clientes
               05  REC-KEY-IREC.
      *
      *KEY         Cod. Int. Comp. Cliente
                   07  REC-CIC-ICLI.
      *
      *                Centro Asignacion Identificacion
                       09  REC-CAI-ICLI.
                           11  REC-COD-TCLI             PIC X(01).
                           11  REC-COD-CCLI             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  REC-IIC-ICLI                 PIC X(08).

      *KEY         Codigo Tipo Relacion entre Clientes  SGC REC
                   07  REC-COD-TREC                     PIC X(02).
      *
      *KEY         Cod. Int. Comp. Cliente Relacionado
                   07  REC-CIC-IREL.
      *
      *                Centro Asignacion Identificacion
                       09  REC-CAI-IREL.
                           11  REC-COD-TREL             PIC X(01).
                           11  REC-COD-CREL             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  REC-IIC-IREL                 PIC X(08).
      *
      *KEY         Num Corr segun Tipo Relacion
                   07  REC-NUM-TREC                     PIC 9(02).
      *
      *        Time Stamp Transaccion
               05  REC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  REC-FEC-FTRN.
                       09  REC-NUM-STRN                 PIC 9(02).
                       09  REC-NUM-ATRN                 PIC 9(02).
                       09  REC-NUM-MTRN                 PIC 9(02).
                       09  REC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  REC-HRA-HRTR.
                       09  REC-NUM-HHTR                 PIC 9(02).
                       09  REC-NUM-MMTR                 PIC 9(02).
                       09  REC-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  REC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  REC-MSC-STAT                             PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  REC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  REC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  REC-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  REC-COD-ATRN                             PIC X(12).
      *
      *    Indicador de Vigencia Relacion (S/N)         TAB VIGE
           03  REC-IND-VIGE                             PIC X(01).
      *
      *OPR Fecha Inicio de la Relacion
           03  REC-FEC-FINI.
               05  REC-NUM-SINI                         PIC 9(02).
               05  REC-NUM-AINI                         PIC 9(02).
               05  REC-NUM-MINI                         PIC 9(02).
               05  REC-NUM-DINI                         PIC 9(02).
      *
      *OPR Fecha Inicio Computacional
           03  REC-FEC-FINP.
               05  REC-NUM-SINP                         PIC 9(02).
               05  REC-NUM-AINP                         PIC 9(02).
               05  REC-NUM-MINP                         PIC 9(02).
               05  REC-NUM-DINP                         PIC 9(02).
      *
      *OPR Fecha de Termino de la Relacion
           03  REC-FEC-FTER.
               05  REC-NUM-STER                         PIC 9(02).
               05  REC-NUM-ATER                         PIC 9(02).
               05  REC-NUM-MTER                         PIC 9(02).
               05  REC-NUM-DTER                         PIC 9(02).
      *
      *OPR Fecha de Termino Computacional
           03  REC-FEC-FOUT.
               05  REC-NUM-SOUT                         PIC 9(02).
               05  REC-NUM-AOUT                         PIC 9(02).
               05  REC-NUM-MOUT                         PIC 9(02).
               05  REC-NUM-DOUT                         PIC 9(02).
      *
      *MOD-01  X(03).
      *RCM Codigo Asociado a la Relacion                SGC REC
           03  REC-COD-AREC                             PIC X(05).
      *
      *    Valor Asociado a la Relacion
           03  REC-SGV-AREC        COMP-3               PIC S9(11)V9(4).
      *
      *OPR Codigo de Valor Asociado a la Relacion       TAB VLR
           03  REC-COD-VCAM.
      *
      *        Miscelaneo Tipo Valor                    TAB TVAL
               05  REC-MSC-TVAL                         PIC X(01).
      *
      *        Codigo Valor
               05  REC-COD-CVAL                         PIC X(03).
      *
      *        Codigo Tipo Cambio
               05  REC-COD-TCAM                         PIC X(02).
      *
      *INF Fecha Referencia Valor Asociado a Relacion
           03  REC-FEC-FVAL.
               05  REC-NUM-SVAL                         PIC 9(02).
               05  REC-NUM-AVAL                         PIC 9(02).
               05  REC-NUM-MVAL                         PIC 9(02).
               05  REC-NUM-DVAL                         PIC 9(02).
      *
      *    Fecha de Verificacion
           03  REC-FEC-FVRF.
               05  REC-NUM-SVRF                         PIC 9(02).
               05  REC-NUM-AVRF                         PIC 9(02).
               05  REC-NUM-MVRF                         PIC 9(02).
               05  REC-NUM-DVRF                         PIC 9(02).
      *
      *MOD-01  X(31).
           03  REC-GLS-DISP                             PIC X(29).
      *
       01  REC-VARI.
           03  REC-COD-CTCA.
               05  REC-COD-TREC                         PIC X(02).
      *MOD-01  X(03).
               05  REC-COD-AREC                         PIC X(05).
           03  REC-COD-DCOM             VALUE 'DC'      PIC X(02).
           03  REC-COD-RBCO             VALUE 'RB'      PIC X(02).
           03  REC-COD-FUNC             VALUE 'FU'      PIC X(02).
           03  REC-COD-CONY             VALUE 'CY'      PIC X(02).
           03  REC-COD-CMAT             VALUE 'CM'      PIC X(02).
           03  REC-COD-REPR             VALUE 'RP'      PIC X(02).
           03  REC-COD-MCLI             VALUE 'MC'      PIC X(02).
           03  REC-COD-RLEG             VALUE 'RL'      PIC X(02).
           03  REC-COD-PRES             VALUE 'PP'      PIC X(02).
           03  REC-COD-APOD             VALUE 'AP'      PIC X(02).
           03  REC-COD-EMPL             VALUE 'EM'      PIC X(02).
           03  REC-COD-COMD             VALUE 'CD'      PIC X(02).
           03  REC-COD-PERA             VALUE 'PA'      PIC X(02).
           03  REC-COD-SOCD             VALUE 'SD'      PIC X(02).
           03  REC-COD-OPOD             VALUE 'OP'      PIC X(02).
      *    Datos Basicos del Cliente
      *    -------------------------
      *
      *    Nombre Registro : DBC
      *    Clave(s)        : DBC-KEY-IDBC(UU), DBC-STP-ITRN(UU),DBC-IDC-
      *                      DBC-GLS-NOMC(DC), DBC-SNX-NOMC(DC)
      *    Largo           : 241
      *    Bloqueo         : 1
      *    Observaciones   :
      *
       01  DBC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  DBC-GLS-FLAG                             PIC X(01).
      *
      *KEY Clave Primaria Registro
           03  DBC-KEY-IREG.
      *
      *        Identificacion Datos Basicos Clientes
               05  DBC-KEY-IDBC.
      *
      *            Cod. Int. Comp. Cliente
                   07  DBC-CIC-ICLI.
      *
      *                Centro Asignacion Identificacion
                       09  DBC-CAI-ICLI.
                           11  DBC-COD-TCLI             PIC X(01).
                           11  DBC-COD-CCLI             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  DBC-IIC-ICLI                 PIC X(08).
      *
      *        Time Stamp Transaccion
               05  DBC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  DBC-FEC-FTRN.
                       09  DBC-NUM-STRN                 PIC 9(02).
                       09  DBC-NUM-ATRN                 PIC 9(02).
                       09  DBC-NUM-MTRN                 PIC 9(02).
                       09  DBC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  DBC-HRA-HRTR.
                       09  DBC-NUM-HHTR                 PIC 9(02).
                       09  DBC-NUM-MMTR                 PIC 9(02).
                       09  DBC-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  DBC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  DBC-MSC-STAT                             PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  DBC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  DBC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  DBC-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  DBC-COD-ATRN                             PIC X(12).
      *
      *KEY Rut Cliente + Indicador o Calificador + Extension
           03  DBC-IDC-ICLI.
      *
      *        RUT Cliente
               05  DBC-NUM-ICLI                         PIC X(08).
      *
      *        Indicador Tipo RUT
               05  DBC-IND-ICLI                         PIC X(01).
      *
      *        Extension del RUT
               05  DBC-GLS-ICLI                         PIC X(03).
      *
      *KEY Digito Verificador del Rut
           03  DBC-VRF-ICLI                             PIC X(01).
      *
      *KEY Nombre Cliente Generico (Persona o Empresa)
           03  DBC-GLS-NOMC.
               05  DBC-GLS-APAT                         PIC X(25).
               05  DBC-GLS-AMAT                         PIC X(25).
               05  DBC-GLS-NOMB                         PIC X(25).
      *
      *    Soundex del Nombre de Cliente
           03  DBC-SNX-NOMC.
               05  DBC-SNX-APAT                         PIC X(03).
               05  DBC-SNX-AMAT                         PIC X(03).
               05  DBC-SNX-NOMB                         PIC X(03).
      *
      *    Numero de Direccion de Envio Correspondencia
           03  DBC-NUM-DCOR                             PIC 9(02).
      *
      *OPR Ind Tipo Cliente ( Persona o Empresa )       SGC TICL
           03  DBC-IND-TICL                             PIC X(01).
      *
      *    Tipo Cliente (Potencial,No Cliente,Grupo)    SGC TCL
           03  DBC-COD-TICL                             PIC X(02).
      *
      *    Codigo Clasificacion Comercial               SGC CLC
           03  DBC-COD-CLCM                             PIC X(02).
      *
      *RCM Codigo del Ejecutivo Encargado del Cliente   TAB USR
           03  DBC-COD-EJEC                             PIC X(12).
      *
      *OPR Codigo de Oficina donde opera el Cliente     TAB OFI
           03  DBC-COD-OFIC                             PIC X(03).
      *
      *RCM Indicador si opera en otra Oficina           SGC OOFI
           03  DBC-IND-OOFI                             PIC X(01).
      *
      *EXT Codigo Actividad Economica                   TAB ACT
           03  DBC-COD-ACTI                             PIC X(09).
      *
      *EXT Origen y Destino Sect (Comp.Inst)            TAB ODS
           03  DBC-COD-ODSE                             PIC X(03).
      *
      *INF Fecha Ultima Declaracion Articulo 85
           03  DBC-FEC-FA85.
               05  DBC-NUM-SA85                         PIC 9(02).
               05  DBC-NUM-AA85                         PIC 9(02).
               05  DBC-NUM-MA85                         PIC 9(02).
               05  DBC-NUM-DA85                         PIC 9(02).
      *
      *    Tipo Ultimo Pago Impuesto a la Renta         SGC UPIR
           03  DBC-IND-UPIR                             PIC X(03).
      *
      *    Fecha Ultimo Pago Impuesto a la Renta
           03  DBC-FEC-FUPI.
               05  DBC-NUM-SUPI                         PIC 9(02).
               05  DBC-NUM-AUPI                         PIC 9(02).
               05  DBC-NUM-MUPI                         PIC 9(02).
               05  DBC-NUM-DUPI                         PIC 9(02).
      *
      *INF Indicador de Multas Banco Central            SGC MUBC
           03  DBC-IND-MUBC                             PIC X(01).
      *
      *INF Indicador de Juicios Banco Central           SGC JUBC
           03  DBC-IND-JUBC                             PIC X(01).
      *
      *EXT Codigo Estado Cliente ( como Deudor )        SGC SCLI
           03  DBC-MSC-SCLI                             PIC X(01).
      *
      *    Fecha Estado Cliente
           03  DBC-FEC-FSCL.
               05  DBC-NUM-SSCL                         PIC 9(02).
               05  DBC-NUM-ASCL                         PIC 9(02).
               05  DBC-NUM-MSCL                         PIC 9(02).
               05  DBC-NUM-DSCL                         PIC 9(02).
      *
      *    Codigo de Bloqueo del Cliente                SGC BLO
           03  DBC-COD-BLOC                             PIC X(02).
      *
      *    Numero Total de Direcciones
           03  DBC-NUM-TDIR                             PIC 9(02).
      *
      *    Numero Total de Comentarios
           03  DBC-NUM-TCMT                             PIC 9(03).
      *
      *    Fecha Inicio Operacion
           03  DBC-FEC-FIOP.
               05  DBC-NUM-SIOP                         PIC 9(02).
               05  DBC-NUM-AIOP                         PIC 9(02).
               05  DBC-NUM-MIOP                         PIC 9(02).
               05  DBC-NUM-DIOP                         PIC 9(02).
      *
      *    Fecha Termino Operacion
           03  DBC-FEC-FTOP.
               05  DBC-NUM-STOP                         PIC 9(02).
               05  DBC-NUM-ATOP                         PIC 9(02).
               05  DBC-NUM-MTOP                         PIC 9(02).
               05  DBC-NUM-DTOP                         PIC 9(02).
      *
      *    Numero Operaciones Fecha Termino Nula
           03  DBC-NUM-OFTN                             PIC 9(03).
      *
      *    Fecha Ingreso de Datos a la Base de Datos
           03  DBC-FEC-FINP.
               05  DBC-NUM-SINP                         PIC 9(02).
               05  DBC-NUM-AINP                         PIC 9(02).
               05  DBC-NUM-MINP                         PIC 9(02).
               05  DBC-NUM-DINP                         PIC 9(02).
      *
      *    Disponible
      *    03  DBC-GLS-DISP                             PIC X(00).
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  WB-ELEM-LIS        PIC X(11).
      *
           COPY RLOVR001.
           EJECT
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           EJECT
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              SORT SUPSSOR ON ASCENDING KEY SRT-CLAVE
              INPUT  PROCEDURE IS C-PROCESAR
              OUTPUT PROCEDURE IS D-LISTAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM E-TERMINAR.
           SKIP1
       AZ-FIN-DE-PROCESO.
           GOBACK.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT SUPL1003.
           SKIP1
       BB-VALIDA-PARAMETRO.
      *--------------------
           ACCEPT W-PARAMETROS.
           IF (WP-DIA NOT NUMERIC OR WP-MES NOT NUMERIC OR
               WP-ANO NOT NUMERIC)
               MOVE 1 TO WS-PROCESO
           ELSE
      ********IF (WP-ANO > 90)
              IF (WP-ANO < ROLL-YY)
                  MOVE 20 TO WR-RUTU-SIG
              ELSE
                  MOVE 19 TO WR-RUTU-SIG
              END-IF
              MOVE WP-ANO TO WR-RUTU-ANO
              MOVE WP-MES TO WR-RUTU-MES
              MOVE WP-DIA TO WR-RUTU-DIA
              MOVE ZEROS TO WR-RUTU-HABANT WR-RUTU-HABPOS WR-RUTU-CODRET
              MOVE ' '    TO WR-RUTU-SN
              CALL 'RUTUFECH' USING  WR-RUTU-FECH   WR-RUTU-SN
                                     WR-RUTU-HABANT WR-RUTU-HABPOS
                                     WR-RUTU-CODRET
              IF WR-RUTU-CODRET NOT = 0
                 MOVE 1 TO WS-PROCESO
              END-IF
           END-IF.
           IF (WS-PROCESO = 1)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BB-VALIDA-PARAMET' TO WR-PARRAFO
               MOVE 'SYSIN      '       TO WR-ARCHIVO
               MOVE '**FECHA INVALIDA*' TO WR-MENSAJE
               MOVE WR-RUTU-CODRET      TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO BZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       BC-INI-LINEA-LIST.
      *------------------
           MOVE 'BANCO DE CREDITO E INVERSIONES'   TO WR-LA04-BCO
           MOVE '97006000'                         TO WR-LA04-RUT
           MOVE '6'                                TO WR-LA04-VRF
           MOVE  WP-DIA                            TO WR-LA04-DIA
           MOVE  WP-MES                            TO WR-LA04-MES
           MOVE  WP-ANO                            TO WR-LA04-ANO

      **** Y2K-COMIENZO (INTERSOFT)
           IF WP-ANO NOT < '60'
              MOVE '19' TO WR-LA04-SIG
           ELSE
              MOVE '20' TO WR-LA04-SIG
           END-IF.
      **** Y2K-FINAL    (INTERSOFT)

           MOVE  '01'                              TO WR-LA04-TIP.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GRAL.
      *----------------
           PERFORM CB-LEER-REC
           MOVE 'REDNE'        TO WB-REQ-REC-CMD
           PERFORM CC-SELECCIONA-REGS
              UNTIL (WB-REQ-REC-RC   NOT = '  '           OR
                     WS-PROCESO-NO-OK)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-REC.
      *------------
           MOVE SPACES TO WB-REQ-REC-RC
           MOVE 'REC00' TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-REC-AREA REC WB-ELEM-LIS
           IF (WB-REQ-REC-RC NOT = '  ' AND NOT = '14')
               MOVE 'C-PROCESAR'        TO WR-SECCION
               MOVE 'CB-LEER-REC'       TO WR-PARRAFO
               MOVE 'REC'               TO WR-ARCHIVO
               MOVE WB-REQ-REC-CMD      TO WR-OPERACI
               MOVE WB-REQ-REC-RC       TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO CZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       CC-SELECCIONA-REGS.
      *-------------------
           ADD 1 TO WA-L-REC
           MOVE REC-COD-AREC IN REC TO WR-COD-AREC.
           IF (REC-COD-TREC IN REC = 'PR' AND REC-IND-VIGE IN REC = 'S'
               AND WR-COD-AREC-OK)
               MOVE REC-CIC-ICLI TO WB-REQ-DBC-KEY-VALUE
               PERFORM DC-LEER-DBC
               IF (WB-REQ-DBC-RC = '  ')
                   EVALUATE WR-COD-AREC
                      WHEN '00001' MOVE '01' TO SRT-COD-CAR-EJEC
                      WHEN '00002' MOVE '01' TO SRT-COD-CAR-EJEC
                      WHEN '00003' MOVE '01' TO SRT-COD-CAR-EJEC
                      WHEN '00004' MOVE '01' TO SRT-COD-CAR-EJEC
                      WHEN '00006' MOVE '02' TO SRT-COD-CAR-EJEC
                      WHEN '00007' MOVE '02' TO SRT-COD-CAR-EJEC
                      WHEN '00050' MOVE '03' TO SRT-COD-CAR-EJEC
                   END-EVALUATE
                   MOVE REC-CIC-ICLI TO SRT-CIC-CLI-EJEC
                   MOVE DBC-NUM-ICLI TO SRT-NUM-RUT-EJEC
                   MOVE DBC-VRF-ICLI TO SRT-VRF-RUT-EJEC
                   MOVE DBC-GLS-APAT TO SRT-APE-PAT-EJEC
                   MOVE DBC-GLS-AMAT TO SRT-APE-MAT-EJEC
                   MOVE DBC-GLS-NOMB TO SRT-NOM-BRE-EJEC
                   RELEASE SUPRSOR
                   ADD 1 TO WA-G-SOR
               ELSE
                   MOVE 2 TO WS-PROCESO
                   DISPLAY '** NO SE ENCUENTRA EN DBC ==> ' REC-CIC-ICLI
                   ADD 1 TO WA-R-REC
               END-IF
           END-IF.
           PERFORM CB-LEER-REC.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-LISTAR SECTION.
      *=================
       DA-SALIDA-SORT.
      *---------------
           IF (WS-PROCESO-NO-OK)
               GO DZ-FIN-DE-SECCION
           END-IF.
           RETURN SUPSSOR AT END MOVE 1 TO WR-FS-SUPSSOR END-RETURN
           PERFORM DB-PROCESO-SORT
             UNTIL (WR-FS-SUPSSOR NOT = 0 OR WS-PROCESO-NO-OK).
           IF (WA-LIN < 13)
               COMPUTE WA-LIN = (13 - WA-LIN) * 5 + 9
               WRITE SUPR1003 FROM WR-LINEA10 AFTER WA-LIN LINES
               WRITE SUPR1003 FROM WR-LINEA11 AFTER 1      LINES
           END-IF.
           GO DZ-FIN-DE-SECCION.
           SKIP1
       DB-PROCESO-SORT.
      *----------------
           PERFORM DB-BUSCA-CONYUGE.
           IF (WB-REQ-REC-RC NOT = '  ')
               MOVE SPACES TO DBC-GLS-NOMC
               MOVE SPACES TO DBC-IDC-ICLI DBC-VRF-ICLI
           ELSE
               MOVE REC-CIC-IREL TO WB-REQ-DBC-KEY-VALUE
               PERFORM DC-LEER-DBC
               IF (WB-REQ-DBC-RC NOT = '  ')
                   MOVE SPACES TO DBC-GLS-NOMC
                   MOVE SPACES TO DBC-IDC-ICLI DBC-VRF-ICLI
               END-IF
           END-IF.
           PERFORM DD-IMPRIME
           RETURN SUPSSOR AT END MOVE 1 TO WR-FS-SUPSSOR END-RETURN.
           SKIP1
       DB-BUSCA-CONYUGE.
      *----------------
           MOVE 'REC00'          TO WB-REQ-REC-NKY
           MOVE SRT-CIC-CLI-EJEC TO WB-REQ-REC-KEY-1
           MOVE 'CY'             TO WB-REQ-REC-KEY-2
           MOVE SPACES           TO WB-REQ-REC-KEY-3 WB-REQ-REC-KEY-4
           MOVE 'REDKG'          TO WB-REQ-REC-CMD
           PERFORM CB-LEER-REC.
           MOVE 'REDNX'          TO WB-REQ-REC-CMD
           PERFORM CB-LEER-REC
             UNTIL REC-IND-VIGE            = 'S'          OR
                   SRT-CIC-CLI-EJEC    NOT = REC-CIC-ICLI OR
                   REC-COD-TREC IN REC NOT = 'CY'         OR
                   WB-REQ-REC-RC       NOT = '  '.

           IF (REC-IND-VIGE        NOT = 'S'          OR
               SRT-CIC-CLI-EJEC    NOT = REC-CIC-ICLI OR
               REC-COD-TREC IN REC NOT = 'CY')
               MOVE '14' TO WB-REQ-REC-RC
           END-IF.
           SKIP1
       DC-LEER-DBC.
      *------------
           MOVE SPACES  TO WB-REQ-DBC-RC
           MOVE 'DBC00' TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-DBC-AREA DBC WB-ELEM-LIS
           IF (WB-REQ-DBC-RC = '  ')
               ADD 1 TO WA-L-DBC
           ELSE
           IF (WB-REQ-DBC-RC = '14')
               MOVE SPACES TO DBC-GLS-NOMC
           ELSE
               MOVE 'D-LISTAR'          TO WR-SECCION
               MOVE 'DC-LEER-DBC'       TO WR-PARRAFO
               MOVE 'DBC'               TO WR-ARCHIVO
               MOVE WB-REQ-DBC-CMD      TO WR-OPERACI
               MOVE WB-REQ-DBC-RC       TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO DZ-FIN-DE-SECCION
           END-IF
           END-IF.
           SKIP1
       DD-IMPRIME.
      *-----------
           IF (WA-LIN NOT < 13)
               DIVIDE WA-G-SOR BY 13 GIVING WR-CUO REMAINDER WR-RES
               IF (WR-RES > 0)
                   ADD 1 TO WR-CUO
               END-IF
               ADD  1      TO WA-PAG
               MOVE 0      TO WA-LIN
               MOVE WA-PAG TO WR-LA02-PAG
               MOVE WR-CUO TO WR-LA02-TOT
               WRITE SUPR1003 FROM WR-LINEA00  AFTER PAGE
               WRITE SUPR1003 FROM WR-LINEA01  AFTER 3 LINES
               WRITE SUPR1003 FROM WR-LINEA02A AFTER 2 LINES
               WRITE SUPR1003 FROM WR-LINEA02  AFTER 5 LINES
               WRITE SUPR1003 FROM WR-LINEA03  AFTER 1 LINES
               WRITE SUPR1003 FROM WR-LINEA04  AFTER 1 LINES
               WRITE SUPR1003 FROM WR-LINEA04A AFTER 0 LINES
               WRITE SUPR1003 FROM WR-LINEA05  AFTER 2 LINES
               WRITE SUPR1003 FROM WR-LINEA06  AFTER 5 LINES
               WRITE SUPR1003 FROM WR-LINEA07  AFTER 2 LINES
           END-IF.
           MOVE SRT-APE-PAT-EJEC TO WR-LA08-APA
           MOVE SRT-APE-MAT-EJEC TO WR-LA08-AMA
           MOVE SRT-NOM-BRE-EJEC TO WR-LA08-NOM
           MOVE SRT-NUM-RUT-EJEC TO WR-LA08-RUT
           MOVE SRT-VRF-RUT-EJEC TO WR-LA08-VRF
           MOVE SRT-COD-CAR-EJEC TO WR-LA08-CAR
           WRITE SUPR1003 FROM WR-LINEA08  AFTER 3 LINES
           WRITE SUPR1003 FROM WR-LINEA08A AFTER 0 LINES
           MOVE DBC-GLS-APAT     TO WR-LA09-APA
           MOVE DBC-GLS-AMAT     TO WR-LA09-AMA
           MOVE DBC-GLS-NOMB     TO WR-LA09-NOM
           MOVE DBC-NUM-ICLI     TO WR-LA09-RUT
           MOVE DBC-VRF-ICLI     TO WR-LA09-VRF
           WRITE SUPR1003 FROM WR-LINEA09  AFTER 2 LINES.
           WRITE SUPR1003 FROM WR-LINEA09A AFTER 0 LINES.
           ADD 1 TO WA-LIN.
           IF (WA-LIN NOT < 13)
               WRITE SUPR1003 FROM WR-LINEA10 AFTER 9 LINES
               WRITE SUPR1003 FROM WR-LINEA11 AFTER 1 LINES
           END-IF.
           SKIP1
       DE-COMPACTA.
      *------------
           SKIP1
       DF-GRABA-REGISTRO.
      *------------------
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       E-TERMINAR SECTION.
      *===================
       EA-ESTADISTICAS.
      *----------------
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           IF WS-PROCESO-NO-OK
               DISPLAY 'SUPL1003, NO EMITIDO O IMCOMPLETO '
               DISPLAY ' '
           ELSE
               DISPLAY 'SUPL1003, EMIMIDO PARA SGTES. OFICINAS : '
               DISPLAY ' '
               DISPLAY '090 L'
           END-IF.
           DISPLAY ' '
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPP1003 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY ' CARGA EN ARCHIVO I02 PERSONAS/GRUPO ECONOMICO'
           DISPLAY ' '
           DISPLAY 'ARCHIVO  LEIDOS  RECHAZADOS   GRABADOS'
           DISPLAY ' '
           DISPLAY 'REC    : ' WA-L-REC '     ' WA-R-REC '    ' WA-G-REC
           DISPLAY 'DBC    : ' WA-L-DBC '     ' WA-R-DBC '    ' WA-G-DBC
           DISPLAY 'SORT   : ' WA-L-SOR '     ' WA-R-SOR '    ' WA-G-SOR
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY W-PARAMETROS
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-NO-OK OR WS-MENSAJE-OP
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       EC-TERMINO.
      *-----------
           CLOSE SUPL1003.
           IF WS-PROCESO-NO-OK
              MOVE 100 TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY ' '
              DISPLAY 'EL ERROR FUE EN LA SECCION          ' WR-SECCION
              DISPLAY '                   PARRAFO          ' WR-PARRAFO
              DISPLAY '                   ARCHIVO          ' WR-ARCHIVO
              DISPLAY '                   OPERACION        ' WR-OPERACI
              DISPLAY '                   FILE STATUS      ' WR-FILE-ST
              DISPLAY '                   MENSAJE DE ERROR ' WR-MENSAJE
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       EZ-FIN-DE-SECCION.
      *------------------
           EXIT.

