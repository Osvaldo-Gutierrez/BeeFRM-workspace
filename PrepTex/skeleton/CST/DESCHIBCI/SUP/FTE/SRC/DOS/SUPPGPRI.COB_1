       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPGPRI.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  08/11/93.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
000000*COMPILED SITE 2000          24.09.98
      ******************************************************************
      *  Sistema  : Super Bancos                                       *
      *  Subsist. :                                                    *
      *  Analista : Donoso Osvaldo                                     *
      *  Objetivo : Generar informacion para auditoria al 31/10/93     *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPMD00: Archivo D00 en formato Super              *
      *             CREMMAE: Archivo maestro de creditos               *
      *             HIPMMAE: Archivo de cuotas impagas y pagadas de hip*
      *             DBC-100: Tabla datos basicos de clientes           *
      *             RCD-100: Tabla de direcciones                      *
      *             TAB-150: Tabla de comunas                          *
      *             Sysin  : Parametros                                *
      *                                                                *
      *   Salida  : SUPMAUD: Archivo D00 en formato Auditoria          *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: ....................................   *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : ....................................   *
      *                         ....................................   *
      *                         ....................................   *
      *                                                                *
      ******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *======================
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
           EJECT
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPMD00 ASSIGN TO SUPMD00
                  ORGANIZATION IS INDEXED
                  ACCESS MODE  IS SEQUENTIAL
                  RECORD KEY   IS D00-CLAVE
                  FILE STATUS  IS WR-FS-SUPMD00.
           SELECT SUPMAUD ASSIGN TO SYS010-DA-3340-S-SUPMAUD
                  FILE STATUS  IS WR-FS-SUPMAUD.
           SELECT CREMMAE ASSIGN TO CREMMAE
                  ORGANIZATION IS INDEXED
                  ACCESS MODE  IS RANDOM
                  RECORD KEY   IS CRE-CLAVE
                  FILE STATUS  IS WR-FS-CREMMAE.
           SELECT HIPMMAE ASSIGN TO HIPMMAE
                  ORGANIZATION IS INDEXED
                  ACCESS MODE  IS RANDOM
                  RECORD KEY   IS HIP-CLAVE
                  FILE STATUS  IS WR-FS-HIPMMAE.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       SKIP1
       FD  SUPMAUD
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS.
       01  SUPRAUD.
           02 AUD-TIP-SIS             PIC  9(01).
           02 AUD-TIP-OPE             PIC  X(03).
           02 AUD-IDE-OPE             PIC  X(21).
           02 AUD-NUM-RUT             PIC  X(10).
           02 AUD-NOM-CLI             PIC  X(40).
           02 AUD-DIR-CLI             PIC  X(40).
           02 AUD-COM-CLI             PIC  X(15).
           02 AUD-CAP-INI             PIC S9(13).
           02 AUD-SLD-FME             PIC S9(13).
           02 AUD-CUO-PAG             PIC  9(03).
           02 AUD-CUO-TOT             PIC  9(03).
           02 AUD-NUM-DIA             PIC  9(03).
           02 AUD-VAL-INT             PIC S9(13).
           02 AUD-VAL-REA             PIC S9(13).
       SKIP1
       FD  SUPMD00
           LABEL RECORD STANDARD.
           COPY CYDV000C.
       SKIP1
       FD  CREMMAE
           LABEL RECORD STANDARD.
           COPY CREM002.
       SKIP1
       FD  HIPMMAE
           LABEL RECORD STANDARD.
       01  HIPRMAE.
           02 HIP-CLAVE                 PIC X(21).
           02 HIP-CUO-PAG               PIC 9(03).
           02 HIP-CUO-TOT               PIC 9(03).
       SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
           SKIP1
       01  W-PARAMETROS.
           05 FILLER                        PIC X(20).
           05 WP-FECHA-PRO.
              10 WP-DIA                     PIC X(02).
              10 FILLER                     PIC X(01).
              10 WP-MES                     PIC X(02).
              10 FILLER                     PIC X(01).
              10 WP-ANO                     PIC X(04).
           05 FILLER                        PIC X(70).
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-D00                      PIC 9(06) VALUE 0.
           05 WA-S-D00                      PIC 9(06) VALUE 0.
           05 WA-G-D00                      PIC 9(06) VALUE 0.
           05 WA-L-CRE                      PIC 9(06) VALUE 0.
           05 WA-L-HIP                      PIC 9(06) VALUE 0.
           SKIP1
       01  W-INDICES.
           05 WI-I                   PIC 9(03) VALUE 0.
           05 WI-J                   PIC 9(03) VALUE 0.
           05 WI-K                   PIC 9(03) VALUE 0.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(15).
              10 WR-PARRAFO                 PIC X(15).
              10 WR-ARCHIVO                 PIC X(10).
              10 WR-OPERACI                 PIC X(10).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-MENSAJE                 PIC X(35).
           05 WR-FS-SUPMD00                 PIC 9(02)  VALUE 0.
           05 WR-FS-SUPMAUD                 PIC 9(02)  VALUE 0.
           05 WR-FS-CREMMAE                 PIC 9(02)  VALUE 0.
           05 WR-FS-HIPMMAE                 PIC 9(02)  VALUE 0.
           05 WR-RUT-DD.
              10 FILLER                     PIC X(01).
              10 WR-NUM-RUT                 PIC X(08).
              10 FILLER                     PIC X(01).
           05 WR-CAR-AUX                    PIC X(01).
           SKIP1
       01  W-TABLAS.
           05 WT-NOMBRE.
              10 WT-NOM-UNO                 PIC X(25).
              10 WT-NOM-DOS                 PIC X(25).
              10 WT-NOM-TRES                PIC X(25).
           05 WT-N REDEFINES WT-NOMBRE OCCURS 75 PIC X(01).
           05 WT-COMUNAS OCCURS 1000 TIMES INDEXED BY WI-IND.
              10 WT-COD-COMU                PIC X(3).
              10 WT-GLS-COMU                PIC X(15).
      ******************************************************************
      *  AREAS RUTINA 'RUTUCALE'                                       *
      ******************************************************************
           COPY RTNWCALE.
      ******************************************************************
      * Areas de uso para Datacom                                      *
      ******************************************************************
      *
       01  WB-UIB.
           05 FILLER                 PIC X(32) VALUE 'SUPPGPRI '.
      *
       01  WB-REQ-DBC-AREA.
           02 WB-REQ-DBC-CMD          PIC X(5) VALUE 'REDKG'.
           02 FILLER                  PIC X(3) VALUE 'DBC'.
           02 FILLER                  PIC X(5) VALUE 'DBC02'.
           02 WB-REQ-DBC-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +100.
           02 FILLER                  PIC X(58).
           02 WB-REQ-DBC-KEY-VALUE    PIC X(30) VALUE SPACES.
      *
       01  WB-REQ-RCD-AREA.
           02 WB-REQ-RCD-CMD          PIC X(5) VALUE 'REDKY'.
           02 FILLER                  PIC X(3) VALUE 'RCD'.
           02 FILLER                  PIC X(5) VALUE 'RCD00'.
           02 WB-REQ-RCD-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +100.
           02 FILLER                  PIC X(58).
           02 WB-REQ-RCD-KEY-VALUE.
              03 WB-REQ-RCD-KEY-CIC   PIC X(12).
              03 WB-REQ-RCD-KEY-DIR   PIC X(20).
      *
       01  WB-REQ-TAB-AREA.
           02 WB-REQ-TAB-CMD          PIC X(5) VALUE 'REDKY'.
           02 FILLER                  PIC X(3) VALUE 'TAB'.
           02 FILLER                  PIC X(5) VALUE 'TAB00'.
           02 WB-REQ-TAB-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +150.
           02 FILLER                  PIC X(58).
           02 WB-REQ-TAB-KEY-VALUE.
              03 WB-REQ-TAB-KEY-TAB   PIC X(03) VALUE 'COM'.
              03 WB-REQ-TAB-KEY-COM   PIC X(12).
      *
       01  WB-ELEM-LIS                PIC X(11).
      *
           COPY SGCBRDBC.
      *
           COPY SGCBRRCD.
      *
           COPY TABBRCOM.
      *
           COPY RLOVR001.
      *
           EJECT
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           EJECT
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF (WS-PROCESO-OK)
               PERFORM E-SELECCION
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           GOBACK.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROCESO-INICIAL.
      *-------------------
           PERFORM BB-ABRIR-ARCHIVOS
           PERFORM BC-VALIDA-PARAMET
           GO BZ-FIN-DE-SECCION.
           SKIP1
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN INPUT SUPMD00 CREMMAE HIPMMAE
               OUTPUT SUPMAUD.
           IF (WR-FS-SUPMD00 NOT = 00 AND NOT = 97)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'SUPMD00'               TO WR-ARCHIVO
               MOVE 'OPEN'                  TO WR-MENSAJE
               MOVE WR-FS-SUPMD00           TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           IF (WR-FS-SUPMAUD NOT = 00 AND NOT = 97)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'SUPMAUD'               TO WR-ARCHIVO
               MOVE 'OPEN'                  TO WR-MENSAJE
               MOVE WR-FS-SUPMAUD           TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           IF (WR-FS-CREMMAE NOT = 00 AND NOT = 97)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'CREMMAE'               TO WR-ARCHIVO
               MOVE 'OPEN'                  TO WR-MENSAJE
               MOVE WR-FS-CREMMAE           TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           END-IF.
240998*    IF (WR-FS-HIPMMAE NOT = 00 AND NOT = 97)
240998     IF (WR-FS-HIPMMAE NOT = 00 AND NOT = 97 AND NOT = 35)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'HIPMMAE'               TO WR-ARCHIVO
               MOVE 'OPEN'                  TO WR-MENSAJE
               MOVE WR-FS-HIPMMAE           TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           SKIP1
       BC-VALIDA-PARAMET.
      *------------------
           ACCEPT W-PARAMETROS
           MOVE SPACES                      TO RTNWCALE.
           MOVE ZEROES                      TO CALEAREA-FECHA-1
           MOVE 'CARGAR'                    TO CALEAREA-FUNCION
           CALL 'RUTUCALE' USING RTNWCALE
           IF (CALEAREA-FUNCION = 'CANCEL' OR CALEAREA-COD-RET NOT = 0)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BC-VALIDA-PARAMET'     TO WR-PARRAFO
               MOVE SPACES                  TO WR-ARCHIVO
               MOVE CALEAREA-FUNCION        TO WR-OPERACI
               MOVE 'ERROR EN RUTUCALE'     TO WR-MENSAJE
               MOVE CALEAREA-COD-RET        TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           ELSE
               MOVE WP-DIA                  TO CALEAREA-FECHA-1-D
               MOVE WP-MES                  TO CALEAREA-FECHA-1-M
               MOVE WP-ANO                  TO CALEAREA-FECHA-1-A
               MOVE 'INFO  '                TO CALEAREA-FUNCION
               CALL 'RUTUCALE' USING RTNWCALE
               IF (CALEAREA-COD-RET NOT = 0)
                   MOVE 'B-INICIALIZAR'     TO WR-SECCION
                   MOVE 'BC-VALIDA-PARAMET' TO WR-PARRAFO
                   MOVE SPACES              TO WR-ARCHIVO
                   MOVE CALEAREA-FUNCION    TO WR-OPERACI
                   MOVE 'FECHA PARAM INVAL' TO WR-MENSAJE
                   MOVE CALEAREA-COD-RET    TO WR-FILE-ST
                   PERFORM BE-DISPLAY-ERROR
               END-IF
           END-IF.
           SKIP1
       BE-DISPLAY-ERROR.
      *-----------------
           SET WS-PROCESO-ERROR             TO TRUE.
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' CLAVE D00        : ' D00-CLAVE OF D00-REGISTRO.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' AREA RTNWCALE    : ' RTNWCALE.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       E-SELECCION SECTION.
      *--------------------
       EA-PROCESO-PPAL.
      *----------------
           MOVE 'XXX'                       TO WT-COD-COMU(1)
           PERFORM EA-LEER-SUPMD00
           PERFORM EB-SELECCION-AUDITORIA
              UNTIL (WR-FS-SUPMD00 NOT = 0 OR WS-PROCESO-ERROR)
           GO EZ-FIN-DE-SECCION.
           SKIP1
       EA-LEER-SUPMD00.
      *----------------
           READ SUPMD00 NEXT END-READ
           IF (WR-FS-SUPMD00 NOT = 0 AND NOT = 10)
               MOVE 'E-SELECCION'           TO WR-SECCION
               MOVE 'EA-LEER-SUPMD00'       TO WR-PARRAFO
               MOVE 'SUPMD00'               TO WR-ARCHIVO
               MOVE 'READ'                  TO WR-OPERACI
               MOVE WR-FS-SUPMD00           TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           SKIP1
       EB-SELECCION-AUDITORIA.
      *-----------------------
           ADD 1                            TO WA-L-D00
           ADD 1                            TO WA-S-D00
           PERFORM EE-CALCULA-DIAS
           PERFORM ED-GRABA-SUPMAUD

           PERFORM EA-LEER-SUPMD00.
           SKIP1
       ED-GRABA-SUPMAUD.
      *-----------------
           PERFORM EG-LEER-DBC.
           IF (WB-REQ-DBC-RC = '  ')
               PERFORM EH-LEER-RCD
               IF (WB-REQ-RCD-RC = '  ')
                   PERFORM EI-BUSCA-COMUNA.

           MOVE D00-SISTEMA                 TO AUD-TIP-SIS
           MOVE D00-TIPO-OPERACION          TO AUD-TIP-OPE
           MOVE D00-NUM-INTERNO-IDENT       TO AUD-IDE-OPE
           MOVE D00-ARR-RUT-DD(1)           TO AUD-NUM-RUT
           MOVE DBC-GLS-NOMC                TO AUD-NOM-CLI
           MOVE RCD-GLS-DIRE                TO AUD-DIR-CLI
           MOVE COM-GLS-COMU                TO AUD-COM-CLI
           MOVE D00-MONTO-ORIGINAL          TO AUD-CAP-INI
           MOVE D00-SALDO-CONTABLE          TO AUD-SLD-FME
           EVALUATE TRUE
            WHEN (D00-SISTEMA = '01')
                  MOVE CRE-NUM-CUOT-PAGADAS TO AUD-CUO-PAG
                  MOVE CRE-TOTAL-CUOTAS     TO AUD-CUO-TOT
            WHEN (D00-SISTEMA = '03')
                  MOVE HIP-CUO-PAG          TO AUD-CUO-PAG
                  MOVE HIP-CUO-TOT          TO AUD-CUO-TOT
            WHEN OTHER
                  MOVE 0                    TO AUD-CUO-PAG
                  MOVE 0                    TO AUD-CUO-TOT
           END-EVALUATE
           MOVE CALEAREA-NUM-DIA            TO AUD-NUM-DIA
           MOVE D00-INT-POR-COBRAR          TO AUD-VAL-INT
           MOVE D00-REAJ-DEVEN              TO AUD-VAL-REA

           WRITE SUPRAUD.
           IF (WR-FS-SUPMAUD = 0)
               ADD 1                        TO WA-G-D00
           ELSE
               MOVE 'E-SELECCION'           TO WR-SECCION
               MOVE 'ED-GRABA-SUPMAUD'      TO WR-PARRAFO
               MOVE 'SUPMAUD'               TO WR-ARCHIVO
               MOVE  D00-CLAVE              TO WR-OPERACI
               MOVE 'WRITE'                 TO WR-MENSAJE
               MOVE WR-FS-SUPMAUD           TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           SKIP1
       EE-CALCULA-DIAS.
      *----------------
           IF (D00-SISTEMA IN D00-REGISTRO = '01')
               MOVE 0                         TO CRE-NUM-CUOT-PAGADAS
               MOVE 0                         TO CRE-TOTAL-CUOTAS
               PERFORM EF-LEER-CREMMAE
           ELSE
           IF (D00-SISTEMA IN D00-REGISTRO = '03')
               MOVE 0                         TO HIP-CUO-PAG
               MOVE 0                         TO HIP-CUO-TOT
               PERFORM EF-LEER-HIPMMAE.

           MOVE 99                            TO CALEAREA-NUM-DIA
           IF (D00-ESTADO-CREDITO IN D00-REGISTRO = 3)
               MOVE 0                         TO CALEAREA-NUM-DIA
           ELSE
           IF (D00-SISTEMA IN D00-REGISTRO = '01')
               IF (WR-FS-CREMMAE = 0 AND CRE-FECHA-SUSPEN > ZEROES)
                   MOVE 0                     TO CALEAREA-NUM-DIA
               END-IF
           END-IF.

           IF (CALEAREA-NUM-DIA = 99)
               IF (D00-FECHA-ULT-PAGO-INT-AA IN D00-REGISTRO NOT = '93')
                   OR
                  (D00-FECHA-ULT-PAGO-INT-AA IN D00-REGISTRO     = '00'
                   AND
                   D00-FECHA-OTORGAM-AA IN D00-REGISTRO NOT = '93')
                   MOVE 31                    TO CALEAREA-NUM-DIA
               END-IF
           END-IF.

           IF (CALEAREA-NUM-DIA = 99)
               IF (D00-FECHA-ULT-PAGO-INT-AA IN D00-REGISTRO
                   NOT > ZEROES OR
                   D00-FECHA-ULT-PAGO-INT-MM IN D00-REGISTRO
                   NOT > ZEROES OR
                   D00-FECHA-ULT-PAGO-INT-DD IN D00-REGISTRO
                   NOT > ZEROES)
                   MOVE D00-FECHA-OTORGAM IN D00-REGISTRO TO
                        D00-FECHA-ULT-PAGO-INT IN D00-REGISTRO
               END-IF
      *********MOVE '19'                      TO CALEAREA-FECHA-1-AM
               MOVE D00-FECHA-ULT-PAGO-INT-AA IN D00-REGISTRO
                                              TO CALEAREA-FECHA-1-AA
               MOVE D00-FECHA-ULT-PAGO-INT-MM IN D00-REGISTRO
                                              TO CALEAREA-FECHA-1-M
               MOVE D00-FECHA-ULT-PAGO-INT-DD IN D00-REGISTRO
                                              TO CALEAREA-FECHA-1-D
               IF CALEAREA-FECHA-1-AA < ROLL-YY
                  MOVE '20' TO CALEAREA-FECHA-1-AM
               ELSE
                  MOVE '19' TO CALEAREA-FECHA-1-AM
               END-IF
               MOVE WP-ANO                    TO CALEAREA-FECHA-2-A
               MOVE WP-MES                    TO CALEAREA-FECHA-2-M
               MOVE WP-DIA                    TO CALEAREA-FECHA-2-D
               MOVE 'CDCEF'                   TO CALEAREA-FUNCION
               MOVE +0                        TO CALEAREA-NUM-DIA
               CALL 'RUTUCALE' USING RTNWCALE
240998*            IF (CALEAREA-COD-RET NOT = 0)
240998*            MOVE 'E-SELECCION'         TO WR-SECCION
240998*            MOVE 'EE-CALCULA-DIAS'     TO WR-PARRAFO
240998*            MOVE CALEAREA-FECHA-1      TO WR-ARCHIVO
240998*            MOVE CALEAREA-FUNCION      TO WR-OPERACI
240998*            MOVE CALEAREA-FECHA-2      TO WR-MENSAJE
240998*            MOVE CALEAREA-COD-RET      TO WR-FILE-ST
240998*            PERFORM BE-DISPLAY-ERROR
240998*        ELSE
240998*        IF (CALEAREA-NUM-DIA > 31)
240998         IF (CALEAREA-COD-RET NOT = 0 OR CALEAREA-NUM-DIA > 31)
                   MOVE 31                    TO CALEAREA-NUM-DIA
240998*               END-IF
               END-IF
           END-IF.
           SKIP1
       EF-LEER-CREMMAE.
      *----------------
           MOVE D00-NUM-INTERNO-IDENT IN D00-REGISTRO
                                            TO CRE-CLAVE
           READ CREMMAE END-READ
           IF (WR-FS-CREMMAE = 00)
               ADD 1                        TO WA-L-CRE
           ELSE
           IF (WR-FS-CREMMAE NOT = 23)
               MOVE 'E-SELECCION'           TO WR-SECCION
               MOVE 'EF-LEER-CREMMAE'       TO WR-PARRAFO
               MOVE 'CREMMAE'               TO WR-ARCHIVO
               MOVE 'READ'                  TO WR-OPERACI
               MOVE WR-FS-CREMMAE           TO WR-FILE-ST
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           SKIP1
       EF-LEER-HIPMMAE.
      *----------------
           MOVE D00-CLAVE                   TO HIP-CLAVE
           READ HIPMMAE END-READ
           IF (WR-FS-HIPMMAE = 00)
               ADD 1                        TO WA-L-HIP
           ELSE
              IF (WR-FS-HIPMMAE NOT = 23)
                 MOVE 'E-SELECCION'           TO WR-SECCION
                 MOVE 'EF-LEER-HIPMMAE'       TO WR-PARRAFO
                 MOVE 'HIPMMAE'               TO WR-ARCHIVO
                 MOVE 'READ'                  TO WR-OPERACI
                 MOVE WR-FS-HIPMMAE           TO WR-FILE-ST
                 PERFORM BE-DISPLAY-ERROR
              END-IF
           END-IF.
           SKIP1
       EG-LEER-DBC.
      *------------
           MOVE D00-ARR-RUT-DD(1)        TO WR-RUT-DD.
           MOVE WR-NUM-RUT               TO WB-REQ-DBC-KEY-VALUE
           MOVE SPACES                   TO WB-REQ-DBC-RC
           MOVE 'DBC00'                  TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-DBC-AREA DBC WB-ELEM-LIS
           IF (WB-REQ-DBC-RC = '  ' OR = '14')
               IF (DBC-NUM-ICLI = WR-NUM-RUT AND WB-REQ-DBC-RC = '  ')
                   MOVE DBC-GLS-NOMC IN DBC  TO WT-NOMBRE
                   PERFORM EJ-COMPACTA-NOMBRE
                   MOVE WT-NOMBRE        TO DBC-GLS-NOMC IN DBC
               ELSE
                   MOVE '14'             TO WB-REQ-DBC-RC
                   MOVE 'SIN NOMBRE'     TO DBC-GLS-NOMC
                   MOVE 'SIN DIRECCION'  TO RCD-GLS-DIRE
                   MOVE 'SIN COMUNA'     TO COM-GLS-COMU
           ELSE
               MOVE 'E-SELECCION'        TO WR-SECCION
               MOVE 'EG-LEER-DBC'        TO WR-PARRAFO
               MOVE 'DBC'                TO WR-ARCHIVO
               MOVE WB-REQ-DBC-CMD       TO WR-OPERACI
               MOVE WB-REQ-DBC-RC        TO WR-FILE-ST
               MOVE WB-REQ-DBC-KEY-VALUE TO WR-MENSAJE
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           SKIP1
       EH-LEER-RCD.
      *------------
           MOVE DBC-CIC-ICLI             TO WB-REQ-RCD-KEY-CIC
           MOVE DBC-NUM-DCOR             TO WB-REQ-RCD-KEY-DIR
           MOVE SPACES                   TO WB-REQ-RCD-RC
           MOVE 'RCD00'                  TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-RCD-AREA RCD WB-ELEM-LIS
           IF (WB-REQ-RCD-RC = '14')
               MOVE 'SIN DIRECCION'      TO RCD-GLS-DIRE
               MOVE 'SIN COMUNA'         TO COM-GLS-COMU
           ELSE
           IF (WB-REQ-RCD-RC NOT = '  ')
               MOVE 'E-SELECCION'        TO WR-SECCION
               MOVE 'EH-LEER-RCD'        TO WR-PARRAFO
               MOVE 'RCD'                TO WR-ARCHIVO
               MOVE WB-REQ-RCD-CMD       TO WR-OPERACI
               MOVE WB-REQ-RCD-RC        TO WR-FILE-ST
               MOVE WB-REQ-RCD-KEY-VALUE TO WR-MENSAJE
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           SKIP1
       EH-LEER-COM.
      *------------
           MOVE 'COM'                    TO WB-REQ-TAB-KEY-TAB
           MOVE  RCD-COD-LUGA            TO WB-REQ-TAB-KEY-COM
           MOVE SPACES                   TO WB-REQ-TAB-RC
           MOVE 'TAB00'                  TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-TAB-AREA COM WB-ELEM-LIS
           IF (WB-REQ-TAB-RC = '14')
               MOVE 'SIN COMUNA'         TO COM-GLS-COMU
           END-IF.

           IF (WB-REQ-TAB-RC = '14' OR = SPACES)
               MOVE COM-GLS-COMU         TO WT-GLS-COMU(WI-IND)
               MOVE RCD-COD-LUGA         TO WT-COD-COMU(WI-IND)
               SET WI-IND                UP BY 1
               MOVE 'XXX'                TO WT-COD-COMU(WI-IND)
           ELSE
           IF (WB-REQ-TAB-RC NOT = '  ')
               MOVE 'E-SELECCION'        TO WR-SECCION
               MOVE 'EH-LEER-COM'        TO WR-PARRAFO
               MOVE 'COM'                TO WR-ARCHIVO
               MOVE WB-REQ-TAB-CMD       TO WR-OPERACI
               MOVE WB-REQ-TAB-RC        TO WR-FILE-ST
               MOVE WB-REQ-TAB-KEY-VALUE TO WR-MENSAJE
               PERFORM BE-DISPLAY-ERROR
           END-IF.
           SKIP1
       EI-BUSCA-COMUNA.
      *----------------
           SET WI-IND                        TO 1.
           SEARCH WT-COMUNAS
              WHEN (WT-COD-COMU(WI-IND) = RCD-COD-LUGA)
                    MOVE WT-GLS-COMU(WI-IND) TO COM-GLS-COMU
              WHEN (WT-COD-COMU(WI-IND) = 'XXX')
                    PERFORM EH-LEER-COM
           END-SEARCH.
           SKIP1
       EJ-COMPACTA-NOMBRE.
      *-------------------
           MOVE 0                   TO WI-K WI-I
           PERFORM EK-COMPACTA VARYING WI-I FROM 1 BY 1
                  UNTIL (WI-I > 75).
           SKIP1
       EK-COMPACTA.
      *------------
           MOVE WT-N(WI-I) TO WR-CAR-AUX
           MOVE ' '        TO WT-N(WI-I)
           IF (WI-K = 0 AND WR-CAR-AUX = ' ')
               NEXT SENTENCE
           ELSE
           IF (WR-CAR-AUX = ' ')
               IF (WT-N(WI-K) = ' ')
                   NEXT SENTENCE
               ELSE
                   ADD 1 TO WI-K
                   MOVE ' ' TO WT-N(WI-K)
               END-IF
           ELSE
               ADD 1 TO WI-K
               MOVE WR-CAR-AUX TO WT-N(WI-K)
           END-IF
           END-IF.
           SKIP1
       EZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPGPRI FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '     GENERACION DE D00 PARA AUDITORIA'
           DISPLAY ' '
           DISPLAY 'Reg. leidos de SUPMD00    : ' WA-L-D00
           DISPLAY 'Reg. selecc. de SUPMD00   : ' WA-S-D00
           DISPLAY 'Reg. leidos  de CREMMAE   : ' WA-L-CRE
           DISPLAY 'Reg. leidos  de HIPMMAE   : ' WA-L-HIP
           DISPLAY ' '
           DISPLAY 'Reg. grabados en SUPMAUD  : ' WA-G-D00
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY W-PARAMETROS
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SUPMD00 SUPMAUD CREMMAE HIPMMAE.
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT

