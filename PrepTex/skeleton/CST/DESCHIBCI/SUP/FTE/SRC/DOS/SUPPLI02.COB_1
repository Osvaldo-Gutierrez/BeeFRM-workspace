       IDENTIFICATION DIVISION.
      ************************
       PROGRAM-ID. SUPPLI02.
      *AUTHOR. DONOSO JARA OSVALDO.
      *DATE-WRITTEN,  12/04/91.
000000*COMPILED SITE 2000          21.09.98
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 13 DE MAYO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Super Bancos                                       *
      *  Subsist. :                                                    *
      *  Analista : Nunez Sergio                                       *
      *  Objetivo : Crear cinta con archivo I02 y su correspondiente   *
      *             caratula  (archivo de grupos relacionados)         *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPVI02, GRUPOS RELACIONADOS (I02)                 *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Salida  : SUPVCIN, Cinta de grupos relacionados              *
      *                                                                *
      *   Trabajo : SUPSSOR, Archivo de sort                           *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: Osvaldo Donoso Jara...............     *
      *            Fecha      : 02/01/92                               *
      *            Cambio     : Se traduce codigo 990 (bci) a codigo   *
      *                         9016 (super) debido a que codigo de    *
      *                         relacion solo es de tres digitos en SGC*
      *                                                                *
      *            Programador: Osvaldo Donoso Jara...............     *
      *            Fecha      : 20/08/93                               *
      *            Cambio     : Se cambia campo rec-cod-arec de tabla  *
      *                         REC de 3 a 5 caracteres y se anula     *
      *                         mantencion del 02/01/92                *
      *                                                                *
      *            Programador: David Parraguez Muoz.............     *
      *            Fecha      : 02/07/98                               *
      *            Cambio     : Se inserta siglo en encabezado del     *
      *                         archivo que se graba en la cinta para  *
      *                         la super                               *
      *                                                                *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      *********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *====================
       FILE-CONTROL.
           SELECT SUPLI02 ASSIGN   TO    UT-S-SUPLI02.

           SELECT SUPVI02 ASSIGN   TO    SUPVI02
                  ORGANIZATION IS INDEXED
                  ACCESS       IS SEQUENTIAL
                  RECORD KEY   IS I02-CLAVE
                  FILE STATUS IS WR-FS-SUPVI02.

           SELECT SUPVCIN ASSIGN   TO     UT-S-SUPVCIN
                  FILE STATUS IS WR-FS-SUPVCIN.

       DATA DIVISION.
      **************
       FILE SECTION.
      *============
       FD  SUPLI02      LABEL RECORD OMITTED.
       01  SUPRI02                   PIC X(80).

       FD  SUPVI02      LABEL RECORD STANDARD.
       COPY SUPBRI02.

       FD  SUPVCIN      LABEL RECORD STANDARD
                        BLOCK CONTAINS 0 RECORDS.
       01  SUPRCIN                   PIC X(80).
       EJECT
       WORKING-STORAGE SECTION.
      *=======================
       SKIP1
       01  W-PARAMETROS.
           05 FILLER                    PIC X(34).
           05 WP-FECHA.
              10 WP-MES                 PIC X(02).
              10 FILLER                 PIC X(01).
              10 WP-ANO                 PIC X(02).
       SKIP1
       01  W-SWITCHES.
           05 WS-TERMINO                PIC 9(01)  VALUE 0.
              88 WS-TERMINO-OK      VALUE 0.
              88 WS-TERMINO-ANORMAL VALUE 1.
       SKIP1
       01  W-INDICES.
           05 WI-I                      PIC 9(05)  VALUE 0.
       SKIP1
       01  W-TABLAS.
           05 WT-DETALLE.
              10 WT-TIP-PERS            PIC 9(07) OCCURS 03 TIMES.
           05 WT-GRUPOS.
              10 WT-GRP-INF             PIC X(01) OCCURS 10000 TIMES.
           05 WT-GLS-DETALLE.
              10 FILLER PIC X(45) VALUE 'NUMERO DE REGISTROS INFORMADOS
      -       '            '.
              10 FILLER PIC X(45) VALUE 'NUMERO DE PERSONAS RELACIONADAS
      -       ' INFORMADAS'.
              10 FILLER PIC X(45) VALUE 'NUMERO DE GRUPOS ECONOMICOS INF
      -       'ORMADOS'.
           05 FILLER REDEFINES WT-GLS-DETALLE OCCURS 03 TIMES.
              10 WT-GLS                 PIC X(45).
           05 WT-MESES.
              10 FILLER PIC X(10) VALUE '  ENERO   '.
              10 FILLER PIC X(10) VALUE ' FEBRERO  '.
              10 FILLER PIC X(10) VALUE '  MARZO   '.
              10 FILLER PIC X(10) VALUE '  ABRIL   '.
              10 FILLER PIC X(10) VALUE '   MAYO   '.
              10 FILLER PIC X(10) VALUE '  JUNIO   '.
              10 FILLER PIC X(10) VALUE '  JULIO   '.
              10 FILLER PIC X(10) VALUE '  AGOSTO  '.
              10 FILLER PIC X(10) VALUE 'SEPTIEMBRE'.
              10 FILLER PIC X(10) VALUE ' OCTUBRE  '.
              10 FILLER PIC X(10) VALUE 'NOVIEMBRE '.
              10 FILLER PIC X(10) VALUE 'DICIEMBRE '.
           05 FILLER REDEFINES WT-MESES OCCURS 12 TIMES.
              10 WT-MES                 PIC X(10).
       SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-I02                  PIC 9(06).
           05 WA-R-I02                  PIC 9(06).
           05 WA-G-I02                  PIC 9(06).
           05 WA-L-CIN                  PIC 9(06).
           05 WA-R-CIN                  PIC 9(06).
           05 WA-G-CIN                  PIC 9(06).
       SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-SECCION                 PIC X(15).
           05 WR-PARRAFO                 PIC X(15).
           05 WR-ARCHIVO                 PIC X(10).
           05 WR-OPERACI                 PIC X(10).
           05 WR-FILE-ST                 PIC X(02).
           05 WR-MENSAJE                 PIC X(35).
           05 WR-FECHA-HOY               PIC X(08).
           05 WR-FS-SUPVI02              PIC 9(02) VALUE 0.
           05 WR-FS-SUPVCIN              PIC 9(02) VALUE 0.
           05 WR-1ER-REG.
              10 WR-CODIGO-INST          PIC X(03) VALUE '016'.
              10 WR-CODIGO-ARCH          PIC X(03) VALUE 'I02'.
      * DPM ini  2-JUL-1998
              10 WR-SIG                  PIC 9(02).
      * DPM fin  2-JUL-1998
              10 WR-ANO                  PIC 9(02).
              10 WR-MES                  PIC 9(02).
              10 FILLER                  PIC X(60) VALUE SPACES.

           05 WR-LINEA01.
              10 FILLER         PIC X(020) VALUE 'INSTITUCION :  BANCO'.
              10 FILLER         PIC X(020) VALUE ' DE CREDITO E INVERS'.
              10 FILLER         PIC X(005) VALUE 'IONES'.
              10 FILLER         PIC X(022) VALUE ALL ' '.
              10 FILLER         PIC X(012) VALUE 'CODIGO : 016'.

           05 WR-LINEA02.
              10 FILLER         PIC X(020) VALUE 'INFORMACION CORRESPO'.
              10 FILLER         PIC X(020) VALUE 'NDIENTE AL MES DE :'.
              10 WR-LA02-MES    PIC X(010).
      **** Y2K-COMIENZO (INTERSOFT)
      *       10 FILLER         PIC X(006) VALUE ' DE 19'.
              10 FILLER         PIC X(004) VALUE ' DE '.
              10 WR-LA02-SIG    PIC X(002).
      **** Y2K-FINAL    (INTERSOFT)

              10 WR-LA02-ANO    PIC X(002).
              10 FILLER         PIC X(020) VALUE '         ARCHIVO: I0'.
              10 FILLER         PIC X(001) VALUE ALL '2'.

           05 WR-LINEA04.
              10 FILLER         PIC X(001) VALUE ALL '.'.
              10 FILLER         PIC X(077) VALUE ALL '-'.
              10 FILLER         PIC X(001) VALUE ALL '.'.

           05 WR-LINEA05.
              10 FILLER         PIC X(002) VALUE '|'.
              10 WR-LA05-GLS    PIC X(045).
              10 FILLER         PIC X(018) VALUE '        :  |'.
              10 WR-LA05-NUM    PIC Z.ZZZ.ZZ9.
              10 FILLER         PIC X(005) VALUE '    |'.

           05 WR-LINEA06.
              10 FILLER         PIC X(001) VALUE ALL '|'.
              10 FILLER         PIC X(057) VALUE ALL '-'.
              10 FILLER         PIC X(020) VALUE '|-------------------'.
              10 FILLER         PIC X(001) VALUE ALL '|'.

           05 WR-LINEA07.
              10 FILLER         PIC X(001) VALUE ALL ''''.
              10 FILLER         PIC X(077) VALUE ALL '-'.
              10 FILLER         PIC X(001) VALUE ALL ''''.

           05 WR-LINEA08.
              10 FILLER         PIC X(017) VALUE '   RESPONSABLE :'.
              10 FILLER         PIC X(028) VALUE ALL '.'.

           05 WR-LINEA09.
              10 FILLER         PIC X(017) VALUE '   FONO        :'.
              10 FILLER         PIC X(012) VALUE ALL '.'.
              10 FILLER         PIC X(026) VALUE ALL ' '.
              10 FILLER         PIC X(015) VALUE ALL '.'.

           05 WR-LINEA10.
              10 FILLER         PIC X(058) VALUE ALL ' '.
              10 FILLER         PIC X(007) VALUE 'GERENTE'.

       EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
      *------------------
           PERFORM B-INICIALIZAR.
       AB-PROCESAMIENTO.
      *-----------------
           IF WS-TERMINO-OK
              PERFORM C-PROCESAR
           END-IF.
       AC-TERMINACION.
      *---------------
           PERFORM D-TERMINAR.
       AZ-FIN-DE-PROCESO.
      *------------------
           STOP RUN.
       EJECT
       B-INICIALIZAR SECTION.
      *=======================
       BA-INICIALES.
      *-------------
           INITIALIZE WT-DETALLE WT-GRUPOS W-CONTADORES-Y-ACUMULADORES.
           OPEN INPUT  SUPVI02
                OUTPUT SUPLI02 SUPVCIN.
           IF (WR-FS-SUPVI02 NOT = 0 AND NOT = 97)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BA-INICIALES'      TO WR-PARRAFO
               MOVE 'SUPVI02'           TO WR-ARCHIVO
               MOVE  'OPEN'             TO WR-OPERACI
               MOVE  WR-FS-SUPVI02      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO BZ-FIN-DE-SECCION
           END-IF.

           IF (WR-FS-SUPVCIN NOT = 0)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BA-INICIALES'      TO WR-PARRAFO
               MOVE 'SUPVCIN'           TO WR-ARCHIVO
               MOVE  'OPEN'             TO WR-OPERACI
               MOVE  WR-FS-SUPVCIN      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO BZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       BB-VALIDA.
      *----------
           ACCEPT W-PARAMETROS.
           IF (WP-MES < '01' OR WP-MES > '12' OR
               WP-ANO NOT NUMERIC)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BB-VALIDA'         TO WR-PARRAFO
               MOVE 'SYSIN'             TO WR-ARCHIVO
               MOVE  '** FECHA INVALIDA **' TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO BZ-FIN-DE-SECCION
           END-IF.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
       EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GENERAL.
      *-------------------
           MOVE WP-MES     TO WR-MES
           MOVE WP-ANO     TO WR-ANO
      * DPM ini  2-JUL-1998
           IF WR-ANO > 50
               MOVE 19 TO WR-SIG
           ELSE
               MOVE 20 TO WR-SIG
           END-IF.
      * DPM fin  2-JUL-1998
           MOVE WR-1ER-REG TO I02.
           PERFORM CE-GRABA-SUPVCIN
           PERFORM CB-LEER-SUPVI02.
           PERFORM CC-GRABAR-CINTA
               UNTIL (WR-FS-SUPVI02 NOT = 0).
           PERFORM CD-LISTA-CARATULA.
           GO TO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-SUPVI02.
      *----------------
           READ SUPVI02 NEXT.
           IF (WR-FS-SUPVI02 NOT = 0 AND NOT = 10)
               MOVE 'C-PROCESAR'        TO WR-SECCION
               MOVE 'CB-LEER-SUPVI02'   TO WR-PARRAFO
               MOVE 'SUPVI02'           TO WR-ARCHIVO
               MOVE  'READ'             TO WR-OPERACI
               MOVE  WR-FS-SUPVI02      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO CZ-FIN-DE-SECCION
           END-IF.
       CC-GRABAR-CINTA.
      *----------------
           ADD 1 TO WA-L-I02
           ADD 1 TO WT-TIP-PERS(2)
           IF (I02-NUM-GRP-ECOM1 > 0)
               MOVE '1' TO WT-GRP-INF(I02-NUM-GRP-ECOM1)
           END-IF.
           IF (I02-NUM-GRP-ECOM2 > 0)
               MOVE '1' TO WT-GRP-INF(I02-NUM-GRP-ECOM2)
           END-IF.
           IF (I02-NUM-GRP-ECOM3 > 0)
               MOVE '1' TO WT-GRP-INF(I02-NUM-GRP-ECOM3)
           END-IF.
           IF (I02-NUM-GRP-ECOM4 > 0)
               MOVE '1' TO WT-GRP-INF(I02-NUM-GRP-ECOM4)
           END-IF.
           IF (I02-NUM-GRP-ECOM5 > 0)
               MOVE '1' TO WT-GRP-INF(I02-NUM-GRP-ECOM5)
           END-IF.
      *
           PERFORM CE-GRABA-SUPVCIN
           ADD 1                            TO WT-TIP-PERS(1).
           PERFORM CB-LEER-SUPVI02.
           SKIP1
       CD-LISTA-CARATULA.
      *------------------
           MOVE 1               TO WI-I
           PERFORM UNTIL (WI-I > 10000)
              IF (WT-GRP-INF(WI-I) = '1')
                  ADD 1 TO WT-TIP-PERS(3)
              END-IF
              ADD 1 TO WI-I
           END-PERFORM.
           MOVE 1               TO WI-I
           MOVE WT-MES (WR-MES) TO WR-LA02-MES
           MOVE WR-ANO          TO WR-LA02-ANO

      **** Y2K-COMIENZO (INTERSOFT)
           IF WR-ANO NOT < 60
              MOVE '19' TO WR-LA02-SIG
           ELSE
              MOVE '20' TO WR-LA02-SIG
           END-IF.
      **** Y2K-FINAL    (INTERSOFT)

           WRITE SUPRI02 FROM WR-LINEA01 AFTER PAGE
           WRITE SUPRI02 FROM WR-LINEA02 AFTER 2
           WRITE SUPRI02 FROM WR-LINEA04 AFTER 7
           PERFORM UNTIL (WI-I > 3)
              MOVE WT-GLS(WI-I)      TO WR-LA05-GLS
              MOVE WT-TIP-PERS(WI-I) TO WR-LA05-NUM
              WRITE SUPRI02 FROM WR-LINEA05 AFTER 1
              IF (WI-I < 3)
                  WRITE SUPRI02 FROM WR-LINEA06 AFTER 1
              END-IF
              ADD 1 TO WI-I
           END-PERFORM.
           WRITE SUPRI02 FROM WR-LINEA07 AFTER 1
           WRITE SUPRI02 FROM WR-LINEA08 AFTER 7
           WRITE SUPRI02 FROM WR-LINEA09 AFTER 2
           WRITE SUPRI02 FROM WR-LINEA10 AFTER 1.
           SKIP1
       CE-GRABA-SUPVCIN.
      *------------------
           WRITE SUPRCIN FROM I02
           IF (WR-FS-SUPVCIN NOT = 0)
               MOVE 'C-PROCESAR'        TO WR-SECCION
               MOVE 'CE-GRABA-SUPVCIN'  TO WR-PARRAFO
               MOVE 'SUPVCIN'           TO WR-ARCHIVO
               MOVE  'WRITE'            TO WR-OPERACI
               MOVE  WR-FS-SUPVCIN      TO WR-FILE-ST
               MOVE  ' '                TO WR-MENSAJE
               SET   WS-TERMINO-ANORMAL TO TRUE
               GO TO CZ-FIN-DE-SECCION
           ELSE
               ADD 1 TO WA-G-CIN
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
       EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           IF WS-TERMINO-ANORMAL
              DISPLAY 'SUPLI02, NO EMITIDO O INCOMPLETO'
              DISPLAY ' '
              DISPLAY ' '
           ELSE
              DISPLAY 'SUPLI02, SE EMITIO PARA LAS SIGUIENTES OFICINAS'
              DISPLAY ' '
              DISPLAY '090 L'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPLI02 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '          CARATULA DE CUADRATURA I02 '
           DISPLAY ' '
           DISPLAY 'ARCHIVO    LEIDOS    RECHAZADOS   GRABADOS'
           DISPLAY ' '
           DISPLAY 'SUPVI02  ' WA-L-I02 '    ' WA-R-I02 '   ' WA-G-I02
           DISPLAY 'SUPVCIN  ' WA-L-CIN '    ' WA-R-CIN '   ' WA-G-CIN
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY W-PARAMETROS.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-TERMINO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
       EB-TERMINO.
      *-----------
           CLOSE SUPLI02 SUPVI02 SUPVCIN.
           IF WS-TERMINO-ANORMAL
              MOVE 100 TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY ' '
              DISPLAY 'EL ERROR FUE EN LA SECCION          ' WR-SECCION
              DISPLAY '                   PARRAFO          ' WR-PARRAFO
              DISPLAY '                   ARCHIVO          ' WR-ARCHIVO
              DISPLAY '                   OPERACION        ' WR-OPERACI
              DISPLAY '                   FILE STATUS      ' WR-FILE-ST
              DISPLAY '                   MENSAJE DE ERROR ' WR-MENSAJE
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.

       EZ-FIN-DE-SECCION.
      *------------------
           EXIT.


