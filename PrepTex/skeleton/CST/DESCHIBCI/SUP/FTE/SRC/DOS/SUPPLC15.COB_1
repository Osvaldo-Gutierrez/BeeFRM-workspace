       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPLC15.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  11/05/95.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Sistema Super Bancos                               *
      *  Subsist. :                                                    *
      *  Analista : Donoso J. Osvaldo                                  *
      *  Objetivo : Emitir caeratula de archivo P15                    *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPVP15, Archivo P15 enviado a la Super            *
      *                                                                *
      *   Salida  : SUPLIS1, Listado caratula de archivo P15           *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : .....................................  *
      *                         .....................................  *
      *                         .....................................  *
      ******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPLIS1 ASSIGN       TO SUPLIS1.
           SELECT SUPVP15 ASSIGN       TO SUPVP15
                  FILE STATUS          IS WR-FS-SUPVP15.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPVP15                LABEL RECORD STANDARD
                                  RECORD CONTAINS 26 CHARACTERS
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBR15S.
           SKIP1
       FD  SUPLIS1                LABEL RECORD OMITTED.
       01  SUPRLIS                          PIC X(80).
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           02 WA-L-P15                      PIC 9(06) VALUE 0.
           02 WA-NUM-REG                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-NUM-REGI                   PIC 9(16) VALUE 0 COMP-3.
           02 WA-VAL-SCON                   PIC 9(16) VALUE 0 COMP-3.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(15).
              10 WR-PARRAFO                 PIC X(15).
              10 WR-ARCHIVO                 PIC X(10).
              10 WR-OPERACI                 PIC X(10).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-MENSAJE                 PIC X(35).
           05 WR-FS-SUPVP15                 PIC 9(02) VALUE 0.
           05 WR-REGI-ANT                  PIC X(06) VALUE SPACES.
           05 WR-MESES.
              10 FILLER                    PIC X(10) VALUE '     ENERO'.
              10 FILLER                    PIC X(10) VALUE '   FEBRERO'.
              10 FILLER                    PIC X(10) VALUE '     MARZO'.
              10 FILLER                    PIC X(10) VALUE '     ABRIL'.
              10 FILLER                    PIC X(10) VALUE '      MAYO'.
              10 FILLER                    PIC X(10) VALUE '     JUNIO'.
              10 FILLER                    PIC X(10) VALUE '     JULIO'.
              10 FILLER                    PIC X(10) VALUE '    AGOSTO'.
              10 FILLER                    PIC X(10) VALUE 'SEPTIEMBRE'.
              10 FILLER                    PIC X(10) VALUE '   OCTUBRE'.
              10 FILLER                    PIC X(10) VALUE ' NOVIEMBRE'.
              10 FILLER                    PIC X(10) VALUE ' DICIEMBRE'.
           05 WR-MES REDEFINES WR-MESES OCCURS 12 TIMES PIC X(10).
      *---------------------------------------------------------------*
      * LINEAS DE LISTADO                                             *
      *---------------------------------------------------------------*
           05 WR-LINEA01.
              10 FILLER           PIC X(029) VALUE ALL ' '.
              10 FILLER           PIC X(015) VALUE 'C A R A T U L A'.
              10 FILLER           PIC X(009) VALUE '      P15'.
           05 WR-LINEA02.
              10 FILLER           PIC X(018) VALUE 'Institucion : BANC'.
              10 FILLER           PIC X(018) VALUE 'O DE CREDITO E INV'.
              10 FILLER           PIC X(008) VALUE 'ERSIONES'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(009) VALUE 'Codigo :'.
              10 WR-LA02-COD-INS-FIN  PIC X(003).
           05 WR-LINEA03.
              10 FILLER           PIC X(018) VALUE 'Informacion Corres'.
              10 FILLER           PIC X(018) VALUE 'pondiente Al Mes D'.
              10 FILLER           PIC X(004) VALUE 'e :'.
              10 WR-LA03-MES-PER  PIC X(010).
              10 FILLER           PIC X(004) VALUE ' De'.
              10 WR-LA03-SIG-PER  PIC X(002).
              10 WR-LA03-ANO-PER  PIC X(002).
              10 FILLER           PIC X(016) VALUE '      Archivo : '.
              10 WR-LA03-IDE-ARC  PIC X(003).
           05 WR-LINEA04.
              10 FILLER           PIC X(001) VALUE '.'.
              10 FILLER           PIC X(078) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA05.
              10 FILLER           PIC X(018) VALUE '| Numero de Regist'.
              10 FILLER           PIC X(027) VALUE 'ros Informados    '.
              10 FILLER           PIC X(013) VALUE '           :|'.
              10 WR-LA05-NUM-REG  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA06.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(056) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA07.
              10 FILLER           PIC X(018) VALUE '| Numero De Region'.
              10 FILLER           PIC X(038) VALUE 'es con Informacion'.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA07-NUM-REGI PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA08.
              10 FILLER           PIC X(018) VALUE '| Total Saldo a Fi'.
              10 FILLER           PIC X(038) VALUE 'n de Mes          '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA08-VAL-SCON PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA09.
              10 FILLER           PIC X(001) VALUE ALL ''''.
              10 FILLER           PIC X(078) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL ''''.
           05 WR-LINEA10.
              10 FILLER           PIC X(014) VALUE 'Responsable :'.
              10 FILLER           PIC X(039) VALUE ALL '_'.
           05 WR-LINEA11.
              10 FILLER           PIC X(014) VALUE 'Fono        :'.
              10 FILLER           PIC X(017) VALUE ALL '_'.
              10 FILLER           PIC X(027) VALUE ALL ' '.
              10 FILLER           PIC X(023) VALUE ALL '_'.
           05 WR-LINEA12.
              10 FILLER           PIC X(062) VALUE ALL ' '.
              10 FILLER           PIC X(013) VALUE 'G E R E N T E'.
      *
           COPY RLOVR001.
           EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           STOP RUN.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-ABRIR-ARCHIVOS.
           GO BZ-FIN-DE-SECCION.
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT SUPLIS1
                INPUT  SUPVP15.

           IF WR-FS-SUPVP15 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVP15    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVP15            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BC-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-ERROR            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROC-PPAL.
      *-------------
           PERFORM CB-LEER-SUPVP15
           PERFORM CC-ACUMULA-CARATULA
              UNTIL (WR-FS-SUPVP15 NOT = 0 OR WS-PROCESO-ERROR)
           IF (NOT WS-PROCESO-ERROR)
               PERFORM CC-LISTA-CARATULA
           END-IF.
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-SUPVP15 .
      *----------------
           READ SUPVP15 END-READ.
           IF WR-FS-SUPVP15 = 0
              ADD 1                         TO WA-L-P15
           ELSE
           IF WR-FS-SUPVP15  NOT = 10
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'CB-LEER-SUPVP15 '       TO WR-PARRAFO
              MOVE 'SUPVP15     '           TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-SUPVP15            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       XX-MUEVE-REG-CABECERA.
      *----------------------
           MOVE P15H-COD-INS-FIN             TO WR-LA02-COD-INS-FIN
           MOVE P15H-IDE-ARC                 TO WR-LA03-IDE-ARC
           MOVE WR-MES(P15H-MES-PER)         TO WR-LA03-MES-PER
           MOVE P15H-ANO-PER                 TO WR-LA03-ANO-PER
      *****IF (P15H-ANO-PER > '90')
           IF (P15H-ANO-PER < ROLL-YY)
               MOVE '20'                     TO WR-LA03-SIG-PER
           ELSE
               MOVE '19'                     TO WR-LA03-SIG-PER
           END-IF.
           SKIP1
       CC-ACUMULA-CARATULA.
      *--------------------
           IF (P15H-IDE-ARC = 'P15')
               PERFORM XX-MUEVE-REG-CABECERA
           ELSE
               ADD 1                        TO WA-NUM-REG
               IF (WR-REGI-ANT NOT = P15S-COD-REGI)
                   ADD 1                    TO WA-NUM-REGI
                   MOVE P15S-COD-REGI       TO WR-REGI-ANT
               END-IF
               ADD P15S-VAL-SCON            TO WA-VAL-SCON
           END-IF.
           PERFORM CB-LEER-SUPVP15.
           SKIP1
       CC-LISTA-CARATULA.
      *------------------
           MOVE WA-NUM-REG                  TO WR-LA05-NUM-REG
           MOVE WA-NUM-REGI                 TO WR-LA07-NUM-REGI
           MOVE WA-VAL-SCON                 TO WR-LA08-VAL-SCON

           WRITE SUPRLIS FROM WR-LINEA01 AFTER PAGE
           WRITE SUPRLIS FROM WR-LINEA02 AFTER 3
           WRITE SUPRLIS FROM WR-LINEA03 AFTER 2
           WRITE SUPRLIS FROM WR-LINEA04 AFTER 3
           WRITE SUPRLIS FROM WR-LINEA05
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA07
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA08
           WRITE SUPRLIS FROM WR-LINEA09
           WRITE SUPRLIS FROM WR-LINEA10 AFTER 4
           WRITE SUPRLIS FROM WR-LINEA11 AFTER 2
           WRITE SUPRLIS FROM WR-LINEA12 AFTER 1.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'INFORME SUPLIS1 EMITIDO EN FORMA LOCAL'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPLC15 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '    CARATULA ARCHIVO P15 PARA LA SUPER'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'REG LEIDOS DE SUPVP15 : ' WA-L-P15
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SUPLIS1 SUPVP15
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT

