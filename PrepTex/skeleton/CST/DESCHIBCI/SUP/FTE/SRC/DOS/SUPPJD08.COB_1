       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPJD08.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  01/12/94.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Sistema Super Bancos                               *
      *  Subsist. :                                                    *
      *  Analista : Donoso J. Osvaldo                                  *
      *  Objetivo : Genera archivo D8S formato Super                   *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPVD8S, Archivo D8S formato super                 *
      *                                                                *
      *   Salida  : SUPVD08, Archivo D08 formato banco                 *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : .....................................  *
      *                         .....................................  *
      *                         .....................................  *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPVD08 ASSIGN       TO SUPVD08
                  ORGANIZATION         IS INDEXED
                  ACCESS MODE          IS RANDOM
                  RECORD KEY           IS D08-COD-ID08
                  FILE STATUS          IS WR-FS-SUPVD08.
           SELECT SUPVD8S ASSIGN       TO SUPVD8S
                  FILE STATUS          IS WR-FS-SUPVD8S.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPVD8S                LABEL RECORD STANDARD
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRD8H.
           SKIP1
       FD  SUPVD08                LABEL RECORD STANDARD
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRD08.
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           02 WA-L-D8S                      PIC 9(06) VALUE 0.
           02 WA-G-D08                      PIC 9(06) VALUE 0.
           02 WA-D-D08                      PIC 9(06) VALUE 0.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(15).
              10 WR-PARRAFO                 PIC X(15).
              10 WR-ARCHIVO                 PIC X(10).
              10 WR-OPERACI                 PIC X(10).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-MENSAJE                 PIC X(35).
           05 WR-FS-SUPVD8S                 PIC 9(02) VALUE 0.
           05 WR-FS-SUPVD08                 PIC 9(02) VALUE 0.
           EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           STOP RUN.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-ABRIR-ARCHIVOS.
           GO BZ-FIN-DE-SECCION.
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN I-O    SUPVD08
                INPUT  SUPVD8S.

           IF WR-FS-SUPVD08 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVD08    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD08            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           IF WR-FS-SUPVD8S NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVD8S    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD8S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BC-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-ERROR            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROC-PPAL.
      *-------------
           PERFORM CB-LEER-SUPVD8S
           PERFORM CC-GENERA-D08-BANCO
              UNTIL (WR-FS-SUPVD8S NOT = 0 OR WS-PROCESO-ERROR)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-SUPVD8S.
      *----------------
           READ SUPVD8S END-READ.
           IF WR-FS-SUPVD8S = 0
              ADD 1                         TO WA-L-D8S
           ELSE
           IF WR-FS-SUPVD8S  NOT = 10
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'CB-LEER-SUPVD8S '       TO WR-PARRAFO
              MOVE 'SUPVD8S     '           TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD8S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       CC-GENERA-D08-BANCO.
      *--------------------
           MOVE D8S-NUM-RUTD                TO D08-NUM-RUTD
           MOVE D8S-VRF-RUTD                TO D08-VRF-RUTD
           MOVE D8S-COD-NIIO                TO D08-COD-NIIO
           MOVE D8S-GLS-NOMD                TO D08-GLS-NOMD
           MOVE D8S-IND-TCRE                TO D08-IND-TCRE
           MOVE D8S-VAL-MCRE                TO D08-VAL-MCRE
           MOVE D8S-FEC-FCRE                TO D08-DMA-FCRE
           MOVE D8S-NUM-PCRE                TO D08-NUM-PCRE
           MOVE D8S-VAL-PRCR                TO D08-VAL-PRCR
           MOVE D8S-VAL-SDCA                TO D08-VAL-SDCA
           MOVE D8S-VAL-SDCO                TO D08-VAL-SDCO
           MOVE D8S-NUM-CCAT                TO D08-NUM-CCAT
           MOVE D8S-VAL-MCAT                TO D08-VAL-MCAT
           MOVE D8S-NUM-CMAA                TO D08-NUM-CMAA
           MOVE D8S-IND-CJUD                TO D08-IND-CJUD
           MOVE SPACES                      TO D08-IND-ESTC
           MOVE SPACES                      TO D08-GLS-FILL
           PERFORM XX-GRABA-SUPVD08
           PERFORM CB-LEER-SUPVD8S.
           SKIP1
       XX-GRABA-SUPVD08.
      *-----------------
           WRITE D08 END-WRITE.
           IF WR-FS-SUPVD08 = 0
              ADD 1                         TO WA-G-D08
           ELSE
           IF WR-FS-SUPVD08 = 22
              DISPLAY 'REG. DUPLICADO : ' D08
              ADD 1                         TO WA-D-D08
           ELSE
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'XX-GRABA-SUPVD08'       TO WR-PARRAFO
              MOVE 'SUPVD08     '           TO WR-ARCHIVO
              MOVE 'WRITE      '            TO WR-OPERACI
              MOVE WR-FS-SUPVD08            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'ESTE PROCESO NO EMITE INFORME'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPJD08 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '    GENERA ARCHIVO D8S EN FORMATO SUPER'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'REG LEIDOS DE  SUPVD8S : ' WA-L-D8S
           DISPLAY 'REG GRABADOS   SUPVD08 : ' WA-G-D08
           DISPLAY 'REG DUPLICADOS SUPVD08 : ' WA-D-D08
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SUPVD8S SUPVD08
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
