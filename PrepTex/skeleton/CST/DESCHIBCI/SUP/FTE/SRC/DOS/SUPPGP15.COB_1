       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. SUPPGP15.
       AUTHOR.     CONSIST.
      *DAD ini
       DATE-WRITTEN.   30-DIC-91 16:00 PM.
      *DAD fin

      * GENERA ARCHIVO P15 EN FORMA BATCH A PARTIR DEL D02 SORTEADO.

       ENVIRONMENT DIVISION.
      *=====================
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *DAD ini  30-DEC-1991
      *DOS SELECT  SRTD02 ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT  SRTD02 ASSIGN TO        DA-S-SORTWK1.
           SELECT  SRTD02 ASSIGN TO        DA-S-SORTWK1.
      *DAD fin  30-DEC-1991

       DATA DIVISION.
      *==============
       FILE SECTION.
       SD  SRTD02.
       01  SRT.
           03 SRT-COD-PROD                        PIC X(04).
           03 SRT-COD-REGI                        PIC X(02).
           03 SRT-IND-TMON                        PIC X(03).
           03 SRT-COD-ODSE                        PIC X(03).
           03 SRT-COD-ACTD                        PIC X(02).
           03 SRT-VAL-SCON                        PIC 9(11)V9(02).

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGIFD.
       COPY GNSWGSYS.
       COPY SUPBRP15.
       COPY SUPBRD02.
       COPY GNSBRTAB.
       COPY GNSBRMSC.
       COPY GNSBRMSG.
       COPY TABBROFI.
       COPY TABBRCOM.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       01  WSS-VARI.
           03 WSS-KOLD.
              05 WSS-COD-PROD                     PIC X(04).
              05 WSS-COD-REGI                     PIC X(02).
              05 WSS-IND-TMON                     PIC X(03).
              05 WSS-COD-ODSE                     PIC X(03).
              05 WSS-COD-ACTD                     PIC X(02).
           03 WSS-KNEW.
              05 WSS-COD-PROD                     PIC X(04).
              05 WSS-COD-REGI                     PIC X(02).
              05 WSS-IND-TMON                     PIC X(03).
              05 WSS-COD-ODSE                     PIC X(03).
              05 WSS-COD-ACTD                     PIC X(02).
           03 WSS-VAL-SCON         VALUE 0        PIC 9(11)V9(02).
           03 WSS-IND-ICOL         VALUE SPACES   PIC X(02).
           03 WSS-COD-NIIO.
              05 WSS-COD-SIST      VALUE SPACES   PIC X(01).
              05 FILLER            VALUE SPACES   PIC X(11).


       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
       INI-MAIN.
           ENTRY 'DBMSCBL'.
           MOVE 'SUPPGP15' TO FIO-PROG.
           PERFORM BUS-FSIS.
           SORT SRTD02
                      ON ASCENDING KEY SRT-COD-PROD IN SRT
                      ON ASCENDING KEY SRT-COD-REGI IN SRT
                      ON ASCENDING KEY SRT-IND-TMON IN SRT
                      ON ASCENDING KEY SRT-COD-ODSE IN SRT
                      ON ASCENDING KEY SRT-COD-ACTD IN SRT
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
           STOP RUN.
       FIN-MAIN.
           EXIT.

       INP-SRT SECTION.
       INI-INP-SRT.
      *JJV MOVE FIO-INP TO FIO-CMND.
      *    PERFORM TAB-FIO-OFI.
      *    MOVE FIO-INP TO FIO-CMND.
      *    PERFORM SUP-FIO-TAB.
      *    MOVE FIO-INP TO FIO-CMND.
      *    PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SUP-FIO-D02.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR DE APERTURA  D02: ABORTO ' FIO-STAT
               STOP RUN.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM SUP-FIO-D02.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ARCHIVO D02 VACIO: ABORTO'
               MOVE FIO-CLO TO FIO-CMND
               PERFORM SUP-FIO-D02
               STOP RUN.
      *DAD ini  17/07/90
      * Para saltarce el primer registro del D02 que es de Control
           MOVE FIO-GET-NXT TO FIO-CMND.
      *DAD fin  17/07/90
       LUP-INP-SRT.
           PERFORM SUP-FIO-D02.
           IF NOT FIO-STAT-OKS
               MOVE FIO-CLO TO FIO-CMND
               PERFORM SUP-FIO-D02
               GO TO FIN-INP-SRT.
      *DAD ini  06/07/90
           IF D02-VAL-SCVD IN D02 > ZEROES OR
              D02-IND-DCAS IN D02 = 'C'
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-INP-SRT.
      *DAD fin  06/07/90
      *DAD ini  05/12/89
           IF NOT ( D02-IND-TDOP IN D02 = 'D' AND
      *DAD ini  08/04/91
                    D02-NUM-DDIR IN D02 = 1 )
      *DAD fin  08/04/91
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-INP-SRT.
      *DAD fin  05/12/89
           IF D02-COD-NIIO IN D02 = WSS-COD-NIIO IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-INP-SRT.
           MOVE D02-COD-NIIO IN D02 TO WSS-COD-NIIO IN WSS-VARI.
           MOVE 'SUP'               TO TAB-COD-SIST.
           MOVE 'PRD'               TO TAB-COD-TTAB IN TAB.
           MOVE D02-COD-TCCO IN D02 TO TAB-COD-CTAB IN TAB.
      *ISP
           MOVE TAB-COD-SIST TO FIO-SIST.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-INP-SRT.
           MOVE TAB-GLS-DATA IN TAB TO WSS-IND-ICOL IN WSS-VARI.
           IF ( WSS-IND-ICOL IN WSS-VARI NOT = 'CE' ) AND
              ( WSS-IND-ICOL IN WSS-VARI NOT = 'LC' )
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-INP-SRT.
           MOVE D02-COD-TCCO IN D02 TO SRT-COD-PROD IN SRT.
           MOVE D02-COD-OFIC IN D02 TO OFI-COD-OFIC IN OFI.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO COM-COD-REGI IN COM
           ELSE
      *DAD ini
               MOVE 'TAB'               TO FIO-SIST
               MOVE 'COM'               TO TAB-COD-TTAB IN TAB
               MOVE OFI-COD-COMU IN OFI TO TAB-COD-CTAB IN TAB
      *DAD fin
      *ISP
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM GNS-FIO-TAB
               IF FIO-STAT-OKS
                   MOVE TAB TO COM
               ELSE
                   MOVE SPACES TO COM-COD-REGI IN COM.
           MOVE COM-COD-REGI IN COM TO SRT-COD-REGI IN SRT.
           MOVE 'TAB'               TO TAB-COD-SIST.
      *ISP
           MOVE SPACES              TO TAB.
           MOVE 'VLR'               TO TAB-EXT-TTAB IN TAB.
           MOVE D02-COD-MONE IN D02 TO TAB-EXT-CTAB IN TAB.
           MOVE 'TAB-EXT-TABL' TO FIO-AKEY.
           PERFORM BUS-TAB.
      *DAD ini
           MOVE TAB-COD-DAT3 IN TAB(1) TO SRT-IND-TMON IN SRT.
      *DAD fin
           MOVE D02-COD-ODSE IN D02 TO SRT-COD-ODSE IN SRT.
           MOVE D02-COD-ACTD IN D02 TO SRT-COD-ACTD IN SRT.
           MOVE D02-VAL-SCON IN D02 TO SRT-VAL-SCON IN SRT.
      *DAD ini  15/01/91 - 06/07/90
      *     ADD D02-VAL-SCVC IN D02 TO SRT-VAL-SCON IN SRT.
      *DAD fin  15/01/91 - 06/07/90
      *DAD ini  30-DEC-1991 / AVM
      * El Saldo Contable debe INCLUIR los Intereses Devengados
      *     SUBTRACT D02-VAL-IPCO IN D02 FROM SRT-VAL-SCON IN SRT.
      *     SUBTRACT D02-VAL-IPCV IN D02 FROM SRT-VAL-SCON IN SRT.
      *DAD fin  30-DEC-1991

      *ODJ CAMBIO REALIZADO POR EL BCI DESDE ODJ A ODJ
           IF (WSS-COD-SIST IN WSS-VARI = '5' AND
               D02-COD-TCCO IN D02      = '1115')
               MOVE '1116'              TO SRT-COD-PROD IN SRT.
      *ODJ

           RELEASE SRT
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-INP-SRT.
      *    MOVE FIO-CLO TO FIO-CMND.
      *    PERFORM TAB-FIO-OFI.
      *    MOVE FIO-CLO TO FIO-CMND.
      *    PERFORM SUP-FIO-TAB.
      *    MOVE FIO-CLO TO FIO-CMND.
      *    PERFORM GNS-FIO-TAB.
       FIN-INP-SRT.
           EXIT.

       OUT-SRT SECTION.
       INI-OUT-SRT.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM SUP-FIO-P15.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR DE APERTURA  P15 ' FIO-STAT
               STOP RUN.
           RETURN SRTD02 AT END
               GO TO CON-OUT-SRT.
           PERFORM PUT-KNW.
           ADD SRT-VAL-SCON IN SRT TO WSS-VAL-SCON IN WSS-VARI.
       LUP-OUT-SRT.
           MOVE WSS-KNEW TO WSS-KOLD.
           RETURN SRTD02 AT END
               GO TO CON-OUT-SRT.
           PERFORM PUT-KNW.
           IF WSS-KNEW = WSS-KOLD
               ADD SRT-VAL-SCON IN SRT TO WSS-VAL-SCON IN WSS-VARI
           ELSE
               PERFORM PUT-P15
               MOVE SRT-VAL-SCON IN SRT TO WSS-VAL-SCON IN WSS-VARI.
           GO TO LUP-OUT-SRT.
       CON-OUT-SRT.
           PERFORM PUT-P15.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SUP-FIO-P15.
       FIN-OUT-SRT.
           EXIT.

       PUT-KNW SECTION.
       INI-PUT-KNW.
           MOVE SRT-COD-PROD IN SRT TO WSS-COD-PROD IN WSS-KNEW.
           MOVE SRT-COD-REGI IN SRT TO WSS-COD-REGI IN WSS-KNEW.
           MOVE SRT-IND-TMON IN SRT TO WSS-IND-TMON IN WSS-KNEW.
           MOVE SRT-COD-ODSE IN SRT TO WSS-COD-ODSE IN WSS-KNEW.
           MOVE SRT-COD-ACTD IN SRT TO WSS-COD-ACTD IN WSS-KNEW.
       FIN-PUT-KNW.
           EXIT.

       PUT-P15 SECTION.
       INI-PUT-P15.
           MOVE WSS-COD-PROD IN WSS-KOLD TO P15-COD-PROD.
           MOVE WSS-COD-REGI IN WSS-KOLD TO P15-COD-REGI.
           MOVE WSS-IND-TMON IN WSS-KOLD TO P15-IND-TMON.
           MOVE WSS-COD-ODSE IN WSS-KOLD TO P15-COD-ODSE.
           MOVE WSS-COD-ACTD IN WSS-KOLD TO P15-COD-ACTD.
           MOVE WSS-VAL-SCON IN WSS-VARI TO P15-VAL-SCON.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM SUP-FIO-P15.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR DE ESCRITURA  P15 ' FIO-STAT.
       FIN-PUT-P15.
           EXIT.

       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSBBMSC.
       COPY GNSBFMSC.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY SUPIOD02.
       COPY SUPIOP15.
       COPY TABBFOFI.
