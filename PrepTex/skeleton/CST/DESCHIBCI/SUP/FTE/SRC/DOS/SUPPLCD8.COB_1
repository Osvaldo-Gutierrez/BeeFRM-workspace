       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPLCD8.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  01/12/94.
000000*COMPILED SITE 2000          21.09.98
      ******************************************************************
      *  Sistema  : Sistema Super Bancos                               *
      *  Subsist. :                                                    *
      *  Analista : Donoso J. Osvaldo                                  *
      *  Objetivo : Emitir caeratula de archivo D08                    *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPVD8S, Archivo D08 enviado a la Super            *
      *                                                                *
      *   Salida  : SUPLIS1, Listado caratula de archivo D08           *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: DAVID PARRAGUEZ MUNOZ                  *
      *            Fecha      : 07/08/1998                             *
      *            Cambio     : SE INSERTA USO DE VARIABLE SIGLO EN    *
      *                         EL TITULO DE LA CARATULA               *
      *                         .....................................  *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : .....................................  *
      *                         .....................................  *
      *                         .....................................  *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPLIS1 ASSIGN       TO SUPLIS1.
           SELECT SUPVD8S ASSIGN       TO SUPVD8S
                  FILE STATUS          IS WR-FS-SUPVD8S.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPVD8S                LABEL RECORD STANDARD
                                  RECORD CONTAINS 168 CHARACTERS
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRD8S.
           SKIP1
       FD  SUPLIS1                LABEL RECORD OMITTED.
       01  SUPRLIS                          PIC X(80).
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           02 WA-L-D8S                      PIC 9(06) VALUE 0.
           02 WA-OPE-TOT                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-OPE-CON                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-OPE-HIP                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-ACT-CON                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-ACT-HIP                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-ORD-CON                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-ORD-HIP                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-CUO-CON                    PIC 9(16) VALUE 0 COMP-3.
           02 WA-CUO-HIP                    PIC 9(16) VALUE 0 COMP-3.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(15).
              10 WR-PARRAFO                 PIC X(15).
              10 WR-ARCHIVO                 PIC X(10).
              10 WR-OPERACI                 PIC X(10).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-MENSAJE                 PIC X(35).
           05 WR-FS-SUPVD8S                 PIC 9(02) VALUE 0.
           05 WR-DDR-ANT                    PIC X(09) VALUE SPACES.
           05 WR-MESES.
              10 FILLER                    PIC X(10) VALUE '     ENERO'.
              10 FILLER                    PIC X(10) VALUE '   FEBRERO'.
              10 FILLER                    PIC X(10) VALUE '     MARZO'.
              10 FILLER                    PIC X(10) VALUE '     ABRIL'.
              10 FILLER                    PIC X(10) VALUE '      MAYO'.
              10 FILLER                    PIC X(10) VALUE '     JUNIO'.
              10 FILLER                    PIC X(10) VALUE '     JULIO'.
              10 FILLER                    PIC X(10) VALUE '    AGOSTO'.
              10 FILLER                    PIC X(10) VALUE 'SEPTIEMBRE'.
              10 FILLER                    PIC X(10) VALUE '   OCTUBRE'.
              10 FILLER                    PIC X(10) VALUE ' NOVIEMBRE'.
              10 FILLER                    PIC X(10) VALUE ' DICIEMBRE'.
           05 WR-MES REDEFINES WR-MESES OCCURS 12 TIMES PIC X(10).
      *---------------------------------------------------------------*
      * LINEAS DE LISTADO                                             *
      *---------------------------------------------------------------*
           05 WR-LINEA01.
              10 FILLER           PIC X(029) VALUE ALL ' '.
              10 FILLER           PIC X(015) VALUE 'C A R A T U L A'.
              10 FILLER           PIC X(009) VALUE '      D08'.
           05 WR-LINEA02.
              10 FILLER           PIC X(018) VALUE 'Institucion : BANC'.
              10 FILLER           PIC X(018) VALUE 'O DE CREDITO E INV'.
              10 FILLER           PIC X(008) VALUE 'ERSIONES'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(009) VALUE 'Codigo :'.
              10 WR-LA02-COD-INS-FIN  PIC X(003).
           05 WR-LINEA03.
              10 FILLER           PIC X(018) VALUE 'Informacion Corres'.
              10 FILLER           PIC X(018) VALUE 'pondiente Al Mes D'.
              10 FILLER           PIC X(004) VALUE 'e :'.
              10 WR-LA03-MES-PER  PIC X(010).
              10 FILLER           PIC X(004) VALUE ' De'.
              10 WR-LA03-SIG-PER  PIC X(002).
              10 WR-LA03-ANO-PER  PIC X(002).
              10 FILLER           PIC X(016) VALUE '      Archivo : '.
              10 WR-LA03-IDE-ARC  PIC X(003).
           05 WR-LINEA04.
              10 FILLER           PIC X(001) VALUE '.'.
              10 FILLER           PIC X(078) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL '.'.
           05 WR-LINEA05.
              10 FILLER           PIC X(018) VALUE '| Numero De Regist'.
              10 FILLER           PIC X(014) VALUE 'ros Informados'.
              10 FILLER           PIC X(024) VALUE ALL ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA05-OPE-TOT  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA06.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(056) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA07.
              10 FILLER           PIC X(018) VALUE '| Operaciones de C'.
              10 FILLER           PIC X(018) VALUE 'onsumo'.
              10 FILLER           PIC X(020) VALUE ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA07-OPE-CON  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA08.
              10 FILLER           PIC X(018) VALUE '| Numero De Regist'.
              10 FILLER           PIC X(018) VALUE 'ros Informados por'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA09.
              10 FILLER           PIC X(018) VALUE '| Operaciones Hipo'.
              10 FILLER           PIC X(018) VALUE 'tecarias para la V'.
              10 FILLER           PIC X(020) VALUE 'ivienda           '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA09-OPE-HIP  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA10.
              10 FILLER           PIC X(018) VALUE '| Total en Cuentas'.
              10 FILLER           PIC X(014) VALUE ' de Activo por'.
              10 FILLER           PIC X(025) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA11.
              10 FILLER           PIC X(018) VALUE '| Credito de Consu'.
              10 FILLER           PIC X(002) VALUE 'mo'.
              10 FILLER           PIC X(036) VALUE ALL ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA11-ACT-CON  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA12.
              10 FILLER           PIC X(018) VALUE '| Total en Cuentas'.
              10 FILLER           PIC X(014) VALUE ' de Activo por'.
              10 FILLER           PIC X(025) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA13.
              10 FILLER           PIC X(018) VALUE '| Credito Hipoteca'.
              10 FILLER           PIC X(017) VALUE 'rio para Vivienda'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA13-ACT-HIP  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA14.
              10 FILLER           PIC X(018) VALUE '| Total en Cuentas'.
              10 FILLER           PIC X(013) VALUE ' de Orden por'.
              10 FILLER           PIC X(026) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA15.
              10 FILLER           PIC X(018) VALUE '| Credito de Consu'.
              10 FILLER           PIC X(002) VALUE 'mo'.
              10 FILLER           PIC X(036) VALUE ALL ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA15-ORD-CON  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA16.
              10 FILLER           PIC X(018) VALUE '| Total en Cuentas'.
              10 FILLER           PIC X(013) VALUE ' de Orden por'.
              10 FILLER           PIC X(026) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA17.
              10 FILLER           PIC X(018) VALUE '| Credito Hipoteca'.
              10 FILLER           PIC X(017) VALUE 'rio para Vivienda'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA17-ORD-HIP  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA18.
              10 FILLER           PIC X(018) VALUE '| Total Monto Cuot'.
              10 FILLER           PIC X(016) VALUE 'as Atrasadas por'.
              10 FILLER           PIC X(023) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA19.
              10 FILLER           PIC X(018) VALUE '| Credito de Consu'.
              10 FILLER           PIC X(002) VALUE 'mo'.
              10 FILLER           PIC X(036) VALUE ALL ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA19-CUO-CON  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA20.
              10 FILLER           PIC X(018) VALUE '| Total Monto Cuot'.
              10 FILLER           PIC X(016) VALUE 'as Atrasadas por'.
              10 FILLER           PIC X(023) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(001) VALUE ALL '|'.
           05 WR-LINEA21.
              10 FILLER           PIC X(018) VALUE '| Credito Hipoteca'.
              10 FILLER           PIC X(017) VALUE 'rio para Vivienda'.
              10 FILLER           PIC X(021) VALUE ALL ' '.
              10 FILLER           PIC X(002) VALUE ':|'.
              10 WR-LA21-CUO-HIP  PIC ZZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
              10 FILLER           PIC X(002) VALUE ' |'.
           05 WR-LINEA22.
              10 FILLER           PIC X(001) VALUE ALL ''''.
              10 FILLER           PIC X(078) VALUE ALL '-'.
              10 FILLER           PIC X(001) VALUE ALL ''''.
           05 WR-LINEA23.
              10 FILLER           PIC X(014) VALUE 'Responsable :'.
              10 FILLER           PIC X(039) VALUE ALL '_'.
           05 WR-LINEA24.
              10 FILLER           PIC X(014) VALUE 'Fono        :'.
              10 FILLER           PIC X(017) VALUE ALL '_'.
              10 FILLER           PIC X(027) VALUE ALL ' '.
              10 FILLER           PIC X(023) VALUE ALL '_'.
           05 WR-LINEA25.
              10 FILLER           PIC X(062) VALUE ALL ' '.
              10 FILLER           PIC X(013) VALUE 'G E R E N T E'.
           EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           STOP RUN.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-ABRIR-ARCHIVOS.
           GO BZ-FIN-DE-SECCION.
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT SUPLIS1
                INPUT  SUPVD8S.

           IF WR-FS-SUPVD8S NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVD8S    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD8S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BC-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-ERROR            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROC-PPAL.
      *-------------
           PERFORM CB-LEER-SUPVD8S
           PERFORM XX-MUEVE-REG-CABECERA
           PERFORM CB-LEER-SUPVD8S
           PERFORM CC-ACUMULA-CARATULA
              UNTIL (WR-FS-SUPVD8S NOT = 0 OR WS-PROCESO-ERROR)
           IF (NOT WS-PROCESO-ERROR)
               PERFORM CC-LISTA-CARATULA
           END-IF.
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-SUPVD8S .
      *----------------
           READ SUPVD8S END-READ.
           IF WR-FS-SUPVD8S = 0
              ADD 1                         TO WA-L-D8S
           ELSE
           IF WR-FS-SUPVD8S  NOT = 10
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'CB-LEER-SUPVD8S '       TO WR-PARRAFO
              MOVE 'SUPVD8S     '           TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD8S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       XX-MUEVE-REG-CABECERA.
      *----------------------
           MOVE D8S-COD-INS-FIN              TO WR-LA02-COD-INS-FIN
           MOVE D8S-IDE-ARC                  TO WR-LA03-IDE-ARC
           MOVE WR-MES(D8S-MES-PER)          TO WR-LA03-MES-PER
           MOVE D8S-ANO-PER                  TO WR-LA03-ANO-PER
      * DPM INI  7-AUG-1998
      *     IF (D8S-ANO-PER > '90')
      *         MOVE '19'                     TO WR-LA03-SIG-PER
      *     ELSE
      *         MOVE '20'                     TO WR-LA03-SIG-PER
      *     END-IF.
           MOVE D8S-SIG-PER                  TO WR-LA03-SIG-PER.
      * DPM FIN  7-AUG-1998
           SKIP1
       CC-ACUMULA-CARATULA.
      *--------------------
           ADD 1                            TO WA-OPE-TOT
           EVALUATE TRUE
              WHEN (D8S-IND-TCRE = 11 OR = 12 OR = 19)
                    ADD 1                   TO WA-OPE-CON
                    ADD D8S-VAL-SDCA        TO WA-ACT-CON
                    ADD D8S-VAL-SDCO        TO WA-ORD-CON
                    ADD D8S-VAL-MCAT        TO WA-CUO-CON
              WHEN OTHER
                    ADD 1                   TO WA-OPE-HIP
                    ADD D8S-VAL-SDCA        TO WA-ACT-HIP
                    ADD D8S-VAL-SDCO        TO WA-ORD-HIP
                    ADD D8S-VAL-MCAT        TO WA-CUO-HIP
           END-EVALUATE.
           PERFORM CB-LEER-SUPVD8S.
           SKIP1
       CC-LISTA-CARATULA.
      *------------------
           MOVE WA-OPE-TOT                  TO WR-LA05-OPE-TOT
           MOVE WA-OPE-CON                  TO WR-LA07-OPE-CON
           MOVE WA-OPE-HIP                  TO WR-LA09-OPE-HIP
           MOVE WA-ACT-CON                  TO WR-LA11-ACT-CON
           MOVE WA-ACT-HIP                  TO WR-LA13-ACT-HIP
           MOVE WA-ORD-CON                  TO WR-LA15-ORD-CON
           MOVE WA-ORD-HIP                  TO WR-LA17-ORD-HIP
           MOVE WA-CUO-CON                  TO WR-LA19-CUO-CON
           MOVE WA-CUO-HIP                  TO WR-LA21-CUO-HIP

           WRITE SUPRLIS FROM WR-LINEA01 AFTER PAGE
           WRITE SUPRLIS FROM WR-LINEA02 AFTER 3
           WRITE SUPRLIS FROM WR-LINEA03 AFTER 2
           WRITE SUPRLIS FROM WR-LINEA04 AFTER 3
           WRITE SUPRLIS FROM WR-LINEA05
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA08
           WRITE SUPRLIS FROM WR-LINEA07
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA08
           WRITE SUPRLIS FROM WR-LINEA09
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA10
           WRITE SUPRLIS FROM WR-LINEA11
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA12
           WRITE SUPRLIS FROM WR-LINEA13
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA14
           WRITE SUPRLIS FROM WR-LINEA15
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA16
           WRITE SUPRLIS FROM WR-LINEA17
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA18
           WRITE SUPRLIS FROM WR-LINEA19
           WRITE SUPRLIS FROM WR-LINEA06
           WRITE SUPRLIS FROM WR-LINEA20
           WRITE SUPRLIS FROM WR-LINEA21
           WRITE SUPRLIS FROM WR-LINEA22
           WRITE SUPRLIS FROM WR-LINEA23 AFTER 4
           WRITE SUPRLIS FROM WR-LINEA24 AFTER 2
           WRITE SUPRLIS FROM WR-LINEA25 AFTER 1.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'INFORME SUPLIS1 EMITIDO EN FORMA LOCAL'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPLCD8 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '    CARATULA ARCHIVO D08 PARA LA SUPER'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'REG LEIDOS DE SUPVD8S : ' WA-L-D8S
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SUPLIS1 SUPVD8S
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT


