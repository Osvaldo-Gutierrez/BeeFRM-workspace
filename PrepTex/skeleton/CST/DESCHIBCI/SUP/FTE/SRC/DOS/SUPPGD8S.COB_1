       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPGD8S.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  01/12/94.
00000 *COMPILED SITE 2000         07.08.98
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Sistema Super Bancos                               *
      *  Subsist. :                                                    *
      *  Analista : Donoso J. Osvaldo                                  *
      *  Objetivo : Genera archivo D08 formato Super                   *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPVD08, Archivo D08 formato banco                 *
      *                                                                *
      *   Salida  : SUPVD8S, Archivo D8S formato super                 *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: David Parraguez Muoz......            *
      *            Fecha      : 25/06/98                               *
      *            Cambio     : Se modifica el formato de las fechas   *
      *                         en el archivo D8S en el item ao, de   *
      *                         AA por AAAA a peticion de la SUPER.    *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPVD8S ASSIGN       TO SUPVD8S
                  FILE STATUS          IS WR-FS-SUPVD8S.
           SELECT SUPVD08 ASSIGN       TO SUPVD08
                  ORGANIZATION         IS INDEXED
                  ACCESS MODE          IS SEQUENTIAL
                  RECORD KEY           IS D08-COD-ID08
                  FILE STATUS          IS WR-FS-SUPVD08.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPVD08                LABEL RECORD STANDARD
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRD08.
           SKIP1
       FD  SUPVD8S                LABEL RECORD STANDARD
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRD8S.
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           02 WA-L-D08                      PIC 9(06) VALUE 0.
           02 WA-G-D8S                      PIC 9(06) VALUE 0.
           SKIP1
       01  W-PARAMETROS.
           02 FILLER                        PIC X(20).
           02 WP-ANO-PER                    PIC X(02).
           02 WP-MES-PER                    PIC X(02).
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(15).
              10 WR-PARRAFO                 PIC X(15).
              10 WR-ARCHIVO                 PIC X(10).
              10 WR-OPERACI                 PIC X(10).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-MENSAJE                 PIC X(35).
           05 WR-FS-SUPVD08                 PIC 9(02) VALUE 0.
           05 WR-FS-SUPVD8S                 PIC 9(02) VALUE 0.
           EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           STOP RUN.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-VALIDAR-PARAME.
           PERFORM BB-ABRIR-ARCHIVOS.
           GO BZ-FIN-DE-SECCION.
       BB-VALIDAR-PARAME.
      *------------------
           ACCEPT W-PARAMETROS.
           IF (WP-ANO-PER NOT NUMERIC OR
               WP-MES-PER NOT NUMERIC OR
               WP-ANO-PER = '00'      OR
               WP-MES-PER > '12'      OR
               WP-MES-PER < '01')
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-VALIDAR-PARAME'     TO WR-PARRAFO
               MOVE 'SYSIN      '           TO WR-ARCHIVO
               MOVE '           '           TO WR-OPERACI
               MOVE WR-FS-SUPVD8S           TO WR-FILE-ST
               MOVE 'ERROR EN PARAMETROS'   TO WR-MENSAJE
               PERFORM BC-DISPLAY-ERROR
           END-IF.
           IF WR-FS-SUPVD08 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVD08    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD08            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT SUPVD8S
                INPUT  SUPVD08.

           IF WR-FS-SUPVD8S NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVD8S    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD8S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           IF WR-FS-SUPVD08 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVD08    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD08            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BC-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-ERROR            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROC-PPAL.
      *-------------
           PERFORM CB-GENERA-REG-CABECERA.
           PERFORM CB-LEER-SUPVD08
           PERFORM CC-GENERA-D8S-SUPER
              UNTIL (WR-FS-SUPVD08 NOT = 0 OR WS-PROCESO-ERROR)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-GENERA-REG-CABECERA.
      *-----------------------
           MOVE SPACES                      TO D8S0.
           MOVE '016'                       TO D8S-COD-INS-FIN
           MOVE 'D08'                       TO D8S-IDE-ARC
      * DPM ini  25-JUN-1998
           IF WP-ANO-PER > 50
               MOVE 19                      TO D8S-SIG-PER
           ELSE
               MOVE 20                      TO D8S-SIG-PER
           END-IF
      * DPM fin  25-JUN-1998
           MOVE WP-ANO-PER                  TO D8S-ANO-PER
           MOVE WP-MES-PER                  TO D8S-MES-PER
           PERFORM XX-GRABA-SUPVD8S.
           SKIP1
       CB-LEER-SUPVD08.
      *----------------
           READ SUPVD08 END-READ.
           IF WR-FS-SUPVD08 = 0
              ADD 1                         TO WA-L-D08
           ELSE
           IF WR-FS-SUPVD08  NOT = 10
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'CB-LEER-SUPVD08 '       TO WR-PARRAFO
              MOVE 'SUPVD08     '           TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD08            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       CC-GENERA-D8S-SUPER.
      *--------------------
           MOVE D08-NUM-RUTD                TO D8S-NUM-RUTD
           MOVE D08-VRF-RUTD                TO D8S-VRF-RUTD
           MOVE D08-COD-NIIO                TO D8S-COD-NIIO
           MOVE D08-GLS-NOMD                TO D8S-GLS-NOMD
           MOVE D08-IND-TCRE                TO D8S-IND-TCRE
           MOVE D08-VAL-MCRE                TO D8S-VAL-MCRE
      * DPM ini  25-JUN-1998
      *     MOVE D08-DMA-FCRE                TO D8S-DMA-FCRE
           IF D08-DMA-FCRE NOT NUMERIC OR
              D08-DMA-FCRE NOT > ZEROES
               MOVE ZEROES TO D8S-FEC-FCRE
           ELSE
               MOVE D08-NUM-ACRE                TO D8S-NUM-ACRE
               MOVE D08-NUM-MCRE                TO D8S-NUM-MCRE
               MOVE D08-NUM-DCRE                TO D8S-NUM-DCRE
               IF D08-NUM-ACRE > 50
                   MOVE 19                      TO D8S-NUM-SCRE
               ELSE
                   MOVE 20                      TO D8S-NUM-SCRE
               END-IF
           END-IF
      * DPM fin  25-JUN-1998
           MOVE D08-NUM-PCRE                TO D8S-NUM-PCRE
           MOVE D08-VAL-PRCR                TO D8S-VAL-PRCR
           MOVE D08-VAL-SDCA                TO D8S-VAL-SDCA
           MOVE D08-VAL-SDCO                TO D8S-VAL-SDCO
           MOVE D08-NUM-CCAT                TO D8S-NUM-CCAT
           MOVE D08-VAL-MCAT                TO D8S-VAL-MCAT
           MOVE D08-NUM-CMAA                TO D8S-NUM-CMAA
           MOVE D08-IND-CJUD                TO D8S-IND-CJUD
           PERFORM XX-GRABA-SUPVD8S
           PERFORM CB-LEER-SUPVD08.
           SKIP1
       XX-GRABA-SUPVD8S.
      *-----------------
           WRITE D8S END-WRITE.
           IF WR-FS-SUPVD8S = 0
              ADD 1                         TO WA-G-D8S
           ELSE
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'XX-GRABA-SUPVD8S'       TO WR-PARRAFO
              MOVE 'SUPVD8S     '           TO WR-ARCHIVO
              MOVE 'WRITE      '            TO WR-OPERACI
              MOVE WR-FS-SUPVD8S            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'ESTE PROCESO NO EMITE INFORME'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPGD8S FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '    GENERA ARCHIVO D08 EN FORMATO SUPER'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'REG LEIDOS DE  SUPVD08 : ' WA-L-D08
           DISPLAY 'REG GRABADOS   SUPVD8S : ' WA-G-D8S
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SUPVD08 SUPVD8S
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT

