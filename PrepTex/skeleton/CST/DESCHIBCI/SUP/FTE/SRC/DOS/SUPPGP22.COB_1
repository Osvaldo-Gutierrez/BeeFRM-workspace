       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     SUPPGP22.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  15/05/95.
00000 *COMPILED SITE 2000         07.08.98
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Sistema Super Bancos                               *
      *  Subsist. :                                                    *
      *  Analista : Donoso J. Osvaldo                                  *
      *  Objetivo : Genera archivo P22 formato Super                   *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : SUPVD02, Archivo D02 formato banco                 *
      *             SUPVTAR, Carteras origen asociadas a Cod. Producto *
      *                                                                *
      *   Salida  : SUPVP22, Archivo P22 enviado a la Super            *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      * Modificacion  2 :                                              *
      *            Programador: David Parraguez Muoz.............     *
      *            Fecha      : 02/07/98                               *
      *            Cambio     : Se inserta siglo en encabezado del     *
      *                         archivo que se graba en la cinta para  *
      *                         la super                               *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : .....................................  *
      *                         .....................................  *
      *                         .....................................  *
      ******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SUPVD02 ASSIGN       TO SUPVD02
                  ORGANIZATION         IS INDEXED
                  ACCESS MODE          IS SEQUENTIAL
                  RECORD KEY           IS D02-COD-ID02
                  FILE STATUS          IS WR-FS-SUPVD02.
           SELECT SUPVP22 ASSIGN       TO SUPVP22
                  FILE STATUS          IS WR-FS-SUPVP22.
           SELECT SUPVTAR ASSIGN       TO SUPVTAR
                  FILE STATUS          IS WR-FS-SUPVTAR.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  SUPVD02                LABEL RECORD STANDARD
                                  RECORD CONTAINS 460 CHARACTERS
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRD02.
           SKIP1
       FD  SUPVP22                LABEL RECORD STANDARD
                                  RECORD CONTAINS 56 CHARACTERS
                                  BLOCK  CONTAINS 0 RECORDS.
       COPY SUPBRP22.
           SKIP1
       FD  SUPVTAR.
       01  SUPRTAR                PIC  X(80).
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-PARAMETROS.
           05 FILLER                        PIC X(20).
           05 WP-FEC-PRO.
              10 WP-SIG-PRO                 PIC X(02).
              10 WP-ANO-PRO                 PIC X(02).
              10 WP-MES-PRO                 PIC X(02).
              10 WP-DIA-PRO                 PIC X(02).
           05 FILLER                        PIC X(52).
           SKIP1
       01  W-TABLAS.
           05 WT-PRODUCTOS OCCURS 100 TIMES INDEXED BY WI-PRO.
              10 WT-COD-TCCO                PIC  X(04).
              10 FILLER                     PIC  X(01).
              10 WT-COD-PROD                PIC  X(03).
           05 WT-ACUM-PROD OCCURS 100 TIMES INDEXED BY WI-IND.
              10 WT-COD-PPRO                PIC  X(03).
              10 WT-NUM-OPER                PIC  9(08).
              10 WT-VAL-SLDO                PIC  9(12)V9(02).
              10 WT-VAL-VCDO                PIC  9(12)V9(02).
              10 WT-NUM-OMES                PIC  9(08).
              10 WT-VAL-OMES                PIC  9(14)V9(02).
              10 WT-VAL-OMPN                PIC  9(14)V9(02).
           SKIP1
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           02 WA-L-D02                      PIC 9(06) VALUE 0.
           02 WA-G-P22                      PIC 9(06) VALUE 0.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(15).
              10 WR-PARRAFO                 PIC X(15).
              10 WR-ARCHIVO                 PIC X(10).
              10 WR-OPERACI                 PIC X(10).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-MENSAJE                 PIC X(35).
           05 WR-FS-SUPVD02                 PIC 9(02) VALUE 0.
           05 WR-FS-SUPVP22                 PIC 9(02) VALUE 0.
           05 WR-FS-SUPVTAR                 PIC 9(02) VALUE 0.
           05 WR-COD-PROD-OK                PIC X(02).
      *<<<< VARIABLES PARA RUTINA DE VAL DE FECHAS
       COPY RTNWCALE.
      *
           COPY RLOVR001.
      *
           EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           STOP RUN.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-ABRIR-ARCHIVOS.
           PERFORM BB-VALIDA-FECHA.
           PERFORM BB-CARGA-TABLA-PROD.
           GO BZ-FIN-DE-SECCION.
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT SUPVP22
                INPUT  SUPVD02 SUPVTAR.

           IF WR-FS-SUPVP22 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVP22    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVP22            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           IF WR-FS-SUPVD02 NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVD02    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD02            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           IF WR-FS-SUPVTAR NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-ABRIR-ARCHIVOS'      TO WR-PARRAFO
              MOVE 'SUPVTAR    '            TO WR-ARCHIVO
              MOVE 'OPEN       '            TO WR-OPERACI
              MOVE WR-FS-SUPVTAR            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BB-VALIDA-FECHA.
      *----------------
           ACCEPT W-PARAMETROS.
           MOVE 'CARGAR'                    TO CALEAREA-FUNCION.
           MOVE ZEROES                      TO CALEAREA-FECHA-1.
           MOVE ZEROES                      TO CALEAREA-FECHA-2.
           PERFORM BD-CALL-RUTUCALE.
           MOVE WP-SIG-PRO                  TO CALEAREA-FECHA-1-AM
           MOVE WP-ANO-PRO                  TO CALEAREA-FECHA-1-AA
           MOVE WP-MES-PRO                  TO CALEAREA-FECHA-1-M
           MOVE WP-DIA-PRO                  TO CALEAREA-FECHA-1-D
           MOVE ZEROES                      TO CALEAREA-FECHA-2
           MOVE 'INFO  '                    TO CALEAREA-FUNCION
           PERFORM BD-CALL-RUTUCALE
           IF (CALEAREA-COD-RET NOT = 0)
               MOVE 'B-INICIALIZAR'    TO WR-SECCION
               MOVE 'BD-CALL-RUTUCALE' TO WR-PARRAFO
               MOVE 'RUTUCALE'         TO WR-ARCHIVO
               MOVE CALEAREA-FUNCION   TO WR-OPERACI
               MOVE '*ERROR EN FECHA ' TO WR-MENSAJE
               MOVE CALEAREA-COD-RET   TO WR-FILE-ST
               DISPLAY 'CALEAREA-FECHA-1 : ' CALEAREA-FECHA-1
               DISPLAY 'CALEAREA-FECHA-2 : ' CALEAREA-FECHA-2
               PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       BD-CALL-RUTUCALE.
      *-----------------
           CALL 'RUTUCALE' USING RTNWCALE.
           IF (CALEAREA-FUNCION = 'CANCEL')
               MOVE 'B-INICIALIZAR'    TO WR-SECCION
               MOVE 'BD-CALL-RUTUCALE' TO WR-PARRAFO
               MOVE 'RUTUCALE'         TO WR-ARCHIVO
               MOVE CALEAREA-FUNCION   TO WR-OPERACI
               MOVE '*ERROR EN RUTINA' TO WR-MENSAJE
               MOVE CALEAREA-COD-RET   TO WR-FILE-ST
               DISPLAY 'CALEAREA-FECHA-1 : ' CALEAREA-FECHA-1
               DISPLAY 'CALEAREA-FECHA-2 : ' CALEAREA-FECHA-2
               PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       BB-CARGA-TABLA-PROD.
      *--------------------
           INITIALIZE W-TABLAS.
           SET WI-PRO                       TO 1
           READ SUPVTAR.
           PERFORM UNTIL (WR-FS-SUPVTAR NOT = 00 OR WS-PROCESO-ERROR OR
                          WI-PRO > 100)
              MOVE SUPRTAR                  TO WT-PRODUCTOS(WI-PRO)
              READ SUPVTAR
              SET WI-PRO                    UP BY 1
           END-PERFORM.
           IF (WI-PRO > 100)
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BB-CARGA-TABLA-PROD'    TO WR-PARRAFO
              MOVE 'SUPVTAR    '            TO WR-ARCHIVO
              MOVE '           '            TO WR-OPERACI
              MOVE '  '                     TO WR-FILE-ST
              MOVE 'SE LLENO TABLA PRODUC'  TO WR-MENSAJE
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       BC-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-ERROR            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROC-PPAL.
      *-------------
           PERFORM XX-MUEVE-REG-CABECERA
           PERFORM CB-LEER-SUPVD02
           PERFORM CC-ACUMULA-P22-SUPER
              UNTIL (WR-FS-SUPVD02 NOT = 0 OR WS-PROCESO-ERROR)
           PERFORM CC-GENERA-P22-SUPER
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-SUPVD02.
      *----------------
           READ SUPVD02 END-READ.
           IF WR-FS-SUPVD02 = 0
              ADD 1                         TO WA-L-D02
           ELSE
           IF WR-FS-SUPVD02  NOT = 10
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'CB-LEER-SUPVD02 '       TO WR-PARRAFO
              MOVE 'SUPVD02     '           TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-SUPVD02            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       XX-MUEVE-REG-CABECERA.
      *----------------------
           MOVE '016'                       TO P22-COD-INS-FIN
           MOVE 'P22'                       TO P22-IDE-ARC
      * DPM ini  2-JUL-1998
           MOVE WP-SIG-PRO                  TO P22-SIG-PER.
      * DPM fin  2-JUL-1998
           MOVE WP-ANO-PRO                  TO P22-ANO-PER.
           MOVE WP-MES-PRO                  TO P22-MES-PER.
           MOVE SPACES                      TO P22-FILLER
           PERFORM XX-GRABA-SUPVP22.
           SKIP1
       CC-ACUMULA-P22-SUPER.
      *---------------------
           SET WI-PRO                       TO 1
           SEARCH WT-PRODUCTOS
             AT END MOVE 'NO'               TO WR-COD-PROD-OK
             WHEN (WT-COD-TCCO(WI-PRO) = D02-COD-TCCO)
                   MOVE 'SI'                TO WR-COD-PROD-OK
           END-SEARCH.

           IF (D02-IND-TDOP     = 'D' AND
               D02-NUM-DDIR     =  1  AND
               D02-IND-DCAS NOT = 'C' AND
               D02-VAL-SCON     >  0  AND
               WR-COD-PROD-OK   = 'SI')

               SET WI-IND                   TO 1
               SEARCH WT-ACUM-PROD
                AT END  DISPLAY 'ERROR INESPERADO EN PGM...'
                        PERFORM BC-DISPLAY-ERROR
                WHEN (WT-COD-PPRO(WI-IND) = SPACES)
                      MOVE WT-COD-PROD(WI-PRO) TO WT-COD-PPRO(WI-IND)
                WHEN (WT-COD-PPRO(WI-IND) = WT-COD-PROD(WI-PRO))
                      CONTINUE
               END-SEARCH

               IF (WS-PROCESO-OK)
                  ADD 1                     TO WT-NUM-OPER(WI-IND)
                  ADD D02-VAL-SCON          TO WT-VAL-SLDO(WI-IND)
                  ADD D02-VAL-SCVC      TO WT-VAL-VCDO(WI-IND)
                  IF (D02-NUM-AOTO = WP-ANO-PRO AND
                      D02-NUM-MOTO = WP-MES-PRO)
                      ADD 1                 TO WT-NUM-OMES(WI-IND)
                      ADD D02-VAL-SCON      TO WT-VAL-OMES(WI-IND)

                      MOVE WP-SIG-PRO       TO CALEAREA-FECHA-2-AM
                      MOVE D02-NUM-AOTO     TO CALEAREA-FECHA-2-AA
                      MOVE D02-NUM-MOTO     TO CALEAREA-FECHA-2-M
                      MOVE D02-NUM-DOTO     TO CALEAREA-FECHA-2-D
                      MOVE D02-NUM-AEXT     TO CALEAREA-FECHA-1-AA
                      MOVE D02-NUM-MEXT     TO CALEAREA-FECHA-1-M
                      MOVE D02-NUM-DEXT     TO CALEAREA-FECHA-1-D
      ****************IF (D02-NUM-AEXT > 90)
                      IF (D02-NUM-AEXT < ROLL-YY)
                          MOVE '20'         TO CALEAREA-FECHA-1-AM
                      ELSE
                          MOVE '19'         TO CALEAREA-FECHA-1-AM
                      END-IF
                      MOVE 'CDCEF '             TO CALEAREA-FUNCION
                      PERFORM BD-CALL-RUTUCALE
                      IF (CALEAREA-COD-RET NOT = 0)
                          DISPLAY '*** ERROR EN CALCULO DE PLAZO ***'
                          DISPLAY 'CALEAREA-FECHA-1 : ' CALEAREA-FECHA-1
                          DISPLAY 'CALEAREA-FECHA-2 : ' CALEAREA-FECHA-2
                          DISPLAY 'SE IGNORA REG D02 : ' D02
                      ELSE
                          COMPUTE WT-VAL-OMPN(WI-IND) =
                                  WT-VAL-OMPN(WI-IND)
                               +  (D02-VAL-SCON
                               *  CALEAREA-NUM-DIA)
                      END-IF
                  END-IF
               END-IF
           END-IF.
           PERFORM CB-LEER-SUPVD02.
           SKIP1
       CC-GENERA-P22-SUPER.
      *--------------------
           SET WI-IND                        TO 1
           PERFORM UNTIL (WI-IND > 100 OR WT-COD-PPRO(WI-IND) = '   ')
             MOVE WT-COD-PPRO(WI-IND)        TO P22-COD-PROD
             MOVE WT-NUM-OPER(WI-IND)        TO P22-NUM-OPER
             MOVE WT-NUM-OMES(WI-IND)        TO P22-NUM-OMES
             COMPUTE P22-VAL-SLDO ROUNDED =  WT-VAL-SLDO(WI-IND) * 1000
             COMPUTE P22-VAL-OMES ROUNDED =  WT-VAL-OMES(WI-IND) * 1000
             IF (WT-VAL-SLDO(WI-IND) = 0)
                 MOVE 0                      TO P22-PLZ-PROM
                 MOVE 0                      TO P22-PRC-CVEN
             ELSE
                 IF (WT-COD-PPRO(WI-IND) = '350' OR = '360' OR
                     WT-VAL-OMES(WI-IND) = 0)
                     MOVE 0                  TO P22-PLZ-PROM
                 ELSE
                     COMPUTE P22-PLZ-PROM ROUNDED = WT-VAL-OMPN(WI-IND)
                                                  / WT-VAL-OMES(WI-IND)
                 END-IF
                 COMPUTE P22-PRC-CVEN ROUNDED = WT-VAL-VCDO(WI-IND)
                                       * 100 /  WT-VAL-SLDO(WI-IND)
             END-IF
             PERFORM XX-GRABA-SUPVP22
             SET WI-IND                      UP BY 1
           END-PERFORM.
           SKIP1
       XX-GRABA-SUPVP22.
      *-----------------
           WRITE P22 END-WRITE.
           IF WR-FS-SUPVP22 = 0
              ADD 1                         TO WA-G-P22
           ELSE
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'XX-GRABA-SUPVP22'       TO WR-PARRAFO
              MOVE 'SUPVP22     '           TO WR-ARCHIVO
              MOVE 'WRITE      '            TO WR-OPERACI
              MOVE WR-FS-SUPVP22            TO WR-FILE-ST
              PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'ESTE PROCESO NO EMITE INFORME'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA SUPPGP22 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '    GENERA ARCHIVO D02 EN FORMATO SUPER'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'REG LEIDOS DE SUPVD02 : ' WA-L-D02
           DISPLAY 'REG GRABADOS  SUPVP22 : ' WA-G-P22
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY W-PARAMETROS
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SUPVD02 SUPVP22 SUPVTAR
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT


