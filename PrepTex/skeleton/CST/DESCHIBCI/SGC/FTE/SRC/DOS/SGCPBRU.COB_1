       IDENTIFICATION DIVISION.
      *========================
      *
       PROGRAM-ID.          SGCPBRU.
      *
      *AUTHOR.              BRUNILDA PARDO G.
      *
      *DATE-WRITTEN.        MAYO 1996.
      *
      *REMARKS.
      *
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      *****************************************************************
      *      SISTEMA    : CONTROL TARJETAS BCI-REDBANC.               *
      *                   -----------------------------               *
      *                                                               *
      *      OBJETIVO  : COMPARAR ARCHIVOS IBM/TANDEM PARA EL COBRO   *
      *                  DE COMISION CTAS PRIMAS, SI HAY DIFERENCIAS  *
      *                  SE ACTUALIZA TABLA OPE EN IBM.               *
      *      ARCHIVOS       :                                         *
      *                                                               *
      *           INPUT     : ARCHIVO CUENTAS PRIMAS TANDEM           *
      *                           (SECUENCIAL)                        *
      *           I-O       : TABLA OPE                               *
      *                                                               *
      *           SALIDA    :                                         *
      *                       DISPLAY TOTALES DE CONTROL              *
      *                       INFORME REGS.OPE ACTUALIZADOS           *
      *                                                               *
      *****************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
      *
       ENVIRONMENT DIVISION.
      *=====================

       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA
                        C01 IS CANAL-1.
      *
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *
      *
      *     ***  ARCHIVO MOVTOS TANDEM   (INPUT)         ***
      *                  (SECUENCIAL)                    ***
      *
           SELECT CCTOS1        ASSIGN TO UT-S-CCTOS1
                                ORGANIZATION SEQUENTIAL
                                FILE STATUS FS-CCTOS1.
      *
      *     ***   INFORME CTAS PRIMAS PERSONAS JURIDICAS   ***
      *
           SELECT INFORME1      ASSIGN   TO   INFORM1.
      *
      *     ***   INFORME CTAS PRIMAS PERSONAS NATURALES   ***
      *
           SELECT INFORME2      ASSIGN   TO   INFORM2.
      *
       DATA DIVISION.
      *==============
      *
       FILE SECTION.
      *-------------
      *
       FD  CCTOS1
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD STANDARD.
      *
       01  REG-CCTOS1.
           05  CCTOS1-CTA-PRIMA        PIC X(12).
           05  CCTOS1-TIPO-COM-IBM     PIC 9(2).
           05  CCTOS1-CTA-CARGO-IBM    PIC X(12).
           05  CCTOS1-TIPO-COM-TDM     PIC 9(2).
           05  CCTOS1-CTA-CARGO-TDM1.
               10  CCTOS-ZEROS         PIC X(4).
               10  CCTOS1-CTA-CARGO-TDM PIC 9(8).
           05  CCTOS1-TIPO-REG         PIC X.
      *
       FD  INFORME1
           LABEL RECORD    OMITTED
           RECORDING MODE  F
           RECORD CONTAINS 132 CHARACTERS.
      *
       01  REG-INFORME1.
           05  O-LINEA1                    PIC X(132).
      *
       FD  INFORME2
           LABEL RECORD    OMITTED
           RECORDING MODE  F
           RECORD CONTAINS 132 CHARACTERS.
      *
       01  REG-INFORME2.
           05  O-LINEA2                    PIC X(132).
      *
       WORKING-STORAGE SECTION.
      *-----------------------
      *
      *
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           02 FILLER               PIC X(32) VALUE 'TRBP98'.
      *
       01  REG-OPE.
      *    COPYDD DGC-R-OPE.DGC-E-OPE00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO OPE
           02  DGC-E-OPE00.
      *                             OPERACIONES
               03  OPE.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  OPE-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  OPE-KEY-IREG.
      *                             IDENTIFICACION OPERACION
                       05  OPE-KEY-IOPE.
      *                             COD. INT. COMP. OPERACION
                       06  OPE-CIC-IOPE.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  OPE-CAI-IOPE.
                       08  OPE-COD-TOPE
                                    PICTURE X(1).
                       08  OPE-COD-COPE
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  OPE-IIC-IOPE.
                       08  OPE-COD-OOPE.
                       09  OPE-COD-SOPE
                                    PICTURE X(2).
                       09  OPE-IND-SOPE
                                    PICTURE X(1).
                       08  OPE-GLS-NOPE
                                    PICTURE X(5).
      *                             TIME STAMP TRANSACCION
                       05  OPE-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  OPE-FEC-FTRN.
                       07  OPE-NUM-STRN
                                    PICTURE 9(2).
                       07  OPE-NUM-ATRN
                                    PICTURE 9(2).
                       07  OPE-NUM-MTRN
                                    PICTURE 9(2).
                       07  OPE-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  OPE-HRA-HRTR.
                       07  OPE-NUM-HHTR
                                    PICTURE 9(2).
                       07  OPE-NUM-MMTR
                                    PICTURE 9(2).
                       07  OPE-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  OPE-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  OPE-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  OPE-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  OPE-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  OPE-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  OPE-COD-ATRN PICTURE X(12).
      *                             CODIGO STATUS
                   04  OPE-COD-STAT PICTURE X(3).
      *                             VALOR DE CAMBIO OPERACION
                   04  OPE-COD-VCOP.
      *                             MISCELANEO TIPO VALOR
                       05  OPE-MSC-TVOP
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       05  OPE-COD-CVOP
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       05  OPE-COD-TCOP
                                    PICTURE X(2).
      *                             CODIGO CLASE OPERACION
                   04  OPE-COD-COPR.
      *                             CODIGO TIPO OPERACION
                       05  OPE-COD-TOPR
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR OPERACION
                       05  OPE-COD-AOPR
                                    PICTURE X(3).
      *                             CODIGO OFICINA
                   04  OPE-COD-OFOP PICTURE X(3).
      *                             EJECUTIVO
                   04  OPE-COD-EJOP PICTURE X(12).
      *                             FECHA INICIO OPERACION
                   04  OPE-FEC-FIOP.
                       05  OPE-NUM-SIOP
                                    PICTURE 9(2).
                       05  OPE-NUM-AIOP
                                    PICTURE 9(2).
                       05  OPE-NUM-MIOP
                                    PICTURE 9(2).
                       05  OPE-NUM-DIOP
                                    PICTURE 9(2).
      *                             FECHA TERMINO OPERACION
                   04  OPE-FEC-FTOP.
                       05  OPE-NUM-STOP
                                    PICTURE 9(2).
                       05  OPE-NUM-ATOP
                                    PICTURE 9(2).
                       05  OPE-NUM-MTOP
                                    PICTURE 9(2).
                       05  OPE-NUM-DTOP
                                    PICTURE 9(2).
      *                             MOTIVO TERMINO OPERACION
                   04  OPE-COD-MOTT PICTURE X(2).
      *                             FECHA REAPERTURA OPERACION
                   04  OPE-FEC-FROP.
                       05  OPE-NUM-SROP
                                    PICTURE 9(2).
                       05  OPE-NUM-AROP
                                    PICTURE 9(2).
                       05  OPE-NUM-MROP
                                    PICTURE 9(2).
                       05  OPE-NUM-DROP
                                    PICTURE 9(2).
      *                             IND MULTIPLES CLIENTES ( Y, O, ETC)
                   04  OPE-IND-MCLI PICTURE X(2).
      *                             CODIGO TIPO DE COMISION
                   04  OPE-COD-TCOM PICTURE X(2).
      *                             COD. INT. COMP. OPERACION DE CARGO
                   04  OPE-CIC-ICAR.
      *                             CENTRO ASIGNACION OPERACION CARGO
                       05  OPE-CAI-ICAR
                                    PICTURE X(4).
      *                             IDENT. INT. COMP. OPERACION CARGO
                       05  OPE-IIC-ICAR
                                    PICTURE X(8).
      *
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  REQ-AREA-OPE.
           02 REQ-COMAN-OPE        PIC X(05).
           02 REQ-TBLNM-OPE        PIC X(03)  VALUE 'OPE'.
           02 REQ-KEYNM-OPE        PIC X(05)  VALUE 'OPE00'.
           02 REQ-RTNCD-OPE        PIC X(02)  VALUE SPACES.
           02 REQ-INRCD-OPE        PIC X(01).
           02 REQ-DBIDE-OPE        PIC S9(4)  COMP VALUE +100.
           02 REQ-TBLID-OPE        PIC X(07).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-OPE        PIC S9(4)  COMP.
           02 REQ-IOCNT-OPE        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-OPE.
              03 REQ-OPE-CIC-IOPE  PIC 9(12).
      *
       01  ELM-LIS-OPE.
           03 ELEM-OPE             PIC X(05)  VALUE 'OPE00'.
           03 ELSE-OPE             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.
      *
      *   ***   REGISTROS DE TRABAJO    ***
      *
       01  WR-CTA-PRIMA-WORK           PIC 9(12).
       01  WR-CTA-PRIMA-WORK-R REDEFINES WR-CTA-PRIMA-WORK.
           05  FILLER                  PIC 9(4).
           05  WR-CUENTA-PRIMA         PIC 9(8).
               88  CUENTA-PRIMA            VALUE 98200000 THRU 99999999.
               88  CUENTA-PRIMA-REMU       VALUE 98200000 THRU 98399999.
               88  CUENTA-PRIMA-TRAD       VALUE 98400000 THRU 99999999.
      *
       01  WR-CLAVE-OPE.
           05  WR-FILLER-OPE           PIC X(4)   VALUE '0000'.
           05  WR-CTACTE-OPE           PIC 9(8).
      *
       01  WR-FECHA-SYS.
           03 WR-FECHA-SYS-DD              PIC 9(02).
           03 WR-FECHA-SYS-MM              PIC 9(02).
      *****03 WR-FECHA-SYS-SS              PIC 9(02)  VALUE 19.
           03 WR-FECHA-SYS-SS              PIC 9(02).
           03 WR-FECHA-SYS-AA              PIC 9(02).
      *
       01  WR-FECHA-NUMERIC1               PIC 9(8).
      *
       01  WR-FECHA-DATE.
           05  WR-FECHA-DATE-AA            PIC 9(2).
           05  WR-FECHA-DATE-MM            PIC 9(2).
           05  WR-FECHA-DATE-DD            PIC 9(2).
      *
       01  WR-CURRENT-DATE.
           05  WR-DD-CURR              PIC 9(2).
           05  FILLER                  PIC X          VALUE '/'.
           05  WR-MM-CURR              PIC 9(2).
           05  FILLER                  PIC X          VALUE '/'.
           05  WR-AA-CURR              PIC 9(2).
      *
       01  WR-TIME-OF-DAY.
           05  WR-HH-TIME              PIC 9(2).
           05  WR-MM-TIME              PIC 9(2).
           05  WR-SS-TIME              PIC 9(2).
      *
       01  WR-TIME-TRANS.
           05  WR-HH-TRANS             PIC 9(2).
           05  WR-MM-TRANS             PIC 9(2).
           05  WR-SS-TRANS             PIC 9(2).
      *
      *
       01  WR-HORA-IMPRES.
           05  WR-HH-IMPRES            PIC 9(2).
           05  FILLER                  PIC X          VALUE '.'.
           05  WR-MM-IMPRES            PIC 9(2).
           05  FILLER                  PIC X          VALUE '.'.
           05  WR-SS-IMPRES            PIC 9(2).
      *
           COPY RTNWCALE.
      *
       01  WR-REGISTRO-LINEA           PIC X(132).
      *
      *
      * ***       PARAMETROS DE ENTRADA   ***
      *
       01  WP-PARAMETROS.
           05  FILLER                  PIC X(8).
           05 WP-FECHA-PROC.
              10  WP-DD-PROC           PIC 99.
              10  WP-MM-PROC           PIC 99.
              10  WP-SS-PROC           PIC 99.
              10  WP-AA-PROC           PIC 99.
      *
           05  WR-FECHA-TRB.
               10  WR-DD-TRB            PIC 99.
               10  WR-MM-TRB            PIC 99.
               10  WR-SS-TRB            PIC 99.
               10  WR-AA-TRB            PIC 99.
           05  WR-FECHA-TRB-NUMERIC-R  REDEFINES WR-FECHA-TRB.

               10  WR-FECHA-TRB-NUMERIC PIC 9(8).
      *
      *      ***   CAMPOS DE TRABAJO    ***
      *
       01  WR-CAMPOS-DE-TRABAJO.
      *-------------------------
      *
           05  WR-SW-ERR1              PIC 9     VALUE 0.
           05  WR-SUC-ANT              PIC 9(3).
           05  WR-RUT-TITULAR-ANT      PIC 9(8).
           05  WR-RUT-USUARIO-ANT      PIC 9(8).
           05  WR-CONT-LINEAS1         PIC 9(2)  VALUE 60.
           05  WR-CONT-LINEAS2         PIC 9(2)  VALUE 60.
           05  WR-TOT-PAG1             PIC 9(2)  VALUE 0.
           05  WR-TOT-PAG2             PIC 9(2)  VALUE 0.
           05  WR-SW-IMPRESION         PIC 9     VALUE 0.
           05  WR-SECCION              PIC X(25).
           05  WR-PARRAFO              PIC X(25).
           05  WR-OPERACION            PIC X(25).
           05  WR-CODIGO               PIC X(25).
      *
       01  W-ACUMULADORES.
      *-------------------
      *
           05  WA-CONT1                PIC 9(11).
           05  WA-CONT2                PIC 9(11).
           05  WA-TOTAL-IMPRESOS       PIC 9(11).
           05  WA-TOTAL-NO-IMPRESOS    PIC 9(11).
           05  WA-TOTAL-LEID-CCTOS1    PIC 9(11).
           05  WA-TOTAL-NOSELECC       PIC 9(11).
           05  WA-TOTAL-SELECC         PIC 9(11).
           05  WA-TOTAL-ACT-OPE        PIC 9(11).
           05  WA-TOTAL-ACT-PRIMA      PIC 9(11).
           05  WA-TOTAL-ACT-CARGOS     PIC 9(11).
      *
      *   ***      SWITCHES  DE PROCESO     ****
      *
       01  WS-SWITCHES.
           05  WS-HAY-MENSAJES       PIC X(2).
               88  HAY-MENSAJES                 VALUE 'SI'.
           05  WS-ACCESO-OPE         PIC X(2).
               88  ACCESO-OPE                   VALUE 'SI'.
           05  WS-TERMINO-ANORMAL    PIC X(2).
               88  TERMINO-ANORMAL              VALUE 'SI'.
           05  WS-EOF-CCTOS1         PIC X(2).
               88  EOF-CCTOS1                   VALUE 'SI'.
           05  WS-IMPRESION          PIC X(2).
               88  IMPRESION-OK                 VALUE 'SI'.
           05  WS-CODIGO-ERROR       PIC X(2).
               88  CODIGO-ERROR                 VALUE 'SI'.
      *
      *   ***          TITULO    DEL INFORME             ***
      *
       01  WT-TITULOS.
           05  WT-TITULO1.
               10  FILLER              PIC X(2)       VALUE SPACES.

               10  FILLER              PIC X(25)      VALUE
                   'BANCO CREDITO INVERSIONES'.
               10  FILLER              PIC X(87)      VALUE SPACES.

               10  FILLER              PIC X(9)       VALUE
                   'FECHA  : '.
               10  WT-TITULO1-FECHA.
                   15  WT-TIT1-DD      PIC 9(2).
                   15  FILLER          PIC X          VALUE '/'.
                   15  WT-TIT1-MM      PIC 9(2).
                   15  FILLER          PIC X          VALUE '/'.
                   15  WT-TIT1-AA      PIC 9(2).
      *
           05  WT-TITULO1A.
               10  FILLER              PIC X(114)     VALUE SPACES.

               10  FILLER              PIC X(9)       VALUE
                   'HORA   : '.
               10  WT-TITULO1A-HORA.
                   15  WT-TIT1A-HH     PIC 9(2).
                   15  FILLER          PIC X          VALUE '.'.
                   15  WT-TIT1A-MM     PIC 9(2).
                   15  FILLER          PIC X          VALUE '.'.
                   15  WT-TIT1A-SS     PIC 9(2).
      *
           05  WT-TITULO2.
               10  FILLER              PIC X(2)       VALUE SPACES.
               10  FILLER              PIC X(25)      VALUE
                   'SISTEMA TARJETA DEBITO   '.
               10  FILLER              PIC X(87)      VALUE SPACES.
               10  FILLER              PIC X(9)       VALUE
                   'PAGINA : '.
               10  WT-TITULO2-PAG.
                   15  WT-TIT2-PAG     PIC ZZZZZ.
      *
           05  WT-TITULO3.
               10  FILLER              PIC X(25)       VALUE SPACES.
               10  FILLER              PIC X(60)      VALUE
           'ACTUALIZACION TABLA OPE CON LAS DIFERENCIAS TANDEM     '.
               10  FILLER              PIC X(48)      VALUE SPACES.

      *
           05  WT-TITULO4.
               10  FILLER              PIC X(25)       VALUE SPACES.
               10  FILLER              PIC X(60)      VALUE
           '---------------------------------------------------    '.
               10  FILLER              PIC X(48)      VALUE SPACES.
      *
           05  WT-TITULO3A.
               10  FILLER              PIC X(25)       VALUE SPACES.
               10  FILLER              PIC X(60)      VALUE
           'ACTUALIZACION TABLA OPE CON LAS DIFERENCIAS TANDEM     '.
               10  FILLER              PIC X(48)      VALUE SPACES.

      *
           05  WT-TITULO4A.
               10  FILLER              PIC X(25)       VALUE SPACES.
               10  FILLER              PIC X(60)      VALUE
           '---------------------------------------------------    '.
               10  FILLER              PIC X(48)      VALUE SPACES.
      *
           05  WT-TITULO5.
               10  FILLER              PIC X(40) VALUE SPACES.
               10  FILLER              PIC X(12) VALUE
                   'CUENTA PRIMA'.
               10  FILLER              PIC X(6)   VALUE SPACES.
               10  FILLER              PIC X(14)  VALUE
                   'TIPO COM.PRIMA'.
               10  FILLER              PIC X(3)   VALUE SPACES.
               10  FILLER              PIC X(12)  VALUE
                   'CUENTA CARGO'.
               10  FILLER              PIC X(4)   VALUE SPACES.
               10  FILLER              PIC X(13)  VALUE
                   'TIPO COM.CARG'.
      *
           05  WT-TITULO5A.
               10  FILLER              PIC X(20) VALUE SPACES.
               10  FILLER              PIC X(12) VALUE
                   'CUENTA PRIMA'.
               10  FILLER              PIC X(6)   VALUE SPACES.
               10  FILLER              PIC X(14)  VALUE
                   'TIPO COM.IBM  '.
               10  FILLER              PIC X(3)   VALUE SPACES.
               10  FILLER              PIC X(17)  VALUE
                   'CUENTA CARGOS IBM'.
               10  FILLER              PIC X(4)   VALUE SPACES.
               10  FILLER              PIC X(13)  VALUE
                   'TIPO COM.TDM '.
               10  FILLER              PIC X(3)   VALUE SPACES.
               10  FILLER              PIC X(17)  VALUE
                   'CUENTA CARGOS TDM'.
      *
           05  WT-TITULO9.
               10  WT-TIT9-LINEA       PIC X(132).
      *
       01  WS-DETALLE1.
           05  FILLER              PIC X(40) VALUE SPACES.
           05  WS-DET1-CTA-PRI     PIC 9(8).
           05  FILLER              PIC X(10)  VALUE SPACES.
           05  WS-DET1-COM-PRI     PIC X(5).
           05  FILLER              PIC X(10)  VALUE SPACES.
           05  WS-DET1-CTA-CARGO   PIC 9(12).
           05  FILLER              PIC X(10)  VALUE SPACES.
           05  WS-DET1-COM-CARG    PIC X(5).
      *
       01  WS-DETALLE2.
           05  FILLER              PIC X(20) VALUE SPACES.
           05  WS-DET2-CTA-PRI     PIC 9(8).
           05  FILLER              PIC X(10)  VALUE SPACES.
           05  WS-DET2-COM-IBM     PIC X(5).
           05  FILLER              PIC X(10)  VALUE SPACES.
           05  WS-DET2-CARGO-IBM   PIC 9(12).
           05  FILLER              PIC X(10)  VALUE SPACES.
           05  WS-DET2-COM-TDM     PIC X(5).
           05  FILLER              PIC X(10)  VALUE SPACES.
           05  WS-DET2-CARGO-TDM   PIC 9(12).
      *
       01  FILES-STATUS.
           05 FS-CCTOS1              PIC 9(2)     VALUE 0.
      *
           COPY RLOVR001.
      *
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
      *
       A-CONTROL                       SECTION.
      *---------
      *
       AA-INICIALIZACION.
      *------------------
           PERFORM B-INICIALIZAR.
      *
       AB-PROCESAMIENTO.
      *-----------------
           IF  NOT TERMINO-ANORMAL
               PERFORM C-PROCESAR
           END-IF.
      *
       AC-TERMINACION.
      *---------------
           PERFORM D-TERMINAR.
      *
       AZ-FIN-DE-SECCION.
      *------------------
           GOBACK.
      *
       B-INICIALIZAR  SECTION.
      *=======================
      *
       BA-CONTROL.
      *-----------
           PERFORM BB-LEER-PARAMETRO.
           IF  NOT TERMINO-ANORMAL
               PERFORM BC-ABRIR-ARCHIVOS
               IF  NOT TERMINO-ANORMAL
                   PERFORM BD-INICIALIZA-ACUM.
           GO TO BZ-FIN-DE-SECCION.
      *
       BB-LEER-PARAMETRO.
      *------------------
      *
           ACCEPT WR-FECHA-DATE FROM DATE.
           MOVE WR-FECHA-DATE-DD           TO   WR-FECHA-SYS-DD.
           MOVE WR-FECHA-DATE-MM           TO   WR-FECHA-SYS-MM.
           MOVE WR-FECHA-DATE-AA           TO   WR-FECHA-SYS-AA.
           IF WR-FECHA-SYS-AA < ROLL-YY
              MOVE 20                      TO   WR-FECHA-SYS-SS
           ELSE
              MOVE 19                      TO   WR-FECHA-SYS-SS
           END-IF.
           ACCEPT WR-TIME-OF-DAY FROM TIME.
           MOVE WR-HH-TIME   TO   WT-TIT1A-HH.
           MOVE WR-MM-TIME   TO   WT-TIT1A-MM.
           MOVE WR-SS-TIME   TO   WT-TIT1A-SS.
           ACCEPT WP-PARAMETROS.
           IF WP-FECHA-PROC NOT NUMERIC
              DISPLAY '***'
              DISPLAY '*** ERROR :'
              DISPLAY '*** FECHA INGRESADA EN PARAMETRO    '
              DISPLAY '*** NOT NUMERICA....:' WP-FECHA-PROC
              DISPLAY '*** PROCESO NO CONTINUA *** '
              MOVE  'B-INICIALIZAR'      TO   WR-SECCION
              MOVE  'BB-LEER-PARAMETRO'  TO   WR-PARRAFO
              MOVE  'VALIDACION PARAM '  TO   WR-OPERACION
              SET TERMINO-ANORMAL  TO  TRUE
           ELSE
              MOVE WP-AA-PROC  TO   WT-TIT1-AA
              MOVE WP-MM-PROC  TO   WT-TIT1-MM
              MOVE WP-DD-PROC  TO   WT-TIT1-DD
           END-IF.
      *
       BD-INICIALIZA-ACUM.
      *-------------------
      *
           MOVE ZEROS    TO   W-ACUMULADORES.
           MOVE 'NO'     TO   WS-EOF-CCTOS1
                              WS-ACCESO-OPE
                              WS-CODIGO-ERROR
                              WS-TERMINO-ANORMAL
                              WS-IMPRESION.
      *
       BBA-VALIDAR-FPRO.
      *-----------------
      *
           MOVE WP-SS-PROC TO CALEAREA-FECHA-1-AM.
           MOVE WP-AA-PROC TO CALEAREA-FECHA-1-AA.
           MOVE WP-MM-PROC TO CALEAREA-FECHA-1-M.
           MOVE WP-DD-PROC TO CALEAREA-FECHA-1-D.
           MOVE 'INFO'    TO CALEAREA-FUNCION.
           CALL 'RUTUCALE' USING RTNWCALE.
           IF CALEAREA-COD-RET NOT = 0
              DISPLAY ' '
              DISPLAY '*****************************************'
              DISPLAY '**   FECHA EN PARAMETRO INVALIDA **     *'
              DISPLAY '**   PROCESO NO CONTINUA         **     *'
              DISPLAY '**   FECHA INGRESADA =           **     *'
                      WP-FECHA-PROC
              DISPLAY '*****************************************'
              DISPLAY ' '
              MOVE  'B-INICIALIZAR'      TO   WR-SECCION
              MOVE  'BBA-VALIDAR-FPRO'   TO   WR-PARRAFO
              MOVE  'VALIDACION FECHA '  TO   WR-OPERACION
              SET TERMINO-ANORMAL  TO  TRUE
           END-IF.
      *
       BC-ABRIR-ARCHIVOS.
      *------------------
      *
           OPEN INPUT  CCTOS1
                OUTPUT INFORME1
                       INFORME2.
      *
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
      *
       C-PROCESAR                      SECTION.
      *----------
      *
           PERFORM CA-LEE-CCTOS1.
           IF  TERMINO-ANORMAL
               DISPLAY '** ERROR AL LEER EL PRIMER REGISTRO **'
               DISPLAY '** PROCESO NO CONTINUA **'
           ELSE
               PERFORM CB-PROCESA-CCTOS1  UNTIL  EOF-CCTOS1 OR
                       TERMINO-ANORMAL.
           GO TO CB-PROCESAR-EXIT.
      *
       CA-LEE-CCTOS1.
      *--------------
      *
           READ CCTOS1    AT END MOVE 'SI'   TO   WS-EOF-CCTOS1.
           IF  NOT EOF-CCTOS1
               ADD 1 TO WA-TOTAL-LEID-CCTOS1
           END-IF.
      *
       CB-PROCESA-CCTOS1.
      *------------------
      *
           PERFORM CC-SELECC-RECORD.
           PERFORM CA-LEE-CCTOS1.
      *
       CC-SELECC-RECORD.
      *-----------------
      *
           IF  CCTOS1-TIPO-REG = 'D'
               ADD 1         TO   WA-TOTAL-SELECC
               PERFORM CF-CHEQUEA-DIFF
           ELSE
               ADD 1         TO   WA-TOTAL-NOSELECC
           END-IF.
      *
       CD-IMPRIME-REG.
      *---------------
      *
           MOVE CCTOS1-CTA-PRIMA     TO   WS-DET1-CTA-PRI.
           MOVE '99'                 TO   WS-DET1-COM-PRI.
           MOVE CCTOS1-CTA-CARGO-TDM TO   WS-DET1-CTA-CARGO.
           MOVE CCTOS1-TIPO-COM-TDM  TO   WS-DET1-COM-CARG.
           MOVE ZEROS                TO   WR-SW-IMPRESION
           MOVE WS-DETALLE1          TO   REG-INFORME1.
           PERFORM CCW-WRITE-REG1.
           ADD 1                     TO   WA-TOTAL-IMPRESOS.
      *
       CDA-IMPRIME-REG2.
      *-----------------
      *
           MOVE CCTOS1-CTA-PRIMA      TO   WS-DET2-CTA-PRI.
           MOVE CCTOS1-TIPO-COM-IBM   TO   WS-DET2-COM-IBM.
           MOVE CCTOS1-CTA-CARGO-IBM  TO   WS-DET2-CARGO-IBM.
           MOVE CCTOS1-TIPO-COM-TDM   TO   WS-DET2-COM-TDM.
           MOVE CCTOS1-CTA-CARGO-TDM  TO   WS-DET2-CARGO-TDM.
           MOVE ZEROS                 TO   WR-SW-IMPRESION
           MOVE WS-DETALLE2           TO   REG-INFORME2.
           PERFORM CCW-WRITE-REG2.
      *
       CE-ACCESA-OPE.
      *---------------
      *
           MOVE 'NO'                 TO    WS-ACCESO-OPE.
           MOVE 'RDUKY'              TO    REQ-COMAN-OPE.
           CALL 'DBNTRY' USING UIB REQ-AREA-OPE REG-OPE ELM-LIS-OPE.
           IF REQ-RTNCD-OPE NOT = SPACES AND
              REQ-RTNCD-OPE NOT = '14'
              MOVE 'C-PROCESO           '     TO WR-SECCION
              MOVE 'CE-ACCESA-OPE       '     TO WR-PARRAFO
              MOVE ' REQ-COMAN-OPE      '     TO WR-OPERACION
              MOVE ' REQ-RTNCD-OPE      '     TO WR-CODIGO
              SET TERMINO-ANORMAL             TO TRUE
           ELSE
              IF REQ-RTNCD-OPE = '14'
                 DISPLAY '** CTA PRIMA NO EXISTE TABLA OPE ** '
                          CCTOS1-CTA-PRIMA
                 DISPLAY '** PROCESO CONTINUA ** '
                 MOVE 'NO' TO WS-ACCESO-OPE
              ELSE
                 IF REQ-RTNCD-OPE = SPACES
                    MOVE 'SI' TO WS-ACCESO-OPE
              END-IF
           END-IF.
      *
       CEA-ACT-CTA-CARGO.
      *------------------
           MOVE OPE-IIC-ICAR            TO    WR-CTACTE-OPE.
           MOVE WR-CLAVE-OPE            TO    REQ-OPE-CIC-IOPE.
           PERFORM CE-ACCESA-OPE.
           IF  NOT TERMINO-ANORMAL AND ACCESO-OPE
               MOVE CCTOS1-TIPO-COM-TDM TO   OPE-COD-TCOM
               PERFORM CG-ACTUALIZA-OPE
               ADD 1                    TO   WA-TOTAL-ACT-CARGOS
           END-IF.
      *
       CF-CHEQUEA-DIFF.
      *----------------
           IF (CCTOS1-TIPO-COM-IBM NOT = 99  OR
                            ((CCTOS1-TIPO-COM-IBM = 99) AND
              (CCTOS1-CTA-CARGO-IBM = ZEROS OR SPACES))) AND
              (CCTOS1-CTA-CARGO-TDM NOT = ZEROS AND NOT = SPACES)
                   MOVE CCTOS1-CTA-PRIMA     TO    WR-CTACTE-OPE
                   MOVE WR-CLAVE-OPE         TO    REQ-OPE-CIC-IOPE
                   PERFORM CE-ACCESA-OPE
                   IF NOT TERMINO-ANORMAL AND ACCESO-OPE
                      PERFORM CGA-MUEVE-CAMPOS-TDM
                      PERFORM CG-ACTUALIZA-OPE
                      IF NOT TERMINO-ANORMAL
                         PERFORM CEA-ACT-CTA-CARGO
                         PERFORM CD-IMPRIME-REG
                      END-IF
                   END-IF
           END-IF.
           IF (CCTOS1-TIPO-COM-IBM NOT = 99 AND (CCTOS1-CTA-CARGO-TDM =
               ZEROS OR SPACES))
               MOVE CCTOS1-CTA-PRIMA     TO    WR-CTACTE-OPE
               MOVE WR-CLAVE-OPE         TO    REQ-OPE-CIC-IOPE
               PERFORM CE-ACCESA-OPE
               IF NOT TERMINO-ANORMAL AND ACCESO-OPE
                  MOVE CCTOS1-TIPO-COM-TDM TO   OPE-COD-TCOM
                  PERFORM CG-ACTUALIZA-OPE
                  PERFORM CDA-IMPRIME-REG2
                  ADD 1                    TO   WA-TOTAL-NO-IMPRESOS
               END-IF
            END-IF.
      *
       CG-ACTUALIZA-OPE.
      *-----------------
           MOVE 'SGCPBRU'                     TO   OPE-COD-ATRN.
           MOVE 'UPDAT'                       TO   REQ-COMAN-OPE.
           CALL 'DBNTRY' USING UIB REQ-AREA-OPE REG-OPE ELM-LIS-OPE.
           IF REQ-RTNCD-OPE NOT = SPACES AND
              REQ-RTNCD-OPE NOT = '14'
              MOVE 'C-PROCESO           '     TO   WR-SECCION
              MOVE 'CCB-BUSCAR-OPE          ' TO   WR-PARRAFO
              MOVE REQ-COMAN-OPE              TO   WR-OPERACION
              MOVE REQ-RTNCD-OPE              TO   WR-CODIGO
              SET TERMINO-ANORMAL             TO   TRUE
           ELSE
              IF REQ-RTNCD-OPE = SPACES
                 ADD 1                        TO WA-TOTAL-ACT-OPE
           END-IF.
      *
       CGA-MUEVE-CAMPOS-TDM.
      *--------------------
           MOVE '99'                   TO   OPE-COD-TCOM.
           MOVE ZEROS                  TO   OPE-CIC-ICAR.
           MOVE CCTOS1-CTA-CARGO-TDM   TO   OPE-IIC-ICAR.
           ADD 1                       TO   WA-TOTAL-ACT-PRIMA.
      *
       CCT-TITULOS1.
      *------------
      *
           MOVE ALL '-'                TO   WT-TIT9-LINEA.
           ADD 1                       TO   WR-TOT-PAG1.
           MOVE WR-TOT-PAG1            TO   WT-TIT2-PAG.
           MOVE ZEROS                  TO   WR-CONT-LINEAS1.
           MOVE  1                     TO   WR-SW-IMPRESION.
           MOVE WT-TITULO1             TO   REG-INFORME1.
           PERFORM CCW-WRITE-REG1.
           MOVE  0                     TO   WR-SW-IMPRESION.
           MOVE WT-TITULO2             TO   REG-INFORME1.
           PERFORM CCW-WRITE-REG1.
           MOVE WT-TITULO3             TO   REG-INFORME1.
           PERFORM CCW-WRITE-REG1.
           MOVE WT-TITULO4             TO   REG-INFORME1.
           PERFORM CCW-WRITE-REG1.
           MOVE WT-TITULO5             TO   REG-INFORME1.
           PERFORM CCW-WRITE-REG1.
           MOVE WT-TITULO9             TO   REG-INFORME1.
           PERFORM CCW-WRITE-REG1.
      *
       CCW-WRITE-REG1.
      *--------------
      *
           IF  WR-CONT-LINEAS1 > 56
               MOVE REG-INFORME1        TO   WR-REGISTRO-LINEA
               PERFORM CCT-TITULOS1
               MOVE WR-REGISTRO-LINEA   TO   REG-INFORME1.
           IF  WR-SW-IMPRESION = 1
               WRITE REG-INFORME1 AFTER ADVANCING CANAL-1
           ELSE
               WRITE REG-INFORME1 AFTER ADVANCING 1 LINE
           END-IF.
           ADD 1                        TO   WR-CONT-LINEAS1.
           MOVE SPACES                  TO   O-LINEA1.
      *
       CCT-TITULOS2.
      *------------
      *
           MOVE ALL '-'                TO   WT-TIT9-LINEA.
           ADD 1                       TO   WR-TOT-PAG2.
           MOVE WR-TOT-PAG2            TO   WT-TIT2-PAG.
           MOVE ZEROS                  TO   WR-CONT-LINEAS2.
           MOVE  1                     TO   WR-SW-IMPRESION.
           MOVE WT-TITULO1             TO   REG-INFORME2.
           PERFORM CCW-WRITE-REG2.
           MOVE  0                     TO   WR-SW-IMPRESION.
           MOVE WT-TITULO2             TO   REG-INFORME2.
           PERFORM CCW-WRITE-REG2.
           MOVE WT-TITULO3             TO   REG-INFORME2.
           PERFORM CCW-WRITE-REG2.
           MOVE WT-TITULO4             TO   REG-INFORME2.
           PERFORM CCW-WRITE-REG2.
           MOVE WT-TITULO5A            TO   REG-INFORME2.
           PERFORM CCW-WRITE-REG2.
           MOVE WT-TITULO9             TO   REG-INFORME2.
           PERFORM CCW-WRITE-REG2.
      *
       CCW-WRITE-REG2.
      *--------------
      *
           IF  WR-CONT-LINEAS2 > 56
               MOVE REG-INFORME2        TO   WR-REGISTRO-LINEA
               PERFORM CCT-TITULOS2
               MOVE WR-REGISTRO-LINEA   TO   REG-INFORME2.
           IF  WR-SW-IMPRESION = 1
               WRITE REG-INFORME2 AFTER ADVANCING CANAL-1
           ELSE
               WRITE REG-INFORME2 AFTER ADVANCING 1 LINE
           END-IF.
           ADD 1                        TO   WR-CONT-LINEAS2.
           MOVE SPACES                  TO   O-LINEA2.
      *
       CB-PROCESAR-EXIT.
      *----------------
           EXIT.
      *
       D-TERMINAR                      SECTION.
      *==========
       DA-CONTROL.
      *----------
           PERFORM DC-CERRAR-ARCHIVOS.
           PERFORM DD-FIN-PROGRAMA.
           GO TO DZ-FIN-DE-SECCION.
      *
       DC-CERRAR-ARCHIVOS.
      *------------------
           CLOSE CCTOS1
                 INFORME1
                 INFORME2.
      *
       DD-FIN-PROGRAMA.
      *---------------
      *
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+     T O T A L E S  D E  C O N T R O L     +'
           DISPLAY '+-------------------------------------------+'
           DISPLAY '** TOTAL REG.LEID. CCTOS1        = '
                       WA-TOTAL-LEID-CCTOS1.
           DISPLAY '** TOTAL REG.SELECC COD. D       = '
                       WA-TOTAL-SELECC.
           DISPLAY '** TOTAL REG.NO SELECCIONADOS    = '
                       WA-TOTAL-NOSELECC.
           DISPLAY '** TOTAL REG.ACTUALIZADOS OPE    = '
                       WA-TOTAL-ACT-OPE.
           DISPLAY '** TOTAL REGS.ACT.CTA PRIMA      = '
                       WA-TOTAL-ACT-PRIMA.
           DISPLAY '** TOTAL REGS.ACT.CARGOS         = '
                       WA-TOTAL-ACT-CARGOS.
           DISPLAY '** TOTAL REGS.ACT.TDM SPACES     = '
                       WA-TOTAL-NO-IMPRESOS.
           DISPLAY ' '
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+ MENSAJES PARA EL EXPLOTADOR               +'
           DISPLAY '+-------------------------------------------+'
           IF TERMINO-ANORMAL OR CODIGO-ERROR
              MOVE 100 TO RETURN-CODE
              DISPLAY ' HAY MENSAJES PARA EL EXPLOTADOR'
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '************************************************'
              DISPLAY '************************************************'
              DISPLAY '****                                        ****'
              DISPLAY '****      TERMINO ERRONEO DEL PROCESO       ****'
              DISPLAY '****                                        ****'
              DISPLAY '************************************************'
              DISPLAY '************************************************'
              DISPLAY 'EL ERROR FUE EN LA SECCION   ' WR-SECCION
              DISPLAY '                   PARRAFO   ' WR-PARRAFO
              DISPLAY '                   OPERACION ' WR-OPERACION
              DISPLAY  '                  CODIGO    ' WR-CODIGO
           ELSE
              DISPLAY ' NO HAY MENSAJES PARA EL EXPLOTADOR'
              END-IF
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL ***'.
      *
       DZ-FIN-DE-SECCION.
      *-----------------
           EXIT.
      *
      *************   FIN PROGRAMA SGCPBRU    *****************
      *

