       IDENTIFICATION DIVISION.
      *========================
      *
       PROGRAM-ID.          SGCPBRD2.
      *
      *AUTHOR.              BRUNILDA PARDO G.
      *
      *DATE-WRITTEN.        ENERO 1997.
      *
      *REMARKS.
      *
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      *****************************************************************
      *      SISTEMA    :  SISTEMA GENERAL DE CLIENTES (SGC).         *
      *                   -----------------------------------         *
      *                                                               *
      *      OBJETIVO   : ACTUALIZAR ARCHIVO RUTS DUPLICADOS,         *
      *                   ACCESANDO LAS SIGUIENTES TABLAS.            *
      *                                                               *
      *      ANALISTA   : G. URETA Q.                                 *
      *                                                               *
      *      INPUT      :                                             *
      *                   -COLOCACIONES      BD 160 TABLA RDC         *
      *                   -LINEA  DE CREDITO BD 260 TABLA RDC         *
      *                   -GARANTIAS         BD 109 TABLA GDD         *
      *                   -DEPOSITOS A PLAZO BD 370 TABLA RCC         *
      *                   -GESTION           BD 362 TABLA RCO         *
      *                   -CUENTAS           KSDS DESDE TANDEM.       *
      *                   -RUTS DUPLICADOS GENERADO POR LA APLICACION *
      *                    SGCPBRD1.                                  *
      *                                                               *
      *                                                               *
      *      SALIDA     : ARCHIVO CON RUTS DUPLICADOS ACTUALIZADO     *
      *                                                               *
      *      PROCESO    : SE LEE ARCHIVO KSDS RDO001 EN FORMA         *
      *                   SECUENCIAL, SE ACCESAN LAS TABLAS ARRIBA    *
      *                   MENCIONADAS Y SE VA CARGANDO LA TABLA       *
      *                   DEFINIDA EN EL REGISTRO RDO001, SE ACCESAN  *
      *                   LAS DISTINTAS TABLAS CON EL CIC DEL CLIENTE.*
      *                   LA POSICION DENTRO DE LA TABLA PARA LOS     *
      *                   DISTINTOS SISTEMAS ES LA SIGUIENTE :        *
      *                                                               *
      *                   POS 1 "COL" (COLOCACIONES).                 *
      *                   POS 2 "LDC" (LINEA DE CREDITO).             *
      *                   POS 3 "GAR" (GARANTIAS).                    *
      *                   POS 4 "DAP" (DEPOSITOS A PLAZO).            *
      *                   POS 5 "GES" (GESTION).                      *
      *                   POS 6 "CCT" (CUENTAS CORRIENTES).           *
      *                                                               *
      *****************************************************************
      *
       ENVIRONMENT DIVISION.
      *=====================

       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA
                        C01 IS CANAL-1.
      *
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *
      *     *** ARCHIVO CON RUTS DUPLICADOS EN TABLA DBC   ***
      *                     (KSDS)
           SELECT RDO001        ASSIGN   TO   RDO001
                                ORGANIZATION INDEXED
                                ACCESS DYNAMIC
                                RECORD KEY RDO001-CLAVE
                                FILE STATUS FS-RDO001.
      *
      *     *** ARCHIVO CUENTAS CON OPERACION CCT Y CPR    ***
      *         TRAIDO DESDE TANDEM.
      *                     (KSDS)
           SELECT CCTCPR        ASSIGN   TO   CCTCPR
                                ORGANIZATION INDEXED
                                ACCESS DYNAMIC
                                RECORD KEY CCTCPR-CLAVE
                                FILE STATUS FS-CCTCPR.
      *
       DATA DIVISION.
      *==============
      *
       FILE SECTION.
      *-------------
      *
       FD  RDO001
           LABEL RECORD    STANDARD.
      *
       01  RDO001-REG.
           05  RDO001-CLAVE.
               10  RDO001-RUT          PIC X(12).
               10  RDO001-CIC          PIC X(12).
           05  RDO001-IND-SIS          PIC 9(02).
           05  RDO001-TAB-SISTEMA.
               10 RDO001-TABLA-SIS  OCCURS 10 TIMES INDEXED BY
                                    WS-INDEX1.
                  15  RDO001-TAB-SIST  PIC X(03).
           05  FILLER                  PIC X.
      *
       FD  CCTCPR
           LABEL RECORD    STANDARD.
      *
       01  CCTCPR-REG.
           05  CCTCPR-CLAVE.
               10  CCTCPR-CIC          PIC X(12).
               10  CCRCPR-CTA          PIC X(12).
      *
       WORKING-STORAGE SECTION.
      *-----------------------
      ***                          ***
      *       DEFINICION REGISTROS   *
      ***                          ***
      *
       COPY COLBRRDC.
      *
       COPY LDCBRRDC REPLACING RDC BY LDC.
      *
       COPY GARBRGDD.
      *
       COPY DAPBRRCC.
      *
      *   ***   TABLA RUTS DUPLICADOS   ***
      *
       01  WS-RDO001-TAB-SISTEMA.
           05  WS-RDO001-CLAVE  OCCURS 100 TIMES INDEXED BY WS-INDEX2.
               10  WS-RDO001-RUT       PIC X(08).
               10  WS-RDO001-CIC       PIC X(12).
      *
       01  WR-CURRENT-DATE.
           05  WR-DD-CURR              PIC 9(2).
           05  FILLER                  PIC X          VALUE '/'.
           05  WR-MM-CURR              PIC 9(2).
           05  FILLER                  PIC X          VALUE '/'.
           05  WR-AA-CURR              PIC 9(2).
      *
       01  WR-TIME-OF-DAY.
           05  WR-HH-TIME              PIC 9(2).
           05  WR-MM-TIME              PIC 9(2).
           05  WR-SS-TIME              PIC 9(2).
      *
       01  WR-TIME-TRANS.
           05  WR-HH-TRANS             PIC 9(2).
           05  WR-MM-TRANS             PIC 9(2).
           05  WR-SS-TRANS             PIC 9(2).
      *
      *
       01  WR-HORA-IMPRES.
           05  WR-HH-IMPRES            PIC 9(2).
           05  FILLER                  PIC X          VALUE '.'.
           05  WR-MM-IMPRES            PIC 9(2).
           05  FILLER                  PIC X          VALUE '.'.
           05  WR-SS-IMPRES            PIC 9(2).
      *
       01  WR-REGISTRO-LINEA           PIC X(132).
      *
      *
      * ***       PARAMETROS DE ENTRADA   ***
      *
       01  WP-PARAMETROS.
           05  FILLER                  PIC X(8).
           05 WP-FECHA-PROC.
              10  WP-DD-PROC           PIC 99.
              10  WP-MM-PROC           PIC 99.
              10  WP-SS-PROC           PIC 99.
              10  WP-AA-PROC           PIC 99.
      *
           05  WR-FECHA-TRB.
               10  WR-DD-TRB            PIC 99.
               10  WR-MM-TRB            PIC 99.
               10  WR-SS-TRB            PIC 99.
               10  WR-AA-TRB            PIC 99.
           05  WR-FECHA-TRB-NUMERIC-R  REDEFINES WR-FECHA-TRB.
               10  WR-FECHA-TRB-NUMERIC PIC 9(8).
      *
      *      ***   CAMPOS DE TRABAJO    ***
      *
       01  WR-CAMPOS-DE-TRABAJO.
      *-------------------------
      *
           05  WR-RUT-ANT-DUP          PIC X(8).
           05  WR-BLANCO               PIC X(8)  VALUE SPACES.
           05  WR-SW-ERR1              PIC 9     VALUE 0.
           05  WR-CONT-LINEAS1         PIC 9(2)  VALUE 60.
           05  WR-TOT-PAG1             PIC 9(2)  VALUE 0.
           05  WR-SW-IMPRESION         PIC 9     VALUE 0.
           05  WR-SECCION              PIC X(25).
           05  WR-PARRAFO              PIC X(25).
           05  WR-OPERACION            PIC X(25).
           05  WR-CODIGO               PIC X(25).
      *
           COPY RTNWCALE.
      *
       01  W-ACUMULADORES.
      *-------------------
      *
           05  WA-CONT1                PIC 9(11).
           05  WA-CONT2                PIC 9(11).
           05  WA-CONT3                PIC 9(11).
           05  WA-CONT4                PIC 9(11).
           05  WA-TOTAL-LEID-RDCCOL    PIC 9(11).
           05  WA-TOTAL-LEID-RDCLDC    PIC 9(11).
           05  WA-TOTAL-LEID-GDDGAR    PIC 9(11).
           05  WA-TOTAL-LEID-RCCDAP    PIC 9(11).
           05  WA-TOTAL-LEID-RCOGES    PIC 9(11).
           05  WA-TOTAL-LEID-RDO001    PIC 9(11).
           05  WA-TOTAL-LEID-CTAS      PIC 9(11).
           05  WA-TOTAL-LEID-DUP       PIC 9(11).
           05  WA-TOTAL-LEID-ERROR     PIC 9(11).
           05  WA-TOTAL-ACT-RDO001     PIC 9(11).
           05  WA-TOTAL-NOACT-RDO001   PIC 9(11).
           05  WA-TOTAL-IMPRESOS       PIC 9(11).
           05  WA-TOTAL-NOENCONT       PIC 9(11).
      *
      *   ***      SWITCHES  DE PROCESO     ****
      *
       01  WS-SWITCHES.
           05  WS-ACCESO-DBC         PIC X(2).
               88  ACCESO-DBC                   VALUE 'SI'.
           05  WS-FIN-RDO001         PIC X(2).
               88  FIN-RDO001                   VALUE 'SI'.
           05  WS-NO-HAY-REGS        PIC X(2).
               88  NO-HAY-REGS                  VALUE 'SI'.
           05  WS-TERMINO-ANORMAL    PIC X(2).
               88  TERMINO-ANORMAL              VALUE 'SI'.
           05  WS-FIRST-REG          PIC X(2).
               88  FIRST-REG                    VALUE 'SI'.
           05  WS-IMPRESION          PIC X(2).
               88  IMPRESION-OK                 VALUE 'SI'.
      *
       01  FILES-STATUS.
           05 FS-RDO001              PIC 9(2)     VALUE 0.
           05 FS-CCTCPR              PIC 9(2)     VALUE 0.
      *

      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           05 FILLER                 PIC X(32) VALUE 'SGCPBRD2'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  REQ-AREA.
           02 REQ-CMD              PIC X(5).
           02 REQ-TBLNM            PIC X(3).
           02 REQ-KEYNM            PIC X(5).
           02 REQ-RETURN-CODE      PIC X(2) VALUE SPACES.
           02 REQ-INT-RTN-CD       PIC X(1).
           02 REQ-DBID             PIC S9(4) COMP.
           02 REQ-TBLID            PIC X(7).
           02 FILLER               PIC X(25).
           02 REQ-COUNT-MAX        PIC S9(4) COMP.
           02 REQ-IO-COUNT         PIC S9(4) COMP.
           02 FILLER               PIC X(22).
           02 REQ-KEY-VALUE        PIC X(24).
           02 REQ-TAB-KEY-VALUE REDEFINES REQ-KEY-VALUE.
              03 REQ-TAB-NUM-ICLI  PIC X(12).
              03 FILLER            PIC X(12).
      *
      ******************************************************************
      *                 WORK AREA                                      *
      ******************************************************************
      *
       01 REG-RCO.
      *   COPYDD OPE-R-RCO.OPE-E-RCO00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO RCO
           02  OPE-E-RCO00.
      *                             RELACION CLIENTE OPERACION
               03  RCO.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  RCO-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  RCO-KEY-IREG.
      *                             IDENTIFICACION RELACION CLIENTE OPER
                       05  RCO-KEY-IRCO.
      *                             COD. INT. COMP. CLIENTE
                       06  RCO-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  RCO-CAI-ICLI.
                       08  RCO-COD-TCLI
                                    PICTURE X(1).
                       08  RCO-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  RCO-IIC-ICLI
                                    PICTURE X(8).
      *                             IDENTIFICACION OPERACION RELACION
                       06  RCO-KEY-IOPE.
      *                             COD. INT. COMP. OPERACION
                       07  RCO-CIC-IOPE.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       08  RCO-CAI-IOPE.
                       09  RCO-COD-TOPE
                                    PICTURE X(1).
                       09  RCO-COD-COPE
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       08  RCO-IIC-IOPE.
                       09  RCO-COD-OOPE.
                       10  RCO-COD-SOPE
                                    PICTURE X(2).
                       10  RCO-IND-SOPE
                                    PICTURE X(1).
                       09  RCO-GLS-NOPE
                                    PICTURE X(5).
      *                             TIME STAMP TRANSACCION
                       05  RCO-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  RCO-FEC-FTRN.
                       07  RCO-NUM-STRN
                                    PICTURE 9(2).
                       07  RCO-NUM-ATRN
                                    PICTURE 9(2).
                       07  RCO-NUM-MTRN
                                    PICTURE 9(2).
                       07  RCO-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  RCO-HRA-HRTR.
                       07  RCO-NUM-HHTR
                                    PICTURE 9(2).
                       07  RCO-NUM-MMTR
                                    PICTURE 9(2).
                       07  RCO-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  RCO-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  RCO-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  RCO-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  RCO-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  RCO-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  RCO-COD-ATRN PICTURE X(12).
      *                             CODIGO STATUS
                   04  RCO-COD-STAT PICTURE X(3).
      *                             COD RELACION CLIENTE OPERACION
                   04  RCO-COD-RCOP PICTURE X(3).
      *                             NUMERO DE DIRECCION
                   04  RCO-NUM-DIRE PICTURE 9(2).
      *                             PORCENTAJE RELACION
                   04  RCO-VAL-PRCO PICTURE 9(3)V9(6)
                                        COMPUTATIONAL.
      *                             CODIGO CLASE OPERACION
                   04  RCO-COD-COPR.
      *                             CODIGO TIPO OPERACION
                       05  RCO-COD-TOPR
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR OPERACION
                       05  RCO-COD-AOPR
                                    PICTURE X(3).
      *                             FECHA INICIO DE LA RELACION
                   04  OPE-FEC-INI-REL.
      *                             ANO INICIO DE LA RELACION
                       05  OPE-FA-INI-REL
                                    PICTURE X(4).
      *                             MES INICIO DE LA RELACION
                       05  OPE-FM-INI-REL
                                    PICTURE X(2).
      *                             DIA INICIO DE LA RELACION
                       05  OPE-FD-INI-REL
                                    PICTURE X(2).
      *                             FECHA TERMINO DE LA RELACION
                   04  OPE-FEC-TER-REL.
      *                             ANO TERMINO DE LA RELACION
                       05  OPE-FA-TER-REL
                                    PICTURE X(4).
      *                             MES TERMINO DE LA RELACION
                       05  OPE-FM-TER-REL
                                    PICTURE X(2).
      *                             DIA TERMINO DE LA RELACION
                       05  OPE-FD-TER-REL
                                    PICTURE X(2).
                   04  FILLER-1     PICTURE X(20).
      *
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  ELEM-LIST.
           03 ELEM-1         PIC X(05).
           03 ELEM-SEC       PIC X(01).
           03 FILLER         PIC X(05) VALUE SPACES.
      *-----------------------------------------------------------------


       PROCEDURE DIVISION.
      *===================
      *
           ENTRY 'DBMSCBL'.
      *
       A-CONTROL                       SECTION.
      *---------
      *
       AA-INICIALIZACION.
      *------------------
           PERFORM B-INICIALIZAR.
      *
       AB-PROCESAMIENTO.
      *-----------------
           IF  NOT TERMINO-ANORMAL
               PERFORM C-PROCESAR
           END-IF.
      *
       AC-TERMINACION.
      *---------------
           PERFORM D-TERMINAR.
      *
       AZ-FIN-DE-SECCION.
      *------------------
           GOBACK.
      *
       B-INICIALIZAR  SECTION.
      *=======================
      *
       BA-CONTROL.
      *-----------
           PERFORM BC-ABRIR-ARCHIVOS.
           IF  NOT TERMINO-ANORMAL
              PERFORM BD-INICIALIZA-ACUM.
           GO TO BZ-FIN-DE-SECCION.
      *
       BD-INICIALIZA-ACUM.
      *-------------------
      *
           MOVE ZEROS                    TO W-ACUMULADORES.
           MOVE 'NO'                     TO WS-ACCESO-DBC
                                            WS-TERMINO-ANORMAL
                                            WS-FIN-RDO001
                                            WS-FIRST-REG.
      *
       BC-ABRIR-ARCHIVOS.
      *------------------
      *
           OPEN I-O    RDO001
                INPUT  CCTCPR.
           IF FS-RDO001 NOT = ZEROS
              DISPLAY '**   ERROR AL ABRIR ARCHIVO RDO001   ** '
              DISPLAY '**   CODIGO DE RETORNO ** ' FS-RDO001
              MOVE  'B-INICIALIZAR'      TO WR-SECCION
              MOVE  'BC-ABRIR-ARCHIVO'   TO WR-PARRAFO
              MOVE  'APERTURA RDO001  '  TO WR-OPERACION
              SET TERMINO-ANORMAL        TO TRUE
           END-IF.
      *
           IF FS-CCTCPR NOT = ZEROS
              DISPLAY '**   ERROR AL ABRIR ARCHIVO CCTCPR   ** '
              DISPLAY '**   CODIGO DE RETORNO ** ' FS-CCTCPR
              MOVE  'B-INICIALIZAR'      TO WR-SECCION
              MOVE  'BC-ABRIR-ARCHIVO'   TO WR-PARRAFO
              MOVE  'APERTURA CCTCPR  '  TO WR-OPERACION
              SET TERMINO-ANORMAL        TO TRUE
           END-IF.
      *
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
      *
       C-PROCESAR                      SECTION.
      *----------
           PERFORM CD-START-RDO001.
           IF NOT TERMINO-ANORMAL
              PERFORM CC-LEER-RDO001
              IF FIN-RDO001
                 DISPLAY 'ARCHIVO RDO001 SIN REGISTROS'
                 DISPLAY 'PROCESO SE CANCELA'
                 SET TERMINO-ANORMAL  TO TRUE
              ELSE
                 PERFORM CB-PROCESO-RDO001 UNTIL FIN-RDO001
                                           OR TERMINO-ANORMAL
             END-IF
           END-IF.
           GO TO CZ-PROCESAR-EXIT.
      *
       CB-PROCESO-RDO001.
      *------------------
           PERFORM CE-ACCESA-COLRDC.
           PERFORM CF-ACCESA-LDCRDC.
           PERFORM CG-ACCESA-GARGDD.
           PERFORM CH-ACCESA-DAPRCC.
           PERFORM CI-ACCESA-GESRCO.
           PERFORM CJ-ACCESA-CCTCPR.
           PERFORM CC-LEER-RDO001.
      *
       CC-LEER-RDO001.
      *---------------
           READ RDO001 NEXT.
           IF FS-RDO001 NOT = ZEROS AND FS-RDO001 NOT = 10
              DISPLAY '**   ERROR AL LEER  ARCHIVO RDO001   ** '
              DISPLAY '**   CODIGO DE RETORNO ** ' FS-RDO001
              MOVE  'C-PROCESAR   '      TO WR-SECCION
              MOVE  'CC-LEER-RDO001  '   TO WR-PARRAFO
              MOVE  'LECTURA  RDO001  '  TO WR-OPERACION
              SET TERMINO-ANORMAL        TO TRUE
           ELSE
              IF FS-RDO001 = ZEROS
                 ADD 1                   TO WA-TOTAL-LEID-RDO001
              ELSE
                 IF FS-RDO001 = 10
                    SET FIN-RDO001       TO TRUE
                 END-IF
              END-IF
           END-IF.
      *
       CD-START-RDO001.
      *---------------
           MOVE LOW-VALUES               TO RDO001-CLAVE.
           START RDO001 KEY > RDO001-CLAVE.
           IF FS-RDO001 NOT = ZEROS
              DISPLAY '**   ERROR AL START ARCHIVO RDO001   ** '
              DISPLAY '**   CODIGO DE RETORNO ** ' FS-RDO001
              MOVE  'C-PROCESAR   '      TO WR-SECCION
              MOVE  'CC-START-RDO001 '   TO WR-PARRAFO
              MOVE  'LECTURA  RDO001  '  TO WR-OPERACION
              SET TERMINO-ANORMAL        TO TRUE
           END-IF.
      *
       CE-ACCESA-COLRDC.
      *------------------
           MOVE LOW-VALUES               TO REQ-KEY-VALUE.
           MOVE RDO001-CIC               TO REQ-TAB-NUM-ICLI.
           MOVE 'REDKG'                  TO REQ-CMD.
           MOVE 'RDC'                    TO REQ-TBLNM.
           MOVE 'RDC00'                  TO REQ-KEYNM.
           MOVE 'RDC00'                  TO ELEM-1.
           MOVE  +160                    TO REQ-DBID.
           CALL 'DBNTRY' USING UIB REQ-AREA RDC ELEM-LIST.
           IF REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              PERFORM  CD-ERROR-DATACOM
           ELSE
              IF REQ-RETURN-CODE = '  ' AND RDC-CIC-ICLI IN RDC =
                 RDO001-CIC
                 PERFORM CKA-MUEVE-COLRDC
                 PERFORM CK-ACTUALIZ-RDO001
              END-IF
           END-IF.
      *
       CF-ACCESA-LDCRDC.
      *-----------------
           MOVE LOW-VALUES               TO REQ-KEY-VALUE.
           MOVE RDO001-CIC               TO REQ-TAB-NUM-ICLI.
           MOVE 'REDKG'                  TO REQ-CMD.
           MOVE 'RDC'                    TO REQ-TBLNM.
           MOVE 'RDC00'                  TO REQ-KEYNM.
           MOVE 'RDC00'                  TO ELEM-1.
           MOVE +260                     TO REQ-DBID.
           CALL 'DBNTRY' USING UIB REQ-AREA LDC ELEM-LIST.
           IF REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              PERFORM  CD-ERROR-DATACOM
           ELSE
              IF REQ-RETURN-CODE = '  ' AND RDC-CIC-ICLI IN LDC =
                 RDO001-CIC
                 PERFORM CKB-MUEVE-LDCRDC
                 PERFORM CK-ACTUALIZ-RDO001
              END-IF
           END-IF.
      *
       CG-ACCESA-GARGDD.
      *-----------------
           MOVE LOW-VALUES               TO REQ-KEY-VALUE.
           MOVE RDO001-CIC               TO REQ-TAB-NUM-ICLI.
           MOVE 'REDKG'                  TO REQ-CMD.
           MOVE 'GDD'                    TO REQ-TBLNM.
           MOVE 'GDD00'                  TO REQ-KEYNM.
      *    MOVE 'GDD01'                  TO ELEM-1.
           MOVE 'GDD00'                  TO ELEM-1.
           MOVE +109                     TO REQ-DBID.
           CALL 'DBNTRY' USING UIB REQ-AREA GDD ELEM-LIST.
           IF REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              PERFORM  CD-ERROR-DATACOM
           ELSE
              IF REQ-RETURN-CODE = '  ' AND GDD-CIC-ICLI = RDO001-CIC
                 PERFORM CKC-MUEVE-GARGDD
                 PERFORM CK-ACTUALIZ-RDO001
              END-IF
           END-IF.
      *
       CH-ACCESA-DAPRCC.
      *-----------------
           MOVE LOW-VALUES               TO REQ-KEY-VALUE.
           MOVE RDO001-CIC               TO REQ-TAB-NUM-ICLI.
           MOVE 'REDKG'                  TO REQ-CMD.
           MOVE 'RCC'                    TO REQ-TBLNM.
           MOVE 'RCC00'                  TO REQ-KEYNM.
           MOVE 'RCC00'                  TO ELEM-1.
           MOVE +370                    TO REQ-DBID.
           CALL 'DBNTRY' USING UIB REQ-AREA RCC ELEM-LIST.
           IF REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              PERFORM  CD-ERROR-DATACOM
           ELSE
              IF REQ-RETURN-CODE = '  ' AND RCC-CIC-ICLI = RDO001-CIC
                 PERFORM CKD-MUEVE-DAPRCC
                 PERFORM CK-ACTUALIZ-RDO001
              END-IF
           END-IF.
      *
       CI-ACCESA-GESRCO.
      *-----------------
           MOVE LOW-VALUES               TO REQ-KEY-VALUE.
           MOVE RDO001-CIC               TO REQ-TAB-NUM-ICLI.
           MOVE 'REDKG'                  TO REQ-CMD.
           MOVE 'OPE'                    TO REQ-TBLNM.
           MOVE 'OPE00'                  TO REQ-KEYNM.
           MOVE 'OPE00'                  TO ELEM-1.
           MOVE +362                     TO REQ-DBID.
           CALL 'DBNTRY' USING UIB REQ-AREA RCO ELEM-LIST.
           IF REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              PERFORM  CD-ERROR-DATACOM
           ELSE
              IF REQ-RETURN-CODE = '  ' AND RCO-CIC-ICLI = RDO001-CIC
                 PERFORM CKE-MUEVE-GESRCO
                 PERFORM CK-ACTUALIZ-RDO001
              END-IF
           END-IF.
      *
       CJ-ACCESA-CCTCPR.
      *-----------------
           MOVE LOW-VALUES               TO CCTCPR-CLAVE.
           MOVE RDO001-CIC               TO CCTCPR-CIC.
           START CCTCPR KEY > CCTCPR-CLAVE.
           IF FS-CCTCPR NOT = ZEROS AND FS-CCTCPR NOT = 23
              DISPLAY '**   ERROR AL START ARCHIVO CCTCPR   ** '
              DISPLAY '**   CODIGO DE RETORNO ** ' FS-CCTCPR
              MOVE  'C-PROCESAR   '      TO WR-SECCION
              MOVE  'CJA-START-CCTCPR'   TO WR-PARRAFO
              MOVE  'LECTURA  CCTCPR  '  TO WR-OPERACION
              SET TERMINO-ANORMAL        TO TRUE
           ELSE
              IF FS-CCTCPR = ZEROS
                 READ CCTCPR NEXT
                 IF FS-CCTCPR NOT = ZEROS AND FS-CCTCPR NOT = 23
                    DISPLAY '**   ERROR AL LEER  ARCHIVO CCTCPR   ** '
                    DISPLAY '**   CODIGO DE RETORNO ** ' FS-CCTCPR
                    MOVE  'C-PROCESAR   '      TO WR-SECCION
                    MOVE  'CJA-LEER-CCTCPR '   TO WR-PARRAFO
                    MOVE  'LECTURA  CCTCPR  '  TO WR-OPERACION
                    SET TERMINO-ANORMAL        TO TRUE
                 ELSE
                    IF FS-CCTCPR = ZEROS AND CCTCPR-CIC = RDO001-CIC
                       PERFORM CG-MUEVE-CCTCPR
                       PERFORM CK-ACTUALIZ-RDO001
                    END-IF
              END-IF
             END-IF.
           IF RDO001-IND-SIS = ZEROS
              ADD 1                       TO WA-TOTAL-NOACT-RDO001
           END-IF.
      *
       CK-ACTUALIZ-RDO001.
      *-------------------
           REWRITE RDO001-REG.
           IF FS-RDO001 NOT = ZEROS
              DISPLAY '**   ERROR AL REWRITE ARCH. RDO001   ** '
              DISPLAY '**   CODIGO DE RETORNO ** ' FS-RDO001
              MOVE  'C-PROCESAR   '      TO WR-SECCION
              MOVE  'CK-REWRITE RDO001'  TO WR-PARRAFO
              MOVE  'REWRITE  RDO001  '  TO WR-OPERACION
              SET TERMINO-ANORMAL        TO TRUE
           ELSE
              ADD 1                      TO WA-TOTAL-ACT-RDO001
           END-IF.
      *
       CKA-MUEVE-COLRDC.
      *-----------------
           SET WS-INDEX1                 TO 1.
           MOVE 'COL'                    TO RDO001-TAB-SIST(WS-INDEX1).
           ADD 1                         TO RDO001-IND-SIS
                                            WA-TOTAL-LEID-RDCCOL.
      *
       CKB-MUEVE-LDCRDC.
      *----------------
           SET WS-INDEX1                 TO 2.
           MOVE 'LDC'                    TO RDO001-TAB-SIST(WS-INDEX1).
           ADD 1                         TO RDO001-IND-SIS
                                            WA-TOTAL-LEID-RDCLDC.
      *
       CKC-MUEVE-GARGDD.
      *----------------
           SET WS-INDEX1                 TO 3.
           MOVE 'GAR'                    TO RDO001-TAB-SIST(WS-INDEX1).
           ADD 1                         TO RDO001-IND-SIS
                                            WA-TOTAL-LEID-GDDGAR.
      *
       CKD-MUEVE-DAPRCC.
      *----------------
           SET WS-INDEX1                 TO 4.
           MOVE 'DAP'                    TO RDO001-TAB-SIST(WS-INDEX1).
           ADD 1                         TO RDO001-IND-SIS
                                            WA-TOTAL-LEID-RCCDAP.
      *
       CKE-MUEVE-GESRCO.
      *-----------------
           SET WS-INDEX1                 TO 5.
           MOVE 'GES'                    TO RDO001-TAB-SIST(WS-INDEX1).
           ADD 1                         TO RDO001-IND-SIS
                                            WA-TOTAL-LEID-RCOGES.
      *
       CG-MUEVE-CCTCPR.
      *---------------
           SET WS-INDEX1                 TO 6.
           MOVE 'CCT'                    TO RDO001-TAB-SIST(WS-INDEX1).
           ADD 1                         TO RDO001-IND-SIS
                                            WA-TOTAL-LEID-CTAS.
           DISPLAY 'TABLA = ' RDO001-TAB-SISTEMA.
      *
       CD-ERROR-DATACOM.
      *-----------------
           DISPLAY WR-BLANCO.
           DISPLAY WR-BLANCO '**************************'.
           DISPLAY WR-BLANCO '* ERROR: TABLA           *'.
           DISPLAY WR-BLANCO '* TABLA          :        ' REQ-TBLNM.
           DISPLAY WR-BLANCO '* CODIGO RETORNO :        '
                                REQ-RETURN-CODE
           DISPLAY WR-BLANCO '* REQ. AREA      :        ' REQ-AREA.
           DISPLAY WR-BLANCO '* PGM. SGCPBRD2 CANCELADO *'.
           DISPLAY WR-BLANCO '**************************'.
           DISPLAY WR-BLANCO.
           SET TERMINO-ANORMAL               TO TRUE.
      *
       CZ-PROCESAR-EXIT.
           EXIT.
      *
       D-TERMINAR                      SECTION.
      *==========
       DA-CONTROL.
      *----------
           PERFORM DC-CERRAR-ARCHIVOS.
           PERFORM DD-FIN-PROGRAMA.
           GO TO DZ-FIN-DE-SECCION.
      *
       DC-CERRAR-ARCHIVOS.
      *------------------
           CLOSE RDO001
                 CCTCPR.
      *
       DD-FIN-PROGRAMA.
      *---------------
      *
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+     T O T A L E S  D E  C O N T R O L     +'
           DISPLAY '+-------------------------------------------+'
           DISPLAY '** TOTAL REG.ENCONT.COL    = ' WA-TOTAL-LEID-RDCCOL.
           DISPLAY '** TOTAL REG.ENCONT.LDC    = ' WA-TOTAL-LEID-RDCLDC.
           DISPLAY '** TOTAL REG.ENCONT.GAR    = ' WA-TOTAL-LEID-GDDGAR.
           DISPLAY '** TOTAL REG.ENCONT.DAP    = ' WA-TOTAL-LEID-RCCDAP.
           DISPLAY '** TOTAL REG.ENCONT.GES    = ' WA-TOTAL-LEID-RCOGES.
           DISPLAY '** TOTAL REG.ENCONT.CTAS   = ' WA-TOTAL-LEID-CTAS.
           DISPLAY '** TOTAL REGS.LEID.RDO001  = ' WA-TOTAL-LEID-RDO001.
           DISPLAY '** TOTAL REG.ACT.  RDO001  = ' WA-TOTAL-ACT-RDO001.
           DISPLAY ' '
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+ MENSAJES PARA EL EXPLOTADOR               +'
           DISPLAY '+-------------------------------------------+'
           IF TERMINO-ANORMAL
              MOVE 100 TO RETURN-CODE
              DISPLAY ' HAY MENSAJES PARA EL EXPLOTADOR'
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '************************************************'
              DISPLAY '************************************************'
              DISPLAY '****                                        ****'
              DISPLAY '****      TERMINO ERRONEO DEL PROCESO       ****'
              DISPLAY '****                                        ****'
              DISPLAY '************************************************'
              DISPLAY '************************************************'
              DISPLAY 'EL ERROR FUE EN LA SECCION   ' WR-SECCION
              DISPLAY '                   PARRAFO   ' WR-PARRAFO
              DISPLAY '                   OPERACION ' WR-OPERACION
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL ***'
           END-IF.
      *
       DZ-FIN-DE-SECCION.
      *-----------------
           EXIT.
      *
      *************   FIN PROGRAMA SGCPBRD1   *****************
      *
