       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.          SGCPGCHP.
       AUTHOR.              CARLOS CID B.
       DATE-WRITTEN.        18/07/97.
       DATE-COMPILED.
      *REMARKS.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      ******************************************************************
      * SISTEMA    : S.G.C., SISTEMA GENERAL DE CLIENTES.              *
      * SUBSISTEMA : PROGRAMAS BATCH.                                  *
      * OBJETIVO   : A PARTIR DE LA BASE DE DATOS CHP GENERAR UN ARC-  *
      *              CHIVO QUE CONTENGA LA SIGUIENTE INFORMACION NU-   *
      *              MERO DE OPERACION, CANTIDAD DE PROTESTOS Y CAN-   *
      *              TIDAD DE ACLARADOS                                *
      * ENTRADA.                                                       *
      *   INPUT :   - TABLA CHP  BD. 100.                              *
      *                                                                *
      *   OUTPUT:   - ARCHIVO ESTADISTICO.                             *
      *                                                                *
      *                                                                *
      ******************************************************************
      *  MANTENCIONES                                                  *
      *  ============                                                  *
      *        PROGRAMADOR : XXXXXXXXXX                               *
      *        FECHA       : DD/MM/SSAA.-                              *
      *        CAMBIO      : XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   *
      *                                                                *
      ******************************************************************
      *

           EJECT
       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.

           SELECT ARCHIVO      ASSIGN TO ARCHIVO
                                FILE STATUS IS FS-ARCHIVO.

       DATA DIVISION.
      *==============
       FILE SECTION.
      *-------------
      *
       FD  ARCHIVO
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 28 CHARACTERS.

       01  REG-ARC.
           02 ARC-NUM-OPER          PIC X(12).
           02 ARC-CANT-PROT         PIC 9(08).
           02 ARC-CANT-ACLA         PIC 9(08).


       WORKING-STORAGE SECTION.
      *------------------------
       01  W-VARIABLES.
           02 WA-CHP-LEI           PIC 9(08) VALUE ZEROES.
           02 WA-GRABADOS          PIC 9(08) VALUE ZEROES.
           02 WA-PROTESTOS         PIC 9(08) VALUE ZEROES.
           02 WA-ACLARADOS         PIC 9(08) VALUE ZEROES.
           02 WA-OPER-AUX          PIC X(12) VALUE SPACES.
           02 FS-ARCHIVO           PIC X(02) VALUE '00'.

      *
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           02 FILLER                        PIC X(32) VALUE 'SGCPGCHP'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *

       01  REQ-CHP-AREA.
           02 REQ-CHP-CMD                   PIC X(5).
           02 REQ-CHP-TBLNM                 PIC X(3) VALUE 'CHP'.
           02 REQ-CHP-KEYNM                 PIC X(5) VALUE 'CHP00'.
           02 REQ-CHP-RTN-CODE              PIC X(2) VALUE SPACES.
           02 REQ-CHP-INT-RTN-CD            PIC X(1).
           02 REQ-CHP-DBID                  PIC S9(4) COMP VALUE +100.
           02 REQ-CHP-TBLID                 PIC X(7).
           02 FILLER                        PIC X(25).
           02 REQ-CHP-COUNT-MAX             PIC S9(4) COMP.
           02 REQ-CHP-IO-COUNT              PIC S9(4) COMP.
           02 FILLER                        PIC X(22).
           02 REQ-CHP-KEY-VALUE.
              03 REQ-CHP-CIC-ICCT           PIC X(12).
              03 REQ-CHP-NUM-ICHP           PIC X(07).
              03 REQ-CHP-FEC-FCHP           PIC X(08).

      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  ELEM-LIST.
           02 ELEM-1           PIC X(05).
           02 ELEM-SEC         PIC X(01).
           02 FILLER           PIC X(05) VALUE SPACES.
      *
      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
       01  REG-CHP.
      *    COPYDD DGC-R-CHP.DGC-E-CHP00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO CHP
           02  DGC-E-CHP00.
      *                             DOCUMENTOS PROTESTADOS
               04  CHP.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  CHP-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  CHP-KEY-IREG.
      *                             IDENTIFICACION DOCTOS PROTESTADOS
                       08  CHP-KEY-ICHP.
      *                             COD. INT. COMP. OPERACION
                       10  CHP-CIC-ICCT.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  CHP-CAI-ICCT.
                       14  CHP-COD-TCCT
                                    PICTURE X(1).
                       14  CHP-COD-CCCT
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  CHP-IIC-ICCT.
                       14  CHP-COD-OCCT.
                       16  CHP-COD-SCCT
                                    PICTURE X(2).
                       16  CHP-IND-SCCT
                                    PICTURE X(1).
                       14  CHP-GLS-NCCT
                                    PICTURE X(5).
      *                             NUMERO IDENTIFICACION DOCTO
                       10  CHP-NUM-ICHP
                                    PICTURE X(7).
      *                             FECHA PROTESTO
                       10  CHP-FEC-FCHP.
                       12  CHP-NUM-SCHP
                                    PICTURE 9(2).
                       12  CHP-NUM-ACHP
                                    PICTURE 9(2).
                       12  CHP-NUM-MCHP
                                    PICTURE 9(2).
                       12  CHP-NUM-DCHP
                                    PICTURE 9(2).
      *                             TIME STAMP TRANSACCION
                       08  CHP-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  CHP-FEC-FTRN.
                       12  CHP-NUM-STRN
                                    PICTURE 9(2).
                       12  CHP-NUM-ATRN
                                    PICTURE 9(2).
                       12  CHP-NUM-MTRN
                                    PICTURE 9(2).
                       12  CHP-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  CHP-HRA-HRTR.
                       12  CHP-NUM-HHTR
                                    PICTURE 9(2).
                       12  CHP-NUM-MMTR
                                    PICTURE 9(2).
                       12  CHP-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  CHP-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  CHP-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  CHP-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  CHP-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  CHP-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  CHP-COD-ATRN PICTURE X(12).
      *                             TIPO DOCUMENTO
                   06  CHP-COD-TDOC PICTURE X(3).
      *                             MONTO DOCTO
                   06  CHP-VAL-CHEQ PICTURE 9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             MOTIVO PROTESTO
                   06  CHP-COD-MOTP PICTURE X(2).
      *                             INDICADOR ENVIO A BOLETIN
                   06  CHP-IND-EBOL PICTURE X(1).
      *                             FECHA ACLARACION PROTESTO
                   06  CHP-FEC-FACL.
                       08  CHP-NUM-SACL
                                    PICTURE 9(2).
                       08  CHP-NUM-AACL
                                    PICTURE 9(2).
                       08  CHP-NUM-MACL
                                    PICTURE 9(2).
                       08  CHP-NUM-DACL
                                    PICTURE 9(2).
      *                             NUMERO BOLETIN PUBLICACION PROTESTO
                   06  CHP-NUM-BOLE PICTURE X(5).
      *                             NUMERO PAGINA PUBLICACION PROTESTO
                   06  CHP-NUM-PAGE PICTURE X(4).
      *                             NUMERO NOMINA ENVIO PROTESTO
                   06  CHP-NUM-NOMI PICTURE X(5).
      *                             VALOR CAMBIO MONTO DOCTO
                   06  CHP-COD-VCMC.
      *                             MISCELANEO TIPO VALOR
                       08  CHP-MSC-TVMC
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       08  CHP-COD-CVMC
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       08  CHP-IND-TCMC
                                    PICTURE X(2).
      *                             OFICINA OPERACION
                   06  CHP-COD-OFIC PICTURE X(3).
      *                             DISPONIBLE
                   06  CHP-GLS-DISP PICTURE X(12).


       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           PERFORM 10-INICIAR.
           PERFORM 12-GSETL-CHP.
           PERFORM 13-GETIT-CHP.
           PERFORM 14-PROCESAR UNTIL REQ-CHP-RTN-CODE = '19'.
           PERFORM 50-TOTALES-DE-CONTROL.


       10-INICIAR.
      *-----------
           PERFORM 11-ABRIR-ARCHIVOS.

       11-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT ARCHIVO.
           IF FS-ARCHIVO NOT = 00 THEN
               DISPLAY '<< ERROR PROG.SGCPGCHP SE CANCELA AL ABRIR >>'
               DISPLAY '<< ARCHIVO DE SALIDA  STATUS ARCHIVO ====>>> '
               FS-ARCHIVO
               MOVE 100  TO RETURN-CODE
               PERFORM 50-TOTALES-DE-CONTROL
           END-IF.

       12-GSETL-CHP.
      *--------------
           MOVE ZEROES            TO REQ-CHP-KEY-VALUE.
           MOVE 'GSETL'           TO  REQ-CHP-CMD.
           MOVE 'CHP00'           TO  ELEM-1.

           CALL 'DBNTRY'   USING UIB REQ-CHP-AREA CHP ELEM-LIST.
           IF REQ-CHP-RTN-CODE NOT = '00' AND NOT = SPACES
              DISPLAY '>>>> ERROR: GSETL TABLA CHP <<<<<'
              DISPLAY '>>>> PROCESO NO CONTINUA    <<<<<'
              DISPLAY '>>>> TABLA          : <<<' REQ-CHP-TBLNM
              DISPLAY '>>>> CODIGO RETORNO : <<<' REQ-CHP-RTN-CODE
              DISPLAY '>>>> REQ-AREA       : <<<' REQ-CHP-AREA
              MOVE 100  TO RETURN-CODE
              PERFORM 50-TOTALES-DE-CONTROL
           END-IF.

       14-PROCESAR.
      *------------>
           MOVE 0                 TO  WA-PROTESTOS
           MOVE 0                 TO  WA-ACLARADOS
           MOVE CHP-CIC-ICCT      TO  WA-OPER-AUX
           PERFORM 15-PROCESAR-CHP UNTIL CHP-CIC-ICCT NOT = WA-OPER-AUX
                                   OR    REQ-CHP-RTN-CODE = '19'.
           PERFORM 16-MOVER-DATOS.

       15-PROCESAR-CHP.
      *--------------->
           ADD 1                  TO  WA-PROTESTOS
           IF CHP-FEC-FACL NOT = ZEROES AND NOT = SPACES AND
                           NOT = LOW-VALUE
              ADD 1               TO  WA-ACLARADOS
           END-IF
           PERFORM 13-GETIT-CHP.

       13-GETIT-CHP.
      *--------------
           MOVE 'GETIT'           TO  REQ-CHP-CMD.
           MOVE 'CHP00'           TO  ELEM-1.
           CALL 'DBNTRY'   USING UIB REQ-CHP-AREA CHP ELEM-LIST.
           IF REQ-CHP-RTN-CODE NOT = '  ' AND NOT = '19' THEN
              DISPLAY '>>>> ERROR: GSETL TABLA CHP <<<<<'
              DISPLAY '>>>> PROCESO NO CONTINUA    <<<<<'
              DISPLAY '>>>> TABLA          : <<<' REQ-CHP-TBLNM
              DISPLAY '>>>> CODIGO RETORNO : <<<' REQ-CHP-RTN-CODE
              DISPLAY '>>>> REQ-AREA       : <<<' REQ-CHP-AREA
              MOVE 100  TO RETURN-CODE
              PERFORM 50-TOTALES-DE-CONTROL
           ELSE
              IF REQ-CHP-RTN-CODE = '  ' THEN
                 ADD 1 TO WA-CHP-LEI
              END-IF
           END-IF.

       16-MOVER-DATOS.
      *-------------->
           MOVE WA-OPER-AUX     TO  ARC-NUM-OPER
           MOVE WA-PROTESTOS    TO  ARC-CANT-PROT
           MOVE WA-ACLARADOS    TO  ARC-CANT-ACLA
           PERFORM 17-GRABAR-ARCHIVO.

       17-GRABAR-ARCHIVO.
      *------------------>
           WRITE REG-ARC
           IF FS-ARCHIVO NOT = 00
               DISPLAY '<< ERROR PROG.SGCPGCHP SE CANCELA AL GRABAR >>'
               DISPLAY '<< ARCHIVO DE SALIDA  STATUS ARCHIVO ====>>> '
               FS-ARCHIVO
               MOVE 100  TO RETURN-CODE
               PERFORM 50-TOTALES-DE-CONTROL
           ELSE
               ADD  1     TO  WA-GRABADOS
           END-IF.

       50-TOTALES-DE-CONTROL.
      *---------------------->
           DISPLAY '**************************************************'
           DISPLAY '*           SISTEMA GENERAL DE CLIENTES.         *'
           DISPLAY '* PROGRAMA SGCPGCHP                              *'
           DISPLAY '**************************************************'
           DISPLAY '*           TOTALES DE CONTROL                   *'
           DISPLAY '**************************************************'
           DISPLAY '* LEIDOS TABLA CHP     : ' WA-CHP-LEI           '*'
           DISPLAY '* REGISTROS GRABADOS   : ' WA-GRABADOS          '*'
           DISPLAY '*                                                *'
           DISPLAY '**************************************************'
           DISPLAY '**************************************************'
           CLOSE ARCHIVO
           GOBACK.
