       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. SGCPGTIT
       AUTHOR.     CARLOS CID BADILLA.
       DATE-WRITTEN. 12 DE ENERO 1998.
       DATE-COMPILED.
           EJECT
       ENVIRONMENT DIVISION.
      *=====================
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *
           SELECT MAESTRO  ASSIGN TO MAESTRO
                                       FILE STATUS  IS FS-MAESTRO.
      *

       DATA DIVISION.
      *==============
       FILE SECTION.
      *-------------

      *
       FD  MAESTRO            LABEL  RECORD IS STANDARD
                              RECORD CONTAINS 24 CHARACTERS
                              BLOCK  CONTAINS 0 RECORDS.
       01  REG-MAESTRO.
           02 MAE-CIC              PIC X(12).
           02 MAE-OPE              PIC X(12).
      *

       WORKING-STORAGE SECTION.
      *------------------------
       01  FS-MAESTRO                  PIC X(02) VALUE SPACES.

       01  WS-CONTADORES.
           03  WS-LEID-MAESTRO         PIC 9(07) VALUE 0.
           03  WS-RDUKY-RCO            PIC 9(07) VALUE 0.
           03  WS-UPDAT-RCO            PIC 9(07) VALUE 0.
           03  WS-NO-RCO               PIC 9(07) VALUE 0.
           03  WS-NO-ACT               PIC 9(07) VALUE 0.
      *
       01  W-FECHAS-DE-TRABAJO.
           03 WR-FECHA-PROCESO.
              04 WR-FILLER                       PIC X(17).
              04 WR-FECHA-PROC.
                 06 WR-FECHA-SS                     PIC 9(02).
                 06 WR-FECHA-AA                     PIC 9(02).
                 06 WR-FECHA-MM                     PIC 9(02).
                 06 WR-FECHA-DD                     PIC 9(02).
      *

      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           05 FILLER               PIC X(32) VALUE 'SGCPGTIT'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  REQ-AREA-RCO.
           05 REQ-COMAN-RCO            PIC X(05).
           05 REQ-TBLNM-RCO            PIC X(03) VALUE 'RCO'.
           05 REQ-KEYNM-RCO            PIC X(05) VALUE 'RCO00'.
           05 REQ-RTNCD-RCO            PIC X(02) VALUE SPACES.
           05 REQ-INRCD-RCO            PIC X(01).
           05 REQ-DBIDE-RCO            PIC S9(4) COMP VALUE +100.
           05 REQ-TBLID-RCO            PIC X(07).
           05 FILLER                   PIC X(25).
           05 REQ-CTMAX-RCO            PIC S9(4) COMP.
           05 REQ-IOCNT-RCO            PIC S9(4) COMP.
           05 FILLER                   PIC X(22).
           05 REQ-KYVAL-RCO.
              10 REQ-RCO-ICLI      PIC X(12).
              10 REQ-RCO-IOPE      PIC X(12).
      *
       01  REG-RCO.
      *    COPYDD DGC-R-RCO.DGC-E-RCO00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO RCO
           02  DGC-E-RCO00.
      *                             RELACION CLIENTE OPERACION
               04  RCO.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  RCO-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  RCO-KEY-IREG.
      *                             IDENTIFICACION RELACION CLIENTE OPER
                       08  RCO-KEY-IRCO.
      *                             COD. INT. COMP. CLIENTE
                       10  RCO-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  RCO-CAI-ICLI.
                       14  RCO-COD-TCLI
                                    PICTURE X(1).
                       14  RCO-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  RCO-IIC-ICLI
                                    PICTURE X(8).
      *                             IDENTIFICACION OPERACION RELACION
                       10  RCO-KEY-IOPE.
      *                             COD. INT. COMP. OPERACION
                       12  RCO-CIC-IOPE.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       14  RCO-CAI-IOPE.
                       16  RCO-COD-TOPE
                                    PICTURE X(1).
                       16  RCO-COD-COPE
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       14  RCO-IIC-IOPE.
                       16  RCO-COD-OOPE.
                       18  RCO-COD-SOPE
                                    PICTURE X(2).
                       18  RCO-IND-SOPE
                                    PICTURE X(1).
                       16  RCO-GLS-NOPE
                                    PICTURE X(5).
      *                             TIME STAMP TRANSACCION
                       08  RCO-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  RCO-FEC-FTRN.
                       12  RCO-NUM-STRN
                                    PICTURE 9(2).
                       12  RCO-NUM-ATRN
                                    PICTURE 9(2).
                       12  RCO-NUM-MTRN
                                    PICTURE 9(2).
                       12  RCO-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  RCO-HRA-HRTR.
                       12  RCO-NUM-HHTR
                                    PICTURE 9(2).
                       12  RCO-NUM-MMTR
                                    PICTURE 9(2).
                       12  RCO-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  RCO-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  RCO-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  RCO-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  RCO-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  RCO-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  RCO-COD-ATRN PICTURE X(12).
      *                             CODIGO STATUS
                   06  RCO-COD-STAT PICTURE X(3).
      *                             COD RELACION CLIENTE OPERACION
                   06  RCO-COD-RCOP PICTURE X(3).
      *                             NUMERO DE DIRECCION
                   06  RCO-NUM-DIRE PICTURE 9(2).
      *                             PORCENTAJE RELACION
                   06  RCO-VAL-PRCO PICTURE 9(3)V9(6)
                                        COMPUTATIONAL.
      *                             CODIGO CLASE OPERACION
                   06  RCO-COD-COPR.
      *                             CODIGO TIPO OPERACION
                       08  RCO-COD-TOPR
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR OPERACION
                       08  RCO-COD-AOPR
                                    PICTURE X(3).
      *                             DISPONIBLE
                   06  RCO-GLS-DISP PICTURE X(17).
      *
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  ELM-LIS-RCO.
           05 ELEM-RCO       PIC X(05) VALUE 'RCO00'.
           05 ELSE-RCO       PIC X(01).
           05 FILLER         PIC X(05) VALUE SPACES.
      *
           EJECT

       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           PERFORM 10-INICIALIZAR.
           PERFORM 30-LEER-MAESTRO.
           PERFORM 40-PROCESO-MAESTRO UNTIL FS-MAESTRO = '10'.
           PERFORM 250-ESTADISTICAS.
           GOBACK.

       10-INICIALIZAR.
      *---------------
           INITIALIZE WR-FECHA-PROCESO WS-CONTADORES.
           PERFORM 20-ABRIR-ARCHIVOS.

       20-ABRIR-ARCHIVOS.
      *------------------
           OPEN INPUT  MAESTRO

           IF FS-MAESTRO  NOT = '00'
              DISPLAY  'ERROR OPEN I-O   MAESTRO  '
              DISPLAY 'VALOR FS-MAESTRO :' FS-MAESTRO
              PERFORM 300-TERMINAR.
      * ACCEPTA FECHA DE PROCESO.
           ACCEPT WR-FECHA-PROCESO.
           DISPLAY WR-FECHA-PROCESO.
      *
       30-LEER-MAESTRO.
      *----------------
           READ MAESTRO.
           IF FS-MAESTRO NOT = '00' AND NOT = '10'
              DISPLAY  'ERROR READ MAESTRO '
              DISPLAY 'VALOR FS-MAESTRO : ' FS-MAESTRO
              PERFORM 300-TERMINAR
           ELSE
              IF FS-MAESTRO = '10'
                 NEXT SENTENCE
              ELSE
                 ADD  1           TO WS-LEID-MAESTRO.
      *
       40-PROCESO-MAESTRO.
      *-------------------
           MOVE MAE-CIC           TO REQ-RCO-ICLI.
           MOVE MAE-OPE           TO REQ-RCO-IOPE.
           MOVE 'RDUKY'           TO REQ-COMAN-RCO.
           MOVE SPACES            TO REQ-RTNCD-RCO.
           PERFORM 50-LEER-RCO-RDUKY.
           IF REQ-RTNCD-RCO = SPACES
              MOVE WR-FECHA-PROC  TO RCO-FEC-FTRN
              MOVE 'SGCPGTIT'     TO RCO-COD-ATRN
              MOVE 'TIT'          TO RCO-COD-RCOP
              MOVE 'UPDAT'        TO REQ-COMAN-RCO
              PERFORM 60-LEER-RCO-UPDAT
           END-IF.
           PERFORM 30-LEER-MAESTRO.
      *
       50-LEER-RCO-RDUKY.
      *-------------------
           CALL 'DBNTRY' USING UIB REQ-AREA-RCO REG-RCO
                                                 ELM-LIS-RCO.
           IF REQ-RTNCD-RCO NOT = ' ' AND REQ-RTNCD-RCO NOT = '14'
                 DISPLAY '********************************'
                 DISPLAY 'ERROR! PROBLEMA LECTURA INDICE *'
                 DISPLAY '       EN TABLA RCO -----------*'
                 DISPLAY 'CODIGO DE RETORNO :' REQ-RTNCD-RCO
                 DISPLAY 'REQ-AREA          :' REQ-AREA-RCO
                 DISPLAY 'CLAVE             :' REQ-KYVAL-RCO
                 PERFORM 300-TERMINAR
           ELSE
              IF REQ-RTNCD-RCO = '14'
                 DISPLAY ' CLTE-OPER NO ENCONTRADO EN TABLA RCO : '
                           MAE-CIC ' - ' MAE-OPE
                 ADD 1    TO         WS-NO-RCO
              ELSE
                 ADD 1    TO         WS-RDUKY-RCO.
      *
       60-LEER-RCO-UPDAT.
      *-------------------
           CALL 'DBNTRY' USING UIB REQ-AREA-RCO REG-RCO
                                                 ELM-LIS-RCO.
           IF REQ-RTNCD-RCO NOT = ' '
                 DISPLAY '********************************'
                 DISPLAY 'ERROR! PROBLEMA UPDAT INDICE *'
                 DISPLAY '       EN TABLA RCO -----------*'
                 DISPLAY 'CODIGO DE RETORNO :' REQ-RTNCD-RCO
                 DISPLAY 'REQ-AREA          :' REQ-AREA-RCO
                 DISPLAY 'CLAVE             :' REQ-KYVAL-RCO
                 PERFORM 300-TERMINAR
           ELSE
              ADD 1    TO         WS-UPDAT-RCO.
      *
       250-ESTADISTICAS.
      *---------------
           DISPLAY '==========================================='
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY '  ESTADISTICAS DE PROCESO PROGRAMA SGCPGTIT '
           DISPLAY ' '.
           DISPLAY '  LEIDOS MAESTROS           : ' WS-LEID-MAESTRO.
           DISPLAY ' '.
           DISPLAY '  LEIDOS TABLA RCO (RDUKY)  : ' WS-RDUKY-RCO.
           DISPLAY ' '.
           DISPLAY '  ACTUALIZADOS RCO (UPDAT)  : ' WS-UPDAT-RCO.
           DISPLAY ' '.
           DISPLAY '  NO ENCONTRADOS EN RCO     : ' WS-NO-RCO.
           DISPLAY ' '.
           DISPLAY '==========================================='.
           DISPLAY ' '.
           CLOSE MAESTRO.

       300-TERMINAR.
      *-------------
           PERFORM 250-ESTADISTICAS
           DISPLAY ' '
           DISPLAY '*********************************************'
           DISPLAY '  PROGRAMA TERMINO ANORMALMENTE              '
           DISPLAY '                                             '
           DISPLAY '*********************************************'
           GOBACK.
