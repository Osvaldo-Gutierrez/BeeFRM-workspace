       IDENTIFICATION DIVISION.
      *========================
      *
       PROGRAM-ID.    SGCPBEP2.
       AUTHOR.        CARLOS CID BADILLA.
       DATE-WRITTEN.  JULIO 97.
      *REMARKS.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      *
      ******************************************************************
      *                                                                *
      *  OBJETIVO : GENERAR ARCHIVO CON DATOS DE CTAS. CTES. PARA      *
      *             LISTADOS DE PLANES. TOTALES.                       *
      *                                                                *
      *  ENTRADA  :                                                    *
      *           : - ARCHIVO SOPPLN                                   *
      *  SALIDA   :                                                    *
      *           : - ARCHIVO SOPPL2                                   *
      *                                                                *
      *                                                                *
      *  FECHA    : JULIO 97.                                          *
      *                                                                *
      ******************************************************************
      ******************************************************************
       ENVIRONMENT DIVISION.
      *=====================
      *
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *
           SELECT SOPPLN       ASSIGN TO SOPPLN
                               FILE STATUS IS FS-SOPPLN.
      *
           SELECT SOPPL2       ASSIGN TO SOPPL2
                               FILE STATUS IS FS-SOPPL2.
      *
       DATA DIVISION.
      *==============
       FILE SECTION.
      *-------------
      *
       FD  SOPPLN
           LABEL RECORD STANDARD
           RECORD CONTAINS 15
           BLOCK CONTAINS 0 RECORDS.
      *
       01  REG-SOPPLN.
           03 PLN-SUC            PIC X(03).
           03 PLN-TIP-CLI        PIC X(01).
           03 PLN-PLAN           PIC X(01).
           03 PLN-FEC-PROC       PIC X(10).
      *
       FD  SOPPL2
           LABEL RECORD STANDARD
           RECORD CONTAINS 23
           BLOCK CONTAINS 0 RECORDS.
      *
       01  REG-SOPPL2.
           03 PL2-SUC            PIC X(03).
           03 PL2-TIP-CLI        PIC X(01).
           03 PL2-PLAN           PIC X(01).
           03 PL2-FEC-PROC       PIC X(10).
           03 PL2-TOTALES        PIC 9(08).
      *
       WORKING-STORAGE SECTION.
      *------------------------
       77  FS-SOPPLN                  PIC X(02) VALUE '00'.
       77  FS-SOPPL2                  PIC X(02) VALUE '00'.
       77  W-CONTADOR                 PIC 9(05) VALUE ZEROES.
       77  W-LEID-SOPPLN              PIC 9(05) VALUE ZEROES.
       77  W-GRABADOS                 PIC 9(05) VALUE ZEROES.
       77  AUX-SUC                    PIC X(03).
       77  AUX-TIP                    PIC X(01).
       77  AUX-PLAN                   PIC X(01).
       77  AUX-FEC                    PIC X(10).
      *

       PROCEDURE DIVISION.
      *===================
      *
           PERFORM 10-ABRIR-ARCHIVOS
           PERFORM 20-LEER-SOPPLN
           PERFORM 30-PROCESAR-SOPPLN  UNTIL FS-SOPPLN = '10'.
      *                                OR    W-LEID-SOPPLN = 370.
           PERFORM TERMINO
           STOP RUN.

      *
       10-ABRIR-ARCHIVOS.
      *-------------------
           OPEN INPUT  SOPPLN
                OUTPUT SOPPL2.
           IF FS-SOPPLN  NOT = '00'
               DISPLAY '<< ERROR PROG.SGCPBEP2 SE CANCELA AL ABRIR >>'
               DISPLAY '<< SOPPLN DE SALIDA  STATUS ====>>>'
               FS-SOPPLN
               MOVE 100  TO RETURN-CODE
               PERFORM TERMINO.
      *
           IF FS-SOPPL2  NOT = '00'
               DISPLAY '<< ERROR PROG.SGCPBEP2 SE CANCELA AL ABRIR >>'
               DISPLAY '<< SOPPL2 DE SALIDA  STATUS ====>>>'
               FS-SOPPL2
               MOVE 100  TO RETURN-CODE
               PERFORM TERMINO.
      *
       20-LEER-SOPPLN.
      *---------------
           READ SOPPLN NEXT
           IF FS-SOPPLN  NOT = '00' AND FS-SOPPLN NOT = '10'
               DISPLAY '<< ERROR PROG.SGCPBEP2 SE CANCELA AL LEER >>'
               DISPLAY '<< SOPPLN DE SALIDA  STATUS ====>>>'
               FS-SOPPLN
               MOVE 100  TO RETURN-CODE
               PERFORM TERMINO
            ELSE
               IF FS-SOPPLN = '10'
                  NEXT SENTENCE
               ELSE
                  ADD 1       TO  W-LEID-SOPPLN.

       30-PROCESAR-SOPPLN.
      *-------------------.
           MOVE PLN-SUC             TO AUX-SUC
           MOVE PLN-TIP-CLI         TO AUX-TIP
           MOVE PLN-PLAN            TO AUX-PLAN
           MOVE PLN-FEC-PROC        TO AUX-FEC
           MOVE ZEROES              TO W-CONTADOR
           PERFORM 40-CICLO-PRINCIPAL UNTIL PLN-SUC NOT   = AUX-SUC
                                      OR  PLN-TIP-CLI NOT = AUX-TIP
                                      OR  PLN-PLAN NOT   = AUX-PLAN
                                      OR  FS-SOPPLN   = '10'.
           PERFORM 50-GRABAR-SOPPL2.
      *
       40-CICLO-PRINCIPAL.
      *-------------------
           ADD 1       TO W-CONTADOR
           PERFORM 20-LEER-SOPPLN.

      *
       50-GRABAR-SOPPL2.
      *-----------------
           MOVE AUX-SUC           TO  PL2-SUC
           MOVE AUX-TIP           TO  PL2-TIP-CLI
           MOVE AUX-PLAN          TO  PL2-PLAN
           MOVE AUX-FEC           TO  PL2-FEC-PROC
           MOVE W-CONTADOR        TO  PL2-TOTALES
           WRITE REG-SOPPL2
           IF FS-SOPPL2  NOT = '00'
               DISPLAY '<< ERROR PROG.SGCPBEP2 SE CANCELA AL GRABAR >>'
               DISPLAY '<< SOPPL2 DE SALIDA  STATUS ====>>>'
               FS-SOPPL2
               MOVE 100  TO RETURN-CODE
               PERFORM TERMINO
           ELSE
               ADD 1              TO  W-GRABADOS.
      *
      *
       TERMINO.
      *--------
           DISPLAY '*****************************************'.
           DISPLAY '*                                       *'.
           DISPLAY '* ESTADISTICAS DEL PROCESO SGCPBEP2      *'.
           DISPLAY '*                                       *'.
           DISPLAY '*****************************************'.
           DISPLAY ' '.
           DISPLAY 'TOTAL LEIDOS SOPPLN       : ' W-LEID-SOPPLN.
           DISPLAY 'TOTAL GRABADOS SOPPL2     : ' W-GRABADOS.
           DISPLAY ' '.
           DISPLAY '*****************************************'.
           CLOSE  SOPPLN SOPPL2.
           STOP RUN.
      *
