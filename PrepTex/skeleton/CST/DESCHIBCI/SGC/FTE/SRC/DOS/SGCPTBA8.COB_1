       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      SGCPTBA8.
       AUTHOR.          CARLOS CID BADILLA.
       DATE-WRITTEN.    10/12/97.
       DATE-COMPILED.
       REMARKS.
      ******************************************************************
      *                                                                *
      *  SISTEMA    : S.G.C.                                           *
      *  SUBSISTEMA :                                                  *
      *  ANALISTA   : GERARDO URETA.                                  *
      *  OBJETIVO   : ACTUALIZA TABLAS DCP, DCE, QUE CONTENGAN COMO    *
      *               TIPO BANCA 'N  ' POR 'PP ', ADEMAS SE ACTUALIZA  *
      *               LA FECHA DE TRANSACCION DE LAS TABLA DCP, DCE Y  *
      *               DBC.                                             *
      *                                                                *
      * TABLAS                                                         *
      *     I  -  O :  - DCP , TABLA B.D. 100.                         *
      *                - DCE , TABLA B.D. 100.                         *
      *                - DBC , TABLA B.D. 100.                         *
      *                                                                *
      *     SALIDA  :                                                  *
      *                                                                *
      ******************************************************************
      *  MANTENCIONES :                                                *
      *  =============                                                 *
      *                                                                *
      *     PROGRAMADOR : XXXXXXXX.                                    *
      *     FECHA       : DD/MM/SSAA.-                                 *
      *     CAMBIO      : XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX      *
      *                                                                *
      ******************************************************************


           EJECT
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS CANAL-1.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *
           EJECT
       DATA DIVISION.
      *=============
       FILE SECTION.
      *
       WORKING-STORAGE SECTION.
      *-----------------------
       01  W-ACUMULADORES.
           02 WS-LEID-DCP                   PIC 9(06).
           02 WS-LEID-DCE                   PIC 9(06).
           02 WS-LEID-DBC                   PIC 9(06).
           02 WS-LEID-RDU-DCP               PIC 9(06).
           02 WS-LEID-RDU-DCE               PIC 9(06).
           02 WS-LEID-RDU-DBC               PIC 9(06).
           02 WS-ACTU-DCP                   PIC 9(06).
           02 WS-ACTU-DCE                   PIC 9(06).
           02 WS-ACTU-DBC                   PIC 9(06).

       01  W-SWITCH.
           02 WS-RETURN-CODE                PIC 9(03).
           02 WS-DBC-RTN-CODE               PIC X(02).

       01  W-CONSTANTES.
           02 CC-TAB                        PIC X(15) VALUE SPACES.
           02 WS-CIC-ICLI                   PIC X(12) VALUE SPACES.

       01  W-PARAMETROS.
           02 WP-TARJ-PARAM.
              03 FILLER                     PIC X(01).
              03 FILLER                     PIC X(19).
              03 WP-FEC-PROC.
                 04 WP-NUM-SPRO             PIC 9(02).
                 04 WP-NUM-APRO             PIC 9(02).
                 04 WP-NUM-MPRO             PIC 9(02).
                 04 WP-NUM-DPRO             PIC 9(02).
              03 FILLER                     PIC X(17).
              03 WP-TBAN-ACT                PIC X(03).
              03 FILLER                     PIC X(15).
              03 WP-TBAN-NVA                PIC X(03).
      *       03 FILLER                     PIC X(12).

       01  W-REG-DE-TRABAJO.
           02 WR-INFOR-ERROR.
              03 WR-SECC-ERR                PIC X(10).
              03 WR-ARCH-ERR                PIC X(20).
              03 WR-OPER-ERR                PIC X(40).
              03 WR-COD-RET                 PIC 9(02).
              03 WR-REG-ERR                 PIC X(12).

      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           05 FILLER                 PIC X(32) VALUE 'SGCPTBA8'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  REQ-DBC-AREA.
           02 REQ-DBC-CMD                   PIC X(05).
           02 REQ-DBC-TBLNM                 PIC X(03) VALUE 'DBC'.
           02 REQ-DBC-KEYNM                 PIC X(05) VALUE 'DBC00'.
           02 REQ-DBC-RTN-CODE              PIC X(02) VALUE SPACES.
           02 REQ-DBC-INT-RTN-CD            PIC X(01).
           02 REQ-DBC-DBID                  PIC S9(04) COMP VALUE +100.
           02 REQ-DBC-TBLID                 PIC X(07).
           02 FILLER                        PIC X(25).
           02 REQ-DBC-COUNT-MAX             PIC S9(04) COMP.
           02 REQ-DBC-IO-COUNT              PIC S9(04) COMP.
           02 FILLER                        PIC X(22).
           02 REQ-DBC-KEY-VALUE             PIC X(12).

       01  REQ-DCE-AREA.
           02 REQ-DCE-CMD                   PIC X(05).
           02 REQ-DCE-TBLNM                 PIC X(03) VALUE 'DCE'.
           02 REQ-DCE-KEYNM                 PIC X(05) VALUE 'DCE00'.
           02 REQ-DCE-RTN-CODE              PIC X(02) VALUE SPACES.
           02 REQ-DCE-INT-RTN-CD            PIC X(01).
           02 REQ-DCE-DBID                  PIC S9(04) COMP VALUE +100.
           02 REQ-DCE-TBLID                 PIC X(07).
           02 FILLER                        PIC X(25).
           02 REQ-DCE-COUNT-MAX             PIC S9(04) COMP.
           02 REQ-DCE-IO-COUNT              PIC S9(04) COMP.
           02 FILLER                        PIC X(22).
           02 REQ-DCE-KEY-VALUE             PIC X(12).

       01  REQ-DCP-AREA.
           02 REQ-DCP-CMD                   PIC X(05).
           02 REQ-DCP-TBLNM                 PIC X(03) VALUE 'DCP'.
           02 REQ-DCP-KEYNM                 PIC X(05) VALUE 'DCP00'.
           02 REQ-DCP-RTN-CODE              PIC X(02) VALUE SPACES.
           02 REQ-DCP-INT-RTN-CD            PIC X(01).
           02 REQ-DCP-DBID                  PIC S9(04) COMP VALUE +100.
           02 REQ-DCP-TBLID                 PIC X(07).
           02 FILLER                        PIC X(25).
           02 REQ-DCP-COUNT-MAX             PIC S9(04) COMP.
           02 REQ-DCP-IO-COUNT              PIC S9(04) COMP.
           02 FILLER                        PIC X(22).
           02 REQ-DCP-KEY-VALUE             PIC X(12).

       01  REG-DBC.
      *    COPYDD DGC-R-DBC.DGC-E-DBC00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO DBC
           02  DGC-E-DBC00.
      *                             DATOS BASICOS DEL CLIENTE
               03  DBC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  DBC-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  DBC-KEY-IREG.
      *                             IDENTIFICACION DATOS BASICOS CLIENTE
                       05  DBC-KEY-IDBC.
      *                             COD. INT. COMP. CLIENTE
                       06  DBC-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  DBC-CAI-ICLI.
                       08  DBC-COD-TCLI
                                    PICTURE X(1).
                       08  DBC-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  DBC-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       05  DBC-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  DBC-FEC-FTRN.
                       07  DBC-NUM-STRN
                                    PICTURE 9(2).
                       07  DBC-NUM-ATRN
                                    PICTURE 9(2).
                       07  DBC-NUM-MTRN
                                    PICTURE 9(2).
                       07  DBC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  DBC-HRA-HRTR.
                       07  DBC-NUM-HHTR
                                    PICTURE 9(2).
                       07  DBC-NUM-MMTR
                                    PICTURE 9(2).
                       07  DBC-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  DBC-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  DBC-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  DBC-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  DBC-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  DBC-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  DBC-COD-ATRN PICTURE X(12).
      *                             KEY RUT CLIENTE + INDICADOR O CALIFI
                   04  DBC-IDC-ICLI.
      *                             RUT CLIENTE
                       05  DBC-NUM-ICLI
                                    PICTURE X(8).
      *                             INDICADOR TIPO RUT
                       05  DBC-IND-ICLI
                                    PICTURE X(1).
      *                             EXTENSION DEL RUT
                       05  DBC-GLS-ICLI
                                    PICTURE X(3).
      *                             KEY DIGITO VERIFICADOR DEL RUT
                   04  DBC-VRF-ICLI PICTURE X(1).
      *                             KEY NOMBRE CLIENTE GENERICO (PERSONA
                   04  DBC-GLS-NOMC.
                       05  DBC-GLS-APAT
                                    PICTURE X(25).
                       05  DBC-GLS-AMAT
                                    PICTURE X(25).
                       05  DBC-GLS-NOMB
                                    PICTURE X(25).
      *                             SOUNDEX DEL NOMBRE DE CLIENTE
                   04  DBC-SNX-NOMC.
                       05  DBC-SNX-APAT
                                    PICTURE X(3).
                       05  DBC-SNX-AMAT
                                    PICTURE X(3).
                       05  DBC-SNX-NOMB
                                    PICTURE X(3).
      *                             NUMERO DE DIRECCION DE ENVIO CORRESP
                   04  DBC-NUM-DCOR PICTURE 9(2).
      *                             OPR IND TIPO CLIENTE ( PERSONA O EMP
                   04  DBC-IND-TICL PICTURE X(1).
      *                             TIPO CLIENTE (POTENCIAL,NO CLIENTE,G
                   04  DBC-COD-TICL PICTURE X(2).
      *                             CODIGO CLASIFICACION COMERCIAL
                   04  DBC-COD-CLCM PICTURE X(2).
      *                             RCM CODIGO DEL EJECUTIVO ENCARGADO D
                   04  DBC-COD-EJEC PICTURE X(12).
      *                             OPR CODIGO DE OFICINA DONDE OPERA EL
                   04  DBC-COD-OFIC PICTURE X(3).
      *                             RCM INDICADOR SI OPERA EN OTRA OFICI
                   04  DBC-IND-OOFI PICTURE X(1).
      *                             EXT CODIGO ACTIVIDAD ECONOMICA
                   04  DBC-COD-ACTI PICTURE X(9).
      *                             EXT ORIGEN Y DESTINO SECT (COMP.INST
                   04  DBC-COD-ODSE PICTURE X(3).
      *                             INF FECHA INGRESO AL PLAN
                   04  DBC-FEC-FPLN.
                       05  DBC-NUM-SPLN
                                    PICTURE 9(2).
                       05  DBC-NUM-APLN
                                    PICTURE 9(2).
                       05  DBC-NUM-MPLN
                                    PICTURE 9(2).
                       05  DBC-NUM-DPLN
                                    PICTURE 9(2).
      *                             CODIGO DE SEGMENTO
                   04  DBC-COD-SETO PICTURE X(5).
      *                             PERIODO DE SEGMENTO (SSAAMM)
                   04  DBC-PDO-SETO PICTURE X(6).
      *                             INF INDICADOR DE MULTAS BANCO CENTRA
                   04  DBC-IND-MUBC PICTURE X(1).
      *                             INF INDICADOR DE JUICIOS BANCO CENTR
                   04  DBC-IND-JUBC PICTURE X(1).
      *                             EXT CODIGO ESTADO CLIENTE ( COMO DEU
                   04  DBC-MSC-SCLI PICTURE X(1).
      *                             FECHA ESTADO CLIENTE
                   04  DBC-FEC-FSCL.
                       05  DBC-NUM-SSCL
                                    PICTURE 9(2).
                       05  DBC-NUM-ASCL
                                    PICTURE 9(2).
                       05  DBC-NUM-MSCL
                                    PICTURE 9(2).
                       05  DBC-NUM-DSCL
                                    PICTURE 9(2).
      *                             CODIGO DE BLOQUEO DEL CLIENTE
                   04  DBC-COD-BLOC PICTURE X(2).
      *                             NUMERO TOTAL DE DIRECCIONES
                   04  DBC-NUM-TDIR PICTURE 9(2).
      *                             NUMERO TOTAL DE COMENTARIOS
                   04  DBC-NUM-TCMT PICTURE 9(3).
      *                             FECHA INICIO OPERACION
                   04  DBC-FEC-FIOP.
                       05  DBC-NUM-SIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-AIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-MIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-DIOP
                                    PICTURE 9(2).
      *                             FECHA TERMINO OPERACION
                   04  DBC-FEC-FTOP.
                       05  DBC-NUM-STOP
                                    PICTURE 9(2).
                       05  DBC-NUM-ATOP
                                    PICTURE 9(2).
                       05  DBC-NUM-MTOP
                                    PICTURE 9(2).
                       05  DBC-NUM-DTOP
                                    PICTURE 9(2).
      *                             NUMERO OPERACIONES FECHA TERMINO NUL
                   04  DBC-NUM-OFTN PICTURE 9(3).
      *                             FECHA INGRESO DE DATOS A LA BASE DE
                   04  DBC-FEC-FINP.
                       05  DBC-NUM-SINP
                                    PICTURE 9(2).
                       05  DBC-NUM-AINP
                                    PICTURE 9(2).
                       05  DBC-NUM-MINP
                                    PICTURE 9(2).
                       05  DBC-NUM-DINP
                                    PICTURE 9(2).

       01  REG-DCP.
      *    COPYDD DGC-R-DCP.DGC-E-DCP00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO DCP
           02  DGC-E-DCP00.
      *                             DATOS COMPLEMENTARIOS PERSONAS
               03  DCP.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  DCP-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  DCP-KEY-IREG.
      *                             ID DATOS COMPLEMENTARIOS PERSONAS
                       05  DCP-KEY-IDCP.
      *                             COD. INT. COMP. CLIENTE
                       06  DCP-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  DCP-CAI-ICLI.
                       08  DCP-COD-TCLI
                                    PICTURE X(1).
                       08  DCP-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  DCP-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       05  DCP-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  DCP-FEC-FTRN.
                       07  DCP-NUM-STRN
                                    PICTURE 9(2).
                       07  DCP-NUM-ATRN
                                    PICTURE 9(2).
                       07  DCP-NUM-MTRN
                                    PICTURE 9(2).
                       07  DCP-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  DCP-HRA-HRTR.
                       07  DCP-NUM-HHTR
                                    PICTURE 9(2).
                       07  DCP-NUM-MMTR
                                    PICTURE 9(2).
                       07  DCP-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  DCP-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  DCP-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  DCP-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  DCP-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  DCP-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  DCP-COD-ATRN PICTURE X(12).
      *                             FECHA DE NACIMIENTO
                   04  DCP-FEC-FNAC.
                       05  DCP-NUM-SNAC
                                    PICTURE 9(2).
                       05  DCP-NUM-ANAC
                                    PICTURE 9(2).
                       05  DCP-NUM-MNAC
                                    PICTURE 9(2).
                       05  DCP-NUM-DNAC
                                    PICTURE 9(2).
      *                             CODIGO DEL PAIS DE LA NACIONALIDAD
                   04  DCP-COD-PNAC PICTURE X(3).
      *                             INDICADOR DE SEXO
                   04  DCP-IND-SEXO PICTURE X(1).
      *                             INDICADOR DE ESTADO CIVIL
                   04  DCP-IND-ECIV PICTURE X(3).
      *                             CEDULA DE IDENTIDAD
                   04  DCP-NUM-CIDE PICTURE X(8).
      *                             DIGITO VERIFICADOR CEDULA IDENTIDAD
                   04  DCP-VRF-CIDE PICTURE X(1).
      *                             GABINETE CEDULA DE IDENTIDAD
                   04  DCP-GLS-GABI PICTURE X(12).
      *                             CODIGO TIPO BANCA
                   04  DCP-COD-TBAN PICTURE X(3).
      *                             INDICADOR NIVEL EDUCACIONAL
                   04  DCP-IND-NEDU PICTURE X(3).
      *                             CODIGO DE ACTIVIDAD SECUNDARIA
                   04  DCP-COD-ACTS PICTURE X(9).
      *                             CODIGO DE PROFESION
                   04  DCP-COD-PROF PICTURE X(5).
      *                             INDICADOR DE SITUACION LABORAL
                   04  DCP-IND-SLAB PICTURE X(3).
      *                             CODIGO CATEGORIA SOCIOECONOMICA
                   04  DCP-COD-CSEC PICTURE X(2).
      *                             INDICADOR SITUACION CASA
                   04  DCP-IND-CASA PICTURE X(5).
      *                             # MSJ ASOC A NOMBRE DE PILA (SBT)
                   04  DCP-NUM-NMSG PICTURE 9(4).
      *                             INDICADOR REGIMEN CONYUGAL
                   04  DCP-IND-RCNY PICTURE X(1).
      *                             TIPO PLAN
                   04  DCP-COD-TPLN PICTURE X(1).

       01  REG-DCE.
      *    COPYDD DGC-R-DCE.DGC-E-DCE00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO DCE
           02  DGC-E-DCE00.
      *                             DATOS COMPLEMENTARIOS EMPRESAS
               03  DCE.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  DCE-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  DCE-KEY-IREG.
      *                             IDENTIFICACION DATOS COMPLEMENTARIOS
                       05  DCE-KEY-IDCE.
      *                             COD. INT. COMP. CLIENTE
                       06  DCE-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  DCE-CAI-ICLI.
                       08  DCE-COD-TCLI
                                    PICTURE X(1).
                       08  DCE-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  DCE-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       05  DCE-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  DCE-FEC-FTRN.
                       07  DCE-NUM-STRN
                                    PICTURE 9(2).
                       07  DCE-NUM-ATRN
                                    PICTURE 9(2).
                       07  DCE-NUM-MTRN
                                    PICTURE 9(2).
                       07  DCE-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  DCE-HRA-HRTR.
                       07  DCE-NUM-HHTR
                                    PICTURE 9(2).
                       07  DCE-NUM-MMTR
                                    PICTURE 9(2).
                       07  DCE-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  DCE-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  DCE-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  DCE-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  DCE-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  DCE-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  DCE-COD-ATRN PICTURE X(12).
      *                             NOMBRE FANTASIA
                   04  DCE-GLS-NFAN PICTURE X(40).
      *                             EXT CODIGO TIPO DE SOCIEDAD
                   04  DCE-COD-TSOC PICTURE X(2).
      *                             FECHA CONSTITUCION EMPRESA
                   04  DCE-FEC-FCTT.
                       05  DCE-NUM-SCTT
                                    PICTURE 9(2).
                       05  DCE-NUM-ACTT
                                    PICTURE 9(2).
                       05  DCE-NUM-MCTT
                                    PICTURE 9(2).
                       05  DCE-NUM-DCTT
                                    PICTURE 9(2).
      *                             FECHA ULTIMA MODIFICACION ESCRITURA
                   04  DCE-FEC-FUME.
                       05  DCE-NUM-SUME
                                    PICTURE 9(2).
                       05  DCE-NUM-AUME
                                    PICTURE 9(2).
                       05  DCE-NUM-MUME
                                    PICTURE 9(2).
                       05  DCE-NUM-DUME
                                    PICTURE 9(2).
      *                             FECHA CADUCIDAD EMPRESA
                   04  DCE-FEC-FCAD.
                       05  DCE-NUM-SCAD
                                    PICTURE 9(2).
                       05  DCE-NUM-ACAD
                                    PICTURE 9(2).
                       05  DCE-NUM-MCAD
                                    PICTURE 9(2).
                       05  DCE-NUM-DCAD
                                    PICTURE 9(2).
      *                             INDICADOR RENOVACION AUTOMATICA ( S/
                   04  DCE-IND-RAUT PICTURE X(1).
      *                             OPR INDICADOR CASA MATRIZ O SUCURSAL
                   04  DCE-IND-CMOS PICTURE X(1).
      *                             EXT INDICADOR CATEGORIA ( COMO DEUDO
                   04  DCE-IND-CATE PICTURE X(3).
      *                             EXT TIPO DE EMPRESA  ( COMO DEUDOR )
                   04  DCE-IND-TEMP PICTURE X(3).
      *                             CODIGO TIPO BANCA
                   04  DCE-COD-TBAN PICTURE X(3).
      *                             TIPO PLAN
                   04  DCE-COD-TPLN PICTURE X(1).
      *                             PAIS DE NACIMIENTO
                   04  DCE-COD-PNAC PICTURE X(3).
      *                             DISPONIBLE
                   04  DCE-GLS-DISP PICTURE X(32).

      *
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  ELEM-LIST.
           03 ELEM-1                        PIC X(05).
           03 ELEM-SEC                      PIC X(01).
           03 FILLER                        PIC X(05) VALUE SPACES.

           EJECT
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           PERFORM 10-INICIO
           PERFORM 20-REDKG-DCP
           PERFORM 40-PROCESAR-DCP UNTIL  REQ-DCP-RTN-CODE = '14'
           PERFORM 50-REDKG-DCE
           PERFORM 70-PROCESAR-DCE UNTIL  REQ-DCE-RTN-CODE = '14'
           PERFORM D-TERMINAR.
           GOBACK.

       10-INICIO.
      *----------
           INITIALIZE W-ACUMULADORES
                      W-REG-DE-TRABAJO
                      W-SWITCH.
           ACCEPT  WP-TARJ-PARAM.
           DISPLAY 'FECH. PROCESO  : ' WP-FEC-PROC
                   ' BANCA ACTUAL  : ' WP-TBAN-ACT
                   ' BANCA NUEVA   : ' WP-TBAN-NVA.
           DISPLAY '************************************************'
           DISPLAY '************************************************'.

       20-REDKG-DCP.
      *-------------
           MOVE LOW-VALUE         TO REQ-DCP-KEY-VALUE.
           MOVE 'REDKG'           TO REQ-DCP-CMD.
           MOVE 'DCP'             TO REQ-DCP-TBLNM.
           MOVE 'DCP00'           TO ELEM-1.
           CALL 'DBNTRY' USING UIB REQ-DCP-AREA DCP ELEM-LIST.
           IF REQ-DCP-RTN-CODE NOT = '  ' AND NOT = '14'
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '20-REDKG-DCP      '             TO WR-SECC-ERR
              MOVE 'DCP'                            TO WR-ARCH-ERR
              MOVE 'REDKG'                          TO WR-OPER-ERR
              MOVE REQ-DCP-RTN-CODE                 TO WR-COD-RET
              PERFORM D-TERMINAR
              GOBACK
           ELSE
              ADD  1  TO WS-LEID-DCP.

       30-REDNX-DCP.
      *--------------
           CALL 'DBNTRY' USING UIB REQ-DCP-AREA DCP ELEM-LIST.
           IF REQ-DCP-RTN-CODE NOT = '  ' AND NOT = '14'
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '30-REDNX-DCP      '             TO WR-SECC-ERR
              MOVE 'DCP'                            TO WR-ARCH-ERR
              MOVE 'REDNX'                          TO WR-OPER-ERR
              MOVE REQ-DCP-RTN-CODE                 TO WR-COD-RET
              PERFORM D-TERMINAR
              GOBACK
           ELSE
              ADD  1  TO WS-LEID-DCP
           END-IF.

       40-PROCESAR-DCP.
      *----------------
           IF DCP-COD-TBAN = WP-TBAN-ACT
              PERFORM 41-ACTUALIZAR-DCP
              IF REQ-DCP-RTN-CODE = SPACES
                 MOVE DCP-CIC-ICLI   TO WS-CIC-ICLI
                 PERFORM 44-ACTUALIZAR-DBC
                 IF REQ-DBC-RTN-CODE NOT = SPACES
                    DISPLAY ' DCP-CIC-ICLI NO ENCONTRADO EN DBC '
                              DCP-CIC-ICLI
                 ELSE
                    ADD  1  TO WS-ACTU-DBC
                 END-IF
              END-IF
           END-IF.
           MOVE 'REDNX'           TO REQ-DCP-CMD
           MOVE 'DCP'             TO REQ-DCP-TBLNM
           MOVE 'DCP00'           TO ELEM-1
           PERFORM 30-REDNX-DCP.

       41-ACTUALIZAR-DCP.
      *------------------
           PERFORM 42-RDUKY-DCP.
           IF REQ-DCP-RTN-CODE = '  '
              ADD 1 TO WS-LEID-RDU-DCP
              PERFORM 43-UPDAT-DCP
              ADD 1 TO WS-ACTU-DCP.

       42-RDUKY-DCP.
      *-------------
           MOVE 'RDUKY'           TO REQ-DCP-CMD.
           MOVE 'DCP00'           TO ELEM-1.
           CALL 'DBNTRY' USING UIB REQ-DCP-AREA DCP ELEM-LIST.
           IF REQ-DCP-RTN-CODE NOT = '  ' THEN
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '42-RDUKY-DCP'                   TO WR-SECC-ERR
              MOVE 'DCP'                            TO WR-ARCH-ERR
              MOVE 'RDUKY'                          TO WR-OPER-ERR
              MOVE REQ-DCP-RTN-CODE                 TO WR-COD-RET
              DISPLAY 'DCP-CIC-ICLI NO LEIDO POR RDUKY : '
                       DCP-CIC-ICLI
              PERFORM D-TERMINAR
              GOBACK
           END-IF.

       43-UPDAT-DCP.
      *--------------
           MOVE WP-TBAN-NVA       TO DCP-COD-TBAN.
           MOVE WP-FEC-PROC       TO DCP-FEC-FTRN.
           MOVE 'UPDAT'           TO REQ-DCP-CMD.
           CALL 'DBNTRY' USING UIB REQ-DCP-AREA DCP ELEM-LIST.
           IF REQ-DCP-RTN-CODE NOT = '  ' THEN
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '43-UPDAT-DCP'                   TO WR-SECC-ERR
              MOVE 'DCP'                            TO WR-ARCH-ERR
              MOVE 'UPDAT'                          TO WR-OPER-ERR
              MOVE REQ-DCP-RTN-CODE                 TO WR-COD-RET
              DISPLAY 'DCP-CIC-ICLI NO ACTUALIZADO :'
                       DCP-CIC-ICLI
              PERFORM D-TERMINAR
              GOBACK
           END-IF.

       44-ACTUALIZAR-DBC.
      *-----------------
           PERFORM 45-RDUKY-DBC.
           MOVE REQ-DBC-RTN-CODE TO WS-DBC-RTN-CODE.
           IF REQ-DBC-RTN-CODE = '  '
              ADD 1 TO WS-LEID-RDU-DBC
              PERFORM 46-UPDAT-DBC.

       45-RDUKY-DBC.
      *--------------
           MOVE WS-CIC-ICLI       TO REQ-DBC-KEY-VALUE.
           MOVE 'RDUKY'           TO REQ-DBC-CMD.
           MOVE 'DBC00'           TO ELEM-1.
           CALL 'DBNTRY' USING UIB REQ-DBC-AREA DBC ELEM-LIST.
           IF REQ-DBC-RTN-CODE NOT = '  ' AND NOT = '14' THEN
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '45-RDUKY-DBC'                   TO WR-SECC-ERR
              MOVE 'DBC'                            TO WR-ARCH-ERR
              MOVE 'RDUKY'                          TO WR-OPER-ERR
              MOVE REQ-DBC-RTN-CODE                 TO WR-COD-RET
              PERFORM D-TERMINAR
              GOBACK
           END-IF.

       46-UPDAT-DBC.
      *--------------
           MOVE WP-FEC-PROC       TO DBC-FEC-FTRN.
           MOVE 'UPDAT'           TO REQ-DBC-CMD.
           CALL 'DBNTRY' USING UIB REQ-DBC-AREA DBC ELEM-LIST.
           IF REQ-DBC-RTN-CODE NOT = '  '
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '46-UPDAT-DBC'                   TO WR-SECC-ERR
              MOVE 'DBC'                            TO WR-ARCH-ERR
              MOVE 'UPDAT'                          TO WR-OPER-ERR
              MOVE REQ-DBC-RTN-CODE                 TO WR-COD-RET
              PERFORM D-TERMINAR
              GOBACK
           END-IF.

       50-REDKG-DCE.
      *-------------
           MOVE LOW-VALUE         TO REQ-DCE-KEY-VALUE.
           MOVE 'REDKG'           TO REQ-DCE-CMD.
           MOVE 'DCE'             TO REQ-DCE-TBLNM.
           MOVE 'DCE00'           TO ELEM-1.
           CALL 'DBNTRY' USING UIB REQ-DCE-AREA DCE ELEM-LIST.
           IF REQ-DCE-RTN-CODE NOT = '  ' AND NOT = '14'
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '50-REDKG-DCE      '             TO WR-SECC-ERR
              MOVE 'DCE'                            TO WR-ARCH-ERR
              MOVE 'REDKG'                          TO WR-OPER-ERR
              MOVE REQ-DCE-RTN-CODE                 TO WR-COD-RET
              PERFORM D-TERMINAR
              GOBACK
           ELSE
              ADD  1  TO WS-LEID-DCE
           END-IF.

       60-REDNX-DCE.
      *--------------
           CALL 'DBNTRY' USING UIB REQ-DCE-AREA DCE ELEM-LIST.
           IF REQ-DCE-RTN-CODE NOT = '  ' AND NOT = '14'
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '60-REDNX-DCE      '             TO WR-SECC-ERR
              MOVE 'DCE'                            TO WR-ARCH-ERR
              MOVE 'REDNX'                          TO WR-OPER-ERR
              MOVE REQ-DCE-RTN-CODE                 TO WR-COD-RET
              PERFORM D-TERMINAR
              GOBACK
           ELSE
              ADD  1  TO WS-LEID-DCE
           END-IF.

       70-PROCESAR-DCE.
      *----------------
           IF DCE-COD-TBAN = WP-TBAN-ACT
              PERFORM 71-ACTUALIZAR-DCE
              IF REQ-DCE-RTN-CODE = SPACES
                 MOVE DCE-CIC-ICLI   TO WS-CIC-ICLI
                 PERFORM 44-ACTUALIZAR-DBC
                 IF REQ-DBC-RTN-CODE NOT = SPACES
                    DISPLAY ' DCE-CIC-ICLI NO ENCONTRADO EN DBC '
                              DCE-CIC-ICLI
                 ELSE
                    ADD  1  TO WS-ACTU-DBC
                 END-IF
              END-IF
           END-IF.
           MOVE 'REDNX'           TO REQ-DCE-CMD
           MOVE 'DCE'             TO REQ-DCE-TBLNM
           MOVE 'DCE00'           TO ELEM-1
           PERFORM 60-REDNX-DCE.

       71-ACTUALIZAR-DCE.
      *------------------
           PERFORM 72-RDUKY-DCE.
           IF REQ-DCE-RTN-CODE = '  '
              ADD 1 TO WS-LEID-RDU-DCE
              PERFORM 73-UPDAT-DCE
              ADD 1 TO WS-ACTU-DCE.

       72-RDUKY-DCE.
      *-------------
           MOVE 'RDUKY'           TO REQ-DCE-CMD.
           MOVE 'DCE00'           TO ELEM-1.
           CALL 'DBNTRY' USING UIB REQ-DCE-AREA DCE ELEM-LIST.
           IF REQ-DCE-RTN-CODE NOT = '  ' AND NOT = '14' THEN
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '42-RDUKY-DCE'                   TO WR-SECC-ERR
              MOVE 'DCE'                            TO WR-ARCH-ERR
              MOVE 'RDUKY'                          TO WR-OPER-ERR
              MOVE REQ-DCE-RTN-CODE                 TO WR-COD-RET
              DISPLAY 'DCE-CIC-ICLI NO LEIDO POR RDUKY : '
                       DCE-CIC-ICLI
              PERFORM D-TERMINAR
              GOBACK
           END-IF.

       73-UPDAT-DCE.
      *--------------
           MOVE WP-TBAN-NVA       TO DCE-COD-TBAN.
           MOVE WP-FEC-PROC       TO DCE-FEC-FTRN.
           MOVE 'UPDAT'           TO REQ-DCE-CMD.
           CALL 'DBNTRY' USING UIB REQ-DCE-AREA DCE ELEM-LIST.
           IF REQ-DCE-RTN-CODE NOT = '  ' THEN
              MOVE 100                              TO WS-RETURN-CODE
              MOVE '73-UPDAT-DCE'                   TO WR-SECC-ERR
              MOVE 'DCE'                            TO WR-ARCH-ERR
              MOVE 'UPDAT'                          TO WR-OPER-ERR
              MOVE REQ-DCE-RTN-CODE                 TO WR-COD-RET
              DISPLAY 'DCE-CIC-ICLI NO ACTUALIZADO :'
                       DCE-CIC-ICLI
              PERFORM D-TERMINAR
              GOBACK
           END-IF.


       D-TERMINAR.
      *-----------
           PERFORM DB-ENVIA-MSG-SEGUN-TERM.
           PERFORM DC-ESTADISTICAS.
           PERFORM DD-VERIFICA-PROCESO.

       DB-ENVIA-MSG-SEGUN-TERM.
      *------------------------
           IF WS-RETURN-CODE = 00 THEN
              DISPLAY CC-TAB '***************************************'
                              UPON CONSOLE
              DISPLAY CC-TAB '*   PROCESO TERMINO EN FORMA NORMAL   *'
                              UPON CONSOLE
              DISPLAY CC-TAB '***************************************'
                              UPON CONSOLE
              DISPLAY CC-TAB '***************************************'
              DISPLAY CC-TAB '*   PROCESO TERMINO EN FORMA NORMAL   *'
              DISPLAY CC-TAB '***************************************'
           ELSE
              MOVE 100 TO RETURN-CODE
              DISPLAY CC-TAB '***************************************'
                              UPON CONSOLE
              DISPLAY CC-TAB '***************************************'
                              UPON CONSOLE
              DISPLAY CC-TAB '***   TERMINO ERRONEO DEL PROCESO   ***'
                              UPON CONSOLE
              DISPLAY CC-TAB '***************************************'
                              UPON CONSOLE
              DISPLAY CC-TAB '***************************************'
                              UPON CONSOLE
              DISPLAY CC-TAB
              DISPLAY CC-TAB '***************************************'
              DISPLAY CC-TAB '***************************************'
              DISPLAY CC-TAB '***   TERMINO ERRONEO DEL PROCESO   ***'
              DISPLAY CC-TAB '***************************************'
              DISPLAY CC-TAB '***************************************'
              PERFORM DE-MOSTRAR-ERROR
           END-IF.

       DC-ESTADISTICAS.
      *----------------
           DISPLAY CC-TAB.
           DISPLAY CC-TAB '***************************************'.
           DISPLAY CC-TAB '*                                     *'.
           DISPLAY CC-TAB '*     ESTADISTICAS DEL  PROCESO       *'.
           DISPLAY CC-TAB '*     PROGRAMA      :  SGCPTBA8       *'.
           DISPLAY CC-TAB '*                                     *'.
           DISPLAY CC-TAB '***************************************'.
           DISPLAY CC-TAB
           DISPLAY CC-TAB 'REG. LEIDOS RDUKY DBC  : ' WS-LEID-RDU-DBC
           DISPLAY CC-TAB 'REG. MODIFICADOS  DBC  : ' WS-ACTU-DBC
           DISPLAY CC-TAB
           DISPLAY CC-TAB 'REG. LEIDOS TABLA DCP  : ' WS-LEID-DCP
           DISPLAY CC-TAB 'REG. LEIDOS RDUKY DCP  : ' WS-LEID-RDU-DCP
           DISPLAY CC-TAB 'REG. MODIFICADOS  DCP  : ' WS-ACTU-DCP
           DISPLAY CC-TAB
           DISPLAY CC-TAB 'REG. LEIDOS TABLA DCE  : ' WS-LEID-DCE
           DISPLAY CC-TAB 'REG. LEIDOS RDUKY DCE  : ' WS-LEID-RDU-DCE
           DISPLAY CC-TAB 'REG. MODIFICADOS  DCE  : ' WS-ACTU-DCE
           DISPLAY CC-TAB
           DISPLAY CC-TAB '***************************************'.

       DD-VERIFICA-PROCESO.
      *--------------------
           IF WS-LEID-DCP = 0 AND WS-LEID-DCE = 0 AND WS-LEID-DBC = 0
              AND WS-RETURN-CODE = 0
              DISPLAY CC-TAB
              DISPLAY CC-TAB '***************************************'
              DISPLAY CC-TAB '*  NO SE ENCONTRARON REGISTROS EN LAS *'
              DISPLAY CC-TAB '* TABLAS DBC DCP Y DCE PARA MODIFICAR *'
              DISPLAY CC-TAB '***************************************'
           END-IF.

       DE-MOSTRAR-ERROR.
      *-----------------
           DISPLAY CC-TAB '*****************************************'.
           DISPLAY CC-TAB '* ERROR FUE EN SECCION : ' WR-SECC-ERR.
           DISPLAY CC-TAB '* TABLA                : ' WR-ARCH-ERR.
           DISPLAY CC-TAB '* OPERACION            : ' WR-OPER-ERR.
           DISPLAY CC-TAB '* CODIGO DE RETORNO    : ' WR-COD-RET.
           DISPLAY CC-TAB '* LLAVE REG. PROCESADO : ' WR-REG-ERR.
