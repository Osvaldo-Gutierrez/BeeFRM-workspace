       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.  SGCP176.
       AUTHOR.      RENE MAHMUD V.
      *REMARKS.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      **************************************************************
      *                                                            *
      *  OBJETIVOS:                                                *
      *              ACTUALIZAR EN TABLA DCC LOS CONTADORES        *
      *              PARA PROTESTOS Y ACLARACIONES VIGENTES Y      *
      *              HISTORICOS.                                   *
      *                                                            *
      *   ENTRADA :                                                *
      *              RCO CHP PHI                                   *
      *                                                            *
      *   ENT/SAL :                                                *
      *              DCC                                           *
      *                                                            *
      **************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      *====================
           SKIP1
       CONFIGURATION SECTION.
      *---------------------
       SOURCE-COMPUTER. IBM-4381-P11.
       OBJECT-COMPUTER. IBM-4381-P11.
       SPECIAL-NAMES.
              C01 IS CANAL-1
              DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============

       WORKING-STORAGE SECTION.
      *-----------------------
       01  WC-CONTADORES.
           02 WC-LEI-RCO           PIC 9(06) VALUE 0.
           02 WC-DIST-RCO          PIC 9(06) VALUE 0.
           02 WC-LEI-CHP           PIC 9(06) VALUE 0.
           02 WC-EXI-CHP           PIC 9(06) VALUE 0.
           02 WC-INE-CHP           PIC 9(06) VALUE 0.
           02 WC-LEI-PHI           PIC 9(06) VALUE 0.
           02 WC-EXI-PHI           PIC 9(06) VALUE 0.
           02 WC-INE-PHI           PIC 9(06) VALUE 0.
           02 WC-LEI-DCC           PIC 9(06) VALUE 0.
           02 WC-INE-DCC           PIC 9(06) VALUE 0.
           02 WC-GRA-DCC           PIC 9(06) VALUE 0.
           02 WC-ACTU-DCC          PIC 9(06) VALUE 0.
           02 WC-BIEN-DCC          PIC 9(06) VALUE 0.

       01  WS-VARIABLES.
           02 WS-NUM-PVIG          PIC 9(03) VALUE 0.
           02 WS-NUM-AVIG          PIC 9(03) VALUE 0.
           02 WS-NUM-PHIS          PIC 9(03) VALUE 0.
           02 WS-NUM-AHIS          PIC 9(03) VALUE 0.
           02 WS-FEC-FACL          PIC X(08).
           02 WS-FEC-FCUR.
              03 WS-NUM-MCUR       PIC X(02).
              03 FILLER            PIC X(01).
              03 WS-NUM-DCUR       PIC X(02).
              03 FILLER            PIC X(01).
              03 WS-NUM-ACUR       PIC X(02).
           02 WS-RCO-ICLI          PIC X(12).
           02 BLANCO               PIC X(12) VALUE SPACES.


      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************

       01  UIB.
           05 FILLER               PIC X(32) VALUE 'SGCP176'.

      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************

       01  RCO-REQ-AREA.
           02 RCO-REQ-CMD          PIC X(5).
           02 RCO-REQ-TBLNM        PIC X(3) VALUE 'RCO'.
           02 RCO-REQ-KEYNM        PIC X(5) VALUE 'RCO00'.
           02 RCO-REQ-RETURN-CODE  PIC X(2) VALUE SPACES.
           02 RCO-REQ-INT-RTN-CD   PIC X(1).
           02 RCO-REQ-DBID         PIC S9(4) COMP VALUE +100.
           02 RCO-REQ-TBLID        PIC X(7).
           02 FILLER               PIC X(25).
           02 RCO-REQ-COUNT-MAX    PIC S9(4) COMP.
           02 RCO-REQ-IO-COUNT     PIC S9(4) COMP.
           02 FILLER               PIC X(22).
           02 RCO-REQ-KEY-VALUE.
              03 RCO-REQ-ICLI      PIC X(12).
              03 RCO-REQ-IOPE      PIC X(12).

       01  CHP-REQ-AREA.
           02 CHP-REQ-CMD          PIC X(5).
           02 CHP-REQ-TBLNM        PIC X(3) VALUE 'CHP'.
           02 CHP-REQ-KEYNM        PIC X(5) VALUE 'CHP00'.
           02 CHP-REQ-RETURN-CODE  PIC X(2) VALUE SPACES.
           02 CHP-REQ-INT-RTN-CD   PIC X(1).
           02 CHP-REQ-DBID         PIC S9(4) COMP VALUE +100.
           02 CHP-REQ-TBLID        PIC X(7).
           02 FILLER               PIC X(25).
           02 CHP-REQ-COUNT-MAX    PIC S9(4) COMP.
           02 CHP-REQ-IO-COUNT     PIC S9(4) COMP.
           02 FILLER               PIC X(22).
           02 CHP-REQ-KEY-VALUE.
              05 CHP-REQ-IOPE      PIC X(12).
              05 CHP-REQ-DOC       PIC X(07).
              05 CHP-REQ-FEC       PIC X(08).

       01  PHI-REQ-AREA.
           02 PHI-REQ-CMD          PIC X(5).
           02 PHI-REQ-TBLNM        PIC X(3) VALUE 'PHI'.
           02 PHI-REQ-KEYNM        PIC X(5) VALUE 'PHI00'.
           02 PHI-REQ-RETURN-CODE  PIC X(2) VALUE SPACES.
           02 PHI-REQ-INT-RTN-CD   PIC X(1).
           02 PHI-REQ-DBID         PIC S9(4) COMP VALUE +104.
           02 PHI-REQ-TBLID        PIC X(7).
           02 FILLER               PIC X(25).
           02 PHI-REQ-COUNT-MAX    PIC S9(4) COMP.
           02 PHI-REQ-IO-COUNT     PIC S9(4) COMP.
           02 FILLER               PIC X(22).
           02 PHI-REQ-KEY-VALUE.
              05 PHI-REQ-IOPE      PIC X(12).
              05 PHI-REQ-DOC       PIC X(07).
              05 PHI-REQ-FEC       PIC X(08).

       01  DCC-REQ-AREA.
           02 DCC-REQ-CMD          PIC X(5).
           02 DCC-REQ-TBLNM        PIC X(3) VALUE 'DCC'.
           02 DCC-REQ-KEYNM        PIC X(5) VALUE 'DCC00'.
           02 DCC-REQ-RETURN-CODE  PIC X(2) VALUE SPACES.
           02 DCC-REQ-INT-RTN-CD   PIC X(1).
           02 DCC-REQ-DBID         PIC S9(4) COMP VALUE +100.
           02 DCC-REQ-TBLID        PIC X(7).
           02 FILLER               PIC X(25).
           02 DCC-REQ-COUNT-MAX    PIC S9(4) COMP.
           02 DCC-REQ-IO-COUNT     PIC S9(4) COMP.
           02 FILLER               PIC X(22).
           02 DCC-REQ-KEY-VALUE    PIC X(12).


      *****************************************************************
      *              WORK AREA                                        *
      *****************************************************************

       01  REG-CHP.
      *    COPYDD DGC-R-CHP.DGC-E-CHP00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO CHP
           02  DGC-E-CHP00.
      *                             DOCUMENTOS PROTESTADOS
               03  CHP.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  CHP-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  CHP-KEY-IREG.
      *                             IDENTIFICACION DOCTOS PROTESTADOS
                       05  CHP-KEY-ICHP.
      *                             COD. INT. COMP. OPERACION
                       06  CHP-CIC-ICCT.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  CHP-CAI-ICCT.
                       08  CHP-COD-TCCT
                                    PICTURE X(1).
                       08  CHP-COD-CCCT
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  CHP-IIC-ICCT.
                       08  CHP-COD-OCCT.
                       09  CHP-COD-SCCT
                                    PICTURE X(2).
                       09  CHP-IND-SCCT
                                    PICTURE X(1).
                       08  CHP-GLS-NCCT
                                    PICTURE X(5).
      *                             NUMERO IDENTIFICACION DOCTO
                       06  CHP-NUM-ICHP
                                    PICTURE X(7).
      *                             FECHA PROTESTO
                       06  CHP-FEC-FCHP.
                       07  CHP-NUM-SCHP
                                    PICTURE 9(2).
                       07  CHP-NUM-ACHP
                                    PICTURE 9(2).
                       07  CHP-NUM-MCHP
                                    PICTURE 9(2).
                       07  CHP-NUM-DCHP
                                    PICTURE 9(2).
      *                             TIME STAMP TRANSACCION
                       05  CHP-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  CHP-FEC-FTRN.
                       07  CHP-NUM-STRN
                                    PICTURE 9(2).
                       07  CHP-NUM-ATRN
                                    PICTURE 9(2).
                       07  CHP-NUM-MTRN
                                    PICTURE 9(2).
                       07  CHP-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  CHP-HRA-HRTR.
                       07  CHP-NUM-HHTR
                                    PICTURE 9(2).
                       07  CHP-NUM-MMTR
                                    PICTURE 9(2).
                       07  CHP-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  CHP-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  CHP-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  CHP-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  CHP-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  CHP-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  CHP-COD-ATRN PICTURE X(12).
      *                             TIPO DOCUMENTO
                   04  CHP-COD-TDOC PICTURE X(3).
      *                             MONTO DOCTO
                   04  CHP-VAL-CHEQ PICTURE 9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             MOTIVO PROTESTO
                   04  CHP-COD-MOTP PICTURE X(2).
      *                             INDICADOR ENVIO A BOLETIN
                   04  CHP-IND-EBOL PICTURE X(1).
      *                             FECHA ACLARACION PROTESTO
                   04  CHP-FEC-FACL.
                       05  CHP-NUM-SACL
                                    PICTURE 9(2).
                       05  CHP-NUM-AACL
                                    PICTURE 9(2).
                       05  CHP-NUM-MACL
                                    PICTURE 9(2).
                       05  CHP-NUM-DACL
                                    PICTURE 9(2).
      *                             NUMERO BOLETIN PUBLICACION PROTESTO
                   04  CHP-NUM-BOLE PICTURE X(5).
      *                             NUMERO PAGINA PUBLICACION PROTESTO
                   04  CHP-NUM-PAGE PICTURE X(4).
      *                             NUMERO NOMINA ENVIO PROTESTO
                   04  CHP-NUM-NOMI PICTURE X(5).
      *                             VALOR CAMBIO MONTO DOCTO
                   04  CHP-COD-VCMC.
      *                             MISCELANEO TIPO VALOR
                       05  CHP-MSC-TVMC
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       05  CHP-COD-CVMC
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       05  CHP-IND-TCMC
                                    PICTURE X(2).
      *                             OFICINA OPERACION
                   04  CHP-COD-OFIC PICTURE X(3).
      *                             DISPONIBLE
                   04  CHP-GLS-DISP PICTURE X(12).

       01  REG-PHI.
      *    COPYDD PIN-R-PHI.PIN-E-PHI00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO PHI
           02  PIN-E-PHI00.
      *                             DOCUMENTOS PROTESTADOS HISTORICOS.
               03  PHI.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  PHI-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  PHI-KEY-IREG.
      *                             IDENTIFICACION DOCTOS PROTESTADOS
                       05  PHI-KEY-ICHP.
      *                             COD. INT. COMP. OPERACION
                       06  PHI-CIC-ICCT.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  PHI-CAI-ICCT.
                       08  PHI-COD-TCCT
                                    PICTURE X(1).
                       08  PHI-COD-CCCT
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  PHI-IIC-ICCT.
                       08  PHI-COD-OCCT.
                       09  PHI-COD-SCCT
                                    PICTURE X(2).
                       09  PHI-IND-SCCT
                                    PICTURE X(1).
                       08  PHI-GLS-NCCT
                                    PICTURE X(5).
      *                             NUMERO IDENTIFICACION DOCTO
                       06  PHI-NUM-ICHP
                                    PICTURE X(7).
      *                             FECHA PROTESTO
                       06  PHI-FEC-FCHP.
                       07  PHI-NUM-SCHP
                                    PICTURE 9(2).
                       07  PHI-NUM-ACHP
                                    PICTURE 9(2).
                       07  PHI-NUM-MCHP
                                    PICTURE 9(2).
                       07  PHI-NUM-DCHP
                                    PICTURE 9(2).
      *                             TIME STAMP TRANSACCION
                       05  PHI-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  PHI-FEC-FTRN.
                       07  PHI-NUM-STRN
                                    PICTURE 9(2).
                       07  PHI-NUM-ATRN
                                    PICTURE 9(2).
                       07  PHI-NUM-MTRN
                                    PICTURE 9(2).
                       07  PHI-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  PHI-HRA-HRTR.
                       07  PHI-NUM-HHTR
                                    PICTURE 9(2).
                       07  PHI-NUM-MMTR
                                    PICTURE 9(2).
                       07  PHI-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  PHI-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  PHI-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  PHI-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  PHI-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  PHI-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  PHI-COD-ATRN PICTURE X(12).
      *                             TIPO DOCUMENTO
                   04  PHI-COD-TDOC PICTURE X(3).
      *                             MONTO DOCTO
                   04  PHI-VAL-CHEQ PICTURE 9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             MOTIVO PROTESTO
                   04  PHI-COD-MOTP PICTURE X(2).
      *                             INDICADOR ENVIO A BOLETIN
                   04  PHI-IND-EBOL PICTURE X(1).
      *                             FECHA ACLARACION PROTESTO
                   04  PHI-FEC-FACL.
                       05  PHI-NUM-SACL
                                    PICTURE 9(2).
                       05  PHI-NUM-AACL
                                    PICTURE 9(2).
                       05  PHI-NUM-MACL
                                    PICTURE 9(2).
                       05  PHI-NUM-DACL
                                    PICTURE 9(2).
      *                             NUMERO BOLETIN PUBLICACION PROTESTO
                   04  PHI-NUM-BOLE PICTURE X(5).
      *                             NUMERO PAGINA PUBLICACION PROTESTO
                   04  PHI-NUM-PAGE PICTURE X(4).
      *                             NUMERO NOMINA ENVIO PROTESTO
                   04  PHI-NUM-NOMI PICTURE X(5).
      *                             VALOR CAMBIO MONTO DOCTO
                   04  PHI-COD-VCMC.
      *                             MISCELANEO TIPO VALOR
                       05  PHI-MSC-TVMC
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       05  PHI-COD-CVMC
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       05  PHI-IND-TCMC
                                    PICTURE X(2).
      *                             OFICINA OPERACION
                   04  PHI-COD-OFIC PICTURE X(3).
      *                             DISPONIBLE
                   04  PHI-GLS-DISP PICTURE X(12).

      *01  REG-DCC.
      *    COPYDD DGC-R-DCC.DGC-E-DCC00(PROD),01,D
      *    Datos Complementarios Clientes
      *    ------------------------------
      *
      *    Nombre Registro : DCC
      *    Clave(s)        : DCC-KEY-IDCC(UU)
      *    Largo           : 092
      *    Bloqueo         : 1
      *    Observaciones   :
      *
      *    01) Modificaciones 03/03/88, LJV
      *    02) Modificaciones 09/03/88, LJV
      *    04) Modificaciones 27/04/89, ALD
      *    05) Modificaciones 18/05/94, LRH
      *
       01  DCC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  DCC-GLS-FLAG                             PIC X(01).
      *
      *KEY Clave Primaria Registro
           03  DCC-KEY-IREG.
      *
      *        Identificacion Informes Comerciales
               05  DCC-KEY-IDCC.
      *
      *            Cod. Int. Comp. Cliente
                   07  DCC-CIC-ICLI.
      *
      *                Centro Asignacion Identificacion
                       09  DCC-CAI-ICLI.
                           11  DCC-COD-TCLI             PIC X(01).
                           11  DCC-COD-CCLI             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  DCC-IIC-ICLI                 PIC X(08).
      *
      *        Time Stamp Transaccion
               05  DCC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  DCC-FEC-FTRN.
                       09  DCC-NUM-STRN                 PIC 9(02).
                       09  DCC-NUM-ATRN                 PIC 9(02).
                       09  DCC-NUM-MTRN                 PIC 9(02).
                       09  DCC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  DCC-HRA-HRTR.
                       09  DCC-NUM-HHTR                 PIC 9(02).
                       09  DCC-NUM-MMTR                 PIC 9(02).
                       09  DCC-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  DCC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  DCC-MSC-STAT                             PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  DCC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  DCC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  DCC-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion                            TAB USR
           03  DCC-COD-ATRN                             PIC X(12).
      *
      *    Nro Total Doctos Protestados
           03  DCC-NUM-TCHP                             PIC 9(03).
      *
      *    Nro Total Doctos Aclarados
           03  DCC-NUM-TCHA                             PIC 9(03).
      *
      *MOD-01  NOM DCC-IND-PBOL PIC X(01)
      *MOD-03  NOM DCC-NUM-PBOL PIC X(03)
      *    Indicador Protestos en Boletin
           03  DCC-IND-PBOL                             PIC X(01).
      *
      *    Nro Unidades Periodo Bloqueo Cuentacorriente
           03  DCC-NUM-UPBC                             PIC 9(03).
      *
      *    Unidad Tiempo Periodo Bloqueo Cuentacorriente
           03  DCC-IND-UTBC                             PIC X(01).
      *
      *    Fecha Inicio Bloqueo Cuentacorriente
           03  DCC-FEC-FIBC.
               05  DCC-NUM-SIBC                         PIC 9(02).
               05  DCC-NUM-AIBC                         PIC 9(02).
               05  DCC-NUM-MIBC                         PIC 9(02).
               05  DCC-NUM-DIBC                         PIC 9(02).
      *
      *MOD-04
      *    Nro Total de Doctos. Protestados Historicos
           03  DCC-NUM-TDPH                             PIC 9(03).
      *
      *MOD-04
      *    Nro Total de Doctos. Aclarados Historicos
           03  DCC-NUM-TDAH                             PIC 9(03).
      *MOD-05
      *    Rechazo apertura de cta. cte.
           03  DCC-COD-RCHZ                             PIC X(01).
      *MOD-05
      *    Oficina de rechazo apertura de cta. cte.
           03  DCC-COD-OFIR                             PIC X(03).
      *
      *MOD-01  PIC X(11)
      *MOD-02  PIC X(09)
      *MOD-03  PIC X(26)
      *MOD-04  PIC X(28)
      *MOD-05  PIC X(22)
      *    Disponible
           03  DCC-GLS-DISP                             PIC X(18).

      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************

       01  ELEM-LIST.
           03 ELEM           PIC X(05).
           03 ELEM-SEC       PIC X(01).
           03 FILLER         PIC X(05) VALUE SPACES.

      *
           COPY RLOVR001.
       77  TIME-OF-DAY   PIC X(6).
       01  LEGA-DATE-BLOCK.
           05 COBOL-II-DATE.
              10 COBOL-II-YEAR
                         PIC XX.
              10 COBOL-II-MONTH
                         PIC XX.
              10 COBOL-II-DAY
                         PIC XX.
           05 OSVS-CURRENT-DATE.
              10 CURRENT-DATE-MONTH
                         PIC XX.
              10 FILLER  PIC X         VALUE '/'.
              10 CURRENT-DATE-DAY
                         PIC XX.
              10 FILLER  PIC X         VALUE '/'.
              10 CURRENT-DATE-YEAR
                         PIC XX.
      *-----------------------------------------------------------------
       PROCEDURE DIVISION.
      *==================
           ENTRY   'DBMSCBL'.
       AA-INICIO.
           PERFORM B-INICIAR.
       AB-PROCESO.
           PERFORM C-PROCESAR.
       AC-TERMINO.
           PERFORM D-TERMINAR.
       A-FIN-PROGRAMA.
           GOBACK.

       B-INICIAR SECTION.
      *==================
       BA-CONTROL-PROCESO.
      *-------------------
           PERFORM BB-LEE-CURRENT-DATE.
           PERFORM BC-LEE-PRIMER-RCO.
           GO TO BZ-FIN-SECTION.

       BB-LEE-CURRENT-DATE.
           ACCEPT COBOL-II-DATE FROM DATE
      *--------------------
           MOVE COBOL-II-YEAR TO CURRENT-DATE-YEAR
           MOVE COBOL-II-MONTH TO CURRENT-DATE-MONTH
           MOVE COBOL-II-DAY TO CURRENT-DATE-DAY
           MOVE OSVS-CURRENT-DATE TO WS-FEC-FCUR.

       BC-LEE-PRIMER-RCO.
      *------------------
           MOVE 'LOCKY' TO RCO-REQ-CMD.
           MOVE LOW-VALUE TO RCO-REQ-KEY-VALUE.
           CALL 'DBNTRY' USING UIB RCO-REQ-AREA.
           IF RCO-REQ-RETURN-CODE NOT = '  '
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: LOCKY TABLA RCO *'
              DISPLAY BLANCO '* REQ. AREA : ' RCO-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.
           ADD 1 TO WC-LEI-RCO.

       BZ-FIN-SECTION.
      *---------------
           EXIT.

       C-PROCESAR SECTION.
      *===================
       CA-CONTROL-PROCESO.
      *-------------------
           MOVE RCO-REQ-ICLI TO WS-RCO-ICLI.
           PERFORM CB-PROCESO UNTIL RCO-REQ-RETURN-CODE = '14'.
           GO TO CZ-FIN-SECTION.

       CB-PROCESO.
      *-----------
           PERFORM CC-LEE-RCO
                   UNTIL RCO-REQ-ICLI NOT = WS-RCO-ICLI
                      OR RCO-REQ-RETURN-CODE = '14'.
           ADD 1 TO WC-DIST-RCO.
           PERFORM CF-LEE-DCC.
           MOVE 0 TO WS-NUM-PVIG.
           MOVE 0 TO WS-NUM-AVIG.
           MOVE 0 TO WS-NUM-PHIS.
           MOVE 0 TO WS-NUM-AHIS.
           MOVE RCO-REQ-ICLI TO WS-RCO-ICLI.

       CC-LEE-RCO.
      *-----------
           PERFORM CD-LOCKY-PRIMER-CHP
           PERFORM CE-LOCKY-PRIMER-PHI

           MOVE 'LOCNX'      TO RCO-REQ-CMD.
           CALL 'DBNTRY' USING UIB RCO-REQ-AREA.
           IF RCO-REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: LOCNX TABLA RCO *'
              DISPLAY BLANCO '* REQ. AREA : ' RCO-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.
           IF RCO-REQ-RETURN-CODE = '  '
              ADD 1 TO WC-LEI-RCO.

       CD-LOCKY-PRIMER-CHP.
      *--------------------
           MOVE 'LOCKY'      TO CHP-REQ-CMD.
           MOVE RCO-REQ-IOPE TO CHP-REQ-IOPE
           MOVE LOW-VALUE    TO CHP-REQ-DOC
           MOVE LOW-VALUE    TO CHP-REQ-FEC
           CALL 'DBNTRY' USING UIB CHP-REQ-AREA.
           IF CHP-REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: LOCKY TABLA CHP *'
              DISPLAY BLANCO '* REQ. AREA : ' CHP-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.

           IF CHP-REQ-RETURN-CODE = '  '
              PERFORM CD1-LEE-PRIMER-CHP
              PERFORM CD2-LEE-NEXT-CHP
                      UNTIL CHP-REQ-IOPE NOT = RCO-REQ-IOPE
                         OR CHP-REQ-RETURN-CODE = '14'
           ELSE
              ADD 1 TO WC-INE-CHP.

       CD1-LEE-PRIMER-CHP.
      *------------------
           MOVE 'REDKY' TO CHP-REQ-CMD.
           MOVE 'CHP00' TO ELEM.
           CALL 'DBNTRY' USING UIB CHP-REQ-AREA CHP ELEM-LIST.
           IF RCO-REQ-RETURN-CODE NOT = '  '
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: REDKY TABLA CHP *'
              DISPLAY BLANCO '* REQ. AREA : ' CHP-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.
           IF CHP-REQ-IOPE = RCO-REQ-IOPE
              ADD 1 TO WC-LEI-CHP
              ADD 1 TO WC-EXI-CHP
           ELSE
              ADD 1 TO WC-INE-CHP.

       CD2-LEE-NEXT-CHP.
      *----------------
           ADD 1 TO WS-NUM-PVIG.
           MOVE CHP-FEC-FACL TO WS-FEC-FACL
           IF WS-FEC-FACL NOT = '00000000' AND NOT = ' 0 0 0 0'
              ADD 1 TO WS-NUM-AVIG.
           MOVE 'REDNX'      TO CHP-REQ-CMD.
           CALL 'DBNTRY' USING UIB CHP-REQ-AREA CHP ELEM-LIST.
           IF CHP-REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: REDNK TABLA CHP *'
              DISPLAY BLANCO '* REQ. AREA : ' CHP-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.
           IF CHP-REQ-RETURN-CODE = '  '
              IF CHP-REQ-IOPE = RCO-REQ-IOPE
                 ADD 1 TO WC-LEI-CHP.

       CE-LOCKY-PRIMER-PHI.
      *--------------------
           MOVE 'LOCKY'      TO PHI-REQ-CMD.
           MOVE RCO-REQ-IOPE TO PHI-REQ-IOPE
           MOVE LOW-VALUE    TO PHI-REQ-DOC
           MOVE LOW-VALUE    TO PHI-REQ-FEC
           CALL 'DBNTRY' USING UIB PHI-REQ-AREA.
           IF PHI-REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: LOCKY TABLA PHI *'
              DISPLAY BLANCO '* REQ. AREA : ' PHI-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.

           IF PHI-REQ-RETURN-CODE = '  '
              PERFORM CE1-LEE-PRIMER-PHI
              PERFORM CE2-LEE-NEXT-PHI
                      UNTIL PHI-REQ-IOPE NOT = RCO-REQ-IOPE
                         OR PHI-REQ-RETURN-CODE = '14'
           ELSE
              ADD 1 TO WC-INE-PHI.

       CE1-LEE-PRIMER-PHI.
      *------------------
           MOVE 'REDKY' TO PHI-REQ-CMD.
           MOVE 'PHI00' TO ELEM.
           CALL 'DBNTRY' USING UIB PHI-REQ-AREA PHI ELEM-LIST.
           IF RCO-REQ-RETURN-CODE NOT = '  '
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: REDKY TABLA PHI *'
              DISPLAY BLANCO '* REQ. AREA : ' PHI-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.
           IF PHI-REQ-IOPE = RCO-REQ-IOPE
              ADD 1 TO WC-LEI-PHI
              ADD 1 TO WC-EXI-PHI
           ELSE
              ADD 1 TO WC-INE-PHI.

       CE2-LEE-NEXT-PHI.
      *----------------
           ADD 1 TO WS-NUM-PHIS.
           MOVE PHI-FEC-FACL TO WS-FEC-FACL
           IF WS-FEC-FACL NOT = '00000000' AND NOT = ' 0 0 0 0'
              ADD 1 TO WS-NUM-AHIS.
           MOVE 'REDNX'      TO PHI-REQ-CMD.
           CALL 'DBNTRY' USING UIB PHI-REQ-AREA PHI ELEM-LIST.
           IF PHI-REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: REDNK TABLA PHI *'
              DISPLAY BLANCO '* REQ. AREA : ' PHI-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.
           IF PHI-REQ-RETURN-CODE = '  '
              IF PHI-REQ-IOPE = RCO-REQ-IOPE
                 ADD 1 TO WC-LEI-PHI.

       CF-LEE-DCC.
      *-----------
           MOVE 'REDKY'     TO DCC-REQ-CMD.
           MOVE 'DCC00'     TO ELEM.
           MOVE WS-RCO-ICLI TO DCC-REQ-KEY-VALUE
           CALL 'DBNTRY' USING UIB DCC-REQ-AREA DCC ELEM-LIST.
           IF DCC-REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: REDKY TABLA DCC *'
              DISPLAY BLANCO '* REQ. AREA : ' CHP-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.

           IF DCC-REQ-RETURN-CODE = '  '
              ADD 1 TO WC-LEI-DCC
              IF DCC-NUM-TCHP NUMERIC AND DCC-NUM-TCHA NUMERIC AND
                 DCC-NUM-TDPH NUMERIC AND DCC-NUM-TDAH NUMERIC
                 IF WS-NUM-PVIG = DCC-NUM-TCHP AND
                    WS-NUM-AVIG = DCC-NUM-TCHA AND
                    WS-NUM-PHIS = DCC-NUM-TDPH AND
                    WS-NUM-AHIS = DCC-NUM-TDAH
                    ADD 1 TO WC-BIEN-DCC
                 ELSE
                 DISPLAY 'DIFERENCIAS; CIC:' DCC-REQ-KEY-VALUE
                 DISPLAY 'PROT VIGE RCO: ' WS-NUM-PVIG  ' ' WS-NUM-AVIG
                 DISPLAY 'PROT HIST RCO: ' WS-NUM-PHIS  ' ' WS-NUM-AHIS
                 DISPLAY 'PROT VIGE DCC: ' DCC-NUM-TCHP ' ' DCC-NUM-TCHA
                 DISPLAY 'PROT HIST DCC: ' DCC-NUM-TDPH ' ' DCC-NUM-TDAH
                 DISPLAY '  '
                 ADD 1 TO WC-ACTU-DCC
                 PERFORM CG-MODIF-DCC
              ELSE
                 DISPLAY 'DIFERENCIAS; CIC:' DCC-REQ-KEY-VALUE
                 DISPLAY 'PROT VIGE RCO: ' WS-NUM-PVIG  ' ' WS-NUM-AVIG
                 DISPLAY 'PROT HIST RCO: ' WS-NUM-PHIS  ' ' WS-NUM-AHIS
                 DISPLAY 'PROT VIGE DCC: ' DCC-NUM-TCHP ' ' DCC-NUM-TCHA
                 DISPLAY 'PROT HIST DCC: ' DCC-NUM-TDPH ' ' DCC-NUM-TDAH
                 DISPLAY '  '
                 ADD 1 TO WC-ACTU-DCC
                 PERFORM CG-MODIF-DCC.

           IF DCC-REQ-RETURN-CODE = '14'
              ADD 1 TO WC-INE-DCC
              IF WS-NUM-PVIG NOT = 0 OR
                 WS-NUM-AVIG NOT = 0 OR
                 WS-NUM-PHIS NOT = 0 OR
                 WS-NUM-AHIS NOT = 0
                 PERFORM CH-CREA-DCC
                 ADD 1 TO WC-GRA-DCC.

       CG-MODIF-DCC.
      *-------------
           MOVE 'RDUKY'     TO DCC-REQ-CMD.
           CALL 'DBNTRY' USING UIB DCC-REQ-AREA DCC ELEM-LIST.
           IF DCC-REQ-RETURN-CODE NOT = '  '
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: RDUKY TABLA DCC *'
              DISPLAY BLANCO '* REQ. AREA : ' CHP-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.

           MOVE 'UPDAT'     TO DCC-REQ-CMD.
           MOVE WS-NUM-PVIG TO DCC-NUM-TCHP.
           MOVE WS-NUM-AVIG TO DCC-NUM-TCHA.
           MOVE WS-NUM-PHIS TO DCC-NUM-TDPH.
           MOVE WS-NUM-AHIS TO DCC-NUM-TDAH.
           CALL 'DBNTRY' USING UIB DCC-REQ-AREA DCC ELEM-LIST.
           IF DCC-REQ-RETURN-CODE NOT = '  '
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: UPDAT TABLA DCC *'
              DISPLAY BLANCO '* REQ. AREA : ' CHP-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              PERFORM D-TERMINAR
              GOBACK.

       CH-CREA-DCC.
      *------------
      *    Filler Compatibilidad VSAM/MVS
           MOVE ' '         TO DCC-GLS-FLAG.

      *    COD. INT. COMP. CLIENTE
           MOVE WS-RCO-ICLI TO DCC-CIC-ICLI.

      *    MUEVE LA FECHA Y HORA DE PROCESO.
      *****MOVE '19'        TO DCC-NUM-STRN.
           MOVE WS-NUM-ACUR TO DCC-NUM-ATRN.
           MOVE WS-NUM-MCUR TO DCC-NUM-MTRN.
           MOVE WS-NUM-DCUR TO DCC-NUM-DTRN.
           IF DCC-NUM-ATRN < ROLL-YY
              MOVE '20'     TO DCC-NUM-STRN
           ELSE
              MOVE '19'     TO DCC-NUM-STRN
           END-IF.
           ACCEPT TIME-OF-DAY FROM TIME
           MOVE TIME-OF-DAY TO DCC-HRA-HRTR.

      *    Indicador Tipo Accion
           MOVE 'I'         TO DCC-MSC-TACC.
      *    Indicador Status
           MOVE 'V'         TO DCC-MSC-STAT.
      *    INDICADOR TIPO ORIGEN
           MOVE 'B'         TO DCC-MSC-TTRN.
      *    CODIGO TIPO ORIGEN
           MOVE '   '       TO DCC-COD-TTRN.

      *    AUTOR TRANSACCION
           MOVE 'SGCP176'   TO DCC-COD-ATRN.

      *    NRO TOTAL CHEQUES PROTESTADOS VIGENTES.
           MOVE WS-NUM-PVIG TO DCC-NUM-TCHP.

      *    NRO TOTAL CHEQUES ACLARADOS VIGENTES.
           MOVE WS-NUM-AVIG TO DCC-NUM-TCHA.

      *    Indicador Protestos en Boletin
           IF WS-NUM-PVIG NOT = 0 OR WS-NUM-AVIG NOT = 0
              MOVE 'S'         TO DCC-IND-PBOL
           ELSE
           IF WS-NUM-PHIS NOT = 0 OR WS-NUM-AHIS NOT = 0
              MOVE 'H'         TO DCC-IND-PBOL
           ELSE
              MOVE 'N'         TO DCC-IND-PBOL.

      *    Nro Unidades Periodo Bloqueo Cuentacorriente
           MOVE 000         TO DCC-NUM-UPBC.
      *    Unidad Tiempo Periodo Bloqueo Cuentacorriente
           MOVE ' '         TO DCC-IND-UTBC.
      *    Fecha Inicio Bloqueo Cuentacorriente
           MOVE 00000000    TO DCC-FEC-FIBC.

      *    NRO TOTAL CHEQUES PROTESTADOS VIGENTES.
           MOVE WS-NUM-PHIS TO DCC-NUM-TDPH.

      *    NRO TOTAL CHEQUES ACLARADOS VIGENTES.
           MOVE WS-NUM-AHIS TO DCC-NUM-TDAH.

      *    Disponible
           MOVE SPACES      TO DCC-GLS-DISP.

           MOVE 'ADDIT' TO DCC-REQ-CMD.
           CALL 'DBNTRY' USING UIB DCC-REQ-AREA DCC ELEM-LIST.
           IF DCC-REQ-RETURN-CODE NOT = '  '
              DISPLAY BLANCO '**************************'
              DISPLAY BLANCO '* ERROR: ADDIT TABLA DCC *'
              DISPLAY BLANCO '* REQ. AREA      : ' DCC-REQ-AREA
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *'
              DISPLAY BLANCO
              DISPLAY BLANCO '* ERROR: ADDIT TABLA DCC *' UPON CONSOLE
              DISPLAY BLANCO '* PGM. SGCP176 CANCELADO *' UPON CONSOLE
              PERFORM D-TERMINAR
              GOBACK.

       CZ-FIN-SECTION.
      *---------------
           EXIT.

       D-TERMINAR SECTION.
      *===================
       DA-TOTALES-CONTROL.
      *-------------------
           DISPLAY BLANCO '*************************************'
           DISPLAY BLANCO '*     ESTADISTICAS DEL PROCESO      *'
           DISPLAY BLANCO '*                                   *'
           DISPLAY BLANCO '*************************************'
           DISPLAY BLANCO.
           DISPLAY BLANCO 'REG. LEIDOS TABLA RCO :' WC-LEI-RCO.
           DISPLAY BLANCO 'OPE. DISTINTAS EN RCO :' WC-DIST-RCO.
           DISPLAY BLANCO 'REG. LEIDOS TABLA CHP :' WC-LEI-CHP.
           DISPLAY BLANCO 'OPE. EXISTE TABLA CHP :' WC-EXI-CHP.
           DISPLAY BLANCO 'OPE. INEXIS TABLA CHP :' WC-INE-CHP.
           DISPLAY BLANCO 'REG. LEIDOS TABLA PHI :' WC-LEI-PHI.
           DISPLAY BLANCO 'OPE. EXISTE TABLA PHI :' WC-EXI-PHI.
           DISPLAY BLANCO 'OPE. INEXIS TABLA PHI :' WC-INE-PHI.
           DISPLAY BLANCO 'REG. LEIDOS TABLA DCC :' WC-LEI-DCC.
           DISPLAY BLANCO 'REG. INEXIS TABLA DCC :' WC-INE-DCC.
           DISPLAY BLANCO 'REG. GRABAD TABLA DCC :' WC-GRA-DCC.
           DISPLAY BLANCO 'REG. ACTUALIZADO DCC  :' WC-ACTU-DCC.
           DISPLAY BLANCO 'REG. BIEN RCO Y DCC   :' WC-BIEN-DCC.
           DISPLAY BLANCO.

       DZ-FIN-SECTION.
      *---------------
           EXIT.

