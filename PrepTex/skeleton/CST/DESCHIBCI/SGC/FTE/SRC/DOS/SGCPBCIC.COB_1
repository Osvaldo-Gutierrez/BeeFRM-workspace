       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.    SGCPBCIC.
       AUTHOR.        VERONICA CORREA.
       DATE-WRITTEN.  ABRIL/1997.
       DATE-COMPILED.
      **************************************************************
      *                                                            *
      *  SISTEMA    : GENERAL DE CLIENTES                          *
      *                                                            *
      *                                                            *
      *  ANALISTA   : GERARDO URETA.                               *
      *                                                            *
      *  OBJETIVO   :                                              *
      *               ACTUALIZAR BASE DE DATOS DCP DCE             *
      *                                                            *
      *   ENTRADA   :                                              *
      *               1. ARCHIVO PLANO                             *
      *                                                            *
      *   SALIDA    :                                              *
      *               1. BASE DE DATOS 180 CIC ACTUALIZADA.        *
      *                                                            *
      *                                                            *
      **************************************************************
       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA
           C01 IS CANAL-1.

       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *-------------
           SELECT    ENTRADA ASSIGN TO ENTRADA
                                     ORGANIZATION IS SEQUENTIAL
                                     FILE STATUS IS FS-ENTRADA.
           SELECT    SALIDA  ASSIGN TO SALIDA
                                     ORGANIZATION IS SEQUENTIAL
                                     FILE STATUS IS FS-SALIDA.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *-------------

       FD ENTRADA LABEL RECORD STANDARD
                                 RECORD CONTAINS 21 CHARACTERS
                                 BLOCK CONTAINS 0 RECORDS
                                 RECORDING MODE F.
       01 REG-ENTRADA.
          05 ENT-FILLER          PIC X(03).
          05 ENT-FEC-PRO         PIC X(10).
          05 ENT-IIC-ICLI        PIC 9(08).
      *
       FD SALIDA  LABEL RECORD STANDARD
                                 BLOCK CONTAINS 0 RECORDS
                                 RECORD CONTAINS 21 CHARACTERS
                                 RECORDING MODE F.
       01 REG-SALIDA.
          05 SAL-FILLER          PIC X(03).
          05 SAL-FEC-PRO         PIC X(10).
          05 SAL-IIC-ICLI        PIC 9(08).
      *
       WORKING-STORAGE SECTION.
      *========================

       01  WA-ACUMULADORES.
      *--------------------
           05   WA-LEIDOS-DBC          PIC 9(08).
           05   WA-ACTUAL-DBC          PIC 9(08).
           05   WA-ACTUAL-DCE          PIC 9(08).
           05   WA-ACTUAL-DCP          PIC 9(08).
           05   WS-FIN                 PIC 9(01).
      *

       01  WP-FECHA-ENTRADA.
           05   FILLER                 PIC X(17).
           05   WP-FECHA-PROC          PIC X(10).
           05   WP-FECHA-PROC-R REDEFINES WP-FECHA-PROC.
                10 WP-FPROC-SS         PIC 9(02).
                10 WP-FPROC-AA         PIC 9(02).
                10 FILLER              PIC X(01).
                10 WP-FPROC-MM         PIC 9(02).
                10 FILLER              PIC X(01).
                10 WP-FPROC-DD         PIC 9(02).

      *

      *
       01  WS-VARIABLES-TRABAJO.
      *-------------------------
           03 WS-SECCION               PIC X(30).
           03 WS-PARRAFO               PIC X(30).
           03 WS-CODIGO-ERROR          PIC 9(03).
           03 WS-BLANCO                PIC X(06) VALUE SPACES.
           03 WS-EXITO                 PIC X(01) VALUE SPACES.
           03 CIC-ULT-USA              PIC 9(08) VALUE ZEROS.
           03 FS-ENTRADA               PIC X(02) VALUE SPACES.
           03 FS-SALIDA                PIC X(02) VALUE SPACES.
           03 AUX-CIC                  PIC 9(08) VALUE ZEROS.

      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           02 FILLER               PIC X(32) VALUE 'SGCPBCIC'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
      *
       01  REQ-AREA-CIC.
           02 REQ-COMAN-CIC        PIC X(5).
           02 REQ-TBLNM-CIC        PIC X(3)  VALUE 'CIC'.
           02 REQ-KYNAM-CIC        PIC X(5)  VALUE 'CIC00'.
           02 REQ-RTNCD-CIC        PIC X(2)  VALUE SPACES.
           02 REQ-INRCD-CIC        PIC X(1).
           02 REQ-DBIDE-CIC        PIC S9(4)  COMP VALUE +180.
           02 REQ-TBLID-CIC        PIC X(7).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-CIC        PIC S9(4)  COMP.
           02 REQ-IOCNT-CIC        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-CIC.
              03 REQ-CAI-ICIC      PIC X(04).
      *
      *
      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
       01  REG-CIC.
      *    COPYDD GEN-R-CIC.GEN-E-CIC00(PROD),01,D.
      *                             EQUIVALENTE AL REGISTRO CIC
           02  GEN-E-CIC00.
      *                             CODIGO INTERNO COMPUTACIONAL
               03  CIC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  CIC-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  CIC-KEY-IREG.
      *                             IDENTIFICACION ENTIDAD
                       05  CIC-KEY-ICIC.
      *                             CODIGO INTERNO COMPUTACIONAL
                       06  CIC-CIC-ICIC.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  CIC-CAI-ICIC.
                       08  CIC-COD-TCIC
                                    PICTURE X(1).
                       08  CIC-COD-CCIC
                                    PICTURE X(3).
      *                             IDENTIFICACION INTERNA COMPUTACIONAL
                       07  CIC-NUM-ICIC
                                    PICTURE 9(8).
                       07  CIC-IIC-ICIC
                             REDEFINES CIC-NUM-ICIC.
                       08  CIC-COD-OCIC.
                       09  CIC-COD-SCIC
                                    PICTURE X(2).
                       09  CIC-IND-SCIC
                                    PICTURE X(1).
                       08  CIC-GLS-NCIC
                                    PICTURE X(5).
      *                             TIME STAMP TRANSACCION
                       05  CIC-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  CIC-FEC-FTRN.
                       07  CIC-NUM-STRN
                                    PICTURE 9(2).
                       07  CIC-NUM-ATRN
                                    PICTURE 9(2).
                       07  CIC-NUM-MTRN
                                    PICTURE 9(2).
                       07  CIC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  CIC-HRA-HRTR.
                       07  CIC-NUM-HHTR
                                    PICTURE 9(2).
                       07  CIC-NUM-MMTR
                                    PICTURE 9(2).
                       07  CIC-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  CIC-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  CIC-MSC-STAT PICTURE X(1).
      *                             INDICADOR DE VIGENCIA CENTRO ASIGNAC
                   04  CIC-IND-VIGE
                             REDEFINES CIC-MSC-STAT
                                    PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  CIC-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  CIC-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  CIC-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  CIC-COD-ATRN PICTURE X(12).
      *                             DISPONIBLE
                   04  CIC-GLS-DISP PICTURE X(3).

      *
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
      *
       01  ELEM-LIST-CIC.
           03 ELEM-CIC             PIC X(05)  VALUE 'CIC00'.
           03 ELSE-CIC             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.


      *
       EJECT
      *------------------------------------------------------------*
      *                                                            *
      *                   PROGRAMA PRINCIPAL                       *
      *                                                            *
      *------------------------------------------------------------*
       PROCEDURE DIVISION.
      *==================
           ENTRY 'DBMSCBL'.
       AA-INICIAR.
      *-----------
           PERFORM B-INICIO.
       AB-PROCESAR.
      *------------
           PERFORM C-PROCESO.
       AD-TERMINO.
      *-----------
           GOBACK.
      *
       B-INICIO SECTION.
      *-----------------
           PERFORM BA-ABRIR-ARCHIVOS.

           PERFORM BB-ACEPTA-PARAMETROS.
           GO TO   BZZ-FIN-DE-SECTION.

       BA-ABRIR-ARCHIVOS.
      *-------------------
           OPEN INPUT ENTRADA.
           IF FS-ENTRADA NOT = '00'
              DISPLAY '*  ERROR EN ARCHIVO DE ENTRADA *'
              DISPLAY '*  ERROR   = ' FS-ENTRADA
              MOVE 100       TO RETURN-CODE
              PERFORM F-TERMINO
           END-IF.
           OPEN OUTPUT SALIDA.
           IF FS-SALIDA NOT = '00'
              DISPLAY '* ERROR EN ARCHIVO DE SALIDA * '
              DISPLAY '* ERROR  = ' FS-SALIDA
              MOVE 100   TO RETURN-CODE
              PERFORM F-TERMINO
           END-IF.
       BB-ACEPTA-PARAMETROS.
      *---------------------
           ACCEPT WP-FECHA-ENTRADA.
           DISPLAY WP-FECHA-PROC.
           IF WP-FPROC-SS IS NOT NUMERIC OR WP-FPROC-AA  IS NOT  NUMERIC
             OR WP-FPROC-MM IS NOT NUMERIC OR WP-FPROC-DD IS NOT NUMERIC
              DISPLAY '*** ERROR EN FECHA PROCESO ***'
              DISPLAY '*** FECHA PROCESO = ' WP-FECHA-PROC
              DISPLAY '*** SE ABORTA EL PROCESO ***'
              MOVE 100 TO RETURN-CODE
              PERFORM F-TERMINO
           END-IF.

       BZZ-FIN-DE-SECTION.
      *-------------------
           EXIT.



       C-PROCESO SECTION.
      *------------------

       CA-LECTURA-ARCHIVO.
      *-------------------
           READ ENTRADA.
           IF ENT-FEC-PRO NOT = WP-FECHA-PROC
             DISPLAY '* ERROR EN COMPARACION DE FECHA *'
             DISPLAY '* FECHA DE PROCESO : ' WP-FECHA-PROC
             DISPLAY '* FECHA DE ARCHIVO : ' ENT-FEC-PRO
             MOVE 100 TO RETURN-CODE
             PERFORM F-TERMINO
           ELSE
              MOVE  SPACES       TO  REQ-CAI-ICIC
           END-IF.
           IF ENT-IIC-ICLI IS NOT NUMERIC OR
              ENT-IIC-ICLI = ZEROS
              DISPLAY '*** CIC INVALIDO     ***'
              DISPLAY '*   CIC : ' ENT-IIC-ICLI
              MOVE ENT-IIC-ICLI   TO  SAL-IIC-ICLI
              MOVE WP-FECHA-PROC  TO  SAL-FEC-PRO
              MOVE ENT-FILLER     TO  SAL-FILLER
              WRITE REG-SALIDA
              PERFORM F-TERMINO
           END-IF.
      *
       CB-BUSCA-CIC.
      *------------
           MOVE 'REDKY' TO REQ-COMAN-CIC.
           CALL 'DBNTRY' USING UIB REQ-AREA-CIC REG-CIC ELEM-LIST-CIC.
           IF REQ-RTNCD-CIC  NOT = SPACES
             IF REQ-RTNCD-CIC   = '14'
                MOVE 'N'   TO WS-EXITO
                DISPLAY ' REGISTRO NO EXISTE EN TABLA CIC '
                GO TO CE-CERRAR-ARCHIVO
             ELSE
                DISPLAY WS-BLANCO '* TABLA          : ' REQ-TBLNM-CIC
                DISPLAY WS-BLANCO '* CODIGO RETORNO : ' REQ-RTNCD-CIC
                DISPLAY WS-BLANCO '* REQ. AREA      : ' REQ-AREA-CIC
                DISPLAY WS-BLANCO '* PGM.          CANCELADO *'
                DISPLAY WS-BLANCO
                PERFORM F-TERMINO
             END-IF
           ELSE
              DISPLAY CIC-KEY-IREG
              MOVE 'S'      TO WS-EXITO
           END-IF.
      *
       CC-ACTUALIZA-CIC.
      *----------------
           MOVE 'RDUKY' TO REQ-COMAN-CIC.
           CALL 'DBNTRY' USING UIB REQ-AREA-CIC REG-CIC ELEM-LIST-CIC.
           IF REQ-RTNCD-CIC  NOT = SPACES
             IF REQ-RTNCD-CIC   = '14'
               GO TO CE-CERRAR-ARCHIVO
             ELSE
                DISPLAY WS-BLANCO '* TABLA          : ' REQ-TBLNM-CIC
                DISPLAY WS-BLANCO '* CODIGO RETORNO : ' REQ-RTNCD-CIC
                DISPLAY WS-BLANCO '* REQ. AREA      : ' REQ-AREA-CIC
                DISPLAY WS-BLANCO '* PGM.          CANCELADO *'
                DISPLAY WS-BLANCO
                PERFORM F-TERMINO
             END-IF
           ELSE
              MOVE CIC-NUM-ICIC    TO  CIC-ULT-USA
              COMPUTE AUX-CIC = CIC-NUM-ICIC + ENT-IIC-ICLI
              MOVE AUX-CIC  TO CIC-NUM-ICIC
              MOVE 'UPDAT' TO REQ-COMAN-CIC
              CALL 'DBNTRY' USING UIB REQ-AREA-CIC REG-CIC ELEM-LIST-CIC
              IF REQ-RTNCD-CIC  NOT = SPACES
                 MOVE 'N'  TO  WS-EXITO
                 DISPLAY WS-BLANCO '* TABLA          : ' REQ-TBLNM-CIC
                 DISPLAY WS-BLANCO '* CODIGO RETORNO : ' REQ-RTNCD-CIC
                 DISPLAY WS-BLANCO '* REQ. AREA      : ' REQ-AREA-CIC
                 DISPLAY WS-BLANCO '* PGM.          CANCELADO *'
                 DISPLAY WS-BLANCO
              END-IF
           END-IF.
      *
       CD-WRITE-ARCH.
      *---------------
           IF WS-EXITO = 'S'
              MOVE CIC-ULT-USA    TO  SAL-IIC-ICLI
              MOVE WP-FECHA-PROC  TO  SAL-FEC-PRO
              MOVE ENT-FILLER     TO  SAL-FILLER
           ELSE
               MOVE SPACES TO REG-SALIDA
           END-IF.
           WRITE REG-SALIDA.
      *
       CE-CERRAR-ARCHIVO.
      *------------------
           CLOSE ENTRADA.
           CLOSE SALIDA.



       CZZ-FIN-SECTION.
      *-------------------
           EXIT.



       F-TERMINO SECTION.
      *------------------
           PERFORM CE-CERRAR-ARCHIVO.
           DISPLAY ' '
           DISPLAY '*********************************************'
           DISPLAY '  PROGRAMA TERMINO ANORMALMENTE              '
           DISPLAY '                                             '
           DISPLAY '*********************************************'
           GOBACK.
