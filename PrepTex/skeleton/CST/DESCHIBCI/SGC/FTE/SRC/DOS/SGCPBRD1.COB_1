       IDENTIFICATION DIVISION.
      *========================
      *
       PROGRAM-ID.          SGCPBRD1.
      *
      *AUTHOR.              BRUNILDA PARDO G.
      *
      *DATE-WRITTEN.        ENERO 1997.
      *
      *REMARKS.
      *
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      *****************************************************************
      *      SISTEMA    :  SISTEMA GENERAL DE CLIENTES (SGC).         *
      *                   -----------------------------------         *
      *                                                               *
      *      OBJETIVO   : GENERAR ARCHIVO CON LOS RUTS DUPLICADOS     *
      *                   EN TABLA DBC (DATOS BASICOS DEL CLIENTE).   *
      *                                                               *
      *      ANALISTA   : G. URETA Q.                                 *
      *                                                               *
      *      ARCHIVOS   :                                             *
      *                                                               *
      *      ENTRADA    : TABLA DATACOM DBC (100)                     *
      *                                                               *
      *      SALIDA     : ARCHIVO CON RUTS DUPLICADOS                 *
      *                                                               *
      *                                                               *
      *****************************************************************
      *
       ENVIRONMENT DIVISION.
      *=====================

       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA
                        C01 IS CANAL-1.
      *
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *
      *     *** ARCHIVO CON RUTS DUPLICADOS EN TABLA DBC   ***
      *                     (KSDS)
           SELECT RDO001        ASSIGN   TO   RDO001
                                ORGANIZATION INDEXED
                                ACCESS RANDOM
                                RECORD KEY RDO001-CLAVE
                                FILE STATUS FS-RDO001.
      *
       DATA DIVISION.
      *==============
      *
       FILE SECTION.
      *-------------
      *
       FD  RDO001
           LABEL RECORD    STANDARD.
      *
       01  RDO001-REG.
           05  RDO001-CLAVE.
               10  RDO001-RUT          PIC X(12).
               10  RDO001-CIC          PIC X(12).
           05  RDO001-IND-SIS          PIC 9(02).
           05  RDO001-TAB-SISTEMA.
               10 RDO001-TABLA-SIS  OCCURS 10 TIMES INDEXED BY
                                    WS-INDEX1.
                  15  RDO001-TAB-SIST  PIC X(03).
           05  FILLER                  PIC X.
      *
      *
      *
       WORKING-STORAGE SECTION.
      *-----------------------
      *
      *   ***   TABLA RUTS DUPLICADOS   ***
      *
       01  WS-RDO001-TAB-SISTEMA.
           05  WS-RDO001-CLAVE  OCCURS 100 TIMES INDEXED BY WS-INDEX2.
               10  WS-RDO001-RUT       PIC X(12).
               10  WS-RDO001-CIC       PIC X(12).
      *
       01  WR-CURRENT-DATE.
           05  WR-DD-CURR              PIC 9(2).
           05  FILLER                  PIC X          VALUE '/'.
           05  WR-MM-CURR              PIC 9(2).
           05  FILLER                  PIC X          VALUE '/'.
           05  WR-AA-CURR              PIC 9(2).
      *
       01  WR-TIME-OF-DAY.
           05  WR-HH-TIME              PIC 9(2).
           05  WR-MM-TIME              PIC 9(2).
           05  WR-SS-TIME              PIC 9(2).
      *
       01  WR-TIME-TRANS.
           05  WR-HH-TRANS             PIC 9(2).
           05  WR-MM-TRANS             PIC 9(2).
           05  WR-SS-TRANS             PIC 9(2).
      *
      *
       01  WR-HORA-IMPRES.
           05  WR-HH-IMPRES            PIC 9(2).
           05  FILLER                  PIC X          VALUE '.'.
           05  WR-MM-IMPRES            PIC 9(2).
           05  FILLER                  PIC X          VALUE '.'.
           05  WR-SS-IMPRES            PIC 9(2).
      *
       01  WR-REGISTRO-LINEA           PIC X(132).
      *
      *
      * ***       PARAMETROS DE ENTRADA   ***
      *
       01  WP-PARAMETROS.
           05  FILLER                  PIC X(8).
           05 WP-FECHA-PROC.
              10  WP-DD-PROC           PIC 99.
              10  WP-MM-PROC           PIC 99.
              10  WP-SS-PROC           PIC 99.
              10  WP-AA-PROC           PIC 99.
      *
           05  WR-FECHA-TRB.
               10  WR-DD-TRB            PIC 99.
               10  WR-MM-TRB            PIC 99.
               10  WR-SS-TRB            PIC 99.
               10  WR-AA-TRB            PIC 99.
           05  WR-FECHA-TRB-NUMERIC-R  REDEFINES WR-FECHA-TRB.
               10  WR-FECHA-TRB-NUMERIC PIC 9(8).
      *
      *      ***   CAMPOS DE TRABAJO    ***
      *
       01  WR-CAMPOS-DE-TRABAJO.
      *-------------------------
      *
           05  WR-RUT-ANT-DUP          PIC X(12).
           05  WR-BLANCO               PIC X(8)  VALUE SPACES.
           05  WR-SW-ERR1              PIC 9     VALUE 0.
           05  WR-CONT-LINEAS1         PIC 9(2)  VALUE 60.
           05  WR-TOT-PAG1             PIC 9(2)  VALUE 0.
           05  WR-SW-IMPRESION         PIC 9     VALUE 0.
           05  WR-SECCION              PIC X(25).
           05  WR-PARRAFO              PIC X(25).
           05  WR-OPERACION            PIC X(25).
           05  WR-CODIGO               PIC X(25).
      *
           COPY RTNWCALE.
      *
       01  W-ACUMULADORES.
      *-------------------
      *
           05  WA-CONT1                PIC 9(11).
           05  WA-CONT2                PIC 9(11).
           05  WA-CONT3                PIC 9(11).
           05  WA-CONT4                PIC 9(11).
           05  WA-TOTAL-LEID-DBC       PIC 9(11).
           05  WA-TOTAL-REGS-DUP       PIC 9(11).
           05  WA-TOTAL-NODUP-DBC      PIC 9(11).
           05  WA-TOTAL-LEID-ERROR     PIC 9(11).
           05  WA-TOTAL-GRAB-RDO001    PIC 9(11).
           05  WA-TOTAL-IMPRESOS       PIC 9(11).
           05  WA-TOTAL-NOENCONT       PIC 9(11).
           05  WA-TOTAL-GRAB-TRBMTAR   PIC 9(11).
      *
      *   ***      SWITCHES  DE PROCESO     ****
      *
       01  WS-SWITCHES.
           05  WS-ACCESO-DBC         PIC X(2).
               88  ACCESO-DBC                   VALUE 'SI'.
           05  WS-NO-HAY-REGS        PIC X(2).
               88  NO-HAY-REGS                  VALUE 'SI'.
           05  WS-TERMINO-ANORMAL    PIC X(2).
               88  TERMINO-ANORMAL              VALUE 'SI'.
           05  WS-FIRST-REG          PIC X(2).
               88  FIRST-REG                    VALUE 'SI'.
           05  WS-IMPRESION          PIC X(2).
               88  IMPRESION-OK                 VALUE 'SI'.
      *
       01  FILES-STATUS.
           05 FS-RDO001              PIC 9(2)     VALUE 0.
      *

      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           05 FILLER                 PIC X(32) VALUE 'SGCPBRD1'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  REQ-AREA.
           02 REQ-CMD              PIC X(5).
           02 REQ-TBLNM            PIC X(3).
           02 REQ-KEYNM            PIC X(5).
           02 REQ-RETURN-CODE      PIC X(2) VALUE SPACES.
           02 REQ-INT-RTN-CD       PIC X(1).
           02 REQ-DBID             PIC S9(4) COMP VALUE +100.
           02 REQ-TBLID            PIC X(7).
           02 FILLER               PIC X(25).
           02 REQ-COUNT-MAX        PIC S9(4) COMP.
           02 REQ-IO-COUNT         PIC S9(4) COMP.
           02 FILLER               PIC X(22).
           02 REQ-KEY-VALUE        PIC X(24).
           02 REQ-DBC-KEY-VALUE REDEFINES REQ-KEY-VALUE.
              03 REQ-DBC-NUM-ICLI  PIC X(12).
              03 FILLER            PIC X(12).
      *
      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
       01  REG-DBC.
      *    COPYDD DGC-R-DBC.DGC-E-DBC00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO DBC
           02  DGC-E-DBC00.
      *                             DATOS BASICOS DEL CLIENTE
               03  DBC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  DBC-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  DBC-KEY-IREG.
      *                             IDENTIFICACION DATOS BASICOS CLIENTE
                       05  DBC-KEY-IDBC.
      *                             COD. INT. COMP. CLIENTE
                       06  DBC-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  DBC-CAI-ICLI.
                       08  DBC-COD-TCLI
                                    PICTURE X(1).
                       08  DBC-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  DBC-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       05  DBC-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  DBC-FEC-FTRN.
                       07  DBC-NUM-STRN
                                    PICTURE 9(2).
                       07  DBC-NUM-ATRN
                                    PICTURE 9(2).
                       07  DBC-NUM-MTRN
                                    PICTURE 9(2).
                       07  DBC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  DBC-HRA-HRTR.
                       07  DBC-NUM-HHTR
                                    PICTURE 9(2).
                       07  DBC-NUM-MMTR
                                    PICTURE 9(2).
                       07  DBC-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  DBC-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  DBC-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  DBC-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  DBC-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  DBC-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  DBC-COD-ATRN PICTURE X(12).
      *                             KEY RUT CLIENTE + INDICADOR O CALIFI
                   04  DBC-IDC-ICLI.
      *                             RUT CLIENTE
                       05  DBC-NUM-ICLI
                                    PICTURE X(8).
      *                             INDICADOR TIPO RUT
                       05  DBC-IND-ICLI
                                    PICTURE X(1).
      *                             EXTENSION DEL RUT
                       05  DBC-GLS-ICLI
                                    PICTURE X(3).
      *                             KEY DIGITO VERIFICADOR DEL RUT
                   04  DBC-VRF-ICLI PICTURE X(1).
      *                             KEY NOMBRE CLIENTE GENERICO (PERSONA
                   04  DBC-GLS-NOMC.
                       05  DBC-GLS-APAT
                                    PICTURE X(25).
                       05  DBC-GLS-AMAT
                                    PICTURE X(25).
                       05  DBC-GLS-NOMB
                                    PICTURE X(25).
      *                             SOUNDEX DEL NOMBRE DE CLIENTE
                   04  DBC-SNX-NOMC.
                       05  DBC-SNX-APAT
                                    PICTURE X(3).
                       05  DBC-SNX-AMAT
                                    PICTURE X(3).
                       05  DBC-SNX-NOMB
                                    PICTURE X(3).
      *                             NUMERO DE DIRECCION DE ENVIO CORRESP
                   04  DBC-NUM-DCOR PICTURE 9(2).
      *                             OPR IND TIPO CLIENTE ( PERSONA O EMP
                   04  DBC-IND-TICL PICTURE X(1).
      *                             TIPO CLIENTE (POTENCIAL,NO CLIENTE,G
                   04  DBC-COD-TICL PICTURE X(2).
      *                             CODIGO CLASIFICACION COMERCIAL
                   04  DBC-COD-CLCM PICTURE X(2).
      *                             RCM CODIGO DEL EJECUTIVO ENCARGADO D
                   04  DBC-COD-EJEC PICTURE X(12).
      *                             OPR CODIGO DE OFICINA DONDE OPERA EL
                   04  DBC-COD-OFIC PICTURE X(3).
      *                             RCM INDICADOR SI OPERA EN OTRA OFICI
                   04  DBC-IND-OOFI PICTURE X(1).
      *                             EXT CODIGO ACTIVIDAD ECONOMICA
                   04  DBC-COD-ACTI PICTURE X(9).
      *                             EXT ORIGEN Y DESTINO SECT (COMP.INST
                   04  DBC-COD-ODSE PICTURE X(3).
      *                             INF FECHA INGRESO AL PLAN
                   04  DBC-FEC-FPLN.
                       05  DBC-NUM-SPLN
                                    PICTURE 9(2).
                       05  DBC-NUM-APLN
                                    PICTURE 9(2).
                       05  DBC-NUM-MPLN
                                    PICTURE 9(2).
                       05  DBC-NUM-DPLN
                                    PICTURE 9(2).
      *                             CODIGO DE SEGMENTO
                   04  DBC-COD-SETO PICTURE X(5).
      *                             PERIODO DE SEGMENTO (SSAAMM)
                   04  DBC-PDO-SETO PICTURE X(6).
      *                             INF INDICADOR DE MULTAS BANCO CENTRA
                   04  DBC-IND-MUBC PICTURE X(1).
      *                             INF INDICADOR DE JUICIOS BANCO CENTR
                   04  DBC-IND-JUBC PICTURE X(1).
      *                             EXT CODIGO ESTADO CLIENTE ( COMO DEU
                   04  DBC-MSC-SCLI PICTURE X(1).
      *                             FECHA ESTADO CLIENTE
                   04  DBC-FEC-FSCL.
                       05  DBC-NUM-SSCL
                                    PICTURE 9(2).
                       05  DBC-NUM-ASCL
                                    PICTURE 9(2).
                       05  DBC-NUM-MSCL
                                    PICTURE 9(2).
                       05  DBC-NUM-DSCL
                                    PICTURE 9(2).
      *                             CODIGO DE BLOQUEO DEL CLIENTE
                   04  DBC-COD-BLOC PICTURE X(2).
      *                             NUMERO TOTAL DE DIRECCIONES
                   04  DBC-NUM-TDIR PICTURE 9(2).
      *                             NUMERO TOTAL DE COMENTARIOS
                   04  DBC-NUM-TCMT PICTURE 9(3).
      *                             FECHA INICIO OPERACION
                   04  DBC-FEC-FIOP.
                       05  DBC-NUM-SIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-AIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-MIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-DIOP
                                    PICTURE 9(2).
      *                             FECHA TERMINO OPERACION
                   04  DBC-FEC-FTOP.
                       05  DBC-NUM-STOP
                                    PICTURE 9(2).
                       05  DBC-NUM-ATOP
                                    PICTURE 9(2).
                       05  DBC-NUM-MTOP
                                    PICTURE 9(2).
                       05  DBC-NUM-DTOP
                                    PICTURE 9(2).
      *                             NUMERO OPERACIONES FECHA TERMINO NUL
                   04  DBC-NUM-OFTN PICTURE 9(3).
      *                             FECHA INGRESO DE DATOS A LA BASE DE
                   04  DBC-FEC-FINP.
                       05  DBC-NUM-SINP
                                    PICTURE 9(2).
                       05  DBC-NUM-AINP
                                    PICTURE 9(2).
                       05  DBC-NUM-MINP
                                    PICTURE 9(2).
                       05  DBC-NUM-DINP
                                    PICTURE 9(2).

      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  ELEM-LIST.
           03 ELEM-1         PIC X(05).
           03 ELEM-SEC       PIC X(01).
           03 FILLER         PIC X(05) VALUE SPACES.
      *-----------------------------------------------------------------


       PROCEDURE DIVISION.
      *===================
      *
           ENTRY 'DBMSCBL'.
      *
       A-CONTROL                       SECTION.
      *---------
      *
       AA-INICIALIZACION.
      *------------------
           PERFORM B-INICIALIZAR.
      *
       AB-PROCESAMIENTO.
      *-----------------
           IF  NOT TERMINO-ANORMAL
               PERFORM C-PROCESAR
           END-IF.
      *
       AC-TERMINACION.
      *---------------
           PERFORM D-TERMINAR.
      *
       AZ-FIN-DE-SECCION.
      *------------------
           GOBACK.
      *
       B-INICIALIZAR  SECTION.
      *=======================
      *
       BA-CONTROL.
      *-----------
           PERFORM BC-ABRIR-ARCHIVOS.
           IF  NOT TERMINO-ANORMAL
              PERFORM BD-INICIALIZA-ACUM.
           GO TO BZ-FIN-DE-SECCION.
      *
       BD-INICIALIZA-ACUM.
      *-------------------
      *
           MOVE ZEROS    TO   W-ACUMULADORES.
           MOVE 'NO'     TO   WS-ACCESO-DBC
                              WS-TERMINO-ANORMAL
                              WS-FIRST-REG.
      *
       BC-ABRIR-ARCHIVOS.
      *------------------
      *
           OPEN OUTPUT RDO001.
           IF FS-RDO001 NOT = ZEROS AND FS-RDO001 NOT = 95
              DISPLAY '**   ERROR AL ABRIR ARCHIVO RDO001   ** '
              DISPLAY '**   CODIGO DE RETORNO ** ' FS-RDO001
              MOVE  'B-INICIALIZAR'      TO   WR-SECCION
              MOVE  'BC-ABRIR-ARCHIVO'   TO   WR-PARRAFO
              MOVE  'APERTURA RDO001  '  TO   WR-OPERACION
              SET TERMINO-ANORMAL  TO  TRUE
           END-IF.
      *
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
      *
       C-PROCESAR                      SECTION.
      *----------
           PERFORM CA-LOCKY-DBC.
           IF REQ-RETURN-CODE  = '  '
              MOVE 'REDLE'      TO REQ-CMD
              PERFORM CB-LEER-DBC UNTIL REQ-RETURN-CODE NOT = SPACES
                                  OR TERMINO-ANORMAL
           END-IF.
           GO TO CZ-PROCESAR-EXIT.
      *
       CA-LOCKY-DBC.
      *-------------
      *
           MOVE 'LOCKY'      TO REQ-CMD.
           MOVE LOW-VALUES   TO REQ-DBC-KEY-VALUE.
           MOVE 'DBC'        TO REQ-TBLNM.
           MOVE 'DBC02'      TO REQ-KEYNM.
           MOVE 'DBC00'      TO ELEM-1.
           CALL 'DBNTRY' USING UIB REQ-AREA DBC ELEM-LIST.
           IF REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              PERFORM  CD-ERROR-DATACOM
           END-IF.
      *
       CB-LEER-DBC.
      *------------
           CALL 'DBNTRY' USING UIB REQ-AREA DBC ELEM-LIST.
           IF REQ-RETURN-CODE NOT = '  ' AND NOT = '14'
              PERFORM CD-ERROR-DATACOM
           ELSE
              IF REQ-RETURN-CODE = '  '
                 PERFORM CC-CARGA-TABLA
                 ADD 1 TO WA-TOTAL-LEID-DBC
              END-IF.
           MOVE 'REDNX'      TO REQ-CMD.
      *
       CC-CARGA-TABLA.
      *---------------
           IF NOT FIRST-REG
              SET FIRST-REG        TO TRUE
              MOVE DBC-IDC-ICLI    TO WR-RUT-ANT-DUP
           END-IF.
           IF DBC-IDC-ICLI = WR-RUT-ANT-DUP
              MOVE DBC-IDC-ICLI    TO WS-RDO001-RUT(WS-INDEX2)
              MOVE DBC-CIC-ICLI    TO WS-RDO001-CIC(WS-INDEX2)
              ADD 1                TO WA-CONT1
              SET WS-INDEX2 UP BY 1
           ELSE
              IF WA-CONT1 > 1
                 ADD 1             TO WA-TOTAL-REGS-DUP
                 PERFORM CE-GRABA-RDO001 VARYING WS-INDEX2 FROM 1 BY 1
                         UNTIL WS-INDEX2 > WA-CONT1
              END-IF
                 PERFORM CF-INIC-TABLA VARYING WS-INDEX2 FROM 1 BY 1
                         UNTIL WS-INDEX2 > 100
                 PERFORM CG-INIC-VARIABLES
                 MOVE DBC-IDC-ICLI    TO WS-RDO001-RUT(WS-INDEX2)
                 MOVE DBC-CIC-ICLI    TO WS-RDO001-CIC(WS-INDEX2)
                 ADD 1                TO WA-CONT1
                 SET WS-INDEX2 UP BY 1
           END-IF.
      *
       CD-ERROR-DATACOM.
      *-----------------
      *
           DISPLAY WR-BLANCO.
           DISPLAY WR-BLANCO '**************************'.
           DISPLAY WR-BLANCO '* ERROR: TABLA DBC       *'.
           DISPLAY WR-BLANCO '* TABLA          :        ' REQ-TBLNM.
           DISPLAY WR-BLANCO '* CODIGO RETORNO :        '
                                REQ-RETURN-CODE
           DISPLAY WR-BLANCO '* REQ. AREA      :        ' REQ-AREA.
           DISPLAY WR-BLANCO '* PGM. SGCPBRD1 CANCELADO *'.
           DISPLAY WR-BLANCO '**************************'.
           DISPLAY WR-BLANCO.
           SET TERMINO-ANORMAL               TO TRUE.
      *
       CE-GRABA-RDO001.
      *----------------
      *
           MOVE WS-RDO001-RUT(WS-INDEX2)     TO RDO001-RUT.
           MOVE WS-RDO001-CIC(WS-INDEX2)     TO RDO001-CIC.
           MOVE SPACES                       TO RDO001-TAB-SISTEMA.
           MOVE ZEROS                        TO RDO001-IND-SIS.
           WRITE RDO001-REG.
           IF FS-RDO001 NOT = ZEROS
              DISPLAY '** ERROR AL GRABAR RDO001   ** '
              DISPLAY '**   CODIGO DE RETORNO      ** ' FS-RDO001
              MOVE  'C-PROCESAR   '          TO WR-SECCION
              MOVE  'CE-GRABA-RDO001 '       TO WR-PARRAFO
              MOVE  'GRABA RUTS DUPLICADOS ' TO WR-OPERACION
              SET TERMINO-ANORMAL            TO TRUE
           ELSE
              ADD 1                          TO WA-TOTAL-GRAB-RDO001
           END-IF.
      *
       CF-INIC-TABLA.
      *--------------
           MOVE SPACES       TO WS-RDO001-RUT(WS-INDEX2)
                                WS-RDO001-CIC(WS-INDEX2).
      *
       CG-INIC-VARIABLES.
      *------------------
           MOVE DBC-IDC-ICLI TO WR-RUT-ANT-DUP.
           MOVE ZEROS        TO WA-CONT1.
           SET WS-INDEX2     TO 1.
      *
       CZ-PROCESAR-EXIT.
           EXIT.
      *
       D-TERMINAR                      SECTION.
      *==========
       DA-CONTROL.
      *----------
           PERFORM DC-CERRAR-ARCHIVOS.
           PERFORM DD-FIN-PROGRAMA.
           GO TO DZ-FIN-DE-SECCION.
      *
       DC-CERRAR-ARCHIVOS.
      *------------------
           CLOSE RDO001.
      *
       DD-FIN-PROGRAMA.
      *---------------
      *
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+     T O T A L E S  D E  C O N T R O L     +'
           DISPLAY '+-------------------------------------------+'
           DISPLAY '** TOTAL REG.LEID. TABDBC  = ' WA-TOTAL-LEID-DBC.
           DISPLAY '** TOTAL RUTS DUPLICADOS   = ' WA-TOTAL-REGS-DUP.
           DISPLAY '** TOTAL REG.GRAB. RDO001  = ' WA-TOTAL-GRAB-RDO001.
           DISPLAY ' '
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+ MENSAJES PARA EL EXPLOTADOR               +'
           DISPLAY '+-------------------------------------------+'
           IF TERMINO-ANORMAL
              MOVE 100 TO RETURN-CODE
              DISPLAY ' HAY MENSAJES PARA EL EXPLOTADOR'
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '************************************************'
              DISPLAY '************************************************'
              DISPLAY '****                                        ****'
              DISPLAY '****      TERMINO ERRONEO DEL PROCESO       ****'
              DISPLAY '****                                        ****'
              DISPLAY '************************************************'
              DISPLAY '************************************************'
              DISPLAY 'EL ERROR FUE EN LA SECCION   ' WR-SECCION
              DISPLAY '                   PARRAFO   ' WR-PARRAFO
              DISPLAY '                   OPERACION ' WR-OPERACION
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL ***'
           END-IF.
      *
       DZ-FIN-DE-SECCION.
      *-----------------
           EXIT.
      *
      *************   FIN PROGRAMA SGCPBRD1   *****************
      *
