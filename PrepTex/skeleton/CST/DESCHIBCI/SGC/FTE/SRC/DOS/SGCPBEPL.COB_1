       IDENTIFICATION DIVISION.
      *========================
      *
       PROGRAM-ID.    SGCPBEPL.
       AUTHOR.        CARLOS CID BADILLA.
       DATE-WRITTEN.  JULIO 97.
      *REMARKS.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      *
      ******************************************************************
      *                                                                *
      *  OBJETIVO : GENERAR ARCHIVO CON DATOS DE CTAS. CTES. PARA      *
      *             LISTADOS DE PLANES. PERSONAS Y EMPRESAS            *
      *                                                                *
      *  ENTRADA  :                                                    *
      *             - TABLA RCO                                        *
      *             - TABLA OPE                                        *
      *             - TABLA DCP                                        *
      *             - TABLA DCE                                        *
      *             - TABLA DBC                                        *
      *                                                                *
      *  SALIDA   : - ARCHIVO SOPPLN                                   *
      *                                                                *
      *                                                                *
      *  FECHA    : JULIO 97.                                          *
      *                                                                *
      ******************************************************************
      ******************************************************************
       ENVIRONMENT DIVISION.
      *=====================
      *
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *
           SELECT SOPPLN       ASSIGN TO SOPPLN
                               FILE STATUS IS FS-SOPPLN.
      *
       DATA DIVISION.
      *==============
       FILE SECTION.
      *-------------
      *
       FD  SOPPLN
           LABEL RECORD STANDARD
           RECORDING MODE IS F
           RECORD CONTAINS 15
           BLOCK CONTAINS 0 RECORDS.
      *
       01  REG-SOPPLN.
           03 PLN-SUC            PIC X(03).
           03 PLN-TIP-CLI        PIC X(01).
           03 PLN-PLAN           PIC X(01).
           03 PLN-FEC-PROC       PIC X(10).
      *
       WORKING-STORAGE SECTION.
      *------------------------
       77  FS-SOPPLN                  PIC X(02) VALUE '00'.
       77  W-TIP-CLIENTE              PIC X(01) VALUE SPACES.
       77  LIN                        PIC 9(02) VALUE 60.
       77  W-LEID-OPE                 PIC 9(07) VALUE ZEROES.
       77  W-LEID-DBC                 PIC 9(07) VALUE ZEROES.
       77  W-LEID-DCE                 PIC 9(07) VALUE ZEROES.
       77  W-LEID-DCP                 PIC 9(07) VALUE ZEROES.
       77  W-LEID-RCO                 PIC 9(07) VALUE ZEROES.
       77  WR-PAGINA                  PIC 9(02) VALUE ZEROES.
       77  WR-LEIDOS-VIG              PIC 9(07) VALUE ZEROES.
       77  WR-LEIDOS-OPE              PIC 9(07) VALUE ZEROES.
       77  WR-CTAS-PER                PIC 9(07) VALUE ZEROES.
       77  WR-CTAS-EMP                PIC 9(07) VALUE ZEROES.
       77  WR-SIN-DCE                 PIC 9(07) VALUE ZEROES.
       77  WR-SIN-DCP                 PIC 9(07) VALUE ZEROES.
       77  WR-SIN-OPE                 PIC 9(07) VALUE ZEROES.
       77  WR-SIN-RCO                 PIC 9(07) VALUE ZEROES.
       77  WR-SIN-DBC                 PIC 9(07) VALUE ZEROES.
       77  WR-GRABADOS                PIC 9(07) VALUE ZEROES.
       77  WR-SUCUR                   PIC X(03) VALUE SPACES.
       77  I                          PIC 9(03) VALUE ZEROES.
       77  WR-FEC-DATE                PIC 9(04) VALUE ZEROES.
       77  W-FEC-PROC                 PIC 9(08) VALUE ZEROES.
       77  SW-ERROR                   PIC 9(01) VALUE ZEROES.
       77  W-PLAN-AUX                 PIC X(01) VALUE SPACES.

       01  WR-FECHA.
           03 WR-FECHA-OPE.
              10 WR-FECHA-OPE-1        PIC X(02).
              10 WR-FECHA-OPE-2        PIC X(06).

       01  FECHA-PARAMETRO.
           03 WE-AA-DATE          PIC 9(04).
           03 WE-MM-DATE          PIC 9(02).
           03 WE-DD-DATE          PIC 9(02).
      *
       01  FECHA-BRITISH.
           02 AA-BRITISH PIC 9(04).
           02 FILLER     PIC X(01) VALUE '-'.
           02 MM-BRITISH PIC 9(02).
           02 FILLER     PIC X(01) VALUE '-'.
           02 DD-BRITISH PIC 9(02).
      *
       01  FECHA-BRITIS2.
           02 SS-BRITIS2 PIC 9(02).
           02 AA-BRITIS2 PIC 9(02).
           02 FILLER     PIC X(01) VALUE '-'.
           02 MM-BRITIS2 PIC 9(02).
           02 FILLER     PIC X(01) VALUE '-'.
           02 DD-BRITIS2 PIC 9(02).
      *
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           02 FILLER               PIC X(32) VALUE 'SGCPBEPL'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  REQ-AREA-RCO.
           02 REQ-COMAN-RCO        PIC X(05).
           02 REQ-TBLNM-RCO        PIC X(03)  VALUE 'RCO'.
           02 REQ-KEYNM-RCO        PIC X(05)  VALUE 'RCO01'.
           02 REQ-RTNCD-RCO        PIC X(02)  VALUE SPACES.
           02 REQ-INRCD-RCO        PIC X(01).
           02 REQ-DBIDE-RCO        PIC S9(4)  COMP VALUE +100.
           02 REQ-TBLID-RCO        PIC X(07).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-RCO        PIC S9(4)  COMP.
           02 REQ-IOCNT-RCO        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-RCO.
              03 REQ-RCO-COD-1     PIC X(4).
              03 REQ-RCO-COD-2     PIC X(8).
      *
       01  REQ-AREA-OPE.
           02 REQ-COMAN-OPE        PIC X(05).
           02 REQ-TBLNM-OPE        PIC X(03)  VALUE 'OPE'.
           02 REQ-KEYNM-OPE        PIC X(05)  VALUE 'OPE00'.
           02 REQ-RTNCD-OPE        PIC X(02)  VALUE SPACES.
           02 REQ-INRCD-OPE        PIC X(01).
           02 REQ-DBIDE-OPE        PIC S9(4)  COMP VALUE +100.
           02 REQ-TBLID-OPE        PIC X(07).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-OPE        PIC S9(4)  COMP.
           02 REQ-IOCNT-OPE        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-OPE.
              03 REQ-OPE-COD-1     PIC X(04).
              03 REQ-OPE-COD-2     PIC X(08).
      *
       01  REQ-AREA-DCP.
           02 REQ-COMAN-DCP        PIC X(05).
           02 REQ-TBLNM-DCP        PIC X(03)  VALUE 'DCP'.
           02 REQ-KEYNM-DCP        PIC X(05)  VALUE 'DCP00'.
           02 REQ-RTNCD-DCP        PIC X(02)  VALUE SPACES.
           02 REQ-INRCD-DCP        PIC X(01).
           02 REQ-DBIDE-DCP        PIC S9(4)  COMP VALUE +100.
           02 REQ-TBLID-DCP        PIC X(07).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-DCP        PIC S9(4)  COMP.
           02 REQ-IOCNT-DCP        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-DCP.
              03 REQ-DCP-CIC       PIC X(12).
      *
       01  REQ-AREA-DCE.
           02 REQ-COMAN-DCE        PIC X(05).
           02 REQ-TBLNM-DCE        PIC X(03)  VALUE 'DCE'.
           02 REQ-KEYNM-DCE        PIC X(05)  VALUE 'DCE00'.
           02 REQ-RTNCD-DCE        PIC X(02)  VALUE SPACES.
           02 REQ-INRCD-DCE        PIC X(01).
           02 REQ-DBIDE-DCE        PIC S9(4)  COMP VALUE +100.
           02 REQ-TBLID-DCE        PIC X(07).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-DCE        PIC S9(4)  COMP.
           02 REQ-IOCNT-DCE        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-DCE.
              03 REQ-DCE-CIC       PIC X(12).
      *
       01  REQ-AREA-DBC.
           02 REQ-COMAN-DBC        PIC X(05).
           02 REQ-TBLNM-DBC        PIC X(03)  VALUE 'DBC'.
           02 REQ-KEYNM-DBC        PIC X(05)  VALUE 'DBC00'.
           02 REQ-RTNCD-DBC        PIC X(02)  VALUE SPACES.
           02 REQ-INRCD-DBC        PIC X(01).
           02 REQ-DBIDE-DBC        PIC S9(4)  COMP VALUE +100.
           02 REQ-TBLID-DBC        PIC X(07).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-DBC        PIC S9(4)  COMP.
           02 REQ-IOCNT-DBC        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-DBC.
              03 REQ-DBC-CIC       PIC X(12).
      *
      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
       01  REG-RCO.
      *    COPYDD DGC-R-RCO.DGC-E-RCO00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO RCO
           02  DGC-E-RCO00.
      *                             RELACION CLIENTE OPERACION
               04  RCO.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  RCO-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  RCO-KEY-IREG.
      *                             IDENTIFICACION RELACION CLIENTE OPER
                       08  RCO-KEY-IRCO.
      *                             COD. INT. COMP. CLIENTE
                       10  RCO-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  RCO-CAI-ICLI.
                       14  RCO-COD-TCLI
                                    PICTURE X(1).
                       14  RCO-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  RCO-IIC-ICLI
                                    PICTURE X(8).
      *                             IDENTIFICACION OPERACION RELACION
                       10  RCO-KEY-IOPE.
      *                             COD. INT. COMP. OPERACION
                       12  RCO-CIC-IOPE.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       14  RCO-CAI-IOPE.
                       16  RCO-COD-TOPE
                                    PICTURE X(1).
                       16  RCO-COD-COPE
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       14  RCO-IIC-IOPE.
                       16  RCO-COD-OOPE.
                       18  RCO-COD-SOPE
                                    PICTURE X(2).
                       18  RCO-IND-SOPE
                                    PICTURE X(1).
                       16  RCO-GLS-NOPE
                                    PICTURE X(5).
      *                             TIME STAMP TRANSACCION
                       08  RCO-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  RCO-FEC-FTRN.
                       12  RCO-NUM-STRN
                                    PICTURE 9(2).
                       12  RCO-NUM-ATRN
                                    PICTURE 9(2).
                       12  RCO-NUM-MTRN
                                    PICTURE 9(2).
                       12  RCO-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  RCO-HRA-HRTR.
                       12  RCO-NUM-HHTR
                                    PICTURE 9(2).
                       12  RCO-NUM-MMTR
                                    PICTURE 9(2).
                       12  RCO-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  RCO-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  RCO-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  RCO-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  RCO-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  RCO-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  RCO-COD-ATRN PICTURE X(12).
      *                             CODIGO STATUS
                   06  RCO-COD-STAT PICTURE X(3).
      *                             COD RELACION CLIENTE OPERACION
                   06  RCO-COD-RCOP PICTURE X(3).
      *                             NUMERO DE DIRECCION
                   06  RCO-NUM-DIRE PICTURE 9(2).
      *                             PORCENTAJE RELACION
                   06  RCO-VAL-PRCO PICTURE 9(3)V9(6)
                                        COMPUTATIONAL.
      *                             CODIGO CLASE OPERACION
                   06  RCO-COD-COPR.
      *                             CODIGO TIPO OPERACION
                       08  RCO-COD-TOPR
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR OPERACION
                       08  RCO-COD-AOPR
                                    PICTURE X(3).
      *                             DISPONIBLE
                   06  RCO-GLS-DISP PICTURE X(17).
      *
       01  REG-OPE.
      *    COPYDD DGC-R-OPE.DGC-E-OPE00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO OPE
           02  DGC-E-OPE00.
      *                             OPERACIONES
               04  OPE.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  OPE-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  OPE-KEY-IREG.
      *                             IDENTIFICACION OPERACION
                       08  OPE-KEY-IOPE.
      *                             COD. INT. COMP. OPERACION
                       10  OPE-CIC-IOPE.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  OPE-CAI-IOPE.
                       14  OPE-COD-TOPE
                                    PICTURE X(1).
                       14  OPE-COD-COPE
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  OPE-IIC-IOPE.
                       14  OPE-COD-OOPE.
                       16  OPE-COD-SOPE
                                    PICTURE X(2).
                       16  OPE-IND-SOPE
                                    PICTURE X(1).
                       14  OPE-GLS-NOPE
                                    PICTURE X(5).
      *                             TIME STAMP TRANSACCION
                       08  OPE-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  OPE-FEC-FTRN.
                       12  OPE-NUM-STRN
                                    PICTURE 9(2).
                       12  OPE-NUM-ATRN
                                    PICTURE 9(2).
                       12  OPE-NUM-MTRN
                                    PICTURE 9(2).
                       12  OPE-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  OPE-HRA-HRTR.
                       12  OPE-NUM-HHTR
                                    PICTURE 9(2).
                       12  OPE-NUM-MMTR
                                    PICTURE 9(2).
                       12  OPE-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  OPE-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  OPE-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  OPE-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  OPE-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  OPE-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  OPE-COD-ATRN PICTURE X(12).
      *                             CODIGO STATUS
                   06  OPE-COD-STAT PICTURE X(3).
      *                             VALOR DE CAMBIO OPERACION
                   06  OPE-COD-VCOP.
      *                             MISCELANEO TIPO VALOR
                       08  OPE-MSC-TVOP
                                    PICTURE X(1).
      *                             CODIGO VALOR
                       08  OPE-COD-CVOP
                                    PICTURE X(3).
      *                             CODIGO TIPO CAMBIO
                       08  OPE-COD-TCOP
                                    PICTURE X(2).
      *                             CODIGO CLASE OPERACION
                   06  OPE-COD-COPR.
      *                             CODIGO TIPO OPERACION
                       08  OPE-COD-TOPR
                                    PICTURE X(3).
      *                             CODIGO AUXILIAR OPERACION
                       08  OPE-COD-AOPR
                                    PICTURE X(3).
      *                             CODIGO OFICINA
                   06  OPE-COD-OFOP PICTURE X(3).
      *                             EJECUTIVO
                   06  OPE-COD-EJOP PICTURE X(12).
      *                             FECHA INICIO OPERACION
                   06  OPE-FEC-FIOP.
                       08  OPE-NUM-SIOP
                                    PICTURE 9(2).
                       08  OPE-NUM-AIOP
                                    PICTURE 9(2).
                       08  OPE-NUM-MIOP
                                    PICTURE 9(2).
                       08  OPE-NUM-DIOP
                                    PICTURE 9(2).
      *                             FECHA TERMINO OPERACION
                   06  OPE-FEC-FTOP.
                       08  OPE-NUM-STOP
                                    PICTURE 9(2).
                       08  OPE-NUM-ATOP
                                    PICTURE 9(2).
                       08  OPE-NUM-MTOP
                                    PICTURE 9(2).
                       08  OPE-NUM-DTOP
                                    PICTURE 9(2).
      *                             MOTIVO TERMINO OPERACION
                   06  OPE-COD-MOTT PICTURE X(2).
      *                             FECHA REAPERTURA OPERACION
                   06  OPE-FEC-FROP.
                       08  OPE-NUM-SROP
                                    PICTURE 9(2).
                       08  OPE-NUM-AROP
                                    PICTURE 9(2).
                       08  OPE-NUM-MROP
                                    PICTURE 9(2).
                       08  OPE-NUM-DROP
                                    PICTURE 9(2).
      *                             IND MULTIPLES CLIENTES ( Y, O, ETC)
                   06  OPE-IND-MCLI PICTURE X(2).
      *                             CODIGO TIPO DE COMISION
                   06  OPE-COD-TCOM PICTURE X(2).
      *                             COD. INT. COMP. OPERACION DE CARGO
                   06  OPE-CIC-ICAR.
      *                             CENTRO ASIGNACION OPERACION CARGO
                       08  OPE-CAI-ICAR
                                    PICTURE X(4).
      *                             IDENT. INT. COMP. OPERACION CARGO
                       08  OPE-IIC-ICAR
                                    PICTURE X(8).
      *
       01  REG-DCP.
      *    COPYDD DGC-R-DCP.DGC-E-DCP00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO DCP
           02  DGC-E-DCP00.
      *                             DATOS COMPLEMENTARIOS PERSONAS
               04  DCP.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  DCP-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  DCP-KEY-IREG.
      *                             ID DATOS COMPLEMENTARIOS PERSONAS
                       08  DCP-KEY-IDCP.
      *                             COD. INT. COMP. CLIENTE
                       10  DCP-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  DCP-CAI-ICLI.
                       14  DCP-COD-TCLI
                                    PICTURE X(1).
                       14  DCP-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  DCP-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       08  DCP-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  DCP-FEC-FTRN.
                       12  DCP-NUM-STRN
                                    PICTURE 9(2).
                       12  DCP-NUM-ATRN
                                    PICTURE 9(2).
                       12  DCP-NUM-MTRN
                                    PICTURE 9(2).
                       12  DCP-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  DCP-HRA-HRTR.
                       12  DCP-NUM-HHTR
                                    PICTURE 9(2).
                       12  DCP-NUM-MMTR
                                    PICTURE 9(2).
                       12  DCP-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  DCP-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  DCP-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  DCP-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  DCP-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  DCP-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  DCP-COD-ATRN PICTURE X(12).
      *                             FECHA DE NACIMIENTO
                   06  DCP-FEC-FNAC.
                       08  DCP-NUM-SNAC
                                    PICTURE 9(2).
                       08  DCP-NUM-ANAC
                                    PICTURE 9(2).
                       08  DCP-NUM-MNAC
                                    PICTURE 9(2).
                       08  DCP-NUM-DNAC
                                    PICTURE 9(2).
      *                             CODIGO DEL PAIS DE LA NACIONALIDAD
                   06  DCP-COD-PNAC PICTURE X(3).
      *                             INDICADOR DE SEXO
                   06  DCP-IND-SEXO PICTURE X(1).
      *                             INDICADOR DE ESTADO CIVIL
                   06  DCP-IND-ECIV PICTURE X(3).
      *                             CEDULA DE IDENTIDAD
                   06  DCP-NUM-CIDE PICTURE X(8).
      *                             DIGITO VERIFICADOR CEDULA IDENTIDAD
                   06  DCP-VRF-CIDE PICTURE X(1).
      *                             GABINETE CEDULA DE IDENTIDAD
                   06  DCP-GLS-GABI PICTURE X(12).
      *                             CODIGO TIPO BANCA
                   06  DCP-COD-TBAN PICTURE X(3).
      *                             INDICADOR NIVEL EDUCACIONAL
                   06  DCP-IND-NEDU PICTURE X(3).
      *                             CODIGO DE ACTIVIDAD SECUNDARIA
                   06  DCP-COD-ACTS PICTURE X(9).
      *                             CODIGO DE PROFESION
                   06  DCP-COD-PROF PICTURE X(5).
      *                             INDICADOR DE SITUACION LABORAL
                   06  DCP-IND-SLAB PICTURE X(3).
      *                             CODIGO CATEGORIA SOCIOECONOMICA
                   06  DCP-COD-CSEC PICTURE X(2).
      *                             INDICADOR SITUACION CASA
                   06  DCP-IND-CASA PICTURE X(5).
      *                             # MSJ ASOC A NOMBRE DE PILA (SBT)
                   06  DCP-NUM-NMSG PICTURE 9(4).
      *                             INDICADOR REGIMEN CONYUGAL
                   06  DCP-IND-RCNY PICTURE X(1).
      *                             TIPO PLAN
                   06  DCP-COD-TPLN PICTURE X(1).
      *
       01  REG-DCE.
      *    COPYDD DGC-R-DCE.DGC-E-DCE00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO DCE
           02  DGC-E-DCE00.
      *                             DATOS COMPLEMENTARIOS EMPRESAS
               04  DCE.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  DCE-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  DCE-KEY-IREG.
      *                             IDENTIFICACION DATOS COMPLEMENTARIOS
                       08  DCE-KEY-IDCE.
      *                             COD. INT. COMP. CLIENTE
                       10  DCE-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  DCE-CAI-ICLI.
                       14  DCE-COD-TCLI
                                    PICTURE X(1).
                       14  DCE-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  DCE-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       08  DCE-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  DCE-FEC-FTRN.
                       12  DCE-NUM-STRN
                                    PICTURE 9(2).
                       12  DCE-NUM-ATRN
                                    PICTURE 9(2).
                       12  DCE-NUM-MTRN
                                    PICTURE 9(2).
                       12  DCE-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  DCE-HRA-HRTR.
                       12  DCE-NUM-HHTR
                                    PICTURE 9(2).
                       12  DCE-NUM-MMTR
                                    PICTURE 9(2).
                       12  DCE-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  DCE-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  DCE-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  DCE-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  DCE-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  DCE-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  DCE-COD-ATRN PICTURE X(12).
      *                             NOMBRE FANTASIA
                   06  DCE-GLS-NFAN PICTURE X(40).
      *                             EXT CODIGO TIPO DE SOCIEDAD
                   06  DCE-COD-TSOC PICTURE X(2).
      *                             FECHA CONSTITUCION EMPRESA
                   06  DCE-FEC-FCTT.
                       08  DCE-NUM-SCTT
                                    PICTURE 9(2).
                       08  DCE-NUM-ACTT
                                    PICTURE 9(2).
                       08  DCE-NUM-MCTT
                                    PICTURE 9(2).
                       08  DCE-NUM-DCTT
                                    PICTURE 9(2).
      *                             FECHA ULTIMA MODIFICACION ESCRITURA
                   06  DCE-FEC-FUME.
                       08  DCE-NUM-SUME
                                    PICTURE 9(2).
                       08  DCE-NUM-AUME
                                    PICTURE 9(2).
                       08  DCE-NUM-MUME
                                    PICTURE 9(2).
                       08  DCE-NUM-DUME
                                    PICTURE 9(2).
      *                             FECHA CADUCIDAD EMPRESA
                   06  DCE-FEC-FCAD.
                       08  DCE-NUM-SCAD
                                    PICTURE 9(2).
                       08  DCE-NUM-ACAD
                                    PICTURE 9(2).
                       08  DCE-NUM-MCAD
                                    PICTURE 9(2).
                       08  DCE-NUM-DCAD
                                    PICTURE 9(2).
      *                             INDICADOR RENOVACION AUTOMATICA ( S/
                   06  DCE-IND-RAUT PICTURE X(1).
      *                             OPR INDICADOR CASA MATRIZ O SUCURSAL
                   06  DCE-IND-CMOS PICTURE X(1).
      *                             EXT INDICADOR CATEGORIA ( COMO DEUDO
                   06  DCE-IND-CATE PICTURE X(3).
      *                             EXT TIPO DE EMPRESA  ( COMO DEUDOR )
                   06  DCE-IND-TEMP PICTURE X(3).
      *                             CODIGO TIPO BANCA
                   06  DCE-COD-TBAN PICTURE X(3).
      *                             TIPO PLAN
                   06  DCE-COD-TPLN PICTURE X(1).
      *                             PAIS DE NACIMIENTO
                   06  DCE-COD-PNAC PICTURE X(3).
      *                             DISPONIBLE
                   06  DCE-GLS-DISP PICTURE X(32).
      *
       01  REG-DBC.
      *    COPYDD DGC-R-DBC.DGC-E-DBC00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO DBC
           02  DGC-E-DBC00.
      *                             DATOS BASICOS DEL CLIENTE
               04  DBC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  DBC-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  DBC-KEY-IREG.
      *                             IDENTIFICACION DATOS BASICOS CLIENTE
                       08  DBC-KEY-IDBC.
      *                             COD. INT. COMP. CLIENTE
                       10  DBC-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  DBC-CAI-ICLI.
                       14  DBC-COD-TCLI
                                    PICTURE X(1).
                       14  DBC-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  DBC-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       08  DBC-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  DBC-FEC-FTRN.
                       12  DBC-NUM-STRN
                                    PICTURE 9(2).
                       12  DBC-NUM-ATRN
                                    PICTURE 9(2).
                       12  DBC-NUM-MTRN
                                    PICTURE 9(2).
                       12  DBC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  DBC-HRA-HRTR.
                       12  DBC-NUM-HHTR
                                    PICTURE 9(2).
                       12  DBC-NUM-MMTR
                                    PICTURE 9(2).
                       12  DBC-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  DBC-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  DBC-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  DBC-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  DBC-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  DBC-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  DBC-COD-ATRN PICTURE X(12).
      *                             KEY RUT CLIENTE + INDICADOR O CALIFI
                   06  DBC-IDC-ICLI.
      *                             RUT CLIENTE
                       08  DBC-NUM-ICLI
                                    PICTURE X(8).
      *                             INDICADOR TIPO RUT
                       08  DBC-IND-ICLI
                                    PICTURE X(1).
      *                             EXTENSION DEL RUT
                       08  DBC-GLS-ICLI
                                    PICTURE X(3).
      *                             KEY DIGITO VERIFICADOR DEL RUT
                   06  DBC-VRF-ICLI PICTURE X(1).
      *                             KEY NOMBRE CLIENTE GENERICO (PERSONA
                   06  DBC-GLS-NOMC.
                       08  DBC-GLS-APAT
                                    PICTURE X(25).
                       08  DBC-GLS-AMAT
                                    PICTURE X(25).
                       08  DBC-GLS-NOMB
                                    PICTURE X(25).
      *                             SOUNDEX DEL NOMBRE DE CLIENTE
                   06  DBC-SNX-NOMC.
                       08  DBC-SNX-APAT
                                    PICTURE X(3).
                       08  DBC-SNX-AMAT
                                    PICTURE X(3).
                       08  DBC-SNX-NOMB
                                    PICTURE X(3).
      *                             NUMERO DE DIRECCION DE ENVIO CORRESP
                   06  DBC-NUM-DCOR PICTURE 9(2).
      *                             OPR IND TIPO CLIENTE ( PERSONA O EMP
                   06  DBC-IND-TICL PICTURE X(1).
      *                             TIPO CLIENTE (POTENCIAL,NO CLIENTE,G
                   06  DBC-COD-TICL PICTURE X(2).
      *                             CODIGO CLASIFICACION COMERCIAL
                   06  DBC-COD-CLCM PICTURE X(2).
      *                             RCM CODIGO DEL EJECUTIVO ENCARGADO D
                   06  DBC-COD-EJEC PICTURE X(12).
      *                             OPR CODIGO DE OFICINA DONDE OPERA EL
                   06  DBC-COD-OFIC PICTURE X(3).
      *                             RCM INDICADOR SI OPERA EN OTRA OFICI
                   06  DBC-IND-OOFI PICTURE X(1).
      *                             EXT CODIGO ACTIVIDAD ECONOMICA
                   06  DBC-COD-ACTI PICTURE X(9).
      *                             EXT ORIGEN Y DESTINO SECT (COMP.INST
                   06  DBC-COD-ODSE PICTURE X(3).
      *                             INF FECHA INGRESO AL PLAN
                   06  DBC-FEC-FPLN.
                       08  DBC-NUM-SPLN
                                    PICTURE 9(2).
                       08  DBC-NUM-APLN
                                    PICTURE 9(2).
                       08  DBC-NUM-MPLN
                                    PICTURE 9(2).
                       08  DBC-NUM-DPLN
                                    PICTURE 9(2).
      *                             CODIGO DE SEGMENTO
                   06  DBC-COD-SETO PICTURE X(5).
      *                             PERIODO DE SEGMENTO (SSAAMM)
                   06  DBC-PDO-SETO PICTURE X(6).
      *                             INF INDICADOR DE MULTAS BANCO CENTRA
                   06  DBC-IND-MUBC PICTURE X(1).
      *                             INF INDICADOR DE JUICIOS BANCO CENTR
                   06  DBC-IND-JUBC PICTURE X(1).
      *                             EXT CODIGO ESTADO CLIENTE ( COMO DEU
                   06  DBC-MSC-SCLI PICTURE X(1).
      *                             FECHA ESTADO CLIENTE
                   06  DBC-FEC-FSCL.
                       08  DBC-NUM-SSCL
                                    PICTURE 9(2).
                       08  DBC-NUM-ASCL
                                    PICTURE 9(2).
                       08  DBC-NUM-MSCL
                                    PICTURE 9(2).
                       08  DBC-NUM-DSCL
                                    PICTURE 9(2).
      *                             CODIGO DE BLOQUEO DEL CLIENTE
                   06  DBC-COD-BLOC PICTURE X(2).
      *                             NUMERO TOTAL DE DIRECCIONES
                   06  DBC-NUM-TDIR PICTURE 9(2).
      *                             NUMERO TOTAL DE COMENTARIOS
                   06  DBC-NUM-TCMT PICTURE 9(3).
      *                             FECHA INICIO OPERACION
                   06  DBC-FEC-FIOP.
                       08  DBC-NUM-SIOP
                                    PICTURE 9(2).
                       08  DBC-NUM-AIOP
                                    PICTURE 9(2).
                       08  DBC-NUM-MIOP
                                    PICTURE 9(2).
                       08  DBC-NUM-DIOP
                                    PICTURE 9(2).
      *                             FECHA TERMINO OPERACION
                   06  DBC-FEC-FTOP.
                       08  DBC-NUM-STOP
                                    PICTURE 9(2).
                       08  DBC-NUM-ATOP
                                    PICTURE 9(2).
                       08  DBC-NUM-MTOP
                                    PICTURE 9(2).
                       08  DBC-NUM-DTOP
                                    PICTURE 9(2).
      *                             NUMERO OPERACIONES FECHA TERMINO NUL
                   06  DBC-NUM-OFTN PICTURE 9(3).
      *                             FECHA INGRESO DE DATOS A LA BASE DE
                   06  DBC-FEC-FINP.
                       08  DBC-NUM-SINP
                                    PICTURE 9(2).
                       08  DBC-NUM-AINP
                                    PICTURE 9(2).
                       08  DBC-NUM-MINP
                                    PICTURE 9(2).
                       08  DBC-NUM-DINP
                                    PICTURE 9(2).
      *
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  ELM-LIS-RCO.
           03 ELEM-RCO             PIC X(05)  VALUE 'RCO00'.
           03 ELSE-RCO             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.
      *
       01  ELM-LIS-OPE.
           03 ELEM-OPE             PIC X(05)  VALUE 'OPE00'.
           03 ELSE-OPE             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.
      *
       01  ELM-LIS-DCP.
           03 ELEM-DCP             PIC X(05)  VALUE 'DCP00'.
           03 ELSE-DCP             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.
      *
       01  ELM-LIS-DCE.
           03 ELEM-DCE             PIC X(05)  VALUE 'DCE00'.
           03 ELSE-DCE             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.
      *
       01  ELM-LIS-DBC.
           03 ELEM-DBC             PIC X(05)  VALUE 'DBC00'.
           03 ELSE-DBC             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.
      *
      *-----------------------------------------------------------------

       PROCEDURE DIVISION.
      *===================
      *
       A-CONTROL SECTION.
      *==================
      *
       AA-INICIO.
      *----------
           ENTRY 'DBMSCBL'.
           PERFORM B-INICIAR.

       AB-PROCESO.
      *-----------
           IF SW-ERROR = 0
              PERFORM C-PROCESAR
           END-IF.

       AC-FIN.
      *-------
           PERFORM TERMINO.

       AZ-FIN-PROGRAMA.
      *----------------
           GOBACK.

       B-INICIAR SECTION.
      *==================
       BA-CONTROL-PROCESO.
      *-------------------
           PERFORM BB-ACCEPT-FECHA.
           PERFORM BC-OPEN-ARCHIVOS.
           GO TO BZ-FIN-SECTION.

       BB-ACCEPT-FECHA.
      *----------------
      * ACCEPTAR FECHA
           ACCEPT FECHA-PARAMETRO
           MOVE FECHA-PARAMETRO TO W-FEC-PROC
           MOVE WE-DD-DATE TO DD-BRITISH.
           MOVE WE-MM-DATE TO MM-BRITISH.
           MOVE WE-AA-DATE TO AA-BRITISH.
      *
       BC-OPEN-ARCHIVOS.
      *-----------------
           OPEN OUTPUT SOPPLN.
           IF FS-SOPPLN  NOT = '00'
               DISPLAY '<< ERROR PROG.SGCPBEPL SE CANCELA AL ABRIR >>'
               DISPLAY '<< SOPPLN DE SALIDA  STATUS ====>>>'
               FS-SOPPLN
               MOVE 1    TO SW-ERROR
               MOVE 100  TO RETURN-CODE
               PERFORM TERMINO.
      *
       BZ-FIN-SECTION.
      *---------------
           EXIT.

       C-PROCESAR SECTION.
      *===================
           PERFORM CB-LEER-REG.
           PERFORM CC-PROCESO UNTIL REQ-RTNCD-OPE NOT = SPACES OR
                   SW-ERROR NOT = 0
                   OR REQ-RTNCD-OPE = '19'.
           GO TO CZ-FIN-SECTION.
      *
       CB-LEER-REG.
      *------------
           MOVE '0000'      TO REQ-OPE-COD-1.
           MOVE LOW-VALUE   TO REQ-OPE-COD-2.
           MOVE 'GSETL'     TO REQ-COMAN-OPE.
           CALL 'DBNTRY' USING UIB REQ-AREA-OPE REG-OPE ELM-LIS-OPE.
           IF REQ-RTNCD-OPE NOT = SPACES
              DISPLAY 'ERROR AL UBICAR EN TABLA OPE '
              DISPLAY 'CODIGO : '  REQ-RTNCD-OPE
              MOVE 1 TO SW-ERROR
           ELSE
              MOVE 'GETIT'     TO REQ-COMAN-OPE
              CALL 'DBNTRY' USING UIB REQ-AREA-OPE REG-OPE ELM-LIS-OPE
              IF REQ-RTNCD-OPE NOT = SPACES
                 MOVE 1 TO SW-ERROR
              END-IF
           END-IF.
      *
       CC-PROCESO.
      *------------
           IF OPE-COD-TOPR = 'CCT' AND OPE-FEC-FIOP NOT > W-FEC-PROC AND
             (OPE-FEC-FTOP NOT < W-FEC-PROC OR = ZEROES)
              ADD 1 TO WR-LEIDOS-VIG
              PERFORM CD-LEER-RCO
           END-IF.
           PERFORM CD-LEER-SGTE-OPE.
      *
       CD-LEER-RCO.
      *------------
           MOVE '0000'       TO REQ-RCO-COD-1.
           MOVE OPE-IIC-IOPE TO REQ-RCO-COD-2.
           MOVE 'REDKY'      TO REQ-COMAN-RCO.
           CALL 'DBNTRY' USING UIB REQ-AREA-RCO REG-RCO ELM-LIS-RCO.
           IF REQ-RTNCD-RCO NOT = SPACES AND REQ-RTNCD-RCO NOT = '14'
              DISPLAY 'ERROR AL UBICAR EN TABLA RCO '
              DISPLAY 'CODIGO : '  REQ-RTNCD-RCO
              DISPLAY 'CUENTA : '  OPE-IIC-IOPE
              MOVE 1 TO SW-ERROR
           ELSE
              IF REQ-RTNCD-RCO = '14'
                 ADD 1 TO WR-SIN-RCO
              ELSE
                 ADD 1  TO   W-LEID-RCO
                 PERFORM CD-LEER-DBC
              END-IF
           END-IF.
      *
       CD-LEER-DBC.
      *------------
           MOVE RCO-CIC-ICLI    TO REQ-DBC-CIC.
           MOVE 'REDKY'         TO REQ-COMAN-DBC.
           CALL 'DBNTRY' USING UIB REQ-AREA-DBC REG-DBC ELM-LIS-DBC.
           IF REQ-RTNCD-DBC NOT = SPACES AND REQ-RTNCD-DBC NOT = '14'
              DISPLAY 'ERROR AL UBICAR EN TABLA DBC '
              DISPLAY 'CODIGO : '  REQ-RTNCD-DBC
              DISPLAY 'CLIENTE: '  RCO-CIC-ICLI
              MOVE 1 TO SW-ERROR
           ELSE
              IF REQ-RTNCD-DBC = '14'
                 ADD 1 TO WR-SIN-DBC
              ELSE
                 ADD   1      TO W-LEID-DBC
                 IF DBC-IND-TICL = 'P'
                    PERFORM CD1-BUSCAR-PLAN-PERSONAS
                 ELSE
                    PERFORM CD1-BUSCAR-PLAN-EMPRESAS
                 END-IF
              END-IF
           END-IF.
      *
       CD1-BUSCAR-PLAN-PERSONAS.
      *-------------------------
           MOVE DBC-CIC-ICLI    TO REQ-DCP-CIC.
           MOVE 'REDKY'         TO REQ-COMAN-DCP.
           CALL 'DBNTRY' USING UIB REQ-AREA-DCP REG-DCP ELM-LIS-DCP.
           IF REQ-RTNCD-DCP NOT = SPACES AND REQ-RTNCD-DCP NOT = '14'
              DISPLAY 'ERROR AL UBICAR EN TABLA DCP '
              DISPLAY 'CODIGO : '  REQ-RTNCD-DCP
              DISPLAY 'CLIENTE: '  DBC-CIC-ICLI
              MOVE 1 TO SW-ERROR
           ELSE
              IF REQ-RTNCD-DCP = '14'
                 ADD 1 TO WR-SIN-DCP
              ELSE
                 ADD 1                   TO W-LEID-DCP
                 MOVE 'P'                TO W-TIP-CLIENTE
                 MOVE DCP-COD-TPLN       TO W-PLAN-AUX
                 PERFORM CD1-GRABAR-REG
              END-IF
           END-IF.
      *
       CD1-BUSCAR-PLAN-EMPRESAS.
      *-------------------------
           MOVE DBC-CIC-ICLI    TO REQ-DCE-CIC.
           MOVE 'REDKY'         TO REQ-COMAN-DCE.
           CALL 'DBNTRY' USING UIB REQ-AREA-DCE REG-DCE ELM-LIS-DCE.
           IF REQ-RTNCD-DCE NOT = SPACES AND REQ-RTNCD-DCE NOT = '14'
              DISPLAY 'ERROR AL UBICAR EN TABLA DCE '
              DISPLAY 'CODIGO : '  REQ-RTNCD-DCE
              DISPLAY 'CLIENTE: '  DBC-CIC-ICLI
              MOVE 1 TO SW-ERROR
           ELSE
              IF REQ-RTNCD-DCP = '14'
                 ADD 1 TO WR-SIN-DCE
              ELSE
                 ADD   1   TO W-LEID-DCE
                 MOVE 'E'  TO W-TIP-CLIENTE
                 MOVE DCE-COD-TPLN       TO W-PLAN-AUX
                 PERFORM CD1-GRABAR-REG
              END-IF
           END-IF.
      *
       CD-LEER-SGTE-OPE.
      *-----------------
           MOVE 'GETIT'     TO REQ-COMAN-OPE.
           CALL 'DBNTRY' USING UIB REQ-AREA-OPE REG-OPE ELM-LIS-OPE.
           IF REQ-RTNCD-OPE NOT = SPACES AND REQ-RTNCD-OPE NOT = '19'
              DISPLAY 'ERROR AL LEER EN TABLA OPE '
              DISPLAY 'CODIGO : '  REQ-RTNCD-OPE
              MOVE 1 TO SW-ERROR
           ELSE
              IF REQ-RTNCD-OPE = '19'
                 NEXT SENTENCE
              ELSE
                 ADD 1   TO W-LEID-OPE
              END-IF
           END-IF.
      *
       CD1-GRABAR-REG.
      *---------------
           IF OPE-COD-OFOP < '010'
              MOVE '090'        TO WR-SUCUR
           ELSE
              MOVE OPE-COD-OFOP TO WR-SUCUR
           END-IF.
           MOVE SPACES             TO REG-SOPPLN.
           MOVE WR-SUCUR           TO PLN-SUC.
           MOVE W-TIP-CLIENTE      TO PLN-TIP-CLI
           MOVE W-PLAN-AUX         TO PLN-PLAN
           MOVE FECHA-BRITISH      TO PLN-FEC-PROC
           WRITE REG-SOPPLN.
           IF FS-SOPPLN  NOT = '00'
               DISPLAY '<< ERROR PROG.SGCPBEPL SE CANCELA AL GRABAR >>'
               DISPLAY '<< SOPPLN DE SALIDA  STATUS ====>>>'
               FS-SOPPLN
               MOVE 1    TO SW-ERROR
               MOVE 100  TO RETURN-CODE
               PERFORM TERMINO
           ELSE
              ADD 1 TO WR-GRABADOS.
      *
       CZ-FIN-SECTION.
      *---------------
           EXIT.
      *
       TERMINO.
      *--------
           IF SW-ERROR = 1
              DISPLAY ' '
              DISPLAY '*****************************************'
              DISPLAY '*   EL PROCESO  TERMINO CON ERROR       *'
              DISPLAY '*          SGCPBEPL                     *'
              DISPLAY '*****************************************'
           END-IF.
           DISPLAY ' '.
           DISPLAY ' FECHA : ' FECHA-BRITISH.
           DISPLAY ' '.
           DISPLAY '*****************************************'.
           DISPLAY '*                                       *'.
           DISPLAY '* ESTADISTICAS DEL PROCESO SGCPBEPL      *'.
           DISPLAY '*                                       *'.
           DISPLAY '*****************************************'.
           DISPLAY ' '.
           DISPLAY 'TOTAL LEIDOS OPE          : ' W-LEID-OPE.
           DISPLAY 'TOTAL CCT VIGENTES OPE    : ' WR-LEIDOS-VIG.
           DISPLAY 'TOTAL LEIDOS RCO          : ' W-LEID-RCO.
           DISPLAY 'TOTAL LEIDOS DBC          : ' W-LEID-DBC.
           DISPLAY 'TOTAL LEIDOS DCP          : ' W-LEID-DCP.
           DISPLAY 'TOTAL LEIDOS DCE          : ' W-LEID-DCP.
           DISPLAY ' '.
           DISPLAY 'TOTAL CTAS NO ENCONT. RCO : ' WR-SIN-RCO.
           DISPLAY 'TOTAL CTAS NO ENCONT. DBC : ' WR-SIN-DBC.
           DISPLAY 'TOTAL CTAS NO ENCONT. DCP : ' WR-SIN-DCP.
           DISPLAY 'TOTAL CTAS NO ENCONT. DCE : ' WR-SIN-DCE.
           DISPLAY ' '.
           DISPLAY 'TOTAL GRABADOS SOPPLN     : ' WR-GRABADOS.
           DISPLAY ' '.
           DISPLAY '*****************************************'.
           CLOSE  SOPPLN.
           STOP RUN.
      *
