       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. SGCPGECI
       AUTHOR.     CARLOS CID BADILLA.
       DATE-WRITTEN. 09 DE ENERO 1998.
       DATE-COMPILED.
           EJECT
       ENVIRONMENT DIVISION.
      *=====================
      *
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *
           SELECT ARCHIVO    ASSIGN   TO ARCHIVO
                             FILE STATUS IS FS-ARCHIVO.
      *
           SELECT MAESTRO  ASSIGN TO MAESTRO
                                       ORGANIZATION IS INDEXED
                                       ACCESS       IS SEQUENTIAL
                                       RECORD KEY   IS KEY-MAESTRO
                                       FILE STATUS  IS FS-MAESTRO.
      *

       DATA DIVISION.
      *==============
       FILE SECTION.
      *-------------
       FD  ARCHIVO            LABEL  RECORD IS STANDARD
                              RECORD CONTAINS 80 CHARACTERS
                              BLOCK  CONTAINS 0 RECORDS.

       01  REG-ARCHIVO.
           02 ARC-IND-LAB        PIC X(03).
           02 ARC-FILLER         PIC X(77).

      *
       FD  MAESTRO            LABEL  RECORD IS STANDARD
                              RECORD CONTAINS 31 CHARACTERS
                              BLOCK  CONTAINS 0 RECORDS.
       01  REG-MAESTRO.
           02 KEY-MAESTRO.
              03 MAE-CIC              PIC X(12).
           02 MAE-OFI              PIC X(03).
           02 MAE-REG              PIC X(03).
           02 MAE-EJE              PIC X(12).
           02 MAE-EST              PIC X(01).
      *

       WORKING-STORAGE SECTION.
      *------------------------
       77  FS-ARCHIVO                  PIC X(02) VALUE SPACES.
       77  FS-MAESTRO                  PIC X(02) VALUE SPACES.
       77  WS-ENC-SIT-LAB              PIC X(02) VALUE SPACES.

       01  WS-CONTADORES.
           03  WS-LEID-ARCHIVO         PIC 9(07) VALUE 0.
           03  WS-LEID-MAESTRO         PIC 9(07) VALUE 0.
           03  WS-GRAB-ARCHIVO         PIC 9(07) VALUE 0.
           03  WS-GRAB-MAESTRO         PIC 9(07) VALUE 0.
           03  WS-LEID-DCP             PIC 9(07) VALUE 0.
           03  WS-LEID-DBC             PIC 9(07) VALUE 0.
           03  WS-NO-DCP               PIC 9(07) VALUE 0.
           03  WS-NO-DBC               PIC 9(07) VALUE 0.
           03  CONTA-TIC               PIC 9(03) COMP-3 VALUE ZEROES.
      *
       01  W-FECHAS-DE-TRABAJO.
           03 WR-FECHA-PROCESO.
              04 FILLER                             PIC X(29).
              04 WR-FEC-INIC-TRAN.
                 06 WR-FECHA-SS                     PIC 9(02).
                 06 WR-FECHA-AA                     PIC 9(02).
                 06 WR-FECHA-MM                     PIC 9(02).
                 06 WR-FECHA-DD                     PIC 9(02).
              04 WR-FILLER                       PIC X(17).
              04 WR-FECHA-PROC-2.
                 06 WR-FECHA-SS                     PIC 9(02).
                 06 WR-FECHA-AA                     PIC 9(02).
                 06 WR-FECHA-MM                     PIC 9(02).
                 06 WR-FECHA-DD                     PIC 9(02).
      *
      *TABLA DE SITUACION LABORAL
       01  W-TAB-TIC.
           05  TABLA-SIT-LAB  OCCURS 50 TIMES DEPENDING ON CONTA-TIC
                                               INDEXED BY TSUX-TIC.
               10 WT-SIT-LAB           PIC X(03).
      *
      *

      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           05 FILLER               PIC X(32) VALUE 'SGCPGECI'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  REQ-AREA-DBC.
           05 REQ-COMAN-DBC            PIC X(05).
           05 REQ-TBLNM-DBC            PIC X(03) VALUE 'DBC'.
           05 REQ-KEYNM-DBC            PIC X(05) VALUE 'DBC00'.
           05 REQ-RTNCD-DBC            PIC X(02) VALUE SPACES.
           05 REQ-INRCD-DBC            PIC X(01).
           05 REQ-DBIDE-DBC            PIC S9(4) COMP VALUE +100.
           05 REQ-TBLID-DBC            PIC X(07).
           05 FILLER                   PIC X(25).
           05 REQ-CTMAX-DBC            PIC S9(4) COMP.
           05 REQ-IOCNT-DBC            PIC S9(4) COMP.
           05 FILLER                   PIC X(22).
           05 REQ-KYVAL-DBC.
              10 REQ-DBC-CIC       PIC X(12).
      *
       01  REQ-AREA-DCP.
           05 REQ-COMAN-DCP            PIC X(05).
           05 REQ-TBLNM-DCP            PIC X(03) VALUE 'DCP'.
           05 REQ-KEYNM-DCP            PIC X(05) VALUE 'DCP00'.
           05 REQ-RTNCD-DCP            PIC X(02) VALUE SPACES.
           05 REQ-INRCD-DCP            PIC X(01).
           05 REQ-DBIDE-DCP            PIC S9(4) COMP VALUE +100.
           05 REQ-TBLID-DCP            PIC X(07).
           05 FILLER                   PIC X(25).
           05 REQ-CTMAX-DCP            PIC S9(4) COMP.
           05 REQ-IOCNT-DCP            PIC S9(4) COMP.
           05 FILLER                   PIC X(22).
           05 REQ-KYVAL-DCP.
              10 REQ-DCP-CIC           PIC X(12).

      *
       01  REG-DBC.
      *    COPYDD DGC-R-DBC.DGC-E-DBC00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO DBC
           02  DGC-E-DBC00.
      *                             DATOS BASICOS DEL CLIENTE
               04  DBC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  DBC-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  DBC-KEY-IREG.
      *                             IDENTIFICACION DATOS BASICOS CLIENTE
                       08  DBC-KEY-IDBC.
      *                             COD. INT. COMP. CLIENTE
                       10  DBC-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  DBC-CAI-ICLI.
                       14  DBC-COD-TCLI
                                    PICTURE X(1).
                       14  DBC-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  DBC-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       08  DBC-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  DBC-FEC-FTRN.
                       12  DBC-NUM-STRN
                                    PICTURE 9(2).
                       12  DBC-NUM-ATRN
                                    PICTURE 9(2).
                       12  DBC-NUM-MTRN
                                    PICTURE 9(2).
                       12  DBC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  DBC-HRA-HRTR.
                       12  DBC-NUM-HHTR
                                    PICTURE 9(2).
                       12  DBC-NUM-MMTR
                                    PICTURE 9(2).
                       12  DBC-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  DBC-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  DBC-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  DBC-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  DBC-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  DBC-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  DBC-COD-ATRN PICTURE X(12).
      *                             KEY RUT CLIENTE + INDICADOR O CALIFI
                   06  DBC-IDC-ICLI.
      *                             RUT CLIENTE
                       08  DBC-NUM-ICLI
                                    PICTURE X(8).
      *                             INDICADOR TIPO RUT
                       08  DBC-IND-ICLI
                                    PICTURE X(1).
      *                             EXTENSION DEL RUT
                       08  DBC-GLS-ICLI
                                    PICTURE X(3).
      *                             KEY DIGITO VERIFICADOR DEL RUT
                   06  DBC-VRF-ICLI PICTURE X(1).
      *                             KEY NOMBRE CLIENTE GENERICO (PERSONA
                   06  DBC-GLS-NOMC.
                       08  DBC-GLS-APAT
                                    PICTURE X(25).
                       08  DBC-GLS-AMAT
                                    PICTURE X(25).
                       08  DBC-GLS-NOMB
                                    PICTURE X(25).
      *                             SOUNDEX DEL NOMBRE DE CLIENTE
                   06  DBC-SNX-NOMC.
                       08  DBC-SNX-APAT
                                    PICTURE X(3).
                       08  DBC-SNX-AMAT
                                    PICTURE X(3).
                       08  DBC-SNX-NOMB
                                    PICTURE X(3).
      *                             NUMERO DE DIRECCION DE ENVIO CORRESP
                   06  DBC-NUM-DCOR PICTURE 9(2).
      *                             OPR IND TIPO CLIENTE ( PERSONA O EMP
                   06  DBC-IND-TICL PICTURE X(1).
      *                             TIPO CLIENTE (POTENCIAL,NO CLIENTE,G
                   06  DBC-COD-TICL PICTURE X(2).
      *                             CODIGO CLASIFICACION COMERCIAL
                   06  DBC-COD-CLCM PICTURE X(2).
      *                             RCM CODIGO DEL EJECUTIVO ENCARGADO D
                   06  DBC-COD-EJEC PICTURE X(12).
      *                             OPR CODIGO DE OFICINA DONDE OPERA EL
                   06  DBC-COD-OFIC PICTURE X(3).
      *                             RCM INDICADOR SI OPERA EN OTRA OFICI
                   06  DBC-IND-OOFI PICTURE X(1).
      *                             EXT CODIGO ACTIVIDAD ECONOMICA
                   06  DBC-COD-ACTI PICTURE X(9).
      *                             EXT ORIGEN Y DESTINO SECT (COMP.INST
                   06  DBC-COD-ODSE PICTURE X(3).
      *                             INF FECHA INGRESO AL PLAN
                   06  DBC-FEC-FPLN.
                       08  DBC-NUM-SPLN
                                    PICTURE 9(2).
                       08  DBC-NUM-APLN
                                    PICTURE 9(2).
                       08  DBC-NUM-MPLN
                                    PICTURE 9(2).
                       08  DBC-NUM-DPLN
                                    PICTURE 9(2).
      *                             CODIGO DE SEGMENTO
                   06  DBC-COD-SETO PICTURE X(5).
      *                             PERIODO DE SEGMENTO (SSAAMM)
                   06  DBC-PDO-SETO PICTURE X(6).
      *                             INF INDICADOR DE MULTAS BANCO CENTRA
                   06  DBC-IND-MUBC PICTURE X(1).
      *                             INF INDICADOR DE JUICIOS BANCO CENTR
                   06  DBC-IND-JUBC PICTURE X(1).
      *                             EXT CODIGO ESTADO CLIENTE ( COMO DEU
                   06  DBC-MSC-SCLI PICTURE X(1).
      *                             FECHA ESTADO CLIENTE
                   06  DBC-FEC-FSCL.
                       08  DBC-NUM-SSCL
                                    PICTURE 9(2).
                       08  DBC-NUM-ASCL
                                    PICTURE 9(2).
                       08  DBC-NUM-MSCL
                                    PICTURE 9(2).
                       08  DBC-NUM-DSCL
                                    PICTURE 9(2).
      *                             CODIGO DE BLOQUEO DEL CLIENTE
                   06  DBC-COD-BLOC PICTURE X(2).
      *                             NUMERO TOTAL DE DIRECCIONES
                   06  DBC-NUM-TDIR PICTURE 9(2).
      *                             NUMERO TOTAL DE COMENTARIOS
                   06  DBC-NUM-TCMT PICTURE 9(3).
      *                             FECHA INICIO OPERACION
                   06  DBC-FEC-FIOP.
                       08  DBC-NUM-SIOP
                                    PICTURE 9(2).
                       08  DBC-NUM-AIOP
                                    PICTURE 9(2).
                       08  DBC-NUM-MIOP
                                    PICTURE 9(2).
                       08  DBC-NUM-DIOP
                                    PICTURE 9(2).
      *                             FECHA TERMINO OPERACION
                   06  DBC-FEC-FTOP.
                       08  DBC-NUM-STOP
                                    PICTURE 9(2).
                       08  DBC-NUM-ATOP
                                    PICTURE 9(2).
                       08  DBC-NUM-MTOP
                                    PICTURE 9(2).
                       08  DBC-NUM-DTOP
                                    PICTURE 9(2).
      *                             NUMERO OPERACIONES FECHA TERMINO NUL
                   06  DBC-NUM-OFTN PICTURE 9(3).
      *                             FECHA INGRESO DE DATOS A LA BASE DE
                   06  DBC-FEC-FINP.
                       08  DBC-NUM-SINP
                                    PICTURE 9(2).
                       08  DBC-NUM-AINP
                                    PICTURE 9(2).
                       08  DBC-NUM-MINP
                                    PICTURE 9(2).
                       08  DBC-NUM-DINP
                                    PICTURE 9(2).
       01  REG-DCP.
      *    COPYDD DGC-R-DCP.DGC-E-DCP00(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO DCP
           02  DGC-E-DCP00.
      *                             DATOS COMPLEMENTARIOS PERSONAS
               04  DCP.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   06  DCP-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   06  DCP-KEY-IREG.
      *                             ID DATOS COMPLEMENTARIOS PERSONAS
                       08  DCP-KEY-IDCP.
      *                             COD. INT. COMP. CLIENTE
                       10  DCP-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       12  DCP-CAI-ICLI.
                       14  DCP-COD-TCLI
                                    PICTURE X(1).
                       14  DCP-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       12  DCP-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       08  DCP-STP-ITRN.
      *                             FECHA TRANSACCION
                       10  DCP-FEC-FTRN.
                       12  DCP-NUM-STRN
                                    PICTURE 9(2).
                       12  DCP-NUM-ATRN
                                    PICTURE 9(2).
                       12  DCP-NUM-MTRN
                                    PICTURE 9(2).
                       12  DCP-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       10  DCP-HRA-HRTR.
                       12  DCP-NUM-HHTR
                                    PICTURE 9(2).
                       12  DCP-NUM-MMTR
                                    PICTURE 9(2).
                       12  DCP-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   06  DCP-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   06  DCP-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   06  DCP-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       08  DCP-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       08  DCP-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   06  DCP-COD-ATRN PICTURE X(12).
      *                             FECHA DE NACIMIENTO
                   06  DCP-FEC-FNAC.
                       08  DCP-NUM-SNAC
                                    PICTURE 9(2).
                       08  DCP-NUM-ANAC
                                    PICTURE 9(2).
                       08  DCP-NUM-MNAC
                                    PICTURE 9(2).
                       08  DCP-NUM-DNAC
                                    PICTURE 9(2).
      *                             CODIGO DEL PAIS DE LA NACIONALIDAD
                   06  DCP-COD-PNAC PICTURE X(3).
      *                             INDICADOR DE SEXO
                   06  DCP-IND-SEXO PICTURE X(1).
      *                             INDICADOR DE ESTADO CIVIL
                   06  DCP-IND-ECIV PICTURE X(3).
      *                             CEDULA DE IDENTIDAD
                   06  DCP-NUM-CIDE PICTURE X(8).
      *                             DIGITO VERIFICADOR CEDULA IDENTIDAD
                   06  DCP-VRF-CIDE PICTURE X(1).
      *                             GABINETE CEDULA DE IDENTIDAD
                   06  DCP-GLS-GABI PICTURE X(12).
      *                             CODIGO TIPO BANCA
                   06  DCP-COD-TBAN PICTURE X(3).
      *                             INDICADOR NIVEL EDUCACIONAL
                   06  DCP-IND-NEDU PICTURE X(3).
      *                             CODIGO DE ACTIVIDAD SECUNDARIA
                   06  DCP-COD-ACTS PICTURE X(9).
      *                             CODIGO DE PROFESION
                   06  DCP-COD-PROF PICTURE X(5).
      *                             INDICADOR DE SITUACION LABORAL
                   06  DCP-IND-SLAB PICTURE X(3).
      *                             CODIGO CATEGORIA SOCIOECONOMICA
                   06  DCP-COD-CSEC PICTURE X(2).
      *                             INDICADOR SITUACION CASA
                   06  DCP-IND-CASA PICTURE X(5).
      *                             # MSJ ASOC A NOMBRE DE PILA (SBT)
                   06  DCP-NUM-NMSG PICTURE 9(4).
      *                             INDICADOR REGIMEN CONYUGAL
                   06  DCP-IND-RCNY PICTURE X(1).
      *                             TIPO PLAN
                   06  DCP-COD-TPLN PICTURE X(1).
      *
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  ELM-LIS-DBC.
           05 ELEM-DBC       PIC X(05) VALUE 'DBC00'.
           05 ELSE-DBC       PIC X(01).
           05 FILLER         PIC X(05) VALUE SPACES.
      *
       01  ELM-LIS-DCP.
           05 ELEM-DCP       PIC X(05) VALUE 'DCP00'.
           05 ELSE-DCP       PIC X(01).
           05 FILLER         PIC X(05) VALUE SPACES.
      *
           EJECT

       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           PERFORM 10-INICIALIZAR.
           PERFORM 30-LEER-ARCHIVO.
           PERFORM 40-LLENAR-TAB-SIT-LAB UNTIL FS-ARCHIVO = '10'.
           PERFORM 50-LEER-MAESTRO.
           PERFORM 60-PROCESO-MAESTRO UNTIL FS-MAESTRO = '10'
      *               OR WS-LEID-MAESTRO = 10
           PERFORM 250-ESTADISTICAS.
           GOBACK.

       10-INICIALIZAR.
      *---------------
           INITIALIZE WR-FECHA-PROCESO WS-CONTADORES.
           PERFORM 20-ABRIR-ARCHIVOS.

       20-ABRIR-ARCHIVOS.
      *------------------
           OPEN INPUT  ARCHIVO
           OPEN I-O    MAESTRO

           IF FS-ARCHIVO NOT = '00'
              DISPLAY  'ERROR OPEN INPUT ARCHIVO '
              DISPLAY 'VALOR FS-ARCHIVO : ' FS-ARCHIVO
              PERFORM 300-TERMINAR.

           IF FS-MAESTRO  NOT = '00'
              DISPLAY  'ERROR OPEN I-O   MAESTRO  '
              DISPLAY 'VALOR FS-MAESTRO :' FS-MAESTRO
              PERFORM 300-TERMINAR.
      * ACCEPTA FECHA DE PROCESO.
           ACCEPT WR-FECHA-PROCESO.
           DISPLAY WR-FECHA-PROCESO.
      *
       30-LEER-ARCHIVO.
      *----------------
           READ ARCHIVO
           IF FS-ARCHIVO NOT = '00' AND NOT = '10'
              DISPLAY  'ERROR READ ARCHIVO '
              DISPLAY 'VALOR FS-ARCHIVO   ' FS-ARCHIVO
              PERFORM 300-TERMINAR
           ELSE
              IF FS-ARCHIVO = '10'
                 NEXT SENTENCE
              ELSE
                 ADD  1   TO WS-LEID-ARCHIVO.
      *
       40-LLENAR-TAB-SIT-LAB.
      *-----------------------
           ADD 1                   TO CONTA-TIC.
           SET TSUX-TIC            TO CONTA-TIC.
           MOVE ARC-IND-LAB        TO WT-SIT-LAB(TSUX-TIC)
           PERFORM 30-LEER-ARCHIVO.
      *
       50-LEER-MAESTRO.
      *----------------
           READ MAESTRO
           IF FS-MAESTRO NOT = '00' AND NOT = '10'
              DISPLAY  'ERROR READ MAESTRO '
              DISPLAY 'VALOR FS-MAESTRO : ' FS-MAESTRO
              PERFORM 300-TERMINAR
           ELSE
              IF FS-MAESTRO = '10'
                 NEXT SENTENCE
              ELSE
                 ADD  1   TO WS-LEID-MAESTRO.
      *
       60-PROCESO-MAESTRO.
      *-------------------
           IF MAE-EST = 'N'
            MOVE MAE-CIC           TO REQ-DCP-CIC
            MOVE 'REDKY'           TO REQ-COMAN-DCP
            PERFORM 70-LEER-DCP
            IF REQ-RTNCD-DCP = SPACES
               MOVE 'NO'           TO WS-ENC-SIT-LAB
               PERFORM 80-ANALIZA-EST-SIT
      *        DISPLAY 'ENC. SIT. LAB : ' WS-ENC-SIT-LAB
               IF WS-ENC-SIT-LAB = 'SI'
                  MOVE MAE-CIC        TO REQ-DBC-CIC
                  MOVE 'REDKY'        TO REQ-COMAN-DBC
                  PERFORM 90-LEER-DBC
                  IF REQ-RTNCD-DBC = SPACES
                     DISPLAY ' FEC. DBC : ' DBC-FEC-FTRN ' NOT < '
                               WR-FEC-INIC-TRAN
                     IF DBC-FEC-FTRN NOT <  WR-FEC-INIC-TRAN
                        PERFORM 100-REWRITE-MAESTRO
                     END-IF
                  END-IF
               END-IF
            END-IF
           END-IF
           PERFORM 50-LEER-MAESTRO.
      *
       70-LEER-DCP.
      *------------
           CALL 'DBNTRY' USING UIB REQ-AREA-DCP REG-DCP
                                                 ELM-LIS-DCP.
           IF REQ-RTNCD-DCP NOT = ' ' AND REQ-RTNCD-DCP NOT = '14'
                 DISPLAY '********************************'
                 DISPLAY 'ERROR! PROBLEMA LECTURA INDICE *'
                 DISPLAY '       EN TABLA DCP -----------*'
                 DISPLAY 'CODIGO DE RETORNO :' REQ-RTNCD-DCP
                 DISPLAY 'REQ-AREA          :' REQ-AREA-DCP
                 DISPLAY 'CLAVE             :' REQ-KYVAL-DCP
                 PERFORM 300-TERMINAR
           ELSE
              IF REQ-RTNCD-DCP = '14'
                 ADD 1    TO         WS-NO-DCP
              ELSE
                 ADD 1    TO         WS-LEID-DCP.
      *
       80-ANALIZA-EST-SIT.
      *-------------------
           SET TSUX-TIC          TO 1
           SEARCH TABLA-SIT-LAB
                  AT END
                      MOVE 'NO'              TO WS-ENC-SIT-LAB
      *               DISPLAY 'DCP (1) : ' DCP-IND-SLAB
                  WHEN
                      WT-SIT-LAB(TSUX-TIC) = DCP-IND-SLAB
      *               DISPLAY 'DCP (2) : ' DCP-IND-SLAB
                      MOVE 'SI'              TO WS-ENC-SIT-LAB
           END-SEARCH.

       90-LEER-DBC.
      *------------
           CALL 'DBNTRY' USING UIB REQ-AREA-DBC REG-DBC
                                                 ELM-LIS-DBC.
           IF REQ-RTNCD-DBC NOT = ' ' AND REQ-RTNCD-DBC NOT = '14'
                 DISPLAY '********************************'
                 DISPLAY 'ERROR! PROBLEMA LECTURA INDICE *'
                 DISPLAY '       EN TABLA DBC------------*'
                 DISPLAY 'CODIGO DE RETORNO :' REQ-RTNCD-DBC
                 DISPLAY 'REQ-AREA          :' REQ-AREA-DBC
                 DISPLAY 'CLAVE             :' REQ-KYVAL-DBC
                 PERFORM 300-TERMINAR
           ELSE
              IF REQ-RTNCD-DBC = '14'
                 ADD 1    TO         WS-NO-DBC
              ELSE
                 ADD 1    TO         WS-LEID-DBC.
      *
       100-REWRITE-MAESTRO.
      *--------------------
           MOVE 'S'       TO MAE-EST
           REWRITE REG-MAESTRO
           IF FS-MAESTRO NOT = '00' AND NOT = '10'
              DISPLAY  'ERROR REWRITE MAESTRO '
              DISPLAY 'VALOR FS-MAESTRO : ' FS-MAESTRO
              PERFORM 300-TERMINAR
           ELSE
              IF FS-MAESTRO = '10'
                 NEXT SENTENCE
              ELSE
                 ADD  1   TO WS-GRAB-MAESTRO.
      *
       250-ESTADISTICAS.
      *---------------
           DISPLAY '==========================================='
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY '  ESTADISTICAS DE PROCESO PROGRAMA SGCPGECI '
           DISPLAY ' '.
           DISPLAY '  LEIDOS MAESTROS           : ' WS-LEID-MAESTRO.
           DISPLAY ' '.
           DISPLAY '  REGRABADOS MAESTRO        : ' WS-GRAB-MAESTRO.
           DISPLAY ' '.
           DISPLAY '  LEIDOS ARCHIVO            : ' WS-LEID-ARCHIVO.
           DISPLAY ' '.
           DISPLAY '  LEIDOS TABLA DCP          : ' WS-LEID-DCP.
           DISPLAY ' '.
           DISPLAY '  NO ENCONTRADOS TABLA DCP  : ' WS-NO-DCP.
           DISPLAY ' '.
           DISPLAY '  LEIDOS TABLA DBC          : ' WS-LEID-DBC.
           DISPLAY ' '.
           DISPLAY '  NO ENCONTRADOS TABLA DBC  : ' WS-NO-DBC.
           DISPLAY '==========================================='.
           DISPLAY ' '.
           CLOSE ARCHIVO MAESTRO.

       300-TERMINAR.
      *-------------
           PERFORM 250-ESTADISTICAS
           DISPLAY ' '
           DISPLAY '*********************************************'
           DISPLAY '  PROGRAMA TERMINO ANORMALMENTE              '
           DISPLAY '                                             '
           DISPLAY '*********************************************'
           GOBACK.
