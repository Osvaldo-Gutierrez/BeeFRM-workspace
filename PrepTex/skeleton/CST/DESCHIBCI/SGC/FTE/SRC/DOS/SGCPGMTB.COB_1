       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      SGCPGMTB.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    28-Jul-93 04:46 PM.

      * Programa que Actualiza campo Tipo Banca segun rango RUT's

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCE-REQA==.
      *>>>>
      *<<<< WSS
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY SGCBRDBC.
       COPY SGCBRDCP.
       COPY SGCBRDCE.

       COPY GNSWGBCD.
       COPY GNSWCNUM.
       COPY GNSWVNUM.

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.

       01  WSS-VARI.
           03 WSS-IDC-RINF.
              05 WSS-NUM-RINF                     PIC X(08).
              05 WSS-IND-RINF                     PIC X(01).
              05 WSS-GLS-RINF                     PIC X(03).
           03 WSS-IDC-RSUP.
              05 WSS-NUM-RSUP                     PIC X(08).
              05 WSS-IND-RSUP                     PIC X(01).
              05 WSS-GLS-RSUP                     PIC X(03).
           03 WSS-NUM-SELE      VALUE ZEROES      PIC 9(08).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-IND-ERRO                        PIC 9(01).
           03 WSS-IDC-ICLI.
              05 WSS-NUM-ICLI                     PIC 9(08).
              05 WSS-IND-ICLI                     PIC X(01).
              05 WSS-GLS-ICLI                     PIC X(03).
           03 WSS-CIC-ICLI.
              05 WSS-CAI-ICLI                     PIC X(04).
              05 WSS-IIC-ICLI                     PIC X(08).
           03 WSS-CONT.
              05 WSS-NUM-READ                     PIC 9(06).
              05 WSS-NUM-RECH                     PIC 9(06).
              05 WSS-NUM-REGP                     PIC 9(06).
              05 WSS-NUM-CLIP                     PIC 9(06).
              05 WSS-NUM-CLIE                     PIC 9(06).
              05 WSS-NUM-CHKP                     PIC 9(06).
      *>>>>


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'SGCPGMTB' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      *Captura Rango Inicial del IDC
           DISPLAY 'INGRESE IDC INICIAL ( XXXXXXXXXXXX ) : '. 
           ACCEPT  WSS-IDC-RINF IN WSS-VARI.
           DISPLAY WSS-IDC-RINF IN WSS-VARI.
      *Captura Rango Final del IDC
           DISPLAY 'INGRESE IDC FINAL   ( XXXXXXXXXXXX ) : '. 
           ACCEPT  WSS-IDC-RSUP IN WSS-VARI.
           DISPLAY WSS-IDC-RSUP IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      * Validacion Ingreso de Rango Inferior de RUT's
           IF WSS-IDC-RINF IN WSS-VARI NOT > SPACES
               DISPLAY 'NO EXISTE RANGO INFERIOR DE RUT INGRESADO'
               DISPLAY 'DEBE DIGITAR RANGO INFERIOR DE RUT '
               PERFORM GNS-PRO-ABT.

      * Validacion Ingreso de Rango Superior de RUT's
           IF WSS-IDC-RSUP IN WSS-VARI NOT > SPACES
               DISPLAY 'NO EXISTE RANGO SUPERIOR DE RUT INGRESADO'
               DISPLAY 'DEBE DIGITAR RANGO SUPERIOR DE RUT '
               PERFORM GNS-PRO-ABT.

      * Validacion de Consistencia en los Rangos de RUT's ingresados
           IF WSS-IDC-RINF IN WSS-VARI > WSS-IDC-RSUP IN WSS-VARI
               DISPLAY 'NO EXISTE CONSISTENCIA EN LOS RANGOS DE RUT '
                       'INGRESADOS'
               PERFORM GNS-PRO-ABT.

      * Proceso de Validacion numerica de los rangos de RUT's
      *--------------------------------------------------------------
      *VAL-NUM Validacion numerica del rango Inferior del Numero del RUT
           IF WSS-NUM-RINF IN WSS-VARI > SPACES
               MOVE ZEROES TO WSS-NUM-SELE IN WSS-VARI
               MOVE WSS-NUM-RINF IN WSS-VARI TO NUM-NUME
               MOVE NUM-ZERO-S               TO NUM-ZERO
               MOVE NUM-SGNO-N               TO NUM-SGNO
               MOVE 8                        TO NUM-NENT
               MOVE 0                        TO NUM-NDCM
               CALL 'GNSPVNUM' USING NUM-VARI
               IF NUM-STAT-OKS
                   MOVE NUM-PENT TO WSS-NUM-SELE IN WSS-VARI
                   MOVE WSS-NUM-SELE IN WSS-VARI TO WSS-NUM-RINF
                                                    IN WSS-VARI
               ELSE
                   IF WSS-NUM-SELE IN WSS-VARI NOT > ZEROES
                       DISPLAY 'CAMPO DE NUMERO DEL RUT SIN INFORMACION'
                       PERFORM GNS-PRO-ABT.

      *VAL-NUM Validacion numerica del rango Superior del Numero del RUT
           IF WSS-NUM-RSUP IN WSS-VARI > SPACES
               MOVE ZEROES TO WSS-NUM-SELE IN WSS-VARI
               MOVE WSS-NUM-RSUP IN WSS-VARI TO NUM-NUME
               MOVE NUM-ZERO-S               TO NUM-ZERO
               MOVE NUM-SGNO-N               TO NUM-SGNO
               MOVE 8                        TO NUM-NENT
               MOVE 0                        TO NUM-NDCM
               CALL 'GNSPVNUM' USING NUM-VARI
               IF NUM-STAT-OKS
                   MOVE NUM-PENT TO WSS-NUM-SELE IN WSS-VARI
                   MOVE WSS-NUM-SELE IN WSS-VARI TO WSS-NUM-RSUP
                                                    IN WSS-VARI
               ELSE
                   IF WSS-NUM-SELE IN WSS-VARI NOT > ZEROES
                       DISPLAY 'CAMPO DE NUMERO DEL RUT SIN INFORMACION'
                       PERFORM GNS-PRO-ABT.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Inicializa Variables
           MOVE ZEROES TO WSS-CONT IN WSS-VARI.

      * Efectua la Apertura de Archivos
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM SGC-FIO-DCP.
           PERFORM SGC-FIO-DCE.

      * Lectura Inicial del Rango de Clientes en DBC con IDC del Parametro
           MOVE WSS-IDC-RINF IN WSS-VARI TO DBC-IDC-ICLI IN DBC.
           MOVE 'DBC-IDC-ICLI' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE CLIENTE : ' WSS-IDC-RINF IN WSS-VARI
               PERFORM GNS-PRO-ABT.

           IF DBC-IDC-ICLI IN DBC > WSS-IDC-RSUP IN WSS-VARI
               DISPLAY 'ERROR : NO HAY REGISTROS DBC EN EL RANGO '
                       'SOLICITADO'
               PERFORM GNS-PRO-ABT.
       LUP-MAIN.
           IF NOT FIO-STAT-OKS OR
              DBC-IDC-ICLI IN DBC > WSS-IDC-RSUP IN WSS-VARI
               GO TO FIN-MAIN.

           ADD  1  TO WSS-NUM-READ IN WSS-VARI.
      *     MOVE DBC-IDC-ICLI IN DBC TO WSS-IDC-ICLI IN WSS-VARI.
      *     MOVE DBC-CIC-ICLI IN DBC TO WSS-CIC-ICLI IN WSS-VARI.

      * Proceso del registro DBC leido y segun el Tipo de Cliente Actualizar
      * el codigo Tipo Banca del Cliente
           MOVE  0  TO WSS-IND-ERRO IN WSS-VARI
                       WSS-IND-NEXT IN WSS-VARI.

      * Segun el Tipo de Cliente Actualizar el codigo Tipo Banca del Cliente
           PERFORM CHK-DBC-TICL.
           IF WSS-IND-ERRO IN WSS-VARI = 1
               ADD  1  TO WSS-NUM-RECH IN WSS-VARI
               MOVE FIO-BCK-OUT TO FIO-CMND
               PERFORM SGC-FIO-DBC
               GO TO NXT-MAIN.

           ADD  1  TO WSS-NUM-REGP IN WSS-VARI.

      * Proceso de CHK-PNT para los registros Procesados
           ADD  1  TO WSS-NUM-CHKP IN WSS-VARI.
           IF WSS-NUM-CHKP IN WSS-VARI = 9
               MOVE FIO-CHK-PNT TO FIO-CMND
               PERFORM SGC-FIO-DBC
               GO TO NXT-MAIN.
       NXT-MAIN.
      * Siguiente lectura de Clientes por Llave Alternativa IDC
           MOVE 'DBC-IDC-ICLI' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           GO TO LUP-MAIN.
       FIN-MAIN.
      * Efectua la Clausura de Archivos
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           PERFORM SGC-FIO-DCP.
           PERFORM SGC-FIO-DCE.

      * Despliegue de los Totales de Control
           DISPLAY 'LOS REGISTROS LEIDOS           SON : '
                    WSS-NUM-READ IN WSS-VARI.
           DISPLAY 'LOS REGISTROS DCP ACTUALIZADOS SON : '
                    WSS-NUM-CLIP IN WSS-VARI.
           DISPLAY 'LOS REGISTROS DCE ACTUALIZADOS SON : '
                    WSS-NUM-CLIE IN WSS-VARI.
           DISPLAY 'PROCESADOS (TOTAL DATOS) OK.   SON : '
                    WSS-NUM-REGP IN WSS-VARI.
           DISPLAY 'LOS REGISTROS RECHAZADOS       SON : '
                    WSS-NUM-RECH IN WSS-VARI.

      * Termino del Proceso
           PERFORM GNS-PRO-END.
      **************************************************************************
      *<<<< EOF
      **************************************************************************
       CHK-DBC-TICL SECTION.
       INI-CHK-DBC-TICL.
      * Segun el Tipo de Cliente Actualiza el codigo Tipo Banca del Cliente
           IF DBC-IND-TICL IN DBC = 'P'
               PERFORM CHK-DCP-TBAN
           ELSE
               PERFORM CHK-DCE-TBAN.
       FIN-CHK-DBC-TICL.
           EXIT.
      *--------------------------------------------------------------
       CHK-DCP-TBAN SECTION.
       INI-CHK-DCP-TBAN.
      * Actualiza Codigo Tipo Banca de cliente en DCP
           MOVE DBC-CIC-ICLI IN DBC TO DCP-CIC-ICLI IN DCP.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM SGC-FIO-DCP.
           IF NOT FIO-STAT-OKS
               MOVE  1  TO WSS-IND-ERRO IN WSS-VARI
               DISPLAY 'ERROR NO EXISTE CLIENTE EN DCP -'
                       ' CIC :' DBC-CIC-ICLI IN DBC
                       ' IDC :' DBC-IDC-ICLI IN DBC
               GO TO FIN-CHK-DCP-TBAN.

           IF DCP-COD-TBAN IN DCP = 'P'
               MOVE 'Q' TO DCP-COD-TBAN IN DCP
               MOVE FIO-MOD TO FIO-CMND
               PERFORM SGC-FIO-DCP
               IF NOT FIO-STAT-OKS
                   MOVE  1  TO WSS-IND-ERRO IN WSS-VARI
                   DISPLAY 'ERROR AL MODIFICAR CLIENTE EN DCP -'
                           ' CIC :' DCP-CIC-ICLI IN DCP
                           ' IDC :' DBC-IDC-ICLI IN DBC
               ELSE
                   ADD  1  TO WSS-NUM-CLIP IN WSS-VARI
           ELSE
               MOVE FIO-ULK-REC TO FIO-CMND
               PERFORM SGC-FIO-DCP
               IF NOT FIO-STAT-OKS
                   MOVE  1  TO WSS-IND-ERRO IN WSS-VARI
                   DISPLAY 'ERROR AL EFECTUAR UNLOCK EN DCP -'
                           ' CIC :' DCP-CIC-ICLI IN DCP
                           ' IDC :' DBC-IDC-ICLI IN DBC.
       FIN-CHK-DCP-TBAN.
           EXIT.
      *--------------------------------------------------------------
       CHK-DCE-TBAN SECTION.
       INI-CHK-DCE-TBAN.
      * Actualiza Codigo Tipo Banca de cliente en DCE
           MOVE DBC-CIC-ICLI IN DBC TO DCE-CIC-ICLI IN DCE.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM SGC-FIO-DCE.
           IF NOT FIO-STAT-OKS
               MOVE  1  TO WSS-IND-ERRO IN WSS-VARI
               DISPLAY 'ERROR NO EXISTE CLIENTE EN DCE -'
                       ' CIC :' DBC-CIC-ICLI IN DBC
                       ' IDC :' DBC-IDC-ICLI IN DBC
               GO TO FIN-CHK-DCE-TBAN.

           IF DCE-COD-TBAN IN DCE = 'P'
               MOVE 'Q' TO DCE-COD-TBAN IN DCE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM SGC-FIO-DCE
               IF NOT FIO-STAT-OKS
                   MOVE  1  TO WSS-IND-ERRO IN WSS-VARI
                   DISPLAY 'ERROR AL MODIFICAR CLIENTE EN DCE -'
                           ' CIC :' DCE-CIC-ICLI IN DCE
                           ' IDC :' DBC-IDC-ICLI IN DBC
               ELSE
                   ADD  1  TO WSS-NUM-CLIE IN WSS-VARI
           ELSE
               MOVE FIO-ULK-REC TO FIO-CMND
               PERFORM SGC-FIO-DCE
               IF NOT FIO-STAT-OKS
                   MOVE  1  TO WSS-IND-ERRO IN WSS-VARI
                   DISPLAY 'ERROR AL EFECTUAR UNLOCK EN DCE -'
                           ' CIC :' DCE-CIC-ICLI IN DCE
                           ' IDC :' DBC-IDC-ICLI IN DBC.
       FIN-CHK-DCE-TBAN.
           EXIT.
      ***********************************************************************

       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY SGCBFDCE.

       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.

       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
      *>>>>
