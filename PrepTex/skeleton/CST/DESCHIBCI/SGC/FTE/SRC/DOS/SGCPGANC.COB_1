       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.    SGCPGANC
       AUTHOR.        VERONICA CORREA.
       DATE-WRITTEN.  MARZO 1998.
       DATE-COMPILED.
      **************************************************************
      *                                                            *
      *  SISTEMA    : SISTEMA GENERAL DE CLIENTES                  *
      *  J.Proyecto : Mariela Albornoz.                            *
      *  Analista   : Gerardo Ureta.                               *
      *  OBJETIVOS  : PROGRAMA GOMA ELIMINA EL PRIMER BYTE DEL     *
      *               NOMBRE DEL CLIENTE.                          *
      *   Entrada   :                                              *
      *               - ARCHIVO ARCHRUT                            *
      *                                                            *
      *   Salida    :                                              *
      *               - BASES ACTUALIZADA DBC                      *
      **************************************************************
       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA
           C01 IS CANAL-1.

       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
      *-------------
           SELECT ARCHRUT      ASSIGN TO ARCHRUT
                               ORGANIZATION IS SEQUENTIAL
                               FILE STATUS  IS FST-ARCHRUT.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *-------------
      *
       FD  ARCHRUT
           RECORDING MODE F
           RECORD CONTAINS 008 CHARACTERS
           BLOCK  CONTAINS 0   RECORDS
           LABEL RECORD IS STANDARD.

       01  REG-ARCHRUT.
           05  ARC-RUT                      PIC X(08).
      *
       WORKING-STORAGE SECTION.
      *========================
       01  VARIOS.
           02 FST-ARCHRUT              PIC X(02) VALUE SPACES.
           02 WS-FIN-ARCH              PIC 9(01) VALUE ZEROS.
           02 WS-NOMC.
              03 FILLER                PIC X(01).
              03 WS-COMP               PIC X(74).
           02 WA-LEIDOS-ARCH           PIC 9(08) VALUE 0.
           02 WA-ACTUALIZ-DBC          PIC 9(08) VALUE 0.
           02 WA-ENC-DBC               PIC 9(08) VALUE 0.
           02 WA-ERROR                 PIC 9(01) VALUE 0.
           02 WA-MONTO-EDIT            PIC ZZZ.ZZZ.ZZ9.
           02 WS-BLANCO                PIC X(10) VALUE SPACES.

       01  WS-VARIABLES-PROCESO.
           03 WS-EXITO-DBC             PIC X(02) VALUE 'NO'.
           03 WS-HORA.
              05 WS-HHH                PIC X(02).
              05 WS-MMM                PIC X(02).
              05 WS-SSS                PIC X(02).
              05 WS-MMS                PIC X(02).

       01  WP-PARAM.
           03 FILLER                   PIC X(17).
           03 WP-FECHA-PROCESO.
              05 WP-SS                 PIC X(02).
              05 WP-AA                 PIC X(02).
              05 WP-MM                 PIC X(02).
              05 WP-DD                 PIC X(02).
      *
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  UIB.
           02 FILLER               PIC X(32) VALUE 'SGCPGANC'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
      *
       01  REQ-AREA-DBC.
           02 REQ-COMAN-DBC        PIC X(5).
           02 REQ-TBLNM-DBC        PIC X(3)  VALUE 'DBC'.
           02 REQ-KYNAM-DBC        PIC X(5)  VALUE 'DBC02'.
           02 REQ-RTNCD-DBC        PIC X(2)  VALUE SPACES.
           02 REQ-INRCD-DBC        PIC X(1).
           02 REQ-DBIDE-DBC        PIC S9(4)  COMP VALUE +100.
           02 REQ-TBLID-DBC        PIC X(7).
           02 FILLER               PIC X(25).
           02 REQ-CTMAX-DBC        PIC S9(4)  COMP.
           02 REQ-IOCNT-DBC        PIC S9(4)  COMP.
           02 FILLER               PIC X(22).
           02 REQ-KYVAL-DBC.
              03 REQ-NUM-ICLI-DBC  PIC X(08).
              03 REQ-RES-ICLI-DBC  PIC X(04).
      *
      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
       01  REG-DBC.
      *    COPYDD DGC-R-DBC.DGC-E-DBC00(PROD),01,D.
      *                             EQUIVALENTE AL REGISTRO DBC
           02  DGC-E-DBC00.
      *                             DATOS BASICOS DEL CLIENTE
               03  DBC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  DBC-GLS-FLAG PICTURE X(1).
      *                             KEY CLAVE PRIMARIA REGISTRO
                   04  DBC-KEY-IREG.
      *                             IDENTIFICACION DATOS BASICOS CLIENTE
                       05  DBC-KEY-IDBC.
      *                             COD. INT. COMP. CLIENTE
                       06  DBC-CIC-ICLI.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  DBC-CAI-ICLI.
                       08  DBC-COD-TCLI
                                    PICTURE X(1).
                       08  DBC-COD-CCLI
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  DBC-IIC-ICLI
                                    PICTURE X(8).
      *                             TIME STAMP TRANSACCION
                       05  DBC-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  DBC-FEC-FTRN.
                       07  DBC-NUM-STRN
                                    PICTURE 9(2).
                       07  DBC-NUM-ATRN
                                    PICTURE 9(2).
                       07  DBC-NUM-MTRN
                                    PICTURE 9(2).
                       07  DBC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  DBC-HRA-HRTR.
                       07  DBC-NUM-HHTR
                                    PICTURE 9(2).
                       07  DBC-NUM-MMTR
                                    PICTURE 9(2).
                       07  DBC-NUM-SSTR
                                    PICTURE 9(2).
      *                             INDICADOR TIPO ACCION
                   04  DBC-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  DBC-MSC-STAT PICTURE X(1).
      *                             CODIGO ORIGEN TRANSACCION
                   04  DBC-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  DBC-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  DBC-COD-TTRN
                                    PICTURE X(3).
      *                             AUTOR TRANSACCION
                   04  DBC-COD-ATRN PICTURE X(12).
      *                             KEY RUT CLIENTE + INDICADOR O CALIFI
                   04  DBC-IDC-ICLI.
      *                             RUT CLIENTE
                       05  DBC-NUM-ICLI
                                    PICTURE X(8).
      *                             INDICADOR TIPO RUT
                       05  DBC-IND-ICLI
                                    PICTURE X(1).
      *                             EXTENSION DEL RUT
                       05  DBC-GLS-ICLI
                                    PICTURE X(3).
      *                             KEY DIGITO VERIFICADOR DEL RUT
                   04  DBC-VRF-ICLI PICTURE X(1).
      *                             KEY NOMBRE CLIENTE GENERICO (PERSONA
                   04  DBC-GLS-NOMC.
                       05  DBC-GLS-APAT
                                    PICTURE X(25).
                       05  DBC-GLS-AMAT
                                    PICTURE X(25).
                       05  DBC-GLS-NOMB
                                    PICTURE X(25).
      *                             SOUNDEX DEL NOMBRE DE CLIENTE
                   04  DBC-SNX-NOMC.
                       05  DBC-SNX-APAT
                                    PICTURE X(3).
                       05  DBC-SNX-AMAT
                                    PICTURE X(3).
                       05  DBC-SNX-NOMB
                                    PICTURE X(3).
      *                             NUMERO DE DIRECCION DE ENVIO CORRESP
                   04  DBC-NUM-DCOR PICTURE 9(2).
      *                             OPR IND TIPO CLIENTE ( PERSONA O EMP
                   04  DBC-IND-TICL PICTURE X(1).
      *                             TIPO CLIENTE (POTENCIAL,NO CLIENTE,G
                   04  DBC-COD-TICL PICTURE X(2).
      *                             CODIGO CLASIFICACION COMERCIAL
                   04  DBC-COD-CLCM PICTURE X(2).
      *                             RCM CODIGO DEL EJECUTIVO ENCARGADO D
                   04  DBC-COD-EJEC PICTURE X(12).
      *                             OPR CODIGO DE OFICINA DONDE OPERA EL
                   04  DBC-COD-OFIC PICTURE X(3).
      *                             RCM INDICADOR SI OPERA EN OTRA OFICI
                   04  DBC-IND-OOFI PICTURE X(1).
      *                             EXT CODIGO ACTIVIDAD ECONOMICA
                   04  DBC-COD-ACTI PICTURE X(9).
      *                             EXT ORIGEN Y DESTINO SECT (COMP.INST
                   04  DBC-COD-ODSE PICTURE X(3).
      *                             INF FECHA INGRESO AL PLAN
                   04  DBC-FEC-FPLN.
                       05  DBC-NUM-SPLN
                                    PICTURE 9(2).
                       05  DBC-NUM-APLN
                                    PICTURE 9(2).
                       05  DBC-NUM-MPLN
                                    PICTURE 9(2).
                       05  DBC-NUM-DPLN
                                    PICTURE 9(2).
      *                             CODIGO DE SEGMENTO
                   04  DBC-COD-SETO PICTURE X(5).
      *                             PERIODO DE SEGMENTO (SSAAMM)
                   04  DBC-PDO-SETO PICTURE X(6).
      *                             INF INDICADOR DE MULTAS BANCO CENTRA
                   04  DBC-IND-MUBC PICTURE X(1).
      *                             INF INDICADOR DE JUICIOS BANCO CENTR
                   04  DBC-IND-JUBC PICTURE X(1).
      *                             EXT CODIGO ESTADO CLIENTE ( COMO DEU
                   04  DBC-MSC-SCLI PICTURE X(1).
      *                             FECHA ESTADO CLIENTE
                   04  DBC-FEC-FSCL.
                       05  DBC-NUM-SSCL
                                    PICTURE 9(2).
                       05  DBC-NUM-ASCL
                                    PICTURE 9(2).
                       05  DBC-NUM-MSCL
                                    PICTURE 9(2).
                       05  DBC-NUM-DSCL
                                    PICTURE 9(2).
      *                             CODIGO DE BLOQUEO DEL CLIENTE
                   04  DBC-COD-BLOC PICTURE X(2).
      *                             NUMERO TOTAL DE DIRECCIONES
                   04  DBC-NUM-TDIR PICTURE 9(2).
      *                             NUMERO TOTAL DE COMENTARIOS
                   04  DBC-NUM-TCMT PICTURE 9(3).
      *                             FECHA INICIO OPERACION
                   04  DBC-FEC-FIOP.
                       05  DBC-NUM-SIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-AIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-MIOP
                                    PICTURE 9(2).
                       05  DBC-NUM-DIOP
                                    PICTURE 9(2).
      *                             FECHA TERMINO OPERACION
                   04  DBC-FEC-FTOP.
                       05  DBC-NUM-STOP
                                    PICTURE 9(2).
                       05  DBC-NUM-ATOP
                                    PICTURE 9(2).
                       05  DBC-NUM-MTOP
                                    PICTURE 9(2).
                       05  DBC-NUM-DTOP
                                    PICTURE 9(2).
      *                             NUMERO OPERACIONES FECHA TERMINO NUL
                   04  DBC-NUM-OFTN PICTURE 9(3).
      *                             FECHA INGRESO DE DATOS A LA BASE DE
                   04  DBC-FEC-FINP.
                       05  DBC-NUM-SINP
                                    PICTURE 9(2).
                       05  DBC-NUM-AINP
                                    PICTURE 9(2).
                       05  DBC-NUM-MINP
                                    PICTURE 9(2).
                       05  DBC-NUM-DINP
                                    PICTURE 9(2).
      *
      *
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
      *
       01  ELEM-LIST-DBC.
           03 ELEM-DBC             PIC X(05)  VALUE 'DBC00'.
           03 ELSE-DBC             PIC X(01).
           03 FILLER               PIC X(05)  VALUE SPACES.

      *------------------------------------------------------------*
      *
      *                   PROGRAMA PRINCIPAL                       *
      *------------------------------------------------------------*
       PROCEDURE DIVISION.
      *==================
           ENTRY 'DBMSCBL'.
       A-CONTROL SECTION.
           PERFORM B-INICIO.
       AB-PROCESAMIENTO.
           IF WA-ERROR = 0
              PERFORM C-PROCESAR
           END-IF.
       AC-TERMINACION.
           PERFORM D-TERMINAR.
       AZZ-FIN-DE-PROCESO.
           GOBACK.
      *
       B-INICIO SECTION.
      *-----------------
       BA-INICIO-PROC.
      *---------------
           PERFORM BA-ACEPTA-PARAMETROS.
           PERFORM BC-ABRIR-ARCHIVOS.
           GO TO BZ-FIN-SECCION.
      *
       BA-ACEPTA-PARAMETROS.
      *---------------------
           ACCEPT WS-HORA FROM TIME.
           ACCEPT WP-PARAM.
           DISPLAY ' LA FECHA ES : ' WP-FECHA-PROCESO.
           IF WP-SS IS NOT NUMERIC OR WP-AA IS NOT  NUMERIC
             OR WP-MM IS NOT NUMERIC OR WP-DD IS NOT NUMERIC
              DISPLAY '*** ERROR EN FECHA PROCESO ***'
              DISPLAY '*** FECHA PROCESO = ' WP-FECHA-PROCESO
              DISPLAY '*** SE ABORTA EL PROCESO ***'
              MOVE 100 TO RETURN-CODE
              GOBACK
           END-IF.
      *
       BC-ABRIR-ARCHIVOS.
      *------------------
           OPEN INPUT   ARCHRUT.
           IF FST-ARCHRUT NOT = '00'
              DISPLAY ' '
              DISPLAY 'ERROR AL ABRIR ARCHIVO DE RUT. '
                      'FILE STATUS = ' FST-ARCHRUT
              DISPLAY 'PROGRAMA CANCELADO'
              DISPLAY ' '
              MOVE 100 TO RETURN-CODE
              GOBACK
           END-IF.
      *
       BZ-FIN-SECCION.
      *---------------
           EXIT.
      *
       C-PROCESAR SECTION.
      *===================
       CA-CONTROL.
      *-----------
           PERFORM CB-PROCESA-ARCHRUT.
           GO TO CZ-FIN-DE-SECCION.
      *
       CB-PROCESA-ARCHRUT.
      *-------------------
           PERFORM CC-LEER-ARCH.
           PERFORM CD-CICLO-LECTURA-ARCH UNTIL WS-FIN-ARCH = 1.
      *
       CC-LEER-ARCH.
      *------------
           READ ARCHRUT.
           IF FST-ARCHRUT NOT = '00' AND FST-ARCHRUT NOT = '10'
              DISPLAY ' '
              DISPLAY 'ERROR AL LEER ARCHIVO ARCHRUT '
                      'FILE STATUS = ' FST-ARCHRUT
              DISPLAY 'PROGRAMA CANCELADO'
              MOVE 100           TO RETURN-CODE
              MOVE 1             TO WA-ERROR
           END-IF.
           IF FST-ARCHRUT = '10'
             MOVE 1              TO WS-FIN-ARCH
           END-IF.
      *
       CD-CICLO-LECTURA-ARCH.
      *---------------------
           ADD 1                 TO WA-LEIDOS-ARCH.
           PERFORM CE-REGISTRA-DBC.
           PERFORM CC-LEER-ARCH.

       CE-REGISTRA-DBC.
      *----------------
           MOVE ARC-RUT          TO REQ-NUM-ICLI-DBC.
           MOVE SPACES           TO REQ-RES-ICLI-DBC.
           MOVE 'DBC02 '         TO REQ-KYNAM-DBC.
           PERFORM CEA-LEER-DBC.
           IF WS-EXITO-DBC = 'SI'
              ADD 1              TO WA-ENC-DBC
              PERFORM CEB-ACTUALIZA-DBC
           END-IF.
      *
       CEA-LEER-DBC.
      *-------------
           MOVE 'REDKY' TO REQ-COMAN-DBC.
           PERFORM EJEC-COMAN-DBC.
      *
       CEB-ACTUALIZA-DBC.
      *------------------
           MOVE 'RDUKY'          TO REQ-COMAN-DBC.
           PERFORM EJEC-COMAN-DBC.
           IF WS-EXITO-DBC = 'SI'
              DISPLAY WS-BLANCO
              DISPLAY '* RUT           : ' ARC-RUT
              DISPLAY '* NOMBRE ACTUAL : ' DBC-GLS-NOMC
              MOVE DBC-GLS-NOMC  TO WS-NOMC
              MOVE SPACES        TO DBC-GLS-NOMC
              MOVE WS-COMP       TO DBC-GLS-NOMC
              MOVE WP-FECHA-PROCESO TO DBC-FEC-FTRN
              MOVE WS-HORA       TO DBC-HRA-HRTR
              MOVE 'SGCPGANC'    TO DBC-COD-ATRN
              MOVE 'UPDAT'       TO REQ-COMAN-DBC
              PERFORM EJEC-COMAN-DBC
              IF WS-EXITO-DBC = 'SI'
                ADD 1            TO WA-ACTUALIZ-DBC
                DISPLAY '* NOMBRE CORREG.: ' DBC-GLS-NOMC
                DISPLAY WS-BLANCO
              ELSE
                DISPLAY '*** NO SE CORREG. '
                DISPLAY WS-BLANCO
                MOVE 'RELES'     TO REQ-COMAN-DBC
                PERFORM EJEC-COMAN-DBC
              END-IF
           ELSE
                DISPLAY '*** NO SE CORREG. '
                DISPLAY WS-BLANCO
                MOVE 'RELES'     TO REQ-COMAN-DBC
                PERFORM EJEC-COMAN-DBC
           END-IF.
      *
       EJEC-COMAN-DBC.
      *---------------
           CALL 'DBNTRY' USING UIB REQ-AREA-DBC REG-DBC ELEM-LIST-DBC.
           IF REQ-RTNCD-DBC  NOT = SPACES
             IF REQ-RTNCD-DBC   = '14'
                MOVE 'NO'        TO WS-EXITO-DBC
             ELSE
                DISPLAY WS-BLANCO
                DISPLAY WS-BLANCO '* TABLA          : ' REQ-TBLNM-DBC
                DISPLAY WS-BLANCO '* CODIGO RETORNO : ' REQ-RTNCD-DBC
                DISPLAY WS-BLANCO '* CODIGO INTERNO : ' REQ-INRCD-DBC
                DISPLAY WS-BLANCO '* COMANDO        : ' REQ-COMAN-DBC
                DISPLAY WS-BLANCO '* CLAVE          : ' REQ-KYVAL-DBC
                DISPLAY WS-BLANCO
                MOVE 'NO'        TO WS-EXITO-DBC
                MOVE 1           TO WA-ERROR
             END-IF
           ELSE
              MOVE 'SI'          TO WS-EXITO-DBC
           END-IF.
      *
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
      *
       D-TERMINAR SECTION.
      *------------------
       DA-CONTROL.
      *-----------
           PERFORM DB-TOTALES.
           PERFORM DC-CERRAR-ARCHIVOS.
           PERFORM DD-FIN-PROGRAMA.
           GO TO DZ-FIN-DE-SECCION.

       DB-TOTALES.
      *----------
           DISPLAY ' '.
           DISPLAY '==========================================='.
           DISPLAY ' '.
           DISPLAY '  ESTADISTICAS DE PROCESO PROGRAMA         '.
           DISPLAY ' '.
           DISPLAY '==========================================='.
           DISPLAY ' '.
           MOVE WA-LEIDOS-ARCH         TO WA-MONTO-EDIT.
           DISPLAY ' RUTS LEIDOS .........................: '
                     WA-MONTO-EDIT.
           DISPLAY ' '.
           MOVE WA-ENC-DBC             TO WA-MONTO-EDIT.
           DISPLAY ' REGISTROS ENCONTRADOS EN DBC .....: '
                     WA-MONTO-EDIT.
           DISPLAY ' '.
           MOVE WA-ACTUALIZ-DBC        TO WA-MONTO-EDIT.
           DISPLAY ' REGISTROS ACTUALIZADOS-DBC........: '
                     WA-MONTO-EDIT.
           DISPLAY ' '.
           DISPLAY '==========================================='.

       DC-CERRAR-ARCHIVOS.
      *-------------------
           CLOSE ARCHRUT.

       DD-FIN-PROGRAMA.
      *----------------
           DISPLAY ' '
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+ MENSAJES PARA EL EXPLOTADOR               +'
           DISPLAY '+-------------------------------------------+'
           DISPLAY '+-------------------------------------------+'
           IF WA-ERROR = 1
              DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*** PROCESO TERMINO EN FORMA ANORMAL'
              DISPLAY ' '
           ELSE
              DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*** PROCESO TERMINO EN FORMA NORMAL'
              DISPLAY ' '
           END-IF.

       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
