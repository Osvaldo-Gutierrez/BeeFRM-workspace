      *$ASCII
       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GNSPSTAB.
       AUTHOR.          CONSIST.
      * 
      *Traspasa una tabla a un archivo secuencial
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
           SELECT GNSTAS ASSIGN TO DA-S-GNSTAS
                  ORGANIZATION   IS SEQUENTIAL
                  ACCESS MODE    IS SEQUENTIAL
                  FILE STATUS    IS FIO-STAT.
      *
       DATA DIVISION.
      *=============
       FILE SECTION.
       FD GNSTAS 
           LABEL RECORDS STANDARD
           RECORD CONTAINS 194.
      *
      *COPY GNSBRTAS.
      *    Registro Generico de Tablas
      *    ---------------------------
      * 
      *    Nombre Registro : TAS
      *    Organizacion    : Secuencial
      *    Largo           : 194
      *    Bloqueo         : 
      *    Observaciones   : 
      *
       01  TAS. 
      *
      *    Filler Compatibilidad VSAM/MVS
           03  TAB-GLS-FLAG                             PIC X(01).      
      *
      *    Clave Primaria Registro
           03  TAB-KEY-IREG.
      *
      *KEY     Tipo + Codigo ( LLave Primaria )
               05  TAB-COD-TABL.
      *
      *            Codigo Tipo Tabla
                   07  TAB-COD-TTAB                     PIC X(03).
      *
      *            Codigo Tabla
                   07  TAB-COD-CTAB                     PIC X(12).
      *
      *        Time Stamp Transaccion
               05  TAB-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  TAB-FEC-FTRN.
                       09  TAB-NUM-STRN                 PIC 9(02).
                       09  TAB-NUM-ATRN                 PIC 9(02).
                       09  TAB-NUM-MTRN                 PIC 9(02).
                       09  TAB-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  TAB-HRA-HRTR.
                       09  TAB-NUM-HHTR                 PIC 9(02).
                       09  TAB-NUM-MMTR                 PIC 9(02).
                       09  TAB-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  TAB-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  TAB-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  TAB-IND-VIGE  REDEFINES  TAB-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  TAB-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  TAB-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  TAB-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  TAB-COD-ATRN                             PIC X(12).            
      *
      *KEY Tipo + Codigo ( Interno Computacional )
           03  TAB-CIC-TABL.
      *
      *        Codigo Tipo Tabla
               05  TAB-CIC-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  TAB-CIC-CTAB                         PIC X(12).
      *
      *    Codigo Externo
           03  TAB-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  TAB-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  TAB-EXT-CTAB                         PIC X(05).
      *
      *    Descripcion Corta
           03  TAB-GLS-DCOR                             PIC X(12).
      *
      *    Abreviacion Descripcion
           03  TAB-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion del Codigo
           03  TAB-GLS-DESC                             PIC X(40).
      *
      *    Datos Anexos
           03  TAB-GLS-DATA.
               05  TAB-GLS-DAT1.
                   07  TAB-COD-DAT1     OCCURS 10       PIC X(01).
               05  TAB-GLS-DAT2.
                   07  TAB-COD-DAT2     OCCURS 10       PIC X(02).
               05  TAB-GLS-DAT3.
                   07  TAB-COD-DAT3     OCCURS 10       PIC X(03).
      *
      *    Clave Soundex
           03  TAB-SNX-TABL.
               05  TAB-SNX-TTAB                         PIC X(03).
               05  TAB-SNX-CTAB                         PIC X(03).
      *
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS
      * WORKING PARA RUTINAS.
       COPY GNSWGSYS.
      * COPY GNSWGFEC.
      * COPY GNSWGHOY.
       COPY GNSWCFIO.
       COPY GNSWVFIO.

       COPY GNSBRTAB.

       01  WSS-VARI.
           03 WSS-RTAB.
              05 WSS-CHR-RTAB  OCCURS 194   PIC X(01).
           03  WSS-I           VALUE ZEROES PIC 9(06).
           03  WSS-J           VALUE ZEROES PIC 9(03).
           03  WSS-COD-TTAB                 PIC X(03).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           DISPLAY 'INGRESE TABLA A SELECCIONAR : '.
           ACCEPT WSS-COD-TTAB IN WSS-VARI.

           PERFORM OPN-FILE.
           PERFORM PRO-FILE.
           PERFORM CLO-FILE.
           STOP RUN.
       FIN-MAIN.
           EXIT.

       OPN-FILE SECTION.
      *------------------
       INI-OPE-FILE.
           OPEN OUTPUT GNSTAS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO OUTPUT GNSTAS FS ' 
                        FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO INPUT  GNSTAB FS '
                       FIO-STAT
               DISPLAY 'ABORTO'
               STOP RUN.
      *
       FIN-OPN-FILE.
           EXIT.

       PRO-FILE SECTION.
      *----------------------
       INI-PRO-FILE.
           MOVE ZEROES       TO WSS-I.

           MOVE WSS-COD-TTAB IN WSS-VARI TO TAB-COD-TTAB IN TAB.
           MOVE SPACES                   TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       LUP-PRO-FILE.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-COD-TTAB IN TAB = WSS-COD-TTAB IN WSS-VARI )
               IF WSS-I = 0
                   DISPLAY 'NO EXISTE TABLA : ' WSS-COD-TTAB IN WSS-VARI
                   GO TO FIN-PRO-FILE
               ELSE
                   GO TO FIN-PRO-FILE.
           PERFORM TAB-TRS-TAS.
       NXT-PRO-FILE.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-FILE.
       FIN-PRO-FILE.
           EXIT.

       TAB-TRS-TAS SECTION.
       INI-TAB-TRS-TAS.
      *GRABA TAS DESDE TAB.
           ADD  1   TO WSS-I.
      *
      *     MOVE CORR TAB TO TAS.
           MOVE TAB TO TAS.

           MOVE TAS TO WSS-RTAB.
           PERFORM LIMPIE-REG-TAB VARYING WSS-J FROM 1 BY 1 UNTIL
                                          WSS-J > 194.
           MOVE WSS-RTAB TO TAS.

           WRITE TAS.
       FIN-TAB-TRS-TAS.
           EXIT.

       CLO-FILE SECTION.
      *------------------
       INI-CLO-FILE.
           DISPLAY 'NRO. REG. TRASPASADOS :' WSS-I.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
      *
           CLOSE GNSTAS.
      *
       FIN-CLO-FILE.
           EXIT.

       LIMPIE-REG-TAB SECTION.
       INI-LIMPIE-REG-TAB.
           IF WSS-CHR-RTAB( WSS-J ) NOT > SPACES
               MOVE ' ' TO WSS-CHR-RTAB( WSS-J ).
       FIN-LIMPIE-REG-TAB.
           EXIT.

      * RUTINAS
       COPY GNSBBSYS.
      * COPY GNSBGEND.

      * COPY GNSBFTAB.
      * Modulo FIO Batch para archivo VSAM GNSTAB, 10-OCT-1988 12:53:06.57
       GNS-FIO-TAB SECTION.
       INI-GNS-FIO-TAB.
           IF FIO-CMND = FIO-PUT OR FIO-MOD
               MOVE TAB-COD-TTAB IN TAB TO TAB-CIC-TTAB IN TAB
                                           TAB-EXT-TTAB IN TAB
                                           TAB-SNX-TTAB IN TAB.
           CALL 'GNSPFTAB' USING FIO-VARI TAB.
       FIN-GNS-FIO-TAB.
           EXIT.
