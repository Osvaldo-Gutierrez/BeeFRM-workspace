      *Modulo para manejar prorrogas para Cancelaciones
      *Se graba campo CAN-SGV-IMOK
       OBT-VAL-IMOK SECTION.
       INI-OBT-VAL-IMOK.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CAN.

           IF CAN-MSC-TCAN IN CAN NOT = 'P'
               MOVE ZEROES TO CAN-SGV-IMOK IN CAN
               GO TO EXT-OBT-VAL-IMOK.

           IF CAN-SGV-TPAG IN CAN NOT > SCR-SGV-INTM
             MOVE CAN-SGV-TPAG IN CAN TO RCV-SGV-INTM IN RCV
             MOVE ZEROES              TO RCV-SGV-REAJ IN RCV
             MOVE ZEROES              TO RCV-SGV-INTN IN RCV
             IF SCR-SGV-IMMO > ZEROES
               COMPUTE CAN-SGV-IMOK IN CAN = ( ROV-SGV-RENO IN ROV *
                                               WSS-SGV-VCOL ) -
                                             ( SCR-SGV-INMO *
                                               WSS-SGV-VCOL ) -
                                             ( RCV-SGV-CAPI IN RCV *
                                               WSS-SGV-VCOL )
             ELSE
             IF SCR-SGV-INTM > ZEROES
               COMPUTE CAN-SGV-IMOK IN CAN = ( ROV-SGV-RENO IN ROV *
                                               WSS-SGV-VCOL ) -
                                             ( RCV-SGV-CAPI IN RCV *
                                               WSS-SGV-VCOL )
             ELSE
               MOVE ZEROES TO CAN-SGV-IMOK IN CAN
           ELSE
           IF CAN-SGV-TPAG IN CAN > SCR-SGV-INTM AND
              CAN-SGV-TPAG IN CAN < SCR-SGV-IPFC
               MOVE SCR-SGV-INTM       TO RCV-SGV-INTM IN RCV
               MOVE ZEROES             TO RCV-SGV-REAJ IN RCV
               SUBTRACT SCR-SGV-INTM FROM CAN-SGV-TPAG IN CAN
                                   GIVING RCV-SGV-INTN IN RCV
               MOVE ZEROES             TO CAN-SGV-IMOK IN CAN
           ELSE
           IF CAN-SGV-TPAG IN CAN NOT < SCR-SGV-IPFC
               MOVE SCR-SGV-INTM TO RCV-SGV-INTM IN RCV
               MOVE SCR-SGV-INTN TO RCV-SGV-INTN IN RCV
               MOVE ZEROES       TO CAN-SGV-IMOK IN CAN
               IF WSS-CIC-TVAL = '0' AND
                 WSS-CIC-VCAM NOT = '0999'
                  COMPUTE RCV-SGV-REAJ IN RCV = ( WSS-SGV-VCAN -
                                                  WSS-SGV-VCOL ) *
                                                ( RCV-SGV-CAPI IN RCV -
                                                  ROV-SGV-RENO IN ROV )
               ELSE
                   MOVE ZEROES             TO RCV-SGV-REAJ IN RCV.

           IF WSS-CIC-TVAL = '0'
               MOVE CAN-SGV-IMOK IN CAN TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO CAN-SGV-IMOK IN CAN

               MOVE RCV-SGV-INTM IN RCV TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO RCV-SGV-INTM IN RCV

               MOVE RCV-SGV-INTN IN RCV TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO RCV-SGV-INTN IN RCV

               MOVE RCV-SGV-REAJ IN RCV TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO RCV-SGV-REAJ IN RCV.

       EXT-OBT-VAL-IMOK.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
       FIN-OBT-VAL-IMOK.
           EXIT.


       OBT-VAL-CAM SECTION.
       INI-OBT-VAL-CAM.
           MOVE ZEROES TO WSS-SGV-RAFC.
           MOVE ZEROES TO WSS-SGV-RAFO.

           MOVE '0999  '     TO TAB-CIC-CTAB IN TAB.
           MOVE SCR-COD-VCOC TO TAB-COD-CTAB IN TAB.
           MOVE CAN-FEC-FCAN IN CAN TO TAB-FEC-FTRN IN TAB.
           PERFORM GET-VCAM.
           IF MSG-COD-MENS = SPACES
      *DPM ini  14-OCT-1996
      *         MULTIPLY WSS-SGV-CAPI BY
      *                CAM-SGV-VCAM IN CAM GIVING
      *              WSS-SGV-RAFC.
               MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-VCAN IN WSS-VARI.
      *DPM fin  14-OCT-1996

           MOVE '0999  '     TO TAB-CIC-CTAB IN TAB.
           MOVE SCR-COD-VCOC TO TAB-COD-CTAB IN TAB.
      *DPM INI  17-FEB-1997
      *    IF CAN-MSC-TCAN IN CAN = 'P'
           IF OPC-IND-RENO IN OPC = 'PRO'
      *DPM FIN  17-FEB-1997
               MOVE OPC-CIC-IOPC IN OPC TO WSS-CIC-IOPC
               MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC
               MOVE RCV-NUM-IDLC IN RCV TO DLC-NUM-IDLC IN DLC
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-DLC
               IF FIO-STAT-OKS AND DLC-CIC-IOCO IN DLC > SPACES
                   MOVE DLC-CIC-IOCO IN DLC TO OPC-CIC-IOPC IN OPC
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM COL-FIO-OPC
                   IF FIO-STAT-OKS
                       MOVE OPC-FEC-FCOL IN OPC TO TAB-FEC-FTRN IN TAB
                       MOVE WSS-CIC-IOPC TO OPC-CIC-IOPC IN OPC
                       MOVE FIO-GET-KEY TO FIO-CMND
                       PERFORM COL-FIO-OPC
                   ELSE
                       MOVE WSS-CIC-IOPC TO OPC-CIC-IOPC IN OPC
                       MOVE FIO-GET-KEY TO FIO-CMND
                       PERFORM COL-FIO-OPC
                       MOVE OPC-FEC-FCOL IN OPC TO TAB-FEC-FTRN IN TAB
               ELSE
                   MOVE OPC-FEC-FCOL IN OPC TO TAB-FEC-FTRN IN TAB
           ELSE
               MOVE OPC-FEC-FCOL IN OPC TO TAB-FEC-FTRN IN TAB.
           PERFORM GET-VCAM.

      *DPM ini  14-OCT-1996
           IF MSG-COD-MENS = SPACES
               MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-VCOL IN WSS-VARI.
      *DPM fin  14-OCT-1996
       FIN-OBT-VAL-CAM.
           EXIT.

