       COL-OBT-TAS SECTION.
      * OBJETIVO:
      * Entrega la tasa del credito a una fecha solicitada.
      *
      * Ojo: Deben estar leida la tabla OPC.
      * Este modulo requiere el modulo COLWPTAS.TXT en la WORKING.
      * Utiliza rutina COL-CAL-INT para recuperar tasa.
      *
      *************************************************************
      * + VARIABLES.
      * -------------
      *  Variables de Input.
      *  TAS-IOPC           PIC X(12).
      *  TAS-VCAM           PIC X(06).
      *  TAS-IDLC           PIC S9(04) COMP.
      *  TAS-FTAS           PIC X(08)  Fecha de Solic. de Tasa.
      *
      *  Variables de Output.
      *  TAS-TINT           PIC S9(04)V9(03).            Valor tasa.
      *  TAS-TIPT           PIC X(12).                   Tipo de tasa.
      *  TAS-CMSG           PIC X(04).                   Cod. de mensaje
      *  TAS-MENS           PIC X(40)                    Mensaje de Erro
      *
      *************************************************************
      *
      * Seccion principal.
      *
       INI-COL-OBT-TAS.
           MOVE SPACES TO TAS-CMSG IN TAS-VARI
                          TAS-MENS IN TAS-VARI.
           PERFORM PRO-FEC-HOY.
           IF TAS-CMSG IN TAS-VARI NOT = SPACES
              GO TO ERR-COL-OBT-TAS.
      *
           IF TAS-IOPC IN TAS-VARI NOT = OPC-CIC-IOPC IN OPC
              PERFORM LEE-OPC.
           IF TAS-CMSG IN TAS-VARI NOT = SPACES
              GO TO ERR-COL-OBT-TAS.
           IF OPC-FEC-FCOL IN OPC > TAS-FTAS IN TAS-VARI
              MOVE 'ERRCUR'  TO TAS-CMSG
              MOVE 'ERROR'   TO TAS-MENS
              GO TO ERR-COL-OBT-TAS.
           PERFORM COL-BUS-SPR.
           IF TAS-CMSG IN TAS-VARI NOT = SPACES
              GO TO ERR-COL-OBT-TAS.
      *
           IF TAS-CMSG IN TAS-VARI NOT = SPACES
              GO TO ERR-COL-OBT-TAS.
      *
           IF TAS-CVTI IN TAS-VARI = SPACES
              NEXT SENTENCE
           ELSE
              IF TAS-TFPR IN TAS-VARI = 'C'
                 PERFORM TAS-CUR
              ELSE
                 PERFORM TAS-FLU.

           IF TAS-CMSG IN TAS-VARI NOT = SPACES
              GO TO ERR-COL-OBT-TAS.
      *
           PERFORM LEE-TINT.
           GO TO FIN-COL-OBT-TAS.
       ERR-COL-OBT-TAS.
              MOVE ZEROES TO TAS-TINT IN TAS-VARI
              MOVE SPACES TO TAS-TIPT.
       FIN-COL-OBT-TAS.
           EXIT.
       LEE-OPC SECTION.
       INI-LEE-OPC.
           MOVE TAS-IOPC IN TAS-VARI TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY          TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
              MOVE 'NEXOPC'                   TO TAS-CMSG IN TAS-VARI
              MOVE TAS-IOPC IN TAS-VARI       TO TAS-MENS IN TAS-VARI
              GO TO FIN-LEE-OPC.
       FIN-LEE-OPC.
           EXIT.
       PRO-FEC-HOY SECTION.
       INI-PRO-FEC-HOY.
           DISPLAY 'PRO-FEC-HOY'
           PERFORM GET-FHOY.
           IF TAS-FTAS IN TAS-VARI >  HOY-FHOY
               MOVE HOY-FHOY           TO TAS-FTAS IN TAS-VARI.
               DISPLAY 'FHOY ' HOY-FHOY.
       FIN-PRO-FEC-HOY.
           EXIT.
      *Suma periodos de repacto y obtiene resultado en TAS-FANT.
       TAS-CUR SECTION.
       INI-TAS-CUR.
           DISPLAY 'TAS-CUR    '
           MOVE SPACES       TO TAS-FANT.
           MOVE OPC-NUM-DCOL TO FEC-ITM1.
           MOVE OPC-NUM-MCOL TO FEC-ITM2.
           MOVE OPC-NUM-SCOL TO FEC-ITM3.
           MOVE OPC-NUM-ACOL TO FEC-ITM4.
       SUM-TAS-CUR.
           MOVE FEC-ITM1 TO TAS-DTRC.
           MOVE FEC-ITM2 TO TAS-MTRC.
           MOVE FEC-ITM3 TO TAS-STRC.
           MOVE FEC-ITM4 TO TAS-ATRC.
           IF TAS-FTRC IN TAS-VARI >  TAS-FTAS IN TAS-VARI
               GO TO FIN-TAS-CUR.
           MOVE FEC-ITM1 TO TAS-DANT.
           MOVE FEC-ITM2 TO TAS-MANT.
           MOVE FEC-ITM3 TO TAS-SANT.
           MOVE FEC-ITM4 TO TAS-AANT.
           IF TAS-UTPR = 'D'
               MOVE TAS-PREP    TO FEC-NDIA
               MOVE FEC-SUM-DIA TO FEC-CMND
           ELSE
           IF TAS-UTPR = 'M'
               MOVE TAS-PREP    TO FEC-NMES
               MOVE FEC-SUM-MES TO FEC-CMND
           ELSE
               MOVE TAS-PREP    TO FEC-NANO
               MOVE FEC-SUM-ANO TO FEC-CMND.
           MOVE FEC-FORM-FEC TO FEC-FORM.
      *Para GENESIS version 3.1
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'INTFCOLPREP+' TO INT-CMSG
               GO TO FIN-TAS-CUR.
           GO TO SUM-TAS-CUR.
       FIN-TAS-CUR.
           EXIT.
       TAS-FLU SECTION.
       INI-TAS-FLU.
           DISPLAY 'TAS-FLU    '
           MOVE VEN                TO TAS-VEN.
           MOVE OPC                TO TAS-OPC.
           MOVE OPC-CIC-IOPC IN    OPC        TO VEN-CIC-IOPC IN VEN
           IF OPC-IND-VSTR   IN    OPC = 'S'
              MOVE 1                          TO VEN-NUM-IVEN IN VEN
           ELSE
              MOVE OPC-NUM-IVEN     IN OPC    TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NLS                   TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
              MOVE 'ERROR'                    TO TAS-CMSG IN TAS-VARI
              MOVE 'NO EXISTE VEN OPC-NUM-IVEN '
                                              TO TAS-MENS IN TAS-VARI
              GO TO RES-TAS-FLU.
           IF VEN-FEC-FVEN IN VEN NOT < TAS-FTAS IN TAS-VARI
              PERFORM BUS-VEN-ANT
           ELSE
              PERFORM BUS-VEN-SIG.
       RES-TAS-FLU.
      *Restituye lectura de VEN invocada por el programa.
           MOVE TAS-VEN                TO VEN.
           MOVE FIO-GET-KEY            TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE TAS-VEN                TO VEN.
           MOVE TAS-OPC                TO OPC.
       FIN-TAS-FLU.
           EXIT.
       BUS-VEN-SIG SECTION.
       INI-BUS-VEN-SIG.
           DISPLAY 'BUS-VEN-SIG'
           MOVE TAS-FTAS IN TAS-VARI   TO TAS-FANT IN TAS-VARI.
       LUP-BUS-VEN-SIG.
           IF NOT ( FIO-STAT-OKS AND
                    VEN-CIC-IOPC IN VEN = OPC-CIC-IOPC IN OPC AND
                    VEN-FEC-FVEN IN VEN NOT > TAS-FTAS IN TAS-VARI)
              GO TO FIN-BUS-VEN-SIG.
           MOVE VEN-FEC-FVEN IN VEN       TO TAS-FANT IN TAS-VARI.
           MOVE FIO-GET-NXT               TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           GO TO LUP-BUS-VEN-SIG.
       FIN-BUS-VEN-SIG.
           EXIT.
       BUS-VEN-ANT SECTION.
       INI-BUS-VEN-ANT.
           DISPLAY 'BUS-VEN-ANT' OPC-NUM-IVEN.
           IF OPC-NUM-IVEN IN OPC = 1
              MOVE OPC-FEC-FCOL     IN OPC  TO TAS-FANT IN TAS-VARI
              GO TO FIN-BUS-VEN-ANT.
           MOVE OPC-NUM-IVEN IN OPC         TO VEN-NUM-IVEN IN VEN
           MOVE TAS-FTAS IN TAS-VARI        TO TAS-FANT IN TAS-VARI.
       LUP-BUS-VEN-ANT.
           IF NOT ( FIO-STAT-OKS AND
                    VEN-CIC-IOPC IN VEN = OPC-CIC-IOPC IN OPC AND
                    VEN-FEC-FVEN IN VEN > TAS-FTAS IN TAS-VARI)
              GO TO FIN-BUS-VEN-ANT.
           IF VEN-NUM-IVEN IN VEN = 1
              MOVE OPC-FEC-FCOL     IN OPC  TO TAS-FANT IN TAS-VARI
              GO TO FIN-BUS-VEN-ANT.
           COMPUTE VEN-NUM-IVEN IN VEN = VEN-NUM-IVEN IN VEN - 1.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           IF OPC-IND-VSTR   IN    OPC = 'S'
              PERFORM COL-OBT-VEN
              IF NOT FIO-STAT-OKS
                 MOVE OPC-FEC-FCOL IN OPC TO TAS-FANT IN TAS-VARI
              ELSE
                 MOVE VEN-FEC-FVEN IN VEN TO TAS-FANT IN TAS-VARI
                 MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVEN IN VEN
           ELSE
              PERFORM COL-FIO-VEN
              IF NOT FIO-STAT-OKS
                 MOVE OPC-FEC-FCOL IN OPC TO TAS-FANT IN TAS-VARI
              ELSE
                 MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVEN IN VEN
                 MOVE VEN-FEC-FVEN IN VEN TO TAS-FANT IN TAS-VARI.
           GO TO LUP-BUS-VEN-ANT.
       FIN-BUS-VEN-ANT.
           EXIT.
      * Rutina para obtener tasa de interes
       COL-BUS-SPR SECTION.
       INI-COL-BUS-SPR.
           DISPLAY 'COL-BUS-SPR'
           MOVE SPACES TO TAS-TIPT IN TAS-VARI TAS-FPAS IN TAS-VARI
                          TAS-CVTI IN TAS-VARI
                          TAS-UTPR IN TAS-VARI
                          TAS-TFPR IN TAS-VARI.
           MOVE ZEROES TO TAS-TINT IN TAS-VARI
                          TAS-PREP IN TAS-VARI.
           MOVE TAS-IOPC IN TAS-VARI TO ICG-CIC-IOPC IN ICG.
           MOVE TAS-IDLC IN TAS-VARI TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES               TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES               TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'        TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES       TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-COL-BUS-SPR.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-CIC-IOPC IN ICG = TAS-IOPC IN TAS-VARI AND
                    ICG-NUM-ICAN IN ICG = TAS-IDLC IN TAS-VARI AND
                    ICG-CIC-IOPV IN ICG = SPACES       AND
                    ICG-NUM-IVCT IN ICG = ZEROES       AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               IF TAS-IDLC IN TAS-VARI = 0
                   IF TAS-TIPT IN TAS-VARI NOT > SPACES
                       MOVE 'INTRICGNEX' TO TAS-CMSG IN TAS-VARI
                       MOVE TAS-IOPC     TO TAS-MENS IN TAS-VARI
                       GO TO FIN-COL-BUS-SPR
                   ELSE
                       GO TO FIN-COL-BUS-SPR
               ELSE
                   IF TAS-TIPT IN TAS-VARI NOT > SPACES
                       MOVE 0 TO TAS-IDLC  IN TAS-VARI
                       GO TO INI-COL-BUS-SPR
                   ELSE
                       GO TO FIN-COL-BUS-SPR.
      *Compara fechas de ICG con fechas solicida.
           IF ICG-FEC-FINI IN ICG NOT > TAS-FTAS IN TAS-VARI AND
              ICG-FEC-FINI IN ICG     > TAS-FPAS IN TAS-VARI
               MOVE ICG-FEC-FINI IN ICG TO TAS-FPAS IN TAS-VARI
               MOVE ICG-COD-TINT IN ICG TO TAS-TIPT IN TAS-VARI
               MOVE ICG-COD-CVTI IN ICG TO TAS-CVTI IN TAS-VARI
               MOVE ICG-MSC-TFPR IN ICG TO TAS-TFPR IN TAS-VARI
               MOVE ICG-MSC-UTPR IN ICG TO TAS-UTPR IN TAS-VARI
               MOVE ICG-NUM-PREP IN ICG TO TAS-PREP IN TAS-VARI
               IF ICG-COD-CVTI IN ICG > SPACES
                   MOVE ICG-SGV-SPRD IN ICG TO TAS-TINT IN TAS-VARI
                   GO TO NXT-COL-BUS-SPR
               ELSE
                   MOVE ICG-SGV-TINT IN ICG TO TAS-TINT IN TAS-VARI
                   GO TO NXT-COL-BUS-SPR
           ELSE
              IF TAS-TIPT IN TAS-VARI NOT > SPACES
                  MOVE 'INTRICGNEX' TO TAS-CMSG IN TAS-VARI
                  MOVE TAS-IOPC     TO TAS-MENS IN TAS-VARI
                  GO TO FIN-COL-BUS-SPR.
       NXT-COL-BUS-SPR.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-COL-BUS-SPR.
       FIN-COL-BUS-SPR.
           EXIT.

       LEE-TINT SECTION.
       INI-LEE-TINT.
           DISPLAY 'LEE-TINT   '  TAS-FANT.
      *Si es tasa variable
           IF ( TAS-CVTI > SPACES )
                MOVE TAS-FANT TO CAM-FEC-FCAM IN CAM(1)
                MOVE TAS-FANT TO CAM-FEC-FCAM IN CAM(2)

                MOVE 'I'          TO CAM-MSC-TVAL IN CAM(1)
                MOVE TAS-CVTI     TO CAM-COD-CVAL IN CAM(1)
                MOVE TAS-PBTI     TO CAM-COD-TCAM IN CAM(1)

                MOVE TAS-VCAM TO CAM-COD-VCAM IN CAM(2)

                MOVE FIO-GET-KEY TO FIO-CMND
                PERFORM TAB-FIO-CAM

                IF ( NOT FIO-STAT-OKS ) OR
                   ( CAM-MSC-STAT IN CAM NOT = 'S' )
                    PERFORM GNS-CHK-BTV
                    IF TAS-CMSG > SPACES
                        MOVE 'NEXTAS'  TO TAS-CMSG
                        MOVE CAM-KEY-IREG TO TAS-MENS
                        GO TO FIN-LEE-TINT
                    ELSE
                        ADD CAM-SGV-VCAM IN CAM TO TAS-TINT
                ELSE
                    ADD CAM-SGV-VCAM IN CAM TO TAS-TINT.

           IF TAS-BCTI = 'A'
               COMPUTE TAS-TINT ROUNDED =
                   ( TAS-TINT * 100 ) / ( 100 - TAS-TINT )
               MOVE 'V'                  TO TAS-BCTI.
       FIN-LEE-TINT.
           EXIT.
