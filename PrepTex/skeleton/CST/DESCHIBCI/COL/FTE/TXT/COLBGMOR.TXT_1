      ******************************************************************
      *DAD ini  31-MAY-1995
      *    Procesos que Calculan Tasa de Interes Periodo de Mora segun
      *    Forma de Calculo Interes de MORA :
      *
      *    Formato Fechas (SSAAMMDD).
      *    INPUT   :  MOR-FCAN IN MOR-VARI     (Fecha de Cancelacion)
      *               MOR-FVEN IN MOR-VARI     (Fecha de Vencimiento o
      *                           F.Can anterior si es CIE y > a F.Vcto)
      *               MOR-FCOL IN MOR-VARI     (Fecha de Colocacion)
      *               MOR-FMAV IN MOR-VARI     (Fecha de Maximo Vcto.)
      *               MOR-IOPC IN MOR-VARI     (Nro. Operacion)
      *               MOR-VCOC IN MOR-VARI     (Moneda del Credito)
      *               MOR-FCAL IN MOR-VARI     (Forma Calculo Interes Mo
      *               MOR-TINF IN MOR-VARI     (Tasa Mora Informada)
      *               MOR-PLZO IN MOR-VARI     (Plazo)
      *               MOR-FIMV IN MOR-VARI     (Fecha Inicio Mora)
      *               MOR-FVCT IN MOR-VARI     (Fecha Vencimiento)
      *ALD-BCI-INI/28.04.1999
      *               MOR-FORI IN MOR-VARI     (Fecha curse original)
      *               MOR-CRED IN MOR-VARI     (Monto Original OPC)
      *               MOR-TXC  IN MOR-VARI     (Version de TMC )
      *ALD-BCI-FIN/28.04.1999
      *    OUTPUT  :  MOR-TIPE IN MOR-VARI     (Factor Tasa Mora Periodo
      *               MOR-CMSG IN MOR-VARI     (Codigo Msg. Error)
      *               MOR-MEN2 IN MOR-VARI     (Complemento Msg. Error)
      *    PERFORM :  CAL-INT-MORA.            RUTINA
      *
      *DAD fin  31-MAY-1995
      ******************************************************************
      *Procesos que Calculan Tasa de Interes Periodo de Mora
       CAL-INT-MORA SECTION.
       INI-CAL-INT-MORA.
      *Validaciones iniciales a variables de INPUT, Trabajo
           MOVE SPACES TO MOR-CMSG IN MOR-VARI
                          MOR-MENS IN MOR-VARI.
           MOVE ZEROES TO MOR-TCAL IN MOR-VARI
                          MOR-TIPE IN MOR-VARI.

      *Valida Exista Identificacion del Credito
           IF MOR-IOPC IN MOR-VARI NOT > SPACES
               MOVE 'OPCIOPCNEX' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida Exista Moneda del Credito
           IF MOR-VCOC IN MOR-VARI NOT > SPACES
               MOVE 'CODVCOCBLK' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida Exista Fecha de Cancelacion
           IF MOR-FCAN IN MOR-VARI NOT > ZEROES
               MOVE 'FECFCANBLK' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida Exista Fecha de Vencimiento
           IF MOR-FVEN IN MOR-VARI NOT > ZEROES
               MOVE 'FECFVENBLK' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida Exista Fecha de Colocacion
           IF MOR-FCOL IN MOR-VARI NOT > ZEROES
               MOVE 'FECFCOLBLK' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida Exista Fecha de Maximo Vencimiento
           IF MOR-FMAV IN MOR-VARI NOT > ZEROES
               MOVE 'FECFMAVBLK' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida que Fecha de Cancelacion > Fecha de Vencimiento
           IF MOR-FCAN IN MOR-VARI < MOR-FVEN IN MOR-VARI
               MOVE 'FECFCAN<FVEN' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida que Fecha de Colocacion < Fecha de Vencimiento
      *                               < Fecha de Cancelacion
      *                               < Fecha de Maximo Vencimiento
           IF MOR-FCOL IN MOR-VARI > MOR-FVEN IN MOR-VARI
               MOVE 'FECFCOL>FVEN' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA
           ELSE
           IF MOR-FCOL IN MOR-VARI > MOR-FCAN IN MOR-VARI
               MOVE 'FECFCOL>FCAN' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA
           ELSE
           IF MOR-FCOL IN MOR-VARI > MOR-FMAV IN MOR-VARI
               MOVE 'FECFCOL>FMAV' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida Exista Tasa Interes de MORA Informada
      *    IF MOR-PASO IN MOR-VARI NOT NUMERIC
           IF MOR-TINF IN MOR-VARI NOT NUMERIC
               MOVE ZEROES TO MOR-TINF IN MOR-VARI
           ELSE
               IF MOR-TINF IN MOR-VARI NOT > ZEROES
                   MOVE ZEROES TO MOR-TINF IN MOR-VARI.

      *Valida Exista Fecha de Vencimiento
           IF MOR-FVCT IN MOR-VARI NOT > ZEROES
               MOVE 'FECFVCTBLK' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Valida Exista Fecha de Inicio de Mora
           IF MOR-FIMV IN MOR-VARI NOT > ZEROES
               MOVE 'FECFIMVBLK' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.

      *Obtiene Periodo de Mora (FCAN - FVEN)
           MOVE MOR-DVEN IN MOR-VARI TO FEC-DEC1.
           MOVE MOR-MVEN IN MOR-VARI TO FEC-MEC1.
           MOVE MOR-SVEN IN MOR-VARI TO FEC-SEC1.
           MOVE MOR-AVEN IN MOR-VARI TO FEC-AEC1.
           MOVE MOR-DCAN IN MOR-VARI TO FEC-DEC2.
           MOVE MOR-MCAN IN MOR-VARI TO FEC-MEC2.
           MOVE MOR-SCAN IN MOR-VARI TO FEC-SEC2.
           MOVE MOR-ACAN IN MOR-VARI TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'PERMORAERROR'       TO MOR-CMSG IN MOR-VARI
               MOVE MOR-IOPC IN MOR-VARI TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-CAL-INT-MORA.
           MOVE FEC-NDIA TO MOR-PERI IN MOR-VARI.

      *DPM ini  25-OCT-1996
           MOVE MOR-PERI TO INT-SEPA INT-SEPD WSS-DIAM.

      *ALD-BCI-INI/05.05.1999
           IF MOR-TXC = 'S'
               PERFORM PRO-MORA-TXC
      *ALD-BCI-FIN/05.05.1999
           ELSE
           IF MOR-FCAN IN MOR-VARI     > MOR-FVCT IN MOR-VARI AND
              MOR-FCAN IN MOR-VARI NOT > MOR-FIMV IN MOR-VARI
               PERFORM VAL-TMAX-CONV
           ELSE
               PERFORM PRO-MORA-TMC.
           IF MOR-CMSG IN MOR-VARI > SPACES
               GO TO FIN-CAL-INT-MORA.

           MOVE INT-TIPE TO MOR-TIPE.
      *DPM fin  25-OCT-1996
       FIN-CAL-INT-MORA.
           EXIT.
      **********************************************************
      *DPM ini  28-OCT-1996
       VAL-TMAX-CONV SECTION.
      ***********************
       INI-VAL-TMAX-CONV.
           MOVE 0 TO WSS-INT-TB90 WSS-INT-TS90 WSS-INT-TMAX.
           MOVE 'TAB'  TO TAB-COD-SIST FIO-SIST.
           MOVE 'VLR'  TO TAB-CIC-TTAB IN TAB.
           MOVE '0999' TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF MOR-VCOC IN MOR-VARI = TAB-COD-CTAB IN TAB
               MOVE 'S' TO WSS-PESO
           ELSE
               MOVE 'N' TO WSS-PESO.

           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           MOVE MOR-VCOC IN MOR-VARI TO WSS-COD-VCOC IN WSS-VARI.
           IF WSS-COD-TVOC IN WSS-VARI = MSC-COD-CMSC IN MSC
             MOVE 'N' TO WSS-IND-TMON IN WSS-VARI
           ELSE
             MOVE 'E' TO WSS-IND-TMON IN WSS-VARI.
           IF WSS-PESO-SI
               PERFORM BUS-CIC-PLZO
               IF WSS-PLZO-B90
                   MOVE 1    TO WSS-INT-TB90
               ELSE
                   MOVE 1    TO WSS-INT-TS90
           ELSE
               MOVE 1        TO WSS-INT-TMAX.
           MOVE ZEROES               TO INT-TIPE.
           MOVE MOR-DCOL IN MOR-VARI TO INT-DDMA.
           MOVE MOR-MCOL IN MOR-VARI TO INT-MDMA.
           MOVE MOR-SCOL IN MOR-VARI TO INT-SDMA.
           MOVE MOR-ACOL IN MOR-VARI TO INT-ADMA.
           MOVE MOR-VCOC IN MOR-VARI TO INT-VCAM.
           IF WSS-PESO-SI
               PERFORM VAL-TMXC-PESO
           ELSE
           IF WSS-IND-TMON = 'N'
               PERFORM VAL-TMXC-MREA
           ELSE
               PERFORM VAL-TMXC-MEXT.
       FIN-VAL-TMAX-CONV.
           EXIT.

       VAL-TMXC-PESO SECTION.
       INI-VAL-TMXC-PESO.
           IF WSS-INT-TB90 NOT > 0
               GO TO TS9-VAL-TMXC-PESO.
           MOVE MOR-DCOL IN MOR-VARI   TO CAM-NUM-DCAM IN CAM(1).
           MOVE MOR-MCOL IN MOR-VARI   TO CAM-NUM-MCAM IN CAM(1).
           MOVE MOR-SCOL IN MOR-VARI   TO CAM-NUM-SCAM IN CAM(1).
           MOVE MOR-ACOL IN MOR-VARI   TO CAM-NUM-ACAM IN CAM(1).
           MOVE CAM-FEC-FCAM IN CAM(1) TO CAM-FEC-FCAM IN CAM(2).
           MOVE CAM-COD-MNAC           TO CAM-COD-VCAM IN CAM(1).
           MOVE 'TB90'                 TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY            TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               MOVE 'CAM$TB9NEX' TO MOR-CMSG IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-PESO.
           IF CAM-IND-VIGE IN CAM NOT = 'S'
               MOVE 'CAM$TB9NVG' TO MOR-CMSG IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-PESO.
           MOVE CAM-SGV-VCAM IN CAM TO INT-TINT.
           MOVE 'VMSS'              TO INT-TIPT.
           PERFORM OBT-VTIN.
           IF INT-CMSG > SPACES
               MOVE INT-CMSG        TO MOR-CMSG IN MOR-VARI
               GO TO FIN-VAL-TMXC-PESO.
      *
       TS9-VAL-TMXC-PESO.
           IF WSS-INT-TS90 NOT > 0
               GO TO FIN-VAL-TMXC-PESO.
           MOVE MOR-DCOL IN MOR-VARI   TO CAM-NUM-DCAM IN CAM(1).
           MOVE MOR-MCOL IN MOR-VARI   TO CAM-NUM-MCAM IN CAM(1).
           MOVE MOR-SCOL IN MOR-VARI   TO CAM-NUM-SCAM IN CAM(1).
           MOVE MOR-ACOL IN MOR-VARI   TO CAM-NUM-ACAM IN CAM(1).
           MOVE CAM-FEC-FCAM IN CAM(1) TO CAM-FEC-FCAM IN CAM(2).
           MOVE CAM-COD-MNAC           TO CAM-COD-VCAM IN CAM(1).
           MOVE 'TS90'                 TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               MOVE 'CAM$TS9NEX' TO MOR-CMSG IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-PESO.
           IF CAM-IND-VIGE IN CAM NOT = 'S'
               MOVE 'CAM$TS9NVG' TO MOR-CMSG IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-PESO.
           MOVE CAM-SGV-VCAM IN CAM TO INT-TINT.
           MOVE 'VMSS'              TO INT-TIPT.
           PERFORM OBT-VTIN.
           IF INT-CMSG > SPACES
               MOVE INT-CMSG            TO MOR-CMSG IN MOR-VARI
               GO TO FIN-VAL-TMXC-PESO.
       FIN-VAL-TMXC-PESO.
           EXIT.

       VAL-TMXC-MREA SECTION.
       INI-VAL-TMXC-MREA.
           IF WSS-INT-TMAX NOT > 0
               GO TO FIN-VAL-TMXC-MREA.
           MOVE MOR-DCOL IN MOR-VARI   TO CAM-NUM-DCAM IN CAM(1).
           MOVE MOR-MCOL IN MOR-VARI   TO CAM-NUM-MCAM IN CAM(1).
           MOVE MOR-SCOL IN MOR-VARI   TO CAM-NUM-SCAM IN CAM(1).
           MOVE MOR-ACOL IN MOR-VARI   TO CAM-NUM-ACAM IN CAM(1).
           MOVE CAM-FEC-FCAM IN CAM(1) TO CAM-FEC-FCAM IN CAM(2).
           MOVE MOR-VCOC IN MOR-VARI   TO CAM-COD-VCAM IN CAM(1).
           MOVE 'TMXC'                 TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY            TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               MOVE 'CAMTMXCNEX' TO MOR-CMSG IN MOR-VARI
               DISPLAY 'CAMTMXCNEX ' MOR-VCOC IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-MREA.
           MOVE CAM-SGV-VCAM IN CAM TO INT-TINT.
           IF CAM-IND-VIGE IN CAM NOT = 'S'
               MOVE 'CAMTMXCNVG' TO MOR-CMSG IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-MREA.
           MOVE 'VASS'              TO INT-TIPT.
           PERFORM OBT-VTIN.
           IF INT-CMSG > SPACES
               MOVE INT-CMSG            TO MOR-CMSG IN MOR-VARI
               GO TO FIN-VAL-TMXC-MREA.
       FIN-VAL-TMXC-MREA.
           EXIT.

       VAL-TMXC-MEXT SECTION.
       INI-VAL-TMXC-MEXT.
           IF WSS-INT-TMAX NOT > 0
               GO TO FIN-VAL-TMXC-MEXT.
           MOVE MOR-DCOL IN MOR-VARI   TO CAM-NUM-DCAM IN CAM(1).
           MOVE MOR-MCOL IN MOR-VARI   TO CAM-NUM-MCAM IN CAM(1).
           MOVE MOR-SCOL IN MOR-VARI   TO CAM-NUM-SCAM IN CAM(1).
           MOVE MOR-ACOL IN MOR-VARI   TO CAM-NUM-ACAM IN CAM(1).
           MOVE CAM-FEC-FCAM IN CAM(1) TO CAM-FEC-FCAM IN CAM(2).
           MOVE 'TMEX'                 TO CAM-COD-VCAM IN CAM(1).
           MOVE 'TMXC'                 TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY            TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               MOVE 'CAMTMXCNEX' TO MOR-CMSG IN MOR-VARI
               DISPLAY 'CAMTMXCNEX ' MOR-VCOC IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-MEXT.
           IF CAM-IND-VIGE IN CAM NOT = 'S'
               MOVE 'CAMTMXCNVG' TO MOR-CMSG IN MOR-VARI
               MOVE CAM-KEY-CAMB IN CAM TO MOR-MEN2 IN MOR-VARI
               GO TO FIN-VAL-TMXC-MEXT.
           MOVE CAM-SGV-VCAM IN CAM TO INT-TINT.
           MOVE 'VASS'              TO INT-TIPT.
           PERFORM OBT-VTIN.
           IF INT-CMSG > SPACES
               MOVE INT-CMSG            TO MOR-CMSG IN MOR-VARI
               GO TO FIN-VAL-TMXC-MEXT.
       FIN-VAL-TMXC-MEXT.
           EXIT.

       BUS-CIC-PLZO SECTION.
       INI-BUS-CIC-PLZO.
           MOVE 'VSN'         TO TAB-COD-TTAB IN TAB.
           MOVE 'CALMORAFMAV' TO TAB-COD-CTAB IN TAB.
           MOVE 'COL'         TO FIO-SIST.
           MOVE FIO-GET-KEY   TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF ( FIO-STAT-OKS AND TAB-MSC-STAT IN TAB = 'S' )
               GO TO FMV-BUS-CIC-PLZO.

           MOVE 'PZO'                TO TAB-COD-TTAB IN TAB.
           MOVE MOR-PLZO IN MOR-VARI TO TAB-COD-CTAB IN TAB.
           MOVE 'TAB'                TO FIO-SIST.
           MOVE FIO-GET-KEY          TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-CIC-CTAB IN TAB TO WSS-PLZO
               GO TO FIN-BUS-CIC-PLZO
           ELSE
               MOVE 'TABPZO NEX' TO MOR-CMSG IN MOR-VARI.
       FMV-BUS-CIC-PLZO.
           MOVE MOR-DCOL IN MOR-VARI TO FEC-DEC1.
           MOVE MOR-MCOL IN MOR-VARI TO FEC-MEC1.
           MOVE MOR-SCOL IN MOR-VARI TO FEC-SEC1.
           MOVE MOR-ACOL IN MOR-VARI TO FEC-AEC1.
           MOVE MOR-DMAV IN MOR-VARI TO FEC-DEC2.
           MOVE MOR-MMAV IN MOR-VARI TO FEC-MEC2.
           MOVE MOR-SMAV IN MOR-VARI TO FEC-SEC2.
           MOVE MOR-AMAV IN MOR-VARI TO FEC-AEC2.
           MOVE FEC-FORM-FEC         TO FEC-FORM.
           MOVE FEC-DIF-DIA          TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF FEC-NDIA < 90
               MOVE 'C1' TO WSS-PLZO
           ELSE
               MOVE 'S9' TO WSS-PLZO.
       FIN-BUS-CIC-PLZO.
           EXIT.

      *ALD-BCI-INI/22.04.1999
       PRO-MORA-TXC SECTION.
       INI-PRO-MORA-TXC.
           PERFORM BUS-CIC-PLZO.
           IF MOR-CMSG > SPACES
               GO TO FIN-PRO-MORA-TXC.
           MOVE 1                            TO CAM-SGV-VCAM IN CAM.
           IF MOR-VCOC  NOT = '0998'
               MOVE '0998  '                 TO CAM-COD-VCAM IN CAM(1)
               MOVE MOR-VCOC                 TO CAM-COD-VCAM IN CAM(2)
               MOVE MOR-DORI                 TO CAM-NUM-DCAM IN CAM(1)
               MOVE MOR-MORI                 TO CAM-NUM-MCAM IN CAM(1)
               MOVE MOR-SORI                 TO CAM-NUM-SCAM IN CAM(1)
               MOVE MOR-AORI                 TO CAM-NUM-ACAM IN CAM(1)
               MOVE CAM-FEC-FCAM IN CAM(1)   TO CAM-FEC-FCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT ( FIO-STAT-OKS AND CAM-IND-VIGE = 'S')
                   MOVE 'CAM    NEX' TO MOR-CMSG
                   GO TO FIN-PRO-MORA-TXC.
           DIVIDE MOR-CRED  BY CAM-SGV-VCAM IN CAM
                  GIVING    TXC-MNTO ROUNDED.
           MOVE WSS-PLZO                TO TXC-PLZO.
           MOVE MOR-VCOC                TO TXC-VCOC.
           MOVE MOR-DCAN                TO TXC-DEC2.
           MOVE MOR-MCAN                TO TXC-MEC2.
           MOVE MOR-SCAN                TO TXC-SEC2.
           MOVE MOR-ACAN                TO TXC-AEC2.
      *
           MOVE MOR-DVEN                TO TXC-DEC1.
           MOVE MOR-MVEN                TO TXC-MEC1.
           MOVE MOR-SVEN                TO TXC-SEC1.
           MOVE MOR-AVEN                TO TXC-AEC1.

      *    DISPLAY 'TXC-CMMA : ' TXC-CMMA.
           PERFORM TAB-BUS-TXC.

           IF TXC-CMSG > SPACES
               MOVE TXC-CMSG     TO MOR-CMSG
               GO TO FIN-PRO-MORA-TXC.
      *    DISPLAY 'ACUMULADO : ' TXC-ATXC.
           DIVIDE TXC-ATXC       BY 100 GIVING
                                 INT-TIPE ROUNDED.
       FIN-PRO-MORA-TXC.
           EXIT.
      *ALD-BCI-FIN/22.04.1999
       PRO-MORA-TMC SECTION.
       INI-PRO-MORA-TMC.
           PERFORM BUS-CIC-PLZO.
           IF MOR-CMSG IN MOR-VARI > SPACES
               GO TO FIN-PRO-MORA-TMC.
           MOVE MOR-DCAN IN MOR-VARI TO TMC-NUM-DTMC IN TMC.
           MOVE MOR-MCAN IN MOR-VARI TO TMC-NUM-MTMC IN TMC.
           MOVE MOR-ACAN IN MOR-VARI TO TMC-NUM-ATMC IN TMC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TMC.
           IF NOT FIO-STAT-OKS
               MOVE 'TASDNEXFCAN' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-PRO-MORA-TMC.
           IF MOR-VCOC IN MOR-VARI = '0998'
               MOVE TMC-SGV-UFAC IN TMC TO WSS-SGV-CANT
           ELSE
           IF WSS-PLZO-B90
               MOVE TMC-SGV-89AC IN TMC TO WSS-SGV-CANT
           ELSE
               MOVE TMC-SGV-PEAC IN TMC TO WSS-SGV-CANT.

           MOVE MOR-DVEN IN MOR-VARI TO TMC-NUM-DTMC IN TMC.
           MOVE MOR-MVEN IN MOR-VARI TO TMC-NUM-MTMC IN TMC.
           MOVE MOR-AVEN IN MOR-VARI TO TMC-NUM-ATMC IN TMC.

           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TMC.
           IF NOT FIO-STAT-OKS
               MOVE 'TASDNEXFVEN' TO MOR-CMSG IN MOR-VARI
               GO TO FIN-PRO-MORA-TMC.
           IF MOR-VCOC IN MOR-VARI = '0998'
               MOVE TMC-SGV-UFAC IN TMC TO WSS-SGV-VENT
           ELSE
           IF WSS-PLZO-B90
               MOVE TMC-SGV-89AC IN TMC TO WSS-SGV-VENT
           ELSE
               MOVE TMC-SGV-PEAC IN TMC TO WSS-SGV-VENT.
           SUBTRACT WSS-SGV-VENT FROM WSS-SGV-CANT GIVING
                    WSS-SGV-MTMC.
           DIVIDE WSS-SGV-MTMC BY 36000 GIVING
                  INT-TIPE ROUNDED.
       FIN-PRO-MORA-TMC.
           EXIT.
      *DPM fin  28-OCT-1996

