       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGER2.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    08-NOV-1991 03:49 PM.

      * Programa que detecta valor de SCPA incorrecto

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
           SELECT RPTER2       ASSIGN TO        UT-S-RPTER2.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       FD  RPTER2 LABEL RECORD OMITTED
           REPORT IS RPT-ER2.

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY COLRRER2.
       COPY COLRWER2.
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *<<<< WSS
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSBRMSC.
      *SMC-INI 13.09.1993
       COPY COLWGINT.
      *SMC-FIN 13.09.1993
       COPY GNSWGPES.

       COPY COLBRRCV.
       COPY COLBRCAN.
       COPY COLBRVEN.
       COPY COLBROPC.
       COPY COLBRICG.
       COPY COLBRDLC.
       COPY COLBREVC.
       COPY COLBRCYA.
       COPY COLBRRDC.
       COPY COLBRROV.
       COPY COLBRTOC.
       COPY SGCBRDBC.
       COPY TABBRCAM.

       COPY COLWGVIN.
       COPY COLWGVEN.
       01  ER2-VARI.
           03 ER2-CIC-IOPC                 PIC X(12).
           03 ER2-VAL-SCRE    COMP-3       PIC S9(11)V9(04).
           03 ER2-VAL-IDNI    COMP-3       PIC S9(11)V9(04).
           03 ER2-VAL-INTE    COMP-3       PIC S9(11)V9(04).
           03 ER2-NUM-IVEN                 PIC 9(03).
           03 ER2-NUM-ICAN                 PIC 9(03).
           03 ER2-FEC-FUNO.
              05 ER2-NUM-SUNO              PIC 9(02).
              05 ER2-NUM-AUNO              PIC 9(02).
              05 ER2-NUM-MUNO              PIC 9(02).
              05 ER2-NUM-DUNO              PIC 9(02).
           03 ER2-CMSG                     PIC X(12).
           03 ER2-MENS                     PIC X(80).
       01  ER2-TAB-DISP.
       COPY COLBRVEN REPLACING ==01  VEN.== BY
                               ==02 VEN-CAN OCCURS 50.==.
       COPY COLBRVEN REPLACING ==01  VEN.== BY
                               ==02 VEN-VEN OCCURS 50.==.
       COPY COLBRCAN REPLACING ==01  CAN.== BY
                               ==02 REG-CAN OCCURS 50.==.

       REPORT SECTION.
      *--------------
       COPY COLRFER2.
           .
       COPY COLRTER2.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-ER2-SRT-CIC-IOPC SECTION.
           USE BEFORE REPORTING CH-ER2-SRT-CIC-IOPC.
       INI-CH-ER2-SRT-CIC-IOPC.
       FIN-CH-ER2-SRT-CIC-IOPC.
           EXIT.
       DEC-PH-ER2 SECTION.
           USE BEFORE REPORTING PH-ER2.
       INI-PH-ER2.
       FIN-PH-ER2.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGER2' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM PROCESO.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       PROCESO SECTION.
       INI-PROCESO.
           MOVE 0 TO WSS-TDP-FINA WSS-TDP-CAPI.
           MOVE 0 TO WSS-TDU-FINA WSS-TDU-CAPI.
           OPEN OUTPUT RPTER2.
           INITIATE RPT-ER2.
       LUP-PROCESO.
           ACCEPT ER2-CIC-IOPC.
           IF ER2-CIC-IOPC NOT > SPACES OR
              ER2-CIC-IOPC = '*'
               GO TO FIN-PROCESO.
           DISPLAY 'OPERACION : ' ER2-CIC-IOPC.
      *LEE OPC
           MOVE ER2-CIC-IOPC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: NO EXISTE OPERACION : '
                       OPC-CIC-IOPC IN OPC
               GO TO LUP-PROCESO.
           IF OPC-MSC-STAT IN OPC NOT = 'A'
               DISPLAY 'ERROR: OPERACION NO ACTIVADA : '
                       OPC-CIC-IOPC IN OPC
               GO TO LUP-PROCESO.
      *     IF OPC-VAL-SCRE IN OPC NOT > ZEROES
      *         DISPLAY 'ERROR: OPERACION CANCELADA : '
      *                 OPC-CIC-IOPC IN OPC
      *         GO TO LUP-PROCESO.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               DISPLAY 'ERROR: OPERACION SIN EVC : '
                       OPC-CIC-IOPC IN OPC
               GO TO LUP-PROCESO.
      *
           MOVE SPACES TO ER2-TAB-DISP.
           MOVE SPACES TO ER2-CMSG ER2-MENS.
           PERFORM LEE-CANC.
           IF ER2-CMSG > SPACES
               GO TO ERR-PROCESO.
           PERFORM DSP-TABL VARYING ER2-NUM-IVEN FROM 1 BY 1 UNTIL
                                    ER2-NUM-IVEN > OPC-NUM-IVEN.
           GO TO LUP-PROCESO.
       ERR-PROCESO.
           DISPLAY 'ERROR : ' ER2-CMSG '. EN OPERACION :'
                   OPC-CIC-IOPC IN OPC.
           DISPLAY ER2-MENS.
           GO TO LUP-PROCESO.
       FIN-PROCESO.
           PERFORM DSP-TGEN.
           TERMINATE RPT-ER2.
           CLOSE RPTER2.
       EXT-PROCESO.
           EXIT.

       LEE-CANC SECTION.
       INI-LEE-CANC.
           MOVE SPACES TO ER2-CMSG.
           MOVE 1 TO ER2-NUM-IVEN.
           MOVE 0 TO WSS-TDF-FINA WSS-TDF-CAPI.
           MOVE OPC-VAL-CRED IN OPC TO ER2-VAL-SCRE.
           MOVE OPC-FEC-FCOL IN OPC TO ER2-FEC-FUNO.
       LUP-LEE-CANC.
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE ER2-NUM-IVEN        TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF NOT VEN-STAT-OKS
               DISPLAY 'AL TRATAR DE RECUPERAR VEN ' OPC-CIC-IOPC IN OPC
                       '/' ER2-NUM-IVEN
                       '. VEN-STAT : ' VEN-STAT
               MOVE VEN-STAT TO ER2-CMSG
               GO TO FIN-LEE-CANC.
           MOVE VEN TO VEN-CAN(ER2-NUM-IVEN).
           PERFORM CMP-FINA.
           IF ER2-CMSG > SPACES
               GO TO FIN-LEE-CANC.
           MOVE VEN TO VEN-VEN(ER2-NUM-IVEN).
           IF ER2-NUM-IVEN < OPC-NUM-IVEN IN OPC
               PERFORM BUS-CANC
           ELSE
               PERFORM CLR-CANC.
       NXT-LEE-CANC.
           ADD 1 TO ER2-NUM-IVEN.
           IF ER2-NUM-IVEN NOT > OPC-NUM-IVEN IN OPC
               IF ER2-NUM-IVEN NOT > 50
                   GO TO LUP-LEE-CANC
               ELSE
                   MOVE 'WSSIVENOVFLW' TO ER2-CMSG.
       FIN-LEE-CANC.
           EXIT.

       BUS-CANC SECTION.
       INI-BUS-CANC.
           MOVE 0 TO ER2-NUM-ICAN.
           MOVE VEN-KEY-IVEN IN VEN TO RCV-KEY-IVCT IN RCV.
           MOVE FIO-GET-KEY TO FIO-CMND.
       LUP-BUS-CANC.
           MOVE 'RCV-KEY-IVCT' TO FIO-AKEY.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS AND
                    RCV-KEY-IVCT IN RCV = VEN-KEY-IVEN IN VEN )
               GO TO CON-BUS-CANC.
           IF RCV-NUM-ICAN IN RCV > ER2-NUM-ICAN
               MOVE RCV-NUM-ICAN IN RCV TO ER2-NUM-ICAN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-BUS-CANC.
       CON-BUS-CANC.
           IF ER2-NUM-ICAN = 0
               PERFORM CLR-CANC
               GO TO FIN-BUS-CANC.
           MOVE VEN-CIC-IOPC IN VEN TO CAN-CIC-IOPC IN CAN.
           MOVE ER2-NUM-ICAN        TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS
               MOVE CAN TO REG-CAN(ER2-NUM-IVEN)
           ELSE
               PERFORM CLR-CANC.
       FIN-BUS-CANC.
           EXIT.

       CLR-CANC SECTION.
       INI-CLR-CANC.
           MOVE SPACES TO                 REG-CAN(ER2-NUM-IVEN).
           MOVE ZEROES TO CAN-NUM-ICAN IN REG-CAN(ER2-NUM-IVEN).
           MOVE ZEROES TO CAN-FEC-FOUT IN REG-CAN(ER2-NUM-IVEN).
           MOVE ZEROES TO CAN-FEC-FCAN IN REG-CAN(ER2-NUM-IVEN).
       FIN-CLR-CANC.
           EXIT.

       DSP-TABL SECTION.
       INI-DSP-TABL.
           IF ( ER2-NUM-IVEN = OPC-NUM-IVEN IN OPC ) AND
              ( ER2-NUM-IVEN NOT = 1 )
               PERFORM DSP-TOTA.
           MOVE VEN-CIC-IOPC IN VEN-VEN(ER2-NUM-IVEN) TO SRT-CIC-IOPC.
           MOVE VEN-NUM-IVEN IN VEN-VEN(ER2-NUM-IVEN) TO SRT-NUM-IVEN.
           MOVE VEN-NUM-DVEN IN VEN-VEN(ER2-NUM-IVEN) TO SRT-NUM-DVEN.
           MOVE VEN-NUM-MVEN IN VEN-VEN(ER2-NUM-IVEN) TO SRT-NUM-MVEN.
           MOVE VEN-NUM-SVEN IN VEN-VEN(ER2-NUM-IVEN) TO SRT-NUM-SVEN.
           MOVE VEN-NUM-AVEN IN VEN-VEN(ER2-NUM-IVEN) TO SRT-NUM-AVEN.
           MOVE VEN-IND-CALC IN VEN-VEN(ER2-NUM-IVEN) TO SRT-IND-CALC.
           MOVE VEN-VAL-CAPI IN VEN-CAN(ER2-NUM-IVEN) TO SRT-CAN-CAPI.
           MOVE VEN-VAL-FINA IN VEN-CAN(ER2-NUM-IVEN) TO SRT-CAN-FINA.
           MOVE VEN-VAL-CAPI IN VEN-VEN(ER2-NUM-IVEN) TO SRT-VEN-CAPI.
           MOVE VEN-VAL-FINA IN VEN-VEN(ER2-NUM-IVEN) TO SRT-VEN-FINA.
           IF ER2-NUM-IVEN = 1 OR SRT-IND-CALC = 'N'
               MOVE 0 TO SRT-DIF-CAPI
                         SRT-DIF-FINA
           ELSE
               SUBTRACT VEN-VAL-CAPI IN VEN-VEN(ER2-NUM-IVEN) FROM
                        VEN-VAL-CAPI IN VEN-CAN(ER2-NUM-IVEN) GIVING
                                                         SRT-DIF-CAPI
               SUBTRACT VEN-VAL-FINA IN VEN-VEN(ER2-NUM-IVEN) FROM
                        VEN-VAL-FINA IN VEN-CAN(ER2-NUM-IVEN) GIVING
                                                         SRT-DIF-FINA.

           IF CAN-FEC-FOUT IN REG-CAN(ER2-NUM-IVEN) NOT > ZEROES
               MOVE SPACES TO SRT-IND-CANC
           ELSE
           IF VEN-FEC-FVEN IN VEN-VEN(ER2-NUM-IVEN) >
              CAN-FEC-FOUT IN REG-CAN(ER2-NUM-IVEN)
               MOVE 'A' TO SRT-IND-CANC
           ELSE
           IF VEN-FEC-FVEN IN VEN-VEN(ER2-NUM-IVEN) =
              CAN-FEC-FOUT IN REG-CAN(ER2-NUM-IVEN)
               MOVE 'F' TO SRT-IND-CANC
           ELSE
               MOVE 'M' TO SRT-IND-CANC.

           MOVE CAN-NUM-ICAN IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-ICAN.
           MOVE CAN-NUM-DOUT IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-DOUT.
           MOVE CAN-NUM-MOUT IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-MOUT.
           MOVE CAN-NUM-SOUT IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-SOUT.
           MOVE CAN-NUM-AOUT IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-AOUT.
           MOVE CAN-NUM-DCAN IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-DCAN.
           MOVE CAN-NUM-MCAN IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-MCAN.
           MOVE CAN-NUM-SCAN IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-SCAN.
           MOVE CAN-NUM-ACAN IN REG-CAN(ER2-NUM-IVEN) TO SRT-NUM-ACAN.
           MOVE CAN-STP-ITRN IN REG-CAN(ER2-NUM-IVEN) TO SRT-STP-ITRN.

           GENERATE DL-ER2.
           ADD SRT-DIF-CAPI TO WSS-TDF-CAPI.
           ADD SRT-DIF-FINA TO WSS-TDF-FINA.
       FIN-DSP-TABL.
           EXIT.

       DSP-TOTA SECTION.
       INI-DSP-TOTA.
           GENERATE DL-TOTA.
           IF OPC-COD-VCOC IN OPC = CAM-COD-MNAC IN CAM-VARI
               ADD WSS-TDF-CAPI TO WSS-TDP-CAPI
               ADD WSS-TDF-FINA TO WSS-TDP-FINA
           ELSE
           IF OPC-COD-VCOC IN OPC = '0998'
               ADD WSS-TDF-CAPI TO WSS-TDU-CAPI
               ADD WSS-TDF-FINA TO WSS-TDU-FINA
           ELSE
               DISPLAY 'ERROR: NO EXISTE ACUMULADOR PARA MONEDA : '
                       OPC-COD-VCOC IN OPC.
       FIN-DSP-TOTA.
           EXIT.

       DSP-TGEN SECTION.
       INI-DSP-TGEN.
           GENERATE DL-TGEN.
       FIN-DSP-TGEN.
           EXIT.

       CMP-FINA SECTION.
       INI-CMP-FINA.
           MOVE SPACES TO ER2-CMSG.
           IF VEN-IND-CALC IN VEN = 'N' AND
              ER2-NUM-IVEN = OPC-NUM-IVEN IN OPC
               GO TO FIN-CMP-FINA.
           MOVE OPC-CIC-IOPC IN OPC TO VIN-CIC-IOPC.
           MOVE VEN-NUM-IDLC IN VEN TO VIN-NUM-IDLC.
           PERFORM BUS-VIN.
           IF NOT FIO-STAT-OKS
               MOVE 'ICG    NEX' TO ER2-CMSG
               GO TO FIN-CMP-FINA.
      *para obtener tasa de periodo
           MOVE OPC-NUM-DCOL IN OPC     TO INT-DCOL.
           MOVE OPC-NUM-MCOL IN OPC     TO INT-MCOL.
           MOVE OPC-NUM-SCOL IN OPC     TO INT-SCOL.
           MOVE OPC-NUM-ACOL IN OPC     TO INT-ACOL.

           MOVE ER2-NUM-DUNO            TO INT-DUNO.
           MOVE ER2-NUM-MUNO            TO INT-MUNO.
           MOVE ER2-NUM-SUNO            TO INT-SUNO.
           MOVE ER2-NUM-AUNO            TO INT-AUNO.

           MOVE VEN-NUM-DVEN IN VEN     TO INT-DDOS.
           MOVE VEN-NUM-MVEN IN VEN     TO INT-MDOS.
           MOVE VEN-NUM-SVEN IN VEN     TO INT-SDOS.
           MOVE VEN-NUM-AVEN IN VEN     TO INT-ADOS.

           MOVE ER2-NUM-DUNO            TO INT-DBTV.
           MOVE ER2-NUM-MUNO            TO INT-MBTV.
           MOVE ER2-NUM-SUNO            TO INT-SBTV.
           MOVE ER2-NUM-AUNO            TO INT-ABTV.

           IF ICG-COD-CVTI IN ICG NOT > SPACES
               MOVE 'ICGTASAFIJA' TO ER2-CMSG
               GO TO FIN-CMP-FINA.
           MOVE ICG-SGV-SPRD IN ICG TO INT-TINT.
           MOVE ICG-COD-TINT IN ICG TO INT-TIPT.
           MOVE OPC-COD-VCOC IN OPC TO INT-VCAM.
      *SMC-INI 13.09.1993
           MOVE OPC-CIC-IOPC IN OPC TO INT-IOPC.
           MOVE VEN-NUM-IDLC IN VEN TO INT-IDLC.
      *     PERFORM GNS-CAL-INT.
           PERFORM COL-CAL-INT.
      *SMC-FIN 13.09.1993
           IF INT-CMSG > SPACES
               MOVE INT-CMSG            TO ER2-CMSG
               MOVE CAM-KEY-CAMB IN CAM TO ER2-MENS
               GO TO FIN-CMP-FINA.
      *Calcula el interes
           MOVE VEN-VAL-IDNI IN VEN TO ER2-VAL-IDNI IN ER2-VARI.
           ADD ER2-VAL-SCRE IN ER2-VARI ER2-VAL-IDNI IN ER2-VARI
               GIVING ER2-VAL-INTE IN ER2-VARI.
           MULTIPLY ER2-VAL-INTE IN ER2-VARI BY INT-TIPE GIVING
                    ER2-VAL-INTE IN ER2-VARI ROUNDED.
           ADD ER2-VAL-IDNI IN ER2-VARI TO ER2-VAL-INTE IN ER2-VARI.
      *Si el credito esta en moneda nacional ( no reajustable ), redonde
      *sin centavos el interes
           IF OPC-COD-VCOC IN OPC = CAM-COD-MNAC IN CAM-VARI
            MOVE ER2-VAL-INTE IN ER2-VARI TO PES-SGV-DECI IN PES-VARI
            PERFORM PES-SCTV
            MOVE PES-SGV-ENTE IN PES-VARI TO ER2-VAL-INTE IN ER2-VARI.
      *Completa valor final
            ADD ER2-VAL-INTE IN ER2-VARI
                VEN-VAL-CAPI IN VEN  GIVING VEN-VAL-FINA IN VEN.
            MOVE VEN-VAL-FINA IN VEN TO     VEN-VAL-SFIN IN VEN.
      *Actualiza valores
            SUBTRACT VEN-VAL-CAPI IN VEN FROM ER2-VAL-SCRE.
            MOVE     VEN-FEC-FVEN IN VEN TO   ER2-FEC-FUNO.
       FIN-CMP-FINA.
           EXIT.

       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBFMSC.
       COPY GNSBBMSG.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
      *SMC-INI 13.09.1993
       COPY COLBGINT.
      *SMC-FIN 13.09.1993
       COPY GNSBGPES.

       COPY COLBFRCV.
       COPY COLBFCAN.
       COPY COLBFVEN.
       COPY COLBFOPC.
       COPY COLBFICG.
       COPY COLBFDLC.
       COPY COLBFEVC.
       COPY COLBFCYA.
       COPY COLBFRDC.
       COPY COLBFROV.
       COPY COLBFTOC.
       COPY SGCBFDBC.
       COPY TABBFCAM.

       COPY COLBGVIN.
       COPY COLBGVEN.
