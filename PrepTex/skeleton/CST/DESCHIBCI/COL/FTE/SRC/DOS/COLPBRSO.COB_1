       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPBRSO.
       AUTHOR.          ADA(rvm).
       DATE-WRITTEN.    10-SEP-1999 15:13:22.

      * Programa Generador Datos para :
      * "Solicitudes Certificados Curse de Operaciones"

       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *------------------------
       COPY COLBRRSC.
       COPY COLBRSCO.
       COPY COLBRCUR.
       COPY COLBRDLC.
       COPY COLBROPC.
       COPY COLBRRDC.
       COPY COLBRVEN.
       COPY COLBRROV.
       COPY COLBRTOC.
       COPY COLWGCTO.
       COPY SGCBRDBC.
       COPY SGCBRRCD.
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY TABBRTIO.
       COPY TABWGTVC.
       COPY TABWGLOC.

       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWVIDD.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGPES.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGCPT.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.

       01  WSS-VARI.
           03  WSS-FEC-FINI.
               05  WSS-NUM-SINI                       PIC 9(02).
               05  WSS-NUM-AINI                       PIC 9(02).
               05  WSS-NUM-MINI                       PIC 9(02).
               05  WSS-NUM-DINI                       PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-SFIN                       PIC 9(02).
               05  WSS-NUM-AFIN                       PIC 9(02).
               05  WSS-NUM-MFIN                       PIC 9(02).
               05  WSS-NUM-DFIN                       PIC 9(02).
           03  WSS-FEC-FINN.
               05  WSS-NUM-DINN                       PIC 9(02).
               05  WSS-NUM-MINN                       PIC 9(02).
               05  WSS-NUM-SINN                       PIC 9(02).
               05  WSS-NUM-AINN                       PIC 9(02).
           03  WSS-FEC-FFNN.
               05  WSS-NUM-DFNN                       PIC 9(02).
               05  WSS-NUM-MFNN                       PIC 9(02).
               05  WSS-NUM-SFNN                       PIC 9(02).
               05  WSS-NUM-AFNN                       PIC 9(02).
           03  WSS-AUX-DIRE.
               05  WSS-GLS-DIRE                       PIC X(40).
               05  WSS-GLS-COMU                       PIC X(40).
           03  WSS-SGV-VCAM       VALUE ZEROES        PIC 9(11)V9(04).
           03  WSS-NUM-RSCG       VALUE ZEROES        PIC 9(06).
           03  WSS-NUM-OPCL       VALUE ZEROES        PIC 9(06).
           03  WSS-NUM-CURL       VALUE ZEROES        PIC 9(06).
           03  WSS-NUM-SCOL       VALUE ZEROES        PIC 9(06).
           03  WSS-COD-PESO       VALUE SPACES        PIC X(06).
           03  WSS-HAY-OPC        VALUE 'N'           PIC X(01).
      *DATOS DE OPC LEIDA
           03  WSS-OPC-IOPC       VALUE SPACES        PIC X(12).
           03  WSS-OPC-FCOL.
               05  WSS-OPC-SCOL   VALUE ZEROES        PIC 9(02).
               05  WSS-OPC-ACOL   VALUE ZEROES        PIC 9(02).
               05  WSS-OPC-MCOL   VALUE ZEROES        PIC 9(02).
               05  WSS-OPC-DCOL   VALUE ZEROES        PIC 9(02).
           03  WSS-OPC-VCOC       VALUE SPACES        PIC X(06).
           03  WSS-OPC-CRED       VALUE ZEROES        PIC 9(11)V9(04).
      *
           03  WSS-VAL-DIFE       VALUE ZEROES        PIC 9(11)V9(04).
           03  WSS-VAL-CALX       VALUE ZEROES        PIC 9(11)V9(04).
           03  WSS-VAL-CALY       VALUE ZEROES        PIC 9(11)V9(04).
           03  WSS-COD-VCOC       VALUE SPACES        PIC X(06).
           03  WSS-SGV-RENO       VALUE ZEROES        PIC S9(11)V9(04).
           03  WSS-CUR-ICLI-ANT   VALUE SPACES        PIC X(12).
      *SW, S: Si Considerar N: No Considerar X: No eXiste ROV
           03  WSS-ROV-OK         VALUE 'S'           PIC X(01).
      *
       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           PERFORM INI.
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN.

           PERFORM PROCESA-COL.

           PERFORM PROCESA-OTROS.
       LUP-MAIN.
       NXT-MAIN.
       FIN-MAIN.
           PERFORM IMP-ESTD.
           PERFORM CLO-FIL.
           PERFORM GNS-PRO-END.

       PROCESA-COL SECTION.
       INI-PROCESA-COL.
           MOVE WSS-FEC-FINI IN WSS-VARI TO SCO-FEC-FSCO IN SCO
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-PROCESA-COL.
           MOVE 'SCO-FEC-FSCO' TO FIO-AKEY.
           PERFORM COL-FIO-SCO.
           IF NOT FIO-STAT-OKS OR
              (SCO-FEC-FSCO IN SCO < WSS-FEC-FINI IN WSS-VARI) OR
              (SCO-FEC-FSCO IN SCO > WSS-FEC-FFIN IN WSS-VARI)
               IF NOT FIO-STAT-OKS
                   IF FIO-CMND = FIO-GET-NLS
                       DISPLAY ' '
                       DISPLAY 'ERROR: NO EXISTEN DATOS SCO ENTRE EL: '
                       WSS-FEC-FINI IN WSS-VARI ' Y EL: '
                       WSS-FEC-FFIN IN WSS-VARI ' STAT: ' FIO-STAT
                       PERFORM GNS-PRO-ABT
                   ELSE
                       GO TO FIN-PROCESA-COL.

      *LLENA CAMPOS COMUNES EN REGISTRO Y GRABA CABECERA
           MOVE SPACES              TO RSC-GLS-FLAG IN RSC.
           MOVE SCO-CIC-ICLI IN SCO TO RSC-CIC-ICLI IN RSC.
           MOVE SCO-FEC-FIRS IN SCO TO RSC-FEC-FINI IN RSC.
           MOVE SCO-FEC-FTRS IN SCO TO RSC-FEC-FFIN IN RSC.
           MOVE SCO-FEC-FSCO IN SCO TO RSC-FEC-FSCO IN RSC.
           MOVE SCO-COD-AUTS IN SCO TO RSC-COD-AUTS IN RSC.
           MOVE SCO-COD-OFSO IN SCO TO RSC-COD-OFIS IN RSC.
           PERFORM GRABA-CABECERA.
           ADD 1 TO WSS-NUM-SCOL IN WSS-VARI.

      *SE LEE RDC ASOCIADAS AL CIC DEL CLIENTE
           PERFORM PROCESO-RDC.
       NXT-PROCESA-COL.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PROCESA-COL.
       FIN-PROCESA-COL.
           EXIT.

       PROCESA-OTROS SECTION.
       INI-PROCESA-OTROS.
           MOVE SPACES TO RSC-CIC-ICLI IN RSC.
      *Archivo secuencial se lee con NEXT
           MOVE LOW-VALUES  TO CUR.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CUR.
           IF NOT ( FIO-STAT-OKS AND CUR > SPACES )
               DISPLAY ' '
               DISPLAY 'ATENCION: NO EXISTEN DATOS COLCUR'
               PERFORM FIN-PROCESA-OTROS.
       LUP-PROCESA-OTROS.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PROCESA-OTROS.

           ADD 1 TO WSS-NUM-CURL IN WSS-VARI.


           MOVE CUR-CIC-ICLI IN CUR TO RSC-CIC-ICLI IN RSC.
      *GRABA CABECERA
           IF CUR-CIC-ICLI IN CUR NOT = WSS-CUR-ICLI-ANT IN WSS-VARI
               MOVE CUR-CIC-ICLI IN CUR TO WSS-CUR-ICLI-ANT IN WSS-VARI
               MOVE CUR-CIC-ICLI IN CUR TO SCO-CIC-ICLI IN SCO
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-SCO
               IF FIO-STAT-OKS
                   MOVE CUR-FEC-FINI IN CUR TO RSC-FEC-FINI IN RSC
                   MOVE CUR-FEC-FFIN IN CUR TO RSC-FEC-FFIN IN RSC
                   MOVE SCO-FEC-FSCO IN SCO TO RSC-FEC-FSCO IN RSC
                   MOVE SCO-COD-AUTS IN SCO TO RSC-COD-AUTS IN RSC
                   MOVE SCO-COD-OFSO IN SCO TO RSC-COD-OFIS IN RSC
                   PERFORM GRABA-CABECERA
               ELSE
                   MOVE SPACES TO WSS-CUR-ICLI-ANT IN WSS-VARI
                   DISPLAY
                    'ATENCION: NO SE ENCONTRO SOLICITUD PARA CIC: '
                         CUR-CIC-ICLI IN CUR '  CON CURSE: '
                         CUR-CIC-IOPC IN CUR
                   GO TO NXT-PROCESA-OTROS.

      *LLENA CAMPOS EN REGISTRO RSC
           MOVE SPACES              TO RSC-GLS-FLAG IN RSC.
      *EFECTUA LLENADO DE CAMPOS (REGISTRO TIPO OPC)
           MOVE '3'                 TO RSC-COD-TREG IN RSC.
           MOVE CUR-COD-SIST IN CUR TO RSC-COD-SIST IN RSC.
           MOVE CUR-CIC-IOPC IN CUR TO RSC-CIC-IOPC IN RSC.
           MOVE CUR-COD-OFOC IN CUR TO RSC-COD-OFOC IN RSC.
           MOVE CUR-COD-COOC IN CUR TO RSC-COD-COOC IN RSC.
           MOVE CUR-FEC-FCOL IN CUR TO RSC-FEC-FCOL IN RSC.
           MOVE CUR-COD-VCOC IN CUR TO RSC-COD-VCOC IN RSC.
           MOVE CUR-COD-OFOC IN CUR TO RSC-COD-OFOC IN RSC.
           MOVE CUR-NUM-VCOR IN CUR TO RSC-NUM-VCOR IN RSC.
           MOVE CUR-FEC-FMAV IN CUR TO RSC-FEC-FMAV IN RSC.
           MOVE ZEROES              TO RSC-NUM-VCVG IN RSC.
      *BUSCA GLOSA MONEDA
           PERFORM BUS-GLS-VCOC.
           MOVE 'A'                 TO RSC-CUR-STAT IN RSC.
           MOVE SPACES              TO RSC-GLS-DSP3 IN RSC.
           MOVE SPACES              TO RSC-CIC-IOPC-CAN IN RSC.
           MOVE ZEROES              TO RSC-VAL-CRED IN RSC.
           MOVE CUR-VAL-CREO IN CUR TO RSC-VAL-CREO IN RSC.
           PERFORM VALORIZA-CRED.
           IF TVC-CMSG > SPACES
               GO TO NXT-PROCESA-OTROS.
      *
      *GRABO DETALLE (REGISTRO TIPO=3)
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RSC.
           IF FIO-STAT-OKS
               MOVE 'S' TO WSS-HAY-OPC IN WSS-VARI
               ADD 1 TO WSS-NUM-RSCG IN WSS-VARI
      *INICALIZA VALORES
               MOVE ZEROES TO RSC-VAL-CRED
                              RSC-VAL-CAPI
                              RSC-VAL-INTE
                              RSC-VAL-REAJ
                              RSC-VAL-CREO
           ELSE
               DISPLAY 'ERROR: AL GRABAR COLRSC : ' FIO-STAT
               PERFORM GNS-PRO-ABT.
       NXT-PROCESA-OTROS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CUR.
           GO TO LUP-PROCESA-OTROS.
       FIN-PROCESA-OTROS.
           EXIT.
      ******************************************************************
       PROCESO-RDC SECTION.
       INI-PROCESO-RDC.
      *SE LEE RDC PARA LEER OPC
           MOVE SCO-CIC-ICLI IN SCO TO RDC-CIC-ICLI IN RDC.
           MOVE SPACES              TO RDC-KEY-IDLC IN RDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-PROCESO-RDC.
           PERFORM COL-FIO-RDC.
           IF FIO-STAT-OKS AND
              RDC-CIC-ICLI IN RDC = SCO-CIC-ICLI IN SCO
               IF RDC-NUM-IDLC IN RDC = 0
                   PERFORM PROCESO-OPC
                   GO TO NXT-PROCESO-RDC
               ELSE
                   GO TO NXT-PROCESO-RDC
           ELSE
               IF FIO-CMND = FIO-GET-NLS
                   DISPLAY 'ATENCION: NO EXISTEN COLRDC PARA CIC : '
                           SCO-CIC-ICLI IN SCO
                   GO TO FIN-PROCESO-RDC
               ELSE
                   GO TO FIN-PROCESO-RDC.
       NXT-PROCESO-RDC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PROCESO-RDC.
       FIN-PROCESO-RDC.
           EXIT.

       PROCESO-OPC SECTION.
       INI-PROCESO-OPC.
      *LEO OPC
           MOVE RDC-CIC-IOPC IN RDC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF ( NOT FIO-STAT-OKS ) OR ( OPC-MSC-STAT IN OPC NOT = 'A' )
               GO TO FIN-PROCESO-OPC.

      *LEE SOLO LAS OPC DE TIPO COL
           IF OPC-COD-TOPC IN OPC NOT = 'D'
               GO TO FIN-PROCESO-OPC.

      *LEE SOLO LAS OPC DEL PERIODO DE SELECCION
           IF OPC-FEC-FCOL IN OPC < SCO-FEC-FIRS IN SCO OR
              OPC-FEC-FCOL IN OPC > SCO-FEC-FTRS IN SCO
               GO TO FIN-PROCESO-OPC.

      * Busca con el Codigo Tipo Operacion en la Tabla cargada en Memori
      * 'Sistema que lo Maneja'
           MOVE OPC-COD-TOOC IN OPC TO CTO-TOOC IN CTO-VARI.
           PERFORM COL-BUS-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
               DISPLAY 'CON CODIGO : '     CTO-TOOC IN CTO-VARI
                       ' EN CREDITO : '    OPC-CIC-IOPC IN OPC
               MOVE ZEROES TO RSC-VAL-CAPI IN RSC
               GO TO FIN-PROCESO-OPC.

      * Valida el 'Sistema que lo Maneja' de la Operacion de Credito
      *     IF CTO-SIST IN CTO-VARI NOT = 'COL'
      *         GO TO FIN-PROCESO-OPC.

      *Verifica el 'Opera Como'
           IF CTO-OPEC IN CTO-VARI = 'B'
               MOVE ZEROES TO RSC-VAL-CAPI IN RSC
               GO TO FIN-PROCESO-OPC.

           ADD 1 TO WSS-NUM-OPCL IN WSS-VARI.

      *EFECTUA LLENADO DE CAMPOS (REGISTRO DE OPC)
           MOVE '3'                 TO RSC-COD-TREG IN RSC.
           MOVE 'COL'               TO RSC-COD-SIST IN RSC.
           MOVE OPC-CIC-IOPC IN OPC TO RSC-CIC-IOPC IN RSC.
           MOVE SPACES              TO RSC-FILLER   IN RSC.
           MOVE OPC-COD-OFOC IN OPC TO RSC-COD-OFOC IN RSC.
           MOVE OPC-FEC-FCOL IN OPC TO RSC-FEC-FCOL IN RSC.
           MOVE OPC-COD-OFOC IN OPC TO RSC-COD-OFOC IN RSC.
           MOVE OPC-COD-VCOC IN OPC TO RSC-COD-VCOC IN RSC.
      *BUSCA GLOSA MONEDA
           PERFORM BUS-GLS-VCOC.
           MOVE OPC-COD-COOC IN OPC TO RSC-COD-COOC IN RSC.
           MOVE OPC-NUM-VCOR IN OPC TO RSC-NUM-VCOR IN RSC.
           MOVE OPC-NUM-VCVG IN OPC TO RSC-NUM-VCVG IN RSC.
      *LEO DLC
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE ZEROES              TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF FIO-STAT-OKS AND
              (OPC-CIC-IOPC IN OPC = DLC-CIC-IOPC IN DLC)
               MOVE DLC-FEC-FMAV TO RSC-FEC-FMAV IN RSC
           ELSE
               DISPLAY 'ERROR: NO SE ENCONTRO COLDLC : '
                       OPC-CIC-IOPC IN OPC ' STAT: ' FIO-STAT
               MOVE ZEROES TO RSC-FEC-FMAV IN RSC.
      *
      *05.01.2000
           IF OPC-IND-RENO IN OPC = 'PRO'
               GO TO FIN-PROCESO-OPC.

      *05.01.2000
           IF OPC-IND-RENO IN OPC = 'REN'
               IF DLC-IND-SCTB IN DLC = 'VCD' OR 'CAS'
                   GO TO FIN-PROCESO-OPC.

           IF CTO-OPEC IN CTO-VARI = 'D'
               MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN
               MOVE 001                 TO VEN-NUM-IVEN IN VEN
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-VEN
               IF FIO-STAT-OKS
                   MOVE VEN-VAL-CAPI IN VEN TO RSC-VAL-CREO IN RSC
               ELSE
                   MOVE OPC-VAL-CRED TO RSC-VAL-CREO IN RSC
           ELSE
               MOVE OPC-VAL-CRED TO RSC-VAL-CREO IN RSC.

           PERFORM VALORIZA-CRED.
           IF TVC-CMSG > SPACES
                GO TO FIN-PROCESO-OPC.

      *BUSCA PRIMER NUMERO OPERACION ENCADENADA
           IF OPC-IND-RENO = 'REN' OR 'NOV'
               MOVE OPC-CIC-IOPC IN OPC TO ROV-CIC-IOPC IN ROV
               MOVE SPACES              TO ROV-KEY-IVCT IN ROV
               MOVE FIO-GET-NLS TO FIO-CMND
               PERFORM COL-FIO-ROV
               IF FIO-STAT-OKS AND
                  OPC-CIC-IOPC IN OPC = ROV-CIC-IOPC IN ROV
                   MOVE ROV-CIC-IOPV IN ROV TO RSC-CIC-IOPC-CAN IN RSC
               ELSE
                   MOVE SPACES              TO RSC-CIC-IOPC-CAN IN RSC
           ELSE
               MOVE DLC-CIC-IOCO IN DLC     TO RSC-CIC-IOPC-CAN IN RSC.
      *
           IF RSC-CIC-IOPC IN RSC = RSC-CIC-IOPC-CAN IN RSC
               MOVE SPACES TO RSC-CIC-IOPC-CAN IN RSC.
      *
           MOVE 'A' TO RSC-CUR-STAT IN RSC.
           MOVE SPACES TO RSC-GLS-DSP3 IN RSC.
      *
      *GRABO DETALLE (REGISTRO TIPO=3)
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RSC.
           IF FIO-STAT-OKS
               MOVE 'S' TO WSS-HAY-OPC IN WSS-VARI
               ADD 1 TO WSS-NUM-RSCG IN WSS-VARI
      *INICALIZA VALORES
               MOVE ZEROES TO RSC-VAL-CRED
                              RSC-VAL-CAPI
                              RSC-VAL-INTE
                              RSC-VAL-REAJ
                              RSC-VAL-CREO
           ELSE
               DISPLAY 'ERROR: AL GRABAR COLRSC : ' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-PROCESO-OPC.
           EXIT.

       VALORIZA-CRED SECTION.
       INI-VALORIZA-CRED.
           MOVE RSC-NUM-DCOL IN RSC      TO TVC-DCAM.
           MOVE RSC-NUM-MCOL IN RSC      TO TVC-MCAM.
           MOVE RSC-NUM-SCOL IN RSC      TO TVC-SCAM.
           MOVE RSC-NUM-ACOL IN RSC      TO TVC-ACAM.
           MOVE RSC-COD-VCOC IN RSC      TO TVC-MON1.
           MOVE WSS-COD-PESO IN WSS-VARI TO TVC-MON2.
           MOVE RSC-VAL-CREO IN RSC      TO TVC-VALO.
           PERFORM TAB-PRO-TVC.
           IF TVC-CMSG > SPACES
               DISPLAY 'ERROR: NO EXISTE VALOR DE CAMBIO DE: ' TVC-MON1
                       '   A: ' TVC-MON2 '  PARA EL: ' TVC-FCAM
               MOVE ZEROES TO RSC-VAL-CRED IN RSC
           ELSE
               MULTIPLY TVC-VALO BY 1 GIVING
                        RSC-VAL-CRED IN RSC ROUNDED.
       FIN-VALORIZA-CRED.
           EXIT.

       GRABA-CABECERA SECTION.
       INI-GRABA-CABECERA.
           MOVE '0'                 TO RSC-COD-TREG IN RSC.
      *LEO RESTO DE ENTIDADES PARA COMPLETAR LLENADO DE CAMPOS
           PERFORM LEO-SGC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: AL LEER SGCDBC : ' DBC-CIC-ICLI IN DBC
                       ' STATUS : ' FIO-STAT
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

           PERFORM BUS-GLS-OFIS.
      *
           MOVE SPACES              TO RSC-GLS-DSP0 IN RSC.
      *
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RSC.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RSCG IN WSS-VARI
           ELSE
               DISPLAY 'ERROR: AL GRABAR COLRSC : ' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-GRABA-CABECERA.
           EXIT.

       LEO-SGC SECTION.
       INI-LEO-SGC.
           MOVE RSC-CIC-ICLI IN RSC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF FIO-STAT-OKS
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN            TO RSC-GLS-NOMC IN RSC
               MOVE DBC-NUM-ICLI IN DBC TO RSC-NUM-ICLI IN RSC
               MOVE DBC-IND-ICLI IN DBC TO RSC-IND-ICLI IN RSC
               MOVE DBC-GLS-ICLI IN DBC TO RSC-GLS-ICLI IN RSC
               MOVE DBC-VRF-ICLI IN DBC TO RSC-VRF-ICLI IN RSC
               MOVE DBC-COD-EJEC IN DBC TO RSC-COD-EJEC IN RSC
           ELSE
               MOVE 'CLIENTE NO EXISTE' TO RSC-GLS-NOMC IN RSC
               MOVE SPACES              TO RSC-NUM-ICLI IN RSC
                                           RSC-IND-ICLI IN RSC
                                           RSC-GLS-ICLI IN RSC
                                           RSC-VRF-ICLI IN RSC
                                           RSC-COD-EJEC IN RSC.

           PERFORM BUS-DOM-PPAL.

       FIN-LEO-SGC.
           EXIT.

       BUS-DOM-PPAL SECTION.
       INI-BUS-DOM-PPAL.
           MOVE SPACES TO WSS-GLS-DIRE IN WSS-VARI.
           MOVE DBC-CIC-ICLI IN DBC TO RCD-CIC-ICLI IN RCD.
           MOVE ZEROES              TO RCD-NUM-DIRE IN RCD.
           MOVE FIO-GET-NLS         TO FIO-CMND.
       LUP-BUS-DOM-PPAL.
           PERFORM SGC-FIO-RCD.
           IF ( FIO-STAT-OKS AND
                DBC-CIC-ICLI IN DBC = RCD-CIC-ICLI IN RCD )
               IF RCD-MSC-TDIR IN RCD = 'D'
                   MOVE RCD-GLS-DIRE IN RCD TO WSS-GLS-DIRE IN WSS-VARI
                   MOVE RCD-COD-LUGA IN RCD TO LOC-LUGA IN LOC-VARI
               ELSE
      *GUARDA PRIMERA DIRECCION OBTENIDA SI NO ES 'D'
                IF WSS-GLS-DIRE IN WSS-VARI NOT > SPACES
                    MOVE RCD-GLS-DIRE IN RCD TO WSS-GLS-DIRE IN WSS-VARI
                    MOVE RCD-COD-LUGA IN RCD TO LOC-LUGA IN LOC-VARI
                    MOVE FIO-GET-NXT TO FIO-CMND
                    GO TO LUP-BUS-DOM-PPAL
                ELSE
                    MOVE FIO-GET-NXT TO FIO-CMND
                    GO TO LUP-BUS-DOM-PPAL
           ELSE
               MOVE 'SIN DIRECCION ASOCIADA' TO RSC-GLS-DIRE IN RSC
               GO TO FIN-BUS-DOM-PPAL.

           PERFORM TAB-BUS-LOC.
           MOVE LOC-DESC     IN LOC-VARI TO WSS-GLS-COMU IN WSS-VARI.
           MOVE WSS-AUX-DIRE IN WSS-VARI TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN                 TO RSC-GLS-DIRE IN RSC.
       FIN-BUS-DOM-PPAL.
           EXIT.

       BUS-GLS-OFIS SECTION.
       INI-BUS-GLS-OFIS.
           IF RSC-COD-OFIS IN RSC > SPACES
               MOVE RSC-COD-OFIS IN RSC TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO RSC-GLS-OFIS IN RSC
           ELSE
               MOVE SPACES              TO RSC-GLS-OFIS IN RSC.
       FIN-BUS-GLS-OFIS.
           EXIT.

       BUS-GLS-VCOC SECTION.
       INI-BUS-GLS-VCOC.
           IF RSC-COD-VCOC IN RSC > SPACES
               MOVE 'TAB'               TO FIO-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE RSC-COD-VCOC IN RSC TO TAB-COD-CTAB IN TAB
               MOVE FIO-GET-KEY    TO FIO-CMND
               PERFORM GNS-FIO-TAB
               IF FIO-STAT-OKS
                   MOVE TAB-GLS-ABRV IN TAB TO RSC-GLS-VCOC IN RSC
               ELSE
                   MOVE SPACES              TO RSC-GLS-VCOC IN RSC
           ELSE
               MOVE SPACES TO RSC-GLS-VCOC IN RSC.
       FIN-BUS-GLS-VCOC.
           EXIT.

       INI SECTION.
       INI-INI.
           MOVE 'COLPBRSO' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
      *
           PERFORM GET-FHOY.
      *Captura Fecha de Inicio
           DISPLAY 'FECHA INICIO  ( DDMMSSAA ) : '.
           ACCEPT  WSS-FEC-FINN IN WSS-VARI.
           DISPLAY WSS-FEC-FINN IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA TERMINO ( DDMMSSAA ) : '.
           ACCEPT  WSS-FEC-FFNN IN WSS-VARI.
           DISPLAY WSS-FEC-FFNN IN WSS-VARI.

      *VAL-FEC Valida Fecha de Inicio
           MOVE WSS-FEC-FINN IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE FIO-STAT-NEX TO FIO-STAT
               PERFORM GNS-PRO-ABT.

      *VAL-FEC Valida Fecha de Termino
           MOVE WSS-FEC-FFNN IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE FIO-STAT-NEX TO FIO-STAT
               PERFORM GNS-PRO-ABT.

      *MUEVE FECHA DE INICIO
           MOVE WSS-NUM-DINN IN WSS-VARI TO WSS-NUM-DINI IN WSS-VARI.
           MOVE WSS-NUM-MINN IN WSS-VARI TO WSS-NUM-MINI IN WSS-VARI.
           MOVE WSS-NUM-SINN IN WSS-VARI TO WSS-NUM-SINI IN WSS-VARI.
           MOVE WSS-NUM-AINN IN WSS-VARI TO WSS-NUM-AINI IN WSS-VARI.

      *MUEVE FECHA DE TERMINO
           MOVE WSS-NUM-DFNN IN WSS-VARI TO WSS-NUM-DFIN IN WSS-VARI.
           MOVE WSS-NUM-MFNN IN WSS-VARI TO WSS-NUM-MFIN IN WSS-VARI.
           MOVE WSS-NUM-SFNN IN WSS-VARI TO WSS-NUM-SFIN IN WSS-VARI.
           MOVE WSS-NUM-AFNN IN WSS-VARI TO WSS-NUM-AFIN IN WSS-VARI.

           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-RSC.
           DISPLAY 'STATUS DE APERTURA OUTPUT COLRSC : ' FIO-STAT
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: EN APERTURA COLSCO, PROCESO CANCELADO'
               PERFORM GNS-PRO-ABT.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-SCO.
           DISPLAY 'STATUS DE APERTURA INPUT  COLSCO : ' FIO-STAT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: EN APERTURA COLSCO, PROCESO CANCELADO'
               PERFORM GNS-PRO-ABT.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CUR.
           DISPLAY 'STATUS DE APERTURA INPUT  COLCUR : ' FIO-STAT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: EN APERTURA COLCUR, PROCESO CANCELADO'
               PERFORM GNS-PRO-ABT.

           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-OPC
           PERFORM COL-FIO-RDC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-ROV.
           PERFORM SGC-FIO-DBC.
           PERFORM SGC-FIO-RCD.
           PERFORM TAB-FIO-CAM.
           PERFORM TAB-FIO-OFI.

           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB.
           MOVE '0999'         TO TAB-CIC-CTAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI
               DISPLAY ' '
               DISPLAY 'CODIGO PESO: ' WSS-COD-PESO IN WSS-VARI
               DISPLAY ' '
           ELSE
               DISPLAY 'ERROR: NO EXISTE CODIGO PESO ' FIO-STAT
               PERFORM GNS-PRO-ABT.

      * Proceso para Cargar las tablas  TAB/TIO  y  COL/TOC  en Memoria
           PERFORM COL-CRG-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
               GO TO FIN-INI.
           MOVE '00' TO FIO-STAT.
       FIN-INI.
           EXIT.

       IMP-ESTD SECTION.
       INI-IMP-ESTD.
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS COLPBRSO'.
           DISPLAY '---------------------'.
           DISPLAY 'REGISTROS    SCO LEIDOS  ......: '
                    WSS-NUM-SCOL IN WSS-VARI.
           DISPLAY 'REGISTROS COLOPC LEIDOS  ......: '
                    WSS-NUM-OPCL IN WSS-VARI.
           DISPLAY 'REGISTROS COLCUR LEIDOS  ......: '
                    WSS-NUM-CURL IN WSS-VARI.
           DISPLAY 'REGISTROS COLRSC GRABADOS......: '
                    WSS-NUM-RSCG IN WSS-VARI.
           DISPLAY ' '.
       FIN-IMP-ESTD.
           EXIT.

       CLO-FIL SECTION.
       INI-CLO-FIL.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-RSC.
           PERFORM COL-FIO-SCO.
           PERFORM COL-FIO-CUR.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-OPC
           PERFORM COL-FIO-RDC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-ROV.
           PERFORM SGC-FIO-DBC.
           PERFORM SGC-FIO-RCD.
           PERFORM TAB-FIO-CAM.
           PERFORM TAB-FIO-OFI.
       FIN-CLO-FIL.
           EXIT.
      *-----------------------------------------------------------------
       COPY COLIORSC.
       COPY COLIOSCO.
       COPY COLIOCUR.
       COPY COLBFDLC.
       COPY COLBFOPC.
       COPY COLBFRDC.
       COPY COLBFVEN.
       COPY COLBFROV.
       COPY COLBFTOC.
       COPY COLBGCTO.
       COPY SGCBFDBC.
       COPY SGCBFRCD.
       COPY TABBBLOC.
       COPY TABBBOFI.
       COPY TABBFOFI.
       COPY TABBFCAM.
       COPY TABBGTVC.
      *
       COPY GNSBGPES.
       COPY GNSBGCPT.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBTABT.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBBSYS.
       COPY GNSBGHOY.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
