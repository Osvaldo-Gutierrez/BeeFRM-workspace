       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPKCYA.
       AUTHOR.          CONSIST.
      * Programa de Carga de Archivo CYA Nuevo.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OLD-REQA==.
      *<<<< WSS
      *COPY GNSBRMSG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY COLBRCYA.
      *COPY COLBROLD.         COPIAR DENTRO DEL PROGRAMA.
      *    Cargos y Abonos
      *    ---------------
      *
      *    Nombre Registro : OLD
      *    Clave(s)        : OLD-KEY-IOLD(UU), OLD-KEY-IREG(UU)
      *    Largo           : 075
      *    Bloqueo         : 1
      *    Observaciones   :
      *
      *    02) Modificacion 24/06/88, LJV
      *    03) Ingreso-Modificacion 22/07/88, JSS
      *
       01  OLD.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OLD-GLS-FLAG                             PIC X(01).
      *
      *MOD-01  STR COD-ICOA
      *    Clave Primaria Registro
           03  OLD-KEY-IREG.
      *
      *        Identificacion Invocacion
               05  OLD-KEY-IINV.
      *
      *            Indicador Acceso Invocacion ( I/O )
                   07  OLD-IND-AINV                     PIC X(01).
      *
      *            Time Stamp Invocacion
                   07  OLD-STP-IINV.
      *                Fecha Invocacion
                       09  OLD-FEC-FINV.
                           11  OLD-NUM-SINV             PIC 9(02).
                           11  OLD-NUM-AINV             PIC 9(02).
                           11  OLD-NUM-MINV             PIC 9(02).
                           11  OLD-NUM-DINV             PIC 9(02).
      *
      *                Hora Invocacion
                       09  OLD-HRA-HRIN.
                           11  OLD-NUM-HHIN             PIC 9(02).
                           11  OLD-NUM-MMIN             PIC 9(02).
                           11  OLD-NUM-SSIN             PIC 9(02).
      *
      *            Codigo Origen Invocacion
                   07  OLD-COD-OINV.
      *
      *                Miscelaneo Tipo Origen
                       09  OLD-MSC-TINV                 PIC X(01).
      *
      *                Codigo Tipo Origen
                       09  OLD-COD-TINV                 PIC X(03).
      *
      *KEY     Identificacion Cargos y Abonos
               05  OLD-KEY-IOLD.
      *
      *            Identificacion Cancelacion
                   07  OLD-KEY-ICAN.
      *
      *                Cod. Int. Comp. Operacion Credito
                       09  OLD-CIC-IOPC.
      *
      *                    Centro Asignacion Identificacion
                           11  OLD-CAI-IOPC.
                               13  OLD-COD-TOPC         PIC X(01).
                               13  OLD-COD-COPC         PIC X(03).
      *
      *                    Ident. Interna Computacional
                           11  OLD-IIC-IOPC.
                               13  OLD-COD-OOPC.
                                   15  OLD-COD-SOPC     PIC X(02).
                                   15  OLD-IND-SOPC     PIC X(01).
                               13  OLD-GLS-NOPC         PIC X(05).
      *
      *                Num Ident. Cancelacion
                       09  OLD-NUM-ICAN                 PIC X(03).
      *
      *            Numero Identificacion Cargo o Abono
                   07  OLD-NUM-IOLD                     PIC 9(01).
      *
      *ELI-01
      *    Digito Verificador Operacion Credito
      *    03  OLD-VRF-IOPC                             PIC X(01).
      *
      *MOD-01  NOM IND-CAAB
      *MOD-01  PIC X(02)
      *REQ Tipo Cargo y Abono                           TAB TOLD
           03  OLD-IND-TOLD                             PIC X(05).
      *
      *REQ Cod. Int. Comp. Cuenta Cargo y Abono
           03  OLD-CIC-IOLD.
      *
      *        Centro Asignacion Identificacion
               05  OLD-CAI-IOLD.
                   07  OLD-COD-TOLD                     PIC X(01).
                   07  OLD-COD-COLD                     PIC X(03).
      *
      *        Ident. Interna Computacional
               05  OLD-IIC-IOLD.
                   07  OLD-COD-OOLD.
                       09  OLD-COD-SOLD                 PIC X(02).
                       09  OLD-IND-SOLD                 PIC X(01).
                   07  OLD-GLS-NOLD                     PIC X(05).
      *
      *    Digito Verificador Cuenta de Cargo y Abono
           03  OLD-VRF-IOLD                             PIC X(01).
      *
      *MOD-01  PIC 9(13)V9(05)
      *MOD-01  NOM VAL-LIQU
      *REQ Valor Liquido
           03  OLD-SGV-LIQU             COMP-3          PIC S9(11)V9(4).
      *
      *ING-02
      *    Indicador Movimiento Automatico
           03  OLD-IND-MAUT                             PIC X(01).
      *
      *ING-03
      *    Indicador registro Libre
           03  OLD-IND-FREE                             PIC X(01).
      *
      *MOD-01  PIC X(18)
      *MOD-02  PIC X(13)
      *MOD-03  PIC X(12)
      *    Glosa Disponible
           03  OLD-GLS-DISP                             PIC X(11).
       01  WSS-VARI.
           03 WSS-NUM-ROLD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RNEW           VALUE 0      PIC 9(05).
           03 WSS-NUM-RMOD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RBAD           VALUE 0      PIC 9(05).
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPKCYA' TO FIO-PROG.
           PERFORM BUS-FSIS.
      *    PERFORM OPEN-FILE.
           PERFORM LUP-PUT-REG.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           COPY GNSBGGBK.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       LUP-PUT-REG SECTION.
      *-------------------
       INI-LUP-REG.
      * Programacion de Ciclo para Registro OLD
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY REGISTROS EN ARCHIVO ANTIGUO '
               GO TO FIN-LUP-REG.
      *         MOVE 'COL'          TO MSG-COD-SIST
      *         MOVE 'CYA    CYANX' TO MSG-COD-MENS
      *         PERFORM GET-MSG
      *         DISPLAY MSG-GLS-DESC   CYA-KEY-ICYA IN CYA
       LUP-OLD-NEW.
      * Proceso de Seleccion y Clasificacion a Partir del Registro ANTIG
           ADD 1 TO WSS-NUM-ROLD IN WSS-VARI.
           PERFORM BUS-REG-NEW.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF FIO-STAT-OKS
               GO TO LUP-OLD-NEW.
       FIN-LUP-REG.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro NEW al Procesar Registro ANT
       BUS-REG-NEW SECTION.
       INI-BUS-REG-NEW.
      * Formacion del Registro NEW
           MOVE SPACES       TO CYA.
           MOVE OLD-KEY-IOLD TO CYA-KEY-ICYA.
           MOVE OLD-IND-TOLD TO CYA-IND-TCYA.
           MOVE OLD-CIC-IOLD TO CYA-CIC-ICYA.
           MOVE OLD-VRF-IOLD TO CYA-VRF-ICYA.
           MOVE OLD-SGV-LIQU TO CYA-SGV-LIQU.
           MOVE OLD-IND-MAUT TO CYA-IND-MAUT.
           MOVE OLD-IND-FREE TO CYA-IND-FREE.
      * Genera el Registro NEW
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-CYA
               ADD 1 TO WSS-NUM-RNEW IN WSS-VARI
           ELSE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-CYA
               ADD 1 TO WSS-NUM-RMOD IN WSS-VARI.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RBAD IN WSS-VARI
               DISPLAY 'REGISTRO ERRONEO : '  CYA.
       FIN-BUS-REG-NEW.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OLD LEIDOS   : ' WSS-NUM-ROLD IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-NUM-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS NEW GRABADOS : ' WSS-NUM-RNEW IN WSS-VARI.
           DISPLAY 'REGISTROS MODIFICADOS  : ' WSS-NUM-RMOD IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-OLD.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
      *COPY GNSBFMSG.
      *COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY COLBFCYA.
      *COPY COLBFOLD.            COPIAR DENTRO DEL PROGRAMA.
      * Modulo FIO para registro OLD en DATACOM, 16-SEP-1988 17:54:34.69
       COL-FIO-OLD SECTION.
       INI-COL-FIO-OLD.
           MOVE ADR-OLD-REQA TO ADR-REQA.
           MOVE 'CYA' TO ADR-TABL IN ADR-REQA.
           MOVE IFD-GSIS( 14 ) TO ADR-DBID IN ADR-REQA.
           MOVE 'CYA00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-OLD.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OLD-KEY-IOLD' OR FIO-AKEY NOT > SPACES )
               MOVE OLD-KEY-IOLD IN OLD TO ADR-VKEY IN ADR-REQA
               MOVE 'CYA00' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-OLD, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
       CON-COL-FIO-OLD.
           MOVE OLD TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OLD
           ELSE
               MOVE 'COLOLD' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-OLD-REQA.
       FIN-COL-FIO-OLD.
           EXIT.
