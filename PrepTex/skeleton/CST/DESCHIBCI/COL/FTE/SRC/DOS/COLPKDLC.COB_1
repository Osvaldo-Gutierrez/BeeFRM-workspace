       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPKDLC.
       AUTHOR.          CONSIST.
      * Programa de Carga de Archivo DLC Nuevo.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OLD-REQA==.
      *<<<< WSS
      *COPY GNSBRMSG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY COLBRDLC.
      *COPY COLBROLD.         COPIAR DENTRO DEL PROGRAMA.
      *    Documento Legal Credito
      *    -----------------------
      *
      *    Nombre Registro : OLD
      *    Clave(s)        : OLD-KEY-IOLD(UU), OLD-KEY-IREG(UU)
      *    Largo           : 194
      *    Bloqueo         : 2
      *    Observaciones   :
      *
       01  OLD.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OLD-GLS-FLAG                             PIC X(01).
      *
      *MOD-01  STR COD-IOLD
      *    Clave Primaria Registro
           03  OLD-KEY-IREG.
      *
      *        Identificacion Invocacion
               05  OLD-KEY-IINV.
      *
      *            Indicador Acceso Invocacion ( I/O )
                   07  OLD-IND-AINV                     PIC X(01).
      *
      *            Time Stamp Invocacion
                   07  OLD-STP-IINV.
      *                Fecha Invocacion
                       09  OLD-FEC-FINV.
                           11  OLD-NUM-SINV             PIC 9(02).
                           11  OLD-NUM-AINV             PIC 9(02).
                           11  OLD-NUM-MINV             PIC 9(02).
                           11  OLD-NUM-DINV             PIC 9(02).
      *
      *                Hora Invocacion
                       09  OLD-HRA-HRIN.
                           11  OLD-NUM-HHIN             PIC 9(02).
                           11  OLD-NUM-MMIN             PIC 9(02).
                           11  OLD-NUM-SSIN             PIC 9(02).
      *
      *            Codigo Origen Invocacion
                   07  OLD-COD-OINV.
      *
      *                Miscelaneo Tipo Origen
                       09  OLD-MSC-TINV                 PIC X(01).
      *
      *                Codigo Tipo Origen
                       09  OLD-COD-TINV                 PIC X(03).
      *
      *KEY     Identificacion Documento Legal
               05  OLD-KEY-IOLD.
      *
      *            Cod. Int. Comp. Operacion Credito
                   07  OLD-CIC-IOPC.
      *
      *                Centro Asignacion Identificacion
                       09  OLD-CAI-IOPC.
                           11  OLD-COD-TOPC             PIC X(01).
                           11  OLD-COD-COPC             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  OLD-IIC-IOPC.
                           11  OLD-COD-OOPC.
                               13  OLD-COD-SOPC         PIC X(02).
                               13  OLD-IND-SOPC         PIC X(01).
                           11  OLD-GLS-NOPC             PIC X(05).
      *
      *            Num Identificacion Documento Legal
                   07  OLD-NUM-IOLD                     PIC 9(03).
      *
      *ELI-01
      *    Digito Verificador Operacion Credito
      *    03  OLD-VRF-IOPC                             PIC X(01).
      *
      *MOD-01  PIC 9(13)V9(05)
      *REQ Valor Nominal Documento Legal
           03  OLD-VAL-NOLD             COMP-3          PIC 9(11)V9(04).
      *
      *ING-01
      *REQ Valor Saldo Documento Legal
           03  OLD-VAL-SOLD             COMP-3          PIC 9(11)V9(04).
      *
      *ELI-01
      *    Valor Saldo Capital
      *    03  OLD-VAL-SCAP             COMP-3          PIC 9(11)V9(04).
      *
      *ELI-01
      *    Valor Saldo Final
      *    03  OLD-VAL-SFIN             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-01  NOM COD-TOLD
      *REQ Codigo Tipo Documento                        TAB DOC
           03  OLD-COD-TDOC                             PIC X(03).
      *
      *MOD-01  NOM IND-nnnn
      *REQ Codigo Plazo ( Contable )                    TAB PZO
           03  OLD-COD-PLZO.
               05  OLD-MSC-TPZO                         PIC X(01).
               05  OLD-MSC-CPZO                         PIC X(01).
      *
      *MOD-01  PIC X(02)
      *    Tipo Impuestos                               COL TIMP
           03  OLD-MSC-TIMP                             PIC X(01).
      *
      *MOD-01  PIC 9(13)V9(05)
      *    Valor Impuestos
           03  OLD-VAL-IMPU             COMP-3          PIC 9(11)V9(04).
      *
      *    Codigo Notario Credito                       TAB IVB
           03  OLD-COD-NOTC.
               05  OLD-COD-INOT                         PIC X(03).
               05  OLD-COD-CNOT                         PIC X(03).
               05  OLD-COD-SNOT                         PIC X(03).
      *
      *MOD-01  PIC 9(13)V9(05)
      *    Valor Gastos Notario
           03  OLD-VAL-GNOT             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-01  COD-SCOB
      *MOD-01  PIC X(02)
      *    Situacion Cobranza                           COL SCBZ
           03  OLD-IND-SCBZ                             PIC X(05).
      *
      *MOD-01  IND-SCAR
      *MOD-01  PIC X(01)
      *REQ Situacion Cartera                            COL SCRT
           03  OLD-IND-SCRT                             PIC X(03).
      *
      *    Codigo Ubicacion Fisica                      TAB IVB
           03  OLD-COD-UBFD.
               05  OLD-COD-IUBF                         PIC X(03).
               05  OLD-COD-CUBF                         PIC X(03).
               05  OLD-COD-SUBF                         PIC X(03).
      *
      *REQ Codigo Plaza                                 TAB PZA
           03  OLD-COD-PLZA                             PIC X(04).
      *
      *ING-01
      *REQ Manejo Intereses Periodos Largos             COL MIPL
           03  OLD-MSC-MIPL                             PIC X(01).
      *
      *MOD-01  NOM COD-TCVE
      *MOD-01  PIC X(03)
      *    Tipo Calculo Vencimientos                    COL TCVE
           03  OLD-MSC-TCVE.
      *
      *ING-01
      *        Base Calculo Credito                     COL BCCR
               05  OLD-MSC-BCCR                         PIC X(01).
      *
      *ING-01
      *        Valores Informados Credito               COL VICR
               05  OLD-MSC-VICR                         PIC X(01).
      *
      *REQ Clasificacion Riesgo                         DEU RIES
           03  OLD-IND-RIES                             PIC X(02).
      *
      *MOD-01  PIC 9(13)V9(05)
      *PEN Valor Reajustes Devengados Documento Legal
           03  OLD-VAL-READ             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-01  PIC 9(13)V9(05)
      *PEN Valor Intereses Devengados Documento Legal
           03  OLD-VAL-INTD             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Fecha Devengo ( Ultimo Pago Total Intereses )
           03  OLD-FEC-FDEV.
               05  OLD-NUM-SDEV                         PIC 9(02).
               05  OLD-NUM-ADEV                         PIC 9(02).
               05  OLD-NUM-MDEV                         PIC 9(02).
               05  OLD-NUM-DDEV                         PIC 9(02).
      *
      *MOD-01  PIC 9(13)V9(05)
      *    Saldo Intereses
           03  OLD-VAL-SINT             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-01  PIC 9(13)V9(05)
      *    Saldo Capital por Amortizar
           03  OLD-VAL-SCPA             COMP-3          PIC 9(11)V9(04).
      *
      *    Fecha Amortizacion
           03  OLD-FEC-FAMT.
               05  OLD-NUM-SAMT                         PIC 9(02).
               05  OLD-NUM-AAMT                         PIC 9(02).
               05  OLD-NUM-MAMT                         PIC 9(02).
               05  OLD-NUM-DAMT                         PIC 9(02).
      *
      *ING-01
      *    Identificacion Documento Legal Original
           03  OLD-KEY-IDLO.
      *
      *        Cod. Int. Comp. Operacion Credito Original
               05  OLD-CIC-IOCO.
      *
      *            Centro Asignacion Identificacion
                   07  OLD-CAI-IOCO.
                       09  OLD-COD-TOCO                 PIC X(01).
                       09  OLD-COD-COCO                 PIC X(03).
      *
      *            Ident. Interna Computacional
                   07  OLD-IIC-IOCO.
                       09  OLD-COD-OOCO.
                           11  OLD-COD-SOCO             PIC X(02).
                           11  OLD-IND-SOCO             PIC X(01).
                       09  OLD-GLS-NOCO                 PIC X(05).
      *
      *        Num Ident Documento Legal Original
               05  OLD-NUM-IDLO                         PIC 9(03).
      *
      *MOD-01  PIC 9(13)V9(05)
      *    Monto Intereses Pendientes
           03  OLD-VAL-IPEN             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-01  PIC 9(13)V9(05)
      *REQ Valor Docto en Pesos (Cuadre Contabilidad)
           03  OLD-VAL-DPES             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-01  PIC X(34)
      *    Glosa Disponible
           03  OLD-GLS-DISP                             PIC X(07).
       01  WSS-VARI.
           03 WSS-NUM-ROLD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RNEW           VALUE 0      PIC 9(05).
           03 WSS-NUM-RMOD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RBAD           VALUE 0      PIC 9(05).
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPKDLC' TO FIO-PROG.
           PERFORM BUS-FSIS.
      *    PERFORM OPEN-FILE.
           PERFORM LUP-PUT-REG.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           COPY GNSBGGBK.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       LUP-PUT-REG SECTION.
      *-------------------
       INI-LUP-REG.
      * Programacion de Ciclo para Registro OLD
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY REGISTROS EN ARCHIVO ANTIGUO '
               GO TO FIN-LUP-REG.
      *         MOVE 'COL'          TO MSG-COD-SIST
      *         MOVE 'DLC    DLCNX' TO MSG-COD-MENS
      *         PERFORM GET-MSG
      *         DISPLAY MSG-GLS-DESC   DLC-KEY-IDLC IN DLC
       LUP-OLD-NEW.
      * Proceso de Seleccion y Clasificacion a Partir del Registro ANTIG
           ADD 1 TO WSS-NUM-ROLD IN WSS-VARI.
           PERFORM BUS-REG-NEW.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF FIO-STAT-OKS
               GO TO LUP-OLD-NEW.
       FIN-LUP-REG.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro NEW al Procesar Registro ANT
       BUS-REG-NEW SECTION.
       INI-BUS-REG-NEW.
      * Formacion del Registro NEW
           MOVE SPACES       TO DLC.
           MOVE OLD-KEY-IOLD TO DLC-KEY-IDLC.
           IF OLD-VAL-NOLD IS NOT NUMERIC
               DISPLAY 'NO NUMERICO NOLD ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-NOLD.
           MOVE OLD-VAL-NOLD TO DLC-VAL-NDLC.
           IF OLD-VAL-SOLD IS NOT NUMERIC
               DISPLAY 'NO NUMERICO SOLD ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-SOLD.
           MOVE OLD-VAL-SOLD TO DLC-VAL-SDLC.
           MOVE OLD-COD-TDOC TO DLC-COD-TDOC.
           MOVE OLD-COD-PLZO TO DLC-COD-PLZO.
           MOVE OLD-MSC-TIMP TO DLC-MSC-TIMP.
           IF OLD-VAL-IMPU IS NOT NUMERIC
               DISPLAY 'NO NUMERICO IMPU ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-IMPU.
           MOVE OLD-VAL-IMPU TO DLC-VAL-IMPU.
           MOVE OLD-COD-NOTC TO DLC-COD-NOTC.
           IF OLD-VAL-GNOT IS NOT NUMERIC
               DISPLAY 'NO NUMERICO GNOT ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-GNOT.
           MOVE OLD-VAL-GNOT TO DLC-VAL-GNOT.
           MOVE OLD-IND-SCBZ TO DLC-IND-SCBZ.
           MOVE OLD-IND-SCRT TO DLC-IND-SCRT.
           MOVE OLD-COD-UBFD TO DLC-COD-UBFD.
           MOVE OLD-COD-PLZA TO DLC-COD-PLZA.
           MOVE OLD-MSC-MIPL TO DLC-MSC-MIPL.
           MOVE OLD-IND-RIES TO DLC-IND-RIES.
           IF OLD-VAL-READ IS NOT NUMERIC
               DISPLAY 'NO NUMERICO READ ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-READ.
           MOVE OLD-VAL-READ TO DLC-VAL-READ.
           IF OLD-VAL-INTD IS NOT NUMERIC
               DISPLAY 'NO NUMERICO INTD ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-INTD.
           MOVE OLD-VAL-INTD TO DLC-VAL-INTD.
           MOVE OLD-FEC-FDEV TO DLC-FEC-FDEV.
           IF OLD-VAL-SINT IS NOT NUMERIC
               DISPLAY 'NO NUMERICO SINT ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-SINT.
           MOVE OLD-VAL-SINT TO DLC-VAL-SINT.
           IF OLD-VAL-SCPA IS NOT NUMERIC
               DISPLAY 'NO NUMERICO SCPA ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-SCPA.
           MOVE OLD-VAL-SCPA TO DLC-VAL-SCPA.
           MOVE OLD-FEC-FAMT TO DLC-FEC-FAMT.
           MOVE OLD-KEY-IDLO TO DLC-KEY-IDLO.
           IF OLD-VAL-IPEN IS NOT NUMERIC
               DISPLAY 'NO NUMERICO IPEN ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-IPEN.
           MOVE OLD-VAL-IPEN TO DLC-VAL-IPEN.
           IF OLD-VAL-DPES IS NOT NUMERIC
               DISPLAY 'NO NUMERICO DPES ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-VAL-DPES.
           MOVE OLD-VAL-DPES TO DLC-VAL-DPES.
      * Genera el Registro NEW
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-DLC
               ADD 1 TO WSS-NUM-RNEW IN WSS-VARI
           ELSE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-DLC
               ADD 1 TO WSS-NUM-RMOD IN WSS-VARI.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RBAD IN WSS-VARI
               DISPLAY 'REGISTRO ERRONEO : '  DLC.
       FIN-BUS-REG-NEW.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OLD LEIDOS   : ' WSS-NUM-ROLD IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-NUM-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS NEW GRABADOS : ' WSS-NUM-RNEW IN WSS-VARI.
           DISPLAY 'REGISTROS MODIFICADOS  : ' WSS-NUM-RMOD IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-OLD.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
      *COPY GNSBFMSG.
      *COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY COLBFDLC.
      *COPY COLBFOLD.            COPIAR DENTRO DEL PROGRAMA.
      * Modulo FIO para registro OLD en DATACOM, 16-SEP-1988 17:54:56.43
       COL-FIO-OLD SECTION.
       INI-COL-FIO-OLD.
           MOVE ADR-OLD-REQA TO ADR-REQA.
           MOVE 'DLC' TO ADR-TABL IN ADR-REQA.
           MOVE IFD-GSIS( 14 ) TO ADR-DBID IN ADR-REQA.
           MOVE 'DLC00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-OLD.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OLD-KEY-IOLD' OR FIO-AKEY NOT > SPACES )
               MOVE OLD-KEY-IOLD IN OLD TO ADR-VKEY IN ADR-REQA
               MOVE 'DLC00' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-OLD, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
       CON-COL-FIO-OLD.
           MOVE OLD TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OLD
           ELSE
               MOVE 'COLOLD' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-OLD-REQA.
       FIN-COL-FIO-OLD.
           EXIT.
