       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGD00.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    29-MAY-1992   ( Ultima Modificacion ).

      * Programa Generador de Archivo D00.

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CCT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AUX-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ITR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CRI-REQA==.
      *<<<< WSS
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY TABBRCAM.
       COPY TABBRTIO.
       COPY COLBROPC.
       COPY COLBRVEN.
       COPY COLBRDLC.
       COPY COLBRRCV.
       COPY COLBRCAN.
       COPY COLBRCCT.
       COPY COLBRROV.
       COPY COLBRTOC.
       COPY COLBRDCC.
       COPY COLBRAUX.
       COPY COLBRICG.
       COPY COLBRITR.
       COPY COLBRRDC.
       COPY COLBREVC.
       COPY SGCBRDBC.
       COPY DEUBRCRI.
       COPY SUPBRD00.
       COPY COLWGVEN.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993

       01  WSS-VARI.
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FREG.
              05 WSS-NUM-SREG                     PIC 9(02).
              05 WSS-NUM-AREG                     PIC 9(02).
              05 WSS-NUM-MREG                     PIC 9(02).
              05 WSS-NUM-DREG                     PIC 9(02).
           03 WSS-FEC-FIMV.
              05 WSS-NUM-DIMV                     PIC 9(02).
              05 WSS-NUM-MIMV                     PIC 9(02).
              05 WSS-NUM-SIMV                     PIC 9(02).
              05 WSS-NUM-AIMV                     PIC 9(02).
           03 WSS-TAB-TOPC.
              05 WSS-COD-TOPC      OCCURS 10      PIC X(01).
           03 WSS-TAB-RDC.
              05 TAB-RDC-DBC       OCCURS 9.
                 07 WSS-NUM-RCLI                  PIC X(09).
                 07 WSS-RED-ICLI REDEFINES WSS-NUM-RCLI.
                    09 FILLER                     PIC X(01).
                    09 WSS-NUM-ICLI               PIC X(08).
                 07 WSS-VRF-ICLI                  PIC X(01).
                 07 WSS-COD-TRDC                  PIC X(01).
           03 WSS-COD-NIID.
              05 WSS-CIC-OPCD                     PIC X(12).
              05 WSS-NUM-CLID                     PIC X(08).
      *        05 WSS-NUM-IDLC                     PIC 9(03).
           03 WSS-SGV-FCOL                        PIC S9(11)V9999.
           03 WSS-SGV-FREG                        PIC S9(11)V9999.
           03 WSS-SGV-FCAN                        PIC S9(11)V9999.
           03 WSS-SGV-FRCV                        PIC S9(11)V9999.
           03 WSS-SGV-FVEN                        PIC S9(11)V9999.
           03 WSS-SGV-FVE1                        PIC S9(11)V9999.
           03 WSS-SGV-FCLA                        PIC S9(11)V9999.
           03 WSS-SGV-DIFE                        PIC S9(11)V9999.
           03 WSS-VAL-CRED                        PIC 9(13).
           03 WSS-VAL-SCRE                        PIC 9(13).
           03 WSS-TAB-VEN.
              05 WSS-FEC-FVEN.
                 07 WSS-NUM-SVEN                  PIC 9(02).
                 07 WSS-NUM-AVEN                  PIC 9(02).
                 07 WSS-NUM-MVEN                  PIC 9(02).
                 07 WSS-NUM-DVEN                  PIC 9(02).
              05 WSS-FEC-FVE1.
                 07 WSS-NUM-SVE1                  PIC 9(02).
                 07 WSS-NUM-AVE1                  PIC 9(02).
                 07 WSS-NUM-MVE1                  PIC 9(02).
                 07 WSS-NUM-DVE1                  PIC 9(02).
              05 WSS-FEC-FCCT                     PIC X(08).
              05 WSS-FEC-FCIN.
                 07 WSS-NUM-SCIN                  PIC 9(02).
                 07 WSS-NUM-ACIN                  PIC 9(02).
                 07 WSS-NUM-MCIN                  PIC 9(02).
                 07 WSS-NUM-DCIN                  PIC 9(02).
              05 WSS-VAL-SOPC                     PIC 9(11)V9999.
              05 WSS-VAL-CAPI                     PIC 9(13).
              05 WSS-VAL-SCAP                     PIC 9(13).
              05 WSS-VAL-FINA                     PIC 9(13).
              05 WSS-VAL-SFIN                     PIC 9(13).
              05 WSS-VAL-IDNI                     PIC 9(13).
              05 WSS-VAL-ACAP                     PIC 9(13).
              05 WSS-VAL-PCAP                     PIC 9(13).
              05 WSS-VAL-TCAP                     PIC 9(13).
              05 WSS-VAL-SMO1                     PIC 9(13).
              05 WSS-VAL-SMO2                     PIC 9(13).
              05 WSS-VAL-SCVC                     PIC 9(13).
              05 WSS-VAL-VMES                     PIC 9(13).
              05 WSS-VAL-DMES                     PIC 9(13).
              05 WSS-VAL-PMES                     PIC 9(13).
              05 WSS-VAL-RMES                     PIC 9(13).
              05 WSS-VAL-INTS                     PIC 9(11)V9999.
              05 WSS-VAL-INTV                     PIC 9(11)V9999.
              05 WSS-VAL-INTE                     PIC 9(13).
              05 WSS-VAL-RRIE                     PIC 9(13).
              05 WSS-NUM-CAPV                     PIC 9(03).
              05 WSS-NUM-VCIM                     PIC 9(02).
              05 WSS-NUM-PPPR                     PIC 9(04).
              05 WSS-IND-SCTB                     PIC 9(01).
              05 WSS-IND-PRIM                     PIC 9(01).
              05 WSS-OLD-SCTB                     PIC X(03).
           03 WSS-TAB-DLC.
              05 WSS-FEC-FUPI.
                 07 WSS-NUM-SUPI                  PIC 9(02).
                 07 WSS-NUM-AUPI                  PIC 9(02).
                 07 WSS-NUM-MUPI                  PIC 9(02).
                 07 WSS-NUM-DUPI                  PIC 9(02).
              05 WSS-VAL-INTD                     PIC 9(13).
              05 WSS-VAL-READ                     PIC 9(13).
              05 WSS-VAL-IPCO                     PIC 9(13).
              05 WSS-VAL-RDEV                     PIC S9(13).
              05 WSS-VAL-IPCV                     PIC 9(13).
              05 WSS-VAL-IPCR                     PIC 9(13).
              05 WSS-VAL-RDER                     PIC S9(13).
              05 WSS-VAL-UIPA                     PIC 9(13)V99.
              05 WSS-VAL-UINT                     PIC 9(13)V99.
              05 WSS-VAL-UINP                     PIC 9(13).
           03 WSS-GLS-DAT3.
              05 FILLER                           PIC X(15).
              05 WSS-IND-OMIT                     PIC X(01).
              05 FILLER                           PIC X(14).
           03 WSS-VAL-TEQV                        PIC 9(03)V9(02).
           03 WSS-VAL-SDLC                        PIC 9(13).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-IND-RENO                        PIC 9(01).
           03 WSS-IND-RCV0                        PIC 9(01).
           03 WSS-COD-PESO                        PIC X(06).
           03 WSS-IND-MNAC                        PIC 9(01).
           03 WSS-KEY-IDLC                        PIC X(15).
           03 WSS-IND-ROPC         VALUE 0        PIC 9(07).
           03 WSS-IND-RDLC         VALUE 0        PIC 9(07).
           03 WSS-IND-RDCC         VALUE 0        PIC 9(07).
           03 WSS-IND-RICG         VALUE 0        PIC 9(07).
           03 WSS-IND-RRDC         VALUE 0        PIC 9(07).
           03 WSS-IND-RVEN         VALUE 0        PIC 9(07).
           03 WSS-IND-RPRO         VALUE 0        PIC 9(07).
           03 WSS-IND-RPUT         VALUE 0        PIC 9(07).
           03 WSS-IND-RERR         VALUE 0        PIC 9(07).
           03 WSS-IND-RCAN         VALUE 0        PIC 9(07).
           03 WSS-RDC              VALUE 0        PIC 9(02).
           03 WSS-I                VALUE 0        PIC 9(02).
           03 WSS-CIC-IOPC                        PIC X(12).
           03 WSS-GLS-MENS                        PIC X(40).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGD00' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM PARAM-D00.
           PERFORM LUP-REG-D00.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY TABBFCAM.
       COPY COLBFOPC.
       COPY COLBFVEN.
       COPY COLBFDLC.
       COPY COLBFRCV.
       COPY COLBFCAN.
       COPY COLBFCCT.
       COPY COLBFROV.
       COPY COLBFTOC.
       COPY COLBFDCC.
       COPY COLBFAUX.
       COPY COLBFICG.
       COPY COLBFITR.
       COPY COLBFRDC.
       COPY COLBFEVC.
       COPY SGCBFDBC.
       COPY GNSBPFEC.
       COPY DEUBFCRI.
      * COPY SUPIOD00.
       SUP-FIO-D00 SECTION.
       INI-SUP-FIO-D00.
      * Modulo FIO Batch para archivo VSAM SUPD00,  3-JUL-1990 17:31:40.
      * "Archivo D00 para COLOCACIONES ACCESO DINAMICO (NO SEQ)"
           CALL 'SUPPFI00' USING FIO-VARI D00.
       FIN-SUP-FIO-D00.
           EXIT.
       COPY COLBGVEN.
       COPY SGCBGCLI.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGPES.
      *SMC-INI 09.09.1993
       COPY COLBGINT.
      *SMC-FIN 09.09.1993
       COPY GNSBGEND.
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
       COPY GNSBGABT.
      *DAD fin  29-MAY-1992
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CCT.
           PERFORM COL-FIO-ROV.
           PERFORM COL-FIO-TOC.
           PERFORM COL-FIO-DCC.
           PERFORM COL-FIO-AUX.
           PERFORM COL-FIO-ICG.
           PERFORM COL-FIO-ITR.
           PERFORM COL-FIO-RDC.
           PERFORM COL-FIO-EVC.
           PERFORM SGC-FIO-DBC.
           PERFORM DEU-FIO-CRI.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM SUP-FIO-D00.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       PARAM-D00 SECTION.
      *-----------------
       INI-PARAM-D00.
      * Rutina para Capturar y Validar Parametros de Rango de Fechas
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      *Captura Fecha de Proceso
           DISPLAY 'FECHA DE PROCESO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Indicador Sistemas a Excluir
      *     MOVE SPACES   TO WSS-TAB-TOPC IN WSS-VARI.
      *     PERFORM PRO-SIS-EXT VARYING WSS-I FROM 1 BY 1 UNTIL
      *                                 WSS-I IN WSS-VARI > 10.
           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DREG IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MREG IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SREG IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AREG IN WSS-VARI.

      *VAL-MSC Valida indicador ( miscelaneo ) en TABLAS
      *     IF WSS-COD-TOPC IN WSS-VARI(WSS-I) NOT > SPACES
      *         MOVE '*' TO WSS-COD-TOPC IN WSS-VARI(WSS-I).
      *     IF WSS-COD-TOPC IN WSS-VARI(WSS-I) NOT = '*'
      *         DISPLAY WSS-COD-TOPC IN WSS-VARI(WSS-I)
      *         MOVE 'TAB'  TO MSC-COD-SIST
      *         MOVE 'TCYA' TO MSC-COD-TMSC IN MSC
      *         MOVE WSS-COD-TOPC IN WSS-VARI(WSS-I) TO MSC-COD-CMSC IN
      *         PERFORM VAL-MSC
      *         IF MSG-COD-MENS > SPACES
      *             DISPLAY MSG-GLS-DESC IN MSG
      *             PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Obtiene Codigo Valor de Cambio Mediante CIC de Moneda Nacional
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
       FIN-PARAM-D00.
           EXIT.
      * PRO-SIS-EXT SECTION.
      * INI-PRO-SIS-EXT.
      *     DISPLAY 'SISTEMAS A EXCLUIR : '.
      *     ACCEPT  WSS-COD-TOPC IN WSS-VARI(WSS-I).
      *     DISPLAY WSS-COD-TOPC IN WSS-VARI(WSS-I).
      *     IF WSS-COD-TOPC IN WSS-VARI(WSS-I) NOT > SPACES
      *         MOVE 11 TO WSS-I IN WSS-VARI.
      * FIN-PRO-SIS-EXT.
      *     EXIT.
      ******************************************************************
       LUP-REG-D00 SECTION.
      *-------------------
      * Programacion de Ciclo para Registros OPC
       INI-LUP-OPC-D00.
      * Lectura Inicial de Operaciones de Credito
           MOVE ZEROES TO OPC-FEC-FCOL IN OPC.
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FREG IN WSS-VARI
               DISPLAY 'NO HAY REGISTRO OPC HASTA FECHA DE PROCESO : '
                       WSS-FEC-FINI IN WSS-VARI
               GO TO FIN-LUP-REG-D00.
      *     PERFORM BUS-TAB-PRO.
       LUP-OPC-D00.
      * Seleccion para ser Procesado del Registro OPC leido
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-OPC-D00.
           ADD  1  TO WSS-IND-ROPC IN WSS-VARI.

      * Mediante el Registro TOC Tipo Operaciones Creditos Obtiene 'Como
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           IF NOT FIO-STAT-OKS
               MOVE 'GNS'        TO MSG-COD-SIST
               MOVE 'COD    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' : ' TOC-COD-TABL '/KEY : '
                       OPC-CIC-IOPC IN OPC
               GO TO NXT-OPC-D00.

      * Obtiene Valor de Cambio en Pesos a Fecha Colocacion
           IF OPC-COD-VCOC IN OPC NOT = WSS-COD-PESO IN WSS-VARI
               MOVE OPC-FEC-FCOL IN OPC      TO CAM-FEC-FCAM IN CAM(1)
               MOVE OPC-FEC-FCOL IN OPC      TO CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR DE CAMBIO PARA MONEDA : '
                            OPC-COD-VCOC IN OPC
                           ' A FECHA COLOCACION : ' OPC-FEC-FCOL IN OPC
                           ' DE OPERACION : '       OPC-CIC-IOPC IN OPC
                   GO TO NXT-OPC-D00
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO PARA MONEDA : '
                                OPC-COD-VCOC IN OPC
                          ' A FECHA COLOCACION : ' OPC-FEC-FCOL IN OPC
                          ' DE OPERACION : '       OPC-CIC-IOPC IN OPC
                       GO TO NXT-OPC-D00
                   ELSE
                       MOVE  1  TO WSS-IND-MNAC IN WSS-VARI
                       MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FCOL
                                                         IN WSS-VARI
           ELSE
               MOVE  0  TO WSS-IND-MNAC IN WSS-VARI.

      * Tranforma a Pesos Valores de la Operacion
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MULTIPLY OPC-VAL-CRED IN OPC BY WSS-SGV-FCOL IN WSS-VARI
                        GIVING WSS-VAL-CRED IN WSS-VARI ROUNDED
               MULTIPLY OPC-VAL-SCRE IN OPC BY WSS-SGV-FCOL IN WSS-VARI
                        GIVING WSS-VAL-SCRE IN WSS-VARI ROUNDED
           ELSE
               MOVE OPC-VAL-CRED IN OPC TO WSS-VAL-CRED IN WSS-VARI
               MOVE OPC-VAL-SCRE IN OPC TO WSS-VAL-SCRE IN WSS-VARI.

      * Obtiene Valor de Cambio en Pesos a Fecha Proceso
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MOVE WSS-FEC-FREG IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1)
               MOVE WSS-FEC-FREG IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR DE CAMBIO PARA MONEDA : '
                            OPC-COD-VCOC IN OPC
                          ' A FECHA PROCESO : ' WSS-FEC-FINI IN WSS-VARI
                           ' EN OPERACION : '   OPC-CIC-IOPC IN OPC
                   GO TO NXT-OPC-D00
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO PARA MONEDA : '
                                OPC-COD-VCOC IN OPC
                          ' A FECHA PROCESO : ' WSS-FEC-FINI IN WSS-VARI
                          ' EN OPERACION : '   OPC-CIC-IOPC IN OPC
                       GO TO NXT-OPC-D00
                   ELSE
                       MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FREG
                                                            IN WSS-VARI.

      * Obtiene Informacion de los Deudores Directos e Indirectos de la
      * Operacion y Clasificacion de Riesgo del Deudor Directo
           MOVE  1     TO WSS-RDC      IN WSS-VARI.
           MOVE  5     TO WSS-IND-NEXT IN WSS-VARI.
           MOVE ZEROES TO WSS-TAB-RDC  IN WSS-VARI.
           MOVE OPC-CIC-IOPC IN OPC TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           PERFORM BUS-RDC-DBC.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-OPC-D00.

      * Busca Documento # 4 de Operacion de Credito
           MOVE ZEROES              TO WSS-VAL-SDLC IN WSS-VARI.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE 4                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF FIO-STAT-OKS
               IF WSS-IND-MNAC IN WSS-VARI = 1
                   MULTIPLY DLC-VAL-SDLC IN DLC BY WSS-SGV-FREG
                                                             IN WSS-VARI
                               GIVING WSS-VAL-SDLC IN WSS-VARI ROUNDED
               ELSE
                   MOVE DLC-VAL-SDLC IN DLC TO WSS-VAL-SDLC IN WSS-VARI.

      * Proceso para Documentos de la Operacion de Credito
           PERFORM BUS-REG-DLC.
       NXT-OPC-D00.
      * Lectura de Siguiente Operacion de Credito
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FREG IN WSS-VARI
               GO TO FIN-LUP-REG-D00.
           GO TO LUP-OPC-D00.
       FIN-LUP-REG-D00.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OPC LEIDOS  : ' WSS-IND-ROPC IN WSS-VARI.
           DISPLAY 'REGISTROS VEN LEIDOS  : ' WSS-IND-RVEN IN WSS-VARI.
           DISPLAY 'REGISTROS DLC LEIDOS  : ' WSS-IND-RDLC IN WSS-VARI.
           DISPLAY 'REGISTROS DCC LEIDOS  : ' WSS-IND-RDCC IN WSS-VARI.
           DISPLAY 'REGISTROS ICG LEIDOS  : ' WSS-IND-RICG IN WSS-VARI.
           DISPLAY 'REGISTROS RDC LEIDOS  : ' WSS-IND-RRDC IN WSS-VARI.
           DISPLAY 'REGISTROS PROCESADOS  : ' WSS-IND-RPRO IN WSS-VARI.
           DISPLAY 'REGISTROS GRABADOS    : ' WSS-IND-RPUT IN WSS-VARI.
           DISPLAY 'REGISTROS ERRONEOS    : ' WSS-IND-RERR IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-TOC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CCT.
           PERFORM COL-FIO-ROV.
           PERFORM COL-FIO-DCC.
           PERFORM COL-FIO-AUX.
           PERFORM COL-FIO-ICG.
           PERFORM COL-FIO-ITR.
           PERFORM COL-FIO-RDC.
           PERFORM SGC-FIO-DBC.
           PERFORM DEU-FIO-CRI.
           PERFORM SUP-FIO-D00.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
      *<<<< EOF
      ******************************************************************
      * Seleccion para ser Procesado del Registro OPC leido
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

      * Exclusion de la Operacion NO Activada
           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO FIN-SEL-REG-PRO.

      * Mediante BUS-TAB Busca Sistema Valido en Tabla TIO de la Operaci
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'TIO'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-TOOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY MSG-GLS-DESC ' : ' TAB-COD-TABL
               GO TO FIN-SEL-REG-PRO.

      * Exclusion de la Operacion con Sistema Valido distinto de COLocac
           MOVE TAB TO TIO.
           IF TIO-GLS-SIST IN TIO NOT = 'COL'
               GO TO FIN-SEL-REG-PRO.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.

      *     MOVE  5  TO WSS-IND-NEXT IN WSS-VARI.
      *     PERFORM PRO-EXT-SIS VARYING WSS-I FROM 1 BY 1 UNTIL
      *                  WSS-I > 10 OR WSS-COD-TOPC(WSS-I) NOT > SPACES.
      *     IF WSS-IND-NEXT IN WSS-VARI = 5
      *         MOVE OPC-CIC-IOPC IN OPC TO WSS-CIC-IOPC IN WSS-VARI
      *         MOVE  0                  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      * PRO-EXT-SIS SECTION.
      * INI-PRO-EXT-SIS.
      *     IF OPC-COD-TOPC IN OPC = WSS-COD-TOPC IN WSS-VARI(WSS-I)
      *         MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
      *         MOVE  11 TO WSS-I        IN WSS-VARI.
      * FIN-PRO-EXT-SIS.
      *     EXIT.
      ******************************************************************
      * Busca CIC del Cliente con el CIC de Operaciones de Creditos en R
      * RDC-NUM-IDLC = 0 ====> DEU.DIR.
      * RDC-NUM-IDLC > 0 ====> DEU.IND.
       BUS-RDC-DBC SECTION.
       INI-BUS-RDC-DBC.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-RDC-DBC.
           IF WSS-RDC IN WSS-VARI > 9
               GO TO FIN-BUS-RDC-DBC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS AND
              WSS-IND-NEXT IN WSS-VARI = 5
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    RDCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC '/KEY : ' RDC-KEY-IDLC IN RDC
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-BUS-RDC-DBC.
           IF NOT FIO-STAT-OKS OR
              RDC-CIC-IOPC IN RDC NOT = OPC-CIC-IOPC IN OPC
               GO TO FIN-BUS-RDC-DBC.

      *CIC-CLI Busca Datos del Cliente Mediante su CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS > SPACES
               MOVE SPACES TO DBC-IDC-ICLI IN DBC
               MOVE  1     TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-BUS-RDC-DBC.
           IF WSS-IND-NEXT IN WSS-VARI = 5
               MOVE OPC-CIC-IOPC IN OPC TO WSS-CIC-OPCD IN WSS-VARI
      *ALD     MOVE DBC-NUM-ICLI IN DBC TO WSS-NUM-CLID IN WSS-VARI
               MOVE SPACES              TO WSS-NUM-CLID IN WSS-VARI
               PERFORM BUS-DBC-CRI
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-BUS-RDC-DBC.

      *ALD     GO TO NXT-RDC-DBC.
      *     MOVE RDC-COD-TRDC IN RDC TO WSS-COD-TRDC IN WSS-VARI(WSS-I).
           IF RDC-NUM-IDLC IN RDC = 0
               MOVE 'D' TO WSS-COD-TRDC IN WSS-VARI(WSS-RDC)
           ELSE
               MOVE 'I' TO WSS-COD-TRDC IN WSS-VARI(WSS-RDC).
           MOVE DBC-NUM-ICLI IN DBC TO WSS-NUM-ICLI IN
                                           WSS-VARI(WSS-RDC).
           MOVE DBC-VRF-ICLI IN DBC TO WSS-VRF-ICLI IN
                                           WSS-VARI(WSS-RDC).
           ADD   1  TO WSS-RDC      IN WSS-VARI.
       NXT-RDC-DBC.
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           ADD   1  TO WSS-IND-RRDC IN WSS-VARI.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-RDC-DBC.
       FIN-BUS-RDC-DBC.
           EXIT.
      *-----------------------------------------------------------------
       BUS-DBC-CRI SECTION.
       INI-BUS-DBC-CRI.
      * Busca Clasificacion de Riesgo Comercial del Cliente
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           MOVE DBC-CIC-ICLI IN DBC TO CRI-CIC-ICLI IN CRI.
           MOVE 'C'                 TO CRI-IND-TCAR IN CRI.
           MOVE '99999999'          TO CRI-FEC-FCLA IN CRI.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM DEU-FIO-CRI.
           IF NOT FIO-STAT-OKS OR
              CRI-CIC-ICLI IN CRI NOT = DBC-CIC-ICLI IN DBC
               MOVE SPACES TO CRI
               GO TO FIN-BUS-DBC-CRI.

      * Obtiene Valor de Cambio en Pesos a Fecha Clasificacion Riesgo
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MOVE CRI-FEC-FCLA IN CRI      TO CAM-FEC-FCAM IN CAM(1)
               MOVE CRI-FEC-FCLA IN CRI      TO CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR DE CAMBIO PARA MONEDA : '
                            OPC-COD-VCOC IN OPC
                           ' A FECHA CLAS.RIESGO : ' CRI-FEC-FCLA IN CRI
                           ' EN OPERACION : '       OPC-CIC-IOPC IN OPC
                   MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO PARA MONEDA : '
                                OPC-COD-VCOC IN OPC
                          ' A FECHA CLAS.RIESGO : ' CRI-FEC-FCLA IN CRI
                          ' EN OPERACION : '       OPC-CIC-IOPC IN OPC
                       MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                   ELSE
                       MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FCLA
                                                          IN WSS-VARI.
       FIN-BUS-DBC-CRI.
           EXIT.
      ******************************************************************
      * Proceso para Documentos de la Operacion de Credito
       BUS-REG-DLC SECTION.
       INI-BUS-REG-DLC.
      * Lectura Inicial de Documentos de la Operacion de Credito
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE ZEROES              TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS OR
              DLC-CIC-IOPC IN DLC NOT = OPC-CIC-IOPC IN OPC
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    DLCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   OPC-CIC-IOPC IN OPC
               GO TO FIN-BUS-REG-DLC.
       LUP-BUS-REG-DLC.
      * Procesa Documento de la Operacion de Credito
           ADD   1     TO WSS-IND-RDLC IN WSS-VARI.
           MOVE ZEROES TO WSS-TAB-DLC  IN WSS-VARI.

      * Tranforma a Pesos Valores del Documento
           IF WSS-IND-MNAC IN WSS-VARI = 1 AND
              OPC-MSC-TVOC IN OPC NOT = '0'
               MULTIPLY DLC-VAL-INTD IN DLC BY WSS-SGV-FREG IN WSS-VARI
                              GIVING WSS-VAL-INTD IN WSS-VARI ROUNDED
               MULTIPLY DLC-VAL-READ IN DLC BY WSS-SGV-FREG IN WSS-VARI
                              GIVING WSS-VAL-READ IN WSS-VARI ROUNDED
           ELSE
               MOVE DLC-VAL-INTD IN DLC TO WSS-VAL-INTD IN WSS-VARI
               MOVE DLC-VAL-READ IN DLC TO WSS-VAL-READ IN WSS-VARI.

      * Busca Ultimo Interes Pagado en el Mes
           PERFORM BUS-DLC-RCV.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-BUS-REG-DLC.

      * Busqueda de Registro DCC
           PERFORM BUS-REG-DCC.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-BUS-REG-DLC.

      * Busqueda de Registro ICG
           PERFORM BUS-REG-ICG.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-BUS-REG-DLC.

      * Conversion de Tasa de Interes a Base 30 dias o Anual (360)
           PERFORM CAL-TAS-EQV.
           IF INT-CMSG > SPACES
               GO TO FIN-BUS-REG-DLC.

      * Proceso para Vencimientos de la Operacion de Credito
           MOVE ZEROES TO WSS-TAB-VEN IN WSS-VARI.
           PERFORM BUS-REG-VEN.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-BUS-REG-DLC.

      * Determina Intereses por Cobrar
           SUBTRACT WSS-VAL-IPCR IN WSS-VARI FROM
                    WSS-VAL-INTD IN WSS-VARI GIVING
                    WSS-VAL-IPCO IN WSS-VARI.

      * Determina Reajustes Devengados
           SUBTRACT WSS-VAL-RDER IN WSS-VARI FROM
                    WSS-VAL-READ IN WSS-VARI GIVING
                    WSS-VAL-RDEV IN WSS-VARI.

      * Calculo Final Promedio Ponderado Residual
           IF WSS-VAL-ACAP IN WSS-VARI > ZEROES
               DIVIDE WSS-VAL-TCAP IN WSS-VARI BY
                                               WSS-VAL-ACAP IN WSS-VARI
                                GIVING WSS-NUM-PPPR IN WSS-VARI ROUNDED
           ELSE
               MOVE ZEROES TO WSS-NUM-PPPR IN WSS-VARI.

      * Formacion y Generacion del Registro D00 al Procesar Varios Regis
           PERFORM GEN-REG-D00.
       NXT-BUS-REG-DLC.
      * Lectura de Siguiente Documento de la Operacion de Credito
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS OR
              DLC-CIC-IOPC IN DLC NOT = OPC-CIC-IOPC IN OPC
               MOVE SPACES TO DLC
               GO TO FIN-BUS-REG-DLC.
           GO TO LUP-BUS-REG-DLC.
       FIN-BUS-REG-DLC.
           EXIT.
      ******************************************************************
      *-----------------------------------------------------------------
       BUS-DLC-RCV SECTION.
       INI-BUS-DLC-RCV.
      * Busca Ultima Cancelacion en el Periodo para la Operacion de Cred
           MOVE OPC-CIC-IOPC IN OPC TO CAN-CIC-IOPC IN CAN.
           MOVE 999                 TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
       NXT-BUS-DLC-RCV.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS OR
              CAN-CIC-IOPC IN CAN NOT = OPC-CIC-IOPC IN OPC
               GO TO FIN-BUS-DLC-RCV.

           IF CAN-NUM-MCAN IN CAN NOT = WSS-NUM-MINI IN WSS-VARI OR
              CAN-NUM-SCAN IN CAN NOT = WSS-NUM-SINI IN WSS-VARI OR
              CAN-NUM-ACAN IN CAN NOT = WSS-NUM-AINI IN WSS-VARI
               MOVE FIO-GET-PRV TO FIO-CMND
               GO TO NXT-BUS-DLC-RCV.

      * Fecha Ultimo Interes Pagado en el Mes
           MOVE CAN-FEC-FCAN IN CAN TO WSS-FEC-FUPI IN WSS-VARI.

      * Obtiene Valor de Cambio en Pesos a Fecha Cancelacion
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MOVE CAN-FEC-FCAN IN CAN      TO CAM-FEC-FCAM IN CAM(1)
               MOVE CAN-FEC-FCAN IN CAN      TO CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR DE CAMBIO PARA MONEDA : '
                            OPC-COD-VCOC IN OPC
                          ' A FECHA CANCELACION : ' CAN-FEC-FCAN IN CAN
                           ' EN CANCELACION : '     CAN-KEY-ICAN IN CAN
                   MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                   GO TO FIN-BUS-DLC-RCV
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO PARA MONEDA : '
                                OPC-COD-VCOC IN OPC
                          ' A FECHA CANCELACION : ' CAN-FEC-FCAN IN CAN
                          ' EN CANCELACION : '      CAN-KEY-ICAN IN CAN
                       MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                       GO TO FIN-BUS-DLC-RCV
                   ELSE
                       MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FCAN
                                                          IN WSS-VARI.

      * Busca Ultimo Interes Pagado en el Mes
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.
           MOVE CAN-KEY-ICAN IN CAN TO RCV-KEY-ICAN IN RCV
                                       RCV-KEY-IVCT IN RCV.
           MOVE  0                  TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS OR
              RCV-KEY-ICAN IN RCV NOT = CAN-KEY-ICAN IN CAN
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CAN    RCVNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CAN-KEY-ICAN IN CAN
               MOVE SPACES TO RCV
               GO TO FIN-BUS-DLC-RCV.
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       NXT-BUS-CAN-RCV.
           SUBTRACT RCV-SGV-CAPI IN RCV FROM RCV-SGV-FINA IN RCV
                        GIVING WSS-VAL-UIPA IN WSS-VARI.
           ADD WSS-VAL-UIPA IN WSS-VARI TO WSS-VAL-UINT IN WSS-VARI.
           ADD RCV-SGV-INTE IN RCV      TO WSS-VAL-UINT IN WSS-VARI.
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MULTIPLY WSS-VAL-UINT IN WSS-VARI BY WSS-SGV-FCAN
                                     IN WSS-VARI GIVING
                        WSS-VAL-UINP IN WSS-VARI ROUNDED
           ELSE
              MOVE WSS-VAL-UINT IN WSS-VARI TO WSS-VAL-UINP IN WSS-VARI.
           IF RCV-NUM-IVCT IN RCV = 0
               GO TO FIN-BUS-DLC-RCV.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS OR
              RCV-KEY-ICAN IN RCV NOT = CAN-KEY-ICAN IN CAN
               GO TO FIN-BUS-DLC-RCV.
           GO TO NXT-BUS-CAN-RCV.
       FIN-BUS-DLC-RCV.
           EXIT.
      *-----------------------------------------------------------------
       BUS-REG-DCC SECTION.
       INI-BUS-REG-DCC.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

      * Forma Llave COD-CCTB para Leer DCC
           MOVE 'CAP'               TO DCC-MSC-TCCT IN DCC.
           MOVE OPC-COD-VCOC IN OPC TO DCC-COD-VCAM IN DCC.
           MOVE OPC-COD-TOOC IN OPC TO DCC-COD-TOCR IN DCC.
           MOVE DLC-COD-PLZO IN DLC TO DCC-COD-PLZO IN DCC.
           MOVE DLC-IND-SCRT IN DLC TO DCC-IND-SCRT IN DCC.
           MOVE SPACES              TO DCC-IND-SCTB IN DCC.
           MOVE DLC-IND-SCBZ IN DLC TO DCC-IND-SCBZ IN DCC.

      * Con BUS-TAB Busca Estado de IND-PCTB de Tabla AUX para Completar
           MOVE 'COL'               TO TAB-COD-SIST.
           MOVE 'AUX'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-COOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC ' :' TAB-COD-TABL IN TAB '/KEY : '
                      OPC-CIC-IOPC IN OPC
               GO TO FIN-BUS-REG-DCC.
           MOVE TAB TO AUX.
           IF AUX-IND-PCTB NOT = 'N'
               MOVE OPC-COD-AOOC IN OPC TO DCC-COD-AOCR IN DCC
           ELSE
               MOVE SPACES TO DCC-COD-AOCR IN DCC.

      * Obtiene DCC al leer por Llave COD-CCTB Formada
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           IF NOT FIO-STAT-OKS
               MOVE 'GNS'        TO MSG-COD-SIST
               MOVE 'COD    NEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' : ' DCC-COD-TABL IN DCC '/KEY :'
                       OPC-CIC-IOPC IN OPC
               GO TO FIN-BUS-REG-DCC.
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           ADD   1  TO WSS-IND-RDCC IN WSS-VARI.
       FIN-BUS-REG-DCC.
           EXIT.
      *-----------------------------------------------------------------
       BUS-REG-ICG SECTION.
       INI-BUS-REG-ICG.
      * Busqueda de Registro ICG
           MOVE  5  TO WSS-IND-NEXT IN WSS-VARI.

      * Forma Llave KEY-IICG para Leer ICG
           MOVE SPACES TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'  TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES TO ICG-FEC-FINI IN ICG.

      * Lectura de registro ICG segun Opera Como
           IF TOC-MSC-OPEC IN TOC = 'D'
               PERFORM GET-ICG-DLC
           ELSE
               PERFORM GET-ICG-OPC.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               DISPLAY 'REGISTRO OPC NO TIENE INTERESES ' OPC-KEY-IOPC.
       FIN-BUS-REG-ICG.
           EXIT.

       GET-ICG-DLC SECTION.
       INI-GET-ICG-DLC.
      * Completa con Registro DLC Llave KEY-IICG para Leer y Obtener ICG
           MOVE DLC-KEY-IDLC IN DLC TO ICG-KEY-ICAN IN ICG.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       LUP-ICG-DLC.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-KEY-ICAN IN ICG = DLC-KEY-IDLC IN DLC AND
                    ICG-CIC-IOPV IN ICG = SPACES              AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               PERFORM GET-ICG-OPC
               GO TO FIN-GET-ICG-DLC.
           IF ICG-FEC-FTER IN ICG NOT > ZEROES
               PERFORM GET-ICG-ITR.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GET-ICG-DLC.
           IF ICG-FEC-FINI IN ICG = ZEROES
               MOVE OPC-FEC-FCOL IN OPC TO ICG-FEC-FINI IN ICG.
           IF WSS-FEC-FREG IN WSS-VARI NOT < ICG-FEC-FINI IN ICG AND
              WSS-FEC-FREG IN WSS-VARI NOT > ICG-FEC-FTER IN ICG
               MOVE  0  TO WSS-IND-NEXT IN WSS-VARI
               ADD   1  TO WSS-IND-RICG IN WSS-VARI
               GO TO FIN-GET-ICG-DLC
           ELSE
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-ICG-DLC.
       FIN-GET-ICG-DLC.
           EXIT.

       GET-ICG-OPC SECTION.
       INI-GET-ICG-OPC.
      * Completa con Registro OPC Llave KEY-IICG para Leer y Obtener ICG
           MOVE OPC-CIC-IOPC IN OPC TO ICG-CIC-IOPC IN ICG.
           MOVE ZEROES              TO ICG-NUM-ICAN IN ICG.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       LUP-ICG-OPC.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-CIC-IOPC IN ICG = OPC-CIC-IOPC IN OPC AND
                    ICG-NUM-ICAN IN ICG = ZEROES              AND
                    ICG-CIC-IOPV IN ICG = SPACES              AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    ICGNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC '/KEY : ' ICG-KEY-ICAN IN ICG
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-GET-ICG-OPC.
           IF ICG-FEC-FTER IN ICG NOT > ZEROES
               PERFORM GET-ICG-ITR.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GET-ICG-OPC.
           IF ICG-FEC-FINI IN ICG = ZEROES
               MOVE OPC-FEC-FCOL IN OPC TO ICG-FEC-FINI IN ICG.
           IF WSS-FEC-FREG IN WSS-VARI NOT < ICG-FEC-FINI IN ICG AND
              WSS-FEC-FREG IN WSS-VARI NOT > ICG-FEC-FTER IN ICG
               MOVE  0  TO WSS-IND-NEXT IN WSS-VARI
               ADD   1  TO WSS-IND-RICG IN WSS-VARI
               GO TO FIN-GET-ICG-OPC
           ELSE
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-ICG-OPC.
       FIN-GET-ICG-OPC.
           EXIT.

       GET-ICG-ITR SECTION.
       INI-GET-ICG-ITR.
      * Completa con Registro DLC Llave KEY-IICG para Leer y Obtener ICG
           MOVE ICG-KEY-IICG IN ICG TO ITR-KEY-IICG IN ITR.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM COL-FIO-ITR.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-KEY-ICAN IN ICG = ITR-KEY-ICAN IN ITR AND
                    ITR-CIC-IOPV IN ITR = SPACES              AND
                    ITR-NUM-IVCT IN ITR = ZEROES              AND
                    ITR-IND-TTAS IN ITR = 'INT' )
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    ITRNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC '/KEY : ' ICG-KEY-ICAN IN ICG
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-GET-ICG-ITR.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-ITR.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-KEY-ICAN IN ICG = ITR-KEY-ICAN IN ITR AND
                    ITR-CIC-IOPV IN ITR = SPACES              AND
                    ITR-NUM-IVCT IN ITR = ZEROES              AND
                    ITR-IND-TTAS IN ITR = 'INT' )
               MOVE '99999999' TO ITR-FEC-FINI IN ITR.
           IF ITR-FEC-FINI IN ITR = ZEROES
               MOVE OPC-FEC-FCOL IN OPC TO ITR-FEC-FINI IN ITR.
           MOVE ITR-FEC-FINI IN ITR TO ICG-FEC-FTER IN ICG.
       FIN-GET-ICG-ITR.
           EXIT.
      *-----------------------------------------------------------------
       CAL-TAS-EQV SECTION.
       INI-CAL-TAS-EQV.
      * Conversion de Tasa de Interes a Base 30 dias o Anual
      * Tasa Interes.                    * INT-TIPT.
      * Valor Tasa Informada.            * INT-TINT.
      * Fecha Inicial Periodo.           * INT-FINI.
      * Separacion entre Vencimientos.   * INT-SEPA.
      * Unidad de separacion.            * INT-IPER.
      * Analiza Feriado ?                * INT-AFER.
      * IF INT-CVTI > SPACES
      * Fecha Interes.                   * INT-FINT.
      * Moneda.                          * INT-VCAM.
           MOVE ZEROES TO WSS-VAL-TEQV IN WSS-VARI.
           IF OPC-COD-VCOC IN OPC = WSS-COD-PESO IN WSS-VARI
               MOVE  30  TO INT-SEPA INT-SEPD
           ELSE
           IF OPC-MSC-TVOC IN OPC = '0'
               MOVE  360  TO INT-SEPA INT-SEPD.
           MOVE ICG-COD-TINT IN ICG TO INT-TIPT.
           IF INT-CVTI > SPACES
               MOVE INT-IBTV-UNA-VEZ    TO INT-IBTV
               MOVE OPC-COD-VCOC IN OPC TO INT-VCAM
               MOVE ICG-SGV-SPRD IN ICG TO INT-TINT
      *        En el Ingreso ICG-FEC-FINI = ZEROES
               IF ICG-FEC-FINI IN ICG > ZEROES
                   MOVE ICG-FEC-FINI IN ICG TO INT-FINT
               ELSE
                   MOVE OPC-FEC-FCOL IN OPC TO INT-FINT
           ELSE
               MOVE ICG-SGV-TINT IN ICG TO INT-TINT.
           PERFORM OBT-VTIN.
           IF INT-CMSG > SPACES
               GO TO MSG-CAL-TAS-EQV.
           MULTIPLY INT-TIPE BY 100 GIVING WSS-VAL-TEQV IN WSS-VARI
                                                               ROUNDED.
           GO TO FIN-CAL-TAS-EQV.
       MSG-CAL-TAS-EQV.
           MOVE 'COL'    TO MSG-COD-SIST.
           MOVE INT-CMSG TO MSG-COD-MENS.
           PERFORM GET-MSG.
           DISPLAY MSG-GLS-DESC.
           DISPLAY 'DOCUMENTO : ' DLC-KEY-IDLC IN DLC.
           DISPLAY '1) TASA INT./V. CAMBIO/FECHA : '
                    INT-TIPT '/' INT-VCAM '/' INT-FINT.
       FIN-CAL-TAS-EQV.
           EXIT.
      *-----------------------------------------------------------------
      ******************************************************************
      * Proceso para Vencimientos de la Operacion de Credito
       BUS-REG-VEN SECTION.
       INI-BUS-REG-VEN.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.
      * Lectura Inicial de Vencimientos de la Operacion de Credito
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'OPC    VENNX' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC '/KEY : ' VEN-KEY-IVEN IN VEN
                   GO TO FIN-BUS-REG-VEN
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'OPC    VENNX' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC '/KEY : ' VEN-KEY-IVEN IN VEN
                   GO TO FIN-BUS-REG-VEN.

           MOVE OPC-VAL-SCRE IN OPC TO WSS-VAL-SOPC IN WSS-VARI.
           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FVEN IN WSS-VARI
                                       WSS-FEC-FVE1 IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-IPCV IN WSS-VARI
                          WSS-IND-SCTB IN WSS-VARI.
           MOVE SPACES TO WSS-FEC-FCIN IN WSS-VARI.

           MOVE VEN-IND-SCTB IN VEN TO WSS-OLD-SCTB IN WSS-VARI.
       LUP-BUS-REG-VEN.
      * Procesa Vencimiento de la Operacion de Credito
           IF VEN-NUM-IDLC IN VEN NOT = DLC-NUM-IDLC IN DLC
               GO TO NXT-BUS-REG-VEN.

           ADD   1  TO WSS-IND-RVEN IN WSS-VARI.
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.

      * Obtiene Valor de Cambio en Pesos a Fecha Vencimiento
           IF WSS-IND-MNAC IN WSS-VARI = 1 AND
              VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               MOVE VEN-FEC-FVEN IN VEN      TO CAM-FEC-FCAM IN CAM(1)
               MOVE VEN-FEC-FVEN IN VEN      TO CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR DE CAMBIO PARA MONEDA : '
                            OPC-COD-VCOC IN OPC
                           ' A FECHA VENCIMIENTO : ' VEN-FEC-FVEN IN VEN
                           ' EN VENCIMIENTO : '      VEN-KEY-IVEN IN VEN
                   MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                   GO TO FIN-BUS-REG-VEN
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO PARA MONEDA : '
                                OPC-COD-VCOC IN OPC
                          ' A FECHA VENCIMIENTO : ' VEN-FEC-FVEN IN VEN
                          ' EN VENCIMIENTO : '      VEN-KEY-IVEN IN VEN
                       MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                       GO TO FIN-BUS-REG-VEN
                   ELSE
                       MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FVEN
                                                             IN WSS-VARI
                       IF VEN-FEC-FVEN IN VEN = WSS-FEC-FVE1 IN WSS-VARI
                           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FVE1
                                                            IN WSS-VARI.

      * Tranforma a Pesos Valores del Vencimiento
           IF WSS-IND-MNAC IN WSS-VARI = 1 AND
              VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               MULTIPLY VEN-VAL-CAPI IN VEN BY WSS-SGV-FVEN IN WSS-VARI
                              GIVING WSS-VAL-CAPI IN WSS-VARI ROUNDED
               MULTIPLY VEN-VAL-SCAP IN VEN BY WSS-SGV-FVEN IN WSS-VARI
                              GIVING WSS-VAL-SCAP IN WSS-VARI ROUNDED
               MULTIPLY VEN-VAL-FINA IN VEN BY WSS-SGV-FVEN IN WSS-VARI
                              GIVING WSS-VAL-FINA IN WSS-VARI ROUNDED
               MULTIPLY VEN-VAL-SFIN IN VEN BY WSS-SGV-FVEN IN WSS-VARI
                              GIVING WSS-VAL-SFIN IN WSS-VARI ROUNDED
               MULTIPLY VEN-VAL-IDNI IN VEN BY WSS-SGV-FVEN IN WSS-VARI
                                 GIVING WSS-VAL-IDNI IN WSS-VARI ROUNDED
           ELSE
               MOVE VEN-VAL-CAPI IN VEN TO WSS-VAL-CAPI IN WSS-VARI
               MOVE VEN-VAL-SCAP IN VEN TO WSS-VAL-SCAP IN WSS-VARI
               MOVE VEN-VAL-FINA IN VEN TO WSS-VAL-FINA IN WSS-VARI
               MOVE VEN-VAL-SFIN IN VEN TO WSS-VAL-SFIN IN WSS-VARI
               MOVE VEN-VAL-IDNI IN VEN TO WSS-VAL-IDNI IN WSS-VARI.

      * Determina Morosidad de Vencimientos y Numero de Vencimientos Imp
           IF VEN-FEC-FIMV IN VEN NOT > WSS-FEC-FREG IN WSS-VARI AND
              VEN-VAL-SFIN IN VEN > ZEROES
               PERFORM DET-MOR-FIMV
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-BUS-REG-VEN.

      * Determina Saldo en Cartera Vencida
           IF VEN-IND-SCTB IN VEN = 'VCD'
               ADD WSS-VAL-CAPI IN WSS-VARI TO WSS-VAL-SCVC IN WSS-VARI.

      * Calculo Inicial Promedio Ponderado Residual
           IF VEN-FEC-FIMV IN VEN > WSS-FEC-FREG IN WSS-VARI
               PERFORM DIF-DIA-FVEN
               MULTIPLY WSS-VAL-SCAP IN WSS-VARI BY FEC-NDIA
                             GIVING WSS-VAL-PCAP IN WSS-VARI ROUNDED
               ADD WSS-VAL-PCAP IN WSS-VARI TO WSS-VAL-TCAP IN WSS-VARI
               ADD WSS-VAL-SCAP IN WSS-VARI TO WSS-VAL-ACAP IN WSS-VARI
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-BUS-REG-VEN.

      * Determina Numero de Vencimientos de Amortizacion por Vencer
           IF VEN-VAL-CAPI IN VEN > 0 AND
              VEN-VAL-SFIN IN VEN > 0 AND
              VEN-FEC-FIMV IN VEN > WSS-FEC-FREG IN WSS-VARI
               ADD 1 TO WSS-NUM-CAPV IN WSS-VARI.

      * Determina Monto Vencimientos del Mes
           IF VEN-NUM-MVEN IN VEN = WSS-NUM-MINI IN WSS-VARI AND
              VEN-NUM-SVEN IN VEN = WSS-NUM-SINI IN WSS-VARI AND
              VEN-NUM-AVEN IN VEN = WSS-NUM-AINI IN WSS-VARI
               ADD WSS-VAL-FINA IN WSS-VARI TO WSS-VAL-VMES IN WSS-VARI.

      * Determina Intereses por Cobrar Vencidos, Intereses por Cobrar Ri
      * y Reajustes Devengados Riesgosos
           IF VEN-IND-SCTB IN VEN = 'VCD' AND
              WSS-IND-SCTB IN WSS-VARI = 0
               PERFORM BUS-VEN-CCT
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-BUS-REG-VEN
               ELSE
               IF WSS-IND-SCTB IN WSS-VARI = 0
                   IF CRI-IND-CRSS IN CRI = 'C' OR
                      CRI-IND-CRSS IN CRI = 'D'
                       PERFORM DET-INT-REA-RIE
                   ELSE
                       PERFORM DET-INT-REA.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-BUS-REG-VEN.

      * Determina Monto Pagado y/o Renovado en el Mes
           IF VEN-NUM-MVEN IN VEN = WSS-NUM-MINI IN WSS-VARI AND
              VEN-NUM-SVEN IN VEN = WSS-NUM-SINI IN WSS-VARI AND
              VEN-NUM-AVEN IN VEN = WSS-NUM-AINI IN WSS-VARI AND
              VEN-VAL-SFIN IN VEN NOT > 0
               PERFORM BUS-VEN-RCV
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-BUS-REG-VEN.

      * Anterior Fecha Vencimiento y Valor de Cambio se guarda
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FVE1 IN WSS-VARI
               MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FVE1 IN WSS-VARI
               MOVE WSS-SGV-FVEN IN WSS-VARI TO WSS-SGV-FVE1
                                                        IN WSS-VARI.
       NXT-BUS-REG-VEN.
      * Lectura de Siguiente Vencimiento de la Operacion de Credito
           MOVE FIO-GET-NXT TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS OR
                  OPC-CIC-IOPC IN OPC NOT = VEN-CIC-IOPC IN VEN
                   GO TO FIN-BUS-REG-VEN
               ELSE
                   NEXT SENTENCE
           ELSE
               PERFORM COL-OBT-VEN
               IF VEN-STAT-EOF
                   GO TO FIN-BUS-REG-VEN
               ELSE
               IF NOT VEN-STAT-OKS
                   MOVE 'COL'    TO MSG-COD-SIST
                   MOVE VEN-STAT TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC   VEN-KEY-IVEN IN VEN
                   GO TO FIN-BUS-REG-VEN.
           GO TO LUP-BUS-REG-VEN.
       FIN-BUS-REG-VEN.
           EXIT.
      *-----------------------------------------------------------------
       DET-MOR-FIMV SECTION.
       INI-DET-MOR-FIMV.
      * Determina Morosidad de Vencimientos y Numero de Vencimientos Imp
           PERFORM DIF-DIA-FIMV.
           ADD 1 TO WSS-NUM-VCIM IN WSS-VARI.
           IF FEC-NDIA > 30
               ADD WSS-VAL-CAPI IN WSS-VARI TO WSS-VAL-SMO2 IN WSS-VARI
           ELSE
               ADD WSS-VAL-CAPI IN WSS-VARI TO WSS-VAL-SMO1 IN WSS-VARI.
       FIN-DET-MOR-FIMV.
           EXIT.

       DIF-DIA-FIMV SECTION.
       INI-DIF-DIA-FIMV.
      *Comparacion en Dias entre Fechas Inicio Mora y Proceso
           MOVE VEN-NUM-DIMV IN VEN      TO WSS-NUM-DIMV IN WSS-VARI.
           MOVE VEN-NUM-MIMV IN VEN      TO WSS-NUM-MIMV IN WSS-VARI.
           MOVE VEN-NUM-SIMV IN VEN      TO WSS-NUM-SIMV IN WSS-VARI.
           MOVE VEN-NUM-AIMV IN VEN      TO WSS-NUM-AIMV IN WSS-VARI.
           MOVE WSS-FEC-FIMV IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-DIF-DIA-FIMV.
           EXIT.
      *-----------------------------------------------------------------
       DIF-DIA-FVEN SECTION.
       INI-DIF-DIA-FVEN.
      *Comparacion en Dias entre Fechas Proceso y Vencimiento
           MOVE VEN-NUM-DVEN IN VEN      TO WSS-NUM-DIMV IN WSS-VARI.
           MOVE VEN-NUM-MVEN IN VEN      TO WSS-NUM-MIMV IN WSS-VARI.
           MOVE VEN-NUM-SVEN IN VEN      TO WSS-NUM-SIMV IN WSS-VARI.
           MOVE VEN-NUM-AVEN IN VEN      TO WSS-NUM-AIMV IN WSS-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FIMV IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-DIF-DIA-FVEN.
           EXIT.
      *-----------------------------------------------------------------
       BUS-VEN-CCT SECTION.
       INI-BUS-VEN-CCT.
      * Busca el Cambio Situacion Contable del Vencimiento con Situacion
           MOVE VEN-KEY-IVEN IN VEN TO CCT-KEY-IVCT IN CCT.
           MOVE 'CCT-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CCT.
           IF NOT FIO-STAT-OKS OR
              VEN-KEY-IVEN IN VEN NOT = CCT-KEY-IVCT IN CCT
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'VEN    CCTNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   VEN-KEY-IVEN IN VEN
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-BUS-VEN-CCT.
       LUP-BUS-VEN-CCT.
           IF CCT-IND-SCTB IN CCT NOT = 'VCD'
               GO TO NXT-BUS-VEN-CCT.

           IF CCT-FEC-FCCT IN CCT > WSS-FEC-FREG IN WSS-VARI
               MOVE  1  TO WSS-IND-SCTB IN WSS-VARI
               GO TO FIN-BUS-VEN-CCT.
       NXT-BUS-VEN-CCT.
           MOVE 'CCT-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-CCT.
           IF NOT FIO-STAT-OKS OR
              VEN-KEY-IVEN IN VEN NOT = CCT-KEY-IVCT IN CCT OR
              CCT-FEC-FCCT IN CCT > WSS-FEC-FREG IN WSS-VARI
               GO TO FIN-BUS-VEN-CCT.
           GO TO LUP-BUS-VEN-CCT.
       FIN-BUS-VEN-CCT.
           EXIT.
      *-----------------------------------------------------------------
      * Determina Intereses por Cobrar Vencidos
       DET-INT-REA SECTION.
       INI-DET-INT-REA.
           IF WSS-FEC-FCIN IN WSS-VARI > SPACES
               MOVE  1  TO WSS-IND-SCTB IN WSS-VARI
               GO TO FIN-DET-INT-REA.

           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FCIN IN WSS-VARI.

      * Determina Intereses por Cobrar Vencidos
           IF VEN-VAL-SCAP IN VEN NOT > ZEROES
               SUBTRACT VEN-VAL-SCAP IN VEN FROM
                        VEN-VAL-SFIN IN VEN GIVING
                        WSS-VAL-INTV IN WSS-VARI
               IF WSS-IND-MNAC IN WSS-VARI = 1
                   MULTIPLY WSS-VAL-INTV IN WSS-VARI BY
                            WSS-SGV-FVEN IN WSS-VARI GIVING
                            WSS-VAL-INTE IN WSS-VARI ROUNDED
                   ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCV
                                                           IN WSS-VARI
               ELSE
                   ADD WSS-VAL-INTV IN WSS-VARI TO WSS-VAL-IPCV
                                                           IN WSS-VARI.
       FIN-DET-INT-REA.
           EXIT.
      *-----------------------------------------------------------------
       DET-INT-REA-RIE SECTION.
       INI-DET-INT-REA-RIE.
      * Determina Intereses por Cobrar Vencidos, Intereses por Cobrar Ri
      * y Reajustes Devengados Riesgosos
           IF WSS-FEC-FCIN IN WSS-VARI NOT > SPACES
               IF CRI-FEC-FCLA IN CRI NOT > VEN-FEC-FVEN IN VEN
                   MOVE CRI-FEC-FCLA IN CRI TO WSS-FEC-FCIN IN WSS-VARI
                   PERFORM PRO-CASO-UNOA
               ELSE
                   MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FCIN IN WSS-VARI
                   PERFORM PRO-CASO-DOSA
           ELSE
           IF WSS-FEC-FCIN IN WSS-VARI > SPACES
               IF CRI-FEC-FCLA IN CRI = WSS-FEC-FCIN IN WSS-VARI
                   PERFORM PRO-CASO-UNOB
               ELSE
                   PERFORM PRO-CASO-DOSB.

      * Rebaja el Saldo Capital del Vencimiento al Saldo Capital Operaci
           SUBTRACT VEN-VAL-SCAP IN VEN FROM
                    WSS-VAL-SOPC IN WSS-VARI ROUNDED.
       FIN-DET-INT-REA-RIE.
           EXIT.
      *-----------------------------------------------------------------
      * Determina Intereses por Cobrar Vencidos, Intereses por Cobrar Ri
      * y Reajustes Devengados Riesgosos del CASO UNO (primero)
       PRO-CASO-UNOA SECTION.
       INI-PRO-CASO-UNOA.
           IF DLC-FEC-FDEV IN DLC > VEN-FEC-FVEN IN VEN
               MOVE ZEROES TO WSS-VAL-IPCV IN WSS-VARI
                              WSS-VAL-IPCR IN WSS-VARI
               GO TO REA-PRO-CASO-UNOA
           ELSE
           IF DLC-FEC-FDEV IN DLC NOT < WSS-FEC-FCIN IN WSS-VARI
               MOVE ZEROES TO WSS-VAL-INTS IN WSS-VARI
           ELSE
               PERFORM DET-INT-FDEV-FCIN
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-PRO-CASO-UNOA.

      * Determina Intereses por Cobrar Riesgosos del CASO UNO (primero)
           SUBTRACT VEN-VAL-SCAP IN VEN
                    WSS-VAL-INTS IN WSS-VARI FROM
                    VEN-VAL-SFIN IN VEN      GIVING
                    WSS-VAL-INTV IN WSS-VARI.
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MULTIPLY WSS-VAL-INTV IN WSS-VARI BY
                        WSS-SGV-FVEN IN WSS-VARI GIVING
                        WSS-VAL-INTE IN WSS-VARI ROUNDED
               ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCR IN WSS-VARI
           ELSE
               ADD WSS-VAL-INTV IN WSS-VARI TO WSS-VAL-IPCR IN WSS-VARI.
       REA-PRO-CASO-UNOA.
      * Determina Reajustes Devengados Riesgosos del CASO UNO (primero)
           IF WSS-IND-MNAC IN WSS-VARI = 1 AND
              OPC-MSC-TVOC IN OPC = '0' AND
              VEN-VAL-SCAP IN VEN > ZEROES
               SUBTRACT WSS-SGV-FCLA IN WSS-VARI FROM
                        WSS-SGV-FVEN IN WSS-VARI GIVING
                        WSS-SGV-DIFE IN WSS-VARI
               MULTIPLY VEN-VAL-SCAP IN VEN BY
                        WSS-SGV-DIFE IN WSS-VARI GIVING
                        WSS-VAL-RRIE IN WSS-VARI
               ADD WSS-VAL-RRIE IN WSS-VARI TO WSS-VAL-RDER IN WSS-VARI.
       FIN-PRO-CASO-UNOA.
           EXIT.

      * Determina Intereses por Cobrar Riesgosos y Reajustes Devengados
      * Riesgo del CASO UNO (otros)
       PRO-CASO-UNOB SECTION.
       INI-PRO-CASO-UNOB.
      * Determina Intereses por Cobrar Riesgosos del CASO UNO (otros)
           IF VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               SUBTRACT VEN-VAL-SCAP IN VEN FROM
                        VEN-VAL-SFIN IN VEN GIVING
                        WSS-VAL-INTV IN WSS-VARI
               IF WSS-IND-MNAC IN WSS-VARI = 1
                   MULTIPLY WSS-VAL-INTV IN WSS-VARI BY
                            WSS-SGV-FVEN IN WSS-VARI GIVING
                            WSS-VAL-INTE IN WSS-VARI ROUNDED
                   ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCR
                                                           IN WSS-VARI
               ELSE
                   ADD WSS-VAL-INTV IN WSS-VARI TO WSS-VAL-IPCR
                                                           IN WSS-VARI.

      * Determina Reajustes Devengados Riesgosos del CASO UNO (otros)
           IF VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               IF WSS-IND-MNAC IN WSS-VARI = 1 AND
                  OPC-MSC-TVOC IN OPC = '0' AND
                  VEN-VAL-SCAP IN VEN > ZEROES
                   SUBTRACT WSS-SGV-FVE1 IN WSS-VARI FROM
                            WSS-SGV-FVEN IN WSS-VARI GIVING
                            WSS-SGV-DIFE IN WSS-VARI
                   MULTIPLY VEN-VAL-SCAP IN VEN BY
                            WSS-SGV-DIFE IN WSS-VARI GIVING
                            WSS-VAL-RRIE IN WSS-VARI
                   ADD WSS-VAL-RRIE IN WSS-VARI TO WSS-VAL-RDER
                                                           IN WSS-VARI.

           IF VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               GO TO FIN-PRO-CASO-UNOB.

      *    IF VEN-FEC-FVEN IN VEN > WSS-FEC-FREG IN WSS-VARI
      * Determina Intereses por Cobrar Riesgosos del CASO UNO (otros)(fi
           PERFORM DET-INT-FVE1-FREG.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-PRO-CASO-UNOB.

      * Determina Reajustes Devengados Riesgosos del CASO UNO (otros)(fi
           IF WSS-IND-MNAC IN WSS-VARI = 1 AND
              OPC-MSC-TVOC IN OPC = '0' AND
              VEN-VAL-SCAP IN VEN > ZEROES
               SUBTRACT WSS-SGV-FVE1 IN WSS-VARI FROM
                        WSS-SGV-FREG IN WSS-VARI GIVING
                        WSS-SGV-DIFE IN WSS-VARI
               MULTIPLY VEN-VAL-SCAP IN VEN BY
                        WSS-SGV-DIFE IN WSS-VARI GIVING
                        WSS-VAL-RRIE IN WSS-VARI
               ADD WSS-VAL-RRIE IN WSS-VARI TO WSS-VAL-RDER IN WSS-VARI.
       FIN-PRO-CASO-UNOB.
           EXIT.

       PRO-CASO-DOSA SECTION.
       INI-PRO-CASO-DOSA.
      * Determina Intereses por Cobrar Vencidos del CASO DOS (primero)
           IF VEN-VAL-SCAP IN VEN = ZEROES
               SUBTRACT VEN-VAL-SCAP IN VEN FROM
                        VEN-VAL-SFIN IN VEN GIVING
                        WSS-VAL-INTV IN WSS-VARI
               IF WSS-IND-MNAC IN WSS-VARI = 1
                   MULTIPLY WSS-VAL-INTV IN WSS-VARI BY
                            WSS-SGV-FVEN IN WSS-VARI GIVING
                            WSS-VAL-INTE IN WSS-VARI ROUNDED
                   ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCV
                                                           IN WSS-VARI
               ELSE
                   ADD WSS-VAL-INTV IN WSS-VARI TO WSS-VAL-IPCV
                                                           IN WSS-VARI.
       FIN-PRO-CASO-DOSA.
           EXIT.

      * Determina Intereses por Cobrar Riesgosos y Reajustes Devengados
      * Riesgo del CASO DOS (otros)
       PRO-CASO-DOSB SECTION.
       INI-PRO-CASO-DOSB.
           IF VEN-FEC-FVEN IN VEN NOT > CRI-FEC-FCLA IN CRI
               GO TO FIN-PRO-CASO-DOSB.

           IF VEN-FEC-FVEN IN VEN > CRI-FEC-FCLA IN CRI AND
              WSS-IND-PRIM IN WSS-VARI = 0
               MOVE  1  TO WSS-IND-PRIM IN WSS-VARI
               PERFORM DET-INT-FVE1-FCLA
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-PRO-CASO-DOSB
               ELSE
                   IF WSS-IND-MNAC IN WSS-VARI = 1 AND
                      OPC-MSC-TVOC IN OPC = '0' AND
                      VEN-VAL-SCAP IN VEN > ZEROES
                       SUBTRACT WSS-SGV-FCLA IN WSS-VARI FROM
                                WSS-SGV-FVEN IN WSS-VARI GIVING
                                WSS-SGV-DIFE IN WSS-VARI
                       MULTIPLY VEN-VAL-SCAP IN VEN BY
                                WSS-SGV-DIFE IN WSS-VARI GIVING
                                WSS-VAL-RRIE IN WSS-VARI
                       ADD WSS-VAL-RRIE IN WSS-VARI TO WSS-VAL-RDER
                                                           IN WSS-VARI
                       GO TO FIN-PRO-CASO-DOSB
                   ELSE
                       GO TO FIN-PRO-CASO-DOSB.

      * Determina Intereses por Cobrar Riesgosos del CASO DOS (otros)
           IF VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               SUBTRACT VEN-VAL-SCAP IN VEN FROM
                        VEN-VAL-SFIN IN VEN GIVING
                        WSS-VAL-INTV IN WSS-VARI
               IF WSS-IND-MNAC IN WSS-VARI = 1
                   MULTIPLY WSS-VAL-INTV IN WSS-VARI BY
                            WSS-SGV-FVEN IN WSS-VARI GIVING
                            WSS-VAL-INTE IN WSS-VARI ROUNDED
                   ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCR
                                                           IN WSS-VARI
               ELSE
                   ADD WSS-VAL-INTV IN WSS-VARI TO WSS-VAL-IPCR
                                                           IN WSS-VARI.

      * Determina Reajustes Devengados Riesgosos del CASO DOS (otros)
           IF VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               IF WSS-IND-MNAC IN WSS-VARI = 1 AND
                  OPC-MSC-TVOC IN OPC = '0' AND
                  VEN-VAL-SCAP IN VEN > ZEROES
                   SUBTRACT WSS-SGV-FVE1 IN WSS-VARI FROM
                            WSS-SGV-FVEN IN WSS-VARI GIVING
                            WSS-SGV-DIFE IN WSS-VARI
                   MULTIPLY VEN-VAL-SCAP IN VEN BY
                            WSS-SGV-DIFE IN WSS-VARI GIVING
                            WSS-VAL-RRIE IN WSS-VARI
                   ADD WSS-VAL-RRIE IN WSS-VARI TO WSS-VAL-RDER
                                                           IN WSS-VARI.

           IF VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FREG IN WSS-VARI
               GO TO FIN-PRO-CASO-DOSB.

      *    IF VEN-FEC-FVEN IN VEN > WSS-FEC-FREG IN WSS-VARI
      * Determina Intereses por Cobrar Riesgosos del CASO DOS (otros)(fi
           PERFORM DET-INT-FVE1-FREG.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-PRO-CASO-DOSB.

      * Determina Reajustes Devengados Riesgosos del CASO DOS (otros)(fi
           IF WSS-IND-MNAC IN WSS-VARI = 1 AND
              OPC-MSC-TVOC IN OPC = '0' AND
              VEN-VAL-SCAP IN VEN > ZEROES
               SUBTRACT WSS-SGV-FVE1 IN WSS-VARI FROM
                        WSS-SGV-FREG IN WSS-VARI GIVING
                        WSS-SGV-DIFE IN WSS-VARI
               MULTIPLY VEN-VAL-SCAP IN VEN BY
                        WSS-SGV-DIFE IN WSS-VARI GIVING
                        WSS-VAL-RRIE IN WSS-VARI
               ADD WSS-VAL-RRIE IN WSS-VARI TO WSS-VAL-RDER IN WSS-VARI.
       FIN-PRO-CASO-DOSB.
           EXIT.
      *-----------------------------------------------------------------
       DET-INT-FDEV-FCIN SECTION.
       INI-DET-INT-FDEV-FCIN.
      *Comparacion en Dias entre Fecha Devengo y Fecha Corte Intereses
           MOVE DLC-NUM-DDEV IN DLC      TO FEC-DEC1 IN FEC-VARI.
           MOVE DLC-NUM-MDEV IN DLC      TO FEC-MEC1 IN FEC-VARI.
           MOVE DLC-NUM-SDEV IN DLC      TO FEC-SEC1 IN FEC-VARI.
           MOVE DLC-NUM-ADEV IN DLC      TO FEC-AEC1 IN FEC-VARI.
           MOVE WSS-NUM-DCIN IN WSS-VARI TO FEC-DEC2 IN FEC-VARI.
           MOVE WSS-NUM-MCIN IN WSS-VARI TO FEC-MEC2 IN FEC-VARI.
           MOVE WSS-NUM-SCIN IN WSS-VARI TO FEC-SEC2 IN FEC-VARI.
           MOVE WSS-NUM-ACIN IN WSS-VARI TO FEC-AEC2 IN FEC-VARI.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-DET-INT-FDEV-FCIN.
           MOVE FEC-NDIA TO INT-SEPD.

      * Calcula Tasa de Interes del Periodo
           PERFORM CAL-TAS-PER.
           IF INT-CMSG > SPACES
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-DET-INT-FDEV-FCIN.

      * Calcula Intereses Devengados en el Periodo
           MULTIPLY INT-TIPE BY WSS-VAL-SOPC IN WSS-VARI GIVING
                                WSS-VAL-INTS IN WSS-VARI ROUNDED.

           IF VEN-VAL-SCAP IN VEN = ZEROES
               IF WSS-IND-MNAC IN WSS-VARI = 1
                   MULTIPLY WSS-VAL-INTS IN WSS-VARI BY
                            WSS-SGV-FVEN IN WSS-VARI GIVING
                            WSS-VAL-INTE IN WSS-VARI ROUNDED
                   ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCV
                                                           IN WSS-VARI
               ELSE
                   ADD WSS-VAL-INTS IN WSS-VARI TO WSS-VAL-IPCV
                                                           IN WSS-VARI.
       FIN-DET-INT-FDEV-FCIN.
           EXIT.

       DET-INT-FVE1-FCLA SECTION.
       INI-DET-INT-FVE1-FCLA.
      *Comparacion en Dias entre Fechas Vencim Anterior y Clasificacion
           MOVE WSS-NUM-DVE1 IN WSS-VARI TO FEC-DEC1 IN FEC-VARI.
           MOVE WSS-NUM-MVE1 IN WSS-VARI TO FEC-MEC1 IN FEC-VARI.
           MOVE WSS-NUM-SVE1 IN WSS-VARI TO FEC-SEC1 IN FEC-VARI.
           MOVE WSS-NUM-AVE1 IN WSS-VARI TO FEC-AEC1 IN FEC-VARI.
           MOVE CRI-NUM-DCLA IN CRI      TO FEC-DEC2 IN FEC-VARI.
           MOVE CRI-NUM-MCLA IN CRI      TO FEC-MEC2 IN FEC-VARI.
           MOVE CRI-NUM-SCLA IN CRI      TO FEC-SEC2 IN FEC-VARI.
           MOVE CRI-NUM-ACLA IN CRI      TO FEC-AEC2 IN FEC-VARI.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-DET-INT-FVE1-FCLA.
           MOVE FEC-NDIA TO INT-SEPD.

      * Calcula Tasa de Interes del Periodo
           PERFORM CAL-TAS-PER.
           IF INT-CMSG > SPACES
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-DET-INT-FVE1-FCLA.

      * Calcula Intereses en el Periodo
           MULTIPLY INT-TIPE BY WSS-VAL-SOPC IN WSS-VARI GIVING
                                WSS-VAL-INTS IN WSS-VARI ROUNDED.

           SUBTRACT VEN-VAL-SCAP IN VEN
                    WSS-VAL-INTS IN WSS-VARI FROM
                    VEN-VAL-SFIN IN VEN      GIVING
                    WSS-VAL-INTV IN WSS-VARI.
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MULTIPLY WSS-VAL-INTV IN WSS-VARI BY
                        WSS-SGV-FVEN IN WSS-VARI GIVING
                        WSS-VAL-INTE IN WSS-VARI ROUNDED
               ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCR IN WSS-VARI
           ELSE
               ADD WSS-VAL-INTV IN WSS-VARI TO WSS-VAL-IPCR IN WSS-VARI.
       FIN-DET-INT-FVE1-FCLA.
           EXIT.

       DET-INT-FVE1-FREG SECTION.
       INI-DET-INT-FVE1-FREG.
      *Comparacion en Dias entre Fecha Vencimiento Anterior y Fecha Proc
           MOVE WSS-NUM-DVE1 IN WSS-VARI TO FEC-DEC1 IN FEC-VARI.
           MOVE WSS-NUM-MVE1 IN WSS-VARI TO FEC-MEC1 IN FEC-VARI.
           MOVE WSS-NUM-SVE1 IN WSS-VARI TO FEC-SEC1 IN FEC-VARI.
           MOVE WSS-NUM-AVE1 IN WSS-VARI TO FEC-AEC1 IN FEC-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FINI-DET-INT-FVE1-FREG.
           MOVE FEC-NDIA TO INT-SEPD.

      * Calcula Tasa de Interes del Periodo
           PERFORM CAL-TAS-PER.
           IF INT-CMSG > SPACES
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FINI-DET-INT-FVE1-FREG.

      * Calcula Intereses de Riesgo en el Periodo
           MULTIPLY INT-TIPE BY WSS-VAL-SOPC IN WSS-VARI GIVING
                                WSS-VAL-INTS IN WSS-VARI ROUNDED.

           IF WSS-IND-MNAC IN WSS-VARI = 1
               MULTIPLY WSS-VAL-INTS IN WSS-VARI BY
                        WSS-SGV-FVEN IN WSS-VARI GIVING
                        WSS-VAL-INTE IN WSS-VARI ROUNDED
               ADD WSS-VAL-INTE IN WSS-VARI TO WSS-VAL-IPCR IN WSS-VARI
           ELSE
               ADD WSS-VAL-INTS IN WSS-VARI TO WSS-VAL-IPCR IN WSS-VARI.
       FINI-DET-INT-FVE1-FREG.
           EXIT.
      *-----------------------------------------------------------------
       CAL-TAS-PER SECTION.
       INI-CAL-TAS-PER.
      * Calculo tasa de interes a partir de una tasa y periodo dado
      * Tasa Interes.                    * INT-TIPT.
      * Valor Tasa Informada.            * INT-TINT.
      * Separacion en Dias.              * INT-SEPD.
      * IF INT-CVTI > SPACES
      *   Fecha Interes.                 * INT-FINT.
      *   Moneda.                        * INT-VCAM.
           MOVE ICG-COD-TINT IN ICG TO INT-TIPT.
           IF INT-CVTI > SPACES
               MOVE INT-IBTV-UNA-VEZ    TO INT-IBTV
               MOVE OPC-COD-VCOC IN OPC TO INT-VCAM
               MOVE ICG-SGV-SPRD IN ICG TO INT-TINT
      *        En el Ingreso ICG-FEC-FINI = ZEROES
               IF ICG-FEC-FINI IN ICG > ZEROES
                   MOVE ICG-FEC-FINI IN ICG TO INT-FINT
               ELSE
                   MOVE OPC-FEC-FCOL IN OPC TO INT-FINT
           ELSE
               MOVE ICG-SGV-TINT IN ICG TO INT-TINT.
           PERFORM OBT-VTIN.
           IF INT-CMSG > SPACES
               GO TO MSG-CAL-TAS-PER.
           GO TO FIN-CAL-TAS-PER.
       MSG-CAL-TAS-PER.
           MOVE 'COL'    TO MSG-COD-SIST.
           MOVE INT-CMSG TO MSG-COD-MENS.
           PERFORM GET-MSG.
           DISPLAY MSG-GLS-DESC.
           DISPLAY 'DOCUMENTO : ' DLC-KEY-IDLC IN DLC.
           DISPLAY '2) TASA INT./V. CAMBIO/FECHA : '
                    INT-TIPT '/' INT-VCAM '/' INT-FINT.
       FIN-CAL-TAS-PER.
           EXIT.
      *-----------------------------------------------------------------
      * Busca Cancelacion en Periodo para determinar monto Pagado y/o Re
       BUS-VEN-RCV SECTION.
       INI-BUS-VEN-RCV.
      * Busca RCV del Vencimiento
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI
                       WSS-IND-RENO IN WSS-VARI
                       WSS-IND-RCV0 IN WSS-VARI.
           MOVE VEN-KEY-IVEN IN VEN TO RCV-KEY-IVCT IN RCV.
           MOVE 'RCV-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS OR
              RCV-KEY-IVCT IN RCV NOT = VEN-KEY-IVEN IN VEN
               PERFORM LEE-RCV-CERO
               IF WSS-IND-NEXT IN WSS-VARI = 1
                   GO TO FIN-BUS-VEN-RCV.

      * Busca Cancelacion en el Periodo
           MOVE RCV-KEY-ICAN IN RCV TO CAN-KEY-ICAN IN CAN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'RCV    CANNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RCV-KEY-ICAN IN RCV
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-BUS-VEN-RCV.

           IF CAN-NUM-MCAN IN CAN NOT = WSS-NUM-MINI IN WSS-VARI OR
              CAN-NUM-SCAN IN CAN NOT = WSS-NUM-SINI IN WSS-VARI OR
              CAN-NUM-ACAN IN CAN NOT = WSS-NUM-AINI IN WSS-VARI
               GO TO FIN-BUS-VEN-RCV.

      * Busca Renovacion del Vencimiento en el Periodo
           IF CAN-MSC-TCAN IN CAN = 'P' OR 'R' OR 'N'
               MOVE  1  TO WSS-IND-RENO IN WSS-VARI
               MOVE VEN-KEY-IVEN IN VEN TO ROV-KEY-IVCT IN ROV
               MOVE 'ROV-KEY-IVCT' TO FIO-AKEY
               MOVE FIO-GET-NLS    TO FIO-CMND
               PERFORM COL-FIO-ROV
               IF NOT FIO-STAT-OKS OR
                  ROV-KEY-IVCT IN ROV NOT = VEN-KEY-IVEN IN VEN
                   MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                   GO TO FIN-BUS-VEN-RCV.

      * Obtiene Valor de Cambio en Pesos a Fecha Cancelacion para RCV
           IF WSS-IND-RENO IN WSS-VARI = 0 AND
              WSS-IND-RCV0 IN WSS-VARI = 0 AND
              WSS-IND-MNAC IN WSS-VARI = 1
               MOVE CAN-FEC-FCAN IN CAN      TO CAM-FEC-FCAM IN CAM(1)
               MOVE CAN-FEC-FCAN IN CAN      TO CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR DE CAMBIO PARA MONEDA : '
                            OPC-COD-VCOC IN OPC
                          ' A FECHA CANCELA : ' CAN-FEC-FCAN IN CAN
                           ' EN CANCELACION : ' CAN-KEY-ICAN IN CAN
                   MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                   GO TO FIN-BUS-VEN-RCV
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO PARA MONEDA : '
                                OPC-COD-VCOC IN OPC
                               ' A FECHA CANCELA : ' CAN-FEC-FCAN IN CAN
                               ' EN CANCELACION : '  CAN-KEY-ICAN IN CAN
                       MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                       GO TO FIN-BUS-VEN-RCV
                   ELSE
                       MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FRCV
                                                           IN WSS-VARI.

      * Determina Monto Pagado y/o Renovado en el Mes
           IF WSS-IND-RENO IN WSS-VARI = 1
               SUBTRACT ROV-SGV-RENO IN ROV FROM WSS-VAL-FINA
                            IN WSS-VARI GIVING WSS-VAL-DMES IN WSS-VARI
               ADD WSS-VAL-DMES IN WSS-VARI TO WSS-VAL-PMES IN WSS-VARI
               ADD ROV-SGV-RENO IN ROV      TO WSS-VAL-RMES IN WSS-VARI
           ELSE
               IF WSS-IND-RCV0 IN WSS-VARI = 1
                   ADD WSS-VAL-FINA IN WSS-VARI
                       WSS-VAL-IDNI IN WSS-VARI
                                        GIVING WSS-VAL-DMES IN WSS-VARI
                   ADD WSS-VAL-DMES IN WSS-VARI TO WSS-VAL-PMES
                                                            IN WSS-VARI
               ELSE
                   ADD RCV-SGV-FINA IN RCV
                       RCV-SGV-INTE IN RCV
                                        GIVING WSS-VAL-UIPA IN WSS-VARI
                   IF WSS-IND-MNAC IN WSS-VARI = 1
                       MULTIPLY WSS-VAL-UIPA IN WSS-VARI BY WSS-SGV-FRCV
                                             IN WSS-VARI GIVING
                                WSS-VAL-DMES IN WSS-VARI ROUNDED
                       ADD WSS-VAL-DMES IN WSS-VARI TO WSS-VAL-PMES
                                                            IN WSS-VARI
                   ELSE
                       ADD WSS-VAL-UIPA IN WSS-VARI TO WSS-VAL-PMES
                                                            IN WSS-VARI.
       FIN-BUS-VEN-RCV.
           EXIT.

       LEE-RCV-CERO SECTION.
       INI-LEE-RCV-CERO.
      * Busca Relacion Cancelacion Vencimiento CERO en el Periodo
           MOVE VEN-CIC-IOPC IN VEN TO RCV-CIC-IOPV IN RCV.
           MOVE 0                   TO RCV-NUM-IVCT IN RCV.
           MOVE 'RCV-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'VEN    RCVNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RCV-KEY-IVCT IN RCV
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.
           MOVE  1  TO WSS-IND-RCV0 IN WSS-VARI.
       FIN-LEE-RCV-CERO.
      *-----------------------------------------------------------------
************************************************************************
      ******************************************************************
      * Formacion y Generacion del Registro D00 al Procesar Varios Regis
       GEN-REG-D00 SECTION.
       INI-GEN-REG-D00.
      * Inicializacion del Registro D00
           MOVE SPACES TO D00.

      * Codigo de Sistema                1-->2
           MOVE OPC-COD-TOPC IN OPC      TO D00-COD-SIST IN D00.
      * Numero Interno Identificador     20-->21   (15-->21)
      *     MOVE DLC-NUM-IDLC IN DLC      TO WSS-NUM-IDLC IN WSS-VARI.
           MOVE WSS-COD-NIID IN WSS-VARI TO D00-COD-NIID IN D00.
      * Tipo de operacion
           MOVE OPC-COD-TOOC IN OPC      TO D00-COD-TOPE IN D00.
      * Situacion de la operacion        COL MSC SCRT
           PERFORM BUS-MSC-SCRT.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GEN-REG-D00.
           MOVE MSC-EXT-CMSC IN MSC      TO D00-IND-SOPE IN D00.
      * Oficina otorgante del credito    3-->5
           MOVE OPC-COD-OFOC IN OPC      TO D00-COD-OFIC IN D00.
      * Tipo Credito Cartera Origen      5-->4
           MOVE DCC-EXT-CTAB IN DCC      TO D00-COD-TCCO IN D00.
      * Estado del credito               COL MSC SCTB
           PERFORM BUS-MSC-SCTB.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GEN-REG-D00.
           MOVE MSC-EXT-CMSC IN MSC      TO D00-IND-ESTC IN D00.
      * Actividad destino del credito    5-->2 COL DES
           PERFORM BUS-TAB-DES.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GEN-REG-D00.
           MOVE TAB-EXT-CTAB IN TAB      TO D00-COD-ACTD IN D00.
      * Fecha Otorgamiento
           MOVE OPC-NUM-ACOL IN OPC      TO D00-NUM-AOTO IN D00.
           MOVE OPC-NUM-MCOL IN OPC      TO D00-NUM-MOTO IN D00.
           MOVE OPC-NUM-DCOL IN OPC      TO D00-NUM-DOTO IN D00.
      * Codigo de moneda y/o reajustabilidad  5-->3 TAB VLR
           PERFORM BUS-TAB-VLR.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GEN-REG-D00.
           MOVE TAB-EXT-CTAB IN TAB      TO D00-COD-MONE IN D00.
      * Monto Original Operacion
           MOVE WSS-VAL-CRED IN WSS-VARI TO D00-VAL-MOOP IN D00.
      * Saldo Contable
           MOVE WSS-VAL-SCRE IN WSS-VARI TO D00-VAL-SCON IN D00.
      * Saldo Moroso 1
           MOVE WSS-VAL-SMO1 IN WSS-VARI TO D00-VAL-SMO1 IN D00.
      * Saldo Moroso 2
           MOVE WSS-VAL-SMO2 IN WSS-VARI TO D00-VAL-SMO2 IN D00.
      * Saldo Cartera Vencida
           MOVE WSS-VAL-SCVC IN WSS-VARI TO D00-VAL-SCVC IN D00.
      * Saldo Reprogramado solo para C.Hip.
           MOVE WSS-VAL-SDLC IN WSS-VARI TO D00-VAL-SREP IN D00.
      * Tipo de garantia
           MOVE ZEROES                   TO D00-COD-TGAR IN D00.
      * Fecha Proximo Vencimiento
           MOVE WSS-NUM-AVEN IN WSS-VARI TO D00-NUM-APVC IN D00.
           MOVE WSS-NUM-MVEN IN WSS-VARI TO D00-NUM-MPVC IN D00.
           MOVE WSS-NUM-DVEN IN WSS-VARI TO D00-NUM-DPVC IN D00.
      * Fecha Ultimo Pago de Intereses
           MOVE WSS-NUM-AUPI IN WSS-VARI TO D00-NUM-AUPI IN D00.
           MOVE WSS-NUM-MUPI IN WSS-VARI TO D00-NUM-MUPI IN D00.
           MOVE WSS-NUM-DUPI IN WSS-VARI TO D00-NUM-DUPI IN D00.
      * Fecha de Extincion
           MOVE DLC-NUM-AMAV IN DLC      TO D00-NUM-AEXT IN D00.
           MOVE DLC-NUM-MMAV IN DLC      TO D00-NUM-MEXT IN D00.
           MOVE DLC-NUM-DMAV IN DLC      TO D00-NUM-DEXT IN D00.
      * Plazo Promedio Ponderado Residual
           MOVE WSS-NUM-PPPR IN WSS-VARI TO D00-NUM-PPPR IN D00.
      * Numero de cuotas amortiz. por vencer
           MOVE WSS-NUM-CAPV IN WSS-VARI TO D00-NUM-CAPV IN D00.
      * Base Calculo de Tasa de Interes  5-->3 COL INT
           PERFORM BUS-TAB-INT.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GEN-REG-D00.
           MOVE TAB-EXT-CTAB IN TAB      TO D00-IND-BCTI IN D00.
      * Tasa de Interes
           MOVE WSS-VAL-TEQV IN WSS-VARI TO D00-VAL-TINT IN D00.
      * Intereses por cobrar
           MOVE WSS-VAL-IPCO IN WSS-VARI TO D00-VAL-IPCO IN D00.
      * Intereses por cobrar vencidos
           MOVE WSS-VAL-IPCV IN WSS-VARI TO D00-VAL-IPCV IN D00.
      * Intereses por cobrar riesgosos
           MOVE WSS-VAL-IPCR IN WSS-VARI TO D00-VAL-IPCR IN D00.
      * Reajustes Devengados
           MOVE WSS-VAL-RDEV IN WSS-VARI TO D00-VAL-RDEV IN D00.
      * Reajustes Devengados riesgosos
           MOVE WSS-VAL-RDER IN WSS-VARI TO D00-VAL-RDER IN D00.
      * Tipo de Credito               5-->3 COL AUX
           PERFORM BUS-TAB-AUX.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO FIN-GEN-REG-D00.
           MOVE TAB-EXT-CTAB IN TAB      TO D00-IND-TCRE IN D00.
      * Numero de Vencimientos Impagos
           MOVE WSS-NUM-VCIM IN WSS-VARI TO D00-NUM-VCIM IN D00.
      * Monto Pagado en el Mes
           MOVE WSS-VAL-PMES IN WSS-VARI TO D00-VAL-PMES IN D00.
      * Vencimientos del Mes
           MOVE WSS-VAL-VMES IN WSS-VARI TO D00-VAL-VMES IN D00.
      * Monto Renovado en el mes
           MOVE WSS-VAL-RMES IN WSS-VARI TO D00-VAL-RMES IN D00.
      * Ultimo interes pagado
           MOVE WSS-VAL-UINP IN WSS-VARI TO D00-VAL-UINP IN D00.
      * Cuenta contable                  12-->10
           MOVE DCC-GLS-CCSB IN DCC      TO D00-COD-CCTB IN D00.
      * Numero de cuotas originales
           MOVE OPC-NUM-VCOR IN OPC      TO D00-NUM-CORI IN D00.
      * Indicador si operacion esta afecta a margenes Art. 84 L.G.B.
           MOVE 'S'                      TO D00-IND-ALGB IN D00.
      * Informacion Referida a esta fecha de cierre
           MOVE WSS-NUM-AREG IN WSS-VARI TO D00-NUM-ACIE IN D00.
           MOVE WSS-NUM-MREG IN WSS-VARI TO D00-NUM-MCIE IN D00.
           MOVE WSS-NUM-DREG IN WSS-VARI TO D00-NUM-DCIE IN D00.
      * Fecha Creacion del Registro
           MOVE WSS-NUM-AREG IN WSS-VARI TO D00-NUM-ACRG IN D00.
           MOVE WSS-NUM-MREG IN WSS-VARI TO D00-NUM-MCRG IN D00.
           MOVE WSS-NUM-DREG IN WSS-VARI TO D00-NUM-DCRG IN D00.
      * Autor creacion del registro
           MOVE SPACES                   TO D00-COD-ACRG IN D00.
      * Autor Ultima Modificacion del registro
           MOVE SPACES                   TO D00-COD-AUMO IN D00.
      * Fecha Ultima Modificacion del registro
           MOVE ZEROES                   TO D00-AMD-FUMO IN D00.
      * Indicador Exclusion Operacion    Se aprovecha COL AUX en Tipo de
           IF WSS-IND-OMIT IN WSS-VARI = 'X'
               MOVE 'E' TO D00-IND-EXCL IN D00.
      * Disponible
           MOVE SPACES                   TO D00-GLS-DISP IN D00.
      * Arreglo con R.U.T. de deudores indirectos (avales) D00-VEC-RUTD
           PERFORM PUT-VEC-CLI VARYING WSS-I FROM 1 BY 1
                                            UNTIL WSS-I IN WSS-VARI > 9.

      * Generacion de Registro D00
           ADD 1 TO WSS-IND-RPRO IN WSS-VARI.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM SUP-FIO-D00.
           IF NOT FIO-STAT-OKS
               MOVE 'GNS'     TO MSG-COD-SIST
               MOVE 'FIOSTAT' TO MSG-COD-TMSG
               MOVE  FIO-STAT TO MSG-COD-CMSG
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC IN MSG TO WSS-GLS-MENS IN WSS-VARI
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'D00    NOPUT' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC
               DISPLAY 'CAUSA DEL ERROR : ' WSS-GLS-MENS IN WSS-VARI
               DISPLAY 'EN REGISTRO D00 : ' D00-COD-ID00
               ADD 1 TO WSS-IND-RERR IN WSS-VARI
           ELSE
               ADD 1 TO WSS-IND-RPUT IN WSS-VARI.
       FIN-GEN-REG-D00.
           EXIT.
      *-----------------------------------------------------------------
       PUT-VEC-CLI SECTION.
       INI-PUT-VEC-CLI.
      * Arreglo con R.U.T. de deudores indirectos (avales) D00-VEC-RUTD
      * Rol Unico Tribut.                8-->9
           MOVE WSS-NUM-RCLI IN WSS-VARI(WSS-I) TO D00-COD-RUTD
                                                        IN D00(WSS-I).
      * Digito Verificador
           MOVE WSS-VRF-ICLI IN WSS-VARI(WSS-I) TO D00-VRF-RUTD
                                                        IN D00(WSS-I).
      * Indicador deudor directo/indirecto
           MOVE WSS-COD-TRDC IN WSS-VARI(WSS-I) TO D00-IND-TDEU
                                                        IN D00(WSS-I).
       FIN-PUT-VEC-CLI.
           EXIT.
      ******************************************************************
      ******************************************************************
       BUS-TAB-PRO SECTION.
       INI-BUS-TAB-PRO.
      * Extraccion de Codigos Externos de Tablas
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       BUS-TAB-DES SECTION.
       INI-BUS-TAB-DES.
      * Busca Codigo Externo de COD-CDOC en Tabla DES
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           IF OPC-COD-CDOC IN OPC NOT > SPACES
               MOVE SPACES TO TAB-EXT-CTAB IN TAB
           ELSE
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'DES'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-CDOC IN OPC TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS > SPACES
                   DISPLAY 'DES : ' MSG-GLS-DESC IN MSG '/'
                            OPC-COD-CDOC IN OPC '/KEY : '
                            OPC-CIC-IOPC IN OPC
                   MOVE  1     TO WSS-IND-NEXT IN WSS-VARI
                   MOVE SPACES TO TAB-EXT-CTAB IN TAB
                   GO TO FIN-BUS-TAB-DES.
       FIN-BUS-TAB-DES.
           EXIT.

       BUS-TAB-VLR SECTION.
       INI-BUS-TAB-VLR.
      * Busca Codigo Externo de COD-VCOC en Tabla VLR
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           IF OPC-COD-VCOC IN OPC NOT > SPACES
               MOVE SPACES TO TAB-EXT-CTAB IN TAB
           ELSE
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS > SPACES
                   DISPLAY 'VLR : ' MSG-GLS-DESC IN MSG '/'
                           OPC-COD-VCOC IN OPC '/KEY : '
                           OPC-CIC-IOPC IN OPC
                   MOVE  1     TO WSS-IND-NEXT IN WSS-VARI
                   MOVE SPACES TO TAB-EXT-CTAB IN TAB
                   GO TO FIN-BUS-TAB-VLR.
       FIN-BUS-TAB-VLR.
           EXIT.

       BUS-TAB-INT SECTION.
       INI-BUS-TAB-INT.
      * Busca Codigo Externo de COD-TINT en Tabla INT
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           IF ICG-COD-TINT IN ICG NOT > SPACES
               MOVE SPACES TO TAB-EXT-CTAB IN TAB
           ELSE
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'INT'               TO TAB-COD-TTAB IN TAB
               MOVE ICG-COD-TINT IN ICG TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS > SPACES
                   DISPLAY 'INT : ' MSG-GLS-DESC IN MSG '/'
                           ICG-COD-TINT IN ICG '/KEY : '
                           ICG-KEY-IICG IN ICG
                   MOVE  1     TO WSS-IND-NEXT IN WSS-VARI
                   MOVE SPACES TO TAB-EXT-CTAB IN TAB
                   GO TO FIN-BUS-TAB-INT.
       FIN-BUS-TAB-INT.
           EXIT.

       BUS-TAB-AUX SECTION.
       INI-BUS-TAB-AUX.
      * Busca Codigo Externo de COD-COOC en Tabla AUX
      * Mediante BUS-TAB Busca Ind. Exclusion en Tabla AUX (Pos.46 Datos
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           IF OPC-COD-COOC IN OPC NOT > SPACES
               MOVE SPACES TO TAB-EXT-CTAB IN TAB
           ELSE
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-COOC IN OPC TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS > SPACES
                   DISPLAY 'AUX : ' MSG-GLS-DESC IN MSG '/'
                           OPC-COD-COOC IN OPC 'KEY : '
                           OPC-CIC-IOPC IN OPC
                   MOVE  1     TO WSS-IND-NEXT IN WSS-VARI
                   MOVE SPACES TO TAB-EXT-CTAB IN TAB
                   GO TO FIN-BUS-TAB-AUX
               ELSE
                   MOVE TAB-GLS-DAT3 IN TAB TO WSS-GLS-DAT3 IN WSS-VARI.
       FIN-BUS-TAB-AUX.
           EXIT.

       BUS-MSC-SCRT SECTION.
       INI-BUS-MSC-SCRT.
      * Busca Codigo Externo de Codigo Miscelaneo 'SCRT' en Tabla MSC
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           IF DLC-IND-SCRT IN DLC NOT > SPACES
               MOVE '1' TO MSC-EXT-CMSC IN MSC
               GO TO FIN-BUS-MSC-SCRT.
           MOVE 'COL'               TO MSC-COD-SIST.
           MOVE 'SCRT'              TO MSC-COD-TMSC IN MSC.
           MOVE DLC-IND-SCRT IN DLC TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY 'SCRT : ' MSG-GLS-DESC IN MSG '/'
                       DLC-IND-SCRT IN DLC '/KEY : '
                       DLC-KEY-IDLC IN DLC
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-BUS-MSC-SCRT.
           IF MSC-EXT-CMSC IN MSC NOT = '2'
               MOVE '1' TO MSC-EXT-CMSC IN MSC.
       FIN-BUS-MSC-SCRT.
           EXIT.

       BUS-MSC-SCTB SECTION.
       INI-BUS-MSC-SCTB.
      * Busca Codigo Externo de Codigo Miscelaneo 'SCTB' en Tabla MSC
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
           MOVE 'COL'                    TO MSC-COD-SIST.
           MOVE 'SCTB'                   TO MSC-COD-TMSC IN MSC.
           MOVE WSS-OLD-SCTB IN WSS-VARI TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY 'SCTB : ' MSG-GLS-DESC IN MSG '/'
                       WSS-OLD-SCTB IN WSS-VARI '/KEY : '
                       OPC-KEY-IOPC IN OPC
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-BUS-MSC-SCTB.

           IF WSS-IND-SCTB IN WSS-VARI NOT > SPACES
               MOVE '1' TO MSC-EXT-CMSC IN MSC.
           IF OPC-VAL-SCRE IN OPC NOT > ZEROES
               MOVE '4' TO MSC-EXT-CMSC IN MSC.
       FIN-BUS-MSC-SCTB.
           EXIT.
       FIN-BUS-TAB-PRO.
           EXIT.
      ******************************************************************
