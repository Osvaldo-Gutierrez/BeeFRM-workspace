       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGPYG.
       AUTHOR.          BEE-TEAM (DPM).
      * Genera archivo con informacion de Cliente con Operaciones
      * que tienen SCTB = VCD

       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLS-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *<<<< WSS
       COPY COLBRPYG.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRDLS.
       COPY COLBRRDC.
       COPY TABBRCAM.
       COPY TABBROFI.

       COPY GNSWVIDD.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
       COPY GNSWGCVI.

       01  WSS-VARI.
           03  WSS-VAL-SCRE        VALUE ZEROES     PIC 9(12).
           03  WSS-IND-FLAG        VALUE SPACES     PIC X(01).
           03  WSS-COD-PESO        VALUE SPACES     PIC X(06).
           03  WSS-COD-COOC.
               05  WSS-COD-TOOC    VALUE SPACES     PIC X(03).
               05  WSS-COD-AOOC    VALUE SPACES     PIC X(03).
           03  WSS-IND-CON         VALUE SPACES     PIC X(01).
           03  WSS-NUM-DIME        VALUE ZEROES     PIC 9(06).
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO    VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-MPRO    VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-SPRO    VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-APRO    VALUE ZEROES     PIC 9(02).
           03  WSS-IND-DISP        VALUE SPACES     PIC X(01).

      * Variables para estadisticas
           03  WSS-NUM-RLEI        VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RPRO        VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RNSE        VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RERR        VALUE ZEROES     PIC 9(06).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGPYG' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM INI-PARAM.
           PERFORM PRO-DLC.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-DLS.
           PERFORM COL-FIO-RDC.

           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-PYG.
       FIN-OPEN-FILE.
           EXIT.

      ******************************************************************
       INI-PARAM SECTION.
      *-----------------
       INI-INI-PARAM.
           DISPLAY 'ACEPTA PARAMETROS.'.
           DISPLAY '------------------'.
           DISPLAY ' '.

           DISPLAY 'CAPTURA FECHA DE PROCESO.'.
           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO WSS-NUM-DPRO IN WSS-VARI.
           MOVE HOY-MHOY TO WSS-NUM-MPRO IN WSS-VARI.
           MOVE HOY-SHOY TO WSS-NUM-SPRO IN WSS-VARI.
           MOVE HOY-AHOY TO WSS-NUM-APRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.

           DISPLAY 'CAPTURA INDICADOR DE DISPLAY.'.
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY WSS-IND-DISP IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
              WSS-IND-DISP IN WSS-VARI NOT = 'N'
               DISPLAY 'ASUME N PARA INDICADOR DE DISPLAY'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI.

           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CICPESONEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT
               GO TO FIN-INI-PARAM.

           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
       FIN-INI-PARAM.
           EXIT.


      ******************************************************************
       PRO-DLC SECTION.
      *---------------
       INI-PRO-DLC.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-DLS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR LECTURA COLDLC: '
                              DLC-IND-SCTB IN DLC ' ' FIO-STAT
           GO TO FIN-PRO-DLC.

       LUP-PRO-DLC.
           MOVE DLS TO DLC.

           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'DLC LEIDO: ' DLC-CIC-IOPC IN DLC.

           PERFORM LEE-OPC.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               MOVE 0 TO WSS-IND-FLAG IN WSS-VARI
               DISPLAY 'ERROR LECTURA COLOPC: '
                              DLC-CIC-IOPC IN DLC ' ' FIO-STAT
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-PRO-DLC.

           IF NOT ( OPC-MSC-STAT IN OPC = 'A' AND
              OPC-VAL-SCRE IN OPC > ZEROES )
               ADD 1 TO WSS-NUM-RNSE IN WSS-VARI
               GO TO NXT-PRO-DLC.

           IF OPC-COD-VCOC IN OPC NOT = WSS-COD-PESO IN WSS-VARI
               PERFORM CONVERTIR-A-PESOS
               IF WSS-IND-FLAG IN WSS-VARI = 1
                   ADD 1 TO WSS-NUM-RERR IN WSS-VARI
                   MOVE 0 TO WSS-IND-FLAG IN WSS-VARI
                   DISPLAY 'ERROR LECTURA TABCAM: '
                                          CAM-KEY-CAMB IN CAM
                   GO TO NXT-PRO-DLC
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE OPC-VAL-SCRE IN OPC TO WSS-VAL-SCRE IN WSS-VARI.

           PERFORM LEE-RDC.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               MOVE 0 TO WSS-IND-FLAG IN WSS-VARI
               DISPLAY 'ERROR LECTURA COLRDC: '
                              RDC-CIC-IOPC IN RDC
               GO TO NXT-PRO-DLC.

      * Valida que clase de OPC sea CON
           PERFORM VAL-CLASE-CON.

           IF WSS-IND-CON IN WSS-VARI = 'S'
               MOVE 'D' TO PYG-IND-DIME IN PYG
           ELSE
               MOVE 'M' TO PYG-IND-DIME IN PYG.

           PERFORM CALCULA-DIF-FECHAS.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               MOVE 0 TO WSS-IND-FLAG IN WSS-VARI
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-PRO-DLC.

           PERFORM GRABAR-ARCHIVO-PYG.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               MOVE 0 TO WSS-IND-FLAG IN WSS-VARI
               DISPLAY 'ERROR GRABAR ARCHIVO PYG: ' PYG
               GO TO NXT-PRO-DLC.

           ADD 1 TO WSS-NUM-RPRO IN WSS-VARI.

       NXT-PRO-DLC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DLS.
           IF FIO-STAT-OKS
               GO TO LUP-PRO-DLC.
       FIN-PRO-DLC.
           EXIT.

      ******************************************************************
       LEE-OPC SECTION.
      *---------------
       INI-LEE-OPC.
           MOVE DLC-CIC-IOPC IN DLC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
               GO TO FIN-LEE-OPC.
       FIN-LEE-OPC.
           EXIT.

      ******************************************************************
       CONVERTIR-A-PESOS SECTION.
      *-------------------------
       INI-CONVERTIR-A-PESOS.
           MOVE OPC-FEC-FCOL IN OPC      TO CAM-FEC-FCAM IN CAM(1)
                                            CAM-FEC-FCAM IN CAM(2).
           MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT ( FIO-STAT-OKS AND
              CAM-IND-VIGE IN CAM = 'S' )
               MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
               GO TO FIN-CONVERTIR-A-PESOS.

           MULTIPLY OPC-VAL-SCRE IN OPC BY CAM-SGV-VCAM IN CAM
                       GIVING WSS-VAL-SCRE IN WSS-VARI ROUNDED .
       FIN-CONVERTIR-A-PESOS.
           EXIT.

      ******************************************************************
       LEE-RDC SECTION.
      *---------------
       INI-LEE-RDC.
           MOVE OPC-CIC-IOPC IN OPC TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
              OPC-CIC-IOPC IN OPC = RDC-CIC-IOPC IN RDC )
               MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
               GO TO FIN-LEE-RDC.
       FIN-LEE-RDC.
           EXIT.

      ******************************************************************
       VAL-CLASE-CON SECTION.
      *---------------------
       INI-VAL-CLASE-CON.
           MOVE 'COL'               TO TAB-COD-SIST.
           MOVE 'AUX'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-COOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-CIC-CTAB IN TAB TO WSS-COD-COOC IN WSS-VARI.
           IF WSS-COD-TOOC IN WSS-VARI = 'CON'
               MOVE 'S' TO WSS-IND-CON IN WSS-VARI
           ELSE
               MOVE 'N' TO WSS-IND-CON IN WSS-VARI.
       FIN-VAL-CLASE-CON.
           EXIT.

      ******************************************************************
       CALCULA-DIF-FECHAS SECTION.
      *--------------------------
       INI-CALCULA-DIF-FECHAS.
           IF WSS-IND-CON IN WSS-VARI = 'S'
               MOVE OPC-NUM-DCOL IN OPC TO FEC-DEC1
                                           PYG-NUM-DFEC IN PYG
               MOVE OPC-NUM-MCOL IN OPC TO FEC-MEC1
                                           PYG-NUM-MFEC IN PYG
               MOVE OPC-NUM-SCOL IN OPC TO FEC-SEC1
                                           PYG-NUM-SFEC IN PYG
               MOVE OPC-NUM-ACOL IN OPC TO FEC-AEC1
                                           PYG-NUM-AFEC IN PYG
           ELSE
               MOVE OPC-NUM-DINP IN OPC TO FEC-DEC1
                                           PYG-NUM-DFEC IN PYG
               MOVE OPC-NUM-MINP IN OPC TO FEC-MEC1
                                           PYG-NUM-MFEC IN PYG
               MOVE OPC-NUM-SINP IN OPC TO FEC-SEC1
                                           PYG-NUM-SFEC IN PYG
               MOVE OPC-NUM-AINP IN OPC TO FEC-AEC1
                                           PYG-NUM-AFEC IN PYG.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-APRO IN WSS-VARI TO FEC-AEC2.

           MOVE FEC-FORM-FEC TO FEC-FORM.

           IF WSS-IND-CON IN WSS-VARI = 'S'
               MOVE FEC-DIF-DIA  TO FEC-CMND
           ELSE
               MOVE FEC-DIF-MES  TO FEC-CMND.

           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ERROR CAL DIF '
                       'ENTRE FUNO Y FDOS '
                        OPC-CIC-IOPC IN OPC ' '
                        FEC-FEC1 ' '
                        FEC-FEC2
               MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
               GO TO FIN-CALCULA-DIF-FECHAS.

           IF WSS-IND-CON IN WSS-VARI = 'S'
               MOVE FEC-NDIA TO WSS-NUM-DIME IN WSS-VARI
           ELSE
               MOVE FEC-NMES TO WSS-NUM-DIME IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'IND DIAS/MESES: ' PYG-IND-DIME IN PYG
               DISPLAY 'FECHA 1: ' FEC-FEC1
               DISPLAY 'FECHA 2: ' FEC-FEC2
               DISPLAY 'NUM DIAS: ' WSS-NUM-DIME IN WSS-VARI.
       FIN-CALCULA-DIF-FECHAS.
           EXIT.

      ******************************************************************
       GRABAR-ARCHIVO-PYG SECTION.
      *--------------------------
       INI-GRABAR-ARCHIVO-PYG.
           MOVE RDC-CIC-ICLI IN RDC      TO PYG-CIC-ICLI IN PYG.
           MOVE OPC-CIC-IOPC IN OPC      TO PYG-CIC-IOPC IN PYG.
           MOVE OPC-COD-COOC IN OPC      TO PYG-COD-COOC IN PYG.
           MOVE WSS-NUM-DIME IN WSS-VARI TO PYG-NUM-DIME IN PYG.
           MOVE WSS-VAL-SCRE IN WSS-VARI TO PYG-VAL-SCRE IN PYG.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-PYG.
           IF NOT FIO-STAT-OKS
               MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
               GO TO FIN-GRABAR-ARCHIVO-PYG.

           DISPLAY 'REG. GRABADO OK: ' PYG.
       FIN-GRABAR-ARCHIVO-PYG.
           EXIT.

      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'ESTADISTICAS.'.
           DISPLAY ' '.
           DISPLAY 'REGISTRO DLC LEIDOS           : '
                                    WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'REGISTRO DLC PROCESADOS       : '
                                    WSS-NUM-RPRO IN WSS-VARI.
           DISPLAY 'REGISTRO DLC NO SELECCIONADOS : '
                                    WSS-NUM-RNSE IN WSS-VARI.
           DISPLAY 'REGISTRO DLC ERRONEOS         : '
                                    WSS-NUM-RERR IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-PYG.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-DLS.
           PERFORM COL-FIO-RDC.
       FIN-CLOSE-FILE.
           EXIT.

      ******************************************************************

       COPY COLBFPYG.

       COPY COLBFOPC.
       COPY COLBFDLS.
       COPY COLBFRDC.
       COPY TABBFCAM.
       COPY TABBVOFI.
       COPY TABBFOFI.

       COPY GNSBGIDD.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBGCAM.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGPES.
