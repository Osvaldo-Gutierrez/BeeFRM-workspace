       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGTPC.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN. 17-DEC-1997
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CRI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
      *<<<< WSS
      *
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGCPT.
      *
       COPY COLBRACU.
       COPY COLBRTPC.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRCYA.
       COPY DEUBRCRI.
       COPY SGCBRDBC.
       COPY SGCBRDCP.
       COPY SGCBRDCE.
       COPY GNSWGFEC.
      *
       COPY GNSBRMSC.
       COPY GNSBRMSG.
       COPY GNSWGHOY.
       COPY GNSBRTAB.
      *
       01  WSS-VARI.
           03 WSS-TPC.
              05 WSS-GLS-STR1    VALUE 'SISTEMA : COL ' PIC X(14).
              05 WSS-GLS-STR2    VALUE 'PROCESO : '     PIC X(10).
              05 WSS-GLS-STR3    VALUE 'COLPGTPC   AL ' PIC X(14).
              05 WSS-FEC-FREP.
                 07  WSS-NUM-DREP                  PIC X(02).
                 07  WSS-FIL1          VALUE '/'   PIC X(01).
                 07  WSS-NUM-MREP                  PIC X(02).
                 07  WSS-FIL2          VALUE '/'   PIC X(01).
                 07  WSS-NUM-SREP                  PIC X(02).
                 07  WSS-NUM-AREP                  PIC X(02).
              05 WSS-REST                          PIC X(91).

           03 WSS-NUM-RCRI       VALUE ZEROES      PIC 9(11).
           03 WSS-NUM-RPRO       VALUE ZEROES      PIC 9(11).
           03 WSS-NUM-RLEI       VALUE ZEROES      PIC 9(11).
           03 WSS-IND-CRSB       VALUE SPACES      PIC X(02).
           03 WSS-IND-CRSS       VALUE SPACES      PIC X(02).
           03 WSS-IND-ACTU       VALUE SPACES      PIC X(01).
           03 WSS-IND-DISP       VALUE SPACES      PIC X(01).
           03 WSS-NUM-ICLI                         PIC 9(08).
           03 WSS-NUM-MNTO                         PIC 9(11).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROES  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROES  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROES  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROES  PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC        VALUE ZEROES  PIC 9(02).
              05 WSS-NUM-AFEC        VALUE ZEROES  PIC 9(02).
              05 WSS-NUM-MFEC        VALUE ZEROES  PIC 9(02).
              05 WSS-NUM-DFEC        VALUE ZEROES  PIC 9(02).
           03 WSS-COD-TDAT       VALUE SPACES      PIC X(06).
       PROCEDURE DIVISION.
      *==================*
       MAIN SECTION.
      *============*
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPGTPC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM LEE-PARAM.
           PERFORM PRO-ACU.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
      *-----------------*
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-ACU.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-TPC.
       FIN-OPEN-FILE.
           EXIT.

       LEE-PARAM SECTION.
      *-----------------*
       INI-LEE-PARAM.
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
           DISPLAY '****************************'.
      *Captura Fecha de Proceso
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FEC.DE PROC. (DDMMSSAA): ' WSS-FEC-FPRO IN WSS-VARI.

      * Captura Indicador de Actualizacion de tabla CRI (S/N)
           DISPLAY 'INDICADOR ACTUALIZACION TABLA DEU CRI: '.
           ACCEPT WSS-IND-ACTU IN WSS-VARI.
           DISPLAY WSS-IND-ACTU IN WSS-VARI.

           IF WSS-IND-ACTU IN WSS-VARI NOT = 'S' AND
              WSS-IND-ACTU IN WSS-VARI NOT = 'N'
               MOVE 'N' TO WSS-IND-ACTU IN WSS-VARI.

      * Captura Indicador de Actualizacion de tabla CRI (S/N)
           DISPLAY 'INDICADOR DISPLAY '
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY WSS-IND-DISP IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
              WSS-IND-DISP IN WSS-VARI NOT = 'N'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI.

      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.

       FIN-LEE-PARAM.
           EXIT.

       PRO-ACU SECTION.
       INI-PRO-ACU.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-ACU.
           IF NOT FIO-STAT-OKS
               DISPLAY '---------------------------------------------'
               DISPLAY 'ERROR EN EL PROCESO, ARCHIVO VACIO           '
               DISPLAY '"MOVIMIENTO ACUMULADO" ACU                   '
               DISPLAY '---------------------------------------------'
               DISPLAY ' ********** PROCESO AUTOCANCELADO ***********'
               DISPLAY '---------------------------------------------'
               PERFORM GNS-PRO-END.

           IF NOT FIO-STAT-OKS
              GO TO FIN-PRO-ACU.

      *REGISTRO CABECERA
           MOVE SPACES TO WSS-TPC.
           MOVE 'SISTEMA : COL ' TO WSS-GLS-STR1.
           MOVE 'PROCESO : '     TO WSS-GLS-STR2.
           MOVE 'COLPGTPC   AL ' TO WSS-GLS-STR3.
           MOVE WSS-NUM-DPRO     TO WSS-NUM-DREP.
           MOVE WSS-NUM-MPRO     TO WSS-NUM-MREP.
           MOVE WSS-NUM-SPRO     TO WSS-NUM-SREP.
           MOVE WSS-NUM-APRO     TO WSS-NUM-AREP.
           MOVE '/'              TO WSS-FIL1.
           MOVE '/'              TO WSS-FIL2.
           MOVE SPACES           TO WSS-REST.
           MOVE WSS-TPC TO TPC.
      *
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-TPC.


       LUP-PRO-ACU.
           IF ACU-FEC-FPRO IN ACU NOT = WSS-FEC-FFEC IN WSS-VARI
               GO TO LEE-NXT-ACU.

           ADD 1 TO WSS-NUM-RLEI.

           IF WSS-IND-DISP = 'S'
               DISPLAY 'OPERACION A PROCESAR : ' ACU-CIC-IOPC IN ACU.


           MOVE ACU-CIC-IOPC IN ACU TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE OPC : ' ACU-CIC-IOPC IN ACU
               GO TO LEE-NXT-ACU.

           MOVE ACU-CIC-IOPC IN ACU TO DLC-CIC-IOPC IN DLC.
           MOVE 1                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'NO EXISTE DLC PARA OPC : ' ACU-CIC-IOPC IN ACU
              GO TO LEE-NXT-ACU.

      *INI
           MOVE SPACES TO TPC.
           MOVE ZEROES TO TPC-NUM-ICLI IN TPC.
           MOVE ZEROES TO TPC-VAL-MTPC IN TPC.
           MOVE '-'    TO TPC-IND-GUIO IN TPC.
           MOVE '/'    TO TPC-IND-DVEN IN TPC.
           MOVE '/'    TO TPC-IND-MVEN IN TPC.
           MOVE '|'    TO TPC-IND-FI01 IN TPC
                          TPC-IND-FI02 IN TPC
                          TPC-IND-FI03 IN TPC
                          TPC-IND-FI04 IN TPC
                          TPC-IND-FI05 IN TPC
                          TPC-IND-FI06 IN TPC
                          TPC-IND-FI07 IN TPC
                          TPC-IND-FI08 IN TPC
                          TPC-IND-FI09 IN TPC
                          TPC-IND-FI10 IN TPC
                          TPC-IND-FI11 IN TPC
                          TPC-IND-FI12 IN TPC
                          TPC-IND-FI13 IN TPC.
      *

           IF WSS-IND-DISP = 'S'
               DISPLAY 'MOV/SCTB/SCBZ ==> ' ACU-COD-MVTO '/'
                                            DLC-IND-SCTB '/'
                                            DLC-IND-SCBZ.


      *iNGRESO CARTERA vENCIDA ( CURVCD     )
           IF ACU-COD-MVTO IN ACU = 'CUR'      AND
              DLC-IND-SCTB IN DLC = 'VCD'      AND
              DLC-IND-SCBZ IN DLC NOT > SPACES
                 MOVE 'IVCD' TO TPC-IND-ITPC IN TPC.

      *EGRESO A CARTERA vENCIDA ( CANVCD     )
           IF ACU-COD-MVTO IN ACU = 'CAN'      AND
              DLC-IND-SCTB IN DLC = 'VCD'      AND
              DLC-IND-SCBZ IN DLC NOT > SPACES
                 MOVE 'EVCD' TO TPC-IND-ITPC IN TPC.

      *iNGRESO COBRANZA JUDICIAL ( CURVCDJUDIC)
           IF ACU-COD-MVTO IN ACU = 'CUR'      AND
              DLC-IND-SCTB IN DLC = 'VCD'      AND
              DLC-IND-SCBZ IN DLC = 'JUDIC'
                 MOVE 'IJUD' TO TPC-IND-ITPC IN TPC.

      *EGRESO COBRANZA JUDICIAL ( CANVCDJUDIC)
           IF ACU-COD-MVTO IN ACU = 'CAN'      AND
              DLC-IND-SCTB IN DLC = 'VCD'      AND
              DLC-IND-SCBZ IN DLC = 'JUDIC'
                 MOVE 'EJUD' TO TPC-IND-ITPC IN TPC.

      *INGRESO A CASTIGO ( CURCAS     )
           IF ACU-COD-MVTO IN ACU = 'CUR'      AND
              DLC-IND-SCTB IN DLC = 'CAS'      AND
              DLC-IND-SCBZ IN DLC NOT > SPACES
                 MOVE 'ICAS' TO TPC-IND-ITPC IN TPC.

      *EGRESO A CASTIGO ( CANCAS     )
           IF ACU-COD-MVTO IN ACU = 'CAN'      AND
              DLC-IND-SCTB IN DLC = 'CAS'      AND
              DLC-IND-SCBZ IN DLC NOT > SPACES
                 MOVE 'ECAS' TO TPC-IND-ITPC IN TPC.

      *iNGRESO CASTIGO EXCLUIBLE ( CURCASNIS  )
           IF ACU-COD-MVTO IN ACU = 'CUR'      AND
              DLC-IND-SCTB IN DLC = 'CAS'      AND
              DLC-IND-SCBZ IN DLC = 'NIS'
      *        NO CONSIDERA VARIOS DEUDORES
               IF ( ACU-COD-TOOC IN ACU = 'VDE' OR
                    ACU-COD-TOOC IN ACU = 'GTO')
                     GO TO LEE-NXT-ACU
               ELSE
                   MOVE 'IEXC' TO TPC-IND-ITPC IN TPC.

      *EGRESO CASTIGO EXCLUIBLE ( CANCASNIS  )
           IF ACU-COD-MVTO IN ACU = 'CAN'      AND
              DLC-IND-SCTB IN DLC = 'CAS'      AND
              DLC-IND-SCBZ IN DLC = 'NIS'
      *        NO CONSIDERA VARIOS DEUDORES
               IF ( ACU-COD-TOOC IN ACU = 'VDE' OR
                    ACU-COD-TOOC IN ACU = 'GTO')
                     GO TO LEE-NXT-ACU
               ELSE
                   MOVE 'EEXC' TO TPC-IND-ITPC IN TPC.

           IF NOT ( TPC-IND-ITPC IN TPC = 'IVCD' OR
                    TPC-IND-ITPC IN TPC = 'EVCD' OR
                    TPC-IND-ITPC IN TPC = 'IJUD' OR
                    TPC-IND-ITPC IN TPC = 'EJUD' OR
                    TPC-IND-ITPC IN TPC = 'ICAS' OR
                    TPC-IND-ITPC IN TPC = 'ECAS' OR
                    TPC-IND-ITPC IN TPC = 'IEXC' OR
                    TPC-IND-ITPC IN TPC = 'EEXC' )
                     GO TO LEE-NXT-ACU.

      *FECHA VENCIMIENTO
           MOVE DLC-NUM-DMAV IN DLC TO TPC-NUM-DVEN IN TPC.
           MOVE DLC-NUM-MMAV IN DLC TO TPC-NUM-MVEN IN TPC.
           MOVE DLC-NUM-SMAV IN DLC TO TPC-NUM-SVEN IN TPC.
           MOVE DLC-NUM-AMAV IN DLC TO TPC-NUM-AVEN IN TPC.

      *MONTO
           MOVE ACU-SGV-ICTB IN ACU TO WSS-NUM-MNTO.
           MOVE WSS-NUM-MNTO        TO TPC-VAL-MTPC IN TPC.

      * OFICINA
           MOVE ACU-COD-OFOC IN ACU TO TPC-COD-OFOC IN TPC.

      * NUMERO DE OPERACION
           MOVE ACU-CAI-IOPC IN ACU TO TPC-CAI-IOPC IN TPC.
           MOVE ACU-IIC-IOPC IN ACU TO TPC-IIC-IOPC IN TPC.

      *CLASE
           MOVE ACU-COD-COOC IN ACU TO TPC-COD-COOC IN TPC.

      *MONEDA
           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'VLR' TO TAB-COD-TTAB IN TAB.
           MOVE ACU-COD-VCOC IN ACU TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-GLS-ABRV IN TAB TO TPC-GLS-VCOC IN TPC.

      *DATOS CLIENTE
           MOVE ACU-IDC-ICLI IN ACU TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               DISPLAY ' CLI INEX PARA OPC : ' ACU-CIC-IOPC IN ACU
               GO TO LEE-NXT-ACU
           ELSE
      *       RUT
              MOVE DBC-NUM-ICLI IN DBC TO WSS-NUM-ICLI
              MOVE WSS-NUM-ICLI        TO TPC-NUM-ICLI IN TPC
              MOVE DBC-VRF-ICLI IN DBC TO TPC-VRF-ICLI IN TPC
      *       NOMBRE
              MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
              PERFORM CPT-BLKS
              MOVE CPT-STRN            TO TPC-GLS-NOMC IN TPC.
      *    BANCA
      * Busca Codigo Tipo Banca
           MOVE DBC-CIC-ICLI IN DBC    TO DCP-CIC-ICLI IN DCP.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DCP.
           IF NOT FIO-STAT-OKS
               MOVE DBC-CIC-ICLI IN DBC TO DCE-CIC-ICLI IN DCE
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM SGC-FIO-DCE
               IF FIO-STAT-OKS
                   MOVE DCE-COD-TBAN IN DCE TO TPC-COD-TBAN IN TPC
               ELSE
                   MOVE SPACES              TO TPC-COD-TBAN IN TPC
           ELSE
              MOVE DCP-COD-TBAN IN DCP TO TPC-COD-TBAN IN TPC.

      *   CLASIFICACION
      * Busca Indicador de Clasificacion del Cliente segun Banco
           MOVE '9'                    TO CRI-IND-TCAR IN CRI.
           MOVE 99999999               TO CRI-FEC-FCLA IN CRI.
           MOVE DBC-CIC-ICLI IN DBC    TO CRI-CIC-ICLI IN CRI.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM DEU-FIO-CRI.
           IF NOT (FIO-STAT-OKS AND CRI-CIC-ICLI IN CRI =
                  DBC-CIC-ICLI IN DBC)
               MOVE 'SC'               TO TPC-IND-RIES IN TPC
               GO TO CON-BUS-CLAS
           ELSE
           IF WSS-IND-ACTU IN WSS-VARI = 'S' AND
              TPC-IND-ITPC IN TPC = 'IVCD' AND
              ( CRI-IND-CRSB IN CRI = 'A ' OR
                CRI-IND-CRSS IN CRI = 'A ' )
                 PERFORM DET-CLASI-CLI.

           MOVE CRI-IND-CRSB IN CRI TO TPC-IND-RIES IN TPC.

       CON-BUS-CLAS.

      *MANUAL O AUTOMATICO
           MOVE 'MAN'  TO TPC-IND-TTPC IN TPC.
           MOVE ACU-CIC-IOPC IN ACU TO CYA-CIC-IOPC IN CYA.
           MOVE ACU-NUM-ICAN IN ACU TO CYA-NUM-ICAN IN CYA.
           MOVE ZEROES              TO CYA-NUM-ICYA IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF FIO-STAT-OKS AND
               ACU-CIC-IOPC IN ACU = CYA-CIC-IOPC IN CYA AND
               ACU-NUM-ICAN IN ACU = CYA-NUM-ICAN IN CYA
               IF CYA-IND-TCYA IN CYA = 'PBCAS' OR
                  CYA-IND-TCYA IN CYA = 'PBVCD'
                   MOVE 'AUT' TO TPC-IND-TTPC IN TPC.

      * INDICADOR DE GARANTIA
           IF OPC-IND-GARA IN OPC > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'GARA'              TO MSC-COD-TMSC IN MSC
               MOVE OPC-IND-GARA IN OPC TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-ABRV IN MSC TO TPC-GLS-IGAR IN TPC.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-TPC.

           ADD 1 TO WSS-NUM-RPRO.

       LEE-NXT-ACU.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-ACU.
           IF FIO-STAT-OKS
              GO TO LUP-PRO-ACU.
       FIN-PRO-ACU.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-ACU.
           PERFORM COL-FIO-TPC.
           DISPLAY 'ESTADISTICAS '.
           DISPLAY '*******************************************'.
           DISPLAY 'REGISTROS LEIDOS           : ' WSS-NUM-RLEI.
           DISPLAY 'REGISTROS PROCESADOS       : ' WSS-NUM-RPRO.
           DISPLAY 'REGISTROS CRI ACTUALIZADOS : ' WSS-NUM-RCRI,
           DISPLAY '*******************************************'.
       FIN-CLOSE-FILE.
           EXIT.


       DET-CLASI-CLI SECTION.
       INI-DET-CLASI-CLI.
           MOVE CRI-IND-CRSB IN CRI TO WSS-IND-CRSB IN WSS-VARI.
           MOVE CRI-IND-CRSS IN CRI TO WSS-IND-CRSS IN WSS-VARI.

           IF CRI-IND-CRSB IN CRI = 'A '
               MOVE 'B ' TO CRI-IND-CRSB IN CRI.

           IF CRI-IND-CRSS IN CRI = 'A '
               MOVE 'B ' TO CRI-IND-CRSS IN CRI.

           MOVE HOY-FHOY TO CRI-FEC-FCLA IN CRI.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM DEU-FIO-CRI.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL CREAR REG. DEUCRI: '
                                 CRI-KEY-ICRI IN CRI ' ' FIO-STAT
               MOVE WSS-IND-CRSB IN WSS-VARI TO CRI-IND-CRSB IN CRI
               MOVE WSS-IND-CRSS IN WSS-VARI TO CRI-IND-CRSS IN CRI
               GO TO FIN-DET-CLASI-CLI.

           ADD 1 TO WSS-NUM-RCRI.

       FIN-DET-CLASI-CLI.
           EXIT.

       COPY GNSBGFEC.
       COPY GNSBPFEC.
      *
       COPY COLBFACU.
       COPY COLBFTPC.
       COPY COLBFOPC.
       COPY COLBFDLC.
       COPY COLBFCYA.
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY SGCBFDCE.
       COPY DEUBFCRI.
      *
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
      *
       COPY GNSBFMSG.
       COPY GNSBGHOY.
       COPY GNSBBMSC.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSBFMSC.
       COPY GNSBGCPT.
