       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGNTR.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN. 31-MAY-2000
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-NTR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
      *<<<< WSS
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGCPT.
      *
       COPY COLBRNTR.
       COPY COLBRDLC.
       COPY COLBRRDC.
       COPY COLBROPS.

       COPY COLBRINY.
       COPY COLBRCNT.
       COPY COLBRDNT.

       COPY GNSWGFEC.
      *
       COPY GNSBRMSC.
       COPY GNSBRMSG.
       COPY GNSWGHOY.
       COPY GNSBRTAB.
      *
       COPY TABBRIVB.
       COPY TABBROFI.
       COPY TABBRUSR.
      *
       COPY SGCBRDBC.

       01  WSS-VARI.
           03  WSS-IND-VEOS                       PIC X(01).
           03  WSS-NUM-RERR      VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RLEI      VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RPRO      VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RINY      VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RCNT      VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-RDNT      VALUE ZEROES     PIC 9(06).
           03  WSS-IND-DISP                       PIC X(01).

           03  WSS-COD-UAUX.
               05  WSS-COD-USUARIO                PIC X(09).
               05  WSS-COD-OFICINA                PIC X(03).

           03  WSS-FEC-FFEC.
               05  WSS-NUM-SFEC                   PIC 9(02).
               05  WSS-NUM-AFEC                   PIC 9(02).
               05  WSS-NUM-MFEC                   PIC 9(02).
               05  WSS-NUM-DFEC                   PIC 9(02).

           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO                   PIC 9(02).
               05  WSS-NUM-MPRO                   PIC 9(02).
               05  WSS-NUM-SPRO                   PIC 9(02).
               05  WSS-NUM-APRO.
                   07  WSS-NUM-APD1               PIC 9(01).
                   07  WSS-NUM-APD2               PIC 9(01).
      * NOTARIOS
           03  WSS-RET-NTR.
               05  AUX-VEC-MONTO                  PIC S9(13).
               05  AUX-VEC-RETEN                  PIC S9(13).
               05  FACT-ABO                       PIC 9(01)V9(04).
               05  FACT-IMP                       PIC 9(01)V9(04).
               05  K-FACT-ABO                     PIC 9(01)V9(04).
               05  K-FACT-IMP                     PIC 9(01)V9(04).
               05  K-IMP-NOT     VALUE 10         PIC 9(03).
      *
           03  WSS-CTA-NTR.
               05  WSS-ENC-NTR                    PIC X(04).
               05  WSS-GLS-CCTE                   PIC X(08).


       PROCEDURE DIVISION.
      *==================*
       MAIN SECTION.
      *============*
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPGNTR' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM LEE-PARAM.
           PERFORM PRO-GNOT.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
      *-----------------*
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-INY.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-CNT.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-DNT.
       FIN-OPEN-FILE.
           EXIT.

       LEE-PARAM SECTION.
      *-----------------*
       INI-LEE-PARAM.
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
           DISPLAY '****************************'.
      *Captura Fecha de Proceso
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FEC.DE PROC. (DDMMSSAA): ' WSS-FEC-FPRO IN WSS-VARI.

      * Captura Indicador de Actualizacion de tabla CRI (S/N)
           DISPLAY 'INDICADOR DISPLAY '
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY WSS-IND-DISP IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
              WSS-IND-DISP IN WSS-VARI NOT = 'N'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI.

      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.

       FIN-LEE-PARAM.
           EXIT.

       PRO-GNOT SECTION.
       INI-PRO-GNOT.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           IF NOT FIO-STAT-OKS
               DISPLAY ' NO EXISTEN REGISTRO EN ARCHIVO OPS'
               GO TO FIN-PRO-GNOT.

       LUP-PRO-GNOT.

           IF OPC-FEC-FINP IN OPS < WSS-FEC-FFEC IN WSS-VARI
               GO TO NXT-LEE-OPS.

           IF OPC-FEC-FINP IN OPS > WSS-FEC-FFEC IN WSS-VARI
               GO TO FIN-PRO-GNOT.

           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

           MOVE OPC-CIC-IOPC IN OPS TO DLC-CIC-IOPC IN DLC.
           MOVE ZEROES              TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS         TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT ( FIO-STAT-OKS AND
              OPC-CIC-IOPC IN OPS = DLC-CIC-IOPC IN DLC )
               DISPLAY 'DLC SIN OPC ' OPC-CIC-IOPC IN OPS
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-LEE-OPS.

       LUP-LEE-DLC.

           IF DLC-VAL-GNOT IN DLC NOT > ZEROES
               GO TO NXT-LEE-DLC.

           IF WSS-IND-DISP = 'S'
               DISPLAY 'OPC      : ' OPC-CIC-IOPC IN OPS
               DISPLAY 'GNOT     : ' DLC-VAL-GNOT IN DLC.

           MOVE 'TAB'               TO FIO-SIST.
           MOVE 'NOT'               TO TAB-COD-TTAB IN TAB.
           MOVE DLC-COD-NOTC IN DLC TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE TAB TO IVB.

           COMPUTE K-FACT-ABO IN WSS-VARI = 100 / ( K-IMP-NOT + 100 ).
           COMPUTE K-FACT-IMP IN WSS-VARI = 1 - K-FACT-ABO IN WSS-VARI.

           MOVE ZEROES              TO AUX-VEC-RETEN IN WSS-VARI.
           MOVE ZEROES              TO AUX-VEC-MONTO IN WSS-VARI.

           PERFORM PRO-NTR.
           IF MSG-COD-MENS IN MSG > SPACES
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-LEE-OPS.

           COMPUTE AUX-VEC-RETEN ROUNDED =
                   DLC-VAL-GNOT IN DLC * FACT-ABO IN WSS-VARI.

           IF FACT-IMP NOT = 0
               COMPUTE  AUX-VEC-MONTO =
                        DLC-VAL-GNOT IN DLC - AUX-VEC-RETEN IN WSS-VARI.

           PERFORM GRA-REG-INY.

           IF AUX-VEC-MONTO IN WSS-VARI > ZEROES
               PERFORM GRA-REG-CNT.

           PERFORM GRA-REG-DNT.

           ADD 1 TO WSS-NUM-RPRO IN WSS-VARI.

       NXT-LEE-DLC.

           MOVE FIO-GET-NXT         TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF FIO-STAT-OKS AND
              OPC-CIC-IOPC IN OPS = DLC-CIC-IOPC IN DLC
               GO TO LUP-LEE-DLC.


       NXT-LEE-OPS.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPS
           IF FIO-STAT-OKS
               GO TO LUP-PRO-GNOT.


       FIN-PRO-GNOT.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           PERFORM COL-FIO-INY.
           PERFORM COL-FIO-CNT.
           PERFORM COL-FIO-DNT.
           DISPLAY 'ESTADISTICAS '.
           DISPLAY '*******************************************'.
           DISPLAY 'REGISTROS OPC LEIDOS VALIDOS : ' WSS-NUM-RLEI.
           DISPLAY 'REGISTROS PROCESADOS POR NOT : ' WSS-NUM-RPRO.
           DISPLAY 'REGISTROS INY ACTUALIZADOS   : ' WSS-NUM-RINY.
           DISPLAY 'REGISTROS CNT ACTUALIZADOS   : ' WSS-NUM-RCNT.
           DISPLAY 'REGISTROS DNT ACTUALIZADOS   : ' WSS-NUM-RDNT.
           DISPLAY '*******************************************'.
       FIN-CLOSE-FILE.
           EXIT.

       PRO-NTR SECTION.
       INI-PRO-NTR.
           MOVE SPACES                TO MSG-COD-MENS IN MSG.
           MOVE DLC-COD-NOTC IN DLC   TO NTR-COD-NOTA IN NTR.
           MOVE OPC-COD-OFOC IN OPS   TO NTR-COD-OFIC IN NTR.
           MOVE DLC-COD-TDOC IN DLC   TO NTR-COD-TDOC IN NTR.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-NTR.
           IF NOT FIO-STAT-OKS
              MOVE 'COL'          TO MSG-COD-SIST
              MOVE 'NTR  NOEXI' TO MSG-COD-MENS
              PERFORM GET-MSG
              DISPLAY MSG-GLS-DESC   OPC-CIC-IOPC IN OPS
              GO TO FIN-PRO-NTR
           ELSE
              IF NTR-IND-RETE = 'S'
                 MOVE K-FACT-ABO IN WSS-VARI TO FACT-ABO IN WSS-VARI
                 MOVE K-FACT-IMP IN WSS-VARI TO FACT-IMP IN WSS-VARI
              ELSE
                 MOVE 0           TO FACT-IMP IN WSS-VARI
                 MOVE 1           TO FACT-ABO IN WSS-VARI.

           IF WSS-IND-DISP = 'S'
               DISPLAY 'IND-RETE : ' NTR-IND-RETE IN NTR.

       FIN-PRO-NTR.
           EXIT.

       GRA-REG-INY SECTION.
       INI-GRA-REG-INY.
      *
      *    Tipo de Registro.
           MOVE 5                   TO INY-NUM-TREG IN INY.
      *
      *    Numero de Cuenta.
           MOVE NTR-GLS-CCTE IN NTR TO WSS-CTA-NTR IN WSS-VARI.
           MOVE WSS-GLS-CCTE        TO INY-NUM-NCTA IN INY.
      *
      *    Imputacion.
           MOVE 9                   TO INY-NUM-IMPU IN INY.
      *
      *    Serial del Cheque.
           MOVE SPACES              TO INY-COD-SCHE IN INY.
      *
      *    Monto.
           IF FACT-IMP NOT = 0
               MOVE AUX-VEC-RETEN       TO INY-SGV-MNTO IN INY
           ELSE
               MOVE DLC-VAL-GNOT IN DLC TO INY-SGV-MNTO IN INY.
      *
      *    Tipo de Movimiento.
           MOVE 'T'                 TO INY-COD-TMVO IN INY.
      *
      *    Codigo de Banco.
           MOVE ZEROES              TO INY-NUM-CBAN IN INY.
      *
      *    Disponible 1.
           MOVE SPACES              TO INY-COD-DIS1 IN INY.
      *
      *    Fecha de Movimiento.
           MOVE WSS-NUM-DPRO        TO INY-NUM-DMVO IN INY.
           MOVE WSS-NUM-MPRO        TO INY-NUM-MMVO IN INY.
           MOVE WSS-NUM-APD2        TO INY-NUM-AMVO IN INY.
      *
      *    Transaccion ATM.
           MOVE '0'                 TO INY-IND-TATM IN INY.
      *
      *    Cod. Int. Comp. Operacion Credito
           MOVE SPACES              TO INY-CIC-IOPC IN INY.
      *
      *    Disponible 2.
           MOVE SPACES              TO INY-GLS-DIS2 IN INY.
      *
      *    Sucursal Origen.
           MOVE SPACES              TO INY-COD-SORI IN INY.
      *
      *    Numero Identificacion Cancelacion
           MOVE ZEROES              TO INY-NUM-ICAN IN INY.
      *
      *    Disponible 3.
           MOVE SPACES              TO INY-GLS-DIS3 IN INY.
      *
      *    Sucursal Receptora.
           MOVE SPACES              TO INY-COD-SREC IN INY.
      *
      *    Disponible 4.
           MOVE SPACES              TO INY-GLS-DIS4 IN INY.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-INY.

           ADD 1 TO WSS-NUM-RINY IN WSS-VARI.

       FIN-GRA-REG-INY.
           EXIT.

       GRA-REG-CNT SECTION.
       INI-GRA-REG-CNT.
      *
      *    Cuenta Contable.
           MOVE 301002524            TO CNT-GLS-CCTE IN CNT.
      *
      *    Fecha de proceso
           MOVE WSS-NUM-MPRO         TO CNT-NUM-MPRO IN CNT.
           MOVE WSS-NUM-APRO         TO CNT-NUM-APRO IN CNT.
      *
      *    Numero de sucursal
           MOVE OPC-COD-OFOC IN OPS  TO CNT-COD-OFOC IN CNT.
      *
      *    Numero de RUT
           MOVE IVB-NUM-IIVB IN IVB  TO CNT-NUM-RUTN IN CNT.
      *
      *    Digito verificador
           MOVE IVB-VRF-IIVB IN IVB  TO CNT-VRF-RUTN IN CNT.
      *
      *    Monto Bruto
           MOVE DLC-VAL-GNOT IN DLC  TO CNT-VAL-MNTO IN CNT.
      *
      *    Monto Retencion
           MOVE AUX-VEC-MONTO        TO CNT-VAL-RETE IN CNT.
      *
      *    Nombre Notario
           MOVE IVB-GLS-DESC IN IVB  TO CNT-GLS-NNOT IN CNT.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CNT.

           ADD 1 TO WSS-NUM-RCNT IN WSS-VARI.

       FIN-GRA-REG-CNT.
           EXIT.

       GRA-REG-DNT SECTION.
       INI-GRA-REG-DNT.
      *
      * Fecha de proceso.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO DNT-NUM-DPRO IN DNT.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO DNT-NUM-MPRO IN DNT.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO DNT-NUM-SPRO IN DNT.
           MOVE WSS-NUM-APRO IN WSS-VARI TO DNT-NUM-APRO IN DNT.
      *
      *    Nombre Notario
           MOVE IVB-GLS-DESC IN IVB  TO DNT-GLS-NOTC IN DNT.
      *
      *    RUT del notario
           MOVE IVB-NUM-IIVB IN IVB  TO DNT-NUM-RUTN IN DNT.
      *
      *    Digito verificador
           MOVE IVB-VRF-IIVB IN IVB  TO DNT-VRF-RUTN IN DNT.
      *
      *    Cuenta Abono
           MOVE INY-NUM-NCTA IN INY  TO DNT-GLS-NCTA IN DNT.
      *
      *    Numero de Operacion
           MOVE OPC-CIC-IOPC IN OPS TO DNT-CIC-IOPC IN DNT.
      *
      *    Monto del Credito
           MOVE OPC-VAL-CRED IN OPS TO DNT-VAL-CRED IN DNT.
      *
      *    Monto del Notario
           MOVE DLC-VAL-GNOT IN DLC TO DNT-VAL-GNOT IN DNT.
      *
      *    Codigo de Usuario
           MOVE OPC-COD-ATRN IN OPS TO DNT-COD-USER IN DNT.
      *
      *
           MOVE OPC-COD-OFOC IN OPS TO DNT-COD-OFOC IN DNT.
           MOVE DLC-COD-NOTC IN DLC TO DNT-COD-NOTC IN DNT.

           MOVE DLC-CIC-IOPC IN DLC TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'OPC-SIN-CLIENTE' OPC-CIC-IOPC IN OPS
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO FIN-GRA-REG-DNT.

           MOVE RDC-CIC-ICLI IN RDC TO DNT-CIC-ICLI IN DNT.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'OPC-SIN-DBC    ' OPC-CIC-IOPC IN OPS
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO FIN-GRA-REG-DNT.

           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
           PERFORM CPT-BLKS
           MOVE CPT-STRN            TO DNT-GLS-NOMC IN DNT.

      *
           MOVE DNT-COD-USER IN DNT     TO USR-COD-USER IN USR.
           PERFORM BUS-USR
           MOVE USR-GLS-DESC IN USR TO DNT-GLS-USER IN DNT.

      *    Oficina cabecera
           MOVE 'COL' TO FIO-SIST.
           MOVE 'VEO'                    TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-ATRN IN OPS      TO WSS-COD-USUARIO.
           MOVE SPACES                   TO WSS-COD-OFICINA.
           MOVE WSS-COD-UAUX IN WSS-VARI TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.

           MOVE 'N' TO WSS-IND-VEOS.

       LUP-LEE-NUEVA-VEO.
           PERFORM GNS-FIO-TAB.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-UAUX IN WSS-VARI.

           IF FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'VEO' AND
              WSS-COD-USUARIO IN WSS-VARI = OPC-COD-ATRN IN OPS
               IF TAB-IND-VIGE IN TAB = 'N'
                   MOVE FIO-GET-NXT TO FIO-CMND
                   GO TO LUP-LEE-NUEVA-VEO
               ELSE
                   MOVE 'S' TO WSS-IND-VEOS.

           IF WSS-IND-VEOS IN WSS-VARI = 'S'
               MOVE TAB-GLS-DATA IN TAB TO DNT-COD-OFIC IN DNT
           ELSE
               MOVE '090'               TO DNT-COD-OFIC IN DNT.

           MOVE DNT-COD-OFIC IN DNT     TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI
           MOVE OFI-GLS-DESC IN OFI TO DNT-GLS-OFIC IN DNT.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-DNT.

           ADD 1 TO WSS-NUM-RDNT IN WSS-VARI.


       FIN-GRA-REG-DNT.
           EXIT.

       COPY GNSBGFEC.
       COPY GNSBPFEC.
      *
       COPY COLBFOPS.
       COPY COLBFNTR.
       COPY COLBFRDC.
       COPY COLBFDLC.

       COPY COLBFINY.
       COPY COLBFCNT.
       COPY COLBFDNT.

      *
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY SGCBFDBC.
      *
       COPY GNSBFMSG.
       COPY GNSBGHOY.
       COPY GNSBBMSC.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBFTAB.
       COPY GNSBFMSC.
       COPY GNSBGCPT.
       COPY TABBFOFI.
       COPY TABBFUSR.
       COPY TABBBUSR.
       COPY TABBBOFI.
