       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGCVD.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.
      * Programa Generador de Vencimientos para el Archivo MCD.
      * RBG / 15-OCT-1996 Para la confeccion de este programa se tomo como
      * base el programa COLPGCPR.
      *
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ERC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-REV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MCD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *<<<< WSS
       COPY COLBRCYA.
       COPY COLBRERC.
       COPY COLBRREV.
       COPY COLBROPC.
       COPY COLBRVEN.
       COPY COLBRDLC.
       COPY COLBRMCD.
       COPY COLBRTOC.
       COPY TABBRTIO.
       COPY TABBRCAM.
       COPY COLBREVC.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
       01  PAR-VARI.
      * FECHA DEL PROCESO
           03  PAR-FEC-FTAR.
               05  PAR-FIL-1                      PIC X(01).
               05  PAR-FEC-FTA1.
                   07  PAR-NUM-DTAR               PIC 9(02).
                   07  PAR-FIL-SLA1               PIC X(01).
                   07  PAR-NUM-MTAR               PIC 9(02).
                   07  PAR-FIL-SLA2               PIC X(01).
                   07  PAR-NUM-ATAR               PIC 9(02).
           03  PAR-FEC-FDES.
               05  PAR-NUM-DDES                   PIC 9(02).
               05  PAR-NUM-MDES                   PIC 9(02).
               05  PAR-NUM-SDES                   PIC 9(02).
               05  PAR-NUM-ADES                   PIC 9(02).
      * INDICE PARA ANALISIS DE VECTOR
           03  PAR-NUM-IVEC                       PIC 9(02).
      * VECTOR DE OPERACIONES O VENCIMIENTOS
           03  PAR-VEC-IVEN.
               05  PAR-COL-IVEN      OCCURS 6.
      *
      *           IDENTIFICACION VENCIMIENTO / OPERACION
                  07  PAR-KEY-IVEN.
      *
      *               COD. INT. COMP. OPERACION CREDITO
                      09  PAR-CIC-IOPC.
      *
      *                   CENTRO ASIGNACION IDENTIFICACION
                          11  PAR-CAI-IOPC.
                              13  PAR-COD-TOPC    PIC X(01).
                              13  PAR-COD-COPC    PIC X(03).
      *
      *                   IDENT. INTERNA COMPUTACIONAL
                          11 PAR-IIC-IOPC.
                             13 PAR-COD-OOPC.
                                15  PAR-COD-SOPC  PIC X(02).
                                15  PAR-IND-SOPC  PIC X(01).
                             13 PAR-GLS-NOPC      PIC X(05).
                      09 PAR-FIL-IOPC             PIC X(01).
      * Vector de Monedas
           03  PAR-VEC-VCOC.
               05  PAR-COL-VCOC      OCCURS 10.
                   07 PAR-COD-VCOC                PIC X(06).
                   07 PAR-FIL-VCOC                PIC X(01).
      * Vector de Sucursales
           03  PAR-VEC-OFOC.
               05 PAR-COL-OFOC      OCCURS 10.
                  07  PAR-COD-OFOC                PIC X(03).
                  07  PAR-FIL-VCOC                PIC X(01).
      * Vector de Lineas
           03 PAR-VEC-COOC.
              05 PAR-COL-COOC      OCCURS 10.
                 07  PAR-COD-COOC                 PIC X(06).
                 07  PAR-FIL-COOC                 PIC X(01).
      * INDICADOR DE REGISTRO SELECCIONADO
           03  PAR-IND-RSEL                       PIC 9(01).
               88  REG-NO-SELECCIONADO        VALUE 0.
               88  REG-SELECCIONADO           VALUE 1.
       01  WSS-VARI.
           03  WSS-SGV       VALUE 1       COMP   PIC 9(1).
      * Variables no Usadas RBG.
           03  WSS-IND1      VALUE ZEROES         PIC 9(04).
           03  WSS-OPEC                           PIC X(01).
           03  WSS-FEC-FFEC.
               05  WSS-NUM-SFEC                   PIC 9(02).
               05  WSS-NUM-AFEC                   PIC 9(02).
               05  WSS-NUM-MFEC                   PIC 9(02).
               05  WSS-NUM-DFEC                   PIC 9(02).
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                   PIC 9(02).
               05  WSS-NUM-MINI                   PIC 9(02).
               05  WSS-NUM-SINI                   PIC 9(02).
               05  WSS-NUM-AINI                   PIC 9(02).
           03  WSS-FEC-FPRI.
               05  WSS-NUM-SPRI                   PIC 9(02).
               05  WSS-NUM-APRI                   PIC 9(02).
               05  WSS-NUM-MPRI                   PIC 9(02).
               05  WSS-NUM-DPRI                   PIC 9(02).
           03  WSS-FEC-FMIN.
               05  WSS-NUM-SMIN                   PIC 9(02).
               05  WSS-NUM-AMIN                   PIC 9(02).
               05  WSS-NUM-MMIN                   PIC 9(02).
               05  WSS-NUM-DMIN                   PIC 9(02).
           03  WSS-ES-VEN-IOPC  VALUE 'NO'        PIC X(02).
           03  WSS-COD-VEN                        PIC X(03).
           03  WSS-COD-SVCD      VALUE LOW-VALUES PIC X(08).
           03  WSS-ANT-NTAS                       PIC X(03).
           03  WSS-IND-TTAS.
               05  WSS-GLS-NTAS                   PIC X(03).
               05  WSS-GLS-STAS                   PIC X(03).
           03  WSS-VEC-MOV OCCURS 100 TIMES INDEXED BY MOV-I MOV-TOPE.
               05  VEC-CPTO                       PIC X(06).
               05  VEC-MONTO                      PIC S9(11)V9(04).
      * MONTOS
           03  WSS-SGV-ICPN                       PIC S9(11)V9(04).
      * CONTADORES
           03  WSS-NUM-ROPC      VALUE 0          PIC 9(06).
           03  WSS-NUM-RGRA      VALUE 0          PIC 9(06).
           03  WSS-IND-ERRO                       PIC 9(01).
               88 ERROR-EN-PROCESO           VALUE 1.
      *
      * Variable utilizada para calcular la diferencia de VEN-VAL-FINA Y
      *                                                   VEN-VAL-CAPI.
           03  WSS-VAL-FINA  VALUE ZEROES         PIC 9(11)V9(04).
           03  WSS-VAL-CAPI  VALUE ZEROES         PIC 9(11)V9(04).
           03  WSS-VAL-REST  VALUE ZEROES         PIC 9(11)V9(04).
           03  WSS-IND-SOFI  VALUE 'N'            PIC X(01).
      * Variable para desplegar o no los Display del Programa.
           03  WSS-IND-DISP  VALUE 'S'            PIC X(01).
           03  WSS-MSC-TVOC                       PIC X(06).
           03  WSS-COD-PESO                       PIC X(06).
           03  WSS-SGV-VCAM                       PIC S9(11)V9(04).
      *
           03  AUX-CIC-IOPC  VALUE SPACES         PIC X(12).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGCVD' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM INI-VAR.
           PERFORM PRO-VEN.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           DISPLAY ' VENCIMIENTOS SELECIONADOS '
                                        WSS-NUM-ROPC IN WSS-VARI.
           DISPLAY ' REGISTROS GRABADOS        '
                                        WSS-NUM-RGRA IN WSS-VARI.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-DLC.
           MOVE 'COL' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE 'TAB' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-EXT TO FIO-CMND.
           PERFORM COL-FIO-MCD.
           PERFORM COL-FIO-ERC.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-REV.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       INI-VAR SECTION.
      *****************
       INI-INI-VAR.
      *<<<< INI_INI
           DISPLAY ' INICIO CAPTURA DE PARAMETROS '.
           DISPLAY ' ============================ '.
           MOVE 1  TO PAR-NUM-IVEC IN PAR-VARI.
      * Captura Fecha de Proceso.
           ACCEPT  PAR-FEC-FTAR IN PAR-VARI.
           DISPLAY ' '.
           DISPLAY ' FECHA DE PROCESO (*DD/MM/AA): 'PAR-FEC-FTA1
                                                         IN PAR-VARI.
           MOVE PAR-NUM-DTAR IN PAR-VARI TO PAR-NUM-DDES IN PAR-VARI.
           MOVE PAR-NUM-MTAR IN PAR-VARI TO PAR-NUM-MDES IN PAR-VARI.
           MOVE PAR-NUM-ATAR IN PAR-VARI TO PAR-NUM-ADES IN PAR-VARI.
           IF PAR-NUM-ADES IN PAR-VARI < 50
              MOVE 20                    TO PAR-NUM-SDES IN PAR-VARI
           ELSE
              MOVE 19                    TO PAR-NUM-SDES IN PAR-VARI.
      * Captura Sucursal.
           MOVE SPACES TO PAR-VEC-OFOC IN PAR-VARI.
           ACCEPT  PAR-VEC-OFOC        IN PAR-VARI.
           DISPLAY ' SUCURSAL (*/XXX )           : ' PAR-VEC-OFOC
                                                            IN PAR-VARI.
      * Captura Moneda.
           MOVE SPACES TO PAR-VEC-VCOC IN PAR-VARI.
           ACCEPT  PAR-VEC-VCOC        IN PAR-VARI.
           DISPLAY ' MONEDA (*/XXXXXX )          : ' PAR-VEC-VCOC
                                                            IN PAR-VARI.
      * Captura Linea (Tipo Operacion y Codigo Auxiliar).
           MOVE SPACES TO PAR-VEC-COOC IN PAR-VARI.
           ACCEPT  PAR-VEC-COOC        IN PAR-VARI.
           DISPLAY ' LINEA  (*/XXXXXX)           : ' PAR-VEC-COOC
                                                            IN PAR-VARI.
      * Captura Numero de Operacion o Vencimiento.
           MOVE SPACES TO PAR-VEC-IVEN IN PAR-VARI.
           ACCEPT  PAR-VEC-IVEN IN PAR-VARI.
           DISPLAY ' NRO OPE/VEN (*/XXXXXXXXXXXX): ' PAR-VEC-IVEN
                                                            IN PAR-VARI.
      * Captura Indicador para desplegar los Display en el programa.
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY ' INDICADOR DE DISPLAY (S/N)  : ' WSS-IND-DISP
                                                            IN WSS-VARI.
      * Valida codigo de display (S/N).
           IF WSS-IND-DISP IN WSS-VARI NOT = ('S' AND 'N')
               DISPLAY ' Lo siento, codigo de Display no es valido, '
               DISPLAY ' Debe Ingresar S o N '
               PERFORM GNS-PRO-ABT
               GO TO FIN-INI-VAR.
      * VAL-FEC VALIDA FECHA PROCESO (DES).
      *    DISPLAY  PAR-FEC-FDES IN PAR-VARI.
           MOVE PAR-FEC-FDES    IN PAR-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'FECVDOCNEX'   TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT
               GO TO FIN-INI-VAR.
      *
      * Mueve Fechas DD-MM-SSAA a Formato SSAA-MM-DD.
           MOVE PAR-NUM-DDES IN PAR-VARI TO WSS-NUM-DFEC IN WSS-VARI
                                            WSS-NUM-DMIN IN WSS-VARI.
           MOVE PAR-NUM-MDES IN PAR-VARI TO WSS-NUM-MFEC IN WSS-VARI
                                            WSS-NUM-MMIN IN WSS-VARI. 
           MOVE PAR-NUM-SDES IN PAR-VARI TO WSS-NUM-SFEC IN WSS-VARI
                                            WSS-NUM-SMIN IN WSS-VARI.
           MOVE PAR-NUM-ADES IN PAR-VARI TO WSS-NUM-AFEC IN WSS-VARI
                                            WSS-NUM-AMIN IN WSS-VARI.
       BUS-DIA-HBL.
           MOVE WSS-NUM-DMIN IN WSS-VARI TO FEC-ITM1 IN FEC-FECH.
           MOVE WSS-NUM-MMIN IN WSS-VARI TO FEC-ITM2 IN FEC-FECH.
           MOVE WSS-NUM-SMIN IN WSS-VARI TO FEC-ITM3 IN FEC-FECH.
           MOVE WSS-NUM-AMIN IN WSS-VARI TO FEC-ITM4 IN FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM CAL-FEC.
           MOVE FEC-FECH TO WSS-FEC-FINI.
      *VAL-FEC Validar Fecha
           MOVE WSS-FEC-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER 
               MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DMIN IN WSS-VARI
               MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MMIN IN WSS-VARI
               MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SMIN IN WSS-VARI
               MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AMIN IN WSS-VARI
               GO TO BUS-DIA-HBL
           ELSE
               MOVE WSS-NUM-DMIN IN WSS-VARI TO WSS-NUM-DINI IN WSS-VARI
               MOVE WSS-NUM-MMIN IN WSS-VARI TO WSS-NUM-MINI IN WSS-VARI
               MOVE WSS-NUM-SMIN IN WSS-VARI TO WSS-NUM-SINI IN WSS-VARI
               MOVE WSS-NUM-AMIN IN WSS-VARI TO WSS-NUM-AINI IN WSS-VARI.
         
      *
      * Muevo Fechas DD-MM-SSAA a Formato SSAA-MM-DD.
           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DPRI IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MPRI IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SPRI IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-APRI IN WSS-VARI.
           DISPLAY ' Este es el Rango de Fechas Selecionado. '
           DISPLAY ' Fecha Inicial: ' WSS-FEC-FPRI IN WSS-VARI.
           DISPLAY ' Fecha Final  : ' WSS-FEC-FFEC IN WSS-VARI.
           DISPLAY ' '.
      *
      * Obtiene COD movimiento mediante el CIC.
           MOVE 'COL'          TO FIO-SIST.
           MOVE 'MVT'          TO TAB-CIC-TTAB IN TAB.
           MOVE 'VEN'          TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'       TO MSG-COD-SIST
               MOVE 'CICVENNEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT
               GO TO FIN-INI-VAR.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-VEN IN WSS-VARI.
      *
      * Busco el CIC de la moneda.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CICPESONEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT
               GO TO FIN-INI-VAR.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'         TO MSG-COD-SIST
               MOVE 'CICTVAL0NEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT
               GO TO FIN-INI-VAR.
           MOVE MSC-COD-CMSC IN MSC TO WSS-MSC-TVOC IN WSS-VARI.
      *
      * Busco codigo de situacion contable "VENCIDA".
           MOVE 'COL'          TO MSC-COD-SIST.
           MOVE 'SCTB'         TO MSC-CIC-TMSC IN MSC.
           MOVE 'VCD'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS = SPACES
               MOVE MSC-COD-CMSC IN MSC TO WSS-COD-SVCD
           ELSE
               DISPLAY 'ABORTO : SIS = COL, MSC = SCTB, '
                       'MSC-CIC-CMSC = VCD'
               PERFORM GNS-PRO-ABT
               GO TO FIN-INI-VAR.
           MOVE SPACES TO MCD.
       FIN-INI-VAR.
           EXIT.

      ******************************************************************
       PRO-VEN SECTION.
      *****************
       INI-PRO-VEN.
           MOVE WSS-FEC-FPRI IN WSS-VARI TO VEN-FEC-FVEN IN VEN.
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       LUP-PRO-VEN.
           MOVE SPACES TO MSG-COD-MENS.
           ADD 1 TO WSS-IND1 IN WSS-VARI.
           IF NOT FIO-STAT-OKS OR
              VEN-FEC-FVEN IN VEN > WSS-FEC-FFEC IN WSS-VARI
              GO TO FIN-PRO-VEN.
      * Acceso a la OPC.
           PERFORM LEE-OPC.
           IF MSG-COD-MENS IN MSG > SPACES
              DISPLAY ' Fallo Lectura OPC: ' WSS-IND1 IN WSS-VARI
              GO TO ERR-PRO-VEN.
      * Proceso de seleccion y clasificacion a partir del registro VEN.
           PERFORM SEL-REG-VEN.
           IF REG-NO-SELECCIONADO
               IF WSS-IND-DISP IN WSS-VARI = 'S'
                   DISPLAY ' No se selecciono: ' WSS-IND1 IN WSS-VARI
                   GO TO NXT-PRO-VEN
               ELSE
                   GO TO NXT-PRO-VEN.
      * Busca sistema en donde opera.
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'TIO'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-TOOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
           DISPLAY ' Fallo BUS-TAB: ' WSS-IND1 IN WSS-VARI
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'TIO    NEX'   TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' ' OPC-KEY-IOPC IN OPC
                       ' ' TAB-COD-CTAB IN TAB
               PERFORM GRABAR-ERROR
               GO TO NXT-PRO-VEN.
           MOVE TAB TO TIO.
           IF TIO-GLS-SIST IN TIO NOT = 'COL'
              DISPLAY ' Fallo TIO-GLS-SIST: ' WSS-IND1 IN WSS-VARI
               GO TO NXT-PRO-VEN.
      * Acceso a la DLC.
           PERFORM LEE-DLC.
           IF MSG-COD-MENS IN MSG > SPACES
              DISPLAY ' Fallo LEE-DLC: ' WSS-IND1 IN WSS-VARI
              GO TO ERR-PRO-VEN.
      * Aumenta Contador de Opreaciones Leidas OK.
           ADD  1  TO WSS-NUM-ROPC IN WSS-VARI.
      * Limpia Vectores para Guardar Datos.
           PERFORM LIM-VEC.
      * Guarda en Vectores Datos del Registro VEN.
           PERFORM CAR-MOV-VEN.
      * Graba Registros en Archivo MCD desde Vectores.
           PERFORM GRA-MOV.
           GO TO NXT-PRO-VEN.
       ERR-PRO-VEN.
           DISPLAY ' Estoy en ERR-PRO-VEN: ' WSS-IND1 IN WSS-VARI.
           MOVE 'COL' TO MSG-COD-SIST.
           PERFORM GET-MSG.
           DISPLAY MSG-GLS-DESC ' ' VEN-KEY-IVEN IN VEN.
           PERFORM GRABAR-ERROR.
           MOVE SPACES TO MSG-COD-SIST MSG-GLS-DESC.
       NXT-PRO-VEN.
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           GO TO LUP-PRO-VEN.
       FIN-PRO-VEN.
           EXIT.

      ******************************************************************
       GRABAR-ERROR SECTION.
      **********************
       INI-GRA-ERR.
           DISPLAY ' Estoy en GRABAR-ERROR: '
                                  WSS-IND1 IN WSS-VARI.
           MOVE VEN-CIC-IOPC IN VEN TO ERC-CIC-IOPC IN ERC.
           MOVE OPC-COD-OFOC IN OPC TO ERC-COD-OFIC IN ERC.
           MOVE 'VEN'               TO ERC-COD-EVEN IN ERC.
           MOVE 'CVD'               TO ERC-COD-PGRM IN ERC.
           MOVE VEN-NUM-IVEN IN VEN TO ERC-NUM-CANC IN ERC.
           MOVE VEN-VAL-FINA IN VEN TO ERC-VAL-MNTO IN ERC.
           MOVE MSG-COD-MENS IN MSG TO ERC-COD-MSGE IN ERC.
           MOVE SPACES              TO ERC-GLS-DISP IN ERC.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-ERC.
       FIN-GRA-ERR.
           EXIT.

      ******************************************************************
       SEL-REG-VEN SECTION.
      *********************
       INI-SEL-REG-VEN.

      * Asume registro no seleccionado.
           MOVE 0 TO PAR-IND-RSEL IN PAR-VARI.
           IF VEN-VAL-SFIN IN VEN NOT > ZEROES
               GO TO FIN-SEL-REG-VEN.
           IF OPC-MSC-STAT IN OPC NOT = 'A'
                   GO TO FIN-SEL-REG-VEN.
      * No selecciona registros cuya oficina no este como parametro.
           IF PAR-COD-OFOC IN PAR-VARI(1) NOT = '*  '
               MOVE 1 TO PAR-NUM-IVEC IN PAR-VARI
               PERFORM REV-COD-OFOC
               IF REG-NO-SELECCIONADO
                   GO TO FIN-SEL-REG-VEN
               ELSE
                   MOVE 0 TO PAR-IND-RSEL IN PAR-VARI.
      * No selecciona registros cuya moneda no este como parametro.
           IF PAR-COD-VCOC IN PAR-VARI(1) NOT = '*     '
               MOVE 1 TO PAR-NUM-IVEC IN PAR-VARI
               PERFORM REV-COD-VCOC
               IF REG-NO-SELECCIONADO
                   GO TO FIN-SEL-REG-VEN
               ELSE
                   MOVE 0 TO PAR-IND-RSEL IN PAR-VARI.
      * No selecciona registros cuya linea no este como parametro.
           IF PAR-COD-COOC IN PAR-VARI(1) NOT = '*  '
               MOVE 1 TO PAR-NUM-IVEC IN PAR-VARI
               PERFORM REV-COD-COOC
               IF REG-NO-SELECCIONADO
                   GO TO FIN-SEL-REG-VEN
               ELSE
                   MOVE 0 TO PAR-IND-RSEL IN PAR-VARI.
      * No selecciona registros cuyas operaciones no este como parametro.
           IF PAR-CIC-IOPC IN PAR-VARI(1) NOT = '*  '
               MOVE 1 TO PAR-NUM-IVEC IN PAR-VARI
               PERFORM REV-COD-OPER
               IF REG-NO-SELECCIONADO
                   GO TO FIN-SEL-REG-VEN
               ELSE
                   MOVE 0 TO PAR-IND-RSEL IN PAR-VARI.

      * Lee TOC.
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TOC.
      * Obtiene Opera Como.
           MOVE 'COL'               TO MSC-COD-SIST FIO-SIST.
           MOVE 'OPEC'              TO MSC-COD-TMSC IN MSC.
           MOVE TOC-MSC-OPEC IN TOC TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           MOVE MSC-CIC-CMSC IN MSC TO WSS-OPEC IN WSS-VARI.

      * Recorro la EVC con la OPC para verificar los 'FI'.
           MOVE OPC-CIC-IOPC IN OPC TO EVC-CIC-IOPC IN EVC.
           MOVE ZEROES              TO EVC-NUM-IEVC IN EVC.
           MOVE FIO-GET-NLS         TO FIO-CMND.
           MOVE 'N'                 TO WSS-IND-SOFI IN WSS-VARI.
       LUP-LEE-EVC.
           PERFORM COL-FIO-EVC.
           IF FIO-STAT-OKS AND
               OPC-CIC-IOPC IN OPC = EVC-CIC-IOPC IN EVC
      * Tipo Calculo Vencimiento.
               IF EVC-MSC-TCVE IN EVC = 'FI' 
                  MOVE 'S' TO WSS-IND-SOFI IN WSS-VARI
                  MOVE FIO-GET-NXT TO FIO-CMND
                  GO TO LUP-LEE-EVC
               ELSE
                  MOVE 'N' TO WSS-IND-SOFI IN WSS-VARI.
           IF NOT ( WSS-OPEC = 'D' OR WSS-IND-SOFI IN WSS-VARI = 'S' )
               GO TO FIN-SEL-REG-VEN.
      * Selecciona Registro.
           MOVE 1 TO PAR-IND-RSEL IN PAR-VARI.
       FIN-SEL-REG-VEN.
           EXIT.

      ******************************************************************
      * Accesa a la Operacion.
       LEE-OPC SECTION.
      *****************
       INI-LEE-OPC.
           MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
              DISPLAY ' No encontre Operacion: '
                        OPC-CIC-IOPC IN OPC
              GO TO FIN-LEE-OPC.
           IF OPC-COD-VCOC IN OPC = WSS-COD-PESO IN WSS-VARI
              MOVE 1 TO WSS-SGV-VCAM IN WSS-VARI
              GO TO FIN-LEE-OPC.
           MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
      *
      * Obtiene valor de cambio con Fecha de Colocacion de OPC original.
      *
           IF OPC-IND-RENO IN OPC = 'PRO'
      *
      * Guarda el CIC de la OPC de la prorroga.
      *
               MOVE OPC-CIC-IOPC IN OPC TO AUX-CIC-IOPC IN WSS-VARI
      *
      * Lee OPC original para rescatar la fecha de origen.
      *
               MOVE DLC-CIC-IOCO IN DLC TO OPC-CIC-IOPC IN OPC
               MOVE FIO-GET-KEY  TO FIO-CMND
               PERFORM COL-FIO-OPC
               IF FIO-STAT-OKS
      * Mueve fecha de OPC original para la conversion a valor origen
      *     MOVE OPC-FEC-FCOL IN OPC TO CAM-FEC-FCAM IN CAM(1).
                   MOVE OPC-FEC-FCOL IN OPC TO CAM-FEC-FCAM IN CAM(1)
      *     MOVE OPC-FEC-FCOL IN OPC TO CAM-FEC-FCAM IN CAM(2).
                   MOVE OPC-FEC-FCOL IN OPC TO CAM-FEC-FCAM IN CAM(2)
      * Vuelve a leer la OPC de la prorroga
                   MOVE AUX-CIC-IOPC IN WSS-VARI TO OPC-CIC-IOPC IN OPC
                   MOVE FIO-GET-KEY TO FIO-CMND
                   PERFORM COL-FIO-OPC
               ELSE
                   MOVE 'COL'        TO MSG-COD-SIST
                   MOVE 'OPCORIGNEX' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC   OPC-CIC-IOPC IN OPC
           ELSE
               MOVE OPC-FEC-FCOL IN OPC TO CAM-FEC-FCAM IN CAM(1)
               MOVE OPC-FEC-FCOL IN OPC TO CAM-FEC-FCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS AND CAM-IND-VIGE NOT = 'S'
              MOVE 'COL'        TO MSG-COD-SIST
              MOVE 'CAMVCAMNEX' TO MSG-COD-MENS
              PERFORM GET-MSG
              GO TO FIN-LEE-OPC.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-VCAM.
       FIN-LEE-OPC.
           EXIT.

      ******************************************************************
      * Revisa si oficina corresponde a parametros.
       REV-COD-OFOC SECTION.
      **********************
       INI-REV-COD-OFOC.
           IF OPC-COD-OFOC IN OPC =
              PAR-COD-OFOC IN PAR-VARI(PAR-NUM-IVEC IN PAR-VARI)
               MOVE 1 TO PAR-IND-RSEL IN PAR-VARI
               GO TO FIN-REV-COD-OFOC.
           ADD 1 TO PAR-NUM-IVEC IN PAR-VARI.
           IF PAR-NUM-IVEC IN PAR-VARI NOT > 10
               GO TO INI-REV-COD-OFOC.
       FIN-REV-COD-OFOC.
           EXIT.

      ******************************************************************
      * Revisa si moneda corresponde a parametros.
       REV-COD-VCOC SECTION.
      **********************
       INI-REV-COD-VCOC.
           IF OPC-COD-VCOC IN OPC =
              PAR-COD-VCOC IN PAR-VARI(PAR-NUM-IVEC IN PAR-VARI)
               MOVE 1 TO PAR-IND-RSEL IN PAR-VARI
               GO TO FIN-REV-COD-VCOC.
           ADD 1 TO PAR-NUM-IVEC IN PAR-VARI.
           IF PAR-NUM-IVEC IN PAR-VARI NOT > 10
               GO TO INI-REV-COD-VCOC.
       FIN-REV-COD-VCOC.
           EXIT.

      ******************************************************************
      * Revisa si linea corresponde a parametros.
       REV-COD-COOC SECTION.
      **********************
       INI-REV-COD-COOC.
           IF OPC-COD-COOC IN OPC =
              PAR-COD-COOC IN PAR-VARI(PAR-NUM-IVEC IN PAR-VARI)
               MOVE 1 TO PAR-IND-RSEL IN PAR-VARI
               GO TO FIN-REV-COD-COOC.
           ADD 1 TO PAR-NUM-IVEC IN PAR-VARI.
           IF PAR-NUM-IVEC IN PAR-VARI NOT > 10
               GO TO INI-REV-COD-COOC.
       FIN-REV-COD-COOC.
           EXIT.

      ******************************************************************
      * Revisa si nro. de operacion corresponde a parametros.
       REV-COD-OPER SECTION.
      **********************
       INI-REV-COD-OPER.
           IF VEN-CIC-IOPC IN VEN =
              PAR-CIC-IOPC IN PAR-VARI(PAR-NUM-IVEC IN PAR-VARI)
               MOVE 1 TO PAR-IND-RSEL IN PAR-VARI
               GO TO FIN-REV-COD-OPER.
           ADD 1 TO PAR-NUM-IVEC IN PAR-VARI.
           IF PAR-NUM-IVEC IN PAR-VARI NOT > 6
               GO TO INI-REV-COD-OPER.
       FIN-REV-COD-OPER.
           EXIT.

      ******************************************************************
      * Limpia vectores.
       LIM-VEC SECTION.
      *****************
       INI-LIM-VEC.
           MOVE ZEROES TO WSS-SGV-ICPN IN WSS-VARI.
           SET MOV-I TO 1.
       LUP-LIM-VEC.
           IF MOV-I > 100
              GO TO FIN-LIM-VEC.
           MOVE SPACES TO  VEC-CPTO  (MOV-I).
           MOVE ZEROES TO  VEC-MONTO (MOV-I).
       SIG-LIM-VEC.
           SET MOV-I UP BY 1.
           GO TO LUP-LIM-VEC.
       FIN-LIM-VEC.
           EXIT.

      ******************************************************************
      * Guarda en vectores por registro de Vencimientos.
       CAR-MOV-VEN SECTION.
      *********************
       INI-CAR-MOV-VEN.
           SET MOV-I TO  1.
           SET MOV-I DOWN BY 1.
           MULTIPLY VEN-VAL-CAPI IN VEN BY WSS-SGV-VCAM IN WSS-VARI
           GIVING WSS-VAL-CAPI IN WSS-VARI ROUNDED.
           MULTIPLY VEN-VAL-FINA IN VEN BY WSS-SGV-VCAM IN WSS-VARI
           GIVING WSS-VAL-FINA IN WSS-VARI ROUNDED.

           COMPUTE WSS-VAL-REST IN WSS-VARI =
                    WSS-VAL-FINA IN WSS-VARI - WSS-VAL-CAPI IN WSS-VARI.

           IF WSS-VAL-REST IN WSS-VARI NOT = 0
              SET MOV-I UP BY 1
              MOVE 'INTPNG'            TO VEC-CPTO  IN WSS-VARI (MOV-I)
              IF OPC-MSC-TVOC IN OPC = WSS-MSC-TVOC IN WSS-VARI
                  MOVE WSS-VAL-REST IN WSS-VARI TO
                                          PES-SGV-DECI IN PES-VARI
                  PERFORM PES-SCTV
                  MOVE PES-SGV-ENTE IN PES-VARI TO
                                          WSS-VAL-REST IN WSS-VARI
                  MOVE WSS-VAL-REST IN WSS-VARI TO
                                          VEC-MONTO IN WSS-VARI (MOV-I)
              ELSE
                  MOVE WSS-VAL-REST IN WSS-VARI TO VEC-MONTO IN
                                                       WSS-VARI (MOV-I).

           IF WSS-VAL-REST IN WSS-VARI NOT = 0
              SET MOV-I UP BY 1
              MOVE 'INT   '            TO VEC-CPTO  IN WSS-VARI (MOV-I)
              IF OPC-MSC-TVOC IN OPC = WSS-MSC-TVOC IN WSS-VARI
                  MOVE WSS-VAL-REST IN WSS-VARI TO
                       PES-SGV-DECI IN PES-VARI
                  PERFORM PES-SCTV
                  MOVE PES-SGV-ENTE IN PES-VARI TO
                       WSS-VAL-REST IN WSS-VARI
                  MOVE WSS-VAL-REST IN WSS-VARI TO
                       VEC-MONTO IN WSS-VARI (MOV-I)
              ELSE
                  MOVE WSS-VAL-REST IN WSS-VARI TO VEC-MONTO IN
                                                       WSS-VARI (MOV-I).
       FIN-CAR-MOV-VEN.
           EXIT.

      ******************************************************************
       CLOSE-FILE SECTION.
      ********************
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-ERC.
           PERFORM COL-FIO-REV.
           PERFORM COL-FIO-MCD.
           MOVE 'COL' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE 'TAB' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
       FIN-CLOSE-FILE.
           EXIT.

      ******************************************************************
      * Obtiene la DLC.
       LEE-DLC SECTION.
      *****************
       INI-LEE-DLC.
           MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'DLC    INEX' TO  MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   VEN-KEY-IVEN IN VEN
               GO TO FIN-LEE-DLC.
       FIN-LEE-DLC.
           EXIT.

      ******************************************************************
      * Graba movimiento en archivo MCD desde Vectores.
       GRA-MOV SECTION.
       INI-GRA-MOV.
           DISPLAY 'ESTOY EN GRA-MOV :'.
           SET MOV-TOPE TO MOV-I.
           SET MOV-I TO 1.
       LUP-GRA-MOV.
           IF MOV-I > MOV-TOPE
              GO TO FIN-GRA-MOV.
           MOVE SPACES                         TO MCD-GLS-FLAG IN MCD.
           MOVE WSS-COD-VEN  IN WSS-VARI       TO MCD-COD-MVTO IN MCD.
           MOVE OPC-COD-VCOC IN OPC            TO MCD-COD-VCOC IN MCD.
           MOVE OPC-COD-COOC IN OPC            TO MCD-COD-COOC IN MCD.
           MOVE VEN-FEC-FVEN IN VEN            TO MCD-FEC-FCTB IN MCD.
           MOVE DLC-IND-SCRT IN DLC            TO MCD-IND-SCRT IN MCD.
           IF DLC-IND-SCTB IN DLC = 'MUL' OR 'MOR'
               MOVE SPACES                     TO MCD-IND-SCTB IN MCD
           ELSE
               MOVE DLC-IND-SCTB IN DLC        TO MCD-IND-SCTB IN MCD.
           MOVE VEN-CIC-IOPC IN VEN            TO MCD-CIC-IOPC IN MCD.
           MOVE VEN-NUM-IVEN IN VEN            TO MCD-NUM-ICAN IN MCD.
           MOVE VEC-CPTO IN WSS-VARI (MOV-I)   TO MCD-SGV-CPTO IN MCD.
           MOVE VEC-MONTO IN WSS-VARI (MOV-I)  TO MCD-SGV-MNTO IN MCD.
           MOVE SPACES                         TO MCD-IND-LIQU IN MCD.
           MOVE VEN-FEC-FVEN IN VEN            TO MCD-FEC-FREA IN MCD.
           MOVE VEN-MSC-STAT IN VEN            TO MCD-IND-CANC IN MCD.
           MOVE OPC-COD-OFOC IN OPC            TO MCD-COD-OFOC IN MCD.
           MOVE OPC-COD-OFOC IN OPC            TO MCD-COD-OFMV IN MCD.
           MOVE OPC-COD-OFOC IN OPC            TO MCD-COD-OFCA IN MCD.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'VEC-CPTO : ' VEC-CPTO IN WSS-VARI (MOV-I)
               DISPLAY 'VEC-MONTO: ' VEC-MONTO IN WSS-VARI (MOV-I).
           MOVE DLC-IND-SCBZ IN DLC            TO MCD-IND-SCBZ IN MCD.
           MOVE DLC-COD-PLZO IN DLC            TO MCD-COD-PLZO IN MCD.
           MOVE WSS-FEC-FFEC IN WSS-VARI       TO MCD-FEC-FMOV IN MCD.
           MOVE OPC-NUM-IVEN IN OPC            TO MCD-NUM-IVCT IN MCD.
           MOVE DLC-NUM-IDLC IN DLC            TO MCD-NUM-IDLC IN MCD.
           MOVE OPC-VAL-SCRE IN OPC            TO MCD-SGV-SCRE IN MCD.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-MCD.
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'FIO-STAT DEL MCD :' FIO-STAT
               DISPLAY 'MCD-GLS-FLAG :' MCD-GLS-FLAG IN MCD
               DISPLAY 'MCD-COD-MVTO :' MCD-COD-MVTO IN MCD
               DISPLAY 'MCD-COD-VCOC :' MCD-COD-VCOC IN MCD
               DISPLAY 'MCD-COD-COOC :' MCD-COD-COOC IN MCD
               DISPLAY 'MCD-IND-SCRT :' MCD-IND-SCRT IN MCD
               DISPLAY 'MCD-IND-SCTB :' MCD-IND-SCTB IN MCD
               DISPLAY 'MCD-CAI-IOPC :' MCD-CAI-IOPC IN MCD
               DISPLAY 'MCD-IIC-IOPC :' MCD-IIC-IOPC IN MCD
               DISPLAY 'MCD-NUM-ICAN :' MCD-NUM-ICAN IN MCD
               DISPLAY 'MCD-SGV-CPTO :' MCD-SGV-CPTO IN MCD
               DISPLAY 'MCD-SGV-MNTO :' MCD-SGV-MNTO IN MCD
               DISPLAY 'MCD-IND-SCBZ :' MCD-IND-SCBZ IN MCD
               DISPLAY 'MCD-COD-OFOC :' MCD-COD-OFOC IN MCD
               DISPLAY 'MCD-COD-PLZO :' MCD-COD-PLZO IN MCD
               DISPLAY 'MCD-FEC-FMOV :' MCD-FEC-FMOV IN MCD
               DISPLAY 'MCD-FEC-FCTB :' MCD-FEC-FCTB IN MCD
               DISPLAY 'MCD-NUM-IVCT :' MCD-NUM-IVCT IN MCD
               DISPLAY 'MCD-NUM-IDLC :' MCD-NUM-IDLC IN MCD
               DISPLAY 'MCD-SGV-SCRE :' MCD-SGV-SCRE IN MCD
               DISPLAY 'MCD-COD-OFMV :' MCD-COD-OFMV IN MCD
               DISPLAY 'MCD-IND-CANC :' MCD-IND-CANC IN MCD
               DISPLAY 'MCD-FEC-FREA :' MCD-FEC-FREA IN MCD
               DISPLAY 'MCD-IND-LIQU :' MCD-IND-LIQU IN MCD.
           ADD 1 TO WSS-NUM-RGRA IN WSS-VARI.
       SIG-GRA-MOV.
           SET MOV-I UP BY 1.
           GO TO LUP-GRA-MOV.
       FIN-GRA-MOV.
           EXIT.

      ******************************************************************
       COPY COLBFERC.
       COPY COLBFREV.
       COPY COLBFOPC.
       COPY COLBFVEN.
       COPY COLBFDLC.
       COPY COLBFMCD.
       COPY COLBFEVC.
       COPY COLBFTOC.
       COPY TABBFCAM.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBGPES.
