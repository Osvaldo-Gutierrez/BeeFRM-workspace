       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLIC3.
       AUTHOR.          BEE (DPM).
       DATE-WRITTEN.    18-Apr-97 03:58 PM.

      * Programa Listador de Reporte IC3 con Sort.

      *<<<< CMT_ID
      *>>>>

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTIC3         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTIC3         ASSIGN TO        UT-S-RPTIC3.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTIC3         ASSIGN TO        UT-S-RPTIC3.
      *<<<< IOS
      *>>>>

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRIC3.
      *<<<< RR
      *>>>>
       FD  RPTIC3 LABEL RECORDS OMITTED
           REPORT IS RPT-IC3
      *<<<< FD_RPT
           RECORD CONTAINS 81 CHARACTERS
      *>>>>
           .
      *<<<< FLS
      *>>>>

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LMS-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRLMS.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWIC3.
      *<<<< RW
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *>>>>
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY TABBROFI.
       COPY COLBRFZE.
       COPY GNSBRMSC.

       01  WSS-VARI.
           03 WSS-SW-LMS-VACIO  VALUE 'N' PIC X(01).
           03 WSS-FEC-FINF.
              05 WSS-NUM-DINF                    PIC 9(02).
              05 WSS-NUM-MINF                    PIC 9(02).
              05 WSS-NUM-SINF                    PIC 9(02).
              05 WSS-NUM-AINF                    PIC 9(02).
           03 WSS-NUM-SSIS                       PIC 9(02).
           03 WSS-FEC-FSIS.
              05 WSS-NUM-ASIS                    PIC 9(02).
              05 WSS-NUM-MSIS                    PIC 9(02).
              05 WSS-NUM-DSIS                    PIC 9(02).
           03 WSS-FEC-FIN2.
              05 WSS-NUM-SIN2       VALUE 19     PIC 9(02).
              05 WSS-NUM-AIN2                    PIC 9(02).
              05 WSS-NUM-MIN2                    PIC 9(02).
              05 WSS-NUM-DIN2                    PIC 9(02).
           03 WSS-HORA-LST.
              05 WSS-NUM-HH                      PIC 9(02).
              05 WSS-NUM-MM                      PIC 9(02).
              05 WSS-NUM-SS                      PIC 9(02).
           03 WSS-COD-OFOC                       PIC X(03).
           03 WSS-COD-OFIC                       PIC X(03).
           03 WSS-CIC-CCSB                       PIC X(12).
           03 WSS-SORT-VACIO                     PIC X(02) VALUE 'NO'.
               88  SORT-VACIO                     VALUE 'SI'.
           03 WSS-COD-SON                        PIC X(01).
              88 VAL-COD-SON VALUE 'S' 's' 'N' 'n'.
           03 WSS-NUM-CONT        VALUE ZEROES   PIC 9(04).
           03 WSS-COD-SUCC.
              05 WSS-NUM-CERO                    PIC 9(01).
              05 WSS-COD-SUCL                    PIC X(03).
           03 WSS-FEC-INTE.
              05 WSS-FEC-DIA                     PIC 9(02).
              05 WSS-FEC-MES                     PIC 9(02).
              05 WSS-FEC-ANO                     PIC 9(02).
           03 WSS-SGV-DDR1          VALUE ZEROES PIC S9(13)V9(02).
           03 WSS-SGV-HHR1          VALUE ZEROES PIC S9(13)V9(02).
           03 WSS-SGV-DDNV          VALUE ZEROES PIC S9(13)V9(02).
           03 WSS-SGV-HHNV          VALUE ZEROES PIC S9(13)V9(02).

      * totalizador de debe para interfaz
           03 WSS-TOT-FZE           VALUE ZEROES PIC 9(14)V9(02).
           03 WSS-NUM-NVIG          VALUE ZEROES PIC 9(08).
           03 WSS-IND-DISP          VALUE SPACES PIC X(01).
      * NUMERO DE CUENTA CONTABLE X(12) ..X(08)
           03 WSS-COD-CTA           VALUE SPACES PIC X(09).
           03 WSS-RED-CTA REDEFINES WSS-COD-CTA  PIC 9(09).
           03 WSS-GLS-AUX.
              05 WSS-GLS-DSC        VALUE SPACES PIC X(40).
              05 WSS-GLS-ADI        VALUE SPACES PIC X(30).
           03 WSS-COD-OFIC2      VALUE SPACES PIC X(03).
           03 WSS-MARCA          VALUE '@$%'  PIC X(03).
           03 WSS-NUM-VIGENTE.
              05 WSS-NUM-VIGE                PIC 9(06).
              05 WSS-GLS-VIGE  VALUE SPACES  PIC X(06).
      *
      * DEFINICION DE CAMPOS PARA ARCHIVO DE INTERFAZ PARA CONTABILIDAD
      *
      *****CABECERA*****
      *   CABECERA DE LOTE (BATCH HEADER)
           03 WSS-FZE-CABECERA.
      *      CABECERA-LOTE
              05 WSS-FZE-COD-CABE     VALUE 'BH'     PIC X(02).
      *      COMPANIA
              05 WSS-FZE-COD-CCIA     VALUE 'BCI '   PIC X(04).
      *      COD.IDENTIFICACION
              05 WSS-FZE-COD-IDEN                    PIC X(02).
      *      NUMERO DE LOTE
              05 WSS-FZE-NUM-LOTE                    PIC 9(02).
      *      TIPO DE MOVIMIENTO
              05 WSS-FZE-COD-MVTO     VALUE 2        PIC 9(01).
      *      TOTAL DE LOTE
              05 WSS-FZE-SGV-LOTE     VALUE ZEROES   PIC 9(14)V9(02).
      *      PERIODO
              05 WSS-FZE-COD-PERI     VALUE 0        PIC 9(01).
      *      FECHA
              05 WSS-FZE-FEC-FECH                    PIC X(06).
      *
      *   DETALLE DEL MOVIMIENTO (**PRIMER REGISTRO**)
      *
           03 WSS-FZE-PMERREG.
      *      IDENTIFICACION DEL REGISTRO.
              05 WSS-FZE-COD-IDE1     VALUE 'PT'     PIC X(02).
      *      NUMERO DE MOVIMIENTO
              05 WSS-FZE-NUM-MOV1                    PIC 9(04).
      *      PRIMER REGISTRO
              05 WSS-FZE-NUM-REG1     VALUE 1        PIC 9(01).
      *      DEBE-HABER
              05 WSS-FZE-NUM-DEHA                    PIC 9(02).
      *      COMPANIA
              05 WSS-FZE-COD-CIA1     VALUE 'BCI '   PIC X(04).
      *      CUENTA
              05 WSS-FZE-COD-CCSB                    PIC 9(09).
      *      BLANCOS
              05 WSS-FZE-COD-BLCO     VALUE SPACES   PIC X(09).
      *      SUCURSAL
              05 WSS-FZE-COD-SUCU                    PIC X(12).
      *      SOURCE CODE
              05 WSS-FZE-COD-CODE                    PIC X(10).
      *      FECHA
              05 WSS-FZE-FEC-FCTB                    PIC X(06).
      *      MONTO
              05 WSS-FZE-NUM-MONT     VALUE ZEROES   PIC 9(13)V9(02).     
      *
      *    (**SEGUNDO REGISTRO**)
      *
           03 WSS-FZE-SGDOREG.
      *      IDENTIFICACION DEL REGISTRO
              05 WSS-FZE-COD-IDE2     VALUE 'PT'     PIC X(02).
      *      NUMERO DEL MOVIMIENTO
              05 WSS-FZE-NUM-MOV2                    PIC 9(04).
      *      SEGUNDO REGISTRO
              05 WSS-FZE-NUM-REG2     VALUE 2        PIC 9(01).
      *      GLOSA
              05 WSS-FZE-GLS-GLOS                    PIC X(30).
      *      VIGENTE
              05 WSS-FZE-COD-VIGE                    PIC X(12).
      *      DESCRIPCION 3 
              05 WSS-FZE-COD-DESC                    PIC X(10).
      *>>>>
      *<<<< WSS_DTC
      *>>>>

       REPORT SECTION.
      *--------------
       COPY COLRFIC3.
      *<<<< RF
      *>>>>
           .
       COPY COLRTIC3.
      *<<<< RT
      *>>>>

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-IC3-SRT-COD-OFIC SECTION.
           USE BEFORE REPORTING CF-IC3-SRT-COD-OFIC.
       INI-CF-IC3-SRT-COD-OFIC.
      *<<<< CF_IC3_SRT_COD_OFIC
           MOVE 0 TO PAGE-COUNTER IN RPT-IC3.
      *>>>>
       FIN-CF-IC3-SRT-COD-OFIC.
           EXIT.
       DEC-PH-IC3 SECTION.
           USE BEFORE REPORTING PH-IC3.
       INI-PH-IC3.
      *<<<< PH_IC3
           IF SRT-COD-OFIC IN SRT > SPACES
               MOVE SRT-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
                                           WSS-COD-OFI  IN WSS-IC3
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFOC IN WSS-IC3.
           IF SRT-COD-OFIC IN SRT NOT = WSS-COD-OFIC2 IN WSS-VARI
              MOVE WSS-MARCA IN WSS-VARI TO WSS-IND-CPAG IN WSS-IC3
              MOVE SRT-COD-OFIC IN SRT TO WSS-COD-OFIC2 IN WSS-VARI
           ELSE
              MOVE SPACES TO WSS-IND-CPAG IN WSS-IC3
                             WSS-COD-OFI  IN WSS-IC3.
      *>>>>
       FIN-PH-IC3.
           EXIT.
      *<<<< FIN_DEC
      *>>>>
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLIC3' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
           DISPLAY '============================'.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
           DISPLAY '============================'.
      * Captura Fecha de Proceso
           DISPLAY 'INGRESE FECHA DEL INFORME (DDMMSSAA) '.
           DISPLAY '-------------------------------------'.
           ACCEPT  WSS-FEC-FINF IN WSS-VARI.
           DISPLAY 'FECHA : ' WSS-FEC-FINF IN WSS-VARI.
      * Captura Codigo de Oficina
           DISPLAY 'OFICINA (*=TODAS): '.
           DISPLAY '-------------------'.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'OFICINA : ' WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'FIN    CAPTURA DE PARAMETROS'.
      * Captura Dato para InterFase
           DISPLAY 'GENERA INTERFASE CONTABLE [S/N]: '
           DISPLAY '---------------------------------'.
           ACCEPT  WSS-COD-SON IN WSS-VARI.
           DISPLAY 'INTERFASE ? : ' WSS-COD-SON IN WSS-VARI.
           DISPLAY '==============================='.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
           DISPLAY '==============================='.
      * Captura Hora del Informe
           ACCEPT WSS-HORA-LST FROM TIME.
      * Captura Fecha del Informe
           ACCEPT WSS-FEC-FSIS FROM DATE.
           IF WSS-NUM-ASIS IN WSS-VARI > 80
              MOVE 19 TO WSS-NUM-SSIS IN WSS-VARI
           ELSE
              MOVE 20 TO WSS-NUM-SSIS IN WSS-VARI.
      * Captura indicador de display
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
                                       NOT = 'N'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI.
      *1
      *VAL-FEC Valida Fecha de Informe
           DISPLAY 'FECHA DEL INFORME : ' WSS-FEC-FINF IN WSS-VARI.
           MOVE WSS-FEC-FINF IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT.

      *2
      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*' AND
              WSS-COD-OFIC IN WSS-VARI > SPACES
               DISPLAY 'OFICINA : ' WSS-COD-OFIC IN WSS-VARI
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-ABT.
      *3
      *Valida Indicador de generacion de Interfase Contable
           IF NOT VAL-COD-SON
              DISPLAY 'Indicador de generacion de Interfaz NO VALIDO'
              PERFORM GNS-PRO-ABT.
      *
           DISPLAY '============================'.
           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
           DISPLAY '============================'.
      *>>>>
           SORT GNSSRT
       COPY COLRSIC3.
      *<<<< RS
      *>>>>
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
      *<<<< FIN
      *>>>>
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-FZE.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO COLFZE'
               DISPLAY 'PROCESO CANCELADO....'
               MOVE PGM-SMAL TO PGM-STAT-SRT
               MOVE 'S' TO WSS-SW-LMS-VACIO IN WSS-VARI
               GO TO FIN-INP-SRT.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-LMS.
       FST-INP-SRT.
      *<<<< INI_FST_INP
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-LMS.
      *<<<< FIN_FST_INP
      * INICIO DE PRIMERA LECTURA SECUENCIAL DE ARCHIVO LMS.
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               PERFORM POS-OFI-PAR.
      *
           IF NOT FIO-STAT-OKS
               MOVE 'S' TO WSS-SW-LMS-VACIO IN WSS-VARI
               GO TO FIN-INP-SRT.

           MOVE LMS-COD-OFOC IN LMS     TO WSS-COD-OFOC IN WSS-VARI.
           MOVE LMS-CIC-CCSB IN LMS     TO WSS-CIC-CCSB IN WSS-VARI.
           MOVE LMS-NUM-NVIG IN LMS     TO WSS-NUM-NVIG IN WSS-VARI.

           MOVE WSS-NUM-SSIS IN WSS-VARI TO WSS-NUM-SPRO IN WSS-IC3.
           MOVE WSS-NUM-ASIS IN WSS-VARI TO WSS-NUM-APRO IN WSS-IC3.
           MOVE WSS-NUM-MSIS IN WSS-VARI TO WSS-NUM-MPRO IN WSS-IC3.
           MOVE WSS-NUM-DSIS IN WSS-VARI TO WSS-NUM-DPRO IN WSS-IC3.
           MOVE WSS-NUM-HH   IN WSS-VARI TO WSS-NUM-HORA IN WSS-IC3.
           MOVE WSS-NUM-MM   IN WSS-VARI TO WSS-NUM-MINU IN WSS-IC3.
           MOVE WSS-NUM-SS   IN WSS-VARI TO WSS-NUM-SEGU IN WSS-IC3.

           MOVE LMS-FEC-FPRO IN LMS      TO LMS-FEC-FCTB IN LMS.

           MOVE LMS-NUM-DCTB IN LMS      TO SRT-NUM-DCTB IN SRT.
           MOVE LMS-NUM-MCTB IN LMS      TO SRT-NUM-MCTB IN SRT.
           MOVE LMS-NUM-SCTB IN LMS      TO SRT-NUM-SCTB IN SRT.
           MOVE LMS-NUM-ACTB IN LMS      TO SRT-NUM-ACTB IN SRT.

      * SE MUEVE FECHA DE PROCESO A FECHA AUXILIAR PARA SU
      * VALIDACION CON FECHA CONTABLE (LMS)
           MOVE WSS-NUM-DINF IN WSS-VARI TO WSS-NUM-DIN2 IN WSS-VARI.
           MOVE WSS-NUM-MINF IN WSS-VARI TO WSS-NUM-MIN2 IN WSS-VARI.
           MOVE WSS-NUM-AINF IN WSS-VARI TO WSS-NUM-AIN2 IN WSS-VARI.
           MOVE WSS-NUM-SINF IN WSS-VARI TO WSS-NUM-SIN2 IN WSS-VARI.
      * VALIDA FECHA EN ARCHIVO CON LA DEL PARAMETRO JCL
           IF LMS-FEC-FCTB IN LMS NOT = WSS-FEC-FIN2 IN WSS-VARI
              DISPLAY ' **** FECHA CONTABLE  EN  ARCHIVO ES'
              DISPLAY ' **** DISTINTA A LA FECHA DE PROCESO'
              DISPLAY ' **** SEGUN PARAMETRO........... '
              DISPLAY ' **** EL PROCESO SE AUTOCANCELA '
              DISPLAY 'FECHA PARAMETRO : ' WSS-FEC-FIN2 IN WSS-VARI
              DISPLAY 'FECHA EN MOVTOS : ' LMS-FEC-FCTB IN LMS
              PERFORM GNS-PRO-ABT.
      *>>>>
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*' AND
              LMS-COD-OFOC IN LMS NOT = WSS-COD-OFIC IN WSS-VARI
               GO TO FIN-INP-SRT.

           IF LMS-COD-OFOC IN LMS NOT = WSS-COD-OFOC IN WSS-VARI
               MOVE WSS-COD-OFOC IN WSS-VARI TO SRT-COD-OFIC IN SRT
               MOVE WSS-CIC-CCSB IN WSS-VARI TO SRT-CIC-CCSB IN SRT
               MOVE WSS-NUM-NVIG IN WSS-VARI TO SRT-NUM-NVIG IN SRT
               MOVE WSS-SGV-DDR1 IN WSS-VARI TO SRT-SGV-DEBE IN SRT
               MOVE WSS-SGV-HHR1 IN WSS-VARI TO SRT-SGV-HABE IN SRT
               RELEASE SRT
               MOVE LMS-COD-OFOC IN LMS TO WSS-COD-OFOC IN WSS-VARI
               MOVE LMS-CIC-CCSB IN LMS TO WSS-CIC-CCSB IN WSS-VARI
               MOVE LMS-NUM-NVIG IN LMS TO WSS-NUM-NVIG IN WSS-VARI
               MOVE ZEROES              TO WSS-SGV-DDR1 IN WSS-VARI
                                           WSS-SGV-HHR1 IN WSS-VARI
                                           WSS-SGV-DDNV IN WSS-VARI
                                           WSS-SGV-HHNV IN WSS-VARI
           ELSE
           IF LMS-CIC-CCSB IN LMS NOT = WSS-CIC-CCSB IN WSS-VARI
               MOVE WSS-SGV-DDR1 IN WSS-VARI TO SRT-SGV-DEBE IN SRT
               MOVE WSS-SGV-HHR1 IN WSS-VARI TO SRT-SGV-HABE IN SRT
               MOVE WSS-COD-OFOC IN WSS-VARI TO SRT-COD-OFIC IN SRT
               MOVE WSS-CIC-CCSB IN WSS-VARI TO SRT-CIC-CCSB IN SRT
               MOVE WSS-NUM-NVIG IN WSS-VARI TO SRT-NUM-NVIG IN SRT
               RELEASE SRT
               MOVE LMS-CIC-CCSB IN LMS TO WSS-CIC-CCSB IN WSS-VARI
               MOVE LMS-NUM-NVIG IN LMS TO WSS-NUM-NVIG IN WSS-VARI
               MOVE ZEROES              TO WSS-SGV-DDR1 IN WSS-VARI
                                           WSS-SGV-HHR1 IN WSS-VARI
                                           WSS-SGV-DDNV IN WSS-VARI
                                           WSS-SGV-HHNV IN WSS-VARI
           ELSE
           IF LMS-NUM-NVIG IN LMS NOT = WSS-NUM-NVIG IN WSS-VARI
               MOVE WSS-SGV-DDNV IN WSS-VARI TO SRT-SGV-DEBE IN SRT
               MOVE WSS-SGV-HHNV IN WSS-VARI TO SRT-SGV-HABE IN SRT
               MOVE WSS-COD-OFOC IN WSS-VARI TO SRT-COD-OFIC IN SRT
               MOVE WSS-CIC-CCSB IN WSS-VARI TO SRT-CIC-CCSB IN SRT
               MOVE WSS-NUM-NVIG IN WSS-VARI TO SRT-NUM-NVIG IN SRT
               RELEASE SRT
               MOVE ZEROES              TO WSS-SGV-DDNV IN WSS-VARI
                                           WSS-SGV-HHNV IN WSS-VARI.

           IF LMS-IND-DBHB IN LMS = 'D'
               COMPUTE WSS-SGV-DDNV IN WSS-VARI =
                       WSS-SGV-DDNV IN WSS-VARI + LMS-SGV-ICTB IN LMS
               COMPUTE WSS-SGV-DDR1 IN WSS-VARI =
                       WSS-SGV-DDR1 IN WSS-VARI + LMS-SGV-ICTB IN LMS
      *Acumula para total de Interfaz
               COMPUTE WSS-TOT-FZE IN WSS-VARI =
                     WSS-TOT-FZE IN WSS-VARI + LMS-SGV-ICTB IN LMS
           ELSE
               COMPUTE WSS-SGV-HHNV IN WSS-VARI =
                       WSS-SGV-HHNV IN WSS-VARI + LMS-SGV-ICTB IN LMS
               COMPUTE WSS-SGV-HHR1 IN WSS-VARI =
                       WSS-SGV-HHR1 IN WSS-VARI + LMS-SGV-ICTB IN LMS.
           GO TO NXT-INP-SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMIC3.
      *<<<< RM
      *>>>>
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-LMS.
      *<<<< FIN_NXT_INP
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
      *<<<< FIN_INP
           IF WSS-SW-LMS-VACIO IN WSS-VARI = 'N'
               MOVE WSS-COD-OFOC IN WSS-VARI TO SRT-COD-OFIC IN SRT
               MOVE WSS-CIC-CCSB IN WSS-VARI TO SRT-CIC-CCSB IN SRT
               MOVE WSS-NUM-NVIG IN WSS-VARI TO SRT-NUM-NVIG IN SRT
               MOVE WSS-SGV-DDR1 IN WSS-VARI TO SRT-SGV-DEBE IN SRT
               MOVE WSS-SGV-HHR1 IN WSS-VARI TO SRT-SGV-HABE IN SRT
               RELEASE SRT.
      *>>>>
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-LMS.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
      *<<<< INI_OUT
      *>>>>
           OPEN OUTPUT RPTIC3.
      *     MOVE 'COLPLIC3' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-IC3.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
           PERFORM MUEVE-CABECERA.
           MOVE WSS-TOT-FZE IN WSS-VARI TO 
                       WSS-FZE-SGV-LOTE IN WSS-VARI.
           MOVE WSS-FZE-CABECERA IN WSS-VARI TO 
                       FZE-FLD-REG IN FZE.
           PERFORM WRITE-INTERFAZ.
           IF SRT-COD-OFIC IN SRT > SPACES
               MOVE SRT-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
                                           WSS-COD-OFI  IN WSS-IC3
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFOC IN WSS-IC3.
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
           PERFORM GRABA-INTERFAZ.
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'GRABA INTERFAZ: '
               DISPLAY 'OFICINA: ' WSS-FZE-COD-SUCU IN WSS-VARI
               DISPLAY 'CODIGO CUENTA: ' WSS-FZE-COD-CCSB IN WSS-VARI
               DISPLAY 'DEBE/HABER: ' WSS-FZE-NUM-DEHA IN WSS-VARI
               DISPLAY 'MONTO: ' WSS-FZE-NUM-MONT IN WSS-VARI.
           PERFORM BUS-GLS-CTA.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-IC3.
      *<<<< FIN_GEN_OUT
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'IMPRIME IC3: '
               DISPLAY 'OFICINA: ' SRT-COD-OFIC IN SRT
               DISPLAY 'CODIGO CUENTA: ' SRT-CIC-CCSB IN SRT
               DISPLAY 'MONTO DEBE : ' SRT-SGV-DEBE IN SRT
               DISPLAY 'MONTO HABER: ' SRT-SGV-HABE IN SRT.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      *<<<< FIN_OUT
      *>>>>
           TERMINATE RPT-IC3.
      *<<<< FIN_TMT
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-FZE.
      *>>>>
           CLOSE RPTIC3.
       EXT-OUT-SRT.
      *<<<< EXT_OUT
      *>>>>
           EXIT.
       COPY COLBFLMS.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
       COPY COLBFFZE.
       COPY TABBFOFI.
       COPY TABBBOFI.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBFMSC.
       COPY TABBVOFI.
       COPY GNSBBMSG.
       COPY GNSBBTAB.

       BUS-GLS-CTA SECTION.
       INI-BUS-GLS-CTA.
      * Mediante BUS-TAB Busca Cuenta Contable de Tabla PCC
           IF SRT-CIC-CCSB IN SRT > SPACES
               MOVE 'TAB'                    TO TAB-COD-SIST
               MOVE 'PCC'                    TO TAB-COD-TTAB IN TAB
               MOVE SRT-CIC-CCSB IN SRT      TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB      TO WSS-GLS-DSC  IN WSS-VARI
               MOVE TAB-GLS-DATA IN TAB      TO WSS-GLS-ADI  IN WSS-VARI
               MOVE WSS-GLS-AUX  IN WSS-VARI TO WSS-GLS-CCSB IN WSS-IC3
           ELSE
               MOVE SPACES TO WSS-GLS-CCSB IN WSS-IC3.
       FIN-BUS-GLS-CTA.
           EXIT.


      * PARRAFO DE INTERFAZ
       GRABA-INTERFAZ SECTION.
       INI-GRABA-INTERFAZ.
           IF WSS-COD-SON IN WSS-VARI = 'N'
               GO TO FIN-GRABA-INTERFAZ.
      * 
      * DATOS DE CABECERA.
      *
      * CONTADOR PARA NUMERO DE MOVIMIENTOS
      *
           ADD 1          TO WSS-NUM-CONT IN WSS-VARI.
           IF SRT-SGV-DEBE IN SRT > 0
              PERFORM MUEVE-REG-1
              MOVE 10     TO WSS-FZE-NUM-DEHA IN WSS-VARI
              MOVE SRT-SGV-DEBE IN SRT 
                          TO WSS-FZE-NUM-MONT IN WSS-VARI
              MOVE WSS-FZE-PMERREG IN WSS-VARI TO
                            FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ
              PERFORM MUEVE-REG-2
              MOVE WSS-FZE-SGDOREG IN WSS-VARI TO
                            FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ.

           IF SRT-SGV-HABE IN SRT > 0
              PERFORM MUEVE-REG-1
              MOVE 60     TO WSS-FZE-NUM-DEHA IN WSS-VARI
              MOVE SRT-SGV-HABE IN SRT 
                          TO WSS-FZE-NUM-MONT IN WSS-VARI
              MOVE WSS-FZE-PMERREG IN WSS-VARI TO
                            FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ
              PERFORM MUEVE-REG-2
              MOVE WSS-FZE-SGDOREG IN WSS-VARI TO
                            FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ.
           GO TO FIN-GRABA-INTERFAZ.
      *
        MUEVE-REG-1.
      *
      * PRIMER REGISTRO
      *
           MOVE 'PT'      TO WSS-FZE-COD-IDE1 IN WSS-VARI.
           MOVE WSS-NUM-CONT IN WSS-VARI 
                          TO WSS-FZE-NUM-MOV1 IN WSS-VARI.
           MOVE 1         TO WSS-FZE-NUM-REG1 IN WSS-VARI.
           MOVE 'BCI '    TO WSS-FZE-COD-CIA1 IN WSS-VARI.
           MOVE SRT-CIC-CCSB IN SRT TO WSS-COD-CTA IN WSS-VARI.
           MOVE WSS-RED-CTA  IN WSS-VARI TO WSS-FZE-COD-CCSB 
                                            IN WSS-VARI.
           MOVE SPACES    TO WSS-FZE-COD-BLCO IN WSS-VARI.
           MOVE 0         TO WSS-NUM-CERO IN WSS-VARI.
           MOVE SRT-COD-OFIC IN SRT
                          TO WSS-COD-SUCL IN WSS-VARI.
           MOVE WSS-COD-SUCC IN WSS-VARI
                          TO WSS-FZE-COD-SUCU IN WSS-VARI.
           MOVE 'CLC' TO WSS-FZE-COD-CODE IN WSS-VARI.
           MOVE WSS-NUM-DIN2 IN WSS-VARI
                          TO WSS-FEC-DIA IN WSS-VARI.
           MOVE WSS-NUM-MIN2 IN WSS-VARI
                          TO WSS-FEC-MES IN WSS-VARI.
           MOVE WSS-NUM-AIN2 IN WSS-VARI
                          TO WSS-FEC-ANO IN WSS-VARI.
           MOVE WSS-FEC-INTE IN WSS-VARI
                          TO WSS-FZE-FEC-FCTB IN WSS-VARI.
      *
       MUEVE-REG-2.
      * SEGUNDO REGISTRO
      *
           MOVE 'PT'      TO WSS-FZE-COD-IDE2 IN WSS-VARI.
           MOVE WSS-NUM-CONT IN WSS-VARI 
                          TO WSS-FZE-NUM-MOV2 IN WSS-VARI.
           MOVE 2         TO WSS-FZE-NUM-REG2 IN WSS-VARI.
      * SE MUEVE GLOSA ESPECIFICA A GLOSA DE INTERFAZ.
           MOVE 'CONTABILIDAD AUTOMATICA DE COLOCACIONES'
                TO WSS-FZE-GLS-GLOS IN WSS-VARI.
           MOVE SRT-NUM-NVIG IN SRT TO WSS-NUM-VIGE.
           MOVE WSS-NUM-VIGENTE TO WSS-FZE-COD-VIGE IN WSS-VARI.
           MOVE SPACES    TO WSS-FZE-COD-DESC IN WSS-VARI.
      *
       WRITE-INTERFAZ.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-FZE.
      *
       FIN-GRABA-INTERFAZ.
           EXIT.


       MUEVE-CABECERA SECTION.
       INI-MUEVE-CABECERA.
              MOVE 'BH'    TO WSS-FZE-COD-CABE IN WSS-VARI.
              MOVE 'BCI '  TO WSS-FZE-COD-CCIA IN WSS-VARI.
              MOVE '01'    TO WSS-FZE-COD-IDEN IN WSS-VARI.
              MOVE 01      TO WSS-FZE-NUM-LOTE IN WSS-VARI.
              MOVE 2       TO WSS-FZE-COD-MVTO IN WSS-VARI.
              MOVE 0       TO WSS-FZE-COD-PERI IN WSS-VARI.
              MOVE WSS-NUM-DIN2 IN WSS-VARI
                           TO WSS-FEC-DIA IN WSS-VARI.
              MOVE WSS-NUM-MIN2 IN WSS-VARI
                           TO WSS-FEC-MES IN WSS-VARI.
              MOVE WSS-NUM-AIN2 IN WSS-VARI
                           TO WSS-FEC-ANO IN WSS-VARI.
              MOVE WSS-FEC-INTE IN WSS-VARI
                           TO WSS-FZE-FEC-FECH IN WSS-VARI.
       FIN-MUEVE-CABECERA.
           EXIT.


       POS-OFI-PAR SECTION.
       INI-POS-OFI-PAR.
           IF NOT FIO-STAT-OKS
               GO TO FIN-POS-OFI-PAR.
           IF WSS-COD-OFIC IN WSS-VARI NOT = LMS-COD-OFOC IN LMS
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM COL-FIO-LMS
               GO TO INI-POS-OFI-PAR.
           GO TO FIN-POS-OFI-PAR.
       FIN-POS-OFI-PAR.
           EXIT.    
      *>>>>
