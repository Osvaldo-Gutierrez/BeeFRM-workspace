       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGER4.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    18-AUG-1992     ( Ultima Modificacion ).

      * Programa que regulariza el plazo contable

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY GNSWGPES.
       COPY GNSBRTAB.
       COPY GNSBRMSC.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.

       COPY COLWGVEN.
       COPY COLBRRCV.
       COPY COLBRVEN.
       COPY COLBROPC.
       COPY COLBRICG.
       COPY COLBRDLC.
       COPY COLBREVC.
       COPY COLBRTOC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
       COPY COLBRDCC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCC-REQA==.
      *DAD fin  06-AUG-1992

       COPY TABBRCAM.
       COPY TABBROFI.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.

       COPY COLWGPLP.

       01  WSS-VARI.
           03 WSS-COFI                        PIC X(03).
           03 WSS-IDSP                        PIC X(01).
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
           03 WSS-IACT                        PIC X(01).
           03 WSS-MENS.
              05 WSS-MEN1                     PIC X(28).
              05 WSS-MEN2                     PIC X(12).
      *DAD fin  06-AUG-1992
           03 WSS-CCHP                VALUE 0 PIC 9(02).
           03 WSS-IOPC                        PIC X(12).
           03 WSS-OPCP                        PIC X(12).
           03 WSS-IEVC                        PIC X(15).
           03 WSS-NEVC                        PIC 9(03).
           03 WSS-OTOT                VALUE 0 PIC 9(08).
           03 WSS-OSUP                VALUE 0 PIC 9(08).
           03 WSS-ONUP                VALUE 0 PIC 9(08).
           03 WSS-VDLC.
              05 WSS-IDLC          OCCURS 100 PIC  9(03).
           03 WSS-SUMA      COMP-3 OCCURS 100 PIC S9(11)V9(04).
           03 WSS-LDLC             VALUE  100 PIC  9(03).
           03 WSS-MDLC             VALUE    0 PIC  9(03).
           03 WSS-FTOR      COMP-3 VALUE +0   PIC S9(11)V9(04).
           03 WSS-IVM1                        PIC  9(03).
           03 WSS-SIND                        PIC  9(03).
           03 WSS-FCOL.
              05 WSS-SCOL                     PIC  9(02).
              05 WSS-ACOL                     PIC  9(02).
              05 WSS-MCOL                     PIC  9(02).
              05 WSS-DCOL                     PIC  9(02).
           03 WSS-PLZO                        PIC  X(02).
           03 WSS-IAUX                        PIC  9(03).
           03 WSS-TPZO.
              05 WSS-VPZO OCCURS 20 INDEXED BY WSS-ITBL.
                 07 WSS-CUPZ                  PIC X(12).
                 07 WSS-CCPZ                  PIC X(12).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGER4' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM INICIO.
           PERFORM ENCABEZADO.
           PERFORM PROCESO.
           PERFORM ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INICIO SECTION.
       INI-INICIO.
           DISPLAY 'INGRESE OFICINA : '.
           ACCEPT WSS-COFI.
           DISPLAY WSS-COFI.
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
           DISPLAY 'INGRESE INDICADOR ACTUALIZACION (S/N) : '.
           ACCEPT  WSS-IACT IN WSS-VARI.
           DISPLAY WSS-IACT IN WSS-VARI.
      *DAD fin  06-AUG-1992

           DISPLAY 'INGRESE INDICADOR DE DESPLIEGUE (S/N) : '.
           ACCEPT WSS-IDSP.
           DISPLAY WSS-IDSP.
      *
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
           IF ( WSS-COFI NOT > SPACES ) OR ( WSS-COFI = '*' )
               MOVE '*' TO WSS-COFI
           ELSE
               MOVE WSS-COFI TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               IF MSG-COD-MENS IN MSG = 'COD    NEX'
                   DISPLAY MSG-GLS-DESC '/ Oficina: ' WSS-COFI
                   PERFORM GNS-PRO-ABT.
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
           IF NOT ( WSS-IACT IN WSS-VARI = 'S' OR 'N' )
               DISPLAY 'Indicador de ACTAUALIZACION debe ser S o N'
               PERFORM GNS-PRO-ABT.
      *DAD fin  06-AUG-1992
           IF NOT ( WSS-IDSP = 'S' OR 'N' )
               DISPLAY 'Indicador de display debe ser S o N'
               PERFORM GNS-PRO-ABT.
           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
      *
           MOVE SPACES TO WSS-TPZO.
           MOVE 0      TO WSS-IAUX.
           MOVE 'PZO'  TO TAB-COD-TTAB IN TAB.
           MOVE SPACES TO TAB-COD-CTAB IN TAB.
           MOVE 'TAB'       TO FIO-SIST.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-INICIO.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-COD-TTAB IN TAB = 'PZO' )
               GO TO FIN-INICIO.
           IF TAB-COD-CTAB IN TAB NOT > SPACES
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-INICIO.
           ADD 1 TO WSS-IAUX.
           IF WSS-IAUX > 20
               DISPLAY 'EXISTEN MAS DE 20 CODIGOS DE PLAZO:OVERFLOW'
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-CUPZ(WSS-IAUX).
           MOVE TAB-CIC-CTAB IN TAB TO WSS-CCPZ(WSS-IAUX).
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-INICIO.
       FIN-INICIO.
           EXIT.

       ENCABEZADO SECTION.
       INI-ENCABEZADO.
           IF WSS-IDSP = 'N'
               GO TO FIN-ENCABEZADO.
           DISPLAY 'º   DOCUMENTO     '
                   'º PO '
                   'º NP º'.
           DISPLAY '+-----------------'
                   '+----'
                   '+----+'.
       FIN-ENCABEZADO.
           EXIT.

       PROCESO SECTION.
       INI-PROCESO.
           MOVE SPACES TO EVC-KEY-IEVC IN EVC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT FIO-STAT-OKS
               DISPLAY '%E - NO HAY REGISTROS EVC : ERROR'
               PERFORM GNS-PRO-ABT.
       ORG-PROCESO.
           MOVE EVC-CIC-IOPC IN EVC TO WSS-IOPC.
           MOVE 1 TO WSS-NEVC.
       LUP-PROCESO.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT FIO-STAT-OKS
               PERFORM CALCULAR
               GO TO FIN-PROCESO.
           IF EVC-CIC-IOPC IN EVC = WSS-IOPC
               ADD 1 TO WSS-NEVC
               GO TO LUP-PROCESO.
           MOVE EVC-KEY-IEVC IN EVC TO WSS-IEVC.
           PERFORM CALCULAR.
           MOVE WSS-IEVC TO EVC-KEY-IEVC IN EVC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF FIO-STAT-OKS
               GO TO ORG-PROCESO
           ELSE
               DISPLAY '%E - ABORTO DEL PROCESO '
                       'NO SE PUDO RECUPERAR POSICION EN EVC: '
                       EVC-KEY-IEVC IN EVC
               PERFORM GNS-PRO-ABT.
       FIN-PROCESO.
           EXIT.

       CALCULAR SECTION.
       INI-CALCULAR.
           MOVE WSS-IOPC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'OPC:ERROR: NO EXISTE OPERACION : '
                       OPC-CIC-IOPC IN OPC
               GO TO FIN-CALCULAR.
           IF ( WSS-COFI NOT = '*' ) AND
              ( WSS-COFI NOT = OPC-COD-OFOC )
               GO TO FIN-CALCULAR.
           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO FIN-CALCULAR.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               GO TO FIN-CALCULAR.
           IF OPC-VAL-SCRE IN OPC NOT > 0 OR
              OPC-NUM-VCVG IN OPC NOT > 0
               GO TO FIN-CALCULAR.
           IF OPC-IND-RENO IN OPC NOT = 'PRO'
               PERFORM CAL-PCTB
           ELSE
               PERFORM PRO-PROR.
           IF PLP-CMSG > SPACES
               MOVE 'COL'    TO MSG-COD-SIST
               MOVE PLP-CMSG TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY 'OPC:' WSS-IOPC '-' MSG-GLS-DESC PLP-MENS
      *         MOVE FIO-BCK-OUT TO FIO-CMND
      *         PERFORM COL-FIO-OPC
      *         IF FIO-STAT-OKS
      *             MOVE 0 TO WSS-CCHP
      *             IF WSS-IDSP = 'S'
      *                 DISPLAY 'BACK OUT OK'
      *             ELSE
      *                 NEXT SENTENCE
      *         ELSE
      *             DISPLAY WSS-IOPC ' EN BACK-OUT:' FIO-MENS
      *             PERFORM GNS-PRO-ABT
           ELSE
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
           IF WSS-IACT IN WSS-VARI = 'S'
      *DAD fin  06-AUG-1992
               IF WSS-CCHP NOT < 20
                   MOVE FIO-CHK-PNT TO FIO-CMND
                   PERFORM COL-FIO-OPC
                   IF FIO-STAT-OKS
                       MOVE 0 TO WSS-CCHP
                       IF WSS-IDSP = 'S'
                           DISPLAY 'CHECK POINT OK'
                       ELSE
                           NEXT SENTENCE
                   ELSE
                       DISPLAY WSS-IOPC ' EN CHK-PNT:' FIO-MENS
                       PERFORM GNS-PRO-ABT.
       FIN-CALCULAR.
           EXIT.

       CAL-PCTB SECTION.
       INI-CAL-PCTB.
           MOVE SPACES              TO PLP-CMSG PLP-MENS.

           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE 0                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT ( FIO-STAT-OKS AND
                    DLC-CIC-IOPC IN DLC = OPC-CIC-IOPC IN OPC )
               MOVE 'DLC    NEX'        TO PLP-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
               GO TO FIN-CAL-PCTB.
           IF ( DLC-COD-PLZO IN DLC NOT = '-1' AND
                DLC-COD-PLZO IN DLC NOT = '+1' ) OR
              ( OPC-IND-VSTR IN OPC NOT = 'S' )
               MOVE DLC-COD-PLZO IN DLC TO PLP-PLZO
               GO TO FIN-CAL-PCTB.

           MOVE ZEROES TO WSS-VDLC.
           MOVE 0      TO WSS-MDLC.
           ADD 1 OPC-NUM-IVEN IN OPC GIVING WSS-IVM1.
           MOVE OPC-FEC-FCOL IN OPC TO WSS-FCOL.
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE 1                   TO VEN-NUM-IVEN IN VEN.
       LUP-CAL-PCTB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           MOVE OPC-FEC-FCOL IN OPC TO VEN-FTBS.
           MOVE 'S'                 TO VEN-KEST.
           PERFORM COL-OBT-VEN.
           IF VEN-STAT-EOF
               PERFORM ACT-PLZO
               GO TO FIN-CAL-PCTB.
           IF NOT VEN-STAT-OKS
               IF VEN-STAT = 'CAMBTSANXABT' OR 'CAMBTSANVABT'
                   MOVE VEN-STAT TO PLP-CMSG
                   MOVE VEN-MEN2 TO PLP-MENS
                   GO TO FIN-CAL-PCTB
               ELSE
                   PERFORM ACT-PLZO
                   GO TO FIN-CAL-PCTB.
           PERFORM PRO-CALC.
           IF PLP-CMSG > SPACES
               GO TO FIN-CAL-PCTB.
           ADD 1 TO VEN-NUM-IVEN IN VEN.
           IF VEN-NUM-IVEN IN VEN > WSS-IVM1
               MOVE 'N' TO VEN-INIT.
           GO TO LUP-CAL-PCTB.
       FIN-CAL-PCTB.
           EXIT.

       PRO-PROR SECTION.
       INI-PRO-PROR.
           MOVE OPC-CIC-IOPC IN OPC TO WSS-OPCP.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE 0                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT ( FIO-STAT-OKS AND
                    DLC-CIC-IOPC IN DLC = OPC-CIC-IOPC IN OPC )
               MOVE 'DLC    NEX'        TO PLP-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
               GO TO FIN-PRO-PROR.
           IF ( DLC-COD-PLZO IN DLC NOT = '-1' AND
                DLC-COD-PLZO IN DLC NOT = '+1' )
               GO TO FIN-PRO-PROR.
           IF DLC-IIC-IOCO IN DLC NOT > ZEROES AND
              DLC-NUM-IDLO IN DLC NOT > ZEROES
               MOVE 'DLCIOCOBLK='       TO PLP-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
               GO TO FIN-PRO-PROR.
           IF DLC-CIC-IOCO IN DLC = DLC-CIC-IOPC IN DLC
               MOVE 'DLCIOCOIOPC='      TO PLP-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
               GO TO FIN-PRO-PROR.
           MOVE DLC-CIC-IOCO IN DLC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLCIOCONEX'        TO PLP-CMSG
               MOVE OPC-CIC-IOPC IN OPC TO PLP-MENS
               GO TO FIN-PRO-PROR.
           PERFORM CAL-PCTB.
           IF PLP-CMSG > SPACES
               GO TO FIN-PRO-PROR.
           MOVE WSS-OPCP TO DLC-CIC-IOPC IN DLC.
           MOVE 0        TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT ( FIO-STAT-OKS AND
                    DLC-CIC-IOPC IN DLC = WSS-OPCP )
               MOVE 'OPCSIN DLC' TO PLP-CMSG
               MOVE WSS-OPCP     TO PLP-MENS
               GO TO FIN-PRO-PROR.
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
           IF WSS-IACT IN WSS-VARI = 'S'
      *DAD fin  06-AUG-1992
               MOVE FIO-GET-KEY-UPD TO FIO-CMND
               PERFORM COL-FIO-DLC
               IF NOT FIO-STAT-OKS
                   ADD 1 TO WSS-ONUP IN WSS-VARI
                   MOVE 'DLC    NOGKU'      TO PLP-CMSG IN PLP-VARI
                   MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS IN PLP-VARI
                   GO TO FIN-PRO-PROR.
           MOVE DLC-COD-PLZO IN DLC TO WSS-PLZO.
           MOVE PLP-PLZO TO DLC-COD-PLZO IN DLC.

      *DAD ini  06-AUG-1992   (Memo.24-07-92)
      * Valida que el plazo calculado corresponda a codigo menos 1 ano
      * si el antiguo es '-1' o a mas 1 ano si el era '+1'
           IF WSS-PLZO IN WSS-VARI = '-1'
               IF PLP-PLZO IN PLP-VARI = 'C1' OR 'C2' OR 'C3' OR 'C4'
                   NEXT SENTENCE
               ELSE
                   MOVE 'PLPCALCNO-1'        TO PLP-CMSG IN PLP-VARI
                   MOVE WSS-OPCP IN WSS-VARI TO WSS-MEN1 IN WSS-VARI
                   MOVE PLP-PLZO IN PLP-VARI TO WSS-MEN2 IN WSS-VARI
                   MOVE WSS-MENS IN WSS-VARI TO PLP-MENS IN PLP-VARI
                   ADD 1 TO WSS-ONUP IN WSS-VARI
                   IF WSS-IACT IN WSS-VARI = 'S'
                       MOVE FIO-ULK-REC TO FIO-CMND
                       PERFORM COL-FIO-DLC
                       GO TO FIN-PRO-PROR
                   ELSE
                       GO TO FIN-PRO-PROR
           ELSE
           IF WSS-PLZO IN WSS-VARI = '+1'
               IF PLP-PLZO IN PLP-VARI = 'M1' OR 'M2' OR 'L0'
                   NEXT SENTENCE
               ELSE
                   MOVE 'PLPCALCNO+1'        TO PLP-CMSG IN PLP-VARI
                   MOVE WSS-OPCP IN WSS-VARI TO WSS-MEN1 IN WSS-VARI
                   MOVE PLP-PLZO IN PLP-VARI TO WSS-MEN2 IN WSS-VARI
                   MOVE WSS-MENS IN WSS-VARI TO PLP-MENS IN PLP-VARI
                   ADD 1 TO WSS-ONUP IN WSS-VARI
                   IF WSS-IACT IN WSS-VARI = 'S'
                       MOVE FIO-ULK-REC TO FIO-CMND
                       PERFORM COL-FIO-DLC
                       GO TO FIN-PRO-PROR
                   ELSE
                       GO TO FIN-PRO-PROR.

      * Valida que exista Registro DCC asociado al plazo calculado
      * Lee registro OPC del proceso
           MOVE WSS-OPCP IN WSS-VARI TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NEX'         TO PLP-CMSG IN PLP-VARI
               MOVE WSS-OPCP IN WSS-VARI TO PLP-MENS IN PLP-VARI
               GO TO FIN-PRO-PROR.

      * Lee registro DCC asociado a registros DLC de la OPC del proceso
           MOVE 'CAP'               TO DCC-MSC-TCCT IN DCC.
           MOVE OPC-COD-VCOC IN OPC TO DCC-COD-VCAM IN DCC.
           MOVE OPC-COD-COOC IN OPC TO DCC-COD-COCR IN DCC.
           MOVE DLC-COD-PLZO IN DLC TO DCC-COD-PLZO IN DCC.
           MOVE DLC-IND-SCRT IN DLC TO DCC-IND-SCRT IN DCC.
      *DAD ini  12-AUG-1992   (Memo.12-08-92)
           IF DLC-IND-SCTB IN DLC = 'MUL'
               MOVE SPACES TO DCC-IND-SCTB IN DCC
           ELSE
      *DAD fin  12-AUG-1992
               MOVE DLC-IND-SCTB IN DLC TO DCC-IND-SCTB IN DCC.
           MOVE DLC-IND-SCBZ IN DLC TO DCC-IND-SCBZ IN DCC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           IF NOT FIO-STAT-OKS
      *        OR DCC-IND-VIGE IN DCC NOT = 'S'
               MOVE 'PLPDCC NEX'         TO PLP-CMSG IN PLP-VARI
               MOVE DCC-COD-CCTB IN DCC  TO WSS-MEN1 IN WSS-VARI
               MOVE OPC-CIC-IOPC IN OPC  TO WSS-MEN2 IN WSS-VARI
               MOVE WSS-MENS IN WSS-VARI TO PLP-MENS IN PLP-VARI
               IF WSS-IACT IN WSS-VARI = 'S'
                   MOVE FIO-ULK-REC TO FIO-CMND
                   PERFORM COL-FIO-DLC
                   GO TO FIN-PRO-PROR
               ELSE
                   GO TO FIN-PRO-PROR.

           IF WSS-IACT IN WSS-VARI = 'S'
      *DAD fin  06-AUG-1992
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-DLC
               IF NOT FIO-STAT-OKS
                   ADD 1 TO WSS-ONUP IN WSS-VARI
                   MOVE 'FIO    NOMOD'      TO PLP-CMSG IN PLP-VARI
                   MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS IN PLP-VARI
                   GO TO FIN-PRO-PROR.
           ADD 1 TO WSS-CCHP.
           ADD 1 TO WSS-OSUP.
           IF WSS-IDSP = 'S'
               DISPLAY 'º ' DLC-KEY-IDLC IN DLC ' º'
                       ' ' WSS-PLZO             ' º'
                       ' ' DLC-COD-PLZO IN DLC  ' º'.
       FIN-PRO-PROR.
           EXIT.

       PRO-CALC SECTION.
       INI-PRO-CALC.
           IF VEN-NUM-IDLC IN VEN > WSS-LDLC
               MOVE 'PLPIDLCOVFLW' TO PLP-CMSG
               MOVE WSS-LDLC       TO PLP-MENS
               GO TO FIN-PRO-CALC.
           MOVE VEN-NUM-IVEN IN VEN TO PLP-VCOR.
           MOVE WSS-DCOL            TO FEC-DEC1.
           MOVE WSS-MCOL            TO FEC-MEC1.
           MOVE WSS-SCOL            TO FEC-SEC1.
           MOVE WSS-ACOL            TO FEC-AEC1.
           MOVE VEN-NUM-DVEN IN VEN TO FEC-DEC2.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-MEC2.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-SEC2.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'PLPDIFFDIA' TO PLP-CMSG
               GO TO FIN-PRO-CALC.
           MULTIPLY VEN-VAL-CAPI IN VEN BY FEC-NDIA GIVING WSS-FTOR.
           IF WSS-IDLC(VEN-NUM-IDLC IN VEN) NOT > ZEROES
               MOVE WSS-FTOR TO WSS-SUMA(VEN-NUM-IDLC IN VEN)
           ELSE
               ADD WSS-FTOR  TO WSS-SUMA(VEN-NUM-IDLC IN VEN).
           MOVE VEN-NUM-IDLC IN VEN TO WSS-IDLC(VEN-NUM-IDLC IN VEN).
           IF VEN-NUM-IDLC IN VEN > WSS-MDLC
               MOVE VEN-NUM-IDLC IN VEN TO WSS-MDLC.
       FIN-PRO-CALC.
           EXIT.

       ACT-PLZO SECTION.
       INI-ACT-PLZO.
           MOVE 0 TO WSS-SIND.
       LUP-ACT-PLZO.
           ADD 1 TO WSS-SIND.
           IF WSS-SIND > WSS-MDLC
               GO TO FIN-ACT-PLZO.
           IF WSS-IDLC(WSS-SIND) NOT > ZEROES
               GO TO LUP-ACT-PLZO.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE WSS-IDLC(WSS-SIND)  TO DLC-NUM-IDLC IN DLC.
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
           IF WSS-IACT IN WSS-VARI = 'N'
               MOVE FIO-GET-KEY TO FIO-CMND
           ELSE
      *DAD fin  06-AUG-1992
               MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-ONUP
               MOVE 'DLC    NOGKU'      TO PLP-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
               GO TO FIN-ACT-PLZO.
           DIVIDE WSS-SUMA(WSS-SIND) BY DLC-VAL-NDLC IN DLC GIVING
                  WSS-FTOR ROUNDED.
           IF WSS-FTOR < 30
               MOVE 'C1' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 90
               MOVE 'C2' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 180
               MOVE 'C3' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 366
               MOVE 'C4' TO PLP-PLZO
           ELSE
      *DAD ini  11-MAY-1992   (Memo.16-04-92)
           IF WSS-FTOR < 1096
               MOVE 'M1' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 3651
               MOVE 'M2' TO PLP-PLZO
           ELSE
               MOVE 'L0' TO PLP-PLZO.
      *DAD fin  11-MAY-1992
           MOVE DLC-COD-PLZO IN DLC TO WSS-PLZO.
           SET WSS-ITBL TO 1.
           SEARCH WSS-VPZO VARYING WSS-IAUX
             AT END
                 GO TO EPZ-ACT-PLZO
             WHEN WSS-CCPZ( WSS-ITBL ) = PLP-PLZO
                 MOVE WSS-CUPZ( WSS-ITBL) TO DLC-COD-PLZO IN DLC.
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
           MOVE DLC-COD-PLZO IN DLC TO PLP-PLZO IN PLP-VARI.

      * Valida que el plazo calculado corresponda a codigo menos 1 ano
      * si el antiguo es '-1' o a mas 1 ano si el era '+1'
           IF WSS-PLZO IN WSS-VARI = '-1'
               IF PLP-PLZO IN PLP-VARI = 'C1' OR 'C2' OR 'C3' OR 'C4'
                   NEXT SENTENCE
               ELSE
                   MOVE 'PLPCALCNO-1'        TO PLP-CMSG IN PLP-VARI
                   MOVE OPC-CIC-IOPC IN OPC  TO WSS-MEN1 IN WSS-VARI
                   MOVE PLP-PLZO IN PLP-VARI TO WSS-MEN2 IN WSS-VARI
                   MOVE WSS-MENS IN WSS-VARI TO PLP-MENS IN PLP-VARI
                   ADD 1 TO WSS-ONUP IN WSS-VARI
                   IF WSS-IACT IN WSS-VARI = 'S'
                       MOVE FIO-ULK-REC TO FIO-CMND
                       PERFORM COL-FIO-DLC
                       GO TO FIN-ACT-PLZO
                   ELSE
                       GO TO FIN-ACT-PLZO
           ELSE
           IF WSS-PLZO IN WSS-VARI = '+1'
               IF PLP-PLZO IN PLP-VARI = 'M1' OR 'M2' OR 'L0'
                   NEXT SENTENCE
               ELSE
                   MOVE 'PLPCALCNO+1'        TO PLP-CMSG IN PLP-VARI
                   MOVE OPC-CIC-IOPC IN OPC  TO WSS-MEN1 IN WSS-VARI
                   MOVE PLP-PLZO IN PLP-VARI TO WSS-MEN2 IN WSS-VARI
                   MOVE WSS-MENS IN WSS-VARI TO PLP-MENS IN PLP-VARI
                   ADD 1 TO WSS-ONUP IN WSS-VARI
                   IF WSS-IACT IN WSS-VARI = 'S'
                       MOVE FIO-ULK-REC TO FIO-CMND
                       PERFORM COL-FIO-DLC
                       GO TO FIN-ACT-PLZO
                   ELSE
                       GO TO FIN-ACT-PLZO.

      * Valida que exista Registro DCC asociado al plazo calculado
      * Lee registro DCC asociado a registros DLC de la OPC del proceso
           MOVE 'CAP'               TO DCC-MSC-TCCT IN DCC.
           MOVE OPC-COD-VCOC IN OPC TO DCC-COD-VCAM IN DCC.
           MOVE OPC-COD-COOC IN OPC TO DCC-COD-COCR IN DCC.
           MOVE DLC-COD-PLZO IN DLC TO DCC-COD-PLZO IN DCC.
           MOVE DLC-IND-SCRT IN DLC TO DCC-IND-SCRT IN DCC.
      *DAD ini  12-AUG-1992   (Memo.12-08-92)
           IF DLC-IND-SCTB IN DLC = 'MUL'
               MOVE SPACES TO DCC-IND-SCTB IN DCC
           ELSE
      *DAD fin  12-AUG-1992
               MOVE DLC-IND-SCTB IN DLC TO DCC-IND-SCTB IN DCC.
           MOVE DLC-IND-SCBZ IN DLC TO DCC-IND-SCBZ IN DCC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           IF NOT FIO-STAT-OKS
      *        OR DCC-IND-VIGE IN DCC NOT = 'S'
               MOVE 'PLPDCC NEX'         TO PLP-CMSG IN PLP-VARI
               MOVE DCC-COD-CCTB IN DCC  TO WSS-MEN1 IN WSS-VARI
               MOVE OPC-CIC-IOPC IN OPC  TO WSS-MEN2 IN WSS-VARI
               MOVE WSS-MENS IN WSS-VARI TO PLP-MENS IN PLP-VARI
               IF WSS-IACT IN WSS-VARI = 'S'
                   MOVE FIO-ULK-REC TO FIO-CMND
                   PERFORM COL-FIO-DLC
                   GO TO FIN-ACT-PLZO
               ELSE
                   GO TO FIN-ACT-PLZO.

           IF WSS-IACT IN WSS-VARI = 'S'
      *DAD fin  06-AUG-1992
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-DLC
               IF NOT FIO-STAT-OKS
                   ADD 1 TO WSS-ONUP
                   MOVE 'DLC    NOMOD'      TO PLP-CMSG
                   MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
                   GO TO FIN-ACT-PLZO.
           ADD 1 TO WSS-CCHP.
           ADD 1 TO WSS-OSUP.
           IF WSS-IDSP = 'S'
               DISPLAY 'º ' DLC-KEY-IDLC IN DLC ' º'
                       ' ' WSS-PLZO             ' º'
                       ' ' DLC-COD-PLZO IN DLC  ' º'.
           GO TO LUP-ACT-PLZO.
       EPZ-ACT-PLZO.
           ADD 1 TO WSS-ONUP.
           MOVE FIO-ULK-REC TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           MOVE 'TABPLZOCICNX' TO PLP-CMSG.
           MOVE PLP-PLZO       TO PLP-MENS.
       FIN-ACT-PLZO.
           EXIT.

       ESTADISTICAS SECTION.
       INI-ESTADISTICAS.
           IF WSS-IDSP = 'S'
               DISPLAY '+-----------------'
                       '+----'
                       '+----+'
               DISPLAY '  '.
           DISPLAY 'DOCUMENTOS  CON PLAZO ACTUALIZADO   : ' WSS-OSUP.
           DISPLAY 'OPERACIONES CON ERROR AL ACTUALIZAR : ' WSS-ONUP.
           ADD WSS-OSUP WSS-ONUP GIVING WSS-OTOT.
           DISPLAY 'TOTAL                               : ' WSS-OTOT.
       FIN-ESTADISTICAS.
           EXIT.

       COPY GNSBPFEC.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBFMSC.
       COPY GNSBBMSG.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
      *SMC-INI 09.09.1993
       COPY COLBGINT.
      *SMC-FIN 09.09.1993
       COPY GNSBGPES.
       COPY GNSBGEND.
       COPY GNSBGABT.

       COPY COLBGVEN.
       COPY COLBFRCV.
       COPY COLBFVEN.
       COPY COLBFOPC.
       COPY COLBFICG.
       COPY COLBFDLC.
       COPY COLBFEVC.
       COPY COLBFTOC.
      *DAD ini  06-AUG-1992   (Memo.24-07-92)
       COPY COLBFDCC.
      *DAD fin  06-AUG-1992

       COPY TABBFCAM.
       COPY TABBBOFI.
       COPY TABBFOFI.
