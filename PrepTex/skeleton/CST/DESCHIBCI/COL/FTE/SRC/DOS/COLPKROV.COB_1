       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPKROV.
       AUTHOR.          CONSIST.
      * Programa de Carga de Archivo ROV Nuevo.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OLD-REQA==.
      *<<<< WSS
      *COPY GNSBRMSG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY COLBRROV.
      *COPY COLBROLD.         COPIAR DENTRO DEL PROGRAMA.
      *    Relacion Operacion Vencimientos
      *    -------------------------------
      *
      *    Nombre Registro : OLD
      *    Clave(s)        : OLD-KEY-IOLD(UU), OLD-KEY-IVCT(DU)
      *                      OLD-KEY-IREG(UU)
      *    Largo           : 064
      *    Bloqueo         : 1
      *    Observaciones   :
      *
       01  OLD.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OLD-GLS-FLAG                            PIC X(01).
      *
      *MOD-01  STR COD-IREN
      *    Clave Primaria Registro
           03  OLD-KEY-IREG.
      *
      *        Identificacion Invocacion
               05  OLD-KEY-IINV.
      *
      *            Indicador Acceso Invocacion ( I/O )
                   07  OLD-IND-AINV                    PIC X(01).
      *
      *            Time Stamp Invocacion
                   07  OLD-STP-IINV.
      *                Fecha Invocacion
                       09  OLD-FEC-FINV.
                           11  OLD-NUM-SINV            PIC 9(02).
                           11  OLD-NUM-AINV            PIC 9(02).
                           11  OLD-NUM-MINV            PIC 9(02).
                           11  OLD-NUM-DINV            PIC 9(02).
      *
      *                Hora Invocacion
                       09  OLD-HRA-HRIN.
                           11  OLD-NUM-HHIN            PIC 9(02).
                           11  OLD-NUM-MMIN            PIC 9(02).
                           11  OLD-NUM-SSIN            PIC 9(02).
      *
      *            Codigo Origen Invocacion
                   07  OLD-COD-OINV.
      *
      *                Miscelaneo Tipo Origen
                       09  OLD-MSC-TINV                PIC X(01).
      *
      *                Codigo Tipo Origen
                       09  OLD-COD-TINV                PIC X(03).
      *
      *KEY     Identificacion Relacion Operacion Vencimiento
               05  OLD-KEY-IOLD.
      *
      *            Cod. Int. Comp. Operacion Credito ( nueva )
                   07  OLD-CIC-IOPC.
      *
      *                Centro Asignacion Identificacion
                       09  OLD-CAI-IOPC.
                           11  OLD-COD-TOPC            PIC X(01).
                           11  OLD-COD-COPC            PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  OLD-IIC-IOPC.
                           11  OLD-COD-OOPC.
                               13  OLD-COD-SOPC        PIC X(02).
                               13  OLD-IND-SOPC        PIC X(01).
                           11  OLD-GLS-NOPC            PIC X(05).
      *
      *            Identificacion Vencimiento
                   07  OLD-KEY-IVCT.
      *
      *                Cod. Int. Comp. Operacion Credito
                       09  OLD-CIC-IOPV.
      *
      *                    Centro Asignacion Identificacion
                           11  OLD-CAI-IOPV.
                               13  OLD-COD-TOPV        PIC X(01).
                               13  OLD-COD-COPV        PIC X(03).
      *
      *                    Ident. Interna Computacional
                           11  OLD-IIC-IOPV.
                               13  OLD-COD-OOPV.
                                   15  OLD-COD-SOPV    PIC X(02).
                                   15  OLD-IND-SOPV    PIC X(01).
                               13  OLD-GLS-NOPV        PIC X(05).
      *
      *                Num Ident. Vencimiento
                       09  OLD-NUM-IVCT                PIC 9(03).
      *
      *ELI-01
      *    Digito Verificador Nueva Operacion
      *    03  OLD-VRF-INOP                            PIC X(01).
      *
      *ELI-01
      *    Digito Verificador Operacion Credito
      *    03  OLD-VRF-IOPC                            PIC X(01).
      *
      *MOD-01  PIC 9(13)V9(05)
      *REQ Valor Renovado
         03  OLD-SGV-RENO             COMP-3           PIC S9(11)V9(04).
      *
      *ING-01
      *REQ Codigo Valor de Cambio Valor Renovado        TAB VLR
           03  OLD-COD-VCVR.
      *
      *        Miscelaneo Tipo Valor                    TAB TVAL
               05  OLD-MSC-TVVR                        PIC X(01).
      *
      *        Codigo Valor
               05  OLD-COD-CVVR                        PIC X(03).
      *
      *        Codigo Tipo Cambio
               05  OLD-COD-TCVR                        PIC X(02).
      *
      *MOD-01  PIC X(22)
      *    Glosa Disponible
           03  OLD-GLS-DISP                            PIC X(03).
       01  WSS-VARI.
           03 WSS-NUM-ROLD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RNEW           VALUE 0      PIC 9(05).
           03 WSS-NUM-RMOD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RBAD           VALUE 0      PIC 9(05).
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPKROV' TO FIO-PROG.
           PERFORM BUS-FSIS.
      *    PERFORM OPEN-FILE.
           PERFORM LUP-PUT-REG.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           COPY GNSBGGBK.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       LUP-PUT-REG SECTION.
      *-------------------
       INI-LUP-REG.
      * Programacion de Ciclo para Registro OLD
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY REGISTROS EN ARCHIVO ANTIGUO '
               GO TO FIN-LUP-REG.
      *         MOVE 'COL'          TO MSG-COD-SIST
      *         MOVE 'ROV    ROVNX' TO MSG-COD-MENS
      *         PERFORM GET-MSG
      *         DISPLAY MSG-GLS-DESC   ROV-KEY-IROV IN ROV
       LUP-OLD-NEW.
      * Proceso de Seleccion y Clasificacion a Partir del Registro ANTIG
           ADD 1 TO WSS-NUM-ROLD IN WSS-VARI.
           PERFORM BUS-REG-NEW.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF FIO-STAT-OKS
               GO TO LUP-OLD-NEW.
       FIN-LUP-REG.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro NEW al Procesar Registro ANT
       BUS-REG-NEW SECTION.
       INI-BUS-REG-NEW.
      * Formacion del Registro NEW
           MOVE SPACES       TO ROV.
           MOVE OLD-KEY-IOLD TO ROV-KEY-IROV.
           IF OLD-SGV-RENO NOT NUMERIC
               DISPLAY 'RENO NO NUMERICO ' OLD-KEY-IOLD
               MOVE ZEROES TO OLD-SGV-RENO.
           MOVE OLD-SGV-RENO TO ROV-SGV-RENO.
           MOVE OLD-COD-VCVR TO ROV-COD-VCVR.
      * Genera el Registro NEW
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT FIO-STAT-OKS
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-ROV
               ADD 1 TO WSS-NUM-RNEW IN WSS-VARI
           ELSE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-ROV
               ADD 1 TO WSS-NUM-RMOD IN WSS-VARI.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RBAD IN WSS-VARI
               DISPLAY 'REGISTRO ERRONEO : '  ROV.
       FIN-BUS-REG-NEW.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OLD LEIDOS   : ' WSS-NUM-ROLD IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-NUM-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS NEW GRABADOS : ' WSS-NUM-RNEW IN WSS-VARI.
           DISPLAY 'REGISTROS MODIFICADOS  : ' WSS-NUM-RMOD IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           PERFORM COL-FIO-OLD.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
      *COPY GNSBFMSG.
      *COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY COLBFROV.
      *COPY COLBFOLD.            COPIAR DENTRO DEL PROGRAMA.
      * Modulo FIO para registro OLD en DATACOM, 16-SEP-1988 17:56:05.01
       COL-FIO-OLD SECTION.
       INI-COL-FIO-OLD.
           MOVE ADR-OLD-REQA TO ADR-REQA.
           MOVE 'ROV' TO ADR-TABL IN ADR-REQA.
           MOVE IFD-GSIS( 14 ) TO ADR-DBID IN ADR-REQA.
           MOVE 'ROV00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-OLD.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OLD-KEY-IOLD' OR FIO-AKEY NOT > SPACES )
               MOVE OLD-KEY-IOLD IN OLD TO ADR-VKEY IN ADR-REQA
               MOVE 'ROV00' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-OLD, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
       CON-COL-FIO-OLD.
           MOVE OLD TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OLD
           ELSE
               MOVE 'COLOLD' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-OLD-REQA.
       FIN-COL-FIO-OLD.
           EXIT.
