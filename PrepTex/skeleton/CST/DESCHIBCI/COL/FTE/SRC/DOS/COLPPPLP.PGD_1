       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.    COLPPPLP.
       AUTHOR.        CONSIST.
       DATE-WRITTEN.  05-SEP-1994      ( Ultima Modificacion )

      *Calcula plazo contable para los documentos legales
      *de una operacion de credito

       ENVIRONMENT DIVISION.
      *=====================
       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWCSCR.
       COPY GNSWVSCR.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWVIDD.
       COPY GNSWGFRM.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY GNSWGPES.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSBRMSC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.

       COPY COLBRRCV.
       COPY COLBRVEN.
       COPY COLBROPC.
       COPY COLBRICG.
       COPY COLBRDLC.
       COPY COLBREVC.
       COPY COLBRTOC.
       COPY COLBRROV.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
      *DAD ini  06-AUG-1992   (Memo.22-07-92 Pto.4)
       COPY COLBRDCC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCC-REQA==.
      *DAD fin  06-AUG-1992
       COPY COLWGVEN.

       COPY TABBRCAM.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.

       COPY COLWGPLP.

       01  WSS-VARI.
           03 WSS-TDLC.
              05 WSS-IDLC          OCCURS 100 PIC  9(03).
      *DAD ini  05-SEP-1994   (Memo.01-09-94)
              05 WSS-SUMA   COMP-3 OCCURS 100 PIC S9(14)V9(04).
      *DAD fin  05-SEP-1994
      *DAD ini  21-SEP-1992
              05 WSS-VDLC   COMP-3 OCCURS 100 PIC  9(11)V9(04).
      *DAD fin  21-SEP-1992
           03 WSS-LDLC             VALUE  100 PIC  9(03).
           03 WSS-MDLC             VALUE    0 PIC  9(03).
      *DAD ini  05-SEP-1994   (Memo.01-09-94)
           03 WSS-FTOR      COMP-3 VALUE +0   PIC S9(14)V9(04).
      *DAD fin  05-SEP-1994
           03 WSS-IVM1                        PIC  9(03).
           03 WSS-IOPC                        PIC  X(12).
           03 WSS-SIND                        PIC  9(03).
           03 WSS-PZOO      VALUE SPACES      PIC  X(03).
           03 WSS-FCOL.
              05 WSS-SCOL                     PIC  9(02).
              05 WSS-ACOL                     PIC  9(02).
              05 WSS-MCOL                     PIC  9(02).
              05 WSS-DCOL                     PIC  9(02).

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
      *LGP ini 30.03.94
           03 FILLER                          PIC X(693).
      *LGP fin 30.03.94
      *JSS 14-05-93
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPPPLP'  TO FIO-PROG.
           MOVE DFHCOMMAREA(1:EIBCALEN) TO PLP-CMMA.
           MOVE 'COL'       TO SCR-SIST.
           MOVE PLP-VIDD    TO IDD-FSIS.
      *LGP ini 31.03.94
           MOVE PLP-MSFL    TO FIO-MSFL.
      *LGP fin 31.03.94
           PERFORM INI-VARI.
           PERFORM PRO-CPLP.
      *LGP ini 31.03.94
           MOVE FIO-MSFL TO PLP-MSFL.
      *LGP fin 31.03.94
           MOVE PLP-CMMA TO DFHCOMMAREA.
        FIN-MAIN.
      *DSP-JSS 14-05-93
           EXEC CICS RETURN END-EXEC.

       INI-VARI SECTION.
       INI-INI-VARI.
           MOVE 1 TO WSS-SIND.
       LUP-INI-VARI.
           MOVE 0 TO WSS-SUMA(WSS-SIND)  WSS-IDLC(WSS-SIND)
      *DAD ini  21-SEP-1992
                     WSS-VDLC(WSS-SIND).
      *DAD fin  21-SEP-1992
           ADD 1 TO WSS-SIND.
           IF WSS-SIND NOT > WSS-LDLC
               GO TO LUP-INI-VARI.
       FIN-INI-VARI.
           EXIT.

       PRO-CPLP SECTION.
       INI-PRO-CPLP.
           MOVE SPACES TO PLP-CMSG PLP-MENS WSS-PZOO.
           MOVE 0      TO PLP-VCOR.
           MOVE PLP-IOPC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NEX'        TO PLP-CMSG
               MOVE OPC-CIC-IOPC IN OPC TO PLP-MENS
               GO TO FIN-PRO-CPLP.
           ADD 1 OPC-NUM-IVEN IN OPC GIVING WSS-IVM1.
           IF OPC-IND-RENO IN OPC = 'PRO'
               PERFORM BUS-FEC-FCOL
               IF PLP-CMSG > SPACES
                   GO TO FIN-PRO-CPLP
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE OPC-FEC-FCOL IN OPC TO WSS-FCOL.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM PRO-OPC-SSTR
           ELSE
               PERFORM PRO-OPC-NSTR.
      *DAD ini  06-AUG-1992   (Memo.22-07-92 Pto.4)
           IF PLP-CMSG IN PLP-VARI > SPACES
               GO TO FIN-PRO-CPLP.
      * Lee registro OPC del proceso
           MOVE PLP-IOPC IN PLP-VARI TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NEX'        TO PLP-CMSG IN PLP-VARI
               MOVE OPC-CIC-IOPC IN OPC TO PLP-MENS IN PLP-VARI
               GO TO FIN-PRO-CPLP.
      * Lee 1er. registro DLC asociado a la OPC del proceso
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE 0                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT ( FIO-STAT-OKS AND
                    DLC-CIC-IOPC IN DLC = OPC-CIC-IOPC IN OPC )
               MOVE 'OPC    DLCNX'      TO PLP-CMSG IN PLP-VARI
               MOVE OPC-CIC-IOPC IN OPC TO PLP-MENS IN PLP-VARI
               GO TO FIN-PRO-CPLP.
       DCC-PRO-CPLP.
      * Lee registro DCC asociado a registros DLC de la OPC del proceso
           MOVE 'CAP'               TO DCC-MSC-TCCT IN DCC.
           MOVE OPC-COD-VCOC IN OPC TO DCC-COD-VCAM IN DCC.
           MOVE OPC-COD-COOC IN OPC TO DCC-COD-COCR IN DCC.
           MOVE DLC-COD-PLZO IN DLC TO DCC-COD-PLZO IN DCC.
           MOVE DLC-IND-SCRT IN DLC TO DCC-IND-SCRT IN DCC.
           MOVE DLC-IND-SCTB IN DLC TO DCC-IND-SCTB IN DCC.
           MOVE DLC-IND-SCBZ IN DLC TO DCC-IND-SCBZ IN DCC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           IF NOT FIO-STAT-OKS
               MOVE 'PLPDCC NEX'        TO PLP-CMSG IN PLP-VARI
               MOVE DCC-COD-CCTB IN DCC TO PLP-MENS IN PLP-VARI
               GO TO FIN-PRO-CPLP
           ELSE
           IF DCC-IND-VIGE IN DCC NOT = 'S'
               MOVE 'PLPDCC NVG'        TO PLP-CMSG IN PLP-VARI
               MOVE DCC-COD-CCTB IN DCC TO PLP-MENS IN PLP-VARI
               GO TO FIN-PRO-CPLP.
      * Lee siguientes registros DLC asociados a la OPC del proceso
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT ( FIO-STAT-OKS AND
                    DLC-CIC-IOPC IN DLC = OPC-CIC-IOPC IN OPC )
               GO TO FIN-PRO-CPLP.
           GO TO DCC-PRO-CPLP.
      *DAD fin  06-AUG-1992
       FIN-PRO-CPLP.
           EXIT.

       BUS-FEC-FCOL SECTION.
       INI-BUS-FEC-FCOL.
           MOVE OPC-FEC-FCOL IN OPC TO WSS-FCOL.
           MOVE OPC-CIC-IOPC IN OPC TO ROV-CIC-IOPC IN ROV.
           MOVE SPACES              TO ROV-KEY-IVCT IN ROV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT ( FIO-STAT-OKS AND
                    ROV-CIC-IOPC IN ROV = OPC-CIC-IOPC IN OPC )
               GO TO FIN-BUS-FEC-FCOL.
           MOVE OPC-CIC-IOPC IN OPC TO WSS-IOPC.
           MOVE ROV-CIC-IOPV IN ROV TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               GO TO OPC-BUS-FEC-FCOL.
           MOVE OPC-FEC-FCOL IN OPC TO WSS-FCOL.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE 0                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT ( FIO-STAT-OKS AND
                    DLC-CIC-IOPC IN DLC = OPC-CIC-IOPC IN OPC )
               GO TO OPC-BUS-FEC-FCOL.
           MOVE DLC-COD-PLZO IN DLC TO WSS-PZOO.
           IF ( DLC-CIC-IOCO IN DLC = DLC-CIC-IOPC IN DLC ) OR
              ( DLC-IIC-IOCO IN DLC NOT > ZEROES AND
                DLC-NUM-IDLO IN DLC NOT > ZEROES )
               GO TO OPC-BUS-FEC-FCOL.
           MOVE DLC-CIC-IOCO IN DLC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF FIO-STAT-OKS
               MOVE OPC-FEC-FCOL IN OPC TO WSS-FCOL.
       OPC-BUS-FEC-FCOL.
           MOVE WSS-IOPC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       FIN-BUS-FEC-FCOL.
           EXIT.

       PRO-OPC-SSTR SECTION.
       INI-PRO-OPC-SSTR.
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE 1                   TO VEN-NUM-IVEN IN VEN.
       LUP-PRO-OPC-SSTR.
           MOVE FIO-GET-KEY TO FIO-CMND.
           MOVE OPC-FEC-FCOL IN OPC TO VEN-FTBS.
           MOVE 'S'                 TO VEN-KEST.
           PERFORM COL-OBT-VEN.
           IF VEN-STAT-EOF
               PERFORM ACT-PLZO
               GO TO FIN-PRO-OPC-SSTR.
           IF NOT VEN-STAT-OKS
               IF VEN-STAT = 'CAMBTSANXABT' OR 'CAMBTSANVABT'
                   MOVE VEN-STAT TO PLP-CMSG
                   MOVE VEN-MEN2 TO PLP-MENS
                   GO TO FIN-PRO-OPC-SSTR
               ELSE
                   PERFORM ACT-PLZO
                   GO TO FIN-PRO-OPC-SSTR.
           PERFORM PRO-CALC.
           IF PLP-CMSG > SPACES
               GO TO FIN-PRO-OPC-SSTR.
           ADD 1 TO VEN-NUM-IVEN IN VEN.
           IF VEN-NUM-IVEN IN VEN > WSS-IVM1
               MOVE 'N' TO VEN-INIT.
           GO TO LUP-PRO-OPC-SSTR.
       FIN-PRO-OPC-SSTR.
           EXIT.

       PRO-OPC-NSTR SECTION.
       INI-PRO-OPC-NSTR.
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE 0                   TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-PRO-OPC-NSTR.
           PERFORM COL-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    VEN-CIC-IOPC IN VEN = OPC-CIC-IOPC IN OPC )
               PERFORM ACT-PLZO
               GO TO FIN-PRO-OPC-NSTR.
           PERFORM PRO-CALC.
           IF PLP-CMSG > SPACES
               GO TO FIN-PRO-OPC-NSTR.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-OPC-NSTR.
       FIN-PRO-OPC-NSTR.
           EXIT.

       PRO-CALC SECTION.
       INI-PRO-CALC.
      *SMC-INI 26.08.1993
           IF PLP-IVCT = VEN-NUM-IVCT IN VEN
              MOVE VEN-FEC-FVEN IN VEN TO PLP-FVEN.
      *SMC-FIN 26.08.1993
           IF VEN-NUM-IDLC IN VEN > WSS-LDLC
               MOVE 'PLPIDLCOVFLW' TO PLP-CMSG
               MOVE WSS-LDLC       TO PLP-MENS
               GO TO FIN-PRO-CALC.
           MOVE VEN-NUM-IVEN IN VEN TO PLP-VCOR.
           MOVE WSS-DCOL            TO FEC-DEC1.
           MOVE WSS-MCOL            TO FEC-MEC1.
           MOVE WSS-SCOL            TO FEC-SEC1.
           MOVE WSS-ACOL            TO FEC-AEC1.
           MOVE VEN-NUM-DVEN IN VEN TO FEC-DEC2.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-MEC2.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-SEC2.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *JSS PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'PLPDIFFDIA' TO PLP-CMSG
               GO TO FIN-PRO-CALC.
           MULTIPLY VEN-VAL-CAPI IN VEN BY FEC-NDIA GIVING WSS-FTOR.
           ADD WSS-FTOR            TO WSS-SUMA(VEN-NUM-IDLC IN VEN).
      *DAD ini  21-SEP-1992
           ADD VEN-VAL-CAPI IN VEN TO WSS-VDLC(VEN-NUM-IDLC IN VEN).
      *DAD fin  21-SEP-1992
           MOVE VEN-NUM-IDLC IN VEN TO WSS-IDLC(VEN-NUM-IDLC IN VEN).
           IF VEN-NUM-IDLC IN VEN > WSS-MDLC
               MOVE VEN-NUM-IDLC IN VEN TO WSS-MDLC.
       FIN-PRO-CALC.
           EXIT.

       ACT-PLZO SECTION.
       INI-ACT-PLZO.
           MOVE 0 TO WSS-SIND.
       LUP-ACT-PLZO.
           ADD 1 TO WSS-SIND.
           IF WSS-SIND > WSS-MDLC
               GO TO FIN-ACT-PLZO.
           IF WSS-IDLC(WSS-SIND) NOT > ZEROES
               GO TO LUP-ACT-PLZO.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE WSS-IDLC(WSS-SIND)  TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLC    NOGKU'      TO PLP-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
               GO TO FIN-ACT-PLZO.
           IF WSS-PZOO > SPACES
               MOVE WSS-PZOO TO DLC-COD-PLZO IN DLC
               GO TO MOD-ACT-PLZO.
      *DAD ini  21-SEP-1992
      *     DIVIDE WSS-SUMA(WSS-SIND) BY DLC-VAL-NDLC IN DLC GIVING
           DIVIDE WSS-SUMA(WSS-SIND) BY WSS-VDLC(WSS-SIND) GIVING
      *DAD fin  21-SEP-1992
                  WSS-FTOR ROUNDED.
           IF WSS-FTOR < 30
               MOVE 'C1' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 90
               MOVE 'C2' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 180
               MOVE 'C3' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 366
               MOVE 'C4' TO PLP-PLZO
           ELSE
      *DAD ini  11-MAY-1992   (Memo.16-04-92)
           IF WSS-FTOR < 1096
               MOVE 'M1' TO PLP-PLZO
           ELSE
           IF WSS-FTOR < 3651
               MOVE 'M2' TO PLP-PLZO
           ELSE
               MOVE 'L0' TO PLP-PLZO.
      *DAD fin  11-MAY-1992
           MOVE 'PZO'    TO TAB-CIC-TTAB IN TAB.
           MOVE PLP-PLZO TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TABPLZOCICNX' TO PLP-CMSG
               MOVE PLP-PLZO       TO PLP-MENS
               GO TO FIN-ACT-PLZO.
           IF TAB-MSC-STAT IN TAB = 'N'
               MOVE 'TABPLZOCICNV' TO PLP-CMSG
               MOVE PLP-PLZO       TO PLP-MENS
               GO TO FIN-ACT-PLZO.
           MOVE TAB-COD-CTAB IN TAB TO DLC-COD-PLZO IN DLC.
       MOD-ACT-PLZO.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLC    NOMOD'      TO PLP-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO PLP-MENS
               GO TO FIN-ACT-PLZO.
           GO TO LUP-ACT-PLZO.
       FIN-ACT-PLZO.
           EXIT.

       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBFMSC.
       COPY GNSBGMSG.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGDTC.
       COPY GNSBIABT.
       COPY GNSBGFRM.
       COPY GNSBGSYS.
      *SMC-INI 09.09.1993
       COPY COLBGINT.
      *SMC-FIN 09.09.1993
       COPY GNSBGPES.

       COPY COLBFRCV.
       COPY COLBFVEN.
       COPY COLBFOPC.
       COPY COLBFICG.
       COPY COLBFDLC.
       COPY COLBFEVC.
       COPY COLBFTOC.
       COPY COLBFROV.
       COPY COLBGVEN.
      *DAD ini  06-AUG-1992   (Memo.22-07-92 Pto.4)
       COPY COLBFDCC.
      *DAD fin  06-AUG-1992

       COPY TABBFCAM.
