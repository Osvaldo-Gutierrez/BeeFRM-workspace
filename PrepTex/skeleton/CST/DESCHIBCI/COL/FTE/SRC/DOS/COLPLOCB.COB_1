       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLOCB.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    26-Jan-94 08:38 PM.

      * Programa Listador de Reporte OCB con Sort.


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTOCB         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTOCB         ASSIGN TO        UT-S-RPTOCB.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTOCB         ASSIGN TO        UT-S-RPTOCB.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRROCB.
       FD  RPTOCB LABEL RECORDS OMITTED
           REPORT IS RPT-OCB
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CCB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRCCB.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWOCB.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY GNSBRMSC.
       COPY TABBRUSR.
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRVEN.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY COLBREVC.
       COPY COLBRICG.
       COPY COLBRTOC.
       COPY COLBRRCV.
       COPY COLWGVEN.
       COPY GNSWGPES.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       COPY GNSWVPPH.
       01  WSS-VARI.
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-DTER                     PIC 9(02).
              05 WSS-NUM-MTER                     PIC 9(02).
              05 WSS-NUM-STER                     PIC 9(02).
              05 WSS-NUM-ATER                     PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                     PIC 9(02).
              05 WSS-NUM-AFEC                     PIC 9(02).
              05 WSS-NUM-MFEC                     PIC 9(02).
              05 WSS-NUM-DFEC                     PIC 9(02).
           03 WSS-FEC-FFET.
              05 WSS-NUM-SFET                     PIC 9(02).
              05 WSS-NUM-AFET                     PIC 9(02).
              05 WSS-NUM-MFET                     PIC 9(02).
              05 WSS-NUM-DFET                     PIC 9(02).
           03 WSS-FEC-FREG.
              05 WSS-NUM-SREG                     PIC 9(02).
              05 WSS-NUM-AREG                     PIC 9(02).
              05 WSS-NUM-MREG                     PIC 9(02).
              05 WSS-NUM-DREG                     PIC 9(02).
           03 WSS-COD-OFOC                        PIC X(03).
           03 WSS-COD-RMOC                        PIC X(12).
           03 WSS-IND-SCBZ                        PIC X(05).
           03 WSS-COD-PESO                        PIC X(06).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-IND-MNAC                        PIC 9(01).
           03 WSS-VAL-MNAC      VALUE ZEROES      PIC 9(11).
           03 WSS-VAL-SDLC      VALUE ZEROES      PIC 9(11)V9(04).
           03 WSS-COD-COOC.
              05 WSS-COD-TOOC                     PIC X(03).
              05 WSS-COD-AOOC                     PIC X(03).
           03 WSS-GLS-TIAU.
              05 WSS-GLS-TIO                      PIC X(05).
              05 WSS-GLS-AUX                      PIC X(05).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY COLRFOCB.
           .
       COPY COLRTOCB.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-OCB-OPC-COD-OFOC SECTION.
           USE BEFORE REPORTING CH-OCB-OPC-COD-OFOC.
       INI-CH-OCB-OPC-COD-OFOC.
      *<<<< CH_OCB_OPC_COD_OFOC
           MOVE ZEROES TO PAGE-COUNTER.
      *>>>>
       FIN-CH-OCB-OPC-COD-OFOC.
           EXIT.
       DEC-CH-OCB-OPC-COD-RMOC SECTION.
           USE BEFORE REPORTING CH-OCB-OPC-COD-RMOC.
       INI-CH-OCB-OPC-COD-RMOC.
      *<<<< CH_OCB_OPC_COD_RMOC
      *>>>>
       FIN-CH-OCB-OPC-COD-RMOC.
           EXIT.
       DEC-CH-OCB-CCB-FEC-FCCB SECTION.
           USE BEFORE REPORTING CH-OCB-CCB-FEC-FCCB.
       INI-CH-OCB-CCB-FEC-FCCB.
      *<<<< CH_OCB_CCB_FEC_FCCB
      *>>>>
       FIN-CH-OCB-CCB-FEC-FCCB.
           EXIT.
       DEC-CH-OCB-CCB-IND-SCBZ SECTION.
           USE BEFORE REPORTING CH-OCB-CCB-IND-SCBZ.
       INI-CH-OCB-CCB-IND-SCBZ.
      *<<<< CH_OCB_CCB_IND_SCBZ
      *>>>>
       FIN-CH-OCB-CCB-IND-SCBZ.
           EXIT.
       DEC-CH-OCB-OPC-MSC-TVOC SECTION.
           USE BEFORE REPORTING CH-OCB-OPC-MSC-TVOC.
       INI-CH-OCB-OPC-MSC-TVOC.
      *<<<< CH_OCB_OPC_MSC_TVOC
      *>>>>
       FIN-CH-OCB-OPC-MSC-TVOC.
           EXIT.
       DEC-CF-OCB-OPC-MSC-TVOC SECTION.
           USE BEFORE REPORTING CF-OCB-OPC-MSC-TVOC.
       INI-CF-OCB-OPC-MSC-TVOC.
      *<<<< CF_OCB_OPC_MSC_TVOC
      *>>>>
       FIN-CF-OCB-OPC-MSC-TVOC.
           EXIT.
       DEC-PH-OCB SECTION.
           USE BEFORE REPORTING PH-OCB.
       INI-PH-OCB.
      *<<<< PH_OCB
      * Omite impresion de la primera pagina al controlar cortes con CH
       COPY GNSBGPPH.

      * Mediante BUS-OFI se Obtiene Nombre de la Oficina de Tabla OFI
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.

      * Mediante BUS-USR se Obtiene Nombre del Ejecutivo de Tabla USR
           IF OPC-COD-RMOC IN SRT > SPACES
               MOVE OPC-COD-RMOC IN SRT TO USR-COD-USER IN USR
               PERFORM BUS-USR.

      *BUS-MSC busca glosa de miscelaneo en tabla
           IF CCB-IND-SCBZ IN SRT > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'SCBZ'              TO MSC-COD-TMSC IN MSC
               MOVE CCB-IND-SCBZ IN SRT TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DCOR IN MSC TO WSS-GLS-SCBZ IN WSS-OCB
           ELSE
               MOVE 'VIGENTE' TO WSS-GLS-SCBZ IN WSS-OCB.
      *>>>>
       FIN-PH-OCB.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLOCB' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           SORT GNSSRT
       COPY COLRSOCB.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMAAAA ) : '
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFOC IN WSS-VARI.
           DISPLAY WSS-COD-OFOC IN WSS-VARI.
      *Captura Ejecutivo Comercial
           DISPLAY 'EJECUTIVO : '.
           ACCEPT  WSS-COD-RMOC IN WSS-VARI.
           DISPLAY WSS-COD-RMOC IN WSS-VARI.
      *Captura Indicador Situacion Cobranza Vencimientos
           DISPLAY 'SITUACION COBRANZA VENCIMIENTOS : '.
           ACCEPT  WSS-IND-SCBZ IN WSS-VARI.
           DISPLAY WSS-IND-SCBZ IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB'         TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-END.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-OCB
                                            WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-OCB
                                            WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-OCB
                                            WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-OCB
                                            WSS-NUM-AFEC IN WSS-VARI.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DTER IN WSS-OCB
                                            WSS-NUM-DFET IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MTER IN WSS-OCB
                                            WSS-NUM-MFET IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-STER IN WSS-OCB
                                            WSS-NUM-SFET IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-ATER IN WSS-OCB
                                            WSS-NUM-AFET IN WSS-VARI.

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFOC IN WSS-VARI
               MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

      *VAL-USR Valida Usuario
           IF WSS-COD-RMOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-RMOC IN WSS-VARI.
           IF WSS-COD-RMOC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-RMOC IN WSS-VARI
               MOVE WSS-COD-RMOC IN WSS-VARI TO USR-COD-USER IN USR
               PERFORM VAL-USR
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

      *VAL-MSC Valida indicador ( miscelaneo ) en TABLAS
           IF WSS-IND-SCBZ IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-IND-SCBZ IN WSS-VARI.
           IF WSS-IND-SCBZ IN WSS-VARI NOT = '*'
               DISPLAY WSS-IND-SCBZ IN WSS-VARI
               MOVE 'COL'                    TO MSC-COD-SIST
               MOVE 'SCBZ'                   TO MSC-COD-TMSC IN MSC
               MOVE WSS-IND-SCBZ IN WSS-VARI TO MSC-COD-CMSC IN MSC
               PERFORM VAL-MSC
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Obtiene el Codigo Valor de Cambio de Pesos mediante el CIC de la
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-END.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CCB.
       FST-INP-SRT.
      *<<<< INI_FST_INP
      * Inicio de Ciclo Proceso para Registro CCB
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CCB-FEC-FCCB IN CCB.
           MOVE 'CCB-FEC-FCCB' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CCB.
           IF NOT FIO-STAT-OKS OR
              CCB-FEC-FCCB IN CCB > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS EN RANGO DE FECHAS DESDE : '
                       WSS-FEC-FINI IN WSS-VARI ' A '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-INP-SRT.
           GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-CCB.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro CCB
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.

      * Busca el Documento Legal Credito del Cambio Situacion Cobranza
           MOVE CCB-KEY-IDLC IN CCB TO DLC-KEY-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CCB    DLCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CCB-KEY-IDLC IN CCB
               GO TO NXT-INP-SRT.

      * POR AHORA NO SE MANEJA Vencimiento del Cambio Situacion Cobranza
      *
      * Busca el Vencimiento del Cambio Situacion Cobranza
      *JSS  26-JAN-1994 20:37:08 MOVE CCB-KEY-IVCT IN CCB TO VEN-KEY-IVE
.
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN.
      *
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'CCB    VENNX' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC  CCB-KEY-IVCT IN CCB
                   MOVE SPACES TO VEN
      *             GO TO NXT-INP-SRT
               ELSE
                   NEXT SENTENCE
           ELSE
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS
                   MOVE 'COL'    TO MSG-COD-SIST
                   MOVE VEN-STAT TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC  CCB-KEY-IVCT IN CCB
                   MOVE SPACES TO VEN.
      *             GO TO NXT-INP-SRT.

      * Completa formacion y generacion del registro SRT
           PERFORM BUS-SRT-REG.

           GO TO NXT-INP-SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMOCB.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
           MOVE 'CCB-FEC-FCCB' TO FIO-AKEY.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CCB.
      *<<<< FIN_NXT_INP
           IF CCB-FEC-FCCB IN CCB > WSS-FEC-FFET IN WSS-VARI
               GO TO FIN-INP-SRT.
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-CCB.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTOCB.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-OCB.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
      * Busca glosas de campos en Lineas de Titulos pues con CH no estan
      * la primera pagina
      * Mediante BUS-OFI se Obtiene Nombre de la Oficina de Tabla OFI
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.

      * Mediante BUS-USR se Obtiene Nombre del Ejecutivo de Tabla USR
           IF OPC-COD-RMOC IN SRT > SPACES
               MOVE OPC-COD-RMOC IN SRT TO USR-COD-USER IN USR
               PERFORM BUS-USR.

      *BUS-MSC busca glosa de miscelaneo en tabla
           IF CCB-IND-SCBZ IN SRT > SPACES
               MOVE 'COL'               TO MSC-COD-SIST
               MOVE 'SCBZ'              TO MSC-COD-TMSC IN MSC
               MOVE CCB-IND-SCBZ IN SRT TO MSC-COD-CMSC IN MSC
               PERFORM BUS-MSC
               MOVE MSC-GLS-DCOR IN MSC TO WSS-GLS-SCBZ IN WSS-OCB
           ELSE
               MOVE 'VIGENTE' TO WSS-GLS-SCBZ IN WSS-OCB.
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
           PERFORM GEN-LIN-OUT.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-OCB.
      *<<<< FIN_GEN_OUT
      * Fecha Cambio Situacion Cobranza
           MOVE CCB-FEC-FCCB IN SRT      TO WSS-FEC-FREG IN WSS-VARI.
           MOVE WSS-NUM-DREG IN WSS-VARI TO WSS-NUM-DCCB IN WSS-OCB.
           MOVE WSS-NUM-MREG IN WSS-VARI TO WSS-NUM-MCCB IN WSS-OCB.
           MOVE WSS-NUM-SREG IN WSS-VARI TO WSS-NUM-SCCB IN WSS-OCB.
           MOVE WSS-NUM-AREG IN WSS-VARI TO WSS-NUM-ACCB IN WSS-OCB.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-OCB.
           CLOSE RPTOCB.
       EXT-OUT-SRT.
           EXIT.
       COPY COLBFCCB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      ******************************************************************
      * Seleccion del registro para Proceso mediante Oficina, Ejecutivo
      * Situacion Cobranza
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

           IF WSS-IND-SCBZ IN WSS-VARI NOT = '*'
               IF WSS-IND-SCBZ IN WSS-VARI NOT = CCB-IND-SCBZ IN CCB
                   GO TO FIN-SEL-REG-PRO.

      * Busca la Operacion de Credito del Cambio Situacion Cobranza
           MOVE CCB-CIC-IOPC IN CCB TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CCB    OPCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CCB-CIC-IOPC IN CCB
               GO TO FIN-SEL-REG-PRO.

      * Proceso de Seleccion a partir del registro OPC
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFOC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   GO TO FIN-SEL-REG-PRO.

           IF WSS-COD-RMOC IN WSS-VARI NOT = '*'
               IF WSS-COD-RMOC IN WSS-VARI NOT = OPC-COD-RMOC IN OPC
                   GO TO FIN-SEL-REG-PRO.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      ******************************************************************
      * Completa formacion y generacion del registro SRT
       BUS-SRT-REG SECTION.
       INI-BUS-SRT-REG.
      * Obtiene Valor de Cambio en Pesos para Moneda Nacional
           MOVE  0  TO WSS-IND-MNAC IN WSS-VARI.
           IF OPC-MSC-TVOC IN OPC = '0' AND
              OPC-COD-VCOC IN OPC NOT = WSS-COD-PESO IN WSS-VARI
               PERFORM DET-VAL-PESO
               IF WSS-IND-MNAC IN WSS-VARI = 0
                   GO TO FIN-BUS-SRT-REG.

      * Para Moneda Nacional distinta de Pesos se transforma valor a pes
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MULTIPLY DLC-VAL-SDLC IN DLC BY CAM-SGV-VCAM IN CAM
                              GIVING WSS-VAL-MNAC IN WSS-VARI ROUNDED
               MOVE WSS-VAL-MNAC IN WSS-VARI TO WSS-VAL-SDLC IN WSS-VARI
           ELSE
               MOVE DLC-VAL-SDLC IN DLC TO WSS-VAL-SDLC IN WSS-VARI.

      * Llena monto del campo Valor Traspasado del reporte
           MOVE WSS-VAL-SDLC IN WSS-VARI TO SRT-VAL-VTRA IN SRT.

      *
      * Por ahora los siguientes campos se Inicializan
      *
      * Para Traspasos de 'xxx' a 'yyy' el Indicador de Aceleracion se l
      * con la informacion del Indicador en CCB
           MOVE SPACES TO SRT-IND-ACEL IN SRT.
      * Segun Indicador Aceleracion se calcula el Numero de Cuotas Trasp
      * para el reporte
           MOVE ZEROES TO SRT-NUM-CUOT IN SRT.
      * Segun Indicador Aceleracion se calcula Valor Traspasado para el
      *

      * Completa Llenado de Campos y Genera el Registro SRT
           COPY COLRMOCB.
           RELEASE SRT.
       FIN-BUS-SRT-REG.
           EXIT.
      ******************************************************************
      * Obtiene Valor de Cambio en Pesos para Moneda Nacional a Fecha Tr
        DET-VAL-PESO SECTION.
        INI-DET-VAL-PESO.
           MOVE CCB-FEC-FCCB IN CCB      TO CAM-FEC-FCAM IN CAM(1).
           MOVE CCB-FEC-FCCB IN CCB      TO CAM-FEC-FCAM IN CAM(2).
           MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A FECHA TRASPASO : '
                       CCB-FEC-FCCB IN CCB ' PARA ' OPC-COD-VCOC IN OPC
                       ' EN OPERACION ' OPC-CIC-IOPC IN OPC
               GO TO FIN-DET-VAL-PESO
           ELSE
               IF CAM-IND-VIGE NOT = 'S'
                  DISPLAY 'IND. VIGENCIA NO VALIDO A FECHA TRASPASO : '
                       CCB-FEC-FCCB IN CCB ' PARA ' OPC-COD-VCOC IN OPC
                          ' EN OPERACION ' OPC-CIC-IOPC IN OPC
                  GO TO FIN-DET-VAL-PESO
               ELSE
                   MOVE  1  TO WSS-IND-MNAC IN WSS-VARI.
        FIN-DET-VAL-PESO.
           EXIT.
      ******************************************************************
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
       GEN-LIN-OUT SECTION.
       INI-GEN-LIN-OUT.
      * Se redefine campo COOC e inicializa glosa de trabajo
           MOVE OPC-COD-COOC IN SRT TO WSS-COD-COOC IN WSS-VARI.
           MOVE SPACES              TO WSS-GLS-TIAU IN WSS-VARI.

      * Mediante BUS-TAB busca la glosa del Tipo de Operacion Credito en
      * tabla TIO
           IF WSS-COD-TOOC IN WSS-VARI > SPACES
               MOVE 'TAB'                    TO TAB-COD-SIST
               MOVE 'TIO'                    TO TAB-COD-TTAB IN TAB
               MOVE WSS-COD-TOOC IN WSS-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-TIO IN WSS-VARI.

      * Mediante BUS-TAB busca la glosa de la Clase de Operacion Credito
      * tabla AUX
           IF OPC-COD-COOC IN SRT > SPACES
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-COOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-AUX IN WSS-VARI.

      * Se concatenan las glosas de tablas TIO y AUX para desplegarlas j
           MOVE WSS-GLS-TIAU IN WSS-VARI TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO WSS-GLS-TIAU IN WSS-OCB.

      * Mediante BUS-TAB Busca Tipo de Moneda de Tabla VLR
           IF OPC-COD-VCOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-VCOC IN WSS-OCB
           ELSE
               MOVE SPACES TO WSS-GLS-VCOC IN WSS-OCB.

      * Para Situacion Cobranza en blanco mover VIGente
           IF CCB-IND-SCBA IN SRT = ' '
               MOVE 'VIG' TO CCB-IND-SCBA IN SRT.

      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro
           PERFORM BUS-REG-CLI.
       FIN-GEN-LIN-OUT.
           EXIT.
      ******************************************************************
      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro
       BUS-REG-CLI SECTION.
       INI-BUS-REG-CLI.
      * Busca CIC del Cliente con el CIC de Operaciones de Creditos en R
      * RDC-NUM-IDLC > 0 ====> DEU.IND.
      * RDC-NUM-IDLC = 0 ====> DEU.DIR.
           MOVE CCB-CAI-IOPC IN SRT TO RDC-CAI-IOPC IN RDC.
           MOVE CCB-IIC-IOPC IN SRT TO RDC-IIC-IOPC IN RDC.
      *    MOVE                     TO RDC-NUM-IDLC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC'      TO FIO-AKEY.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    RDCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RDC-KEY-IDLC IN RDC.
      *        GO TO FIN-BUS-REG-CLI.

      *CIC-CLI Busca Datos del Cliente Mediante su CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS > SPACES
               MOVE SPACES TO DBC-NUM-ICLI IN DBC
                              DBC-VRF-ICLI IN DBC
                              DBC-IND-ICLI IN DBC
                              WSS-GLS-AUXI IN WSS-OCB
                              DBC-GLS-ICLI IN DBC
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC
               IF DBC-GLS-ICLI IN DBC > SPACES
                   MOVE '-'    TO WSS-GLS-AUXI IN WSS-OCB
               ELSE
                   MOVE SPACES TO WSS-GLS-AUXI IN WSS-OCB.
       FIN-BUS-REG-CLI.
           EXIT.
      ******************************************************************

       COPY GNSBFMSC.
       COPY TABBFUSR.
       COPY TABBFOFI.
       COPY TABBFCAM.
       COPY COLBFOPC.
       COPY COLBFDLC.
       COPY COLBFVEN.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY COLBFEVC.
       COPY COLBFICG.
       COPY COLBFTOC.
       COPY COLBFRCV.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY TABBBUSR.
       COPY TABBBOFI.
       COPY GNSBVMSC.
       COPY TABBVUSR.
       COPY TABBVOFI.
       COPY COLBGVEN.
       COPY GNSBGPES.
      *SMC-INI 09.09.1993
       COPY COLBGINT.
      *SMC-FIN 09.09.1993
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY SGCBGCLI.
      *>>>>
