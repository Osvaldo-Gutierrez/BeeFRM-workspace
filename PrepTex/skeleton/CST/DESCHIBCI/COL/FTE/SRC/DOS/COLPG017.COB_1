       IDENTIFICATION  DIVISION.
       PROGRAM-ID.     COLPG017.
      *AUTHOR.         BEE-TEEAM ( BASE PLCPA017 ).
      *COMPILED.       28.10.1999.
      *----------------------------------------------------------------*
      *  OBJETIVO : A PARTIR DE LA INFORMACION SELECCIONADA DEL VSAM   *
      *            KOLOK.DOCUM.PAGOS.BIC, SE GENERAN REGISTROS PARA    *
      *            ARCHIVO SAM DE DOCUMENTOS   PAGOS A FIN DE ENVIARLOS*
      *            ACLARAR AL BOLETIN COMERCIAL.                       *
      *----------------------------------------------------------------*
      *  ENTRADA  : - COLVBIC (KOLSK.DIARIO.DOCUM.PAGOS)               *
      *  SALIDA   : - COLVPDI (KOLSK.ENVIO.DOCUM.PAGOS)                *
      *                                                                *
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *------------
           SELECT  COLVBIC  ASSIGN  TO  S-COLVBIC
                            FILE  STATUS  IS  FS-COLVBIC.

           SELECT  COLVPDI  ASSIGN  TO  S-COLVPDI
                            FILE  STATUS  IS  FS-COLVPDI.
       DATA DIVISION.
       FILE SECTION.
      *-------------
       FD  COLVBIC            LABEL  RECORD STANDARD
                              RECORD CONTAINS 300 CHARACTERS
                              BLOCK  CONTAINS 0 RECORDS.
      *   REGISTRO PRINCIPAL RP-DI (Aclaracion Documento Pagados)
      *   -------------------------------------------------------
      *    Nombre Registro : ADP
      *    Clave(s)        : ADP-KEY-01.
      *                      ADP-KEY-02.
      *
      *    Largo           : 265
      *    Bloqueo         : 2
      *    Observaciones   :
      * ---------------------------------------------------------
      *
       01 ADP.
          03 ADP-KEY-01.
      *      Codigo de Oficina / Ejecutivo / Correlativo.
             05 ADP-COD-OFOC                    PIC 9(10).
             05 ADP-COD-EJEC                    PIC X(12).
             05 ADP-NUM-CORR                    PIC 9(05).
      *
      *   RUT del Deudor (Numero - digito Verificador).
          03 ADP-COD-IRUT.
             05 ADP-NUM-RUTD                    PIC 9(09).
             05 ADP-VRF-RUTD                    PIC X(01).
      *
      *   Calidad Juridica.
          03 ADP-IND-CJUR                       PIC X(01).
      *
      *   NOMbre del Deudor(Paterno, Materno, Nombres )
          03 ADP-GLS-NOMB.
             05 ADP-GLS-APAT                    PIC X(25).
             05 ADP-GLS-AMAT                    PIC X(25).
             05 ADP-GLS-NBRE                    PIC X(20).

      *
      *   Glosa  de Oficina /Sucursal.
          03 ADP-GLS-OFOC                       PIC X(25).
      *
      *   Localidad- Codigo / Glosa
          03 ADP-COD-LOCA                       PIC 9(06).
          03 ADP-GLS-LOCA                       PIC X(25).
      *
      *   Codigo - Glosa de Moneda.
          03 ADP-COD-MONE                       PIC X(06).
          03 ADP-GLS-MONE                       PIC X(05).
      *
      *   Monto Cuota .( Saldo Valor Final Vcmto )
          03 ADP-VAL-SFIN                       PIC 9(11)V9(04).
      *
      *   Monto Saldo Credito .
          03 ADP-VAL-SCRE                       PIC 9(11)V9(04).
      *
      *   Numero de Cuotas .
          03 ADP-NUM-VCOR                       PIC 9(03).

          03 ADP-KEY-02.
      *      Num. Identificacion Vcmto. / Num. Operacion.
             05 ADP-NUM-IVCT                    PIC 9(03).
             05 ADP-CIC-IOPC                    PIC X(12).
      *
      *   Codigo de Causal.
          03 ADP-COD-CAUS                       PIC X(02).
      *
      *   Indicador de envio boletin.
          03 ADP-IND-EBOL                       PIC X(01).
      *
      *   Fecha de vencimiento.
          03 ADP-FEC-FVEN.
             05 ADP-NUM-SVEN                    PIC X(02).
             05 ADP-NUM-AVEN                    PIC X(02).
             05 ADP-NUM-MVEN                    PIC X(02).
             05 ADP-NUM-DVEN                    PIC X(02).
      *
      *   Telefono y anexo de la direccin.
          03 ADP-GLS-FONO.
             05 ADP-NUM-FONO                    PIC 9(08).
             05 ADP-NUM-ANXO                    PIC 9(04).
      *
      *   Num. Identificacion Vencimiento
          03 ADP-NUM-IVEN                       PIC 9(03).

      * Indicador de cancelacion.
          03 ADP-IND-CANC                       PIC X(01).
      *
      * Fecha de Cancelacion Real.
          03 ADP-FEC-FCAN.
             05 ADP-NUM-SCAN                    PIC X(02).
             05 ADP-NUM-ACAN                    PIC X(02).
             05 ADP-NUM-MCAN                    PIC X(02).
             05 ADP-NUM-DCAN                    PIC X(02).
      *
      * Fecha de Cancelacion computacional.
          03 ADP-FEC-FOUT.
             05 ADP-NUM-SOUT                    PIC X(02).
             05 ADP-NUM-AOUT                    PIC X(02).
             05 ADP-NUM-MOUT                    PIC X(02).
             05 ADP-NUM-DOUT                    PIC X(02).
      *
      * Operacion cancelada.
          03 ADP-CIC-ICAN                       PIC X(12).
      *
      * FILLER.
          03 ADP-COD-ERRO                       PIC X(01).
          03 ADP-GLS-DISP                       PIC X(21).

       FD  COLVPDI            LABEL  RECORD   STANDARD
                              RECORD CONTAINS 201 CHARACTERS
                              BLOCK  CONTAINS  0 RECORDS.
       01  REG-COLVPDI        PIC X(201).

       WORKING-STORAGE SECTION.
      *------------------------
       01  VARIABLES-STATUS-88.
           05 FS-COLVBIC                 PIC 9(02)     VALUE 0.
           88 COLVBIC-88                               VALUE 00 , 97.
           05 FS-COLVPDI                 PIC 9(02)     VALUE 0.
           88 COLVPDI-88                               VALUE 00 , 97.

       01  VARIABLES-NIVEL-88.
           05 EOF-BIC                    PIC 9(01)     VALUE 0.
           88 FIN-ARCHIVO-BIC                          VALUE 1.
           05 EOF-PDI                    PIC 9(01)     VALUE 0.
           88 FIN-ARCHIVO-PDI                          VALUE 1.

       01  VARIABLES-CONTADORES.
           05 WK-CORR                    PIC 9(05)     VALUE 0.
           05 WK-CONT-1                  PIC 9(06)     VALUE 0.
           05 WK-CONT-2                  PIC 9(06)     VALUE 0.
           05 WS-COD-OFI                 PIC X(10)     VALUE SPACES.
           05 WS-GLS-OFI                 PIC X(25)     VALUE SPACES.
           05 WS-COD-REGION              PIC 9(02)     VALUE 0.
           05 WS-COD-LOCALIDAD           PIC 9(04)     VALUE 0.
           05 WS-GLS-LOCALIDAD           PIC X(25)     VALUE SPACES.
           05 WS-FLAG                    PIC 9(01)     VALUE 0.
      *ALD-BCI-INI
           05 WK-RUT                     PIC 9(14)     VALUE 0.
           05 WK-SFIN                    PIC 9(14)V9(02) VALUE 0.
           05 WK-IDENTF                  PIC 9(18)     VALUE 0.

           05 WK-VSSAA                   PIC 9(09)     VALUE 0.
           05 WK-VMM                     PIC 9(07)     VALUE 0.
           05 WK-VDD                     PIC 9(07)     VALUE 0.

           05 WK-PSSAA                   PIC 9(09)     VALUE 0.
           05 WK-PMM                     PIC 9(07)     VALUE 0.
           05 WK-PDD                     PIC 9(07)     VALUE 0.

      *ALD-BCI-FIN



       01  WS-VARIABLES-FECHA.
           05 WS-FEC-PROC.
              10  WS-DD-PROC             PIC 9(02).
              10  WS-MM-PROC             PIC 9(02).
              10  WS-SS-PROC             PIC 9(02).
              10  WS-AA-PROC             PIC 9(02).
           05 WS-PAR-FECHA               PIC X(06).
           05 FILLER        REDEFINES    WS-PAR-FECHA.
              10 WS-AA                   PIC 9(02).
              10 WS-MM                   PIC 9(02).
              10 WS-DD                   PIC 9(02).
           05 WS-PAR-FECHA-8             PIC X(08).
           05 FILLER        REDEFINES    WS-PAR-FECHA-8.
              10 WS-SS-8                 PIC 9(02).
              10 WS-AA-8                 PIC 9(02).
              10 WS-MM-8                 PIC 9(02).
              10 WS-DD-8                 PIC 9(02).
           05 WK-FECHA-DESDE-VEN         PIC X(08).
           05 FILLER        REDEFINES    WK-FECHA-DESDE-VEN.
              10 FDESDE-VEN-SS           PIC 9(02).
              10 FDESDE-VEN-AA           PIC 9(02).
              10 FDESDE-VEN-MM           PIC 9(02).
              10 FDESDE-VEN-DD           PIC 9(02).
           05 WK-FECHA-HASTA-VEN         PIC X(08).
           05 FILLER        REDEFINES    WK-FECHA-HASTA-VEN.
              10 FHASTA-VEN-SS           PIC 9(02).
              10 FHASTA-VEN-AA           PIC 9(02).
              10 FHASTA-VEN-MM           PIC 9(02).
              10 FHASTA-VEN-DD           PIC 9(02).
      *ALD-BCI-INI
           05 WK-FEC-FAUX.
               07 WK-NUM-SSAA            PIC 9(04).
               07 FILLER    REDEFINES WK-NUM-SSAA.
                   10  WK-NUM-SS         PIC 9(02).
                   10  WK-NUM-AA         PIC 9(02).
               07 WK-NUM-MM              PIC 9(02).
               07 WK-NUM-DD              PIC 9(02).
      *ALD-BCI-FIN

       01  VAR-INDI-OTRAS.
           10 K                          PIC 9(02)   VALUE 0.
           10 IDENT-AUX                  PIC X(12)   VALUE SPACES.

      *ALD-BCI-INI
           10 WK-CIC-IOPC.
              20 WK-TOPC                 PIC X(01).
              20 WK-IOPC                 PIC 9(11).
      *ALD-BCI-FIN

       01  VAR-MONEDAS.
           05 WK-COD-MONEDAS.
              10 FILLER                  PIC X(05) VALUE '99901'.
              10 FILLER                  PIC X(05) VALUE '99802'.
              10 FILLER                  PIC X(05) VALUE '99504'.
              10 FILLER                  PIC X(05) VALUE '99705'.
              10 FILLER                  PIC X(05) VALUE '01312'.
              10 FILLER                  PIC X(05) VALUE '05813'.
              10 FILLER                  PIC X(05) VALUE '03014'.
              10 FILLER                  PIC X(05) VALUE '07115'.
              10 FILLER                  PIC X(05) VALUE '10216'.
              10 FILLER                  PIC X(05) VALUE '05318'.
              10 FILLER                  PIC X(05) VALUE '08219'.
              10 FILLER                  PIC X(05) VALUE '03720'.
              10 FILLER                  PIC X(05) VALUE '07221'.
              10 FILLER                  PIC X(05) VALUE '06422'.
              10 FILLER                  PIC X(05) VALUE '05123'.
              10 FILLER                  PIC X(05) VALUE '11324'.
              10 FILLER                  PIC X(05) VALUE '00627'.
              10 FILLER                  PIC X(05) VALUE '11328'.
              10 FILLER                  PIC X(05) VALUE '00000'.
           05 FILLER           REDEFINES       WK-COD-MONEDAS.
              10  WK-COD-MON      OCCURS       19 TIMES.
                  20 WK-CODBANC                   PIC 9(03).
                  20 WK-CODSIB                    PIC 9(02).
          05 WK-MONEDA                   PIC X(06).
          05 FILLER       REDEFINES     WK-MONEDA.
             10 FILLER                   PIC X(01).
             10 WK-CODMON                PIC 9(03).
             10 FILLER                   PIC X(02).
          05 WK-AUX-LOCALIDAD.
             10 WK-AUX-COD-LOCALIDAD     PIC 9(04).
             10 WK-AUX-COD-REGION        PIC 9(02).

      *----------------------------------------------------------------*
      *        REGISTRO CABECERA        LARGO REG 201                  *
      *----------------------------------------------------------------*
       01 REG-CABEZA.
          10 RCAB-TIPO-REG               PIC X(02).
          10 RCAB-TIPO-DOC               PIC X(02).
          10 RCAB-COD-INST               PIC 9(03).
          10 RCAB-NOM-INST               PIC X(20).
          10 RCAB-CANT-RGS               PIC 9(05).
          10 RCAB-FECHA-DESDE            PIC 9(08).
          10 FILLER       REDEFINES    RCAB-FECHA-DESDE.
             20 RCAB-FEC-DESDE-S         PIC 9(02).
             20 RCAB-FEC-DESDE-A         PIC 9(02).
             20 RCAB-FEC-DESDE-M         PIC 9(02).
             20 RCAB-FEC-DESDE-D         PIC 9(02).
          10 RCAB-FECHA-HASTA            PIC 9(08).
          10 FILLER       REDEFINES    RCAB-FECHA-HASTA.
             20 RCAB-FEC-HASTA-S         PIC 9(02).
             20 RCAB-FEC-HASTA-A         PIC 9(02).
             20 RCAB-FEC-HASTA-M         PIC 9(02).
             20 RCAB-FEC-HASTA-D         PIC 9(02).
          10 RCAB-NUM-VOL                PIC 9(02).
          10 RCAB-FECHA-GENER            PIC 9(08).
          10 FILLER       REDEFINES    RCAB-FECHA-GENER.
             20 RCAB-FEC-GENER-SS        PIC 9(02).
             20 RCAB-FEC-GENER-AA        PIC 9(02).
             20 RCAB-FEC-GENER-MM        PIC 9(02).
             20 RCAB-FEC-GENER-DD        PIC 9(02).
      *ALD-BCI-INI
          10 RCAB-SUMA-RUT               PIC 9(14).
          10 RCAB-SUMA-SFIN              PIC 9(14)V9(02).
          10 RCAB-SUMA-IDENTF            PIC 9(18).
          10 RCAB-SUMA-FVCTO.
             20 RCAB-FEC-VCTO-SSAA       PIC 9(09).
             20 RCAB-FEC-VCTO-MM         PIC 9(07).
             20 RCAB-FEC-VCTO-DD         PIC 9(07).
          10 RCAB-SUMA-FPAGO.
             20 RCAB-FEC-PAGO-SSAA       PIC 9(09).
             20 RCAB-FEC-PAGO-MM         PIC 9(07).
             20 RCAB-FEC-PAGO-DD         PIC 9(07).
          10 FILLER                      PIC X(49) VALUE SPACES.
      *ALD-BCI-FIN
      *----------------------------------------------------------------*
      *            REGISTRO PRINCIPAL LARGO 201                        *
      *----------------------------------------------------------------*
       01 REG-CUERPO.
          10 RPPAL-TIPO-REG                  PIC X(02).
          10 RPPAL-TIPO-DOC                  PIC X(02).
          10 RPPAL-NUM-CORR                  PIC 9(05).
          10 RPPAL-RUT.
             20 RPPAL-NUM-RUT                PIC 9(09).
             20 RPPAL-DVR-RUT                PIC X(01).
          10 RPPAL-CALDAD-JRCA               PIC X(01).
          10 RPPAL-NOMBRE-SUPCTROR.
             20 RPPAL-APLL-PATNO             PIC X(25).
             20 RPPAL-APLL-MATNO             PIC X(25).
             20 RPPAL-NOMBRES                PIC X(20).
          10 RPPAL-COD-OFI                   PIC X(10).
          10 RPPAL-GLS-OFI                   PIC X(25).
          10 WK-LOCALIDAD.
             15 WK-COD-REGION                PIC 9(02).
             15 WK-COD-LOCALIDAD             PIC 9(04).
          10 WK-GLS-LOCALIDAD                PIC X(25).
          10 RPPAL-COD-MONEDA                PIC 9(02).
          10 RPPAL-MONTO                     PIC 9(10)V9(02).
          10 RPPAL-NUM-IDENTF                PIC 9(12).
          10 RPPAL-COD-CAUSAL                PIC X(02).
          10 RPPAL-FECHA-VEN                 PIC 9(08).
          10 FILLER           REDEFINES      RPPAL-FECHA-VEN.
             20 RPPAL-FEC-VEN-SS             PIC 9(02).
             20 RPPAL-FEC-VEN-AA             PIC 9(02).
             20 RPPAL-FEC-VEN-MM             PIC 9(02).
             20 RPPAL-FEC-VEN-DD             PIC 9(02).
      *ALD-BCI-INI
          10 RPPAL-FECHA-PAGO                PIC 9(08).
          10 FILLER           REDEFINES      RPPAL-FECHA-PAGO.
             20 RPPAL-FEC-PAG-SS             PIC 9(02).
             20 RPPAL-FEC-PAG-AA             PIC 9(02).
             20 RPPAL-FEC-PAG-MM             PIC 9(02).
             20 RPPAL-FEC-PAG-DD             PIC 9(02).
          10 RPPAL-TIP-REL                   PIC X(01).
      *ALD-BCI-FIN

       PROCEDURE DIVISION.
      *-------------------
       A-CONTROL SECTION.
      *-----------------
           PERFORM B-INICIO.

       AA-PROCESO.
      *-----------
           PERFORM C-PROCESAR.

       AB-TERMINO.
      *-----------
           PERFORM D-TERMINO.

       AZ-FIN-DE-PROCESO.
      *------------------
           GOBACK.

       B-INICIO SECTION.
      *-----------------
      *---------------------------------------------------------------*
      *                  APERTURA DE ACHIVOS                          *
      *---------------------------------------------------------------*
            OPEN INPUT  COLVBIC
            IF NOT COLVBIC-88
               DISPLAY '<< ERROR PROG.COLPG017 SE CANCELA AL ABRIR >>'
               DISPLAY '<< ARCHIVO COLVBIC FILE STATUS ====>>> '
               FS-COLVBIC
               MOVE 100 TO RETURN-CODE
               STOP RUN.

            OPEN OUTPUT COLVPDI
            IF NOT COLVPDI-88
               DISPLAY '<< ERROR PROG.COLPG017 SE CANCELA AL ABRIR >>'
               DISPLAY '<< ARCHIVO COLVPDI FILE STATUS ====>>> '
               FS-COLVPDI
               CLOSE COLVBIC
               MOVE 100 TO RETURN-CODE
               STOP RUN.
      *---------------------------------------------------------------*
      *                  ACCEPT FECHA DEL SISTEMA                     *
      *---------------------------------------------------------------*
            ACCEPT    WS-PAR-FECHA    FROM    DATE.
            MOVE WS-AA TO WS-AA-8.
            MOVE WS-MM TO WS-MM-8.
            MOVE WS-DD TO WS-DD-8.
      *
      ****MAD INI-2000****
           IF   WS-AA > 60
               MOVE 19  TO WS-SS-8
           ELSE
              MOVE 20   TO WS-SS-8
           END-IF.

      ****MAD FIN-2000****
      *
            INITIALIZE VAR-MONEDAS.

       B-FIN-INICIO. EXIT.
      *-------------

       C-PROCESAR SECTION.
      *------------------
           PERFORM C-10-GRABA-ENCABEZ-LEE.
           PERFORM C-20-PROCESO-GEN      UNTIL   FIN-ARCHIVO-BIC.
           GO TO FIN-PROCESAR.

       C-10-GRABA-ENCABEZ-LEE.
      *----------------------
           MOVE  'RC'        TO    RCAB-TIPO-REG.
           MOVE  'CM'        TO    RCAB-TIPO-DOC.
           MOVE   016        TO    RCAB-COD-INST.
           MOVE  'BCI'       TO    RCAB-NOM-INST.
           MOVE   ZEROES     TO    RCAB-CANT-RGS.
           MOVE   ZEROES     TO    RCAB-FEC-DESDE-A.
           MOVE   ZEROES     TO    RCAB-FEC-DESDE-M.
           MOVE   ZEROES     TO    RCAB-FEC-DESDE-D.
           MOVE   ZEROES     TO    RCAB-FEC-HASTA-A.
           MOVE   ZEROES     TO    RCAB-FEC-HASTA-M.
           MOVE   ZEROES     TO    RCAB-FEC-HASTA-D.
           MOVE   1          TO    RCAB-NUM-VOL.
      *MAD 22/09/98 CAMBIO POR FORMATO 2000 DE AGREGA SIGLO
           MOVE   WS-SS-8    TO    RCAB-FEC-GENER-SS.
           MOVE   WS-AA-8    TO    RCAB-FEC-GENER-AA.
           MOVE   WS-MM-8    TO    RCAB-FEC-GENER-MM.
           MOVE   WS-DD-8    TO    RCAB-FEC-GENER-DD.
           WRITE  REG-COLVPDI   FROM   REG-CABEZA.
           IF NOT COLVPDI-88
              DISPLAY '>>>> PROBLEMAS DE GRABACION      <<<<'
              DISPLAY '>>>> ARCHIVO DE SALIDA           <<<<'
              DISPLAY '>>>> REGISTRO CABECERA (COLVPDI) <<<<'
           DISPLAY '>>>> FILE STATUS           ======>>> ' FS-COLVPDI
              MOVE 100 TO RETURN-CODE
              STOP RUN.
           PERFORM C-30-LECTURA-BIC.
           MOVE  ADP-FEC-FOUT IN ADP   TO  WK-FECHA-DESDE-VEN
                                           WK-FECHA-HASTA-VEN.
       C-10-FIN. EXIT.
      *---------

       C-20-PROCESO-GEN.
      *-----------------
           ADD     1    TO    WK-CORR.
           ADD     1    TO    WK-CONT-2.
           MOVE   'RP'                   TO     RPPAL-TIPO-REG.
           MOVE   'CM'                   TO     RPPAL-TIPO-DOC.
           MOVE    WK-CORR               TO     RPPAL-NUM-CORR.
           MOVE    SPACES                TO     RPPAL-TIP-REL.
           MOVE    ADP-NUM-RUTD IN ADP   TO     RPPAL-NUM-RUT.

           ADD     ADP-NUM-RUTD IN ADP   TO     WK-RUT.

           MOVE    ADP-VRF-RUTD IN ADP   TO     RPPAL-DVR-RUT.
           MOVE    ADP-IND-CJUR IN ADP   TO     RPPAL-CALDAD-JRCA.
           MOVE    ADP-GLS-APAT IN ADP   TO     RPPAL-APLL-PATNO.
           MOVE    ADP-GLS-AMAT IN ADP   TO     RPPAL-APLL-MATNO.
           MOVE    ADP-GLS-NBRE IN ADP   TO     RPPAL-NOMBRES.
           IF ADP-COD-OFOC IN ADP = '0000000248'
              MOVE  WS-COD-OFI              TO RPPAL-COD-OFI
              MOVE  WS-GLS-OFI              TO RPPAL-GLS-OFI
              MOVE  WS-COD-REGION           TO WK-COD-REGION
              MOVE  WS-COD-LOCALIDAD        TO WK-COD-LOCALIDAD
              MOVE  WS-GLS-LOCALIDAD        TO WK-GLS-LOCALIDAD
           ELSE
              MOVE    ADP-COD-OFOC IN ADP   TO     RPPAL-COD-OFI
              MOVE    ADP-GLS-OFOC IN ADP   TO     RPPAL-GLS-OFI
              MOVE    ADP-COD-LOCA IN ADP   TO     WK-AUX-LOCALIDAD
              MOVE    WK-AUX-COD-REGION     TO     WK-COD-REGION
              MOVE    WK-AUX-COD-LOCALIDAD  TO     WK-COD-LOCALIDAD
              MOVE    ADP-GLS-LOCA IN ADP   TO     WK-GLS-LOCALIDAD
           END-IF.
           MOVE    ADP-COD-MONE IN ADP      TO     WK-MONEDA.
           IF  ADP-COD-OFOC IN ADP = '0000000090' AND WS-FLAG = 0
              MOVE    ADP-COD-OFOC IN ADP   TO     WS-COD-OFI
              MOVE    ADP-GLS-OFOC IN ADP   TO     WS-GLS-OFI
              MOVE    WK-AUX-COD-REGION     TO     WS-COD-REGION
              MOVE    WK-AUX-COD-LOCALIDAD  TO     WS-COD-LOCALIDAD
              MOVE    ADP-GLS-LOCA IN ADP   TO     WS-GLS-LOCALIDAD
              MOVE    1                     TO     WS-FLAG
           END-IF.
           MOVE     0                    TO     K.
           PERFORM  C-35-BUSCA-MON  THRU C-35-FIN.
           MOVE    WK-CODSIB(K)          TO     RPPAL-COD-MONEDA.
           MOVE    ADP-VAL-SFIN IN ADP   TO     RPPAL-MONTO.

           ADD     ADP-VAL-SFIN IN ADP   TO     WK-SFIN.

           MOVE    ADP-CIC-IOPC IN ADP   TO     RPPAL-NUM-IDENTF.

           MOVE    ADP-CIC-IOPC IN ADP   TO     WK-CIC-IOPC.
           ADD     WK-IOPC               TO     WK-IDENTF.

           MOVE    ADP-COD-CAUS IN ADP   TO     RPPAL-COD-CAUSAL.
           MOVE    ADP-NUM-SVEN IN ADP   TO     RPPAL-FEC-VEN-SS.
           MOVE    ADP-NUM-AVEN IN ADP   TO     RPPAL-FEC-VEN-AA.
           MOVE    ADP-NUM-MVEN IN ADP   TO     RPPAL-FEC-VEN-MM.
           MOVE    ADP-NUM-DVEN IN ADP   TO     RPPAL-FEC-VEN-DD.

           MOVE    ADP-FEC-FVEN IN ADP   TO     WK-FEC-FAUX.
           ADD     WK-NUM-SSAA           TO     WK-VSSAA.
           ADD     WK-NUM-MM             TO     WK-VMM.
           ADD     WK-NUM-DD             TO     WK-VDD.

           MOVE    ADP-NUM-SOUT IN ADP   TO     RPPAL-FEC-PAG-SS.
           MOVE    ADP-NUM-AOUT IN ADP   TO     RPPAL-FEC-PAG-AA.
           MOVE    ADP-NUM-MOUT IN ADP   TO     RPPAL-FEC-PAG-MM.
           MOVE    ADP-NUM-DOUT IN ADP   TO     RPPAL-FEC-PAG-DD.

           MOVE    ADP-FEC-FOUT IN ADP   TO     WK-FEC-FAUX.
           ADD     WK-NUM-SSAA           TO     WK-PSSAA.
           ADD     WK-NUM-MM             TO     WK-PMM.
           ADD     WK-NUM-DD             TO     WK-PDD.

           IF WK-FECHA-DESDE-VEN > ADP-FEC-FOUT IN ADP
              MOVE ADP-FEC-FOUT IN ADP   TO  WK-FECHA-DESDE-VEN
           ELSE
              IF WK-FECHA-HASTA-VEN < ADP-FEC-FOUT IN ADP
                 MOVE ADP-FEC-FOUT IN ADP   TO  WK-FECHA-HASTA-VEN.

           WRITE   REG-COLVPDI   FROM   REG-CUERPO.
           IF NOT COLVPDI-88
              DISPLAY '>>>> PROBLEMAS DE GRABACION      <<<<'
              DISPLAY '>>>> ARCHIVO DE SALIDA           <<<<'
              DISPLAY '>>>> REGISTRO CUERPO   (COLVPDI) <<<<'
           DISPLAY '>>>> FILE STATUS           ======>>> ' FS-COLVPDI
              MOVE 100 TO RETURN-CODE
              STOP RUN.
           PERFORM C-30-LECTURA-BIC.
       C-20-FIN. EXIT.
      *--------

       C-30-LECTURA-BIC.
      *----------------
           READ COLVBIC AT END MOVE 1 TO EOF-BIC.
           IF NOT FIN-ARCHIVO-BIC
              ADD 1 TO WK-CONT-1.
       C-30-FIN. EXIT.
      *--------

       C-35-BUSCA-MON.
      *---------------
           ADD  1  TO  K.
           IF WK-CODMON NOT = WK-CODBANC(K) AND K NOT = 19
              GO TO C-35-BUSCA-MON.
       C-35-FIN. EXIT.
      *--------

       FIN-PROCESAR. EXIT.
      *-------------

       D-TERMINO SECTION.
      *-----------------
           MOVE WS-SS-8   TO WS-SS-PROC.
           MOVE WS-MM-8   TO WS-MM-PROC.
           MOVE WS-DD-8   TO WS-DD-PROC.
           MOVE WS-AA-8   TO WS-AA-PROC.
           DISPLAY '>> FECHA DEL PROCESO <<'
                 WS-DD-PROC '/' WS-MM-PROC '/' WS-SS-PROC  WS-AA-PROC.
           DISPLAY '|-----------------------------------------|'.
           DISPLAY '|      TOTALES DE CONTROL PROCESO         |'.
           DISPLAY '|         PROGRAMA    COLPG017            |'.
           DISPLAY '|-----------------------------------------|'.
           DISPLAY 'REG.LEIDOS ARCHIVO DOCUMENTOS   PAGOS  ' WK-CONT-1.
           DISPLAY 'REG.GRABADOS PARA ACLARAR A BOLETIN    ' WK-CONT-2.
           CLOSE COLVBIC COLVPDI.
           OPEN  I-O COLVPDI
           READ COLVPDI AT END MOVE 1 TO EOF-PDI.
           IF NOT FIN-ARCHIVO-PDI
              ADD  1  TO WK-CORR
      *ALD-BCI-INI/28.03.2000
              MOVE WK-RUT         TO    RCAB-SUMA-RUT
              MOVE WK-SFIN        TO    RCAB-SUMA-SFIN
              MOVE WK-IDENTF      TO    RCAB-SUMA-IDENTF
              MOVE WK-VSSAA       TO    RCAB-FEC-VCTO-SSAA
              MOVE WK-VMM         TO    RCAB-FEC-VCTO-MM
              MOVE WK-VDD         TO    RCAB-FEC-VCTO-DD

              MOVE WK-PSSAA       TO    RCAB-FEC-PAGO-SSAA
              MOVE WK-PMM         TO    RCAB-FEC-PAGO-MM
              MOVE WK-PDD         TO    RCAB-FEC-PAGO-DD
      *ALD-BCI-FIN/28.03.2000

              MOVE WK-CORR        TO    RCAB-CANT-RGS
              MOVE FDESDE-VEN-SS  TO    RCAB-FEC-DESDE-S
              MOVE FDESDE-VEN-AA  TO    RCAB-FEC-DESDE-A
              MOVE FDESDE-VEN-MM  TO    RCAB-FEC-DESDE-M
              MOVE FDESDE-VEN-DD  TO    RCAB-FEC-DESDE-D
              MOVE FHASTA-VEN-SS  TO    RCAB-FEC-HASTA-S
              MOVE FHASTA-VEN-AA  TO    RCAB-FEC-HASTA-A
              MOVE FHASTA-VEN-MM  TO    RCAB-FEC-HASTA-M
              MOVE FHASTA-VEN-DD  TO    RCAB-FEC-HASTA-D
              REWRITE  REG-COLVPDI   FROM   REG-CABEZA  END-REWRITE
              IF NOT COLVPDI-88
                 DISPLAY '>>>> PROBLEMAS DE REGRABACION    <<<<'
                 DISPLAY '>>>> ARCHIVO DE SALIDA           <<<<'
                 DISPLAY '>>>> REGISTRO CABECERA (COLVPDI) <<<<'
                 DISPLAY '>>>> FILE STATUS            ======>>>'
                 FS-COLVPDI
                 MOVE 100 TO RETURN-CODE
                 STOP RUN.
           CLOSE COLVPDI.
       D-TERMINO-FIN. EXIT.
      *--------------


