       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGVMV.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.
      *JPROGRAMA GENERADOR DE ARCHIVOS PARA MVD.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------*
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCC-REQA==.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VLR-REQA==.


       COPY TABBRUSR.
       COPY TABBROFI.
       COPY TABBRVLR.
       COPY TABBRCAM.
       COPY TABBRTIO.

       COPY COLBRVMV.
       COPY COLBRVES.
       COPY COLBRVEN.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRTOC.
       COPY COLBRICG.
       COPY COLBRCYA.
       COPY COLBRRDC.
       COPY COLBREVC.
       COPY COLBRRCV.
       COPY COLBRRDT.

       COPY COLWGINT.
       COPY COLWGVEN.
       COPY COLWGCTO.
       COPY COLWGCAU.

       COPY SGCBRDBC.

       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       COPY GNSWGCVI.
       COPY GNSWGPES.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA.
       COPY GNSBRMSG.
       COPY GNSWVIDD.

        01 PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).

        01 WSS-VARI.
            03 VEN-FECH        VALUE SPACES   PIC X(01).
            03  WSS-FEC-FUCT.
               05  WSS-NUM-SUCT               PIC 9(02).
               05  WSS-NUM-AUCT               PIC 9(02).
               05  WSS-NUM-MUCT               PIC 9(02).
               05  WSS-NUM-DUCT               PIC 9(02).
           03  WSS-FEC-FXXX.
               05  WSS-NUM-SXXX               PIC 9(02).
               05  WSS-NUM-AXXX               PIC 9(02).
               05  WSS-NUM-MXXX               PIC 9(02).
               05  WSS-NUM-DXXX               PIC 9(02).
           03  WSS-PVEZ        VALUE SPACES   PIC X(01).
      *       RESPALDA VEN(1) PARA OPERACIONES ESTRUCTURADAS, PARA
      *       EXPANDIR PROXIMOS VENCIMIENTOS DE ESA ESTRUCTURA DENTRO
      *       DEL RANGO DE FECHAS Y LUEGO RESTAURARLO Y SEGUIR RECORRIEN
      *       LA BASE DE DATOS
           03 SCR-VEN-KEY-IVEN                       PIC X(15).
           03 WSS-VEN-KEY-IVEN                       PIC X(15).
           03 PGM-STAT-RDT VALUE SPACES              PIC X(02).
              88 PGM-STAT-RDT-OKS VALUE  '00'.

      *       INDICADOR PARA OBTENER VENCIMIENTOS DENTRO DEL RANGO
      *       ASOCIADOS A UNA ESTRUCTURA
           03 WSS-IND-RECO   VALUE 0              PIC 9(01).
              88 RECORRE-ESTRUCTURA               VALUE 1.
      *       INDICADOR PARA DETERMINAR EOF DE RDT
           03 WSS-IND-LAST   VALUE ' '             PIC X(01).
              88 LAST-RDT VALUE 'S'.

      *       NRO OPERACION PARA RDT'S DE UN MISMO DOCUMENTO
           03 WSS-RDT-CIC-IOPC                    PIC X(12).

      *       NRO DOCUMENTO PARA RDT'S DE UN MISMO DOCUMENTO
           03 WSS-RDT-NUM-IDLC                    PIC 9(03).
           03 WSS-NUM-IVCT                        PIC 9(03).

      *       STATUS VALIDACION PRO-DLC-VEN
           03 WSS-IND-DLC                         PIC X(01).
      *
      *    Saldo Documento
           03  WSS-RDT-VAL-SRDT   COMP-3          PIC 9(11)V9(04).
      *

           03 WSS-COD-OFOC                        PIC X(03).
           03 WSS-COD-RMOC                        PIC X(12).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-CIC-IAUX                        PIC X(12).
           03 WSS-GLS-NOMM                        PIC X(25).
           03 WSS-GLS-NOMF                        PIC X(25).
           03 WSS-SW                              PIC 9(01).
      * CODIGOS PARA SITUACIONES MOROSA.
           03 WSS-COD-SMOR      VALUE LOW-VALUES  PIC X(08).

      *VERIFCAR CIC MONEDA CHILENA Y NO PESOS
           03  WSS-VLR.
               05  WSS-CIC-VCAM.
      *            Tipo Valor
                   07  WSS-CIC-TVAL                    PIC X(01).
      *            Codigo Valor
                   07  WSS-CIC-CVAL                    PIC X(03).
      *            Tipo Cambio
                   07  WSS-CIC-TCAM                    PIC X(08).

      * NUMERO SIN DECIMALES ( PARA TRUNCAR )
            03 WSS-NUM-NDEC                            PIC 9(12).
            03 WSS-COD-CAUX.
               05  WSS-COD-TOOC                        PIC X(03).
               05  WSS-COD-AOOC                        PIC X(03).
      * GLOSAS TIO + AUX
            03  WSS-GLS-TIAU.
               05 WSS-GLS-TIO                          PIC X(40).
               05 WSS-GLS-AUX                          PIC X(40).
      *
            03  WSS-GLS-DAT3.
               05 FILLER                               PIC X(17).
               05 WSS-IND-OMIT                         PIC X(01).
               05 FILLER                               PIC X(12).
            03 WSS-IND                                 PIC 9(02).
            03 TAB-FLU-MOR-FUT.
               05 WSS-TAB-MOR      OCCURS 25.
                  07 WSS-COD-MORN                      PIC X(06).
                  07 WSS-VAL-MORN                      PIC 9(12)V9(04).
                  07 WSS-COD-MORE                      PIC X(06).
                  07 WSS-VAL-MORE                      PIC 9(12)V9(04).
               05 WSS-TAB-FUT      OCCURS 25.
                  07 WSS-COD-FUTN                      PIC X(06).
                  07 WSS-VAL-FUTN                      PIC 9(12)V9(04).
                  07 WSS-COD-FUTE                      PIC X(06).
                  07 WSS-VAL-FUTE                      PIC 9(12)V9(04).

            03 WSS-COD-COOC.
               05 WSS-IND-COOC      OCCURS 10          PIC X(06).
            03 WSS-IND-HAYR                            PIC 9(01).

            03 WSS-IND-MORA                            PIC X(01).
            03 WSS-IND-EXPA         VALUE ' '          PIC X(01).
               88 EXPANDI-SI        VALUE 'S'.
               88 EXPANDI-NO        VALUE 'N'.

            03 WSS-STAT-GRA                            PIC X(01).
               88 WSS-GRA-OKS       VALUE '0'.
               88 WSS-GRA-OKN       VALUE '1'.

            03 WSS-CIC-IOPC-ANT                        PIC X(12).
            03 WSS-CIC-IOPC                            PIC X(12).
            03 WSS-CIC-ICLI                            PIC X(12).

            03 WSS-COD-CAUX.
               05 WSS-COD-TOOC                         PIC X(03).
               05 WSS-COD-AOOC                         PIC X(03).

      *Contadores
            03 WSS-NUM-LVEN            PIC 9(06).
            03 WSS-NUM-LRDC            PIC 9(06).
            03 WSS-NUM-LSRT            PIC 9(06).
      *Inconsistentes
            03 WSS-NUM-IVEN            PIC 9(06).
            03 WSS-NUM-IRDC            PIC 9(06).
            03 WSS-NUM-IOPC            PIC 9(06).
      *Seleccionado.
            03 WSS-NUM-MVEN            PIC 9(06).
            03 WSS-NUM-VVEN            PIC 9(06).
            03 WSS-NUM-RERR            PIC 9(06).
      *Grabados.
            03 WSS-NUM-GVMV            PIC 9(06).


      *Saldo Documento
            03  WSS-RDT-VAL-SRDT       PIC 9(11)V9(04).
            03 WSS-NUM-FFVA            PIC 9(04).
      *VAR.ENCABEZADO
            03 WSS-VMV.
               07 WSS-GLS-VMV  VALUE 'COLPGVMV'
                                                   PIC X(08).
            05 WSS-FEC-FVM1.
               07 WSS-NUM-DVM1                     PIC 9(02).
               07 WSS-NUM-MVM1                     PIC 9(02).
               07 WSS-NUM-SVM1                     PIC 9(02).
               07 WSS-NUM-AVM1                     PIC 9(02).
            05 WSS-FEC-FVM2.
               07 WSS-NUM-DVM2                     PIC 9(02).
               07 WSS-NUM-MVM2                     PIC 9(02).
               07 WSS-NUM-SVM2                     PIC 9(02).
               07 WSS-NUM-AVM2                     PIC 9(02).
      *Var.Fechas
            03 WSS-FEC-FINI.
               05 WSS-NUM-DINI                     PIC 9(02).
               05 WSS-NUM-MINI                     PIC 9(02).
               05 WSS-NUM-SINI                     PIC 9(02).
               05 WSS-NUM-AINI                     PIC 9(02).
            03 WSS-FEC-FTER.
               05 WSS-NUM-DTER                     PIC 9(02).
               05 WSS-NUM-MTER                     PIC 9(02).
               05 WSS-NUM-STER                     PIC 9(02).
               05 WSS-NUM-ATER                     PIC 9(02).
            03 WSS-FEC-FFEC.
               05 WSS-NUM-SFEC                     PIC 9(02).
               05 WSS-NUM-AFEC                     PIC 9(02).
               05 WSS-NUM-MFEC                     PIC 9(02).
               05 WSS-NUM-DFEC                     PIC 9(02).
            03 WSS-FEC-FFET.
               05 WSS-NUM-SFET                     PIC 9(02).
               05 WSS-NUM-AFET                     PIC 9(02).
               05 WSS-NUM-MFET                     PIC 9(02).
               05 WSS-NUM-DFET                     PIC 9(02).
            03 WSS-FEC-FINT.
               05 WSS-NUM-SINT                   PIC 9(02).
               05 WSS-NUM-AINT                   PIC 9(02).
               05 WSS-NUM-MINT                   PIC 9(02).
               05 WSS-NUM-DINT                   PIC 9(02).
            03 WSS-FEC-FREG.
               05 WSS-NUM-SREG                     PIC 9(02).
               05 WSS-NUM-AREG                     PIC 9(02).
               05 WSS-NUM-MREG                     PIC 9(02).
               05 WSS-NUM-DREG                     PIC 9(02).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGVMV' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM CAP-PARAMETROS.
           PERFORM OPEN-FILE.

           PERFORM CICLO-VEN.
           PERFORM MUS-ESTADISTICA.

       FIN-MAIN.
           PERFORM GNS-PRO-END.
      *
       CAP-PARAMETROS SECTION.
      *----------------------*
       INI-CAP-PARAMETROS.
      *Contadores de registros grabados.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO WSS-NUM-DINI IN WSS-FEC-FINI.
           MOVE HOY-MHOY TO WSS-NUM-MINI IN WSS-FEC-FINI.
           MOVE HOY-SHOY TO WSS-NUM-SINI IN WSS-FEC-FINI.
           MOVE HOY-AHOY TO WSS-NUM-AINI IN WSS-FEC-FINI.

      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMSSAA ) : '.
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           IF WSS-FEC-FTER NOT > SPACES
              MOVE WSS-FEC-FINI TO WSS-FEC-FTER.

      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFOC IN WSS-VARI.

      *Captura Ejecutivo Operacion Credito
           DISPLAY 'EJECUTIVO : '.
           ACCEPT  WSS-COD-RMOC IN WSS-VARI.

      * Captura Codigo Clase Operacion
           MOVE SPACES TO WSS-COD-COOC IN WSS-VARI.
           DISPLAY 'CLASE OPERACION (*=TODAS) : '.
           ACCEPT  WSS-COD-COOC IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Termino
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ABORTO: FECHA DE TERMINO ERRONEA'
               DISPLAY WSS-FEC-FTER ' ' FEC-MENS
               PERFORM GNS-PRO-ABT.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB'         TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY 'ABORTO:'
               DISPLAY MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.

           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DFET IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MFET IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-SFET IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-AFET IN WSS-VARI.

      *Compara Fechas en Dias entregando Nro. de Dias de Futuros Vencimi
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF FEC-STAT-OKS
               MOVE FEC-NDIA TO WSS-NUM-FFVA IN WSS-VARI.

      *VAL-OFI Valida Codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.

           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
              MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
              PERFORM VAL-OFI
              IF MSG-COD-MENS > SPACES
                 DISPLAY 'ABORTO : OFICINA INEXISTENTE ' WSS-COD-OFOC
                 PERFORM GNS-PRO-ABT.

           IF WSS-COD-OFOC IN WSS-VARI  = '*'
              DISPLAY 'OFICINA : ' 'TODAS'
           ELSE
               DISPLAY 'OFICINA : ' OFI-COD-OFIC IN OFI '  '
                                    OFI-GLS-DESC IN OFI.

      *VAL-USR Valida Codigo de Usuario en TABLAS
           IF WSS-COD-RMOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-RMOC IN WSS-VARI.

           IF WSS-COD-RMOC IN WSS-VARI NOT = '*'
              MOVE WSS-COD-RMOC IN WSS-VARI TO USR-COD-USER IN USR
              PERFORM VAL-USR
              IF MSG-COD-MENS > SPACES
                 DISPLAY 'ABORTO : USUARIO INEXISTENTE '
                          WSS-COD-RMOC
                 PERFORM GNS-PRO-ABT.

           IF WSS-COD-RMOC IN WSS-VARI NOT = '*'
              DISPLAY 'EJECUTIVO : ' USR-COD-USER '  '
                                     USR-GLS-DESC
           ELSE
               DISPLAY 'EJECUTIVO : ' 'TODOS'.


      *VAL-TAB Valida Clase de Operacion en TABLAS
           IF WSS-COD-COOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-COOC IN WSS-VARI.

           IF WSS-COD-COOC IN WSS-VARI NOT = '*'
               MOVE 1 TO WSS-IND IN WSS-VARI
               PERFORM VAL-PAR-COOC UNTIL WSS-IND IN WSS-VARI > 10
               DISPLAY WSS-COD-COOC IN WSS-VARI
               IF WSS-COD-COOC IN WSS-VARI NOT > SPACES
                   DISPLAY 'NO EXISTE NINGUNA CLASE INGRESADA VALIDA'
                   PERFORM GNS-PRO-ABT.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
      ******************************************************************
      * Proceso para Cargar la tabla  COL/AUX  en Memoria
           PERFORM COL-CRG-CAU.
           IF CAU-CMSG IN CAU-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CAU-CMSG IN CAU-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CAU-MEN2 IN CAU-VARI
               PERFORM GNS-PRO-ABT.

      * Proceso para Cargar las tablas  TAB/TIO  y  COL/TOC  en Memoria
           PERFORM COL-CRG-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
               PERFORM GNS-PRO-ABT.
      *
           DISPLAY WSS-COD-COOC IN WSS-VARI.
       FIN-CAP-PARAMETROS.
           EXIT.
      *
       OPEN-FILE SECTION.
      *-----------------*
       INI-OPEN-FILE.
      *Apertura archivo VEN secuencial
            MOVE FIO-INP TO FIO-CMND.
            PERFORM COL-FIO-VES.
            IF NOT FIO-STAT-OKS OR
               VEN-FEC-FVEN IN VEN  > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO EXISTEN REG. PARA PROCESAR'
               PERFORM GNS-PRO-ABT
            ELSE
               MOVE FIO-GET-FST  TO FIO-CMND.
               PERFORM COL-FIO-VES.
               IF  FIO-STAT-OKS
                    MOVE VES TO VEN
                    DISPLAY '1) REG.VES: ' VEN-CIC-IOPC IN VEN.
      *
            MOVE FIO-OUT TO FIO-CMND.
            PERFORM COL-FIO-VMV.
            IF NOT FIO-STAT-OKS
               PERFORM GNS-PRO-ABT.
      *
            PERFORM GRABA-ENCABEZADO.
       FIN-OPEN-FILE.
           EXIT.

       CICLO-VEN SECTION.
      *---------------
       INI-CICLO-VEN.
      * Reg.procesados VEN
            MOVE 1       TO WSS-NUM-LVEN IN WSS-VARI.
            MOVE ZEROES  TO WSS-NUM-LRDC IN WSS-VARI.
      *Reg. inconsistentes
            MOVE ZEROS   TO WSS-NUM-IVEN IN WSS-VARI.
            MOVE ZEROS   TO WSS-NUM-IRDC IN WSS-VARI.

      *Reg.Moroso,Vigentes
            MOVE ZEROS   TO WSS-NUM-MVEN IN WSS-VARI.
            MOVE ZEROS   TO WSS-NUM-VVEN IN WSS-VARI.
      *Ind.Mora
            MOVE SPACES  TO WSS-IND-MORA IN WSS-VARI.
      *
            MOVE ZEROES  TO WSS-NUM-GVMV IN WSS-VARI.
            MOVE ZEROES  TO WSS-NUM-LSRT IN WSS-VARI.
            MOVE SPACES TO WSS-CIC-IOPC-ANT IN WSS-VARI.
      *
       LUP-REG-VES.
           IF VEN-FEC-FVEN IN VEN  > WSS-FEC-FFET IN WSS-VARI
              GO TO NEXT-REG-VES.

      *Ven Canceladas.
           IF  VEN-VAL-SFIN IN VEN NOT > ZEROES AND
               VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FFEC IN WSS-VARI
               GO TO NEXT-REG-VES.
      *
           IF  VEN-IND-SCTB IN VEN = SPACES AND
               VEN-FEC-FVEN IN VEN < WSS-FEC-FFEC IN WSS-VARI
               ADD 1 TO WSS-NUM-IVEN IN WSS-VARI
               GO TO NEXT-REG-VES.
      *
           IF  VEN-IND-SCTB IN VEN > SPACES
               MOVE 'M' TO WSS-IND-MORA IN WSS-VARI
           ELSE
               MOVE 'V' TO WSS-IND-MORA IN WSS-VARI.

      *Guardar la VEN-CIC-IOPC
           IF VEN-CIC-IOPC IN VEN = WSS-CIC-IOPC-ANT IN WSS-VARI
              GO TO  VAL-REG-VEN.
      *
           MOVE VEN-CIC-IOPC IN VEN TO WSS-CIC-IOPC-ANT IN WSS-VARI.
           PERFORM VALIDA-OPC.
           IF WSS-GRA-OKN
              PERFORM BUS-SIG-OPERACION
              GO TO LUP-REG-VES
           ELSE
              PERFORM BUS-REG-RDC
              IF WSS-GRA-OKN
                 PERFORM BUS-SIG-OPERACION
                 GO TO LUP-REG-VES
              ELSE
                PERFORM BUS-DAT-ICLI
                IF WSS-GRA-OKN
                   PERFORM BUS-SIG-OPERACION
                   GO TO LUP-REG-VES.

       VAL-REG-VEN.
           PERFORM BUS-COM-OPERA-VEN.
           PERFORM GRABA-REG-VMV.

       NEXT-REG-VES.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-VES.
           IF NOT FIO-STAT-OKS
              GO TO CERRA-CICLO-VEN.

           ADD 1 TO WSS-NUM-LVEN IN WSS-VARI.
            MOVE VES TO VEN.
           GO TO LUP-REG-VES.

       CERRA-CICLO-VEN.
            MOVE FIO-CLO TO FIO-CMND.
            PERFORM COL-FIO-VES.

            MOVE FIO-CLO TO FIO-CMND.
            PERFORM COL-FIO-VMV.
       FIN-CICLO-VEN.
           EXIT.

       BUS-REG-RDC SECTION.
      *--------------------*
       INI-BUS-REG-RDC.
            MOVE '0'     TO WSS-STAT-GRA IN WSS-VARI.
            MOVE VEN-CAI-IOPC IN VEN TO RDC-CAI-IOPC IN RDC.
            MOVE VEN-IIC-IOPC IN VEN TO RDC-IIC-IOPC IN RDC.
            MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
            MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
            MOVE FIO-GET-KEY    TO FIO-CMND.
            PERFORM COL-FIO-RDC.
            IF NOT FIO-STAT-OKS
               ADD 1     TO WSS-NUM-IRDC IN WSS-VARI
               MOVE '1'  TO WSS-STAT-GRA IN WSS-VARI
               GO TO FIN-BUS-REG-RDC.
       FIN-BUS-REG-RDC.
           EXIT.

       BUS-SIG-OPERACION SECTION.
       INI-BUS-SIG-OPERACION.
           MOVE VEN-CIC-IOPC IN VEN TO WSS-CIC-IOPC-ANT IN WSS-VARI.
       LUP-SIG-OPERACION.
           IF VEN-CIC-IOPC IN VEN NOT = WSS-CIC-IOPC-ANT IN WSS-VARI
              GO TO FIN-BUS-SIG-OPERACION.

           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-VES.
           IF NOT FIO-STAT-OKS
              GO TO CERRA-CICLO-VEN.

           ADD 1    TO WSS-NUM-LVEN IN WSS-VARI.
           MOVE VES TO VEN.
           GO TO LUP-SIG-OPERACION.
       FIN-BUS-SIG-OPERACION.
           EXIT.
      *----------------------------------------------------------*
       GRABA-ENCABEZADO SECTION.
       INI-GRABA-ENCABEZADO.
            MOVE ZEROES TO VMV.
            MOVE HOY-DHOY                 TO WSS-NUM-DVM1 IN WSS-VMV.
            MOVE HOY-MHOY                 TO WSS-NUM-MVM1 IN WSS-VMV.
            MOVE HOY-SHOY                 TO WSS-NUM-SVM1 IN WSS-VMV.
            MOVE HOY-AHOY                 TO WSS-NUM-AVM1 IN WSS-VMV.
            MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DVM2 IN WSS-VMV.
            MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MVM2 IN WSS-VMV.
            MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-SVM2 IN WSS-VMV.
            MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-AVM2 IN WSS-VMV.
            MOVE WSS-VMV               TO VMV.
            MOVE FIO-PUT TO FIO-CMND.
            PERFORM COL-FIO-VMV.
            IF NOT FIO-STAT-OKS
               DISPLAY ' ERROR AL GRABAR VMV(FIO-STAT) ' FIO-STAT
                       ' EN CIC-OPC : ' OPC-CIC-IOPC IN OPC
               PERFORM GNS-PRO-ABT.
       FIN-GRABA-ENCABEZADO.
           EXIT.

       GRABA-REG-VMV SECTION.
      *--------------------*
       INI-GRABA-REG-VMV.
            MOVE SPACES         TO VMV-IND-TAPO IN VMV.
            MOVE WSS-IND-MORA IN WSS-VARI TO VMV-IND-MORA IN VMV.
            MOVE VEN-NUM-IVCT IN VEN      TO VMV-NUM-IVCT IN VMV.
            MOVE VEN-FEC-FVEN IN VEN      TO VMV-FEC-FVEN IN VMV.
            MOVE VEN-IND-CALC IN VEN      TO VMV-IND-CALC IN VMV.
            MOVE VEN-VAL-SFIN IN VEN      TO VMV-VAL-VTRA IN VMV.
       VAL-IND-CAL.
      * Verifica si el vencimiento esta calculado.
      * Tengo OPC Leida.
            IF VEN-IND-CALC IN VEN = 'N'  AND
               VEN-VAL-SFIN IN VEN = ZEROES
                  MOVE '*'                 TO VMV-IND-TAPO IN VMV
                  MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN
                  MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVEN IN VEN
                  MOVE OPC-FEC-FCOL IN OPC TO WSS-FEC-FINT IN WSS-VARI
                  PERFORM CAL-ULT-CAMBIO-TASA
      *
                  MOVE WSS-FEC-FUCT IN WSS-VARI TO VEN-FTBS
                  MOVE 'S'         TO VEN-KEST
                  MOVE FIO-GET-KEY TO FIO-CMND
                  PERFORM COL-OBT-VEN
                  MOVE VEN-VAL-SFIN IN VEN TO VMV-VAL-VTRA IN VMV.

      *Grava primer reg.Relacionado a la VEN(ver expancion).
           PERFORM GRABA-REG-ARCH.
      *
      *Vencimiento no es extructurado(No)
           IF OPC-IND-VSTR IN OPC = 'N'
              MOVE 0 TO WSS-IND-RECO
              GO TO FIN-GRABA-REG-VMV.

      *Vencimiento es moroso(No)
           IF VMV-IND-MORA IN VMV = 'M'
              GO TO FIN-GRABA-REG-VMV.
      *
       VAL-VEN-EXPANDE.
      *ultimo vencimiento(No).
           IF VMV-NUM-IVCT IN VMV = OPC-NUM-VCOR IN OPC
              GO TO FIN-GRABA-REG-VMV.

      *Buscar OPC
      *     MOVE VMV-CIC-IOPC IN VMV TO OPC-CIC-IOPC IN OPC.
      *     MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM COL-FIO-OPC.
      *     IF NOT FIO-STAT-OKS
      *        DISPLAY 'ERROR (COL-FIO-OPC)'
      *        GO TO FIN-GRABA-REG-VMV.

      *Fecha del vencimiento es Mayor que la de termino
           MOVE 1    TO WSS-IND-RECO.
           MOVE 'F'  TO VEN-FECH.
           COMPUTE
           WSS-NUM-IVCT IN WSS-VARI = (VMV-NUM-IVCT IN VMV + 1).
      *
      *
           MOVE OPC-CIC-IOPC IN OPC      TO VEN-CIC-IOPC IN VEN.
           MOVE WSS-NUM-IVCT IN WSS-VARI TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF NOT VEN-STAT-OKS
              GO TO FIN-GRABA-REG-VMV.

           IF VEN-FEC-FVEN IN VEN  > WSS-FEC-FFET IN WSS-VARI
              GO TO FIN-GRABA-REG-VMV.
      *
      *Expandir
           MOVE SPACES                   TO VMV-IND-TAPO IN VMV.
           MOVE VMV-CIC-IOPC IN VMV      TO VEN-CIC-IOPC IN VEN.
           MOVE WSS-NUM-IVCT IN WSS-VARI TO VEN-NUM-IVEN IN VEN.
           MOVE OPC-FEC-FCOL IN OPC      TO WSS-FEC-FINT IN WSS-VARI.
           PERFORM CAL-ULT-CAMBIO-TASA.

           MOVE WSS-FEC-FUCT IN WSS-VARI TO VEN-FTBS.
           MOVE 'S'         TO VEN-KEST.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF NOT VEN-STAT-OKS
              DISPLAY 'ERROR COL-OBT-VEN: ' VEN-STAT
              GO TO FIN-GRABA-REG-VMV.

      *Mover VEN -> VMV.
       MOV-NXT-OUT-VEN.
           IF VEN-FEC-FVEN IN  VEN NOT >  WSS-FEC-FFEC IN WSS-VARI
              MOVE 'M'  TO  VMV-IND-MORA IN VMV
           ELSE
               MOVE 'V' TO VMV-IND-MORA IN VMV.
      *
           IF VEN-IND-CALC IN VEN = 'N'
              MOVE '*' TO VMV-IND-TAPO IN VMV.

           MOVE VEN-NUM-IVCT IN VEN TO VMV-NUM-IVCT IN VMV.
           MOVE VEN-IND-CALC IN VEN TO VMV-IND-CALC IN VMV.
           MOVE VEN-FEC-FVEN IN VEN TO VMV-FEC-FVEN IN VMV.
           MOVE VEN-VAL-SFIN IN VEN TO VMV-VAL-VTRA IN VMV.
      *Graba Reg.
           PERFORM GRABA-REG-ARCH.
           GO TO VAL-VEN-EXPANDE.
       FIN-GRABA-REG-VMV.
           EXIT.


       GRABA-REG-ARCH SECTION.
       INI-GRABA-REG-ARCH.
           IF  VMV-IND-MORA IN VMV  = 'M'
               ADD 1    TO WSS-NUM-MVEN IN WSS-VARI
           ELSE
               ADD 1    TO WSS-NUM-VVEN IN WSS-VARI.
      *
            MOVE DBC-GLS-NOMC IN DBC TO VMV-GLS-NOMC IN VMV.
            MOVE DBC-IDC-ICLI IN DBC TO VMV-IDC-ICLI IN VMV
            MOVE DBC-VRF-ICLI IN DBC TO VMV-VRF-ICLI IN VMV.
            MOVE RDC-CIC-ICLI IN RDC TO VMV-CIC-ICLI IN VMV.
      *Hasta Que Cambie Operacion
            MOVE OPC-CIC-IOPC IN OPC TO VMV-CIC-IOPC IN VMV.
            MOVE OPC-COD-OFOC IN OPC TO VMV-COD-OFOC IN VMV.
            MOVE OPC-COD-RMOC IN OPC TO VMV-COD-RMOC IN VMV.
            MOVE OPC-COD-VCOC IN OPC TO VMV-COD-VCOC IN VMV.
            MOVE OPC-COD-COOC IN OPC TO VMV-COD-COOC IN VMV.
            MOVE OPC-MSC-TVOC IN OPC TO VMV-MSC-TVOC IN VMV.
      *Por cada reg.leido
            MOVE CYA-IIC-ICYA IN CYA TO VMV-IIC-ICYA IN VMV.
            MOVE ICG-SGV-TINT IN ICG TO VMV-SGV-TINT IN VMV.
      *
            MOVE FIO-PUT TO FIO-CMND.
            PERFORM COL-FIO-VMV.
            IF NOT FIO-STAT-OKS
               DISPLAY ' ERROR AL GRABAR VMV(FIO-STAT) ' FIO-STAT
                       ' EN CIC-OPC : ' OPC-CIC-IOPC IN OPC
               PERFORM GNS-PRO-ABT
           ELSE
           ADD 1 TO WSS-NUM-GVMV.
       FIN-GRABA-REG-ARCH.
           EXIT.
      *----------------------------------------------------------*
       VALIDA-OPC SECTION.
      *------------------*
       INI-VALIDA-OPC.
            MOVE '0'                 TO WSS-STAT-GRA IN WSS-VARI.
            MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
            MOVE FIO-GET-KEY         TO FIO-CMND.
            PERFORM COL-FIO-OPC.
            IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-IOPC IN WSS-VARI
               MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
               GO TO FIN-VALIDA-OPC.

           IF OPC-MSC-STAT IN OPC NOT = 'A'
              MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
              GO TO FIN-VALIDA-OPC.

           IF OPC-VAL-SCRE IN OPC = ZEROES
              MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
              GO TO FIN-VALIDA-OPC.

           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFOC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                  MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
                  GO TO FIN-VALIDA-OPC.

           IF WSS-COD-RMOC IN WSS-VARI NOT = '*'
               IF WSS-COD-RMOC IN WSS-VARI NOT = OPC-COD-RMOC IN OPC
                  MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
                  GO TO FIN-VALIDA-OPC.

           IF WSS-COD-COOC IN WSS-VARI NOT = '*'
                  MOVE 1 TO WSS-IND IN WSS-VARI
                  PERFORM VAL-SEL-COOC UNTIL
                          WSS-IND IN WSS-VARI > 10 OR
                          WSS-IND-COOC IN WSS-VARI(WSS-IND) =
                                        OPC-COD-COOC IN OPC
               IF WSS-IND IN WSS-VARI > 10
                  MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
                  GO TO FIN-VALIDA-OPC.

      * Busca con el Codigo Clase Operacion
           MOVE OPC-COD-COOC IN OPC TO CAU-COOC IN CAU-VARI.
           PERFORM COL-BUS-CAU.
           IF CAU-CMSG IN CAU-VARI > SPACES
                DISPLAY 'OPERACION SIN COD-COOC : '
                         OPC-KEY-IOPC IN OPC
                MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
                GO TO FIN-VALIDA-OPC.

      * Excluye Tipos de Operaciones segun Indicador en Pos. 48 Datos An
           MOVE CAU-DAT3 IN CAU-VARI TO WSS-GLS-DAT3 IN WSS-VARI.
           IF WSS-IND-OMIT IN WSS-VARI = 'X'
                MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
                GO TO FIN-VALIDA-OPC.
      *
           MOVE OPC-COD-TOOC IN OPC TO CTO-TOOC IN CTO-VARI.
           PERFORM COL-BUS-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
              DISPLAY 'CON CODIGO : ' CTO-TOOC IN CTO-VARI
              MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
              GO TO FIN-VALIDA-OPC.

      * Obtiene 'Opera Como'
            MOVE SPACES               TO TOC.
            MOVE CTO-OPEC IN CTO-VARI TO TOC-MSC-OPEC IN TOC.
            PERFORM BUS-COM-OPERA-OPC.

      * obtiene cuenta cargo
            MOVE OPC-CAI-IOPC IN OPC TO CYA-CAI-IOPC IN CYA.
            MOVE OPC-IIC-IOPC IN OPC TO CYA-IIC-IOPC IN CYA.
            MOVE 'AUT'               TO CYA-NUM-ICAN IN CYA.
            MOVE 1                   TO CYA-NUM-ICYA IN CYA.
            MOVE FIO-GET-KEY TO FIO-CMND.
            PERFORM COL-FIO-CYA.
            IF NOT FIO-STAT-OKS
               MOVE SPACES TO CYA-IIC-ICYA IN CYA.
       FIN-VALIDA-OPC.
           EXIT.

       BUS-COM-OPERA-VEN SECTION.
      *---------------------*
       INI-BUS-COM-OPERA-VEN.
      * Complementa Formacion del Registro SRT
           MOVE SPACES               TO TOC.
           MOVE CTO-OPEC IN CTO-VARI TO TOC-MSC-OPEC IN TOC.
           MOVE '00'  TO  FIO-STAT.
           IF TOC-MSC-OPEC IN TOC NOT = 'D'
              GO TO FIN-BUS-COM-OPERA-VEN.

           MOVE ZEROES TO ICG-NUM-ICAN IN ICG.
           PERFORM BUS-DLC-ICG.
      *
           IF ICG-COD-CVTI IN ICG > SPACES
               MOVE ICG-SGV-SPRD IN ICG TO ICG-SGV-TINT IN ICG.
      *
           IF NOT FIO-STAT-OKS
               MOVE ZEROES TO ICG-SGV-TINT IN ICG.
       FIN-BUS-COM-OPERA-VEN.
           EXIT.

       BUS-COM-OPERA-OPC SECTION.
      *---------------------*
       INI-BUS-COM-OPERA-OPC.
      * Complementa Formacion del Registro SRT
           MOVE '00'  TO  FIO-STAT.
           IF TOC-MSC-OPEC IN TOC = 'D'
               GO TO FIN-BUS-COM-OPERA-OPC.

           MOVE ZEROES TO ICG-NUM-ICAN IN ICG.

       OPC-BUS-OPC-ICG.
           MOVE ZEROES TO ICG-NUM-ICAN IN ICG.
           PERFORM GET-KEY-IICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-NUM-ICAN IN ICG = ZEROES              AND
                    ICG-CIC-IOPC IN ICG = OPC-CIC-IOPC IN OPC AND
                    ICG-CIC-IOPV IN ICG = SPACES              AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
                     MOVE '23' TO FIO-STAT.
      *
           IF ICG-COD-CVTI IN ICG > SPACES
               MOVE ICG-SGV-SPRD IN ICG TO ICG-SGV-TINT IN ICG.
      *
           IF NOT FIO-STAT-OKS
               MOVE ZEROES TO ICG-SGV-TINT IN ICG.
       FIN-BUS-COM-OPERA-OPC.
           EXIT.
      * Busca Documento Legal de Operacion de Credito segun Vencimiento
      * Obtiene Tasa de Interes al Leer ICG por Llave KEY-IICG Formada

       BUS-DLC-ICG SECTION.
      *-------------------*
       INI-BUS-DLC-ICG.
           IF VEN-NUM-IDLC IN VEN NOT = 0
               MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC
           ELSE
               MOVE VEN-NUM-IVCT IN VEN TO DLC-NUM-IDLC IN DLC.

            MOVE OPC-CIC-IOPC IN OPC     TO DLC-CIC-IOPC IN DLC.
            MOVE FIO-GET-KEY             TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    DLCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   DLC-KEY-IDLC
               GO TO FIN-BUS-DLC-ICG.
       GET-ICG-DLC.
           MOVE DLC-NUM-IDLC IN DLC TO ICG-NUM-ICAN IN ICG.
           PERFORM GET-KEY-IICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-NUM-ICAN IN ICG = DLC-NUM-IDLC IN DLC AND
                    ICG-CIC-IOPC IN ICG = OPC-CIC-IOPC IN OPC AND
                    ICG-CIC-IOPV IN ICG = SPACES              AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               PERFORM BUS-OPC-ICG.
       FIN-BUS-DLC-ICG.
           EXIT.

      * Obtiene Tasa de Interes al Leer ICG por Llave KEY-IICG Formada
       BUS-OPC-ICG SECTION.
      *-------------------*
       INI-BUS-OPC-ICG.
            MOVE ZEROES TO ICG-NUM-ICAN IN ICG.
           PERFORM GET-KEY-IICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-NUM-ICAN IN ICG = ZEROES              AND
                    ICG-CIC-IOPC IN ICG = OPC-CIC-IOPC IN OPC AND
                    ICG-CIC-IOPV IN ICG = SPACES              AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
                     MOVE '23' TO FIO-STAT.
       FIN-BUS-OPC-ICG.
           EXIT.


       GET-KEY-IICG SECTION.
      *--------------------*
       INI-GET-KEY-IICG.
      * Forma Llave KEY-IICG y Lee ICG para Obtener Tasa de Interes
            MOVE OPC-CIC-IOPC IN OPC TO ICG-CIC-IOPC IN ICG.
            MOVE SPACES              TO ICG-CIC-IOPV IN ICG.
            MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
            MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
            MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
            MOVE FIO-GET-NLS         TO FIO-CMND.
           PERFORM COL-FIO-ICG.
       FIN-GET-KEY-IICG.
           EXIT.


       BUS-DAT-ICLI SECTION.
      *--------------------*
       INI-BUS-DAT-ICLI.
      *CIC-CLI Busca Datos del Cliente Mediante su CIC
            MOVE '0' TO WSS-STAT-GRA IN WSS-VARI.
            MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
            MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS > SPACES
              MOVE '1' TO WSS-STAT-GRA IN WSS-VARI
              MOVE SPACES TO DBC-NUM-ICLI IN DBC
                              DBC-VRF-ICLI IN DBC
                              DBC-IND-ICLI IN DBC
                              DBC-GLS-ICLI IN DBC
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.
       FIN-BUS-DAT-ICLI.
           EXIT.

       VAL-PAR-COOC SECTION.
       INI-VAL-PAR-COOC.
           IF WSS-IND-COOC IN WSS-VARI(WSS-IND) > SPACES
               MOVE 'COL'           TO TAB-COD-SIST
               MOVE 'AUX'           TO TAB-COD-TTAB IN TAB
               MOVE WSS-IND-COOC IN WSS-VARI(WSS-IND)
                                    TO TAB-COD-CTAB IN TAB
               PERFORM VAL-TAB
               IF MSG-COD-MENS IN MSG > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                           ' CLASE : ' WSS-IND-COOC IN WSS-VARI(WSS-IND)
                   MOVE SPACES TO WSS-IND-COOC IN WSS-VARI(WSS-IND).
           ADD 1 TO WSS-IND IN WSS-VARI.
       FIN-VAL-PAR-COOC.
           EXIT.

      * Valida que pertenezca a Clase del parametro
       VAL-SEL-COOC SECTION.
       INI-VAL-SEL-COOC.
           ADD 1 TO WSS-IND IN WSS-VARI.
       FIN-VAL-SEL-COOC.
           EXIT.


       MUS-ESTADISTICA SECTION.
       INI-MUS-ESTADISTICA.
           DISPLAY 'ESTADISTICA COLPGVMV        '.
           DISPLAY '============================'.
           DISPLAY 'REG. LEIDOS    VEN  :' WSS-NUM-LVEN.
           DISPLAY 'REG. GRABADOS  VMV  :' WSS-NUM-GVMV.
           DISPLAY 'REG. ERRONEOS  RDC  :' WSS-NUM-IRDC.
           DISPLAY 'REG. ERRONEOS  OPC  :' WSS-NUM-IOPC.
       FIN-MUS-ESTADISTICA.
           EXIT.
      * COPY COLBGVEN.

      ******************************************************************
      * COPY COLBGVEN REPLACING ==IN OPC== BY ==IN OPC==.
      *Rutina de expansion de vencimientos
      *Se modifico para que expandiera todas las cuotas,
      *incluyendo la ultima que no conoce
      *JSS LAST MOD. 16-JUN-1997
       COL-OBT-VEN SECTION.
       INI-COL-OBT-VEN.
      * Carga variable necesaria para procesar con la variable VEN-INIT
           ADD  1  OPC-NUM-IVEN IN OPC GIVING VEN-OIM1.

      *Calcula la cantidad de VENCIMIENTOS TOTAL de la operacion
           IF OPC-NUM-TVCC IN OPC NOT > ZEROES
               ADD OPC-NUM-VCVG IN OPC
                   OPC-NUM-VCCO IN OPC
                   OPC-NUM-VCCM IN OPC
                   OPC-NUM-VCCV IN OPC GIVING VEN-VCTO
           ELSE
               ADD OPC-NUM-VCVG IN OPC
                   OPC-NUM-TVCC IN OPC GIVING VEN-VCTO.
      *
      *Analiza comando ( cual vencimiento desea obtener )
           IF FIO-CMND = FIO-GET-FST
               MOVE 1 TO VEN-NUM-IVEN IN VEN
           ELSE
           IF FIO-CMND = FIO-GET-NLS
               IF VEN-NUM-IVEN IN VEN = ZEROES
                   MOVE 1 TO VEN-NUM-IVEN IN VEN
               ELSE
                   NEXT SENTENCE
           ELSE
           IF FIO-CMND = FIO-GET-NXT
               ADD 1 TO VEN-NUM-IVEN IN VEN
               IF VEN-NUM-IVEN IN VEN > VEN-VCTO
                   MOVE VEN-SEOF TO VEN-STAT
                   GO TO FIN-COL-OBT-VEN.
      *
      *Parrafo que lee vencimiento, estructuras y carga variables de tra
           PERFORM INI-VEN.
           IF VEN-STAT-CUR
               MOVE RCV-IRCV IN RCV-VRBL TO RCV-KEY-IRCV IN RCV
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-RCV
               MOVE ICG-IICG IN ICG-VRBL TO ICG-KEY-IICG IN ICG
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-ICG
               GO TO FIN-COL-OBT-VEN.
      *
      *Si pidio un vencimiento siguiente o un vencimiento anterior
      *el VEN-IEVC parte con la estructura que genero el vencimiento act
      *el VEN-IVCT es el vencimiento pedido
           IF VEN-IVAS-SIG
               PERFORM INC-VEN UNTIL ( NOT VEN-STAT-OKS ) OR
                       VEN-IVCT = EVC-IVCT(VEN-IEVC)
           ELSE
               PERFORM DEC-VEN UNTIL ( NOT VEN-STAT-OKS ) OR
                       VEN-IVCT = EVC-IVCT(VEN-IEVC).
      *
      *Calcula Inicio Mora para el VEN encontrado, y lee su DLC y EVC as
           IF VEN-FECH NOT = 'F'
           PERFORM FIN-VEN.
           IF VEN-STAT-OKS
               PERFORM LOD-RVEN.
       FIN-COL-OBT-VEN.
           MOVE ZEROES TO VEN-FTBS.
           MOVE 'S'    TO VEN-INIT.
           MOVE 'N'    TO VEN-KEST.
      * Carga variables necesarias para procesar con la variable VEN-INI
           MOVE OPC-CIC-IOPC IN OPC TO VEN-OANT.
           MOVE VEN-IVCT            TO VEN-VANT.
           MOVE SPACES              TO VEN-FECH.
       EXT-COL-OBT-VEN.
           EXIT.

       LOD-RVEN SECTION.
       INI-LOD-RVEN.
      *carga valores de VEN-VARI para conformar el registro VEN a retorn
           MOVE EVC-FFVC(VEN-IEVC) TO VEN-FEC-FVEN IN VEN.
           MOVE VEN-FIMV           TO VEN-FEC-FIMV IN VEN.
           MOVE VEN-CAPI           TO VEN-VAL-CAPI IN VEN.
           MOVE VEN-SCAP           TO VEN-VAL-SCAP IN VEN.
           MOVE VEN-IDNI           TO VEN-VAL-IDNI IN VEN.
           MOVE VEN-FINA           TO VEN-VAL-FINA IN VEN.
           MOVE VEN-SFIN           TO VEN-VAL-SFIN IN VEN.
           MOVE VEN-SCTB           TO VEN-IND-SCTB IN VEN
                                      VEN-IND-CTBA IN VEN.
           MOVE VEN-IVCT           TO VEN-NUM-IVCT IN VEN.
           MOVE EVC-IEVC(VEN-IEVC) TO VEN-NUM-IEVC IN VEN.
           MOVE VEN-IDLC           TO VEN-NUM-IDLC IN VEN.
           MOVE EVC-CAUT(VEN-IEVC) TO VEN-IND-CAUT IN VEN.
           MOVE VEN-SCBZ           TO VEN-IND-SCBZ IN VEN.
           MOVE ZEROES             TO VEN-VAL-MTSC IN VEN.
           MOVE ZEROES             TO VEN-IND-MTSC IN VEN.
           MOVE VEN-CALC           TO VEN-IND-CALC IN VEN.
       FIN-LOD-RVEN.
           EXIT.

       INI-VEN SECTION.
       INI-INI-VEN.
      * Discrimina la validez de procesar con la variable VEN-INIT
           IF VEN-NUM-IVEN IN VEN > VEN-OIM1 AND
              OPC-CIC-IOPC IN OPC = VEN-OANT AND
              VEN-VANT > OPC-NUM-IVEN IN OPC AND
              VEN-VANT < VEN-NUM-IVEN IN VEN
               MOVE SPACES              TO VEN-SCTB
               MOVE SPACES              TO VEN-SCBZ
               MOVE 'N'                 TO VEN-INIT
               MOVE VEN-NUM-IVEN IN VEN TO VEN-IVCT
               GO TO FIN-INI-VEN
           ELSE
               MOVE 'S' TO VEN-INIT.
      *Inicializacion de variables
           MOVE SPACES     TO VEN-VARI EVC-VARI DLC-VARI AYF-VARI.
           MOVE LOW-VALUES TO VEN-VARC                   AYF-VARC.
           MOVE 10         TO VEN-LVCC.
           MOVE RCV-KEY-IRCV IN RCV TO RCV-IRCV IN RCV-VRBL.
           MOVE ICG-KEY-IICG IN ICG TO ICG-IICG IN ICG-VRBL.
      *
      *Lee el vencimiento en la base de datos ( variable VEN-NUM-IVCT co
      *el numero del vencimiento )
           MOVE VEN-SOKS TO VEN-STAT.
           MOVE VEN-NUM-IVEN IN VEN TO VEN-IVCT.
           MOVE 1 TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE VEN-SFAL TO VEN-STAT
               GO TO FIN-INI-VEN.
           MOVE SPACES              TO VEN-SCTB.
           MOVE SPACES              TO VEN-SCBZ.
           MOVE VEN-FEC-FVEN IN VEN TO VEN-FVEN.
           MOVE VEN-FEC-FIMV IN VEN TO VEN-FIMV.
           MOVE VEN-NUM-IDLC IN VEN TO VEN-IDLC.
           MOVE VEN-VAL-CAPI IN VEN TO VEN-CAPI.
           MOVE VEN-VAL-SCAP IN VEN TO VEN-SCAP.
           MOVE VEN-VAL-IDNI IN VEN TO VEN-IDNI.
           MOVE VEN-VAL-FINA IN VEN TO VEN-FINA.
           MOVE VEN-VAL-SFIN IN VEN TO VEN-SFIN.
           MOVE VEN-NUM-IVCT IN VEN TO VEN-NVEN.
           MOVE VEN-IVCT TO VEN-NUM-IVEN IN VEN.
      *Realiza analisis de feriado a la fecha de vencimiento
      *en el caso de que esta sea feriado
           IF OPC-IND-AFER IN OPC = 'N'
               GO TO CON-INI-VEN.
           MOVE VEN-DVEN TO FEC-ITM1.
           MOVE VEN-MVEN TO FEC-ITM2.
           MOVE VEN-SVEN TO FEC-ITM3.
           MOVE VEN-AVEN TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
               MOVE FEC-SIG-HBL  TO FEC-CMND
               MOVE FEC-FORM-FEC TO FEC-FORM
      *        PERFORM PRO-FEC
               PERFORM CAL-FEC
               MOVE FEC-ITM1 TO VEN-DVEN
               MOVE FEC-ITM2 TO VEN-MVEN
               MOVE FEC-ITM3 TO VEN-SVEN
               MOVE FEC-ITM4 TO VEN-AVEN.
       CON-INI-VEN.
      *
           IF VEN-IND-CALC IN VEN = 'N' AND VEN-KEST = 'S' AND
              VEN-INIT = 'S' AND VEN-NUM-IVCT IN VEN NOT > VEN-IVCT
               PERFORM COM-REG-VEN
               MOVE VEN-NUM-IVCT IN VEN TO VEN-NVEN
               IF NOT VEN-STAT-OKS
                   GO TO FIN-INI-VEN.

      *Pregunta para ver si el vencimiento pedido ( VEN-IVCT ) correspon
      *a un vencimiento siguiente, a uno anterior, o al actual
           IF VEN-NUM-IVCT IN VEN < VEN-IVCT
               MOVE 'S' TO VEN-IVAS
           ELSE
           IF VEN-NUM-IVCT IN VEN > VEN-IVCT
               MOVE 'A' TO VEN-IVAS
           ELSE
      *Pidio el 'current'
               MOVE VEN-SCUR TO VEN-STAT
      *        Si el saldo del credito es cero ==> que ya se pago el ult
      *        vencimiento y este no puede ser otro que el 'current', po
      *        tanto :
      *        si el VEN 'current' esta incompleto suma su CAPITAL al SC
      *        porque al estar incompleto no debe rebajarse el SCPA con
      *        CAPITAL
               IF ( OPC-VAL-SCRE IN OPC NOT > ZEROES ) AND
                  ( ( VEN-IND-CALC IN VEN = 'N'    ) AND
                    ( VEN-VAL-FINA IN VEN > ZEROES ) )
                   MOVE VEN-NUM-IDLC IN VEN TO VEN-IDLC
                   PERFORM LOD-RDLC
                   IF VEN-STAT-OKS
                       ADD VEN-VAL-CAPI IN VEN TO DLC-SCPA(VEN-IDLC)
                       GO TO FIN-INI-VEN
                   ELSE
                       GO TO FIN-INI-VEN
               ELSE
                   GO TO FIN-INI-VEN.
      *
      *Calcula numero de vencimientos cancelados
           IF OPC-NUM-TVCC IN OPC NOT > ZEROES
               ADD OPC-NUM-VCCO IN OPC
                   OPC-NUM-VCCM IN OPC
                   OPC-NUM-VCCV IN OPC GIVING VEN-VCAN
           ELSE
               MOVE OPC-NUM-TVCC IN OPC TO VEN-VCAN.
      *
      *Si estan todos los vencimientos cancelados resta 1 al numero de
      *vencimientos cancelados como un 'truco' para no considerarlos
      *posteriormente
           IF OPC-NUM-VCVG IN OPC NOT > ZEROES
               SUBTRACT 1 FROM VEN-VCAN.
      *
      *Parrafo para obtener las estructuras de vencimiento, en VEN-EAUX
      *retorna el numero de la estructura que genero el vencimiento actu
           PERFORM OBT-EVC.
           IF NOT VEN-STAT-OKS
               GO TO FIN-INI-VEN.
           IF VEN-EAUX = 0
               MOVE VEN-SEFL TO VEN-STAT
               GO TO FIN-INI-VEN.
      *
      *Si desea un vencimiento anterior al actual
           IF VEN-IVAS-ANT
      *        llama a parrafo ANT-EVC para
      *        decrementar todas las estructuras salvo aquellas que dan
      *        origen al vencimiento o las que estan totalmente cancelad
               PERFORM ANT-EVC
      *        Si el VEN original esta completo suma su CAPITAL al SCPA,
      *        porque al estar completo se rebajo el SCPA con dicho CAPI
               IF ( VEN-IND-CALC IN VEN NOT = 'N' ) OR
                  ( ( VEN-IND-CALC IN VEN = 'N'    ) AND
                    ( VEN-VAL-FINA IN VEN NOT > ZEROES ) )
                   MOVE VEN-NUM-IDLC IN VEN TO VEN-IDLC
                   PERFORM LOD-RDLC
                   IF VEN-STAT-OKS
                       ADD VEN-VAL-CAPI IN VEN TO DLC-SCPA(VEN-IDLC)
                   ELSE
                       GO TO FIN-INI-VEN.

      *
      *Busca codigo de tabla asociado a CIC= CAM-COD-MNAC IN CAM-VARI.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR' TO TAB-CIC-TTAB IN TAB.
           MOVE CAM-COD-MNAC IN CAM-VARI TO TAB-CIC-CTAB IN TAB.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'CICDOC PAGNX' TO VEN-STAT
               GO TO FIN-INI-VEN.
           MOVE TAB-COD-CTAB IN TAB TO CIC-VLR-MNAC.

      *Busca codigo de tabla asociado a CIC='MIPL'
           MOVE FIO-GET-KEY    TO FIO-CMND.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE 'COL'          TO FIO-SIST.
           MOVE 'MIPL' TO MSC-CIC-TMSC IN MSC.
           MOVE 'P'    TO MSC-CIC-CMSC IN MSC.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE 'CICMIPLPNX' TO VEN-STAT
               GO TO FIN-INI-VEN.
           MOVE MSC-COD-CMSC IN MSC TO CIC-MIPL-P.

      *Carga Tipo de Tasa de Interes INT-TIPT
           MOVE OPC-CAI-IOPC IN OPC TO ICG-CAI-IOPC IN ICG.
           MOVE OPC-IIC-IOPC IN OPC TO ICG-IIC-IOPC IN ICG.
           MOVE ZEROES              TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES              TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-CAI-IOPC IN ICG = OPC-CAI-IOPC IN OPC AND
                    ICG-IIC-IOPC IN ICG = OPC-IIC-IOPC IN OPC AND
                    ICG-NUM-ICAN IN ICG = ZEROES AND
                    ICG-CIC-IOPV IN ICG = SPACES AND
                    ICG-NUM-IVCT IN ICG = ZEROES AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               MOVE 'ICG    NEX' TO VEN-STAT
               GO TO FIN-INI-VEN.
           MOVE ICG-COD-TINT IN ICG TO INT-TIPT.
           IF INT-CVTI NOT > SPACES
               MOVE ICG-SGV-TINT IN ICG TO INT-TINT
           ELSE
               MOVE ICG-SGV-SPRD IN ICG TO INT-TINT.

      *Busca codigo de como opera
           MOVE SPACES TO CIC-OPEC.
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    TOCNX' TO VEN-STAT
               GO TO FIN-INI-VEN
           ELSE
               MOVE 'COL'               TO MSC-COD-SIST FIO-SIST
               MOVE 'OPEC'              TO MSC-COD-TMSC IN MSC
               MOVE TOC-MSC-OPEC IN TOC TO MSC-COD-CMSC IN MSC
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM GNS-FIO-MSC
               IF NOT FIO-STAT-OKS
                   MOVE 'VEN    OPENX'      TO VEN-STAT
                   GO TO FIN-INI-VEN
               ELSE
                   MOVE MSC-CIC-CMSC IN MSC TO CIC-OPEC.
       FIN-INI-VEN.
           EXIT.

       FIN-VEN SECTION.
       INI-FIN-VEN.
           IF NOT VEN-STAT-OKS
               GO TO FIN-FIN-VEN.
      *INI Obtener Fecha Inicio Mora para  el vencimiento
           MOVE EVC-DFVC(VEN-IEVC) TO FEC-ITM1.
           MOVE EVC-MFVC(VEN-IEVC) TO FEC-ITM2.
           MOVE EVC-SFVC(VEN-IEVC) TO FEC-ITM3.
           MOVE EVC-AFVC(VEN-IEVC) TO FEC-ITM4.
           IF EVC-DESD(VEN-IEVC) > 0
               MOVE EVC-DESD(VEN-IEVC) TO FEC-NDIA
               MOVE FEC-FORM-FEC       TO FEC-FORM
               MOVE FEC-SUM-DIA        TO FEC-CMND
               PERFORM CAL-FEC
               IF NOT FEC-STAT-OKS
                   MOVE 'VENFIMVER+' TO VEN-STAT
                   GO TO FIN-FIN-VEN.


       IMV-FIN-VEN.
           MOVE FEC-ITM1 TO VEN-DIMV.
           MOVE FEC-ITM2 TO VEN-MIMV.
           MOVE FEC-ITM3 TO VEN-SIMV.
           MOVE FEC-ITM4 TO VEN-AIMV.
      *FIN Obtener Fecha Inicio Mora
      *Lee el documento y la estructura de vencimiento asociado al
      *vencimiento encontrado
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           IF EVC-IDLC(VEN-IEVC) NOT > 0
               MOVE EVC-IVCT(VEN-IEVC) TO DLC-NUM-IDLC IN DLC
           ELSE
               MOVE EVC-IDLC(VEN-IEVC) TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE VEN-SDFL TO VEN-STAT
               GO TO FIN-FIN-VEN.
           MOVE OPC-CIC-IOPC IN OPC TO EVC-CIC-IOPC IN EVC.
           MOVE EVC-IEVC(VEN-IEVC)  TO EVC-NUM-IEVC IN EVC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT FIO-STAT-OKS
               MOVE VEN-SEFL TO VEN-STAT
               GO TO FIN-FIN-VEN.
           IF VEN-IVAS-ANT
               PERFORM ANT-CAN
           ELSE
               GO TO CON-FIN-VEN.
      *
      *Si el VEN generado esta incompleto con FINA > 0, suma el capital
      *SCPA, ya que:
      *Caso 1) hacia atras    : al cancelar dicho VEN el CAPI se rebajo
      *                         SCPA, y ahora debe sumarse al SCPA
      *((Caso 2) hacia adelante : para calcular intereses el CAPI se reb
      *                         del SCPA, y ahora debe sumarse al SCPA))
           IF ( VEN-CALC = 'N' ) AND ( VEN-FINA > ZEROES )
               PERFORM OBT-DOC
               IF NOT VEN-STAT-OKS
                   GO TO FIN-VEN
               ELSE
               IF EVC-IDLC(VEN-IEVC) NOT > 0
                   MOVE EVC-IVCT(VEN-IEVC) TO VEN-I
                   ADD VEN-VKCC TO DLC-SCPA(VEN-I)
               ELSE
                   MOVE EVC-IDLC(VEN-IEVC) TO VEN-I
                   ADD VEN-VKCC TO DLC-SCPA(VEN-I).
       CON-FIN-VEN.
      *
      *Si pidio un vencimiento anterior las estructuras que no generan
      *el vencimiento pedido deben ser proyectadas nuevamente, para
      *que la fecha de proximo vencimiento quede siempre adelantada
           IF VEN-IVAS-ANT
               PERFORM RST-FPVC.
      *
       FIN-FIN-VEN.
           MOVE RCV-IRCV IN RCV-VRBL TO RCV-KEY-IRCV IN RCV.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           MOVE ICG-IICG IN ICG-VRBL TO ICG-KEY-IICG IN ICG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-ICG.
       EXT-FIN-VEN.
           EXIT.

       OBT-EVC SECTION.
       INI-OBT-EVC.
      *Pone en 0 indice de estructuras concurrentes
           MOVE 0 TO VEN-IVCC.
      *Lee estructuras de vencimiento
           MOVE 0 TO VEN-IEVC.
           MOVE VEN-CIC-IOPC IN VEN TO EVC-CIC-IOPC IN EVC.
           MOVE 1                   TO EVC-NUM-IEVC IN EVC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT ( FIO-STAT-OKS AND
                    EVC-CIC-IOPC IN EVC = OPC-CIC-IOPC IN OPC )
               MOVE VEN-SEFL TO VEN-STAT
               GO TO FIN-OBT-EVC.
       LOOP-OBT-EVC.
      *Si pidio un vencimiento anterior y no ha cancelado ningun vencimi
      *de la estructura y el vencimiento actual no pertenece a dicha est
      * o bien si
      *Se pidio un vencimiento siguiente y los vencimientos de la estruc
      *estan todos cancelados entonces
      *la estructura NO se considera
           IF ( VEN-IVAS-ANT AND EVC-NUM-VCAN IN EVC = 0 AND
                EVC-NUM-IEVC IN EVC NOT = VEN-NUM-IEVC IN VEN ) OR
              ( VEN-IVAS-SIG AND
                EVC-NUM-VIGU IN EVC = EVC-NUM-VCAN IN EVC )
               GO TO NXT-OBT-EVC.
      *
      *Suma el contador de estructuras y valida contra el limte
           ADD 1 TO VEN-IEVC.
           IF VEN-IEVC > 10
               MOVE VEN-SEML TO VEN-STAT
               GO TO FIN-OBT-EVC.
      *
      *Guarda los valores de la estructura en EVC-VARI
           MOVE EVC-NUM-IEVC IN EVC TO EVC-IEVC(VEN-IEVC).
      *
      *     marca la estructura que genero el vencimiento actual (grabad
      *     IF EVC-NUM-IEVC IN EVC = VEN-NUM-IEVC IN VEN
      *         MOVE VEN-IEVC TO VEN-EAUX.
      *    marca mas abajo considerando que puede haber sido generado
      *    por varias estructuras
      *
           MOVE EVC-NUM-IVCT IN EVC TO EVC-IVCT(VEN-IEVC).
           MOVE EVC-NUM-VIGU IN EVC TO EVC-VIGU(VEN-IEVC).
           MOVE EVC-NUM-VCAN IN EVC TO EVC-VCAN(VEN-IEVC).
           MOVE EVC-NUM-SEVE IN EVC TO EVC-SEVE(VEN-IEVC).
           MOVE EVC-IND-UTSV IN EVC TO EVC-UTSV(VEN-IEVC).
           MOVE EVC-NUM-DPVC IN EVC TO EVC-DPVC(VEN-IEVC).
           MOVE EVC-NUM-MPVC IN EVC TO EVC-MPVC(VEN-IEVC).
           MOVE EVC-NUM-SPVC IN EVC TO EVC-SPVC(VEN-IEVC).
           MOVE EVC-NUM-APVC IN EVC TO EVC-APVC(VEN-IEVC).
      *
      *La fecha de proximo vencimiento no considera analisis de feriado,
      *por lo tanto ahora calcula EVC-FFVC
           MOVE EVC-DPVC(VEN-IEVC) TO FEC-ITM1.
           MOVE EVC-MPVC(VEN-IEVC) TO FEC-ITM2.
           MOVE EVC-SPVC(VEN-IEVC) TO FEC-ITM3.
           MOVE EVC-APVC(VEN-IEVC) TO FEC-ITM4.
           IF OPC-IND-AFER IN OPC = 'N'
               MOVE EVC-FPVC(VEN-IEVC) TO EVC-FFVC(VEN-IEVC)
               GO TO CON-OBT-EVC.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
               MOVE FEC-SIG-HBL  TO FEC-CMND
               MOVE FEC-FORM-FEC TO FEC-FORM
      *       PERFORM PRO-FEC.
              PERFORM CAL-FEC.
           MOVE FEC-ITM1 TO EVC-DFVC(VEN-IEVC).
           MOVE FEC-ITM2 TO EVC-MFVC(VEN-IEVC).
           MOVE FEC-ITM3 TO EVC-SFVC(VEN-IEVC).
           MOVE FEC-ITM4 TO EVC-AFVC(VEN-IEVC).
       CON-OBT-EVC.
      *Carga las estructuras que dieron origen al vencimiento
           IF EVC-FFVC(VEN-IEVC) = VEN-FVEN
               ADD 1 TO VEN-IVCC
               IF VEN-IVCC NOT > VEN-LVCC
                   MOVE VEN-IEVC TO VEN-VVCC(VEN-IVCC)
                   IF VEN-IVCC = 1
                       MOVE VEN-IEVC TO VEN-EAUX
                   ELSE
                       NEXT SENTENCE
               ELSE
                   MOVE 'VEN    CCOVF' TO VEN-STAT
                   GO TO EXT-OBT-EVC.
      *
           MOVE EVC-NUM-DESD IN EVC TO EVC-DESD(VEN-IEVC).
           MOVE EVC-VAL-VENC IN EVC TO EVC-VENC(VEN-IEVC).
           MOVE EVC-IND-CAUT IN EVC TO EVC-CAUT(VEN-IEVC).
           MOVE EVC-VAL-VCVF IN EVC TO EVC-VCVF(VEN-IEVC).
      *
      *Busca CIC en tabla asociado a Codigo=EVC-MSC-TCVE IN EVC
           MOVE 'COL'       TO FIO-SIST.
           MOVE FIO-GET-KEY TO FIO-CMND.
           MOVE 'CVC'               TO TAB-COD-TTAB IN TAB.
           MOVE EVC-MSC-TCVE IN EVC TO TAB-COD-CTAB IN TAB.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'CICCVC NEX' TO VEN-STAT
               GO TO FIN-OBT-EVC.
           MOVE TAB-CIC-CTAB IN TAB TO EVC-TCVE(VEN-IEVC).
           IF EVC-TCVE(VEN-IEVC) = 'IF'
               IF ( AYF-ITCV = 'FI' ) OR ( AYF-ITCV NOT > SPACES )
                   MOVE 'FI' TO AYF-ITCV
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE EVC-TCVE(VEN-IEVC) TO AYF-ITCV.
      *
           MOVE EVC-NUM-DPVE IN EVC TO EVC-DPVE(VEN-IEVC).
           MOVE EVC-NUM-IDLC IN EVC TO EVC-IDLC(VEN-IEVC).
      *
      *INI Obtener Juliano para Fecha FINAL de Vencimiento
           MOVE EVC-DFVC(VEN-IEVC) TO FEC-ITM1.
           MOVE EVC-MFVC(VEN-IEVC) TO FEC-ITM2.
           MOVE EVC-SFVC(VEN-IEVC) TO FEC-ITM3.
           MOVE EVC-AFVC(VEN-IEVC) TO FEC-ITM4.
           MOVE FEC-FORM-FEC       TO FEC-FORM.
           MOVE FEC-RET-JUL        TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE VEN-SEFL TO VEN-STAT
               GO TO FIN-OBT-EVC.
           MOVE FEC-FJUL TO EVC-JPVC(VEN-IEVC).
      *FIN Obtener Juliano para Fecha FINAL de Vencimiento
      *
       NXT-OBT-EVC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF FIO-STAT-OKS AND EVC-CIC-IOPC IN EVC = OPC-CIC-IOPC IN OPC
               GO TO LOOP-OBT-EVC.
       FIN-OBT-EVC.
      *Redundante preguntar por los dos
           IF VEN-IEVC = 0 OR VEN-IVCC = 0
               MOVE VEN-SEFL TO VEN-STAT
           ELSE
               MOVE VEN-IEVC TO VEN-EMAX
               MOVE VEN-IVCC TO VEN-MVCC.
       EXT-OBT-EVC.
           EXIT.


       ANT-EVC SECTION.
       INI-ANT-EVC.
           MOVE 1 TO VEN-IEVC.
       LU1-ANT-EVC.
           IF EVC-VIGU(VEN-IEVC) = EVC-VCAN(VEN-IEVC)
               GO TO NXT-ANT-EVC.
           MOVE 1 TO VEN-IVCC.
      *
       LU2-ANT-EVC.
           IF VEN-VVCC(VEN-IVCC) = VEN-IEVC
               GO TO NXT-ANT-EVC.
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO LU2-ANT-EVC.
      *
           PERFORM OBT-FYJ.
       NXT-ANT-EVC.
           ADD 1 TO VEN-IEVC.
           IF VEN-IEVC NOT > VEN-EMAX
               GO TO LU1-ANT-EVC.
       FIN-ANT-EVC.
           EXIT.

       INC-VEN SECTION.
       INI-INC-VEN.
      *Incrementa estructura nro. VEN-IEVC ( variando segun VEN-VVCC() )
           PERFORM INC-STR.
      *Obtiene la estructura siguiente a vencer ( el minimo juliano )
           PERFORM MIN-STR.
           IF NOT VEN-STAT-OKS
               GO TO FIN-INC-VEN.
           IF VEN-FECH = 'F'
               GO TO FIN-INC-VEN.
      *Realiza un LUP para todos los vencimientos concurrentes guardados
      *en el vector VEN-VVCC usando la variable VEN-IEVC ( toma primero
      *la estructura que tiene FO si existe ).
           MOVE 0 TO VEN-VKCC VEN-SKCC.
           MOVE 0 TO VEN-VFCC VEN-SFCC.
           MOVE 'S' TO AYF-CALCULAR-INTERES.
           MOVE 'S' TO AYF-SZRO.

           IF VEN-IXFO > 1
               MOVE VEN-IXFO TO VEN-IVCC
               PERFORM CAL-VVCC
               IF NOT VEN-STAT-OKS
                   GO TO FIN-INC-VEN
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE 0 TO VEN-IXFO.

           MOVE 1 TO VEN-IVCC.
       LUP-INC-VEN.
           PERFORM CAL-VVCC.
           IF NOT VEN-STAT-OKS
               GO TO FIN-INC-VEN.
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC = VEN-IXFO
               ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO LUP-INC-VEN.

      *Revisa si todos los SCPA estan en 0, eso significa que el
      *credito se pago antes de esta cuota por lo tanto implica STAT=EOF
           IF AYF-SZRO = 'S'
               MOVE VEN-SEOF TO VEN-STAT
               GO TO FIN-INC-VEN.
      *Mueve los valores acumulados para los vencimientos concurrentes
      *a los valores de resultado
           MOVE VEN-VKCC TO VEN-CAPI.
           MOVE VEN-SKCC TO VEN-SCAP.
           MOVE VEN-VFCC TO VEN-FINA.
           MOVE VEN-SFCC TO VEN-SFIN.
      *
      *Actualiza el Juliano de la amortizacion del Documento Y
      *Mueve la fecha del vencimiento a la fecha de amortizacion del
      *documento ( Recordar que existen por cada 'vencimiento' dos fecha
      *la FFVC que es la fecha del vencimiento sin analisis de feriado y
      *la FPVC que es la fecha del vencimiento mas analisis de feriado )
           MOVE 1 TO VEN-IVCC.
       AMT-INC-VEN.
           MOVE VEN-VVCC(VEN-IVCC) TO VEN-IEVC.
           IF EVC-TCVE(VEN-IEVC) = 'IO' OR 'CO' OR 'FO' OR 'CE'
                                    OR 'FP'
               IF EVC-IDLC(VEN-IEVC) = 0
                   MOVE EVC-IVCT(VEN-IEVC) TO VEN-IDLC
                   MOVE AYF-JAMT TO DLC-JAMT(VEN-IDLC)
                   MOVE EVC-DFVC(VEN-IEVC) TO DLC-DAMT(VEN-IDLC)
                   MOVE EVC-MFVC(VEN-IEVC) TO DLC-MAMT(VEN-IDLC)
                   MOVE EVC-SFVC(VEN-IEVC) TO DLC-SAMT(VEN-IDLC)
                   MOVE EVC-AFVC(VEN-IEVC) TO DLC-AAMT(VEN-IDLC)
               ELSE
                   MOVE EVC-IDLC(VEN-IEVC) TO VEN-IDLC
                   MOVE AYF-JAMT TO DLC-JAMT(VEN-IDLC)
                   MOVE EVC-DFVC(VEN-IEVC) TO DLC-DAMT(VEN-IDLC)
                   MOVE EVC-MFVC(VEN-IEVC) TO DLC-MAMT(VEN-IDLC)
                   MOVE EVC-SFVC(VEN-IEVC) TO DLC-SAMT(VEN-IDLC)
                   MOVE EVC-AFVC(VEN-IEVC) TO DLC-AAMT(VEN-IDLC).
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO AMT-INC-VEN.
      *
           MOVE VEN-VVCC(1) TO VEN-IEVC.
       FIN-INC-VEN.
           EXIT.

       CAL-VVCC SECTION.
       INI-CAL-VVCC.
           MOVE VEN-VVCC(VEN-IVCC) TO VEN-IEVC.
      *Obtiene documento legal credito y calcula juliano para fec.amorti
           PERFORM OBT-DOC.
      *Obtiene el VEN con numero de vencimiento EVC-IVCT(VEN-IEVC)
           PERFORM OBT-AYF.
           IF NOT VEN-STAT-OKS
               GO TO FIN-CAL-VVCC.
      *Suma el valor del capital del vencimiento al valor ya amortizado
           IF AYF-TCVE = 'IO' OR 'CO' OR 'FO' OR 'CE' OR 'FP'
               ADD VEN-CAPI TO AYF-SCYA.
           IF AYF-IULT-YES AND CIC-OPEC = 'B'
               SUBTRACT VEN-CAPI FROM VEN-FINA
               MOVE VEN-FINA TO VEN-SFIN.
      *Acumula los valores de resultado en los valores para vencimientos
      *concurrentes
           ADD VEN-CAPI TO VEN-VKCC.
           ADD VEN-SCAP TO VEN-SKCC.
           ADD VEN-FINA TO VEN-VFCC.
           ADD VEN-SFIN TO VEN-SFCC.
       FIN-CAL-VVCC.
           EXIT.

       DEC-VEN SECTION.
       INI-DEC-VEN.
           PERFORM DEC-STR.
           PERFORM MAX-STR.
       FIN-DEC-VEN.
           EXIT.

       SIG-CAN SECTION.
       INI-SIG-CAN.
           MOVE SPACES TO VEN-SCTB VEN-SCBZ.
           PERFORM SIG-ANT-CAN-FST.
       LUP-SIG-CAN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = RCV-CIC-IOPV IN RCV AND
                    VEN-NUM-IVEN IN VEN = RCV-NUM-IVCT IN RCV )
               GO TO FIN-SIG-CAN.
           SUBTRACT RCV-SGV-CAPI IN RCV FROM VEN-SCAP.
           MOVE     RCV-SGV-INTE IN RCV TO   VEN-IDNI.
           SUBTRACT RCV-SGV-FINA IN RCV FROM VEN-SFIN.
           PERFORM SIG-ANT-CAN-NXT.
           GO TO LUP-SIG-CAN.
       FIN-SIG-CAN.
           EXIT.

       ANT-CAN SECTION.
       INI-ANT-CAN.
           MOVE SPACES TO VEN-SCTB VEN-SCBZ.
           MOVE ZEROES TO VEN-CAPI VEN-SCAP
                          VEN-FINA VEN-SFIN VEN-VKCC.
           PERFORM SIG-ANT-CAN-FST.
       LUP-ANT-CAN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = RCV-CIC-IOPV IN RCV AND
                    VEN-NUM-IVEN IN VEN = RCV-NUM-IVCT IN RCV )
               GO TO FIN-ANT-CAN.
           IF RCV-SGV-CAPI IN RCV < ZEROES OR
              RCV-SGV-FINA IN RCV < ZEROES OR
              RCV-MSC-STAT IN RCV = 'R'
               GO TO NXT-ANT-CAN.
      *Usa VEN-VKCC solo como auxiliar, para usarlo en actualizacion de
           ADD RCV-SGV-CAPI IN RCV TO VEN-VKCC.
           IF RCV-MSC-STAT IN RCV = 'F'
               ADD RCV-SGV-CAPI IN RCV TO VEN-CAPI
               MOVE 'N'                TO VEN-CALC
           ELSE
           IF RCV-MSC-STAT IN RCV = 'C'
               ADD RCV-SGV-FINA IN RCV TO VEN-FINA
               MOVE 'N'                TO VEN-CALC
           ELSE
               ADD RCV-SGV-CAPI IN RCV TO VEN-CAPI
               ADD RCV-SGV-FINA IN RCV TO VEN-FINA
               MOVE SPACES             TO VEN-CALC.
           MOVE RCV-SGV-INTE IN RCV TO VEN-IDNI.
       NXT-ANT-CAN.
           PERFORM SIG-ANT-CAN-NXT.
           GO TO LUP-ANT-CAN.
       FIN-ANT-CAN.
           EXIT.

       SIG-ANT-CAN-FST SECTION.
       INI-SIG-ANT-CAN-FST.
      *Busca primera cancelacion para VEN
           MOVE 'RCV-KEY-IVCT' TO FIO-AKEY.
           MOVE OPC-CIC-IOPC IN OPC TO RCV-CIC-IOPV IN RCV.
           MOVE VEN-NUM-IVEN IN VEN TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
       FIN-SIG-ANT-CAN-FST.
           EXIT.

       SIG-ANT-CAN-NXT SECTION.
       INI-SIG-ANT-CAN-NXT.
           MOVE RCV-IND-SCTB IN RCV TO VEN-SCTB.
           MOVE RCV-IND-SCBZ IN RCV TO VEN-SCBZ.
      *Busca siguiente cancelacion para VEN
           MOVE 'RCV-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-RCV.
       FIN-SIG-ANT-CAN-NXT.
           EXIT.

       RST-FPVC SECTION.
       INI-RST-FPVC.
           MOVE 1 TO VEN-I.
       LU1-RST-FPVC.
           IF EVC-VCAN(VEN-I) NOT > 0
               GO TO NXT-RST-FPVC.
           IF EVC-VIGU(VEN-I) = EVC-VCAN(VEN-I)
               GO TO NXT-RST-FPVC.
           MOVE 1 TO VEN-IVCC.
      *
       LU2-RST-FPVC.
           IF VEN-VVCC(VEN-IVCC) = VEN-I
               GO TO NXT-RST-FPVC.
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO LU2-RST-FPVC.
      *Respalda VEN-IEVC
           MOVE VEN-IEVC TO VEN-AUXI.
      *Carga VEN-IEVC y VEN-IVAS ( antes de llamar a OBT-FYJ )
           MOVE VEN-I TO VEN-IEVC.
           MOVE 'S'   TO VEN-IVAS.
           PERFORM OBT-FYJ.
      *Restaura VEN-IEVC y VEN-IVAS
           MOVE VEN-AUXI TO VEN-IEVC.
           MOVE 'A' TO VEN-IVAS.
       NXT-RST-FPVC.
           ADD 1 TO VEN-I.
           IF VEN-I NOT > VEN-EMAX
               GO TO LU1-RST-FPVC.
       FIN-RST-FPVC.
           EXIT.

       INC-STR SECTION.
       INI-INC-STR.
           ADD 1 TO VEN-VCAN.
           MOVE 1 TO VEN-IVCC.
       LUP-INC-STR.
           MOVE VEN-VVCC(VEN-IVCC) TO VEN-IEVC.
      *Va a incrementar estructura: suma 1 al numero de vencimientos
      *cancelados, si no quedan todos cancelados llama a OBT-FYJ
           ADD 1 TO EVC-VCAN(VEN-IEVC).
           IF EVC-VIGU(VEN-IEVC) NOT = EVC-VCAN(VEN-IEVC)
               PERFORM OBT-FYJ.
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO LUP-INC-STR.
       FIN-INC-STR.
           EXIT.

       MIN-STR SECTION.
       INI-MIN-STR.
           MOVE 0          TO VEN-EAUX.
           MOVE 0          TO VEN-IVCC.
           MOVE 0          TO VEN-IXFO.
           MOVE 0          TO VEN-NEVF VEN-NEVK.
           MOVE 1          TO VEN-IEVC.
           MOVE '99999999' TO VEN-FMIN.
       LOOP-MIN-STR.
      *Si la estructura esta toda cancelada no la considera, esto es
      *porque al ir proyectando existen estructuras que se van terminand
           IF EVC-VIGU(VEN-IEVC) = EVC-VCAN(VEN-IEVC)
               GO TO NXT-MIN-STR.

           IF ( EVC-FFVC(VEN-IEVC) < VEN-FMIN OR
                EVC-FFVC(VEN-IEVC) = VEN-FMIN ) AND
                EVC-TCVE(VEN-IEVC) = ( 'FO' OR 'FP' ) AND
                VEN-IXFO > 0
               GO TO NXT-MIN-STR.

           IF EVC-FFVC(VEN-IEVC) < VEN-FMIN
               MOVE EVC-FFVC(VEN-IEVC) TO VEN-FMIN
               MOVE VEN-IEVC           TO VEN-EAUX
               MOVE 1 TO VEN-IVCC
               MOVE VEN-IEVC TO VEN-VVCC(VEN-IVCC)
               MOVE 0        TO VEN-NEVF VEN-NEVK
               PERFORM ANL-TCV
               IF EVC-TCVE(VEN-IEVC) = ( 'FO' OR 'FP' )
                   MOVE VEN-IVCC TO VEN-IXFO
               ELSE
                   MOVE 0 TO VEN-IXFO
           ELSE
           IF EVC-FFVC(VEN-IEVC) = VEN-FMIN
               ADD 1 TO VEN-IVCC
               IF VEN-IVCC NOT > VEN-LVCC
                   MOVE VEN-IEVC TO VEN-VVCC(VEN-IVCC)
                   PERFORM ANL-TCV
                   IF EVC-TCVE(VEN-IEVC) = ( 'FO' OR 'FP' )
                       MOVE VEN-IVCC TO VEN-IXFO
                   ELSE
                       NEXT SENTENCE
               ELSE
                   MOVE 'VEN    CCOVF' TO VEN-STAT
                   GO TO EXT-MIN-STR.

       NXT-MIN-STR.
           ADD 1 TO VEN-IEVC.
           IF VEN-IEVC NOT > VEN-EMAX
               GO TO LOOP-MIN-STR.
       FIN-MIN-STR.
      *Carga variable con el total de los vencimientos concurrentes
           MOVE VEN-IVCC TO VEN-MVCC.
      *
      *Pregunta si no encontro minima estructura ( todas canceladas ) mu
      *status SENO
      *en caso contrario
      *carga el VEN-IEVC con el numero de la estructura minima y
      *se suma 1 a los cancelados de la operacion para obtener el siguie
      *numero de vencimiento que se carga en la estructura que correspon
           IF VEN-EAUX = 0
               MOVE VEN-SENO TO VEN-STAT
               GO TO EXT-MIN-STR.
      *Suma 1 al numero de vencimiento
           ADD 1 TO VEN-NVEN.
      *Recorre todos los vencimientos concurrentes
           MOVE 1 TO VEN-IVCC.
       CAN-MIN-STR.
           MOVE VEN-VVCC(VEN-IVCC) TO VEN-IEVC.
           MOVE VEN-NVEN TO EVC-IVCT(VEN-IEVC).
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO CAN-MIN-STR.
      *
           MOVE VEN-EAUX TO VEN-IEVC.
       EXT-MIN-STR.
           EXIT.

       ANL-TCV SECTION.
       INI-ANL-TCV.
           IF EVC-BCCR(VEN-IEVC) = 'F' OR EVC-VICR(VEN-IEVC) = 'F'
               ADD 1 TO VEN-NEVF
           ELSE
               ADD 1 TO VEN-NEVK.
       FIN-ANL-TCV.
           EXIT.

       DEC-STR SECTION.
       INI-DEC-STR.
           SUBTRACT 1 FROM VEN-VCAN.
           MOVE 1 TO VEN-IVCC.
       LUP-DEC-STR.
           MOVE VEN-VVCC(VEN-IVCC) TO VEN-IEVC.
           IF EVC-VCAN(VEN-IEVC) > 0
               PERFORM OBT-FYJ.
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO LUP-DEC-STR.
       FIN-DEC-STR.
           EXIT.

       MAX-STR SECTION.
       INI-MAX-STR.
           MOVE 0 TO VEN-EAUX.
           MOVE 0 TO VEN-IVCC.
           MOVE 0 TO VEN-IXFO.
           MOVE 0 TO VEN-NEVF VEN-NEVK.
           MOVE 1 TO VEN-IEVC.
           MOVE 0 TO VEN-FMAX.
       LOOP-MAX-STR.
           IF EVC-VCAN(VEN-IEVC) NOT > 0
               GO TO NXT-MAX-STR.

           IF ( EVC-FFVC(VEN-IEVC) > VEN-FMAX OR
                EVC-FFVC(VEN-IEVC) = VEN-FMAX ) AND
                EVC-TCVE(VEN-IEVC) = ( 'FO' OR 'FP' ) AND
                VEN-IXFO > 0
               GO TO NXT-MAX-STR.

           IF EVC-FFVC(VEN-IEVC) > VEN-FMAX
               MOVE EVC-FFVC(VEN-IEVC) TO VEN-FMAX
               MOVE VEN-IEVC           TO VEN-EAUX
               MOVE 1 TO VEN-IVCC
               MOVE VEN-IEVC TO VEN-VVCC(VEN-IVCC)
               MOVE 0        TO VEN-NEVF VEN-NEVK
               PERFORM ANL-TCV
               IF EVC-TCVE(VEN-IEVC) = ( 'FO' OR 'FP' )
                   MOVE VEN-IVCC TO VEN-IXFO
               ELSE
                   MOVE 0 TO VEN-IXFO
           ELSE
           IF EVC-FFVC(VEN-IEVC) = VEN-FMAX
               ADD 1 TO VEN-IVCC
               IF VEN-IVCC NOT > VEN-LVCC
                   MOVE VEN-IEVC TO VEN-VVCC(VEN-IVCC)
                   PERFORM ANL-TCV
                   IF EVC-TCVE(VEN-IEVC) = ( 'FO' OR 'FP' )
                       MOVE VEN-IVCC TO VEN-IXFO
                   ELSE
                       NEXT SENTENCE
               ELSE
                   MOVE 'VEN    CCOVF' TO VEN-STAT
                   GO TO EXT-MAX-STR.
       NXT-MAX-STR.
           ADD 1 TO VEN-IEVC.
           IF VEN-IEVC NOT > VEN-EMAX
               GO TO LOOP-MAX-STR.
       FIN-MAX-STR.
      *Carga variable con el total de los vencimientos concurrentes
           MOVE VEN-IVCC TO VEN-MVCC.
      *
      *Pregunta si no encontro maxima estructura ( todas canceladas ) mu
      *status SENO
      *en caso contrario
      *carga el VEN-IEVC con el numero de la estructura maxima y
      *se suma 1 a los cancelados de la operacion para obtener el siguie
      *numero de vencimiento que se carga en la estructura que correspon
           IF VEN-EAUX = 0
               MOVE VEN-SENO TO VEN-STAT
               GO TO EXT-MAX-STR.
      *
      *Recorre toda las etructuras de tipo 1 y actualiza FAMT con la fec
      *de la estructura maxima
           PERFORM ACT-FAMT.
           IF NOT VEN-STAT-OKS
               GO TO EXT-MAX-STR.
      *Resta 1 al numero de vencimiento
           SUBTRACT 1 FROM VEN-NVEN.
      *Recorre todos los vencimientos concurrentes
           MOVE 1 TO VEN-IVCC.
       CAN-MAX-STR.
           MOVE VEN-VVCC(VEN-IVCC) TO VEN-IEVC.
      *JSS 7-APR-1991 18:03:17
      *    ADD 1 VEN-VCAN GIVING EVC-IVCT(VEN-IEVC).
           MOVE VEN-NVEN TO EVC-IVCT(VEN-IEVC).
      *
           SUBTRACT 1 FROM EVC-VCAN(VEN-IEVC).
      *
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO CAN-MAX-STR.
      *
           MOVE VEN-EAUX TO VEN-IEVC.
       EXT-MAX-STR.
           EXIT.

       ACT-FAMT SECTION.
       INI-ACT-FAMT.
      *Recorre toda las etructuras de tipo 1 y actualiza FAMT con la fec
      *de la estructura maxima
           MOVE 0 TO VEN-FMAX.
           MOVE 1 TO VEN-IEVC.
       LUP-ACT-FAMT.
           IF EVC-VCAN(VEN-IEVC) NOT > 0
               GO TO NXT-ACT-FAMT.
           IF EVC-TCVE(VEN-IEVC) = 'IC' OR 'IF' OR 'FE'
               GO TO NXT-ACT-FAMT.
           IF EVC-FFVC(VEN-IEVC) < VEN-FMAX
               GO TO NXT-ACT-FAMT.
           MOVE EVC-FFVC(VEN-IEVC) TO VEN-FMAX.
           PERFORM OBT-DOC.
           IF NOT VEN-STAT-OKS
               GO TO FIN-ACT-FAMT.
           IF EVC-IDLC(VEN-IEVC) = 0
               MOVE EVC-IVCT(VEN-IEVC) TO VEN-I
           ELSE
               MOVE EVC-IDLC(VEN-IEVC) TO VEN-I.
           MOVE EVC-DFVC(VEN-IEVC) TO DLC-DAMT(VEN-I).
           MOVE EVC-MFVC(VEN-IEVC) TO DLC-MAMT(VEN-I).
           MOVE EVC-SFVC(VEN-IEVC) TO DLC-SAMT(VEN-I).
           MOVE EVC-AFVC(VEN-IEVC) TO DLC-AAMT(VEN-I).
       NXT-ACT-FAMT.
           ADD 1 TO VEN-IEVC.
           IF VEN-IEVC NOT > VEN-EMAX
               GO TO LUP-ACT-FAMT.
       FIN-ACT-FAMT.
           EXIT.

       OBT-FYJ SECTION.
       INI-OBT-FYJ.
      *Suma o resta periodo entre vencimiento para la estructura nro. VE
      *y deja en variable EVC-FPVC(VEN-IEVC)
           IF ( EVC-UTSV(VEN-IEVC) = 'M' OR 'A' ) AND
              EVC-DPVE(VEN-IEVC) > ZEROES
               MOVE EVC-DPVE(VEN-IEVC) TO FEC-ITM1
           ELSE
               MOVE EVC-DPVC(VEN-IEVC) TO FEC-ITM1.
           MOVE EVC-MPVC(VEN-IEVC) TO FEC-ITM2.
           MOVE EVC-SPVC(VEN-IEVC) TO FEC-ITM3.
           MOVE EVC-APVC(VEN-IEVC) TO FEC-ITM4.
           IF EVC-UTSV(VEN-IEVC) = 'D'
               MOVE EVC-SEVE(VEN-IEVC) TO FEC-NDIA
               IF VEN-IVAS-SIG
                   MOVE FEC-SUM-DIA TO FEC-CMND
               ELSE
                   MOVE FEC-RST-DIA TO FEC-CMND
           ELSE
           IF EVC-UTSV(VEN-IEVC) = 'M'
               MOVE EVC-SEVE(VEN-IEVC) TO FEC-NMES
               IF VEN-IVAS-SIG
                   MOVE FEC-SUM-MES TO FEC-CMND
               ELSE
                   MOVE FEC-RST-MES TO FEC-CMND
           ELSE
      *    Asume ano
               MOVE EVC-SEVE(VEN-IEVC) TO FEC-NANO
               IF VEN-IVAS-SIG
                   MOVE FEC-SUM-ANO TO FEC-CMND
               ELSE
                   MOVE FEC-RST-ANO TO FEC-CMND.
           MOVE FEC-FORM-FEC TO FEC-FORM.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-ITM1 TO EVC-DPVC(VEN-IEVC).
           MOVE FEC-ITM2 TO EVC-MPVC(VEN-IEVC).
           MOVE FEC-ITM3 TO EVC-SPVC(VEN-IEVC).
           MOVE FEC-ITM4 TO EVC-APVC(VEN-IEVC).
      *
      *Analiza feriado para la fecha del vencimiento
           IF OPC-IND-AFER IN OPC = 'N'
               MOVE EVC-FPVC(VEN-IEVC) TO EVC-FFVC(VEN-IEVC)
               GO TO CON-OBT-FYJ.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
               MOVE FEC-SIG-HBL  TO FEC-CMND
               MOVE FEC-FORM-FEC TO FEC-FORM
      *        PERFORM PRO-FEC.
               PERFORM CAL-FEC.
           MOVE FEC-ITM1 TO EVC-DFVC(VEN-IEVC).
           MOVE FEC-ITM2 TO EVC-MFVC(VEN-IEVC).
           MOVE FEC-ITM3 TO EVC-SFVC(VEN-IEVC).
           MOVE FEC-ITM4 TO EVC-AFVC(VEN-IEVC).
       CON-OBT-FYJ.
      *
      *Calcula el juliano para la fecha del vencimiento
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RET-JUL  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-FJUL TO EVC-JPVC(VEN-IEVC).
       FIN-OBT-FYJ.
           EXIT.

       OBT-DOC SECTION.
       INI-OBT-DOC.
           IF NOT VEN-STAT-OKS
               GO TO FIN-OBT-DOC.
      *
      *Si la estructura no esta asociada a ningun documento mueve
      *el numero de la estructura para accesar el DLC en caso contrario
      *mueve el numero del documento que tiene asociado la estructura
           IF EVC-IDLC(VEN-IEVC) = 0
               MOVE EVC-IVCT(VEN-IEVC) TO VEN-IDLC
           ELSE
               MOVE EVC-IDLC(VEN-IEVC) TO VEN-IDLC.
           IF VEN-IDLC > 10
               MOVE VEN-SDML TO VEN-STAT
               GO TO FIN-OBT-DOC.
      *Carga el documento con numero VEN-IDLC
           PERFORM LOD-RDLC.
       FIN-OBT-DOC.
           EXIT.

       LOD-RDLC SECTION.
       INI-LOD-RDLC.
      *Revisa si el documento ya fue cargado anteriormente
           IF DLC-TDOC(VEN-IDLC) NOT = SPACES
               GO TO FIN-LOD-RDLC.
      *
      *Lee el documento y carga en variables DLC-VARI
           MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-IDLC            TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE VEN-SDFL TO VEN-STAT
               GO TO FIN-LOD-RDLC.
      *Carga codigo computacional del codigo tipo de documento del DLC
           MOVE FIO-GET-KEY    TO FIO-CMND.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'DOC'               TO TAB-COD-TTAB IN TAB.
           MOVE DLC-COD-TDOC IN DLC TO TAB-COD-CTAB IN TAB.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'CICDOC PAGNX' TO VEN-STAT
               GO TO FIN-LOD-RDLC.
           MOVE TAB-CIC-CTAB IN TAB TO DLC-TDOC(VEN-IDLC).
      *
           MOVE DLC-MSC-MIPL IN DLC TO DLC-MIPL(VEN-IDLC).
           MOVE DLC-VAL-SDLC IN DLC TO DLC-SDLC(VEN-IDLC).
           MOVE DLC-VAL-SCPA IN DLC TO DLC-SCPA(VEN-IDLC).
      *Calcula el valor de capital ya amortizado
           SUBTRACT DLC-VAL-SCPA IN DLC FROM OPC-VAL-CRED IN OPC
                    GIVING AYF-SCYA.
           MOVE DLC-VAL-SINT IN DLC TO DLC-SINT(VEN-IDLC).
           MOVE DLC-FEC-FAMT IN DLC TO DLC-FAMT(VEN-IDLC).
      *
           MOVE DLC-CAI-IOPC IN DLC TO ICG-CAI-IOPC IN ICG.
           MOVE DLC-IIC-IOPC IN DLC TO ICG-IIC-IOPC IN ICG.
           MOVE DLC-NUM-IDLC IN DLC TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES       TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES       TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'        TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES       TO ICG-FEC-FINI IN ICG.
           MOVE 'ICG-KEY-IICG ' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF FIO-STAT-OKS AND
              ICG-CAI-IOPC IN ICG = DLC-CAI-IOPC IN DLC AND
              ICG-IIC-IOPC IN ICG = DLC-IIC-IOPC IN DLC AND
              ICG-NUM-ICAN IN ICG = DLC-NUM-IDLC IN DLC AND
              ICG-CIC-IOPV IN ICG = SPACES       AND
              ICG-NUM-IVCT IN ICG = ZEROES       AND
              ICG-IND-TTAS IN ICG = 'INT'
               IF INT-CVTI NOT > SPACES
                   MOVE ICG-SGV-TINT IN ICG TO DLC-TINT(VEN-IDLC)
               ELSE
                   MOVE ICG-SGV-SPRD IN ICG TO DLC-TINT(VEN-IDLC)
           ELSE
               MOVE INT-TINT TO DLC-TINT(VEN-IDLC).
       CON-LOD-RDLC.
      *Calcula el juliano para la fecha de amortizacion
           MOVE DLC-DAMT(VEN-IDLC) TO FEC-ITM1.
           MOVE DLC-MAMT(VEN-IDLC) TO FEC-ITM2.
           MOVE DLC-SAMT(VEN-IDLC) TO FEC-ITM3.
           MOVE DLC-AAMT(VEN-IDLC) TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RET-JUL  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-FJUL TO DLC-JAMT(VEN-IDLC).
       FIN-LOD-RDLC.
           EXIT.

       OBT-AYF SECTION.
       INI-OBT-AYF.
           IF NOT VEN-STAT-OKS
               GO TO FIN-OBT-AYF.
      *
      *Mueve tipo de calculo de la estructura a calcular
           MOVE EVC-TCVE(VEN-IEVC) TO AYF-TCVE.
      *
      *Busca si el vencimiento que esta revisando es el ultimo por cance
      *considerando todo el documento ( es decir si esta cancelando con
      *cuota todo el documento : AYF-IULT = 'S' )
           PERFORM ULT-AYF.
      *
      *Si el tipo de calculo es sin intereses
           IF AYF-TCVE = 'IN'
               IF AYF-IULT-YES
      *        Si con el vencimiento actual se cancela todo el documento
      *        Mueve el saldo insoluto al capital total
      *        Suma el saldo insoluto, intereses devengados para obtener
      *        final ademas si el manejo de interes es prorrateado segun
      *        de cada cuota suma al valor final el saldo de los interes
                   ADD DLC-SCPA(VEN-IDLC) , DLC-SINT(VEN-IDLC) ,
                       EVC-VCVF(VEN-IEVC)   GIVING VEN-CAPI
                   MOVE VEN-CAPI TO VEN-SCAP VEN-FINA VEN-SFIN
                   MOVE 0 TO DLC-SCPA(VEN-IDLC)
                   MOVE 0 TO DLC-SINT(VEN-IDLC)
                   GO TO FIN-OBT-AYF
               ELSE
                   MOVE EVC-VCVF(VEN-IEVC) TO VEN-CAPI VEN-SCAP
                                              VEN-FINA VEN-SFIN
                   GO TO FIN-OBT-AYF.
      *
      *Si el tipo de documento asociado a la estructura no es letra
      *calcula en PAG-OBT-AYF
           IF ( DLC-TDOC(VEN-IDLC) NOT = CIC-TDOC-LTR ) AND
              ( NOT (  AYF-TCVE = 'IC' OR 'IF' OR 'FE' ) )
               GO TO PAG-OBT-AYF.
           MOVE 'N' TO AYF-SZRO.
      *
      **     IF AYF-TCVE = 'FE'
      ***Mueve Juliano de la amortizacion del documento ( es la fecha de
      ***vencimiento anterior de la estructura ) y Juliano del vencimien
      ***proyectado ahora
      **         MOVE DLC-JAMT(VEN-IDLC) TO AYF-JANT
      **         MOVE EVC-JPVC(VEN-IEVC) TO AYF-JAMT
      ***Calcula la separacion en dias entre los julianos
      **         SUBTRACT AYF-JANT FROM AYF-JAMT GIVING INT-SEPD
      ***Actualiza el Juliano anterior con el Juliano actual de amortiza
      **         MOVE AYF-JAMT TO AYF-JANT
      ***Actualiza el Juliano de la amortizacion del Documento
      **         MOVE AYF-JAMT TO DLC-JAMT(VEN-IDLC)
      **     ELSE
      *Obtiene separacion en dias ( INT-SEPD ) entre fecha de colocacion
      *y la fecha del vencimiento
           PERFORM OBT-SEDF.
           IF NOT VEN-STAT-OKS
               GO TO FIN-OBT-AYF.

           IF AYF-BCCR = 'F' OR AYF-VICR = 'F'
      *        Si tipo calculo es 'F*' mueve valor final de estructura
      *        a valor final y a saldo valor final del vencimiento
      *        y calcula valor capital
               MOVE EVC-VCVF(VEN-IEVC) TO VEN-FINA
                                          VEN-SFIN
               PERFORM CAL-CAPI
               IF AYF-IULT-YES
      *    Si con el vencimiento actual se cancela todo el documento
      *    Mueve el saldo insoluto al capital total
      *    Suma el saldo insoluto, intereses devengados para obtener val
      *    final ademas si el manejo de interes es prorrateado segun cap
      *    de cada cuota suma al valor final el saldo de los intereses
                   ADD DLC-SCPA(VEN-IDLC) , DLC-SINT(VEN-IDLC) ,
                       VEN-CAPI GIVING VEN-CAPI
                   MOVE VEN-CAPI TO VEN-SCAP
                   PERFORM CAL-FINA
                   MOVE 0 TO DLC-SCPA(VEN-IDLC)
                   MOVE 0 TO DLC-SINT(VEN-IDLC)
               ELSE
                   NEXT SENTENCE
           ELSE
      *        Si tipo calculo NO es 'F*' mueve valor capital de estruct
      *        a valor capital y a saldo valor capital del vencimiento
      *        y calcula valor final
               IF AYF-IULT-YES
                   ADD DLC-SCPA(VEN-IDLC) , DLC-SINT(VEN-IDLC) ,
                       EVC-VCVF(VEN-IEVC)   GIVING VEN-CAPI
                   MOVE VEN-CAPI TO VEN-SCAP
                   PERFORM CAL-FINA
                   MOVE 0 TO DLC-SCPA(VEN-IDLC)
                   MOVE 0 TO DLC-SINT(VEN-IDLC)
               ELSE
                   MOVE EVC-VCVF(VEN-IEVC) TO VEN-CAPI
                                              VEN-SCAP
                   PERFORM CAL-FINA.
           GO TO FIN-OBT-AYF.
       PAG-OBT-AYF.
      *
      *Mueve el Saldo Capital por Amortizar ( Saldo Insoluto )
           MOVE DLC-SCPA(VEN-IDLC) TO AYF-SCPA.
           IF AYF-SCPA > ZEROES
               MOVE 'N' TO AYF-SZRO.
      *
           IF AYF-BCCR = 'F' OR AYF-VICR = 'F'
      *mueve 0 al saldo de interes
      *Mueve saldo de intereses a intereses devengados no incluidos
      *Mueve el valor final de EVC al valor final
               MOVE 0                  TO AYF-SINT
               MOVE DLC-SINT(VEN-IDLC) TO AYF-IDNI
               MOVE EVC-VCVF(VEN-IEVC) TO AYF-FINA
           ELSE
      *mueve saldo de intereses al saldo de interes
      *Mueve 0 a intereses devengados no incluidos
      *Mueve el valor capital de EVC al valor capital
               MOVE DLC-SINT(VEN-IDLC) TO AYF-SINT
               MOVE 0                  TO AYF-IDNI
               MOVE EVC-VCVF(VEN-IEVC) TO AYF-CAPI.
      *
      *Mueve Juliano de la amortizacion del documento ( es la fecha del
      *vencimiento anterior de la estructura ) y Juliano del vencimiento
      *proyectado ahora
           MOVE DLC-JAMT(VEN-IDLC) TO AYF-JANT.
           MOVE EVC-JPVC(VEN-IEVC) TO AYF-JAMT.
      *
      *Busca si el vencimiento que esta revisando es el ultimo por cance
      *considerando todo el documento ( es decir si esta cancelando con
      *cuota todo el documento : AYF-IULT = 'S' )
      *     PERFORM ULT-AYF.
      *
      *Obtiene valor final, valor capital, saldo insoluto, actualiza sal
      *interes
           PERFORM CAL-AYF.
           IF NOT VEN-STAT-OKS
               GO TO FIN-OBT-AYF.
      *     MOVE EVC-DPVC(VEN-IEVC) TO DLC-DAMT(VEN-IDLC).
      *     MOVE EVC-MPVC(VEN-IEVC) TO DLC-MAMT(VEN-IDLC).
      *     MOVE EVC-SPVC(VEN-IEVC) TO DLC-SAMT(VEN-IDLC).
      *     MOVE EVC-APVC(VEN-IEVC) TO DLC-AAMT(VEN-IDLC).
      *Mueve la fecha del vencimiento a la fecha de amortizacion del
      *documento ( Recordar que existen por cada 'vencimiento' dos fecha
      *la FFVC que es la fecha del vencimiento sin analisis de feriado y
      *la FPVC que es la fecha del vencimiento mas analisis de feriado )
      *     MOVE EVC-DFVC(VEN-IEVC) TO DLC-DAMT(VEN-IDLC).
      *     MOVE EVC-MFVC(VEN-IEVC) TO DLC-MAMT(VEN-IDLC).
      *     MOVE EVC-SFVC(VEN-IEVC) TO DLC-SAMT(VEN-IDLC).
      *     MOVE EVC-AFVC(VEN-IEVC) TO DLC-AAMT(VEN-IDLC).
      *Mueve el saldo insoluto al documento
           MOVE AYF-SCPA TO DLC-SCPA(VEN-IDLC).
      *
           IF VEN-CALC = 'N'
               AND VEN-KEST NOT = 'S'
               GO TO CON-OBT-AYF.
      *
           IF AYF-BCCR = 'F' OR AYF-VICR = 'F'
      *        Si tipo de calculo es 'F*' mueve al saldo de interes del
      *        el valor de los intereses
               IF AYF-CALCULAR-INTERES-SI
                   MOVE AYF-IDNI TO DLC-SINT(VEN-IDLC)
               ELSE
                   NEXT SENTENCE
           ELSE
      *        Si tipo de calculo NO es 'F*' mueve al saldo de interes d
      *        documento el valor del saldo de interes
               MOVE AYF-SINT TO DLC-SINT(VEN-IDLC).
           IF AYF-CALCULAR-INTERES-SI
               MOVE 'N' TO AYF-CALCULAR-INTERES.
      *
      *Actualiza el Juliano de la amortizacion del Documento
      *Al haber vencimientos concurrentes debe hacerlo al final
      *     MOVE AYF-JAMT TO DLC-JAMT(VEN-IDLC).
      *
      *Mueve el valor capital al valor capital del vencimiento y al sald
      *valor capital del vencimiento,
      *mueve el saldo de los intereses al valor de los interes no inclui
      *del vencimiento, y
      *mueve el valor final al valor final del vencimiento y al saldo de
      *valor final del vencimiento
       CON-OBT-AYF.
           MOVE AYF-CAPI TO VEN-CAPI , VEN-SCAP.
           MOVE DLC-SINT(VEN-IDLC) TO VEN-IDNI.
           MOVE AYF-FINA TO VEN-FINA , VEN-SFIN.
       FIN-OBT-AYF.
           EXIT.

       OBT-SEDF SECTION.
       INI-OBT-SEDF.
      *Diferencia en dias entre OPC-FEC-FCOL y EVC-FFVC(VEN-IEVC)
           MOVE OPC-NUM-DCOL IN OPC TO FEC-DEC1.
           MOVE OPC-NUM-MCOL IN OPC TO FEC-MEC1.
           MOVE OPC-NUM-SCOL IN OPC TO FEC-SEC1.
           MOVE OPC-NUM-ACOL IN OPC TO FEC-AEC1.
           MOVE EVC-DFVC(VEN-IEVC)  TO FEC-DEC2.
           MOVE EVC-MFVC(VEN-IEVC)  TO FEC-MEC2.
           MOVE EVC-SFVC(VEN-IEVC)  TO FEC-SEC2.
           MOVE EVC-AFVC(VEN-IEVC)  TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'FCOLPVDFDIA' TO VEN-STAT
               GO TO FIN-OBT-SEDF.
           MOVE FEC-NDIA TO INT-SEPD.
       FIN-OBT-SEDF.
           EXIT.

       CAL-CAPI SECTION.
       INI-CAL-CAPI.
      *Carga parametros para calculo de interes
           PERFORM BUS-PINT.
           PERFORM OBT-VTIN.
           IF INT-CMSG NOT > SPACES
      *Calcula valor capital = valor final / ( 1 + TIPE )
               ADD 1 TO INT-TIPE
               DIVIDE VEN-FINA BY INT-TIPE GIVING VEN-CAPI ROUNDED
               SUBTRACT 1 FROM INT-TIPE
               IF OPC-COD-VCOC IN OPC = CIC-VLR-MNAC
                   MOVE VEN-CAPI TO PES-SGV-DECI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE TO VEN-CAPI
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE INT-CMSG TO VEN-STAT
               MOVE INT-MEN2 TO VEN-MEN2
               GO TO FIN-CAL-CAPI.
           MOVE VEN-CAPI TO VEN-SCAP.
       FIN-CAL-CAPI.
           EXIT.

       CAL-FINA SECTION.
       INI-CAL-FINA.
      *Carga parametros para calculo de interes
           PERFORM BUS-PINT.
           PERFORM OBT-VTIN.
           IF INT-CMSG NOT > SPACES
      *Calcula valor final = valor capital * ( 1 + TIPE )
               ADD 1 TO INT-TIPE
               MULTIPLY VEN-CAPI BY INT-TIPE GIVING VEN-FINA ROUNDED
               SUBTRACT 1 FROM INT-TIPE
               IF OPC-COD-VCOC IN OPC = CIC-VLR-MNAC
                   MOVE VEN-FINA TO PES-SGV-DECI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE TO VEN-FINA
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE INT-CMSG TO VEN-STAT
               MOVE INT-MEN2 TO VEN-MEN2
               GO TO FIN-CAL-FINA.
           MOVE VEN-FINA TO VEN-SFIN.
       FIN-CAL-FINA.
           EXIT.

       ULT-AYF SECTION.
       INI-ULT-AYF.
      *Suma 1 a los cancelados de la estructura para efectos de la pregu
           MOVE VEN-IEVC TO VEN-EAUX.
      *
           MOVE 1 TO VEN-I.
       LU1-ULT-AYF.
           MOVE VEN-VVCC(VEN-I) TO VEN-IEVC.
           ADD 1 TO EVC-VCAN(VEN-IEVC).
           ADD 1 TO VEN-I.
           IF VEN-I NOT > VEN-MVCC
               GO TO LU1-ULT-AYF.
      *
           MOVE 1 TO VEN-IEVC.
           MOVE 'S' TO AYF-IULT.
       LOOP-ULT-AYF.
           IF EVC-IDLC(VEN-IEVC) = VEN-IDLC AND
              EVC-VCAN(VEN-IEVC) < EVC-VIGU(VEN-IEVC)
               MOVE 'N' TO AYF-IULT
           ELSE
               ADD 1 TO VEN-IEVC
               IF VEN-IEVC NOT > VEN-EMAX
                   GO TO LOOP-ULT-AYF.
       FIN-ULT-AYF.
      *
           MOVE 1 TO VEN-I.
       LU2-ULT-AYF.
           MOVE VEN-VVCC(VEN-I) TO VEN-IEVC.
           SUBTRACT 1 FROM EVC-VCAN(VEN-IEVC).
           ADD 1 TO VEN-I.
           IF VEN-I NOT > VEN-MVCC
               GO TO LU2-ULT-AYF.
      *
           MOVE VEN-EAUX TO VEN-IEVC.
       EXT-ULT-AYF.
           EXIT.

       CAL-AYF SECTION.
       INI-CAL-AYF.
      *Calcula la separacion en dias entre los julianos
           SUBTRACT AYF-JANT FROM AYF-JAMT GIVING INT-SEPD.
      *Carga parametros para obtener tasa de interes del periodo
           PERFORM BUS-PINT.
      *Actualiza el Juliano anterior con el Juliano actual de amortizaci
           MOVE AYF-JAMT TO AYF-JANT.
      *Obtiene tasa de interes del periodo TIPE
           IF ( INT-CVTI > SPACES AND VEN-FTBS > ZEROES )
               MOVE VEN-DTBS TO INT-DBTV
               MOVE VEN-MTBS TO INT-MBTV
               MOVE VEN-STBS TO INT-SBTV
               MOVE VEN-ATBS TO INT-ABTV.
           PERFORM COL-CAL-INT.
           IF INT-CMSG > SPACES
               MOVE INT-CMSG TO VEN-STAT
               MOVE INT-MEN2 TO VEN-MEN2
               GO TO FIN-CAL-AYF.
           IF INT-IETB > SPACES
               MOVE 'N' TO VEN-CALC.
      *
           ADD 1 TO INT-TIPE.
      *Calcula intereses devengados no incluidos
           IF AYF-CALCULAR-INTERES-SI
               COMPUTE AYF-IDNI ROUNDED =
                      ( AYF-SCPA + AYF-SINT ) * ( INT-TIPE - 1 ) +
                                       AYF-IDNI * INT-TIPE
      *Si la moneda del credito es nacional, redondea sin centavos
               IF OPC-COD-VCOC IN OPC = CIC-VLR-MNAC
                   MOVE AYF-IDNI TO PES-SGV-DECI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE TO AYF-IDNI
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE 0 TO AYF-IDNI.
      *
           IF AYF-IULT-YES
      *        Si con el vencimiento actual se cancela todo el documento
      *        llama a parrafo CAL-AYF-ULT
               PERFORM CAL-AYF-ULT
           ELSE
           IF AYF-BCCR = 'F' OR AYF-VICR = 'F'
      *        Si tipo de calculo es 'F*'
      *        Si la tasa es estimada entonces el valor final es
      *        el conocido ( AYF-FINA ) y el capital = 0 ( AYF-CAPI )
      *        ( KEST es un switch para indicar que devuelva el capital
      *          estimado, esta inicializado siempre en 'N' )
               IF INT-IETB > SPACES AND ( VEN-KEST NOT = 'S' )
                   MOVE 0 TO AYF-CAPI
               ELSE
      *            Analiza como es el interes respecto del valor final
                   IF AYF-IDNI > AYF-FINA
                       SUBTRACT AYF-FINA FROM AYF-IDNI
                       MOVE 0 TO AYF-CAPI
                   ELSE
                       SUBTRACT AYF-IDNI FROM AYF-FINA GIVING AYF-CAPI
      *                El capital calculado puede ser sea mayor que el
      *                resto del documento
                       IF AYF-CAPI > AYF-SCPA
                           PERFORM CAL-AYF-ULT
                       ELSE
                           MOVE 0 TO AYF-IDNI
           ELSE
      *        Si tipo de calculo NO es 'F*'
      *        Si la tasa es estimada
      *        IF INT-IETB > SPACES
               IF INT-IETB > SPACES AND ( VEN-KEST NOT = 'S' )
      *            Si existe un F* en el vencimiento que se genera
      *            se mueve al final el capital conocido y el capital en
      *            ( ya que el valor final sera el conocido : el final d
      *            f* + los capitales de las otras estructuras )
      *            en caso de no existir ningun f* el capital es el cono
      *            ( AYF-CAPI ) y el final es 0 ( AYF-FINA )
                   IF VEN-NEVF > 0
                       MOVE AYF-CAPI TO AYF-FINA
                       MOVE 0        TO AYF-CAPI
                   ELSE
                       MOVE 0        TO AYF-FINA
               ELSE
      *        Si la tasa es real
      *        El capital calculado puede ser sea mayor que el resto del
      *        documento
               IF AYF-CAPI > AYF-SCPA
                   PERFORM CAL-AYF-ULT
               ELSE
      *        Si el capital calculado no es mayor que el resto del docu
      *        Calcula valor final
      *        En el caso de interes prorrateado segun capital de cada c
      *        saca la proporcion del saldo de interes correspondiente
               ADD AYF-CAPI , AYF-IDNI GIVING AYF-FINA
               MOVE 0 TO AYF-IDNI
               IF DLC-MIPL(VEN-IDLC) = CIC-MIPL-P
                   MULTIPLY AYF-SINT BY AYF-CAPI GIVING AYF-VAUX
                   DIVIDE AYF-SCPA INTO AYF-VAUX
                   IF OPC-COD-VCOC IN OPC = CIC-VLR-MNAC
      *                Si es moneda nacional redondea sin centavos y
      *                suma la proporcion de interes al valor final
      *                y lo resta al saldo de interes
                       MOVE AYF-VAUX TO PES-SGV-DECI
                       PERFORM PES-SCTV
                       MOVE PES-SGV-ENTE TO AYF-VAUX
                       ADD AYF-VAUX TO AYF-FINA
                       SUBTRACT AYF-VAUX FROM AYF-SINT
                   ELSE
      *                suma la proporcion de interes al valor final
      *                y lo resta al saldo de interes
                       ADD AYF-VAUX TO AYF-FINA
                       SUBTRACT AYF-VAUX FROM AYF-SINT
      *
               ELSE
      *            Si el manejo del saldo de interes no es prorrateado
      *            suma el saldo de interes al valor final y
      *            deja el saldo de intereses en 0
                   ADD AYF-SINT TO AYF-FINA
                   MOVE 0 TO AYF-SINT.
       CON-CAL-AYF.
      *Actualiza saldo insoluto restando el valor del capital
           SUBTRACT AYF-CAPI FROM AYF-SCPA.
       FIN-CAL-AYF.
           EXIT.

       CAL-AYF-ULT SECTION.
       INI-CAL-AYF-ULT.
      *Setea que es el ultimo vencimiento
           MOVE 'S' TO AYF-IULT.
      *En este caso con el vencimiento actual se cancela todo el documen
      *Mueve el saldo insoluto al capital total
           MOVE AYF-SCPA TO AYF-CAPI.
      *Si la tasa de interes es estimada el valor final = 0, ya
      *que no se conoce realmente
      *JSS-INI 13-JUN-1997
      *    IF INT-IETB > SPACES
           IF INT-IETB > SPACES AND ( VEN-KEST NOT = 'S' )
      *JSS-FIN 13-JUN-1997
               MOVE 0 TO AYF-FINA
           ELSE
      *    Suma el saldo insoluto, intereses devengados para obtener val
      *    final ademas si el manejo de interes es prorrateado segun cap
      *    de cada cuota suma al valor final el saldo de los intereses
               ADD AYF-CAPI , AYF-IDNI GIVING AYF-FINA
               MOVE 0 TO AYF-IDNI
               IF DLC-MIPL(VEN-IDLC) = CIC-MIPL-P
                   ADD AYF-SINT TO AYF-FINA
                   MOVE 0 TO AYF-SINT.
       FIN-CAL-AYF-ULT.
           EXIT.

      *JSS-INI 28-FEB-1994
       COM-REG-VEN SECTION.
       INI-COM-REG-VEN.
           MOVE 0 TO VEN-AUXI.
           MOVE VEN-NUM-IDLC IN VEN TO VEN-IDLC.
           PERFORM LOD-RDLC.
           IF NOT VEN-STAT-OKS
               GO TO FIN-COM-REG-VEN.
      *Calcula numero de vencimientos cancelados
           IF OPC-NUM-TVCC IN OPC NOT > ZEROES
               ADD OPC-NUM-VCCO IN OPC
                   OPC-NUM-VCCM IN OPC
                   OPC-NUM-VCCV IN OPC GIVING VEN-VCAN
           ELSE
               MOVE OPC-NUM-TVCC IN OPC TO VEN-VCAN.
      *Si estan todos los vencimientos cancelados resta 1 al numero de
      *vencimientos cancelados como un 'truco' para no considerarlos
      *posteriormente
           IF OPC-NUM-VCVG IN OPC NOT > ZEROES
               SUBTRACT 1 FROM VEN-VCAN.
      *
           MOVE 'A' TO VEN-IVAS.
           PERFORM OBT-EVC.
           IF NOT VEN-STAT-OKS
               GO TO FIN-COM-REG-VEN.
      *
           IF OPC-VAL-SCRE IN OPC NOT > 0
               IF VEN-NUM-IVCT IN VEN = OPC-NUM-IVEN IN OPC
                   IF VEN-NUM-IVCT IN VEN = 1
                       MOVE OPC-FEC-FCOL IN OPC TO VEN-FMAX
                       GO TO CON-COM-REG-VEN
                   ELSE
                       PERFORM ANT-EVC
                       PERFORM DEC-VEN
                       IF NOT VEN-STAT-OKS
                           GO TO FIN-COM-REG-VEN
                       ELSE
                           GO TO CON-COM-REG-VEN
               ELSE
                   NEXT SENTENCE
           ELSE
           IF VEN-NUM-IVCT IN VEN = 1
               IF DLC-FEC-FDEV IN DLC > OPC-FEC-FCOL IN OPC
                   MOVE DLC-FEC-FDEV IN DLC TO VEN-FMAX
                   GO TO CON-COM-REG-VEN
               ELSE
                   MOVE OPC-FEC-FCOL IN OPC TO VEN-FMAX
                   GO TO CON-COM-REG-VEN.
      *
           PERFORM ANT-EVC.
           PERFORM DEC-VEN.
           IF NOT VEN-STAT-OKS
               GO TO FIN-COM-REG-VEN.
      *Compara la fecha del DLC con la FMAX del VEN anterior
      *FMAX = MAX(FDEV,FVEN anterior) y continua
           IF DLC-FEC-FDEV IN DLC > VEN-FMAX
               MOVE DLC-FEC-FDEV IN DLC TO VEN-FMAX.
       CON-COM-REG-VEN.
      *FUNO = MIN(CANDIDATO=MAX(FDEV,FCOL o FVEN anterior),FVEN actual)
           IF VEN-FEC-FVEN IN VEN < VEN-FMAX
               MOVE VEN-NUM-DVEN IN VEN TO INT-DUNO
               MOVE VEN-NUM-MVEN IN VEN TO INT-MUNO
               MOVE VEN-NUM-SVEN IN VEN TO INT-SUNO
               MOVE VEN-NUM-AVEN IN VEN TO INT-AUNO
           ELSE
               MOVE VEN-DMAX TO INT-DUNO
               MOVE VEN-MMAX TO INT-MUNO
               MOVE VEN-SMAX TO INT-SUNO
               MOVE VEN-AMAX TO INT-AUNO.
      *Carga FDOS, FCOL, FBTV, VCAM, IOPC, IDLC y llama a rutina GNS-CAL
      *para obtener tasa de periodo
           MOVE VEN-NUM-DVEN IN VEN     TO INT-DDOS.
           MOVE VEN-NUM-MVEN IN VEN     TO INT-MDOS.
           MOVE VEN-NUM-SVEN IN VEN     TO INT-SDOS.
           MOVE VEN-NUM-AVEN IN VEN     TO INT-ADOS.

           MOVE OPC-NUM-DCOL IN OPC     TO INT-DCOL.
           MOVE OPC-NUM-MCOL IN OPC     TO INT-MCOL.
           MOVE OPC-NUM-SCOL IN OPC     TO INT-SCOL.
           MOVE OPC-NUM-ACOL IN OPC     TO INT-ACOL.

           IF VEN-FTBS > ZEROES
               MOVE VEN-DTBS TO INT-DBTV
               MOVE VEN-MTBS TO INT-MBTV
               MOVE VEN-STBS TO INT-SBTV
               MOVE VEN-ATBS TO INT-ABTV
           ELSE
               MOVE OPC-NUM-DCOL IN OPC TO INT-DBTV
               MOVE OPC-NUM-MCOL IN OPC TO INT-MBTV
               MOVE OPC-NUM-SCOL IN OPC TO INT-SBTV
               MOVE OPC-NUM-ACOL IN OPC TO INT-ABTV.

           MOVE OPC-COD-VCOC IN OPC     TO INT-VCAM.
           MOVE OPC-CIC-IOPC IN OPC     TO INT-IOPC.
           MOVE VEN-NUM-IDLC IN VEN     TO INT-IDLC.
      *     PERFORM GNS-CAL-INT.
           PERFORM COL-CAL-INT.
      *SMC-FIN 09.09.1993
           IF INT-CMSG > SPACES
               MOVE INT-CMSG TO VEN-STAT
               MOVE INT-MEN2 TO VEN-MEN2
               GO TO FIN-COM-REG-VEN.
      *
      *Por definicion al entrar a esta SECTION el VEN esta incompleto
      *Si esta completo en terminos de final, el capital es 0 y por lo
      *tanto la instruccion ADD no afecta.
      *Si esta completo en terminos de capital, entonces el SCPA
      *ya ha sido rebajado, con lo cual el ADD deja el SCPA con el
      *saldo corregido para el calculo de interes respectivo
           IF OPC-VAL-SCRE IN OPC > 0
               MULTIPLY OPC-VAL-SCRE IN OPC BY INT-TIPE GIVING
                    AYF-SINT ROUNDED
           ELSE
      *JSS-INI 26-AUG-1994 Se suma el SCAP en vez del CAPI ya que
      *pueden existir pagos parciales a cuotas de capital conocido
      *        ADD VEN-VAL-CAPI IN VEN TO DLC-SCPA(VEN-IDLC)
               ADD VEN-VAL-SCAP IN VEN TO DLC-SCPA(VEN-IDLC)
      *JSS-FIN 26-AUG-1994
               MULTIPLY DLC-SCPA(VEN-IDLC) BY INT-TIPE GIVING
                        AYF-SINT ROUNDED.
      *
           MOVE OPC-COD-VCOC IN OPC TO PES-COD-VCAM.
           MOVE AYF-SINT  TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO AYF-SINT.
      *
      *Si a la cuota le falta el valor final, lo completa con el interes
      *correspondiente
           IF VEN-VAL-FINA IN VEN = 0
      *JSS-INI 26-AUG-1994 Se suma el SCAP en vez del CAPI ya que
      *pueden existir pagos parciales a cuotas de capital conocido
      *        ADD AYF-SINT VEN-VAL-CAPI IN VEN GIVING
               ADD AYF-SINT VEN-VAL-SCAP IN VEN GIVING
                            VEN-VAL-FINA IN VEN
      *JSS-FIN 26-AUG-1994
               MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN
           ELSE
      *Si a la cuota le falta el valor capital, lo completa con el inter
      *correspondiente
               MOVE 1 TO VEN-AUXI
               SUBTRACT AYF-SINT FROM VEN-VAL-FINA IN VEN
                               GIVING VEN-VAL-CAPI IN VEN
               MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN.

      *Verifica si el capital es mayor que el saldo del credito,
      *asume capital el saldo del credito
           IF OPC-VAL-SCRE IN OPC > 0
               IF VEN-VAL-SCAP IN VEN > OPC-VAL-SCRE IN OPC
                   MOVE OPC-VAL-SCRE IN OPC TO VEN-VAL-CAPI IN VEN
                   MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN
                   ADD AYF-SINT VEN-VAL-CAPI IN VEN
                         GIVING VEN-VAL-FINA IN VEN
                   MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN
               ELSE
                   NEXT SENTENCE
           ELSE
               IF VEN-VAL-SCAP IN VEN > DLC-SCPA(VEN-IDLC)
                   MOVE DLC-SCPA(VEN-IDLC)  TO VEN-VAL-CAPI IN VEN
                   MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN
                   ADD AYF-SINT VEN-VAL-CAPI IN VEN
                         GIVING VEN-VAL-FINA IN VEN
                   MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN.

      *Si los intereses son mayores que el valor final ( se considera
      *la diferencia como intereses devengados no incluidos )
           IF AYF-SINT > VEN-VAL-FINA IN VEN
               SUBTRACT VEN-VAL-FINA IN VEN FROM AYF-SINT
                 GIVING VEN-VAL-IDNI IN VEN
               MOVE 0 TO VEN-VAL-CAPI IN VEN
               MOVE 0 TO VEN-VAL-SCAP IN VEN
           ELSE
               MOVE 0 TO VEN-VAL-IDNI IN VEN.
      *Si es la ultima cuota y el valor de capital es menor que el saldo
      *del credito, asume capital el saldo del credito
           IF VEN-FEC-FVEN IN VEN = DLC-FEC-FMAV IN DLC
               IF OPC-VAL-SCRE IN OPC > 0
                   IF VEN-VAL-SCAP IN VEN < OPC-VAL-SCRE IN OPC
                       MOVE OPC-VAL-SCRE IN OPC TO VEN-VAL-CAPI IN VEN
                       MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN
                       ADD AYF-SINT VEN-VAL-CAPI IN VEN
                             GIVING VEN-VAL-FINA IN VEN
                       MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN
                       MOVE 0                   TO VEN-VAL-IDNI IN VEN
                   ELSE
                       NEXT SENTENCE
               ELSE
                   IF VEN-VAL-SCAP IN VEN < DLC-SCPA(VEN-IDLC)
                       MOVE DLC-SCPA(VEN-IDLC)  TO VEN-VAL-CAPI IN VEN
                       MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN
                       ADD AYF-SINT VEN-VAL-CAPI IN VEN
                             GIVING VEN-VAL-FINA IN VEN
                       MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN
                       MOVE 0                   TO VEN-VAL-IDNI IN VEN.
      *
           MOVE OPC-COD-VCOC IN OPC TO PES-COD-VCAM.
           MOVE VEN-VAL-CAPI IN VEN TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO VEN-VAL-CAPI IN VEN.
      *
           MOVE OPC-COD-VCOC IN OPC TO PES-COD-VCAM.
           MOVE VEN-VAL-FINA IN VEN TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO VEN-VAL-FINA IN VEN.
      *
      *Solo si la cuota la completo con capital, tengo que restar
      *al SCPA
           IF VEN-AUXI = 1
               SUBTRACT VEN-VAL-SCAP IN VEN FROM
                        DLC-SCPA(VEN-NUM-IDLC IN VEN).
           IF VEN-NUM-IVCT IN VEN = 1
               GO TO FIN-COM-REG-VEN.
           MOVE 'S' TO VEN-IVAS.
           PERFORM OBT-FYJ.
           MOVE EVC-JPVC(VEN-IEVC) TO AYF-JAMT.
           PERFORM ACT-VDLC.
       FIN-COM-REG-VEN.
           EXIT.
      *JSS-FIN 28-FEB-1994

       ACT-VDLC SECTION.
       INI-ACT-VDLC.
      *Actualiza el Juliano de la amortizacion del Documento Y
      *Mueve la fecha del vencimiento a la fecha de amortizacion del
      *documento ( Recordar que existen por cada 'vencimiento' dos fecha
      *la FPVC que es la fecha del vencimiento sin analisis de feriado y
      *la FFVC que es la fecha del vencimiento mas analisis de feriado )
           MOVE 1 TO VEN-IVCC.
       AMT-ACT-VDLC.
           MOVE VEN-VVCC(VEN-IVCC) TO VEN-IEVC.
           IF EVC-TCVE(VEN-IEVC) = 'IO' OR 'CO' OR 'FO' OR 'CE'
               IF EVC-IDLC(VEN-IEVC) = 0
                   MOVE EVC-IVCT(VEN-IEVC) TO VEN-IDLC
                   MOVE AYF-JAMT TO DLC-JAMT(VEN-IDLC)
                   MOVE EVC-DFVC(VEN-IEVC) TO DLC-DAMT(VEN-IDLC)
                   MOVE EVC-MFVC(VEN-IEVC) TO DLC-MAMT(VEN-IDLC)
                   MOVE EVC-SFVC(VEN-IEVC) TO DLC-SAMT(VEN-IDLC)
                   MOVE EVC-AFVC(VEN-IEVC) TO DLC-AAMT(VEN-IDLC)
               ELSE
                   MOVE EVC-IDLC(VEN-IEVC) TO VEN-IDLC
                   MOVE AYF-JAMT TO DLC-JAMT(VEN-IDLC)
                   MOVE EVC-DFVC(VEN-IEVC) TO DLC-DAMT(VEN-IDLC)
                   MOVE EVC-MFVC(VEN-IEVC) TO DLC-MAMT(VEN-IDLC)
                   MOVE EVC-SFVC(VEN-IEVC) TO DLC-SAMT(VEN-IDLC)
                   MOVE EVC-AFVC(VEN-IEVC) TO DLC-AAMT(VEN-IDLC).
           ADD 1 TO VEN-IVCC.
           IF VEN-IVCC NOT > VEN-MVCC
               GO TO AMT-ACT-VDLC.
      *
           MOVE VEN-VVCC(1) TO VEN-IEVC.
       FIN-ACT-VDLC.
           EXIT.

       ANL-FRD-FINT SECTION.
       INI-ANL-FRD-FINT.
           MOVE INT-DDMA TO FEC-ITM1.
           MOVE INT-MDMA TO FEC-ITM2.
           MOVE INT-SDMA TO FEC-ITM3.
           MOVE INT-ADMA TO FEC-ITM4.
       LUP-ANL-FRD-FINT.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-OKS
               MOVE FEC-ITM1 TO INT-DDMA
               MOVE FEC-ITM2 TO INT-MDMA
               MOVE FEC-ITM3 TO INT-SDMA
               MOVE FEC-ITM4 TO INT-ADMA
               GO TO FIN-ANL-FRD-FINT.
           IF NOT FEC-STAT-FER
               MOVE MSG-COD-MENS TO VEN-STAT
               GO TO FIN-ANL-FRD-FINT.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE MSG-COD-MENS TO VEN-STAT
           ELSE
               GO TO LUP-ANL-FRD-FINT.
       FIN-ANL-FRD-FINT.
           EXIT.

       BUS-PINT SECTION.
       INI-BUS-PINT.
      *Carga los parametros para obtener tasa de interes
           MOVE DLC-TINT(VEN-IDLC)  TO INT-TINT.
           MOVE OPC-COD-VCOC IN OPC TO INT-VCAM.

           MOVE DLC-DAMT(VEN-IDLC)  TO INT-DUNO.
           MOVE DLC-MAMT(VEN-IDLC)  TO INT-MUNO.
           MOVE DLC-SAMT(VEN-IDLC)  TO INT-SUNO.
           MOVE DLC-AAMT(VEN-IDLC)  TO INT-AUNO.
           MOVE EVC-DFVC(VEN-IEVC)  TO INT-DDOS.
           MOVE EVC-MFVC(VEN-IEVC)  TO INT-MDOS.
           MOVE EVC-SFVC(VEN-IEVC)  TO INT-SDOS.
           MOVE EVC-AFVC(VEN-IEVC)  TO INT-ADOS.

           MOVE OPC-NUM-DCOL IN OPC TO INT-DCOL INT-DBTV.
           MOVE OPC-NUM-MCOL IN OPC TO INT-MCOL INT-MBTV.
           MOVE OPC-NUM-SCOL IN OPC TO INT-SCOL INT-SBTV.
           MOVE OPC-NUM-ACOL IN OPC TO INT-ACOL INT-ABTV.
           MOVE OPC-CIC-IOPC IN OPC TO INT-IOPC.
           MOVE 1                   TO INT-IDLC.
       FIN-BUS-PINT.
           EXIT.


      * Rutina que a partir de la fecha de colocacion, busca la
      * ultima fecha de cambio de tasa antes de la fecha de ejecucion
      * La fecha de ultimo cambio de tasa se calcula solo para el primer
      * vencimiento, para los demas se mantiene la fecha calculada
      * Entrada : WSS-FEC-FINT  ( FCOL )
      * Salida  : WSS-FEC-FUCT
       CAL-ULT-CAMBIO-TASA SECTION.
      *----------------------------
       INI-CAL-ULT-CAMBIO-TASA.
           IF ICG-COD-CVTI IN ICG NOT > SPACES
               GO TO FIN-CAL-ULT-CAMBIO-TASA.

           IF ICG-MSC-TFPR IN ICG = 'F'
               PERFORM CARGA-FTBS
               GO TO FIN-CAL-ULT-CAMBIO-TASA.

           IF WSS-PVEZ IN WSS-VARI NOT = 'S'
               GO TO FIN-CAL-ULT-CAMBIO-TASA.

       LUP-CAL-ULT-CAMBIO-TASA.
           IF WSS-FEC-FINT IN WSS-VARI > HOY-FHOY
               GO TO FIN-CAL-ULT-CAMBIO-TASA.

           MOVE WSS-FEC-FINT IN WSS-VARI TO WSS-FEC-FUCT IN WSS-VARI.

           IF ICG-MSC-UTPR IN ICG = 'D'
               MOVE ICG-NUM-PREP IN ICG TO FEC-NDIA
               MOVE FEC-SUM-DIA         TO FEC-CMND
           ELSE
           IF ICG-MSC-UTPR IN ICG = 'M'
               MOVE ICG-NUM-PREP IN ICG TO FEC-NMES
               MOVE FEC-SUM-MES         TO FEC-CMND
           ELSE
               MOVE ICG-NUM-PREP IN ICG TO FEC-NANO
               MOVE FEC-SUM-ANO         TO FEC-CMND.

           MOVE WSS-NUM-DINT IN WSS-VARI TO FEC-ITM1.
           MOVE WSS-NUM-MINT IN WSS-VARI TO FEC-ITM2.
           MOVE WSS-NUM-SINT IN WSS-VARI TO FEC-ITM3.
           MOVE WSS-NUM-AINT IN WSS-VARI TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               GO TO FIN-CAL-ULT-CAMBIO-TASA.

           MOVE FEC-ITM1 TO WSS-NUM-DINT IN WSS-VARI.
           MOVE FEC-ITM2 TO WSS-NUM-MINT IN WSS-VARI.
           MOVE FEC-ITM3 TO WSS-NUM-SINT IN WSS-VARI.
           MOVE FEC-ITM4 TO WSS-NUM-AINT IN WSS-VARI.
           GO TO LUP-CAL-ULT-CAMBIO-TASA.
       FIN-CAL-ULT-CAMBIO-TASA.
           EXIT.


      ******************************************************************
      *El parametro VEN-FTBS sirve para buscar la ultima tasa vigente
      *a esa fecha (si lleva valor), y si no lleva valor, busca la tasa
      *a fecha de colocacion
       CARGA-FTBS SECTION.
      *******************
       INI-CARGA-FTBS.
           IF WSS-PVEZ IN WSS-VARI = 'S'
               MOVE OPC-FEC-FCOL IN OPC TO WSS-FEC-FUCT IN WSS-VARI
                                           WSS-FEC-FXXX IN WSS-VARI
           ELSE
           IF WSS-PVEZ IN WSS-VARI NOT = 'S' AND
              VEN-FEC-FVEN IN VEN NOT > HOY-FHOY
               MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FUCT IN WSS-VARI
               MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FXXX IN WSS-VARI
           ELSE
               MOVE WSS-FEC-FXXX TO WSS-FEC-FUCT IN WSS-VARI.
       FIN-CARGA-FTBS.
           EXIT.

       COPY TABBFUSR.
       COPY TABBFOFI.
       COPY TABBFVLR.
       COPY TABBFCAM.
       COPY TABBBUSR.
       COPY TABBBOFI.
       COPY TABBVUSR.
       COPY TABBVOFI.

       COPY COLBFVMV.
       COPY COLBFVES.
       COPY COLBFVEN.
       COPY COLBFOPC.
       COPY COLBFDLC.
       COPY COLBFTOC.
       COPY COLBFICG.
       COPY COLBFCYA.
       COPY COLBFRDC.
       COPY COLBFEVC.
       COPY COLBFRCV.
       COPY COLBFRDT.

       COPY COLBGINT.
       COPY COLBGCAU.
       COPY COLBGCTO.

       COPY SGCBFDBC.
       COPY SGCBGCLI.

       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGPES.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBBMSG.
       COPY GNSBFMSC.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBTABT.
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY GNSBGCPT.

