       IDENTIFICATION DIVISION.
       PROGRAM-ID. COLPBPA2.
       AUTHOR. H.ZAPATA.
      *REMARKS. PAREA EL MAESTRO DE CREDITOS V/S
      *         TABLA OPC DESDE ARCHIVO GENERADO POR COLPBPA1.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ************************************************
      **   M O D I F I C A C I O N E S        ********
      ************************************************
      ***                                            *
      ************************************************
      ************************************************
      ************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.

       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT MAECRED         ASSIGN TO        MAECRE
                                  ORGANIZATION IS INDEXED
                                  ACCESS IS SEQUENTIAL
                                  RECORD KEY IS CRE-CLAVE
                                  FILE STATUS IS FS-MAECRED.
           SELECT SAM-ENTRADA ASSIGN TO DA-S-ENTRADA.
           SELECT LISTADO ASSIGN TO SYS020-UR-1403-S-LISTADO.
       DATA DIVISION.
       FILE SECTION.



       FD  LISTADO
           RECORD CONTAINS 133 CHARACTERS.

       01  LINEA PIC X(133).

       FD  MAECRED
           RECORD CONTAINS 377 CHARACTERS.
      ******************************************************************
      *             DESCRIPCION DE REGISTRO (CREM002)
      *             ----------------------------------
      *
      *    ARCHIVO   : BCVO.MAESTRO.CREDITOS
      *              : MAESTRO DE CREDITOS
      *    FD        : CREM002
      *    PREFIJO   : 'CRE'
      *    LARGO REG.: 377
      *    ORGANIZAC.: VSAM-KSDS
      *    BLOQUEO   : NO
      *    CLAVE PRIM: CRE-CLAVE
      *    LARGO CL.1: 21
      *    CLAVE SEC.: CRE-RUT-DD
      *    LARGO CL.1: 10
      *
      ******************************************************************
      *
       01  CRE-CREDITOS-REG.

           05  CRE-CLAVE.
               10  CRE-SUCURSAL                PIC  9(03).
               10  CRE-CODIGO-OPER.
                   20  CRE-COD-OPER            PIC  9(03).
                       88 CRE-CREDITO-AL-PERSONAL VALUE
                       109 125 610 611 614 620 712 713 722 725.
                   20  CRE-SERIAL-OPER         PIC  9(05).
               10  CRE-RUT-DD.
                   20  CRE-RUT-DD-NUM          PIC  9(09).
                   20  CRE-RUT-DD-VER          PIC  X(01).

           05  CRE-RUT-DI.
               10  CRE-RUT-DI-DIG              PIC  9(09).
               10  CRE-RUT-DI-VER              PIC  X(01).
           05  CRE-RUT-EMP.
               10  CRE-RUT-EMP-DIG             PIC  9(09).
               10  CRE-RUT-EMP-VER             PIC  X(01).
           05  CRE-CTA-CTE                     PIC  X(10).

           05  CRE-TOTAL-CUOTAS                PIC  9(03).
           05  CRE-NUM-CUOT-PAGADAS            PIC  9(03).
           05  CRE-NUM-CUOT-MOROSAS            PIC  9(03).
           05  CRE-NUM-CUOT-PENDIENTES         PIC  9(03).

           05  CRE-FECH-CURSE.
               10  CRE-FECH-CURSE-AA           PIC  9(02).
               10  CRE-FECH-CURSE-MM           PIC  9(02).
               10  CRE-FECH-CURSE-DD           PIC  9(02).

           05  CRE-FECH-PAGARE.
               10  CRE-FECH-PAGARE-AA          PIC  9(02).
               10  CRE-FECH-PAGARE-MM          PIC  9(02).
               10  CRE-FECH-PAGARE-DD          PIC  9(02).
           05  CRE-FECH-PRIMER-VENC.
               10  CRE-FECH-PRIMER-VENC-AA     PIC  9(02).
               10  CRE-FECH-PRIMER-VENC-MM     PIC  9(02).
               10  CRE-FECH-PRIMER-VENC-DD     PIC  9(02).
           05  CRE-FECH-VENC-ANTERIOR.
               10  CRE-FECH-VENC-ANTERIOR-DD   PIC  9(02).
               10  CRE-FECH-VENC-ANTERIOR-MM   PIC  9(02).
               10  CRE-FECH-VENC-ANTERIOR-AA   PIC  9(02).

           05  CRE-FECH-PROX-VENC.
               10  CRE-FECH-PROX-VENC-AA       PIC  9(02).
               10  CRE-FECH-PROX-VENC-MM       PIC  9(02).
               10  CRE-FECH-PROX-VENC-DD       PIC  9(02).

           05  CRE-FECH-ULTIMO-MOV.
               10  CRE-FECH-ULTIMO-MOV-DD      PIC  9(02).
               10  CRE-FILLER-1                PIC  X(01).
               10  CRE-FECH-ULTIMO-MOV-MM      PIC  9(02).
               10  CRE-FILLER-2                PIC  X(01).
               10  CRE-FECH-ULTIMO-MOV-AA      PIC  9(02).

           05  CRE-TIPO-OPER                   PIC  X(03).
           05  CRE-TIPO-GARANTIA               PIC  X(01).
           05  CRE-TIPO-INTERES                PIC  X(01).
           05  CRE-TIPO-CLIENTE                PIC  9(03).
           05  CRE-PERIODICIDAD                PIC  9(01).
           05  CRE-REAJUSTABILIDAD             PIC  X(01).
           05  CRE-DIAS-PLAZO                  PIC  9(03).
           05  CRE-MESES-DIFERIDOS             PIC  X(03).
           05  CRE-TIEMPO-GRACIA               PIC  9(02).
           05  CRE-COD-DISTRIBUIDOR            PIC  X(02).
           05  CRE-AGENTE                      PIC  9(03).
           05  CRE-LINEA-CRED                  PIC  9(02).
           05  CRE-DESTINO                     PIC  X(02).
           05  CRE-TIPO-COBRANZA               PIC  9(01).
           05  CRE-INDIC-MONEDA                PIC  9(01).
           05  CRE-VIGENCIA                    PIC  X(01).
           05  CRE-OTRO-DD                     PIC  X(01).
           05  CRE-OTRO-DI                     PIC  X(01).

           05  CRE-MONTO-COLOC                 PIC S9(12)V9(03)  COMP-3.
           05  CRE-INTERES-COLOC               PIC S9(12)V9(03)  COMP-3.
           05  CRE-CUOTA-BASE                  PIC S9(10)V9(03)  COMP-3.
           05  CRE-TASA-VIGENTE                PIC S9(02)V9(02)  COMP-3.

           05  CRE-MONTO-PAGADO                PIC S9(12)V9(03)  COMP-3.
           05  CRE-INTERES-PAGADO              PIC S9(12)V9(03)  COMP-3.

           05  CRE-ULT-MONTO-CANC              PIC S9(10)V9(03)  COMP-3.
           05  CRE-ULT-INTERES-CANC            PIC S9(10)V9(03)  COMP-3.

           05  CRE-CUOTA-PROX-VENC             PIC S9(10)V9(03)  COMP-3.
           05  CRE-INTERES-PROX-VENC           PIC S9(10)V9(03)  COMP-3.

           05  CRE-SALDO-COLOCAC               PIC S9(12)V9(03)  COMP-3.
           05  CRE-TOT-INT-PAG-MES             PIC S9(12)V9(03)  COMP-3.

           05  CRE-REAJUSTES-PAG               PIC S9(10)V9(03)  COMP-3.
           05  CRE-VAL-IMPU                    PIC S9(11)V9(04)  COMP-3.
           05  CRE-HABER                       PIC S9(11)V9(04)  COMP-3.

           05  CRE-AJUSTE                      PIC S9(06)V9(02)  COMP-3.
           05  CRE-GASTO-NOTARIAL              PIC S9(06)V9(02)  COMP-3.
           05  CRE-OPERAC-ANTERIOR             PIC  X(08).
           05  CRE-COD-HIPOTECARIO             PIC  X(08).
           05  CRE-CLAVE-LINEA                 PIC  X(11).
           05  CRE-TASA-SPREAD-COLOC           PIC S9(02)V9(02).
           05  CRE-SALDO-CAPIT-REAJ            PIC S9(12)V9(03)  COMP-3.
           05  CRE-CUOTAS-ORIGINALES           PIC  9(03).
           05  CRE-NUM-OPERATIVAS              PIC  9(03).
           05  CRE-CONT-CAPITALIZ              PIC  9(02).
           05  CRE-CUOTAS-CURSE                PIC  9(03).
           05  CRE-PERIODICIDAD-INTERES        PIC  9(01).
           05  CRE-FECHA-CANCELACION.
               10  CRE-FECHA-CANCELACION-AA    PIC  9(02).
               10  CRE-FECHA-CANCELACION-MM    PIC  9(02).
               10  CRE-FECHA-CANCELACION-DD    PIC  9(02).
           05  CRE-FECH-PROX-VCTO-INT.
               10  CRE-FECH-PROX-VENC-INT-AA   PIC  9(02).
               10  CRE-FECH-PROX-VENC-INT-MM   PIC  9(02).
               10  CRE-FECH-PROX-VENC-INT-DD   PIC  9(02).
           05  CRE-FECH-ULT-PAGO-INT.
               10  CRE-FECH-ULT-PAGO-INT-AA    PIC  9(02).
               10  CRE-FECH-ULT-PAGO-INT-MM    PIC  9(02).
               10  CRE-FECH-ULT-PAGO-INT-DD    PIC  9(02).
           05  CRE-MTO-INTERES-CAPITALIZADO    PIC S9(08)V9(02)  COMP-3.
           05  CRE-TASA-INTERES-2              PIC S9(02)V9(02)  COMP-3.
           05  CRE-TASA-INTERES-3              PIC S9(02)V9(02)  COMP-3.
           05  CRE-TIPO-CUOTA                  PIC  9(01).
           05  CRE-IND-I8                      PIC  9(01).
           05  CRE-IND-I9                      PIC  9(01).
           05  CRE-IND-I11                     PIC  9(01).
           05  CRE-IND-I14                     PIC  9(02).
           05  CRE-IND-I16                     PIC  9(02).
           05  CRE-IND-I17                     PIC  9(01).
           05  CRE-IND-I18                     PIC  9(01).
           05  CRE-FECHA-SUSPEN.
               10  CRE-FECHA-SUSPEN-AA         PIC  9(02).
               10  CRE-FECHA-SUSPEN-MM         PIC  9(02).
               10  CRE-FECHA-SUSPEN-DD         PIC  9(02).
           05  CRE-VCTOS-EN-MES                PIC S9(12)V9(03)  COMP-3.
           05  CRE-VCTOS-PAG-MES               PIC S9(12)V9(03)  COMP-3.
           05  CRE-TASA-MAX-CONV               PIC  X(1).
           05  CRE-ESTADO-PRO                  PIC  X(1).
           05  CRE-NRO-PREPAGOS                PIC  9(03).
           05  CRE-CLASIF-RIESGO               PIC  X(02).
           05  CRE-IND-RENO                    PIC  X(01).
           05  CRE-IND-SUS-COB                 PIC  X(01).
           05  CRE-FILLER                      PIC  X(03).

       FD  SAM-ENTRADA
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 96 CHARACTERS.
       01  REG-ENTRADA.
           05  SAM-CLAVE.
               10 SAM-SUC       PIC 9(03).
               10 SAM-CODIGO-OPER.
                  20 SAM-OPE       PIC 9(03).
                  20 SAM-SER       PIC 9(05).
               10 SAM-RUT.
                  20 SAM-RUT-DIG  PIC 9(09).
                  20 SAM-RUT-VER  PIC X(01).
           05  SAM-CLAVE-BD     PIC X(12).
           05  SAM-STATUS       PIC X(03).
           05  SAM-SALDO        PIC S9(13)V9999 COMP-3.
           05  SAM-NUM-ACOL     PIC XX.
           05  SAM-NUM-MCOL     PIC XX.
           05  SAM-NUM-DCOL     PIC XX.
           05  SAM-NUM-VCOR     PIC 999.
           05  SAM-NUM-VCVG     PIC 999.
           05  SAM-NUM-TVCC     PIC 999.
           05  SAM-VAL-CRED     PIC S9(13)V9999    COMP-3.
           05  SAM-FILLER       PIC X(27).



       WORKING-STORAGE SECTION.
      *-----------------------
       01  WS-VARIOS.
           10  FIN-B160                  PIC X(2)        VALUE SPACES.
           10  FIN-CREDITO               PIC X(2)        VALUE SPACES.
           10  FS-MAECRED                PIC 9(2)        VALUE ZEROES.
           10  FS-UEFES                  PIC 9(2)        VALUE ZEROES.
           10  SW-IMP                    PIC 9(1)        VALUE 0.
           10  CT-NOBD                   PIC 9(06) VALUE 0 COMP-3.
           10  CL                        PIC 99          VALUE 70.
           10  CP                        PIC 9(4)        VALUE 0.
           10  VALOR-A                   PIC S9(12)V9999.
           10  VALOR-B                   PIC S9(12)V9999.
           10  WS-VALOR                  PIC S9(12)V9999.
           10  SW-DD                     PIC 9               VALUE 0.
           10  SW-DI                     PIC 9               VALUE 0.
       01  MENSAJES-ERROR.
           03 MENS-PROC               PIC X(30)          VALUE SPACES.
           03 MENS-ARCH               PIC X(20)          VALUE SPACES.
           03 MENS-STAT               PIC XX             VALUE SPACES.
           03 MENS-KEY                PIC X(30)          VALUE SPACES.
           03 MENS-SW                 PIC 9              VALUE 1.
       01  WS-CONTADORES.
           10  CT-NOMAE              PIC 9(9)            VALUE 0.
           10  VAL-IGUALES           PIC 9(9)            VALUE 0.
           10  VAL-DISTINTOS         PIC 9(9)            VALUE 0.
           10  VCOR-IGUALES          PIC 9(9)            VALUE 0.
           10  VCOR-DISTINTOS        PIC 9(9)            VALUE 0.
           10  VCVG-IGUALES          PIC 9(9)            VALUE 0.
           10  VCVG-DISTINTOS        PIC 9(9)            VALUE 0.
           10  TVCC-IGUALES          PIC 9(9)            VALUE 0.
           10  TVCC-DISTINTOS        PIC 9(9)            VALUE 0.
           10  CONT-DD-IGUALES       PIC 9(9)            VALUE 0.
           10  CONT-DD-DISTINTOS     PIC 9(9)            VALUE 0.
           10  CONT-DI-IGUALES       PIC 9(9)            VALUE 0.
           10  CONT-DI-DISTINTOS     PIC 9(9)            VALUE 0.
           10  CT-CORR-CRED          PIC 9(9)            VALUE 0.
           10  CT-CORR-CURSE         PIC 9(9)            VALUE 0.
           10  CT-CORR-MODIF         PIC 9(9)            VALUE 0.
           10  CT-CAN-ELIM           PIC 9(9)            VALUE 0.
           10  PIL-CONT              PIC 9(06)    COMP-3 VALUE 0.
           10  LEIDOS                    PIC 9(9)        VALUE 0.
           10  LEIDOS-BD                 PIC 9(9)        VALUE 0.
           10  IGUALES                   PIC 9(9)        VALUE 0.
       01  WS-AREAS-VARIAS.
           05 WS-FVEN.
              10 WS-ANOV             PIC 9(02).
              10 WS-MESV             PIC 9(02).
              10 WS-DIAV             PIC 9(02).


       01  TARJETA.
           05 FILLER         PIC X(21) VALUE 'GENERA CORRECCIONES: '.
           05 OPCION-GENERA  PIC X(02) VALUE SPACES.
              88 SI-GENERA  VALUE 'SI'.
           05 FILLER         PIC X(01).
           05 FECH-TAR.
              10 FT-ANO      PIC 99.
              10 FT-MES      PIC 99.
              10 FT-DIA      PIC 99.
           05 FILLER         PIC X(01).
           05 PROC-TAR       PIC X(01).
           05 FILLER         PIC X(40).

       01  FECHAS-VARIAS.
           05 FECHA-TARJETA PIC 9(6).
           05 RFECHA-TARJETA REDEFINES FECHA-TARJETA.
              10 ANO-TARJETA PIC 99.
              10 MES-TARJETA PIC 99.
              10 DIA-TARJETA PIC 99.
           05 FECHA-APERTURA PIC 9(6).
           05 RFECHA-APERTURA REDEFINES FECHA-APERTURA.
              10 ANO-APERTURA PIC 99.
              10 MES-APERTURA PIC 99.
              10 DIA-APERTURA PIC 99.
       01  LLAVES.
           05 LLAVE-AUX.
              10 LLAVE-1             PIC 999.
              10 LLAVE-2             PIC 9(8).
           05 FECHA-HOY.
              10 DD-HOY PIC 99.
              10 FILLER PIC X.
              10 MM-HOY PIC 99.
              10 FILLER PIC X.
              10 AA-HOY PIC 99.
       01  CAMPOS-BCD.
           05 FEC-BCD.
              10 FECHA-BCD           PIC XXX.
              10 RFECHA-BCD REDEFINES FECHA-BCD.
                 15 R-DIA            PIC X.
                 15 R-MES            PIC X.
                 15 R-ANO            PIC X.
              10 FILLER              PIC 9 COMP-3 VALUE 0.
           05 RFEC-BCD REDEFINES FEC-BCD.
              10 FECHA-INT           PIC 9(6)V9 COMP-3.
           05 FECHA-EBCDIC           PIC 9(6).
           05 RFECHA-EBCDIC REDEFINES FECHA-EBCDIC.
              10 DIA-EBCDIC          PIC 99.
              10 MES-EBCDIC          PIC 99.
              10 ANO-EBCDIC          PIC 99.
      *
           05 RUT-MAE                PIC X(6).
           05 FILLER REDEFINES RUT-MAE.
              10 RUT-BASE            PIC X(5).
              10 RUT-DV              PIC X.
           05 RUT-BCD.
              10 RUT-BCD-BASE        PIC X(5).
              10 FILLER-1            PIC 9 COMP-3 VALUE 0.
           05 RUT-COMP REDEFINES RUT-BCD PIC 9(11) COMP-3.
           05 RUT-EBCDIC             PIC 9(11).
           05 FILLER REDEFINES RUT-EBCDIC.
              10 FILLER              PIC 99.
              10 RUT-OK-EBCDIC       PIC 9(8).
              10 FILLER              PIC 9.

           05 WK-AREA-CTACTE-BCD.
              10 WK-CTACTE-BCD       PIC X(4).
              10 FILLER              PIC 9 COMP-3 VALUE 0.
           05 WK-AREA-CTACTE-EBCDIC REDEFINES WK-AREA-CTACTE-BCD.
              10 WK-CTACTE-DISP      PIC 9(8)V9 COMP-3.
           05 WK-CTACTE-EBCDIC       PIC 9(8).

           05 WK-AREA-OFIC-BCD.
              10 WK-OFIC-BCD         PIC XX.
              10 FILLER              PIC 9 COMP-3 VALUE 0.
           05 WK-AREA-OFIC-EBCDIC REDEFINES WK-AREA-OFIC-BCD.
              10 WK-OFIC-DISP        PIC 999V9 COMP-3.
           05 WK-OFIC-EBCDIC         PIC 999.


       01  TIT-1.
           05 FILLER PIC X(46) VALUE 'BANCO DE CREDITO E INVERSIONES'.
           05 FILLER PIC X(70) VALUE
              ' PAREADOR MAESTRO CREDITOS  V/S BASE DATOS 160'.
           05 FILLER PIC X(7) VALUE 'FECHA:'.
           05 T1-FECHA PIC X(8).
       01  TIT-2.
           05 FILLER PIC X(45) VALUE 'INTERFAZ COLOCACIONES'.
           05 FILLER PIC X(71) VALUE
              '-----------------------------------------------'.
           05 FILLER PIC X(7) VALUE 'PAG  :'.
           05 PAG-T1 PIC ZZZ9.
       01  RAYA PIC X(132) VALUE ALL '-'.
       01  ESPA PIC X(132) VALUE ALL SPACES.
       01  TIT-3.
           05 FILLER PIC XXX VALUE SPACES.
           05 FILLER PIC X(46)
              VALUE 'C L A V E       R U T       CLAVE       FECHA '.
           05 FILLER PIC X(40)
              VALUE '  FECHA         NCUO NCUO  NCUO         '.
           05 FILLER PIC X(24)
              VALUE 'MONTO'.
           05 FILLER PIC X(18)
              VALUE 'SALDO       ERR'.

       01  TIT-4.
           05 FILLER PIC XXX VALUE SPACES.
           05 FILLER PIC X(46)
              VALUE '                D.D.        COLOC.      CURSE '.
           05 FILLER PIC X(39)
              VALUE '  VCTO.         CURS  PEND   PAG       '.
           05 FILLER PIC X(44)
              VALUE 'ORIGINAL'.

       01  DETA-1.
           05 FILLER      PIC X VALUE SPACE.
           05 SUC-S       PIC XXXB.
           05 CLAVE-S     PIC X(8)BBB.
           05 RUTDD-S     PIC X(9).
           05 FILLER      PIC X VALUE '-'.
           05 DVDD-S      PIC XBBB.
           05 CLAVE-BD    PIC X(12).
           05 FILLER      PIC X(1)  VALUE SPACES.
           05 DIAC-S      PIC 99.
           05 MESC-S      PIC 99.
           05 ANOC-S      PIC 99.
           05 FILLER      PIC X(2)  VALUE SPACES.
           05 DIAV-S      PIC 99.
           05 MESV-S      PIC 99.
           05 ANOV-S      PIC 99.
           05 FILLER      PIC X(2)  VALUE SPACES.
           05 FILLER      PIC X(3)  VALUE SPACES.
           05 FILLER      PIC X(4)  VALUE SPACES.
           05 CCURSE-S    PIC 9(3).
           05 FILLER      PIC X(3)  VALUE SPACES.
           05 CPEND-S     PIC 9(3).
           05 FILLER      PIC X(3)  VALUE SPACES.
           05 CPAG-S      PIC 9(3).
           05 FILLER      PIC X(2)  VALUE SPACES.
           05 MONTO-S     PIC ZZZZ.ZZZ.ZZ9,9999.
           05 FILLER      PIC X(2)  VALUE SPACES.
           05 SALDO-S     PIC ZZZZZ.ZZZ.ZZ9,9999.
           05 FILLER      PIC X(3)  VALUE SPACES.
           05 OBS-S       PIC X(03).
           05 FILLER      PIC X(2)  VALUE SPACES.
           05 STATUS-S    PIC X(03) VALUE SPACES.
       01  LEGA-DATE-BLOCK.
           05 COBOL-II-DATE.
              10 COBOL-II-YEAR
                          PIC XX.
              10 COBOL-II-MONTH
                          PIC XX.
              10 COBOL-II-DAY
                          PIC XX.
           05 OSVS-CURRENT-DATE.
              10 CURRENT-DATE-MONTH
                          PIC XX.
              10 FILLER   PIC X     VALUE '/'.
              10 CURRENT-DATE-DAY
                          PIC XX.
              10 FILLER   PIC X     VALUE '/'.
              10 CURRENT-DATE-YEAR
                          PIC XX.


       PROCEDURE DIVISION.
      *------------------
      *
       A-CONTROL-SETION.
      *
       AA-INICIALIZACION.
           ENTRY 'DBMSCBL'
           PERFORM B-INICIALIZAR.
      *
       AB-PROCESAMIENTO.
           PERFORM C-PROCESAR.
      *
       AC-TERMINO.
           PERFORM D-TERMINAR.
      *
       AZZ-FIN-PROG.
           STOP RUN.
      *
      *
       B-INICIALIZAR SECTION.
      *
       BA-CONTROL.
      *-----------
           PERFORM BB-ABRIR-ARCHIVOS
           GO TO   BZ-FIN-SECCION.


       BB-ABRIR-ARCHIVOS.
      *------------------
           ACCEPT TARJETA.
           OPEN INPUT MAECRED SAM-ENTRADA
                OUTPUT LISTADO.
           ACCEPT COBOL-II-DATE FROM DATE
           MOVE COBOL-II-YEAR TO CURRENT-DATE-YEAR
           MOVE COBOL-II-MONTH TO CURRENT-DATE-MONTH
           MOVE COBOL-II-DAY TO CURRENT-DATE-DAY
           MOVE OSVS-CURRENT-DATE TO T1-FECHA
           IF FS-MAECRED NOT = 0
                 MOVE  'ERROR EN OPEN   '  TO MENS-PROC
                 MOVE  'MCREDITOS       '  TO MENS-ARCH
                 MOVE  FS-MAECRED          TO MENS-STAT
                 MOVE  CRE-CLAVE           TO MENS-KEY
                 MOVE  100                 TO RETURN-CODE.

       BZ-FIN-SECCION.
      *---------------
           EXIT.

      *
      *
       C-PROCESAR SECTION.
      *
      *
       CA-CONTROL.
      *-----------
           IF RETURN-CODE = 100
              GO TO   CZ-FIN-SECCION.

           PERFORM CB-LEER-CREDITOS
           PERFORM CC-LEER-B160
           PERFORM CD-PROCESO-PAREO UNTIL FIN-CREDITO = 'SI'
                                    OR    FIN-B160    = 'SI'
                                    OR    RETURN-CODE = 100.
           IF  FIN-CREDITO = 'NO'
           THEN PERFORM CE-PROCESO-MENOR UNTIL FIN-CREDITO = 'SI'
                                         OR    RETURN-CODE = 100.

           IF FIN-B160 = 'SI'
           THEN PERFORM CF-PROCESO-MAYOR UNTIL FIN-B160 = 'SI'
                                         OR    RETURN-CODE = 100.
           GO TO   CZ-FIN-SECCION.


       CB-LEER-CREDITOS.
      *-----------------
           READ MAECRED
           IF FS-MAECRED NOT = 0 AND NOT = 10
                 MOVE  'ERROR EN READ   '  TO MENS-PROC
                 MOVE  'MCREDITOS       '  TO MENS-ARCH
                 MOVE  FS-MAECRED          TO MENS-STAT
                 MOVE  CRE-CLAVE           TO MENS-KEY
                 MOVE  100                 TO RETURN-CODE
           ELSE
              IF FS-MAECRED = 10
                 MOVE 'SI'             TO FIN-CREDITO
              ELSE
                 ADD 1 TO LEIDOS.

       CC-LEER-B160.
      *-------------
           READ   SAM-ENTRADA AT END MOVE 'SI' TO FIN-B160.


       CD-PROCESO-PAREO.
      *--------------
             IF CRE-CLAVE = SAM-CLAVE
                ADD 1 TO IGUALES
                PERFORM CG-PROCESO-IGUAL
             ELSE
                IF CRE-CLAVE < SAM-CLAVE
                   PERFORM CE-PROCESO-MENOR
                ELSE
                   PERFORM CF-PROCESO-MAYOR.

       CE-PROCESO-MENOR.
      *------------------
           MOVE CRE-HABER             TO VALOR-A


           MOVE 'NBD'                TO OBS-S
           ADD 1              TO CT-NOBD
           PERFORM CH-IMPRIME-LINEA-MAECRED
           MOVE SPACES      TO LINEA
           WRITE LINEA      AFTER 1
           ADD 1 TO CL.

           PERFORM CB-LEER-CREDITOS.

       CF-PROCESO-MAYOR.
      *-----------------
           IF SAM-SALDO    NOT = ZEROES
              MOVE ZEROES      TO WS-FVEN
              MOVE 'NMC'       TO OBS-S
              ADD 1            TO CT-NOMAE
              PERFORM CI-IMPRIME-LINEA-BASE-DATOS
              MOVE SPACES      TO LINEA
              WRITE LINEA      AFTER 1
              ADD 1 TO CL.

           PERFORM CC-LEER-B160.

       CG-PROCESO-IGUAL.
      *------------------
           MOVE CRE-HABER             TO VALOR-A
           MOVE SAM-SALDO             TO VALOR-B

           MOVE 0                     TO SW-IMP

           COMPUTE WS-VALOR = VALOR-A - VALOR-B

           IF VALOR-A = VALOR-B
              ADD 1                   TO VAL-IGUALES
           ELSE
      ***** PRP.INI. MODIF ENE.97. DDE NO REVISA MONTO
              IF SAM-OPE NOT = 107
      ***** PRP.FIN
                 ADD 1                   TO VAL-DISTINTOS
                 MOVE 1                  TO SW-IMP.

           IF (SW-IMP = 1) OR (SW-DD = 1) OR (SW-DI = 1)
              MOVE 'DIF'                TO OBS-S
              PERFORM CJ-RTN-IMPRESION.


           PERFORM CB-LEER-CREDITOS
           PERFORM CC-LEER-B160.




       CJ-RTN-IMPRESION.
      *-----------------
           PERFORM CI-IMPRIME-LINEA-BASE-DATOS
           PERFORM CH-IMPRIME-LINEA-MAECRED.
           MOVE SPACES      TO LINEA
           WRITE LINEA      AFTER 1
           ADD 1 TO CL.

       CK-ENCABEZADO.
      *------------
           ADD 1   TO CP.
           MOVE CP TO PAG-T1
           WRITE LINEA FROM TIT-1 AFTER CANAL-1
           WRITE LINEA FROM TIT-2 AFTER 1
           WRITE LINEA FROM RAYA  AFTER 2
           WRITE LINEA FROM TIT-3 AFTER 2
           WRITE LINEA FROM TIT-4 AFTER 2
           WRITE LINEA FROM RAYA  AFTER 1
           MOVE 10 TO CL.


       CI-IMPRIME-LINEA-BASE-DATOS.
      *-------------------------
           IF CL > 60
              PERFORM CK-ENCABEZADO.
           MOVE SAM-CODIGO-OPER      TO CLAVE-S
           MOVE SAM-SUC              TO SUC-S
           MOVE SAM-RUT-DIG          TO RUTDD-S
           MOVE SAM-RUT-VER          TO DVDD-S
           MOVE SAM-NUM-ACOL         TO ANOC-S
           MOVE SAM-NUM-MCOL         TO MESC-S
           MOVE SAM-NUM-DCOL         TO DIAC-S
           MOVE SAM-CLAVE-BD         TO CLAVE-BD
           MOVE 00                   TO ANOV-S
           MOVE 00                   TO MESV-S
           MOVE 00                   TO DIAV-S

           MOVE SAM-NUM-VCOR            TO CCURSE-S
           MOVE SAM-NUM-VCVG            TO CPEND-S
           MOVE SAM-NUM-TVCC            TO CPAG-S

           MOVE SAM-VAL-CRED         TO MONTO-S
           MOVE SAM-SALDO            TO SALDO-S
           MOVE SAM-STATUS           TO STATUS-S
           WRITE LINEA FROM DETA-1    AFTER 1
           ADD 1 TO CL.

       CH-IMPRIME-LINEA-MAECRED.
      *-------------------------
           IF CL > 60
              PERFORM CK-ENCABEZADO.
           MOVE CRE-CODIGO-OPER      TO CLAVE-S
           MOVE CRE-SUCURSAL         TO SUC-S
           MOVE CRE-RUT-DD-NUM       TO RUTDD-S
           MOVE CRE-RUT-DD-VER       TO DVDD-S
           MOVE CRE-FECH-CURSE-AA    TO ANOC-S
           MOVE CRE-FECH-CURSE-MM    TO MESC-S
           MOVE CRE-FECH-CURSE-DD    TO DIAC-S
           MOVE CRE-FECH-PROX-VENC-AA TO ANOV-S
           MOVE CRE-FECH-PROX-VENC-MM TO MESV-S
           MOVE CRE-FECH-PROX-VENC-DD TO DIAV-S

           MOVE SPACES                  TO CLAVE-BD
           MOVE CRE-TOTAL-CUOTAS        TO CCURSE-S
           MOVE CRE-NUM-CUOT-PENDIENTES TO CPEND-S
           MOVE CRE-NUM-CUOT-PAGADAS    TO CPAG-S

           MOVE CRE-MONTO-COLOC      TO MONTO-S
           MOVE VALOR-A              TO SALDO-S
           MOVE SPACES               TO STATUS-S
           WRITE LINEA FROM DETA-1    AFTER 1
           ADD 1 TO CL.

       CZ-FIN-SECCION.
      *---------------
           EXIT.

      *
      *
       D-TERMINAR    SECTION.
      *
      *
       DA-CONTROL.
      *-------------
           DISPLAY 'PAREADOR DE CREDITOS'
           DISPLAY 'LEIDOS MAESTRO       : ' LEIDOS
           DISPLAY 'LEIDOS BD            : ' LEIDOS-BD
           DISPLAY 'NO EXISTEN EN OPC    : ' CT-NOBD
           DISPLAY 'NO EXISTEN EN MAE    : ' CT-NOMAE
           DISPLAY 'CLAVES IGUALES       : ' IGUALES
           DISPLAY '------------------------------------'
           DISPLAY 'SALDOS IGUALES       : ' VAL-IGUALES
           DISPLAY 'SALDOS DISTINTOS     : ' VAL-DISTINTOS
           DISPLAY 'CUOT. CURSE IGUALES  : ' VCOR-IGUALES
           DISPLAY 'CUOT. CURSE DISTINT. : ' VCOR-DISTINTOS
           DISPLAY 'CUOT. PEND. IGUALES  : ' VCVG-IGUALES
           DISPLAY 'CUOT. PEND. DISTINT. : ' VCVG-DISTINTOS
           DISPLAY 'CUOT. PAG.  IGUALES  : ' TVCC-IGUALES
           DISPLAY 'CUOT. PAG.  DISTINT. : ' TVCC-DISTINTOS
           DISPLAY ' '
           DISPLAY 'RUT DD IGUALES  :' CONT-DD-IGUALES
           DISPLAY 'RUT DD DISTINTOS:' CONT-DD-DISTINTOS
           DISPLAY 'RUT DI IGUALES  :' CONT-DI-IGUALES
           DISPLAY 'RUT DI DISTINTOS:' CONT-DI-DISTINTOS
           DISPLAY '                 '.

           IF RETURN-CODE = 100
              DISPLAY '****  PROGRAMA CANCELADO  COLPBPA2   *****'
              DISPLAY '   '
              DISPLAY 'COMANDO  :           '     MENS-PROC
              DISPLAY 'TABLA    :           '     MENS-ARCH
              DISPLAY 'COD. RET.:           '     MENS-STAT
              DISPLAY 'CLAVE    :           '     MENS-KEY
           ELSE
              DISPLAY '*****    PROCESO TERMINO OK      ******'.



           CLOSE MAECRED SAM-ENTRADA
                 LISTADO.

       DZ-FIN-SECCION.
      *---------------
           EXIT.
       FIN-PROGRAMA.

