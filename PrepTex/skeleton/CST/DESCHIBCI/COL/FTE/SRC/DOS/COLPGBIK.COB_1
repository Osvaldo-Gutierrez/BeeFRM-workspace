       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGBIK.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    31-OCT-1996.
      * Programa Generador de Interfaz para el Devengo.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DVG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY COLBRDVG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWICB.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY TABBROFI.
       COPY COLBRFZE.
       COPY GNSBRMSC.

       01  WSS-VARI.
           03 WSS-SW-DVG-VACIO  VALUE 'N' PIC X(01).
           03 WSS-FLD-T003.
              05 WS3-SGV-DEBE   VALUE ZEROES PIC S9(13)V9(02).
              05 WS3-SGV-HABE   VALUE ZEROES PIC S9(13)V9(02).
              05 FILLER         PIC X(130).
           03 WSS-FEC-FINF.
              05 WSS-NUM-DINF                    PIC 9(02).
              05 WSS-NUM-MINF                    PIC 9(02).
              05 WSS-NUM-SINF                    PIC 9(02).
              05 WSS-NUM-AINF                    PIC 9(02).
      * DPM ini  3-MAR-1998
           03 WSS-FEC-FHBL.
              05 WSS-NUM-DHBL                    PIC 9(02).
              05 WSS-NUM-MHBL                    PIC 9(02).
              05 WSS-NUM-SHBL                    PIC 9(02).
              05 WSS-NUM-AHBL                    PIC 9(02).
      * DPM fin  3-MAR-1998
           03 WSS-FEC-INTE.
              05 WSS-FEC-DIA                     PIC 9(02).
              05 WSS-FEC-MES                     PIC 9(02).
180698*       05 WSS-FEC-SIG                     PIC 9(02).
              05 WSS-FEC-ANO                     PIC 9(02).
           03 WSS-FEC-FIN2.
              05 WSS-NUM-SIN2                    PIC 9(02).
              05 WSS-NUM-AIN2                    PIC 9(02).
              05 WSS-NUM-MIN2                    PIC 9(02).
              05 WSS-NUM-DIN2                    PIC 9(02).
           03 WSS-COD-OFOC                       PIC X(03).
           03 WSS-COD-OFIC                       PIC X(03).
           03 WSS-COD-NOPE                       PIC X(12).
           03 WSS-CIC-CCSB                       PIC X(12).
           03 WSS-NUM-CONT        VALUE ZEROES   PIC 9(04).
           03 WSS-COD-SUCC.
              05 WSS-NUM-CERO                    PIC 9(01).
              05 WSS-COD-SUCL                    PIC X(03).
           03 WSS-SGV-DDR1          VALUE ZEROES PIC S9(13)V9(02).
           03 WSS-SGV-HHR1          VALUE ZEROES PIC S9(13)V9(02).
           03 WSS-TOT-DEBE          VALUE ZEROES PIC S9(13)V9(02).
           03 WSS-TOT-HABE          VALUE ZEROES PIC S9(13)V9(02).
           03 WSS-NUM-REGC          VALUE ZEROES PIC 9(05).
           03 WSS-NUM-REGA          VALUE ZEROES PIC 9(05).
           03 WSS-COD-GRUP          VALUE SPACES PIC X(03).
      * Totalizador de "DEBE" para Interfaz
           03 WSS-TOT-FZE           VALUE ZEROES PIC 9(14)V9(02).
           03 WSS-NUM-CCON          VALUE SPACES PIC X(12).
           03 WSS-NUM-NVIG          VALUE ZEROES PIC 9(08).
      * Numero de Cuenta Contable X(12) ..X(08)
           03 WSS-COD-CTA           VALUE SPACES PIC X(09).
           03 WSS-RED-CTA REDEFINES WSS-COD-CTA  PIC 9(09).
           03 WSS-GLS-AUX.
              05 WSS-GLS-DSC        VALUE SPACES PIC X(40).
              05 WSS-GLS-ADI        VALUE SPACES PIC X(30).
           03 WSS-COD-OFIC2      VALUE SPACES PIC X(03).
           03 WSS-NUM-VIGENTE.
              05 WSS-NUM-VIGE                PIC 9(06).
              05 WSS-GLS-VIGE  VALUE SPACES  PIC X(06).
      * Contadores para las Estadisticas.
           03 WSS-NUM-RLEI      VALUE ZEROES PIC 9(06).

      *
      * Definicion de campos para archivo de Interfaz para Contabilidad.
      ****************
      *** CABECERA ***
      ****************
      *   CABECERA DE LOTE (BATCH HEADER)
           03 WSS-FZE-CABECERA.
      *      CABECERA-LOTE
              05 WSS-FZE-COD-CABE     VALUE 'BH'     PIC X(02).
      *      COMPANIA
              05 WSS-FZE-COD-CCIA     VALUE 'BCI '   PIC X(04).
      *      COD.IDENTIFICACION
              05 WSS-FZE-COD-IDEN                    PIC X(02).
      *      NUMERO DE LOTE
              05 WSS-FZE-NUM-LOTE                    PIC 9(02).
      *      TIPO DE MOVIMIENTO
              05 WSS-FZE-COD-MVTO     VALUE 2        PIC 9(01).
      *      TOTAL DE LOTE
              05 WSS-FZE-SGV-LOTE     VALUE ZEROES   PIC 9(14)V9(02).
      *      PERIODO
              05 WSS-FZE-COD-PERI     VALUE 0        PIC 9(01).
      *      FECHA
              05 WSS-FZE-FEC-FECH                    PIC X(06).
180698*       05 WSS-FZE-FEC-FECH                    PIC X(08).
      ******************************
      *** DETALLE DEL MOVIMIENTO ***     (*** PRIMER REGISTRO ***)
      ******************************
           03 WSS-FZE-PMERREG.
      *      IDENTIFICACION DEL REGISTRO.
              05 WSS-FZE-COD-IDE1     VALUE 'PT'     PIC X(02).
      *      NUMERO DE MOVIMIENTO
              05 WSS-FZE-NUM-MOV1                    PIC 9(04).
      *      PRIMER REGISTRO
              05 WSS-FZE-NUM-REG1     VALUE 1        PIC 9(01).
      *      DEBE-HABER
              05 WSS-FZE-NUM-DEHA                    PIC 9(02).
      *      COMPANIA
              05 WSS-FZE-COD-CIA1     VALUE 'BCI '   PIC X(04).
      *      CUENTA
              05 WSS-FZE-COD-CCSB                    PIC 9(09).
      *      BLANCOS
              05 WSS-FZE-COD-BLCO     VALUE SPACES   PIC X(09).
      *      SUCURSAL
              05 WSS-FZE-COD-SUCU                    PIC X(12).
      *      SOURCE CODE
              05 WSS-FZE-COD-CODE                    PIC X(10).
      *      FECHA
              05 WSS-FZE-FEC-FCTB                    PIC X(06).
180698*       05 WSS-FZE-FEC-FCTB                    PIC X(08).
      *      MONTO
              05 WSS-FZE-NUM-MONT     VALUE ZEROES   PIC 9(13)V9(02).
      ************************
      *** SEGUNDO REGISTRO ***
      ************************
           03 WSS-FZE-SGDOREG.
      *      IDENTIFICACION DEL REGISTRO
              05 WSS-FZE-COD-IDE2     VALUE 'PT'     PIC X(02).
      *      NUMERO DEL MOVIMIENTO
              05 WSS-FZE-NUM-MOV2                    PIC 9(04).
      *      SEGUNDO REGISTRO
              05 WSS-FZE-NUM-REG2     VALUE 2        PIC 9(01).
      *      GLOSA
              05 WSS-FZE-GLS-GLOS                    PIC X(30).
      *      VIGENTE
              05 WSS-FZE-COD-VIGE                    PIC X(12).
      *      DESCRIPCION 3
              05 WSS-FZE-COD-DESC                    PIC X(10).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
      *>>>>

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGBIK' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM INI-VAR.
           PERFORM INP-SRT.
           PERFORM OUT-SRT.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************
       INI-VAR SECTION.
      ****************
           DISPLAY '============================'.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
           DISPLAY '============================'.
      * Captura Fecha de Proceso
           DISPLAY 'INGRESE FECHA DEL INFORME (DDMMSSAA) '.
           DISPLAY '-------------------------------------'.
           ACCEPT  WSS-FEC-FINF IN WSS-VARI.
           DISPLAY 'FECHA : ' WSS-FEC-FINF IN WSS-VARI.
      * Captura Codigo de Oficina
           DISPLAY 'OFICINA (*=TODAS): '.
           DISPLAY '-------------------'.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'OFICINA : ' WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'FIN    CAPTURA DE PARAMETROS'.
           DISPLAY '==============================='.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
           DISPLAY '==============================='.

      *VAL-FEC Valida Fecha de Informe
           DISPLAY 'FECHA DEL INFORME : ' WSS-FEC-FINF IN WSS-VARI.
           MOVE WSS-FEC-FINF IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-OKS
               MOVE WSS-FEC-FINF IN WSS-VARI TO WSS-FEC-FHBL IN WSS-VARI
               GO TO NXT-DIA-HBL.

      * DPM ini  3-MAR-1998
       BUS-DIA-HBL.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM CAL-FEC.

      *VAL-FEC Validar Fecha
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               GO TO BUS-DIA-HBL.

           MOVE FEC-FECH TO WSS-FEC-FHBL IN WSS-VARI.

       NXT-DIA-HBL.
      * DPM fin  3-MAR-1998

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*' AND
              WSS-COD-OFIC IN WSS-VARI > SPACES
               DISPLAY 'OFICINA : ' WSS-COD-OFIC IN WSS-VARI
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-ABT.
           DISPLAY '============================'.
           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
           DISPLAY '============================'.
       FIN-INI-VAR.
           EXIT.

      ******************************************************************
       OPEN-FILE SECTION.
      ******************
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-FZE.
       FIN-OPEN-FILE.
           EXIT.

      ******************************************************************
       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-DVG.
      * Verifica que DVG tenga Datos.
           IF NOT FIO-STAT-OKS
               DISPLAY ' ARCHIVO DE ENTRADA COLDVG VACIO '
               DISPLAY 'FIN DEL PROCESO...'
               DISPLAY ' '
               MOVE 'S' TO WSS-SW-DVG-VACIO IN WSS-VARI
               GO TO FIN-INP-SRT.

           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               PERFORM POS-OFI-PAR.

           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN REGISTROS EN DVG CON OFICINA '
                            WSS-COD-OFIC IN WSS-VARI
               DISPLAY 'PROCESO CANCELADO.'
               MOVE 'S' TO WSS-SW-DVG-VACIO IN WSS-VARI
               GO TO FIN-INP-SRT.

      * Almacena codigo de Oficina y Numero de Operacion.
           MOVE DVG-COD-OFOC IN DVG TO WSS-COD-OFOC IN WSS-VARI.
           MOVE DVG-CIC-IOPC IN DVG TO WSS-COD-NOPE IN WSS-VARI.
      * Se mueve Fecha de Proceso a Fecha Auxiliar para su validacion
      * con Fecha Contable (DVG).
           MOVE WSS-NUM-DINF IN WSS-VARI TO WSS-NUM-DIN2 IN WSS-VARI.
           MOVE WSS-NUM-MINF IN WSS-VARI TO WSS-NUM-MIN2 IN WSS-VARI.
           MOVE WSS-NUM-AINF IN WSS-VARI TO WSS-NUM-AIN2 IN WSS-VARI.
           MOVE WSS-NUM-SINF IN WSS-VARI TO WSS-NUM-SIN2 IN WSS-VARI.
      * Valida Fecha en Archivo con la del Parametro JCL.
           IF DVG-FEC-FCTB IN DVG NOT = WSS-FEC-FIN2 IN WSS-VARI
              DISPLAY ' **** FECHA CONTABLE  EN  ARCHIVO ES '
              DISPLAY ' **** DISTINTA A LA FECHA DE PROCESO '
              DISPLAY ' **** SEGUN PARAMETRO............... '
              DISPLAY ' **** EL PROCESO SE AUTOCANCELA      '
              DISPLAY 'FECHA PARAMETRO : ' WSS-FEC-FIN2 IN WSS-VARI
              DISPLAY 'FECHA EN MOVTOS : ' DVG-FEC-FCTB IN DVG
              PERFORM GNS-PRO-ABT.
           MOVE DVG-CIC-CCSB IN DVG TO WSS-CIC-CCSB IN WSS-VARI.
           MOVE DVG-COD-OFOC IN DVG TO WSS-COD-OFOC IN WSS-VARI.

      * DPM ini  3-MAR-1998
           MOVE WSS-NUM-DHBL IN WSS-VARI TO WSS-NUM-DIN2 IN WSS-VARI.
           MOVE WSS-NUM-MHBL IN WSS-VARI TO WSS-NUM-MIN2 IN WSS-VARI.
           MOVE WSS-NUM-AHBL IN WSS-VARI TO WSS-NUM-AIN2 IN WSS-VARI.
           MOVE WSS-NUM-SHBL IN WSS-VARI TO WSS-NUM-SIN2 IN WSS-VARI.
      * DPM fin  3-MAR-1998

       LUP-INP-SRT.
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*' AND
              DVG-COD-OFOC IN DVG NOT = WSS-COD-OFIC IN WSS-VARI
               GO TO FIN-INP-SRT.

           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.
           PERFORM GRABA-REG-3.
           PERFORM PROCESA-REG-3.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           IF FIO-STAT-OKS
               GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           EXIT.

      ******************************************************************
       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF WSS-SW-DVG-VACIO IN WSS-VARI = 'S'
               GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
           PERFORM PROCESA-ULT-ANTERIOR.
           PERFORM MUEVE-CABECERA.
           MOVE WSS-TOT-FZE IN WSS-VARI TO WSS-FZE-SGV-LOTE IN WSS-VARI.
           MOVE WSS-FZE-CABECERA IN WSS-VARI TO FZE-FLD-REG IN FZE.
           PERFORM WRITE-INTERFAZ.
       FIN-OUT-SRT.
           EXIT.

      ******************************************************************
       GRABA-REG-3 SECTION.
      ********************
       INI-GRABA-REG-3.
           IF DVG-SGV-ICTB IN DVG NOT NUMERIC
               MOVE ZEROES TO DVG-SGV-ICTB IN DVG.
           IF DVG-IND-DBHB IN DVG = 'D'
              MOVE DVG-SGV-ICTB IN DVG   TO WS3-SGV-DEBE IN WSS-VARI
              MOVE ZEROES                TO WS3-SGV-HABE IN WSS-VARI
      * Acumula para total de Interfaz
              COMPUTE WSS-TOT-FZE IN WSS-VARI = WSS-TOT-FZE IN WSS-VARI
                                                + DVG-SGV-ICTB IN DVG
           ELSE
              MOVE DVG-SGV-ICTB IN DVG   TO WS3-SGV-HABE IN WSS-VARI
              MOVE ZEROES                TO WS3-SGV-DEBE IN WSS-VARI.
       FIN-GRABA-REG-3.
           EXIT.

      ******************************************************************
       PROCESA-ULT-ANTERIOR SECTION.
      *****************************
       INI-PRO-ULT-ANT.
           PERFORM GRABA-INTERFAZ.
       FIN-PRO-ULT-ANT.
           EXIT.

      ******************************************************************
       PROCESA-REG-3 SECTION.
      **********************
       INI-PROCESA-REG-3.
           IF DVG-COD-OFOC IN DVG NOT = WSS-COD-OFOC IN WSS-VARI
              PERFORM GRABA-INTERFAZ
              MOVE ZEROES              TO WSS-TOT-DEBE IN WSS-VARI
              MOVE ZEROES              TO WSS-TOT-HABE IN WSS-VARI
              MOVE ZEROES              TO WSS-SGV-DDR1 IN WSS-VARI
              MOVE ZEROES              TO WSS-SGV-HHR1 IN WSS-VARI
              MOVE DVG-CIC-CCSB IN DVG TO WSS-CIC-CCSB IN WSS-VARI
              MOVE DVG-COD-OFOC IN DVG TO WSS-COD-OFOC IN WSS-VARI
           ELSE
           IF DVG-CIC-CCSB IN DVG NOT = WSS-CIC-CCSB IN WSS-VARI
               PERFORM GRABA-INTERFAZ
               MOVE DVG-CIC-CCSB IN DVG TO WSS-CIC-CCSB IN WSS-VARI
               MOVE ZEROES              TO WSS-SGV-DDR1 IN WSS-VARI
               MOVE ZEROES              TO WSS-SGV-HHR1 IN WSS-VARI.

           COMPUTE WSS-SGV-DDR1 IN WSS-VARI = WSS-SGV-DDR1 IN WSS-VARI +
                   WS3-SGV-DEBE IN WSS-VARI.
           COMPUTE WSS-SGV-HHR1 IN WSS-VARI = WSS-SGV-HHR1 IN WSS-VARI +
                   WS3-SGV-HABE IN WSS-VARI.
           COMPUTE WSS-TOT-DEBE IN WSS-VARI = WSS-TOT-DEBE IN WSS-VARI +
                   WS3-SGV-DEBE IN WSS-VARI.
           COMPUTE WSS-TOT-HABE IN WSS-VARI = WSS-TOT-HABE IN WSS-VARI +
                   WS3-SGV-HABE IN WSS-VARI.
       FIN-PROCESA-REG-3.
           EXIT.

      ******************************************************************
       GRABA-INTERFAZ SECTION.
      ************************
       INI-GRABA-INTERFAZ.
      *************************
      *** DATOS DE CABECERA ***
      *************************
      * Contador para numero de movimientos.
           ADD 1 TO WSS-NUM-CONT IN WSS-VARI.
           IF WSS-SGV-DDR1 IN WSS-VARI > 0
              PERFORM MUEVE-REG-1
              MOVE 10     TO WSS-FZE-NUM-DEHA IN WSS-VARI
              MOVE WSS-SGV-DDR1 IN WSS-VARI TO WSS-FZE-NUM-MONT
                                                         IN WSS-VARI
              MOVE WSS-FZE-PMERREG IN WSS-VARI TO FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ
              PERFORM MUEVE-REG-2
              MOVE WSS-FZE-SGDOREG IN WSS-VARI TO FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ.
           IF WSS-SGV-HHR1 IN WSS-VARI > 0
              PERFORM MUEVE-REG-1
              MOVE 60 TO WSS-FZE-NUM-DEHA IN WSS-VARI
              MOVE WSS-SGV-HHR1 IN WSS-VARI TO WSS-FZE-NUM-MONT
                                                         IN WSS-VARI
              MOVE WSS-FZE-PMERREG IN WSS-VARI TO FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ
              PERFORM MUEVE-REG-2
              MOVE WSS-FZE-SGDOREG IN WSS-VARI TO FZE-FLD-REG IN FZE
              PERFORM WRITE-INTERFAZ.
           GO TO FIN-GRABA-INTERFAZ.
        MUEVE-REG-1.
      ***********************
      *** PRIMER REGISTRO ***
      ***********************
           MOVE 'PT'                TO WSS-FZE-COD-IDE1  IN WSS-VARI.
           MOVE WSS-NUM-CONT IN WSS-VARI
                                    TO WSS-FZE-NUM-MOV1  IN WSS-VARI.
           MOVE 1                   TO WSS-FZE-NUM-REG1  IN WSS-VARI.
           MOVE 'BCI '              TO WSS-FZE-COD-CIA1  IN WSS-VARI.
           MOVE WSS-CIC-CCSB IN WSS-VARI TO WSS-COD-CTA  IN WSS-VARI.
           MOVE WSS-RED-CTA  IN WSS-VARI TO WSS-FZE-COD-CCSB
                                                         IN WSS-VARI.
           MOVE SPACES              TO WSS-FZE-COD-BLCO  IN WSS-VARI.
           MOVE 0                   TO WSS-NUM-CERO      IN WSS-VARI.
           MOVE WSS-COD-OFOC IN WSS-VARI TO WSS-COD-SUCL IN WSS-VARI.
           MOVE WSS-COD-SUCC IN WSS-VARI TO WSS-FZE-COD-SUCU
                                                          IN WSS-VARI.
      * SE REEMPLAZA COD.DE SISTEMA (SISTCOL) POR (CLC)
      *     MOVE 'SISTCOL' TO WSS-FZE-COD-CODE IN WSS-VARI.
           MOVE 'CLC' TO WSS-FZE-COD-CODE IN WSS-VARI.
           MOVE WSS-NUM-DIN2 IN WSS-VARI TO WSS-FEC-DIA  IN WSS-VARI.
           MOVE WSS-NUM-MIN2 IN WSS-VARI TO WSS-FEC-MES  IN WSS-VARI.
           MOVE WSS-NUM-AIN2 IN WSS-VARI TO WSS-FEC-ANO  IN WSS-VARI.
180698*    MOVE WSS-NUM-SIN2 IN WSS-VARI TO WSS-FEC-SIG  IN WSS-VARI.
           MOVE WSS-FEC-INTE TO WSS-FZE-FEC-FCTB IN WSS-VARI.
       MUEVE-REG-2.
      ************************
      *** SEGUNDO REGISTRO ***
      ************************
           MOVE 'PT'      TO WSS-FZE-COD-IDE2 IN WSS-VARI.
           MOVE WSS-NUM-CONT IN WSS-VARI
                                     TO WSS-FZE-NUM-MOV2 IN WSS-VARI.
           MOVE 2                    TO WSS-FZE-NUM-REG2 IN WSS-VARI.
      * Se mueve glosa especifica a glosa de interfaz.
           MOVE 'CONTABILIDAD AUTOMATICA DE COLOCACIONES'
                                     TO WSS-FZE-GLS-GLOS IN WSS-VARI.
           MOVE WSS-NUM-NVIG IN WSS-VARI TO WSS-NUM-VIGE IN WSS-VARI.
           MOVE WSS-NUM-VIGENTE IN WSS-VARI TO WSS-FZE-COD-VIGE
                                                         IN WSS-VARI.
           MOVE SPACES TO WSS-FZE-COD-DESC IN WSS-VARI.
       WRITE-INTERFAZ.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-FZE.
       FIN-GRABA-INTERFAZ.
           EXIT.

      ******************************************************************
       MUEVE-CABECERA SECTION.
      ***********************
       INI-MUEVE-CABECERA.
              MOVE 'BH'    TO WSS-FZE-COD-CABE IN WSS-VARI.
              MOVE 'BCI '  TO WSS-FZE-COD-CCIA IN WSS-VARI.
              MOVE '01'    TO WSS-FZE-COD-IDEN IN WSS-VARI.
              MOVE 01      TO WSS-FZE-NUM-LOTE IN WSS-VARI.
              MOVE 2       TO WSS-FZE-COD-MVTO IN WSS-VARI.
              MOVE 0       TO WSS-FZE-COD-PERI IN WSS-VARI.
              MOVE WSS-NUM-DIN2 IN WSS-VARI
                           TO WSS-FEC-DIA IN WSS-VARI.
              MOVE WSS-NUM-MIN2 IN WSS-VARI
                           TO WSS-FEC-MES IN WSS-VARI.
              MOVE WSS-NUM-AIN2 IN WSS-VARI
                           TO WSS-FEC-ANO IN WSS-VARI.
180698*       MOVE WSS-NUM-SIN2 IN WSS-VARI
180698*                    TO WSS-FEC-SIG IN WSS-VARI.
              MOVE WSS-FEC-INTE IN WSS-VARI
                           TO WSS-FZE-FEC-FECH IN WSS-VARI.
       FIN-MUEVE-CABECERA.
           EXIT.

      ******************************************************************
       CLOSE-FILE SECTION.
      *******************
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-FZE.
           PERFORM COL-FIO-DVG.
       FIN-CLOSE-FILE.
           EXIT.

      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *****************************
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY ' '.
           DISPLAY ' ESTADISTICAS DEL PROCESO.'.
           DISPLAY ' -------------------------'.
           DISPLAY ' '.
           DISPLAY 'REGISTROS LEIDOS DESDE ARCHIVO DVG    : '
                                        WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'MOVIMIENTOS GRABADOS EN INTERFAZ      : '
                                        WSS-NUM-CONT IN WSS-VARI.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.

      ******************************************************************

       POS-OFI-PAR SECTION.
      ********************
       INI-POS-OFI-PAR.
           IF NOT FIO-STAT-OKS
               GO TO FIN-POS-OFI-PAR.

           IF DVG-COD-OFOC IN DVG NOT = WSS-COD-OFIC IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               PERFORM COL-FIO-DVG
               GO TO INI-POS-OFI-PAR.
       FIN-POS-OFI-PAR.
           EXIT.

      ******************************************************************

       COPY GNSBGDTC.
       COPY GNSBTABT.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY COLBFDVG.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY COLBFFZE.
       COPY TABBFOFI.
       COPY TABBBOFI.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGIDD.
       COPY GNSBFMSC.
       COPY TABBVOFI.
       COPY GNSBBMSG.
       COPY GNSBBTAB.

