       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGDVL.
       AUTHOR.          CONSIST (JGM).
       DATE-WRITTEN.    27-Feb-98 12:30 PM.

      * Programa Listador de Reporte DVG con Sort.
      *
      * V 1.1.2
      *
      *<<<< CMT_ID
      *>>>>

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            C01 IS SALTO
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DVG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGGRC.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGSYS.
      *
       COPY COLBRDVG.
       COPY COLBRDVL.
       01  PGM-HRSR-DVG   VALUE 'N'       PIC X(01).
           88 HAY-REGISTROS-EN-SORT-DVG  VALUE 'S'.

       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       01  WSS-VARI.
           03  WSS-NUM-RLEI       VALUE ZEROES    PIC 9(03).
           03  WSS-NUM-RSEL       VALUE ZEROES    PIC 9(03).
           03  WSS-IND-VACIO      VALUE 'N'       PIC X(01).
           03  WSS-IND-LSIG       VALUE 'N'       PIC X(01).

      * Variables para emulacion de archivo MCD para comparar con
      * CIC de tabla ALG para saber el concepto a grabar en DVG
           03 WSS-NUM-GDVL       VALUE ZEROES    PIC 9(03).
           03 WSS-IND-IDN                         PIC X(03).
           03 WSS-IND-RDN                         PIC X(03).
           03 WSS-IND-IDS                         PIC X(03).
           03 WSS-IND-RDS                         PIC X(03).
           03 WSS-IND-IDM                         PIC X(03).
           03 WSS-IND-RDM                         PIC X(03).
           03 WSS-COD-PESO                        PIC X(06).
           03 WSS-MSC-MNAC                        PIC X(01).

           03 WSS-CIC-IOPC                        PIC X(12).
           03 WSS-COD-OFOC                        PIC X(03).
           03 WSS-MSC-TCCT                        PIC X(03).
           03 WSS-FEC-FVEN.
              05  WSS-NUM-SVEN   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-AVEN   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-MVEN   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-DVEN   VALUE ZEROES    PIC 9(02).
           03 WSS-FEC-FDEV.
              05  WSS-NUM-SDEV   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-ADEV   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-MDEV   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-DDEV   VALUE ZEROES    PIC 9(02).
           03 WSS-FEC-FCOL.
              05  WSS-NUM-SCOL   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-ACOL   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-MCOL   VALUE ZEROES    PIC 9(02).
              05  WSS-NUM-DCOL   VALUE ZEROES    PIC 9(02).
           03 WSS-IND-SCTB                       PIC X(03).
           03 WSS-IND-SCBZ                       PIC X(05).
           03 WSS-IND-SCRT                       PIC X(03).
           03 WSS-COD-PLZO.
              05 WSS-MSC-TPZO                    PIC X(01).
              05 WSS-MSC-CPZO                    PIC X(01).
           03 WSS-COD-VCOC.
              05 WSS-MSC-TVOC                    PIC X(01).
              05 WSS-MSC-CVOC                    PIC X(03).
              05 WSS-MSC-TCOC                    PIC X(02).
           03 WSS-COD-COOC.
              05 WSS-COD-TOOC                    PIC X(03).
              05 WSS-COD-AOOC                    PIC X(03).
           03 WSS-SGV-ICTB    VALUE ZEROES       PIC S9(13)V9(02).
           03 WSS-VAL-SCRE    VALUE ZEROES       PIC 9(11)V9(04).
           03 WSS-GLS-COOC.
              05 WSS-GLS-TOOC                    PIC X(25).
              05 WSS-GLS-AOOC                    PIC X(25).
           03 WSS-SGV-TASA    VALUE ZEROES       PIC S9(04)V9(04).
      *INI-JGM 28-03-2000.
           03 WSS-NUM-VCVG                       PIC 9(03).
           03 WSS-NUM-VCOR                       PIC 9(03).
           03 WSS-COD-CDOC                       PIC X(03).
           03 WSS-VAL-CRED      COMP-3           PIC S9(11)V9(04).
           03 WSS-SGV-FCOL                       PIC S9(11)V9(04).
           03 WSS-IND-GARA                       PIC X(03).
           03 WSS-FEC-FMAV                       PIC 9(08).
      *>>>>
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY TABBROFI.
       COPY COLBRDCC.
       COPY GNSBRMSC.
       COPY GNSWGCPT.
       COPY TABBRCAM.
       COPY GNSWGPES.

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPGDVL' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           PERFORM ABRE-ARCH.
           PERFORM INP-PROC-DVG.
           PERFORM CERRAR-ARCH.
           PERFORM ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
      *
      *
      *
       ABRE-ARCH SECTION.
       INI-ABRE-ARCH.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           PERFORM SGC-FIO-DBC.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-DVG.
      *
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-DVL.
       FIN-ABRE-ARCH.
           EXIT.
      *
       INP-PROC-DVG SECTION.
      *---------------
       INI-INP-PROC-DVG.
      *RESCATA COD DE INT NORMAL
           MOVE 'IDN'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUSCA-CIC-TCCT.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-IDN IN WSS-VARI.
      *RESCATA COD DE REAJ NORMALES
           MOVE 'RDN'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUSCA-CIC-TCCT.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-RDN IN WSS-VARI.
      *RESCATA COD DE INT SUSPENDIDOS
           MOVE 'IDS'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUSCA-CIC-TCCT.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-IDS IN WSS-VARI.
      *RESCATA COD DE REAJ SUSPENDIDOS
           MOVE 'RDS'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUSCA-CIC-TCCT.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-RDS IN WSS-VARI.
      *RESCATA COD DE INT MORATORIOS
           MOVE 'IDM'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUSCA-CIC-TCCT.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-IDM IN WSS-VARI.
      *RESCATA COD DE REAJ MORATORIOS
           MOVE 'RDM'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUSCA-CIC-TCCT.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-RDM IN WSS-VARI.

      *Rescata el codigo de PESOS
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CICPESONEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

      *Rescata codigo de Mon. Nacional
           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CICTVAL0NEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT.
           MOVE MSC-COD-CMSC IN MSC TO WSS-MSC-MNAC IN WSS-VARI.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           IF NOT FIO-STAT-OKS
               MOVE 'S' TO WSS-IND-VACIO IN WSS-VARI
               GO TO FIN-INP-DVG.

           PERFORM MOVER-DVG-WSS.

      *Obtiene Cuenta CAP
           PERFORM OBT-CUENTA-DCC.
           IF WSS-IND-LSIG IN WSS-VARI = 'S'
               GO TO NXT-INP-DVG.

      *Lee registro DBC
           PERFORM LEE-DBC.
           IF WSS-IND-LSIG IN WSS-VARI = 'S'
               GO TO NXT-INP-DVG.


           MOVE ZEROES TO DVL-VAL-INTD IN DVL
                          DVL-VAL-READ IN DVL
                          DVL-VAL-INTO IN DVL
                          DVL-VAL-REAO IN DVL
                          DVL-VAL-INTM IN DVL
                          DVL-VAL-REAM IN DVL.

           MOVE DVG-NUM-DPRO IN DVG TO DVL-NUM-DPRO IN DVL.
           MOVE DVG-NUM-MPRO IN DVG TO DVL-NUM-MPRO IN DVL
           MOVE DVG-NUM-SPRO IN DVG TO DVL-NUM-SPRO IN DVL.
           MOVE DVG-NUM-APRO IN DVG TO DVL-NUM-APRO IN DVL.
      *
       LUP-INP-DVG.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-DVG.
      *
           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

           MOVE DVG-MSC-TCCT IN DVG TO WSS-MSC-TCCT IN WSS-VARI.
           MOVE DVG-SGV-ICTB IN DVG TO WSS-SGV-ICTB IN WSS-VARI.

           IF DVG-CIC-IOPC IN DVG = WSS-CIC-IOPC IN WSS-VARI AND
              DVG-COD-OFOC IN DVG = WSS-COD-OFOC IN WSS-VARI
               PERFORM DET-TIPO-CPTO
               GO TO NXT-INP-DVG.

           IF WSS-IND-LSIG IN WSS-VARI = 'S'
               MOVE 'N' TO WSS-IND-LSIG IN WSS-VARI
               GO TO SIGUE-INP-DVG.

           IF WSS-MSC-TVOC IN WSS-VARI = WSS-MSC-MNAC IN WSS-VARI
               PERFORM DET-SALDO-EN-PESOS
               IF WSS-IND-LSIG IN WSS-VARI = 'S'
                   MOVE 'N' TO WSS-IND-LSIG IN WSS-VARI
                   GO TO NXT-INP-DVG
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE WSS-VAL-SCRE IN WSS-VARI TO DVL-VAL-SCRE IN DVL.

           PERFORM MOVER-DATOS-DVL.

           ADD 1 TO WSS-NUM-RSEL IN WSS-VARI.

       SIGUE-INP-DVG.
           MOVE ZEROES TO DVL-VAL-INTD IN DVL
                          DVL-VAL-READ IN DVL
                          DVL-VAL-INTO IN DVL
                          DVL-VAL-REAO IN DVL
                          DVL-VAL-INTM IN DVL
                          DVL-VAL-REAM IN DVL.

           PERFORM MOVER-DVG-WSS.
           PERFORM DET-TIPO-CPTO.

      *Obtiene Cuenta CAP
           PERFORM OBT-CUENTA-DCC.
           IF WSS-IND-LSIG IN WSS-VARI = 'S'
               DISPLAY 'ERROR LECTURA DCC: ' DCC-KEY-IREG IN DCC
               GO TO NXT-INP-DVG.

      *Lee registro DBC
           PERFORM LEE-DBC.
           IF WSS-IND-LSIG IN WSS-VARI = 'S'
               GO TO NXT-INP-DVG.
      *
       MOV-INP-DVG.
       NXT-INP-DVG.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-DVG.
           GO TO LUP-INP-DVG.
      *
       FIN-INP-DVG.
           IF WSS-IND-VACIO IN WSS-VARI = 'S' AND
              WSS-IND-LSIG IN WSS-VARI = 'N'
               DISPLAY 'ARCHIVO DVG VACIO...'
               DISPLAY 'PROCESO CANCELADO...'
               GO TO FIN-INP-PROC-DVG.

           IF WSS-IND-VACIO IN WSS-VARI NOT = 'S' AND
              WSS-IND-LSIG IN WSS-VARI = 'N'
               IF WSS-MSC-TVOC IN WSS-VARI = WSS-MSC-MNAC IN WSS-VARI
                  PERFORM DET-SALDO-EN-PESOS
                  IF WSS-IND-LSIG IN WSS-VARI = 'S'
                     MOVE 'N' TO WSS-IND-LSIG IN WSS-VARI
                     GO TO FIN-INP-PROC-DVG
                  ELSE
                     NEXT SENTENCE
               ELSE
                  MOVE WSS-VAL-SCRE IN WSS-VARI TO DVL-VAL-SCRE IN DVL.

           IF WSS-IND-VACIO IN WSS-VARI NOT = 'S' AND
              WSS-IND-LSIG IN WSS-VARI = 'N'
               PERFORM DET-TIPO-CPTO
               PERFORM MOVER-DATOS-DVL
               ADD 1 TO WSS-NUM-RSEL IN WSS-VARI.
       FIN-INP-PROC-DVG.
           EXIT.
      *
      *
       CERRAR-ARCH SECTION.
       INI-CERRAR-ARCH.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           PERFORM SGC-FIO-DBC.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           PERFORM COL-FIO-DVL.
       FIN-CERRAR-ARCH.
           EXIT.

       LEE-DBC SECTION.
      *---------------*
       INI-LEE-DBC.
           MOVE WSS-CIC-IOPC IN WSS-VARI TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES                   TO RDC-NUM-IDLC IN RDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           PERFORM COL-FIO-RDC.
           IF NOT (FIO-STAT-OKS AND
                   WSS-CIC-IOPC IN WSS-VARI = RDC-CIC-IOPC IN RDC)
               MOVE 'S' TO WSS-IND-LSIG IN WSS-VARI
               DISPLAY 'ERROR LECTURA RDC: ' WSS-CIC-IOPC IN WSS-VARI
               GO TO FIN-LEE-DBC.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               MOVE 'S' TO WSS-IND-LSIG IN WSS-VARI
               DISPLAY 'ERROR LECTURA DBC: ' RDC-CIC-ICLI IN RDC.
       FIN-LEE-DBC.
           EXIT.




       DET-TIPO-CPTO SECTION.
      *---------------------*
       INI-DET-TIPO-CPTO.
           IF WSS-MSC-TCCT IN WSS-VARI = WSS-IND-IDN IN WSS-VARI
               MOVE WSS-SGV-ICTB IN WSS-VARI TO DVL-VAL-INTD IN DVL
           ELSE
           IF WSS-MSC-TCCT IN WSS-VARI = WSS-IND-IDS IN WSS-VARI
               MOVE WSS-SGV-ICTB IN WSS-VARI TO DVL-VAL-INTO IN DVL
           ELSE
           IF WSS-MSC-TCCT IN WSS-VARI = WSS-IND-IDM IN WSS-VARI
               MOVE WSS-SGV-ICTB IN WSS-VARI TO DVL-VAL-INTM IN DVL
           ELSE
           IF WSS-MSC-TCCT IN WSS-VARI = WSS-IND-RDN IN WSS-VARI
               MOVE WSS-SGV-ICTB IN WSS-VARI TO DVL-VAL-READ IN DVL
           ELSE
           IF WSS-MSC-TCCT IN WSS-VARI = WSS-IND-RDS IN WSS-VARI
               MOVE WSS-SGV-ICTB IN WSS-VARI TO DVL-VAL-REAO IN DVL
           ELSE
           IF WSS-MSC-TCCT IN WSS-VARI = WSS-IND-RDM IN WSS-VARI
               MOVE WSS-SGV-ICTB IN WSS-VARI TO DVL-VAL-REAM IN DVL.
       FIN-DET-TIPO-CPTO.
           EXIT.


       OBT-CUENTA-DCC SECTION.
      *----------------------*
       INI-OBT-CUENTA-DCC.
           IF WSS-IND-SCTB IN WSS-VARI = 'MUL'
               MOVE SPACES TO DCC-IND-SCTB IN DCC
           ELSE
               MOVE WSS-IND-SCTB IN WSS-VARI TO DCC-IND-SCTB IN DCC.

           MOVE WSS-IND-SCBZ IN WSS-VARI TO DCC-IND-SCBZ IN DCC.
           MOVE WSS-IND-SCRT IN WSS-VARI TO DCC-IND-SCRT IN DCC.
           MOVE 'DCC'                    TO DCC-COD-TTAB IN DCC.
           MOVE 'CAP'                    TO DCC-MSC-TCCT IN DCC.
           MOVE WSS-COD-VCOC IN WSS-VARI TO DCC-COD-VCAM IN DCC.
           MOVE WSS-COD-COOC IN WSS-VARI TO DCC-COD-COCR IN DCC.
           MOVE WSS-COD-PLZO IN WSS-VARI TO DCC-COD-PLZO IN DCC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           IF NOT FIO-STAT-OKS
              MOVE '* ' TO DCC-COD-PLZO IN DCC
              MOVE FIO-GET-KEY TO FIO-CMND
              PERFORM COL-FIO-DCC
              IF NOT FIO-STAT-OKS
                 MOVE '*     ' TO DCC-COD-COCR IN DCC
                 MOVE FIO-GET-KEY TO FIO-CMND
                 PERFORM COL-FIO-DCC
                 IF NOT FIO-STAT-OKS
                    MOVE 'S' TO WSS-IND-LSIG IN WSS-VARI.
       FIN-OBT-CUENTA-DCC.
           EXIT.


       BUSCA-CIC-TCCT SECTION.
      *----------------------*
       INI-BUSCA-TCCT.
           MOVE 'COL'                            TO FIO-SIST.
           MOVE 'MSC'                            TO MSC-CIC-TTAB IN MSC.
           MOVE 'TCCT'                           TO MSC-CIC-TMSC IN MSC.
           MOVE FIO-GET-KEY                      TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
              MOVE 'COL'       TO MSG-COD-SIST
              MOVE 'CODTCYANX' TO MSG-COD-MENS IN MSG
              PERFORM GET-MSG
              DISPLAY MSG-GLS-DESC   MSC-COD-CMSC
              DISPLAY 'LLAVE : ' MSC-CIC-CMSC IN MSC
              PERFORM GNS-PRO-ABT.
       FIN-BUSCA-CIC-TCCT.
           EXIT.


       DET-SALDO-EN-PESOS SECTION.
      *--------------------------*
       INI-DET-SALDO-EN-PESOS.
           IF WSS-COD-VCOC IN WSS-VARI = WSS-COD-PESO IN WSS-VARI
               MOVE WSS-VAL-SCRE IN WSS-VARI TO DVL-VAL-SCRE IN DVL
               GO TO FIN-DET-SALDO-EN-PESOS.

           MOVE WSS-COD-VCOC IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE WSS-FEC-FCOL IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1)
           MOVE WSS-FEC-FCOL IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).

           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS AND CAM-IND-VIGE NOT = 'S'
              MOVE 'COL'        TO MSG-COD-SIST
              MOVE 'CAMVCAMNEX' TO MSG-COD-MENS
              PERFORM GET-MSG
              DISPLAY MSG-GLS-DESC    CAM-KEY-CAMB IN CAM
              MOVE 'S' TO WSS-IND-LSIG IN WSS-VARI
              GO TO FIN-DET-SALDO-EN-PESOS.

      *XX
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-FCOL IN WSS-VARI.
      *XX
           MULTIPLY CAM-SGV-VCAM IN CAM BY WSS-VAL-SCRE  IN WSS-VARI
                                    GIVING WSS-VAL-SCRE  IN WSS-VARI.
           MOVE WSS-VAL-SCRE IN WSS-VARI TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO DVL-VAL-SCRE IN DVL.
       FIN-DET-SALDO-EN-PESOS.
           EXIT.


       MOVER-DVG-WSS SECTION.
      *---------------------*
       INI-MOVER-DVG-WSS.
           MOVE DVG-NUM-DPRO IN DVG TO DVL-NUM-DPRO IN DVL.
           MOVE DVG-NUM-MPRO IN DVG TO DVL-NUM-MPRO IN DVL
           MOVE DVG-NUM-SPRO IN DVG TO DVL-NUM-SPRO IN DVL.
           MOVE DVG-NUM-APRO IN DVG TO DVL-NUM-APRO IN DVL.
           MOVE DVG-CIC-IOPC IN DVG TO WSS-CIC-IOPC IN WSS-VARI.
           MOVE DVG-COD-OFOC IN DVG TO WSS-COD-OFOC IN WSS-VARI.
           MOVE DVG-FEC-FVEN IN DVG TO WSS-FEC-FVEN IN WSS-VARI.
           MOVE DVG-FEC-FDEV IN DVG TO WSS-FEC-FDEV IN WSS-VARI.
           MOVE DVG-FEC-FCOL IN DVG TO WSS-FEC-FCOL IN WSS-VARI.
           MOVE DVG-IND-SCTB IN DVG TO WSS-IND-SCTB IN WSS-VARI.
           MOVE DVG-IND-SCBZ IN DVG TO WSS-IND-SCBZ IN WSS-VARI.
           MOVE DVG-IND-SCRT IN DVG TO WSS-IND-SCRT IN WSS-VARI.
           MOVE DVG-COD-PLZO IN DVG TO WSS-COD-PLZO IN WSS-VARI.
           MOVE DVG-COD-VCOC IN DVG TO WSS-COD-VCOC IN WSS-VARI.
           MOVE DVG-COD-COOC IN DVG TO WSS-COD-COOC IN WSS-VARI.
           MOVE DVG-VAL-SCRE IN DVG TO WSS-VAL-SCRE IN WSS-VARI.
           MOVE DVG-SGV-TASA IN DVG TO WSS-SGV-TASA IN WSS-VARI.
      *INI-JGM 28-03-2000.
           MOVE DVG-NUM-VCVG IN DVG TO WSS-NUM-VCVG IN WSS-VARI.
           MOVE DVG-NUM-VCOR IN DVG TO WSS-NUM-VCOR IN WSS-VARI.
           MOVE DVG-COD-CDOC IN DVG TO WSS-COD-CDOC IN WSS-VARI.
           MOVE DVG-VAL-CRED IN DVG TO WSS-VAL-CRED IN WSS-VARI.
           MOVE DVG-SGV-FCOL IN DVG TO WSS-SGV-FCOL IN WSS-VARI.
           MOVE DVG-IND-GARA IN DVG TO WSS-IND-GARA IN WSS-VARI.
           MOVE DVG-FEC-FMAV IN DVG TO WSS-FEC-FMAV IN WSS-VARI.
      *FIN-JGM 28-03-2000.
       FIN-MOVER-DVG-WSS.
           EXIT.


       MOVER-DATOS-DVL SECTION.
      *---------------------*
       INI-MOVER-DATOS-SRT.
           MOVE WSS-COD-OFOC IN WSS-VARI TO DVL-COD-OFOC IN DVL.
           MOVE WSS-CIC-IOPC IN WSS-VARI TO DVL-CIC-IOPC IN DVL.
           MOVE WSS-NUM-DVEN IN WSS-VARI TO DVL-NUM-DVEN IN DVL.
           MOVE WSS-NUM-MVEN IN WSS-VARI TO DVL-NUM-MVEN IN DVL.
           MOVE WSS-NUM-SVEN IN WSS-VARI TO DVL-NUM-SVEN IN DVL.
           MOVE WSS-NUM-AVEN IN WSS-VARI TO DVL-NUM-AVEN IN DVL.
           MOVE WSS-COD-COOC IN WSS-VARI TO DVL-COD-COOC IN DVL.
           MOVE WSS-NUM-DCOL IN WSS-VARI TO DVL-NUM-DCOL IN DVL.
           MOVE WSS-NUM-MCOL IN WSS-VARI TO DVL-NUM-MCOL IN DVL.
           MOVE WSS-NUM-SCOL IN WSS-VARI TO DVL-NUM-SCOL IN DVL.
           MOVE WSS-NUM-ACOL IN WSS-VARI TO DVL-NUM-ACOL IN DVL.
           MOVE WSS-NUM-DDEV IN WSS-VARI TO DVL-NUM-DDEV IN DVL.
           MOVE WSS-NUM-MDEV IN WSS-VARI TO DVL-NUM-MDEV IN DVL.
           MOVE WSS-NUM-SDEV IN WSS-VARI TO DVL-NUM-SDEV IN DVL.
           MOVE WSS-NUM-ADEV IN WSS-VARI TO DVL-NUM-ADEV IN DVL.
           MOVE WSS-SGV-TASA IN WSS-VARI TO DVL-SGV-TASA IN DVL.
           MOVE DBC-NUM-ICLI IN DBC      TO DVL-NUM-ICLI IN DVL.
           MOVE DBC-VRF-ICLI IN DBC      TO DVL-VRF-ICLI IN DVL.
           MOVE DCC-GLS-CCSB IN DCC      TO DVL-CIC-CCSB IN DVL.
      *
      *INI-JGM 28-03-2000.
           MOVE WSS-NUM-VCVG IN WSS-VARI TO DVL-NUM-VCVG IN DVL.
           MOVE WSS-NUM-VCOR IN WSS-VARI TO DVL-NUM-VCOR IN DVL.
           MOVE WSS-IND-SCTB IN WSS-VARI TO DVL-IND-SCTB IN DVL.
           MOVE WSS-COD-CDOC IN WSS-VARI TO DVL-COD-CDOC IN DVL.
           MOVE WSS-COD-VCOC IN WSS-VARI TO DVL-COD-VCOC IN DVL.
           MOVE WSS-VAL-CRED IN WSS-VARI TO DVL-VAL-CRED IN DVL.
           MOVE WSS-SGV-FCOL IN WSS-VARI TO DVL-SGV-FCOL IN DVL.
           MOVE WSS-IND-GARA IN WSS-VARI TO DVL-IND-GARA IN DVL.
           MOVE WSS-FEC-FMAV IN WSS-VARI TO DVL-FEC-FMAV IN DVL.
           MOVE WSS-COD-PLZO IN WSS-VARI TO DVL-COD-PLZO IN DVL.
      *FIN-JGM 28-03-2000.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-DVL.
           IF FIO-STAT-OKS
              ADD 1 TO WSS-NUM-GDVL IN WSS-VARI.

       FIN-MOVER-DATOS-SRT.
           EXIT.
       ESTADISTICAS SECTION.
       INI-ESTADISTICAS.
            DISPLAY 'ESTADISTICAS PROGRAMA : COLPGDVL'
            DISPLAY '---------------------------------'.
            DISPLAY 'REGISTROS LEIDOS DVG   : '
                               WSS-NUM-RSEL IN WSS-VARI.
            DISPLAY 'REGISTROS GRABADOS DVL : '
                               WSS-NUM-GDVL IN WSS-VARI.
       FIN-ESTADISTICAS.
           EXIT.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGIRH.
       COPY GNSBGABT.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY TABBBOFI.
       COPY TABBFOFI.
       COPY COLBFDCC.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBFMSC.
       COPY GNSBGCPT.
       COPY TABBFCAM.
       COPY GNSBGPES.
      *
       COPY COLBFDVL.
       COPY COLBFDVG.

