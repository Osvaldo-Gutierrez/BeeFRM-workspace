       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLM82.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    05-Nov-90 05:12 PM.
      * Programa Listador de Reporte M82 con Sort.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTM82         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTM82         ASSIGN TO        UT-S-RPTM82.
           SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
           SELECT RPTM82         ASSIGN TO SYS011-UR-1403-S.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRM82.
       FD  RPTM82 LABEL RECORDS OMITTED
           REPORT IS RPT-M82
           .
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-M82-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRM82.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWM82.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY TABBRUSR.
       COPY TABBRCAM.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       01  WSS-VARI.
           03 WSS-FEC-FINF.
              05 WSS-NUM-DINF                     PIC 9(02).
              05 WSS-NUM-MINF                     PIC 9(02).
              05 WSS-NUM-SINF                     PIC 9(02).
              05 WSS-NUM-AINF                     PIC 9(02).
           03 WSS-FEC-FCAM.
              05 WSS-NUM-SCAM                     PIC 9(02).
              05 WSS-NUM-ACAM                     PIC 9(02).
              05 WSS-NUM-MCAM                     PIC 9(02).
              05 WSS-NUM-DCAM                     PIC 9(02).
           03 WSS-CIC-VCAM                        PIC X(06).
           03 WSS-CIC-MENS                        PIC X(12).
           03 WSS-COD-VCOC                        PIC X(06).
           03 WSS-COD-PESO                        PIC X(06).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-IND-MNAC      VALUE ZEROES      PIC 9(01).
           03 WSS-VAL-MNAC      VALUE ZEROES      PIC 9(11).
           03 WSS-COD-REGI                        PIC X(02).
           03 WSS-NUM-REGI REDEFINES WSS-COD-REGI PIC 9(02).
           03 WSS-I                               PIC 9(02).
           03 WSS-TAB-REGI.
              05 WSS-TAB-VIGE   OCCURS 13.
                 07 WSS-NUM-VIGE                  PIC 9(05).
                 07 WSS-VAL-VIGE                  PIC 9(12).
              05 WSS-TAB-IMPA   OCCURS 13.
                 07 WSS-NUM-IMPA                  PIC 9(05).
                 07 WSS-VAL-IMPA                  PIC 9(12).
              05 WSS-TAB-PROT   OCCURS 13.
                 07 WSS-NUM-PROT                  PIC 9(05).
                 07 WSS-VAL-PROT                  PIC 9(12).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *>>>>
       REPORT SECTION.
      *--------------
       COPY COLRFM82.
           .
       COPY COLRTM82.
       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-PH-M82 SECTION.
           USE BEFORE REPORTING PH-M82.
       INI-PH-M82.
       FIN-PH-M82.
           EXIT.
       DEC-FF-M82 SECTION.
           USE BEFORE REPORTING FF-M82.
       INI-FF-M82.
       FIN-FF-M82.
           EXIT.
       END DECLARATIVES.
       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLM82' TO FIO-PROG.
           PERFORM BUS-FSIS.
           SORT GNSSRT
       COPY COLRSM82.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      * Captura Fecha de Informe
           DISPLAY 'FECHA INFORME ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINF IN WSS-VARI.
           DISPLAY WSS-FEC-FINF IN WSS-VARI.
           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
      *VAL-FEC Valida Fecha de Informe
           DISPLAY WSS-FEC-FINF IN WSS-VARI.
           MOVE WSS-FEC-FINF    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
           MOVE WSS-NUM-DINF IN WSS-VARI TO WSS-NUM-DACT IN WSS-M82
                                            WSS-NUM-DCAM IN WSS-VARI.
           MOVE WSS-NUM-MINF IN WSS-VARI TO WSS-NUM-MACT IN WSS-M82
                                            WSS-NUM-MCAM IN WSS-VARI.
           MOVE WSS-NUM-SINF IN WSS-VARI TO WSS-NUM-SACT IN WSS-M82
                                            WSS-NUM-SCAM IN WSS-VARI.
           MOVE WSS-NUM-AINF IN WSS-VARI TO WSS-NUM-AACT IN WSS-M82
                                            WSS-NUM-ACAM IN WSS-VARI.
           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
      *BUS-USR busca usuario en tablas
           MOVE 'USR' TO USR-COD-USER IN USR.
           PERFORM BUS-USR.
      * Obtiene Equivalencias de Monedas en Pesos Mediante CIC de la Mon
           MOVE '0999  '       TO WSS-CIC-VCAM IN WSS-VARI.
           MOVE 'CIC0999  NEX' TO WSS-CIC-MENS IN WSS-VARI.
           PERFORM COD-CIC-VCAM.
           MOVE WSS-COD-VCOC IN WSS-VARI TO WSS-COD-PESO IN WSS-VARI.
           MOVE '101301'       TO WSS-CIC-VCAM IN WSS-VARI.
           MOVE 'CIC101301NEX' TO WSS-CIC-MENS IN WSS-VARI.
           PERFORM COD-CIC-VCAM.
           PERFORM BUS-VAL-CAM.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VDOL IN WSS-M82.
           MOVE '0998  '       TO WSS-CIC-VCAM IN WSS-VARI.
           MOVE 'CIC0998  NEX' TO WSS-CIC-MENS IN WSS-VARI.
           PERFORM COD-CIC-VCAM.
           PERFORM BUS-VAL-CAM.
           MOVE CAM-SGV-VCAM IN CAM TO WSS-VAL-VUEF IN WSS-M82.
      * Inicializa Variables
           MOVE ZEROES TO WSS-TAB-REGI IN WSS-VARI.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-M82.
       FST-INP-SRT.
      *DAD
      *     MOVE FIO-GET-FST TO FIO-CMND.
           MOVE FIO-GET-NXT TO FIO-CMND.
      *
           PERFORM COL-FIO-M82.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro M82
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMM82.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-M82.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-M82.
       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTM82.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-M82.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      * Clasifica segun Region y Situacion Cobranza
           PERFORM BUS-DIS-M82.
           GO TO NXT-OUT-SRT.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-M82.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      *<<<< FIN_OUT
           PERFORM GEN-LIN-OUT VARYING WSS-I IN WSS-VARI FROM 1 BY 1
                                 UNTIL WSS-I IN WSS-VARI > 13.
      *>>>>
           TERMINATE RPT-M82.
           CLOSE RPTM82.
       EXT-OUT-SRT.
           EXIT.
       COPY COLBFM82.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
      *<<<< EOF
      ******************************************************************
      * Obtiene Codigo Valor de Cambio Mediante CIC de Moneda
       COD-CIC-VCAM SECTION.
       INI-COD-CIC-VCAM.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE WSS-CIC-VCAM   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'                    TO MSG-COD-SIST
               MOVE WSS-CIC-MENS IN WSS-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-END.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-VCOC IN WSS-VARI.
       FIN-COD-CIC-VCAM.
           EXIT.
      ******************************************************************
      * Obtiene Valor de Cambio en Moneda Nacional
       BUS-VAL-CAM SECTION.
       INI-BUS-VAL-CAM.
           IF WSS-COD-VCOC IN WSS-VARI NOT = WSS-COD-PESO IN WSS-VARI
               MOVE WSS-FEC-FCAM IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1)
               MOVE WSS-FEC-FCAM IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2)
               MOVE WSS-COD-VCOC IN WSS-VARI TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY  TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR CAMBIO A ESTA FECHA : '
                            WSS-FEC-FINF IN WSS-VARI ' PARA '
                            WSS-COD-VCOC IN WSS-VARI
                   PERFORM GNS-PRO-END
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO A ESTA FECHA : '
                               WSS-FEC-FINF IN WSS-VARI ' PARA '
                               WSS-COD-VCOC IN WSS-VARI
                       PERFORM GNS-PRO-END
                   ELSE
                       MOVE 1 TO WSS-IND-MNAC IN WSS-VARI
           ELSE
               MOVE 0 TO WSS-IND-MNAC IN WSS-VARI.
       FIN-BUS-VAL-CAM.
           EXIT.
      ******************************************************************
      * Seleccion del Registro para Proceso Mediante Fecha del Listado
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.
           IF M82-FEC-FM82 IN M82 > WSS-FEC-FCAM IN WSS-VARI OR
              M82-VAL-SM82 IN M82 NOT > ZEROES
               GO TO FIN-SEL-REG-PRO.
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      ******************************************************************
      * Busca y Clasifica segun Region y Situacion Cobranza
       BUS-DIS-M82 SECTION.
       INI-BUS-DIS-M82.
           MOVE M82-COD-REGI IN SRT TO WSS-COD-REGI IN WSS-VARI.
           MOVE M82-COD-VCOC IN SRT TO WSS-COD-VCOC IN WSS-VARI.
           PERFORM BUS-VAL-CAM.
           IF WSS-IND-MNAC IN WSS-VARI = 1
               COMPUTE WSS-VAL-MNAC IN WSS-VARI ROUNDED =
                       M82-VAL-SM82 IN SRT * CAM-SGV-VCAM IN CAM
           ELSE
               DIVIDE M82-VAL-SM82 IN SRT BY 1 GIVING WSS-VAL-MNAC
                                                   IN WSS-VARI ROUNDED.
           IF M82-IND-SCBZ IN SRT = 'V'
               ADD  1 TO WSS-NUM-VIGE IN WSS-VARI(WSS-NUM-REGI)
               ADD WSS-VAL-MNAC IN WSS-VARI TO WSS-VAL-VIGE
                                      IN WSS-VARI(WSS-NUM-REGI)
           ELSE
           IF M82-IND-SCBZ IN SRT = 'P'
               ADD  1 TO WSS-NUM-PROT IN WSS-VARI(WSS-NUM-REGI)
               ADD WSS-VAL-MNAC IN WSS-VARI TO WSS-VAL-PROT
                                      IN WSS-VARI(WSS-NUM-REGI)
           ELSE
           IF M82-IND-SCBZ IN SRT = 'I'
               ADD  1 TO WSS-NUM-IMPA IN WSS-VARI(WSS-NUM-REGI)
               ADD WSS-VAL-MNAC IN WSS-VARI TO WSS-VAL-IMPA
                                      IN WSS-VARI(WSS-NUM-REGI).
       FIN-BUS-DIS-M82.
           EXIT.
      ******************************************************************
      * Genera Lineas de Detalle a Partir de Totales Almacenados
       GEN-LIN-OUT SECTION.
       INI-GEN-LIN-OUT.
           MOVE WSS-I IN WSS-VARI TO WSS-COD-REGI IN WSS-M82.
           ADD WSS-NUM-VIGE IN WSS-VARI(WSS-I)
               WSS-NUM-IMPA IN WSS-VARI(WSS-I)
               WSS-NUM-PROT IN WSS-VARI(WSS-I)
                                        GIVING WSS-NUM-PRES IN WSS-M82.
           ADD WSS-VAL-VIGE IN WSS-VARI(WSS-I)
               WSS-VAL-IMPA IN WSS-VARI(WSS-I)
               WSS-VAL-PROT IN WSS-VARI(WSS-I)
                                        GIVING WSS-VAL-PRES IN WSS-M82.
           MOVE WSS-NUM-IMPA IN WSS-VARI(WSS-I) TO WSS-NUM-IMPA
                                                            IN WSS-M82.
           MOVE WSS-VAL-IMPA IN WSS-VARI(WSS-I) TO WSS-VAL-IMPA
                                                            IN WSS-M82.
           MOVE WSS-NUM-PROT IN WSS-VARI(WSS-I) TO WSS-NUM-PROT
                                                            IN WSS-M82.
           MOVE WSS-VAL-PROT IN WSS-VARI(WSS-I) TO WSS-VAL-PROT
                                                            IN WSS-M82.
           GENERATE DL-M82.
       FIN-GEN-LIN-OUT.
           EXIT.
      ******************************************************************
       COPY TABBFUSR.
       COPY TABBFCAM.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY TABBBUSR.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
      *>>>>
