       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGADP.
       AUTHOR.          BEE TEAM (JGM).
       DATE-WRITTEN.    27-JUL-1999
      *
      *Generar Informacion , CAMARA DE COMERCIO.
      *Aclaracion Documentos Pagados.
      *Modificar : 16-03-2000 , se modifica el programa
      *            para que seleccion cancelaciones en un rango
      *            anterior solo un dia.
      *
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCD-REQA==.
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
      *<<<< WSS
      ***************
       COPY COLBRHDI.
       COPY COLBRADP.
       COPY COLBRMOT.
      ***************
       COPY COLBROPC.
       COPY COLBRVEN.
       COPY COLBRCAN.
       COPY COLBRDLC.
       COPY COLBRCYA.
       COPY COLBRRCV.
       COPY COLBRRDC.
       COPY COLBRROV.
      *
       COPY SGCBRDBC.
       COPY SGCBRDCP.
       COPY SGCBRRCD.
      *
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWCVRF.
       COPY GNSWVVRF.
       COPY GNSWGFRM.
      ***************
       COPY COLWGVEN.
       COPY COLWGINT.
       COPY GNSWGPES.
       COPY COLBRICG.
       COPY COLBRTOC.
       COPY COLBREVC.
       COPY TABBRCAM.
       COPY TABBROFI.
      ***************
      *
       01  WSS-VARI.
           03 WSS-NUM-ICLI    VALUE 50000000     PIC 9(08).
           03 WSS-IND-TCYA.
              05 WSS-COD-TRA1                    PIC X(02).
              05 WSS-COD-TRA2                    PIC X(03).
      *
           03 WSS-IND-PROC   VALUE SPACES        PIC X(01).
           03 WSS-LEE-ROP    VALUE SPACES        PIC X(01).

           03 WK-COMUNA.
              05 WK-COD-COMUN                     PIC X(04).
              05 FILLER                           PIC X(01).

           03 WK-REGION.
              05 FILLER                           PIC X(22).
              05 WK-COD-REGION                    PIC X(02).
              05 FILLER                           PIC X(36).

           03 WK-COD-LOCRC                        PIC X(06).
           03 FILLER           REDEFINES       WK-COD-LOCRC.
             05 WK-COD-COM                        PIC 9(04).
             05 WK-COD-REG                        PIC 9(02).
      *
           03 WSS-PRM-FPR1.
              05 WSS-PRM-DPR1                     PIC 9(02).
              05 WSS-PRM-MPR1                     PIC 9(02).
              05 WSS-PRM-SPR1                     PIC 9(02).
              05 WSS-PRM-APR1                     PIC 9(02).
      *
           03 WSS-PRM-FPR2.
              05 WSS-PRM-DPR2                     PIC 9(02).
              05 WSS-PRM-MPR2                     PIC 9(02).
              05 WSS-PRM-SPR2                     PIC 9(02).
              05 WSS-PRM-APR2                     PIC 9(02).
      *
      *
           03 WSS-FEC-FPR1.
              05 WSS-NUM-SPR1                     PIC 9(02).
              05 WSS-NUM-APR1                     PIC 9(02).
              05 WSS-NUM-MPR1                     PIC 9(02).
              05 WSS-NUM-DPR1                     PIC 9(02).
      *
           03 WSS-FEC-FPR2.
              05 WSS-NUM-SPR2                     PIC 9(02).
              05 WSS-NUM-APR2                     PIC 9(02).
              05 WSS-NUM-MPR2                     PIC 9(02).
              05 WSS-NUM-DPR2                     PIC 9(02).
      *
           03 WSS-FEC-FVEN.
              05 WSS-NUM-SVEN                     PIC X(02).
              05 WSS-NUM-AVEN                     PIC X(02).
              05 WSS-NUM-MVEN                     PIC X(02).
              05 WSS-NUM-DVEN                     PIC X(02).
      *
           03 WSS-COD-PESO       VALUE SPACES     PIC X(06).
           03 WSS-CIC-ORIG       VALUE SPACES     PIC X(12).
           03 WSS-CIC-IOPC       VALUE SPACES     PIC X(12).
           03 WSS-CIC-IROV       VALUE SPACES     PIC X(12).
           03 WSS-CIC-IOCO       VALUE SPACES     PIC X(12).
      *
           03 WSS-NUM-LCAN       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-CAUS       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-ECAN       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-NCAN       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-GADP       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-CORR       VALUE 1          PIC 9(05).
           03 WSS-IND-DISP       VALUE SPACES     PIC X(01).
      *
           03 WSS-GLS-MONE       VALUE ZERO       PIC 9(02).
           03 WSS-COD-MENS       VALUE SPACES     PIC X(30).
           03 WSS-IND-EXISTE     VALUE SPACE      PIC X(01).
      *
           03 WSS-COD-ERRO                        PIC X(01).
              88 WSS-OK                           VALUE  '0'.
              88 WSS-NOINFORMADA-BOLETIN          VALUE  '1'.


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGADP' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           PERFORM OPEN-FILE.
           PERFORM LEE-PARAM.
           PERFORM BUS-CODIGO-PESO.
           PERFORM PRO-REG-CAN.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-HDI.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLHDI ' FIO-STAT.
      *
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-ADP.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLADP ' FIO-STAT
              PERFORM GNS-PRO-ABT.
      *
      *
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-MOT.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLMOT ' FIO-STAT
              PERFORM GNS-PRO-ABT.
       FIN-OPEN-FILE.
           EXIT.

       CLOSE-FILE SECTION.
      *-----------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-HDI.
      *
      * Archivo de aclaracion   de documentos Protestados.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-ADP.
           DISPLAY 'CERRAR ARCHIVOS: ADP, HDI'.
      *
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-MOT.
       FIN-CLOSE-FILE.
           EXIT.


      ******************************************************************
       PRO-REG-CAN SECTION.
      *------------------*
       INI-PRO-REG-CAN.
      * fecha computacional
           MOVE WSS-FEC-FPR1 IN WSS-VARI TO CAN-FEC-FOUT IN CAN.
      *
           MOVE 'CAN-FEC-FOUT'           TO FIO-AKEY.
           MOVE FIO-GET-NLS              TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN CANCELACIONES  '
               GO TO FIN-PRO-REG-CAN.
      **
       LUP-BUS-CAN.
           MOVE SPACES TO  WSS-COD-MENS IN WSS-VARI.
      *
           IF CAN-FEC-FOUT IN CAN < WSS-FEC-FPR1 IN WSS-VARI
               GO TO NXT-PRO-CAN.
      *
           IF CAN-FEC-FOUT IN CAN > WSS-FEC-FPR2 IN WSS-VARI
               GO TO FIN-PRO-REG-CAN.

           ADD 1 TO WSS-NUM-LCAN IN WSS-VARI.
      *
      * Centro Asignacion Identificacion
      * La primera posicion del CAI debe ser solo 'D'.
           IF CAN-COD-TOPC IN CAN NOT = 'D'
               ADD 1 TO WSS-NUM-ECAN IN WSS-VARI
               GO TO NXT-PRO-CAN.

      * Verifica estado de las Cancelaciones.
           IF CAN-MSC-STAT IN CAN NOT = 'A'
               ADD 1 TO WSS-NUM-ECAN IN WSS-VARI
               GO TO NXT-PRO-CAN.

      *  Verifico ,CAN fue reversada.
           IF (CAN-SGV-TPAG IN CAN < ZEROES) OR
              (CAN-SGV-TREN IN CAN < ZEROES)
               ADD 1 TO WSS-NUM-ECAN IN WSS-VARI
               GO TO NXT-PRO-CAN.

      *  Verifico que forma  cancelacion : TOTAL
            IF CAN-MSC-FCAN IN CAN NOT = 'T'
               ADD 1 TO WSS-NUM-ECAN IN WSS-VARI
               GO TO NXT-PRO-CAN.
      *
      * Verifico si es Fue traspaso.
           PERFORM BUS-REG-CYA.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO NXT-PRO-CAN.
      *
      *  Tipo Cancelacion = Renovacion
           IF CAN-MSC-TCAN IN CAN  = 'R'
              PERFORM BUS-REG-ROV-RE1
              IF WSS-COD-MENS IN WSS-VARI > SPACES
                 GO TO NXT-PRO-CAN.
      *
      *  TIPO CANCELACION = PTT
           IF CAN-MSC-TCAN IN CAN  = 'P'
              PERFORM BUS-REG-ROV-RE1
              IF WSS-COD-MENS IN WSS-VARI > SPACES
                 GO TO NXT-PRO-CAN.

      * Verifico si es D.Directo es natural.
           PERFORM BUS-DATOS-DD.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO NXT-PRO-CAN.
      *
      *  RCV( Excluir cancelaciones, Anticipadas o del Dia )
           PERFORM BUS-REG-RCV.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO NXT-PRO-CAN.
      *
           PERFORM BUS-REG-DLC.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO NXT-PRO-CAN.

      * Leo el VEN.
           PERFORM BUS-REG-VEN.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO NXT-PRO-CAN.

           PERFORM BUS-OPERACION-BOLETIN.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO NXT-PRO-CAN.
      *
           PERFORM GRABA-REG-ADP.

       NXT-PRO-CAN.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              ADD 1 TO WSS-NUM-ECAN IN WSS-VARI.
           IF WSS-IND-DISP IN WSS-VARI = 'S'
              IF WSS-COD-MENS IN WSS-VARI > SPACES
                 DISPLAY WSS-COD-MENS '  '
                         CAN-CIC-IOPC IN CAN  ' NUMERO : '
                         CAN-NUM-ICAN IN CAN.
      *
           MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS
               GO TO LUP-BUS-CAN.
       FIN-PRO-REG-CAN.
           EXIT.
      *
       BUS-REG-CYA  SECTION.
      *---------------------*
       INI-BUS-REG-CYA.
      *Buscar CYA asociado a la cancelacion ( es suficiente con buscar e
           MOVE SPACES              TO WSS-COD-MENS IN WSS-VARI.
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE CAN-KEY-ICAN IN CAN TO CYA-KEY-ICAN IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT (FIO-STAT-OKS AND
                   CYA-KEY-ICAN IN CYA = CAN-KEY-ICAN IN CAN )
               GO TO FIN-BUS-REG-CYA.
      *
           MOVE CYA-IND-TCYA  IN CYA TO WSS-IND-TCYA IN WSS-VARI.
           IF WSS-COD-TRA1 IN WSS-VARI = 'PB' OR
              WSS-COD-TRA1 IN WSS-VARI = 'TR'
               MOVE 'TRASPASO :CYA'   TO WSS-COD-MENS IN WSS-VARI
               GO TO FIN-BUS-REG-CYA.
       FIN-BUS-REG-CYA.
           EXIT.
      *
       BUS-REG-ROV-RE1 SECTION.
      *-------------------*
       INI-BUS-REG-ROV-RE1.
      *Busca credito madre del relacionado.
           MOVE  SPACES              TO WSS-COD-MENS.
           MOVE CAN-CIC-IOPC IN CAN TO ROV-CIC-IOPV IN ROV.
           MOVE CAN-NUM-ICAN IN CAN TO ROV-NUM-IVCT IN ROV.
           MOVE 'ROV-KEY-IVCT'      TO FIO-AKEY.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT (FIO-STAT-OKS AND
                   CAN-CIC-IOPC IN CAN  = ROV-CIC-IOPV IN ROV AND
                   CAN-NUM-ICAN IN CAN  = ROV-NUM-IVCT IN ROV )
      *
              MOVE 'NO EXISTE ROV-REN' TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-BUS-REG-ROV-RE1.
      *
       BUS-REG-ROV-DLC-RE1.
           MOVE  ROV-CIC-IOPC IN ROV TO DLC-CIC-IOPC IN DLC.
           MOVE 1                    TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY          TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
              MOVE 'NO EXISTE ROV-DLC' TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-BUS-REG-ROV-RE1.
      *
           IF DLC-IND-SCTB IN DLC = 'CAS'  OR
              DLC-IND-SCTB IN DLC = 'VCD'
              MOVE 'ROV-RENO. CAS,VCD' TO WSS-COD-MENS IN WSS-VARI.
       FIN-BUS-REG-ROV-RE1.
           EXIT.
      *
       BUS-DATOS-DD SECTION.
      *--------------------*
       INI-BUS-DATOS-DD.
      * Busco fecha de nacimiento del deudor directo o indirecto.
           MOVE SPACES              TO WSS-COD-MENS IN WSS-VARI.
           MOVE CAN-CIC-IOPC IN CAN TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC'      TO FIO-AKEY.
           MOVE FIO-GET-NLS         TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
              RDC-CIC-IOPC IN RDC = CAN-CIC-IOPC IN CAN AND
              RDC-NUM-IDLC IN RDC = ZEROES)
               MOVE 'NO EXISTE RDC : '   TO WSS-COD-MENS IN WSS-VARI
               GO TO FIN-BUS-DATOS-DD.
      *
      * Si RUT del cliente es > 50.000.0000 (empresa) no cobrar seguro.
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
              MOVE 'NO EXISTE DBC : '   TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-BUS-DATOS-DD.
      *
           IF DBC-NUM-ICLI IN DBC NOT < WSS-NUM-ICLI IN WSS-VARI OR
              DBC-IND-TICL IN DBC NOT = 'P'
              MOVE 'CLIENTE.EMPRESA : ' TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-BUS-DATOS-DD.
      *
           MOVE DBC-CIC-ICLI IN DBC TO DCP-CIC-ICLI IN DCP.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM SGC-FIO-DCP.
           IF NOT FIO-STAT-OKS
              MOVE 'NO EXISTE DCP : ' TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-BUS-DATOS-DD.

      *RCD.
           MOVE DBC-CIC-ICLI IN DBC TO RCD-CIC-ICLI IN RCD.
           MOVE ZEROES              TO RCD-NUM-DIRE IN RCD.
           MOVE FIO-GET-NLS         TO FIO-CMND.
       LUP-REG-RCD.
           PERFORM SGC-FIO-RCD.
           IF NOT ( FIO-STAT-OKS AND
               DBC-CIC-ICLI IN DBC = RCD-CIC-ICLI IN RCD )
                MOVE ZEROES        TO RCD-NUM-FONO IN RCD
                MOVE ZEROES        TO RCD-NUM-ANXO IN RCD
                GO TO FIN-BUS-DATOS-DD.

           IF RCD-NUM-FONO  IN RCD = ZEROES
              MOVE FIO-GET-NXT TO FIO-CMND
              GO TO LUP-REG-RCD.

       FIN-BUS-DATOS-DD.
           EXIT.
      *
       BUS-REG-DLC SECTION.
      *-------------------*
       INI-BUS-REG-DLC.
           MOVE SPACES              TO WSS-COD-MENS IN WSS-VARI.
           MOVE CAN-CIC-IOPC IN CAN TO DLC-CIC-IOPC IN DLC.
           MOVE 1                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
             MOVE 'ERROR REG : DLC ' TO WSS-COD-MENS IN WSS-VARI
             GO TO FIN-BUS-REG-DLC.
      *
      *    IF DLC-IND-SCTB IN DLC = 'VIG' OR
      *       DLC-IND-SCTB IN DLC = '   '
      *       MOVE 'CAN: DIA,ANTICIPADO' TO
      *                                WSS-COD-MENS IN WSS-VARI
      *       GO TO FIN-BUS-REG-DLC.
      *
      *  Tipo Cancelacion = Prorrogas( Morosa )
           IF CAN-MSC-CANC IN CAN = 'PTT'
              PERFORM BUS-REG-DLC-ORIG
              GO TO FIN-BUS-REG-DLC.

           IF DLC-IND-SCTB IN DLC = 'VCD' OR
              DLC-IND-SCTB IN DLC = 'CAS'
              PERFORM BUS-REG-ROV
              GO TO FIN-BUS-REG-DLC.
      *
           MOVE CAN-CIC-IOPC IN CAN TO WSS-CIC-IOPC IN WSS-VARI
           MOVE CAN-CIC-IOPC IN CAN TO WSS-CIC-ORIG IN WSS-VARI
           PERFORM BUS-REG-OPC.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO FIN-BUS-REG-DLC.
      *
      *Cancelacion Normal. opc prov. prorroga.
           IF OPC-IND-RENO IN OPC = 'PRO'
               PERFORM BUS-REG-DLC-PTT
               PERFORM BUS-REG-OPC.
       FIN-BUS-REG-DLC.
           EXIT.
      *
       BUS-REG-DLC-ORIG SECTION.
      *-------------------*
       INI-BUS-REG-DLC-ORIG.
           MOVE SPACES              TO WSS-COD-MENS IN WSS-VARI.
           MOVE DLC-CIC-IOCO IN DLC TO DLC-CIC-IOPC IN DLC.
           MOVE 1                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
             MOVE 'ERROR IOCO: DLC ' TO WSS-COD-MENS IN WSS-VARI
             GO TO FIN-BUS-REG-DLC-ORIG.
      *
           IF DLC-IND-SCTB IN DLC = 'VCD' OR
              DLC-IND-SCTB IN DLC = 'CAS'
              PERFORM BUS-REG-ROV
              GO TO FIN-BUS-REG-DLC-ORIG.

           MOVE CAN-CIC-IOPC IN CAN TO WSS-CIC-IOPC IN WSS-VARI
           MOVE DLC-CIC-IOCO IN DLC TO WSS-CIC-ORIG IN WSS-VARI
           PERFORM BUS-REG-OPC.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO FIN-BUS-REG-DLC-ORIG.
      *
       FIN-BUS-REG-DLC-ORIG.
           EXIT.
      *
       BUS-REG-DLC-PTT  SECTION.
      *-------------------*
       INI-BUS-REG-DLC-PTT.
           MOVE SPACES              TO WSS-COD-MENS IN WSS-VARI.
           MOVE DLC-CIC-IOCO IN DLC TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
             MOVE ' ERROR REG : OPC ' TO WSS-COD-MENS IN WSS-VARI
             GO TO FIN-BUS-REG-DLC-PTT.
      *
      *
           MOVE DLC-CIC-IOCO IN DLC TO DLC-CIC-IOPC IN DLC.
           MOVE 1                   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
             MOVE 'ERROR IOCO: DLC ' TO WSS-COD-MENS IN WSS-VARI
             GO TO FIN-BUS-REG-DLC-ORIG.

      *
           IF DLC-IND-SCTB IN DLC = 'VCD' OR
              DLC-IND-SCTB IN DLC = 'CAS'
              PERFORM BUS-REG-ROV
              GO TO FIN-BUS-REG-DLC-PTT.

           IF OPC-IND-RENO IN OPC  = '   '
             MOVE DLC-CIC-IOCO IN DLC TO WSS-CIC-ORIG IN WSS-VARI
             GO TO FIN-BUS-REG-DLC-PTT.
      *
           IF OPC-IND-RENO IN OPC  = 'REN'
             MOVE CAN-CIC-IOPC IN CAN TO WSS-CIC-ORIG IN WSS-VARI
             GO TO FIN-BUS-REG-DLC-PTT.
      *
           GO TO INI-BUS-REG-DLC-PTT.
       FIN-BUS-REG-DLC-PTT.
           EXIT.
      *
       BUS-REG-ROV SECTION.
      *-------------------*
       INI-BUS-REG-ROV.
      *Busca credito madre del relacionado.
           MOVE  SPACES              TO WSS-COD-MENS.
           MOVE  DLC-CIC-IOPC IN DLC TO WSS-CIC-IROV IN WSS-VARI.
           MOVE  DLC-CIC-IOPC IN DLC TO ROV-CIC-IOPC IN ROV.
       LEE-REG-ROV.
           MOVE SPACES               TO ROV-CIC-IOPV IN ROV.
           MOVE ZEROES               TO ROV-NUM-IVCT IN ROV.
           MOVE FIO-GET-NLS          TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT (FIO-STAT-OKS AND
                   WSS-CIC-IROV IN WSS-VARI = ROV-CIC-IOPC IN ROV)
              MOVE 'NO EXISTE ROV' TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-BUS-REG-ROV.
      *
       BUS-REG-ROV-DLC.
           MOVE  ROV-CIC-IOPV IN ROV TO DLC-CIC-IOPC IN DLC.
           MOVE 1                    TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY          TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
              MOVE 'NO EXISTE ROV-DLC' TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-BUS-REG-ROV.
      *
           IF DLC-IND-SCTB IN DLC = 'CAS'  OR
              DLC-IND-SCTB IN DLC = 'VCD'
      *
              MOVE ROV-CIC-IOPV IN ROV TO ROV-CIC-IOPC IN ROV
              MOVE ROV-CIC-IOPV IN ROV TO WSS-CIC-IROV IN WSS-VARI
              GO TO LEE-REG-ROV.
      *
           MOVE  ROV-CIC-IOPV IN ROV TO WSS-CIC-IOPC IN WSS-VARI.
           MOVE  ROV-CIC-IOPV IN ROV TO WSS-CIC-ORIG IN WSS-VARI.
           PERFORM BUS-REG-OPC.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO FIN-BUS-REG-ROV.
      *
       FIN-BUS-REG-ROV.
           EXIT.
      *
       BUS-REG-OPC SECTION.
      *-------------------*
       INI-BUS-REG-OPC.
           MOVE SPACES                   TO WSS-COD-MENS IN WSS-VARI.
           MOVE CAN-CIC-IOPC IN CAN      TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
             MOVE ' ERROR REG : OPC ' TO WSS-COD-MENS IN WSS-VARI
             GO TO FIN-BUS-REG-OPC.
       FIN-BUS-REG-OPC.
           EXIT.
      *
       BUS-REG-OPC-ORIG SECTION.
      *-------------------*
       INI-BUS-REG-OPC-ORIG.
           MOVE SPACES                   TO WSS-COD-MENS IN WSS-VARI.
           MOVE WSS-CIC-ORIG IN WSS-VARI TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
             MOVE ' ERROR REG : OPC ' TO WSS-COD-MENS IN WSS-VARI
             GO TO FIN-BUS-REG-OPC-ORIG.
       FIN-BUS-REG-OPC-ORIG.
           EXIT.
      *
       BUS-REG-RCV SECTION.
      *-------------------*
       INI-BUS-REG-RCV.
           MOVE SPACES                   TO WSS-COD-MENS IN WSS-VARI.
           MOVE CAN-CIC-IOPC IN CAN      TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN      TO RCV-NUM-ICAN IN RCV.
           MOVE SPACES                   TO RCV-CIC-IOPV IN RCV.
           MOVE ZEROES                   TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS
                AND CAN-CIC-IOPC IN CAN = RCV-CIC-IOPC IN RCV
                AND CAN-NUM-ICAN IN CAN = RCV-NUM-ICAN IN RCV )
                 MOVE 'ERROR RCV ' TO WSS-COD-MENS IN WSS-VARI
                 GO TO FIN-BUS-REG-RCV.
           IF RCV-MSC-MCAN IN RCV = 'A'
                  MOVE 'CAN: DIA,ANTICIPADO' TO
                                       WSS-COD-MENS IN WSS-VARI
                 GO TO FIN-BUS-REG-RCV.
           IF RCV-MSC-MCAN IN RCV NOT = 'V'
              IF CAN-FEC-FOUT IN CAN = CAN-FEC-FCAN IN CAN
                  MOVE 'CAN: DIA,ANTICIPADO' TO
                                       WSS-COD-MENS IN WSS-VARI.
       FIN-BUS-REG-RCV.
           EXIT.
      *
       BUS-REG-VEN SECTION.
      *-------------------*
       INI-BUS-REG-VEN.
           MOVE SPACES                   TO WSS-COD-MENS IN WSS-VARI.
      *    MOVE CAN-CIC-IOPC IN CAN      TO RCV-CIC-IOPC IN RCV.
      *    MOVE CAN-NUM-ICAN IN CAN      TO RCV-NUM-ICAN IN RCV.
      *    MOVE SPACES                   TO RCV-CIC-IOPV IN RCV.
      *    MOVE ZEROES                   TO RCV-NUM-IVCT IN RCV.
      *    MOVE FIO-GET-NLS TO FIO-CMND.
      *    PERFORM COL-FIO-RCV.
      *    IF NOT ( FIO-STAT-OKS
      *         AND CAN-CIC-IOPC IN CAN = RCV-CIC-IOPC IN RCV
      *         AND CAN-NUM-ICAN IN CAN = RCV-NUM-ICAN IN RCV )
      *          MOVE 'ERROR RCV ' TO WSS-COD-MENS IN WSS-VARI
      *          GO TO FIN-BUS-REG-VEN.

           MOVE RCV-CIC-IOPC IN RCV     TO VEN-CIC-IOPC IN VEN.
      *
           IF RCV-NUM-IVCT IN RCV = ZEROES
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN
           ELSE
               MOVE RCV-NUM-IVCT IN RCV TO VEN-NUM-IVEN IN VEN.

           IF OPC-IND-VSTR IN OPC = 'S'
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS
                  MOVE 'OBTVENERR'  TO WSS-COD-MENS IN WSS-VARI
                  GO TO FIN-BUS-REG-VEN
               ELSE
                   GO TO FIN-BUS-REG-VEN.

           IF OPC-IND-VSTR IN OPC NOT = 'S'
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                  MOVE 'OBTVENERR'  TO WSS-COD-MENS IN WSS-VARI.
       FIN-BUS-REG-VEN.
           EXIT.
      *
       BUS-OPERACION-BOLETIN SECTION.
      *-----------------------------*
       INI-BUS-OPERACION-BOLETIN.
      *

           MOVE '0'                   TO WSS-COD-ERRO IN WSS-VARI.
           MOVE VEN-FEC-FVEN IN VEN   TO WSS-FEC-FVEN IN WSS-VARI.
      *
           MOVE WSS-CIC-ORIG IN WSS-VARI TO HDI-CIC-IOPC IN HDI.
           MOVE WSS-FEC-FVEN IN WSS-VARI TO HDI-FEC-FVEN IN HDI.
           MOVE FIO-GET-KEY              TO FIO-CMND.
           PERFORM COL-FIO-HDI.
           IF NOT FIO-STAT-OKS
              MOVE '1' TO WSS-COD-ERRO IN WSS-VARI.
      *
       FIN-BUS-OPERACION-BOLETIN.
           EXIT.

      *
       GRABA-REG-ADP SECTION.
      *---------------------*
       INI-GRABA-REG-ADP.

           PERFORM MOVER-DATOS-ADP.
           MOVE SPACES              TO WSS-COD-MENS IN WSS-VARI.
           PERFORM BUS-REG-OPC-ORIG.
           IF OPC-COD-TOOC IN OPC = 'TCR'
              ADD 1 TO WSS-NUM-CAUS IN WSS-VARI
              MOVE 'TCR'     TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-GRABA-REG-ADP.
      *
           PERFORM BUS-COD-CAUSAL.
           IF ADP-COD-CAUS IN ADP = 'NO'
              ADD 1 TO WSS-NUM-CAUS IN WSS-VARI
              MOVE ADP-COD-CAUS IN ADP TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-GRABA-REG-ADP.
      *
           IF WSS-COD-MENS IN WSS-VARI  = 'NO'
              ADD 1 TO WSS-NUM-CAUS IN WSS-VARI
              MOVE ADP-COD-CAUS IN ADP TO WSS-COD-MENS IN WSS-VARI
              GO TO FIN-GRABA-REG-ADP.

      *
           IF WSS-COD-ERRO IN WSS-VARI = '0'
              MOVE HDI-FEC-FVEN IN HDI TO ADP-FEC-FVEN IN ADP
              MOVE HDI-VAL-MONT IN HDI TO ADP-VAL-SFIN IN ADP.
      *
           PERFORM BUS-GLOSA-MONEDA.
           PERFORM BUS-GLOSA-OFICINA.
      * Operacion.
           MOVE WSS-NUM-CORR IN WSS-VARI TO ADP-NUM-CORR IN ADP.
           MOVE WSS-CIC-ORIG IN WSS-VARI TO ADP-CIC-IOPC IN ADP.
           MOVE OPC-COD-VCOC IN OPC      TO ADP-COD-MONE IN ADP.
           MOVE OPC-COD-OFOC IN OPC      TO ADP-COD-OFOC IN ADP.
      *
           MOVE WSS-COD-ERRO IN WSS-VARI TO ADP-COD-ERRO IN ADP.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-ADP.
           IF  NOT FIO-STAT-OKS
               DISPLAY 'ERROR GRABA REG : ADP '
               GO TO FIN-GRABA-REG-ADP.
      *
           IF WSS-IND-DISP IN WSS-VARI = 'S'
              DISPLAY 'GRABA ORIG : ' WSS-CIC-ORIG IN WSS-VARI
                      ' CAN-IOPC : ' CAN-CIC-IOPC IN CAN
                      ' NUM-ICAN : ' CAN-NUM-ICAN IN CAN.
      *
            ADD 1 TO WSS-NUM-GADP IN WSS-VARI.
            ADD 1 TO WSS-NUM-CORR IN WSS-VARI.
       FIN-GRABA-REG-ADP.
           EXIT.

       MOVER-DATOS-ADP SECTION.
      *---------------------------*
       INI-MOVER-DATOS-ADP.
      *
      * Tabla OPC.
           MOVE OPC-NUM-VCOR IN OPC TO ADP-NUM-VCOR IN ADP.
           MOVE OPC-VAL-SCRE IN OPC TO ADP-VAL-SCRE IN ADP.
      *
      * Tabla DBC.
      * Rut del cliente. DV.
           MOVE DBC-NUM-ICLI IN DBC TO ADP-NUM-RUTD IN ADP.
           MOVE DBC-VRF-ICLI IN DBC TO ADP-VRF-RUTD IN ADP.
      *
      * Nombres.
           MOVE DBC-GLS-APAT IN DBC TO ADP-GLS-APAT IN ADP.
           MOVE DBC-GLS-AMAT IN DBC TO ADP-GLS-AMAT IN ADP.
           MOVE DBC-GLS-NOMB IN DBC TO ADP-GLS-NBRE IN ADP.

           MOVE DBC-COD-EJEC IN DBC TO ADP-COD-EJEC IN ADP.
           MOVE   'N'               TO ADP-IND-CJUR IN ADP.
      *
      * Tabla RCD.
           MOVE RCD-NUM-FONO IN RCD  TO ADP-NUM-FONO IN ADP.
           MOVE RCD-NUM-ANXO IN RCD  TO ADP-NUM-ANXO IN ADP.
      *
      * Tabla Ven.
           MOVE VEN-NUM-IVCT IN VEN  TO ADP-NUM-IVCT IN ADP.
           MOVE VEN-NUM-IVEN IN VEN  TO ADP-NUM-IVEN IN ADP.
           MOVE VEN-VAL-FINA IN VEN  TO ADP-VAL-SFIN IN ADP.
           MOVE VEN-FEC-FVEN IN VEN  TO ADP-FEC-FVEN IN ADP.
      *
      * Fec.cancelacion.
           MOVE CAN-FEC-FOUT IN CAN  TO ADP-FEC-FOUT IN ADP.
      *
      * Fec.cancelacion.
           MOVE CAN-FEC-FCAN IN CAN  TO ADP-FEC-FCAN IN ADP.
      *
      * Operacion cancelada.
           MOVE CAN-CIC-IOPC IN CAN  TO ADP-CIC-ICAN IN ADP.
      *
      *  Indicador de envio aclaracion a Boletin.
           MOVE 'S'                  TO ADP-IND-EBOL IN ADP.
           MOVE SPACES               TO ADP-IND-CANC IN ADP.

       FIN-MOVER-DATOS-ADP.
           EXIT.
      *
       BUS-COD-CAUSAL SECTION.
      *-------------------------*
       INI-BUS-COD-CAUSAL.
           MOVE SPACES              TO ADP-COD-CAUS IN ADP.
           MOVE SPACES              TO WSS-COD-MENS IN WSS-VARI.
           MOVE OPC-COD-VCOC IN OPC TO MOT-COD-VCOC IN MOT.
           MOVE OPC-COD-TOOC IN OPC TO MOT-COD-TOOC IN MOT.
           MOVE OPC-COD-AOOC IN OPC TO MOT-COD-AOOC IN MOT.
           MOVE FIO-GET-KEY              TO FIO-CMND.
           PERFORM COL-FIO-MOT.
           IF FIO-STAT-OKS
              MOVE MOT-COD-MBIC IN MOT TO ADP-COD-CAUS IN ADP
           ELSE
           IF FIO-STAT NOT = '23'
               MOVE 'NO'    TO WSS-COD-MENS IN WSS-VARI.
       FIN-BUS-COD-CAUSAL.
           EXIT.

       BUS-GLOSA-MONEDA SECTION.
      *------------------------*
       INI-BUS-GLOSA-MONEDA.
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'TAB'               TO FIO-SIST.
           MOVE 'VLR'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR
                  TAB-IND-VIGE NOT = 'S'
               MOVE SPACES TO TAB-GLS-DESC IN TAB.
           MOVE TAB-GLS-ABRV IN TAB TO ADP-GLS-MONE IN ADP.
       FIN-BUS-GLOSA-MONEDA.
           EXIT.

       BUS-GLOSA-OFICINA SECTION.
      *-------------------------*
       INI-BUS-GLOSA-OFICINA.
      *VAL-OFI Valida codigo de Oficina en TABLAS
           MOVE OPC-COD-OFOC IN OPC TO OFI-COD-OFIC IN OFI.
           PERFORM VAL-OFI.
           IF MSG-COD-MENS NOT = SPACES
              MOVE SPACES            TO OFI-GLS-DESC IN OFI
              GO TO FIN-BUS-GLOSA-OFICINA.

           IF MSG-COD-MENS = 'COD    NEX'
              MOVE SPACES            TO OFI-GLS-DESC IN OFI
              GO TO FIN-BUS-GLOSA-OFICINA.

           MOVE OFI-GLS-DESC        TO ADP-GLS-OFOC IN ADP.
           PERFORM BUS-LOCALIDAD.

       FIN-BUS-GLOSA-OFICINA.
           EXIT.

       BUS-LOCALIDAD SECTION.
      *--------------------*
       INI-BUS-LOCALIDAD.
      * Busca Comuna En TAB.
      *
           MOVE 'TAB'          TO TAB-COD-SIST.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'COM'          TO  TAB-COD-TTAB.
           MOVE  OFI-COD-COMU  TO  TAB-COD-CTAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR
                  TAB-IND-VIGE NOT = 'S'
                  GO TO FIN-BUS-LOCALIDAD.

           MOVE  TAB-EXT-CTAB IN TAB  TO   WK-COMUNA.

           IF WK-COD-COMUN = SPACES OR LOW-VALUE OR ZEROES
              MOVE  9999          TO   WK-COD-COMUN
              MOVE 'SIN COMUNA'   TO   ADP-GLS-LOCA
            ELSE
               MOVE  WK-COD-COMUN  TO   WK-COD-COM
               MOVE  TAB-GLS-DESC  TO   ADP-GLS-LOCA.

      *
      *BUSCA REGION EN TABLA TAB                            *
      *
           MOVE 'TAB'         TO TAB-COD-SIST.
           MOVE 'TAB'         TO FIO-SIST.
           MOVE 'LOC'         TO TAB-COD-TTAB.
           MOVE  TAB-EXT-CTAB TO TAB-COD-CTAB.
           MOVE  FIO-GET-KEY  TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR
                  TAB-IND-VIGE NOT = 'S'
                  GO TO FIN-BUS-LOCALIDAD.

           MOVE TAB-GLS-DATA TO WK-REGION.
           IF WK-COD-REGION = SPACES OR LOW-VALUE OR ZEROES
               MOVE 99              TO   WK-COD-REGION
           ELSE
               MOVE WK-COD-REGION   TO   WK-COD-REG.


           IF WK-COD-LOCRC NOT NUMERIC
              MOVE  ZEROES          TO   ADP-COD-LOCA
              MOVE  'SIN LOCALIDAD' TO   ADP-GLS-LOCA
           ELSE
              MOVE  WK-COD-LOCRC    TO   ADP-COD-LOCA.

       FIN-BUS-LOCALIDAD.
           EXIT.

      *****************************************************
       LEE-PARAM SECTION.
      *-----------------
       INI-LEE-PARAM.
           DISPLAY 'CAPTURA PARAMETROS'.
           DISPLAY '------------------- '.

      * CAPTURA FECHA DE PROCESO1
           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '.
           ACCEPT WSS-PRM-FPR1 IN WSS-VARI.

      * CAPTURA FECHA DE PROCESO 2
           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '.
           ACCEPT WSS-PRM-FPR2 IN WSS-VARI.

      * Validacion de parametros
           MOVE WSS-PRM-FPR1 IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ERROR VALIDACION FECHA DE PROCESO: '
                                                    FEC-MENS
               PERFORM GNS-PRO-ABT.
      *
           MOVE WSS-PRM-DPR1 IN WSS-VARI TO WSS-NUM-DPR1 IN WSS-VARI.
           MOVE WSS-PRM-MPR1 IN WSS-VARI TO WSS-NUM-MPR1 IN WSS-VARI.
           MOVE WSS-PRM-SPR1 IN WSS-VARI TO WSS-NUM-SPR1 IN WSS-VARI.
           MOVE WSS-PRM-APR1 IN WSS-VARI TO WSS-NUM-APR1 IN WSS-VARI.
      *
      * VALIDACION DE PARAMETROS2
           MOVE WSS-PRM-FPR2 IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ERROR VALIDACION FECHA DE PROCESO 2 '
                                                    FEC-MENS
               PERFORM GNS-PRO-ABT.
      *
           MOVE WSS-PRM-DPR2 IN WSS-VARI TO WSS-NUM-DPR2 IN WSS-VARI.
           MOVE WSS-PRM-MPR2 IN WSS-VARI TO WSS-NUM-MPR2 IN WSS-VARI.
           MOVE WSS-PRM-SPR2 IN WSS-VARI TO WSS-NUM-SPR2 IN WSS-VARI.
           MOVE WSS-PRM-APR2 IN WSS-VARI TO WSS-NUM-APR2 IN WSS-VARI.
      *
           IF WSS-FEC-FPR1 IN WSS-VARI > WSS-FEC-FPR2 IN WSS-VARI
             DISPLAY ' FECHA INICIO > FECHA TERMINO '
               PERFORM GNS-PRO-ABT.
      *DISPLAY
           ACCEPT WSS-IND-DISP    IN WSS-VARI.
           IF  WSS-IND-DISP    IN WSS-VARI NOT = 'S' AND
               WSS-IND-DISP    IN WSS-VARI NOT = 'N'
               DISPLAY 'ERROR INDICADOR DISPLAY S/N  : '
               PERFORM GNS-PRO-ABT.

           DISPLAY 'FECHA DE PROCESO-1 (DDMMSSAA): '
                    WSS-PRM-FPR1 IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO-2 (DDMMSSAA): '
                    WSS-PRM-FPR2 IN WSS-VARI.
           DISPLAY 'DISPLAY S/N  : ' WSS-IND-DISP IN WSS-VARI.

       FIN-LEE-PARAM.
           EXIT.

      *******************************************
        BUS-CODIGO-PESO SECTION.
        INI-BUS-CODIGO-PESO.
      * Obtengo COD de Peso Chileno del Banco.
           MOVE 'TAB'               TO TAB-COD-SIST FIO-SIST.
           MOVE 'VLR'               TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '            TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL'      TO FIO-AKEY.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL LEER VLR: ' TAB-CIC-TABL IN TAB
                            ' ' FIO-STAT
              PERFORM GNS-PRO-ABT
              GO TO FIN-BUS-CODIGO-PESO.

           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
        FIN-BUS-CODIGO-PESO.
            EXIT.
      *
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------*
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS PROCESO: COLPGADP '.
           DISPLAY '==============================='.
           DISPLAY ' REGISTROS LEIDOS   CAN    : ' WSS-NUM-LCAN.
           DISPLAY ' REGISTROS NO PROC. CAN    : ' WSS-NUM-ECAN.
           DISPLAY ' REGISTROS GRABADOS ADP    : ' WSS-NUM-GADP.
           DISPLAY ' REGISTROS NO GRABADOS MOT : ' WSS-NUM-CAUS.
           DISPLAY '==============================='.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.

      ***************
       COPY COLBFHDI.
       COPY COLBFADP.
       COPY COLBFMOT.
      ***************
       COPY COLBFOPC.
       COPY COLBFVEN.
       COPY COLBFCAN.
       COPY COLBFDLC.
       COPY COLBFCYA.
       COPY COLBFRCV.
       COPY COLBFRDC.
       COPY COLBFROV.
      ***************
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY SGCBFRCD.
      ***************
       COPY COLBGVEN.
       COPY COLBGINT.
       COPY GNSBGPES.
       COPY COLBFICG.
       COPY COLBFTOC.
       COPY COLBFEVC.
       COPY TABBFCAM.
       COPY TABBFOFI.
       COPY TABBVOFI.
      ***************
       COPY GNSBFMSG.
       COPY GNSBFMSC.

       COPY GNSBFTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGABT.

       COPY GNSBBMSG.
