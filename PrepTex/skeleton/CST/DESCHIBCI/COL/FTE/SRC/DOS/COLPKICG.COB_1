       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPKICG.
       AUTHOR.          CONSIST.
      * Programa de Carga de Archivo ICG Nuevo.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OLD-REQA==.
      *<<<< WSS
      *COPY GNSBRMSG.
       COPY COLBRICG.
      *COPY COLBROLD.         COPIAR DENTRO DEL PROGRAMA.
      *    Interes Comisiones Gastos
      *    -------------------------
      *
      *    Nombre Registro : OLD
      *    Clave(s)        : OLD-KEY-IOLD(UU), OLD-KEY-IVCT(DU)
      *                      OLD-KEY-IREG(UU)
      *    Largo           : 105
      *    Bloqueo         : 2
      *    Observaciones   :
      *
       01  OLD.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OLD-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  OLD-KEY-IREG.
      *
      *        Identificacion Invocacion
               05  OLD-KEY-IINV.
      *
      *            Indicador Acceso Invocacion ( I/O )
                   07  OLD-IND-AINV                     PIC X(01).
      *
      *            Time Stamp Invocacion
                   07  OLD-STP-IINV.
      *                Fecha Invocacion
                       09  OLD-FEC-FINV.
                           11  OLD-NUM-SINV             PIC 9(02).
                           11  OLD-NUM-AINV             PIC 9(02).
                           11  OLD-NUM-MINV             PIC 9(02).
                           11  OLD-NUM-DINV             PIC 9(02).
      *
      *                Hora Invocacion
                       09  OLD-HRA-HRIN.
                           11  OLD-NUM-HHIN             PIC 9(02).
                           11  OLD-NUM-MMIN             PIC 9(02).
                           11  OLD-NUM-SSIN             PIC 9(02).
      *
      *            Codigo Origen Invocacion
                   07  OLD-COD-OINV.
      *
      *                Miscelaneo Tipo Origen
                       09  OLD-MSC-TINV                 PIC X(01).
      *
      *                Codigo Tipo Origen
                       09  OLD-COD-TINV                 PIC X(03).
      *
      *KEY     Identificacion Tasas
               05  OLD-KEY-IOLD.
      *
      *            Identificacion Cancelacion
                   07  OLD-KEY-ICAN.
      *
      *                Cod. Int. Comp. Operacion Credito
                       09  OLD-CIC-IOPC.
      *
      *                    Centro Asignacion Identificacion
                           11  OLD-CAI-IOPC.
                               13  OLD-COD-TOPC         PIC X(01).
                               13  OLD-COD-COPC         PIC X(03).
      *
      *                    Ident. Interna Computacional
                           11  OLD-IIC-IOPC.
                               13  OLD-COD-OOPC.
                                   15  OLD-COD-SOPC     PIC X(02).
                                   15  OLD-IND-SOPC     PIC X(01).
                               13  OLD-GLS-NOPC         PIC X(05).
      *
      *                Num Ident. Cancelacion
                       09  OLD-NUM-ICAN                 PIC 9(03).
      *
      *            Identificacion Vencimiento
                   07  OLD-KEY-IVCT.
      *
      *                Cod. Int. Comp. Operacion Credito
                       09  OLD-CIC-IOPV.
      *
      *                    Centro Asignacion Identificacion
                           11  OLD-CAI-IOPV.
                               13  OLD-COD-TOPV         PIC X(01).
                               13  OLD-COD-COPV         PIC X(03).
      *
      *                    Ident. Interna Computacional
                           11  OLD-IIC-IOPV.
                               13  OLD-COD-OOPV.
                                   15  OLD-COD-SOPV     PIC X(02).
                                   15  OLD-IND-SOPV     PIC X(01).
                               13  OLD-GLS-NOPV         PIC X(05).
      *
      *                Num Ident. Vencimiento
                       09  OLD-NUM-IVCT                 PIC 9(03).
      *
      *            Tipo Tasa                            COL TTAS
                   07  OLD-IND-TTAS                     PIC X(03).
      *
      *    La TASA esta asociada a una FECHA, lo que permite que esta pu
      *    variar segun periodos irregulares y arbitrarios de tiempo.
      *
      *    Esta fecha puede ser la de curse del credito o una fecha arbi
      *    ria, a eleccion del usuario.
      *
      *            Fecha Inicio Vigencia Tasa
                   07  OLD-FEC-FINI.
                       09  OLD-NUM-SINI                 PIC 9(02).
                       09  OLD-NUM-AINI                 PIC 9(02).
                       09  OLD-NUM-MINI                 PIC 9(02).
                       09  OLD-NUM-DINI                 PIC 9(02).
      *
      *    Definicion fisica de la estructura de la tasa
      *
      *REQ Codigo Tipo Tasa Interes
           03  OLD-COD-TINT.
      *
      *        Codigo Tipo Tasa
               05  OLD-COD-TIPT.
      *
      *            Base Calculo Tasa Interes            COL BCTI
                   07  OLD-MSC-BCTI                     PIC X(01).
      *
      *            Periodo Base Tasa Interes            COL PBTI
                   07  OLD-MSC-PBTI                     PIC X(01).
      *
      *            Modalidad Calculo Tasa Interes
                   07  OLD-MSC-MCTI.
      *
      *                Bajo Periodo Base Tasa           COL MCTI
                       09  OLD-MSC-BPBT                 PIC X(01).
      *
      *                Sobre Periodo Base Tasa          COL MCTI
                       09  OLD-MSC-SPBT                 PIC X(01).
      *
      *        Miscelaneo Tipo Valor Tasa Interes       TAB TVAL
      *            OLD-MSC-TVTI = "I"                   PIC X(01).
      *
      *        Codigo Valor Tasa Interes
               05  OLD-COD-CVTI                         PIC X(03).
      *
      *        Codigo Tipo Cambio Tasa Interes
      *            OLD-COD-TCTI = OLD-MSC-PBTI          PIC X(02).
      *
      *        Tipo Fecha Periodo Repacto               COL TFPR
               05  OLD-MSC-TFPR                         PIC X(01).
      *
      *        Numero Periodo Repacto
               05  OLD-NUM-PREP                         PIC 9(03).
      *
      *        Unidad Tiempo Periodo Repacto            TAB IDMA
               05  OLD-MSC-UTPR                         PIC X(01).
      *
      *    Fecha Termino Vigencia Tasa
           03  OLD-FEC-FTER.
               05  OLD-NUM-STER                         PIC 9(02).
               05  OLD-NUM-ATER                         PIC 9(02).
               05  OLD-NUM-MTER                         PIC 9(02).
               05  OLD-NUM-DTER                         PIC 9(02).
      *
      *REQ Valor Spread
           03  OLD-SGV-SPRD             COMP-3          PIC S9(03)V9(4).
      *
      *REQ Valor Tasa
           03  OLD-SGV-TINT             COMP-3          PIC S9(03)V9(4).
      *
      *    Valor en Monto
           03  OLD-SGV-MNTO             COMP-3          PIC S9(11)V9(4).
      *
      *    Glosa Disponible
           03  OLD-GLS-DISP                             PIC X(08).
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       01  WSS-VARI.
           03 WSS-NUM-ROLD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RNEW           VALUE 0      PIC 9(05).
           03 WSS-NUM-RMOD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RBAD           VALUE 0      PIC 9(05).
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPKICG' TO FIO-PROG.
           PERFORM BUS-FSIS.
      *    PERFORM OPEN-FILE.
           PERFORM LUP-PUT-REG.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           COPY GNSBGGBK.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       LUP-PUT-REG SECTION.
      *-------------------
       INI-LUP-REG.
      * Programacion de Ciclo para Registro OLD
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY REGISTROS EN ARCHIVO ANTIGUO '
               GO TO FIN-LUP-REG.
      *         MOVE 'COL'          TO MSG-COD-SIST
      *         MOVE 'OPC    ICGNX' TO MSG-COD-MENS
      *         PERFORM GET-MSG
      *         DISPLAY MSG-GLS-DESC   OPC-KEY-IOPC IN OPC
       LUP-OLD-NEW.
      * Proceso de Seleccion y Clasificacion a Partir del Registro ANTIG
           ADD 1 TO WSS-NUM-ROLD IN WSS-VARI.
           PERFORM BUS-REG-NEW.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF FIO-STAT-OKS
               GO TO LUP-OLD-NEW.
       FIN-LUP-REG.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro NEW al Procesar Registro ANT
       BUS-REG-NEW SECTION.
       INI-BUS-REG-NEW.
      * Formacion del Registro NEW
           MOVE SPACES       TO ICG.
           MOVE SPACES       TO ICG-GLS-FLAG.
           MOVE OLD-KEY-IOLD TO ICG-KEY-IICG.
           MOVE SPACES       TO ICG-STP-ITRN.
           MOVE SPACES       TO ICG-MSC-TACC.
           MOVE SPACES       TO ICG-MSC-STAT.
           MOVE SPACES       TO ICG-COD-OTRN.
           MOVE SPACES       TO ICG-COD-ATRN.
           MOVE OLD-COD-TINT TO ICG-COD-TINT.
           MOVE OLD-FEC-FTER TO ICG-FEC-FTER.
           MOVE OLD-SGV-SPRD TO ICG-SGV-SPRD.
           MOVE OLD-SGV-TINT TO ICG-SGV-TINT.
           MOVE OLD-SGV-MNTO TO ICG-SGV-MNTO.
           MOVE SPACES       TO ICG-GLS-DISP.
      * Genera el Registro NEW
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT FIO-STAT-OKS
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-ICG
               ADD 1 TO WSS-NUM-RNEW IN WSS-VARI
           ELSE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-ICG
               ADD 1 TO WSS-NUM-RMOD IN WSS-VARI.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RBAD IN WSS-VARI
               DISPLAY 'REGISTRO ERRONEO : '  ICG.
       FIN-BUS-REG-NEW.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OLD LEIDOS   : ' WSS-NUM-ROLD IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-NUM-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS NEW GRABADOS : ' WSS-NUM-RNEW IN WSS-VARI.
           DISPLAY 'REGISTROS MODIFICADOS  : ' WSS-NUM-RMOD IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           PERFORM COL-FIO-OLD.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
      *COPY GNSBFMSG.
       COPY COLBFICG.
      *COPY COLBFOLD.            COPIAR DENTRO DEL PROGRAMA.
      * Modulo FIO para registro OLD en DATACOM, 16-SEP-1988 17:55:27.54
       COL-FIO-OLD SECTION.
       INI-COL-FIO-OLD.
           MOVE ADR-OLD-REQA TO ADR-REQA.
           MOVE 'ICG' TO ADR-TABL IN ADR-REQA.
           MOVE IFD-GSIS( 14 ) TO ADR-DBID IN ADR-REQA.
           MOVE 'ICG00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-OLD.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OLD-KEY-IOLD' OR FIO-AKEY NOT > SPACES )
               MOVE OLD-KEY-IOLD IN OLD TO ADR-VKEY IN ADR-REQA
               MOVE 'ICG00' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-OLD, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
       CON-COL-FIO-OLD.
           MOVE OLD TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OLD
           ELSE
               MOVE 'COLOLD' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-OLD-REQA.
       FIN-COL-FIO-OLD.
           EXIT.
      *COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
