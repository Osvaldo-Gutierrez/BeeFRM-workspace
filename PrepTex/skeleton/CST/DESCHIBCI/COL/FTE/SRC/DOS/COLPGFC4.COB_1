       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGFC4.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.
      * Programa generador de archivos para C4.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       WORKING-STORAGE SECTION.
      *-----------------------*
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.

       COPY COLBRDVG.
       COPY COLBRDVG REPLACING == DVG. == BY == DV2.==.
       COPY COLBRTVP.
       COPY COLBRVES.
       COPY COLBRVEN.
       COPY COLBRFC4.
       COPY COLBRDC4.
       COPY COLBRRDC.
       COPY COLBRDCC.

       COPY SGCBRDBC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
       COPY GNSBRTAB.
       COPY TABBRCAM.
       COPY GNSWGCPT.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWVIDD.


       01  WSS-VARI.

          03 WSS-IND-SU90            PIC X(01).

          03 WSS-EXISTE-VES          PIC X(01).
          03 WSS-COD-PESO            PIC X(06).
          03 WSS-COD-VCOC            PIC X(06).
          03 WSS-CIC-IOPC            PIC X(12).
          03 WSS-CIC-ICLI            PIC X(12).
          03 WSS-COD-CAUX.
             05 WSS-COD-TOOC         PIC X(03).
             05 WSS-COD-AOOC         PIC X(03).
          03 WSS-VAL-CUAT            PIC S9(13)V9(04).
          03 WSS-VAL-CAPI            PIC S9(13)V9(04).
          03 WSS-VAL-INTE            PIC S9(13)V9(02).
          03 WSS-VAL-REAJ            PIC S9(13)V9(02).
      *Contadores para archivos de input.
          03 WSS-NUM-LVES            PIC 9(06).
          03 WSS-NUM-LDVG            PIC 9(06).
          03 WSS-NUM-LDV2            PIC 9(06).
          03 WSS-NUM-LTVP            PIC 9(06).
      *Contadores para registros Seleccionado.
          03 WSS-NUM-SDVG            PIC 9(06).
          03 WSS-NUM-SDV2            PIC 9(06).
          03 WSS-NUM-STVP            PIC 9(06).
      *Contadores para Grabar.
          03 WSS-NUM-GFC4            PIC 9(06).
          03 WSS-NUM-GDC4            PIC 9(06).
      *ALD-BCI-INI/16.04.1999
          03 WSS-FEC-FIMV.
            05 WSS-NUM-SIMV          PIC 9(02).
            05 WSS-NUM-AIMV          PIC 9(02).
            05 WSS-NUM-MIMV          PIC 9(02).
            05 WSS-NUM-DIMV          PIC 9(02).
          03 WSS-NUM-DMOR            PIC 9(05).
      *ALD-BCI-FIN/16.04.1999
          03 WSS-FEC-FVEN.
            05 WSS-NUM-SVEN          PIC 9(02).
            05 WSS-NUM-AVEN          PIC 9(02).
            05 WSS-NUM-MVEN          PIC 9(02).
            05 WSS-NUM-DVEN          PIC 9(02).

          03 WSS-FEC-FPRO.
            05 WSS-NUM-DPRO          PIC 9(02).
            05 WSS-NUM-MPRO          PIC 9(02).
            05 WSS-NUM-SPRO          PIC 9(02).
            05 WSS-NUM-APRO          PIC 9(02).

          03 WSS-FEC-FAUX.
            05 WSS-NUM-SAUX          PIC 9(02).
            05 WSS-NUM-AAUX          PIC 9(02).
            05 WSS-NUM-MAUX          PIC 9(02).
            05 WSS-NUM-DAUX          PIC 9(02).

          03 WSS-FEC-FINP.
            05 WSS-NUM-SINP          PIC 9(02).
            05 WSS-NUM-AINP          PIC 9(02).
            05 WSS-NUM-MINP          PIC 9(02).
            05 WSS-NUM-DINP          PIC 9(02).
      *
          03 WSS-FEC-FCOL.
            05 WSS-NUM-SCOL          PIC 9(02).
            05 WSS-NUM-ACOL          PIC 9(02).
            05 WSS-NUM-MCOL          PIC 9(02).
            05 WSS-NUM-DCOL          PIC 9(02).

          03 WSS-IND-DISP            PIC X(01).
          03 WSS-IND-CONSUMO         PIC X(01).
          03 WSS-IND-ERROR           PIC X(01).
      *
          03 WSS-IND-SCTB            PIC X(03).
          03 WSS-IND-SCBZ            PIC X(05).
          03 WSS-IND-SCRT            PIC X(03).
          03 WSS-COD-COOC.
             05 WSS-COD-TOOA         PIC X(03).
             05 WSS-COD-AOOA         PIC X(03).
          03 WSS-COD-OFOC            PIC X(03).
          03 WSS-CIC-CCSB            PIC X(12).
      *ALD-BCI-INI/12.01.1999
          03 WSS-GLS-CCSB            PIC X(40).
      *ALD-BCI-FIN/12.01.1999
          03 WSS-COD-PLZO.
             05 WSS-MSC-TPZO         PIC X(01).
             05 WSS-MSC-CPZO         PIC X(01).




          03 WSS-IND-PASO            PIC X(01).

          03 WSS-CLASIFICACION       PIC X(01).
             88 WSS-NUM-DIA          VALUE '1'.
             88 WSS-NUM-UNMES        VALUE '2'.
             88 WSS-NUM-DOSMES       VALUE '3'.
             88 WSS-NUM-CUATROMES    VALUE '4'.
             88 WSS-NUM-MASCUATRO    VALUE '5'.
      *
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGFC4' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM CAP-PARAMETROS.
           PERFORM BUSCA-COD-PESO.
           PERFORM OPEN-FILE.
           PERFORM PROCESO-DVG.
           PERFORM PROCESO-DV2.
           PERFORM PROCESO-TVP.
           PERFORM CLOSE-FILE.
           PERFORM MUS-ESTADISTICA.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
      *
       CAP-PARAMETROS SECTION.
       INI-CAP-PARAMETROS.
      *Contadores de registros grabados.
           MOVE ZEROES  TO WSS-NUM-GFC4 IN WSS-VARI.
           MOVE ZEROES  TO WSS-NUM-GDC4 IN WSS-VARI.
      *
      *     DISPLAY '============================'.
      *     DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      *     DISPLAY '============================'.
      * Captura Fecha de Proceso
           DISPLAY 'INGRESE FECHA PROCESO (DDMMSSAA) '.
           DISPLAY '-------------------------------------'.
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.

      * Captura indicador de display
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
                                       NOT = 'N'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI.

      *VAL-FEC Valida Fecha de Informe
           DISPLAY 'FECHA DE PROCESO : ' WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
      *ALD-BCI-INI/15.11.1999
      *    MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
      *ALD-BCI-FIN/15.11.1999
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT.

      * MOVER FECHA DE PROCESO.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DAUX IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MAUX IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AAUX IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SAUX IN WSS-VARI.
      *     DISPLAY 'FECHA : ' WSS-FEC-FAUX IN WSS-VARI.
      *
      *     DISPLAY '============================'.
      *     DISPLAY 'FIN VALIDACION DE PARAMETROS'.
      *     DISPLAY '============================'.
       FIN-CAP-PARAMETROS.
           EXIT.
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
      * Archivo del devengo.
            MOVE FIO-INP TO FIO-CMND.
            PERFORM COL-FIO-DVG.
            IF NOT FIO-STAT-OKS
                DISPLAY 'ERROR DE APERTURA DVG (FIO-STAT) ' FIO-STAT
                PERFORM GNS-PRO-ABT.


      * ARCHIVO DE CREDITOS CASTIGADOS.(1 POR CREDITO)
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-DV2.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR DE APERTURA DV2 (FIO-STAT) ' FIO-STAT
               PERFORM GNS-PRO-ABT.


      * Archivo de Tarjetas VISAS.
            MOVE FIO-INP TO FIO-CMND.
            PERFORM COL-FIO-TVP.
            IF NOT FIO-STAT-OKS
                DISPLAY 'ERROR DE APERTURA TVP (FIO-STAT) ' FIO-STAT
                PERFORM GNS-PRO-ABT.


      *Apertura archivo VEN secuencial
            MOVE FIO-INP TO FIO-CMND.
            PERFORM COL-FIO-VES.
            IF NOT FIO-STAT-OKS
                DISPLAY 'ERROR DE APERTURA VES (FIO-STAT) ' FIO-STAT
                PERFORM GNS-PRO-ABT.



      * Archivo de salida (Datos que utiliza Informe).
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-DC4.
      * Archivo de salida .
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-FC4.
       FIN-OPEN-FILE.
           EXIT.
      *---------------------------------------------------------*
       PROCESO-DVG SECTION.
       INI-PROCESO-DVG.
      * DVG-MSC-TCCT IN DVG = 'CAP' INT=0,REAJ=0
      *     DISPLAY 'INICIO LEE ARCHIVO DVG '.


           MOVE ZEROES  TO WSS-VAL-CUAT IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-CAPI IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-REAJ IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-INTE IN WSS-VARI.
      *CONTADORES DE REGISTROS LEIDOS VES
           MOVE ZEROES  TO WSS-NUM-LVES IN WSS-VARI.
      *CONTADORES DE REGISTROS LEIDOS DVG
           MOVE ZEROES  TO WSS-NUM-LDVG IN WSS-VARI.
      *CONTADORES DE REGISTROS SELECCIONADO DVG
           MOVE ZEROES  TO WSS-NUM-SDVG IN WSS-VARI.

       INI-REG-DVG.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           IF NOT FIO-STAT-OKS
              GO TO FIN-PROCESO-DVG.


           MOVE 'S' TO WSS-EXISTE-VES.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VES.
           IF NOT FIO-STAT-OKS
               MOVE 'N' TO WSS-EXISTE-VES
           ELSE
               ADD 1 TO WSS-NUM-LVES IN WSS-VARI
               MOVE VES TO VEN.



       PRI-REG-DVG.
           MOVE DVG-CIC-IOPC IN DVG TO WSS-CIC-IOPC IN WSS-VARI.
      *   Valida fecha de proceso.
           IF DVG-FEC-FPRO IN DVG NOT = WSS-FEC-FAUX IN WSS-VARI
             DISPLAY 'FECHA DE PROCESO DVG NO CORRESPONDE'
             DISPLAY 'FECHA DE PROCESO DVG '
                      DVG-FEC-FPRO IN DVG
             DISPLAY 'FECHA DE PROCESO  '
                      WSS-FEC-FAUX IN WSS-VARI
      *ALD-BCI-INI/15.11.1999
             PERFORM CLOSE-FILE
             PERFORM GNS-PRO-ABT
      *ALD-BCI-FIN/15.11.1999
             GO TO FIN-PROCESO-DVG.
      *
           PERFORM MOVER-DVG-WSS.
      *   Validar si es credito de consumo.
           PERFORM LEE-TAB-AUX.
           IF WSS-IND-CONSUMO = 'N'
             MOVE '1' TO WSS-IND-PASO IN WSS-VARI
             GO TO  NXT-PROCESO-DVG.

           MOVE 'N' TO WSS-IND-SU90 IN WSS-VARI.
           IF WSS-COD-TOOA IN WSS-VARI = 'TCR'
               MOVE 'S' TO WSS-IND-SU90 IN WSS-VARI.

      *   buscar clasificacion.
           PERFORM BUS-CLASIFICA.
           IF  WSS-IND-ERROR IN WSS-VARI NOT = '0'
              MOVE '1' TO WSS-IND-PASO IN WSS-VARI
              GO TO  NXT-PROCESO-DVG.
      *ALD-BCI-INI/16.04.1999
           MOVE FEC-NDIA          TO WSS-NUM-DMOR IN WSS-VARI.
      *ALD-BCI-FIN/16.04.1999
      *   buscar cic cliente.
           PERFORM OBT-CIC-ICLI.
           IF  WSS-IND-ERROR IN WSS-VARI NOT = '0'
              MOVE '1' TO WSS-IND-PASO IN WSS-VARI
              GO TO  NXT-PROCESO-DVG.

      *   Busca cuenta capital.
           PERFORM OBT-CUENTA-DCC.
           IF  WSS-IND-ERROR IN WSS-VARI NOT = '0'
              MOVE '1' TO WSS-IND-PASO IN WSS-VARI
              GO TO  NXT-PROCESO-DVG.

      *Loop de lectura para Archivo DVG.
       LOP-REG-DVG.
           IF DVG-CIC-IOPC IN DVG NOT = WSS-CIC-IOPC IN WSS-VARI
             ADD 1  TO WSS-NUM-SDVG IN WSS-VARI
      *Validar Moneda
             PERFORM DET-SALDO-EN-PESOS
             IF  WSS-IND-ERROR IN WSS-VARI NOT = '0'
                 GO TO PRI-REG-DVG
             ELSE
                PERFORM GRABA-REG-FC4
                PERFORM PRO-CUOTAS-ATRASADAS
                PERFORM GRABA-REG-DC4
                GO TO PRI-REG-DVG.
      *
           IF DVG-MSC-TCCT IN DVG NOT = 'RPC' AND
             DVG-MSC-TCCT IN DVG NOT = 'IPC'
             MOVE DVG-VAL-SCRE IN DVG TO WSS-VAL-CAPI IN WSS-VARI
           ELSE
           IF DVG-MSC-TCCT IN DVG = 'RPC'
             MOVE DVG-VAL-SCRE IN DVG TO WSS-VAL-CAPI IN WSS-VARI
             MOVE DVG-SGV-ICTB IN DVG TO WSS-VAL-REAJ IN WSS-VARI
           ELSE
           IF DVG-MSC-TCCT IN DVG = 'IPC'
             MOVE DVG-VAL-SCRE IN DVG TO WSS-VAL-CAPI IN WSS-VARI
             MOVE DVG-SGV-ICTB IN DVG TO WSS-VAL-INTE IN WSS-VARI.

       NXT-PROCESO-DVG.
           PERFORM LEE-REG-DVG.
           IF  FIO-STAT-OKS
              IF WSS-IND-PASO IN WSS-VARI = '1'
                 GO TO PRI-REG-DVG
               ELSE
                 GO TO LOP-REG-DVG.

           IF  NOT FIO-STAT-OKS
              IF  WSS-VAL-CAPI IN WSS-VARI NOT = ZEROES OR
                  WSS-VAL-INTE IN WSS-VARI NOT = ZEROES OR
                  WSS-VAL-REAJ IN WSS-VARI NOT = ZEROES
                  ADD 1  TO WSS-NUM-SDVG IN WSS-VARI
      *Validar Moneda
                 PERFORM DET-SALDO-EN-PESOS
                 IF  WSS-IND-ERROR IN WSS-VARI = '0'
                     PERFORM GRABA-REG-FC4
                     PERFORM PRO-CUOTAS-ATRASADAS
                     PERFORM GRABA-REG-DC4.
       FIN-PROCESO-DVG.
           EXIT.
      *---------------------------------------------------------*
      *
       PROCESO-DV2 SECTION.
       INI-PROCESO-DV2.
      *     DISPLAY 'INICIO LEE ARCHIVO DV2 '.
      * Loop de lectura para Archivo DV2.
       INI-REG-DV2.
      *     DISPLAY 'INICIO LEE ARCHIVO DV2 CREDITOS =CAS= '
           MOVE ZEROES  TO WSS-VAL-CUAT IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-CAPI IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-REAJ IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-INTE IN WSS-VARI.
      *CONTADORES DE REGISTROS LEIDOS DV2.
           MOVE ZEROES  TO WSS-NUM-LDV2 IN WSS-VARI.
      *CONTADORES DE REGISTROS SELECCIONADO DV2.
           MOVE ZEROES  TO WSS-NUM-SDV2 IN WSS-VARI.
      *Busca fecha para considerar castigos (-12 mes).
           PERFORM BUS-FECHA-CAS.
           IF  WSS-IND-ERROR IN WSS-VARI NOT = '0'
               DISPLAY 'ERROR CALCULO DE FECHA PARA CASTIGO'
               GO TO FIN-PROCESO-DV2.

       LOP-REG-DV2.
           PERFORM LEE-REG-DV2.
           IF NOT FIO-STAT-OKS
              GO TO FIN-PROCESO-DV2.

      *Valida fecha de proceso.
           IF DVG-FEC-FPRO IN DV2 NOT = WSS-FEC-FAUX IN WSS-VARI
             DISPLAY 'FECHA DE PROCESO DV2 NO CORRESPONDE'
             DISPLAY 'FECHA DE PROCESO DV2 '
                      DVG-FEC-FPRO IN DV2
             DISPLAY 'FECHA DE PROCESO  '
                      WSS-FEC-FAUX IN WSS-VARI
      *ALD-BCI-INI/15.11.1999
             PERFORM CLOSE-FILE
             PERFORM GNS-PRO-ABT
      *ALD-BCI-FIN/15.11.1999
             GO TO FIN-PROCESO-DV2.

      *Valida fecha De castigo si corresponde.
           IF DVG-FEC-FINP IN DV2  < WSS-FEC-FINP IN WSS-VARI
             GO TO LOP-REG-DV2.

           PERFORM MOVER-DV2-WSS.

      *Validar credito de consumo.
           PERFORM LEE-TAB-AUX.
           IF WSS-IND-CONSUMO = 'N'
             GO TO LOP-REG-DV2.

      *  buscar cic cliente.
           PERFORM OBT-CIC-ICLI.
           IF  WSS-IND-ERROR IN WSS-VARI NOT = '0'
              MOVE '1' TO WSS-IND-PASO IN WSS-VARI
              GO TO LOP-REG-DV2.

      * NO Busca cuenta capital.
      * NO Validar Moneda
      * grabar archivo
           ADD 1  TO WSS-NUM-SDV2 IN WSS-VARI.
           PERFORM GRABA-REG-FC4.
           PERFORM GRABA-REG-DC4.
           GO TO LOP-REG-DV2.
       FIN-PROCESO-DV2.
           EXIT.

       PROCESO-TVP SECTION.
      *--------------------*
       INI-PROCESO-TVP.
      *     DISPLAY 'INICIO LEE ARCHIVO TVP '.
      * LOOP DE LECTURA PARA ARCHIVO TVP.
       INI-REG-TVP.
      *     DISPLAY 'INICIO LEE ARCHIVO TVP '.
           MOVE ZEROES  TO WSS-VAL-CUAT IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-CAPI IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-REAJ IN WSS-VARI.
           MOVE ZEROES  TO WSS-VAL-INTE IN WSS-VARI.
      *CONTADORES DE REGISTROS LEIDOS TVP.
           MOVE ZEROES  TO WSS-NUM-LTVP IN WSS-VARI.
      *CONTADORES DE REGISTROS SELECCIONADO TVP.
           MOVE ZEROES  TO WSS-NUM-STVP IN WSS-VARI.

       LOP-REG-TVP.
           PERFORM LEE-REG-TVP.
           IF NOT FIO-STAT-OKS
              GO TO FIN-PROCESO-TVP.

      *Valida fecha de proceso.
           IF TVP-FEC-FPRO IN TVP NOT = WSS-FEC-FAUX IN WSS-VARI
             DISPLAY 'FECHA DE PROCESO TVP NO CORRESPONDE'
             DISPLAY 'FECHA DE PROCESO TVP '
                      TVP-FEC-FPRO IN TVP
             DISPLAY 'FECHA DE PROCESO  '
                      WSS-FEC-FAUX IN WSS-VARI
      *ALD-BCI-INI/15.11.1999
             PERFORM CLOSE-FILE
             PERFORM GNS-PRO-ABT
      *ALD-BCI-FIN/15.11.1999
             GO TO LOP-REG-TVP.

           PERFORM MOVER-TVP-WSS.

           MOVE 'N' TO WSS-IND-SU90 IN WSS-VARI.

      *   buscar clasificacion.
           PERFORM BUS-CLASIFICA.
      *ALD-BCI-INI/16.04.1999
           MOVE FEC-NDIA          TO WSS-NUM-DMOR IN WSS-VARI.
      *ALD-BCI-FIN/16.04.1999

      *NO SE VALIDA SI ES  CREDITO DE CONSUMO.
      *NO BUSCAR CIC CLIENTE.
           MOVE TVP-CIC-ICLI IN TVP TO RDC-CIC-ICLI IN RDC.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

      *NO  BUSCA CUENTA CAPITAL.
      *ALD-BCI-INI/12.07.1999
           MOVE SPACES TO WSS-GLS-CCSB IN WSS-VARI.
           IF WSS-CIC-CCSB IN WSS-VARI > SPACES
             MOVE 'TAB'                    TO TAB-COD-SIST
             MOVE 'PCC'                    TO TAB-COD-TTAB IN TAB
             MOVE WSS-CIC-CCSB IN WSS-VARI TO TAB-COD-CTAB IN TAB
             PERFORM BUS-TAB
             MOVE TAB-GLS-DESC IN TAB      TO WSS-GLS-CCSB IN WSS-VARI.
      *ALD-BCI-FIN/12.07.1999
      *NO VALIDAR MONEDA
      * grabar archivo
           ADD 1  TO WSS-NUM-STVP IN WSS-VARI.
           PERFORM GRABA-REG-FC4.
           PERFORM GRABA-REG-DC4.
           GO TO LOP-REG-TVP.
       FIN-PROCESO-TVP.
           EXIT.

      *----------------------------------------------------------*
       MOVER-DVG-WSS SECTION.
      *---------------------*
       INI-MOVER-DVG-WSS.
           MOVE ZEROES              TO WSS-VAL-CAPI IN WSS-VARI.
           MOVE ZEROES              TO WSS-VAL-REAJ IN WSS-VARI.
           MOVE ZEROES              TO WSS-VAL-INTE IN WSS-VARI.
           MOVE '0'                 TO WSS-CLASIFICACION.
           MOVE '0'                 TO WSS-IND-PASO IN WSS-VARI.
      *
      *  Codigo de plzo.
           MOVE DVG-COD-PLZO IN DVG TO WSS-COD-PLZO IN WSS-VARI.
           MOVE DVG-IND-SCTB IN DVG TO WSS-IND-SCTB IN WSS-VARI.
           MOVE DVG-IND-SCBZ IN DVG TO WSS-IND-SCBZ IN WSS-VARI.
           MOVE DVG-IND-SCRT IN DVG TO WSS-IND-SCRT IN WSS-VARI.
           MOVE DVG-COD-VCOC IN DVG TO WSS-COD-VCOC IN WSS-VARI.
           MOVE DVG-COD-OFOC IN DVG TO WSS-COD-OFOC IN WSS-VARI.
           MOVE DVG-COD-COOC IN DVG TO WSS-COD-COOC IN WSS-VARI.
           MOVE DVG-FEC-FCOL IN DVG TO WSS-FEC-FCOL IN WSS-VARI.
           MOVE DVG-FEC-FVEN IN DVG TO WSS-FEC-FVEN IN WSS-VARI.
      *ALD-BCI-INI/16.04.1999
           MOVE DVG-FEC-FVEN IN DVG TO WSS-FEC-FIMV IN WSS-VARI.
      *ALD-BCI-FIN/16.04.1999

       FIN-MOVER-DVG-WSS.
           EXIT.

       MOVER-DV2-WSS SECTION.
      *---------------------*
       INI-MOVER-DVG-WSS.
           MOVE '0'                 TO WSS-IND-PASO IN WSS-VARI.
      *
           MOVE ZEROES              TO WSS-VAL-CUAT IN WSS-VARI.
           MOVE ZEROES              TO WSS-VAL-CAPI IN WSS-VARI.
           MOVE ZEROES              TO WSS-VAL-REAJ IN WSS-VARI.
           MOVE ZEROES              TO WSS-VAL-INTE IN WSS-VARI.
           MOVE SPACES              TO WSS-CIC-CCSB IN WSS-VARI.
      *ALD-BCI-FIN/12.07.1999
           MOVE SPACES              TO WSS-GLS-CCSB IN WSS-VARI.
      *ALD-BCI-FIN/12.07.1999
           MOVE '5'                 TO WSS-CLASIFICACION.
           MOVE DVG-CIC-IOPC IN DV2 TO WSS-CIC-IOPC IN WSS-VARI.
           MOVE DVG-IND-SCBZ IN DV2 TO WSS-IND-SCBZ IN WSS-VARI.
           MOVE DVG-COD-OFOC IN DV2 TO WSS-COD-OFOC IN WSS-VARI.
           MOVE DVG-FEC-FVEN IN DV2 TO WSS-FEC-FVEN IN WSS-VARI.
      *ALD-BCI-INI/16.04.1999
           MOVE ZEROES              TO WSS-FEC-FIMV IN WSS-VARI.
           MOVE ZEROES              TO WSS-NUM-DMOR IN WSS-VARI.
      *ALD-BCI-FIN/16.04.1999
      *
           MOVE DVG-COD-PLZO IN DV2 TO WSS-COD-PLZO IN WSS-VARI.
           MOVE DVG-IND-SCTB IN DV2 TO WSS-IND-SCTB IN WSS-VARI.
           MOVE DVG-IND-SCRT IN DV2 TO WSS-IND-SCRT IN WSS-VARI.
           MOVE DVG-COD-VCOC IN DV2 TO WSS-COD-VCOC IN WSS-VARI.
           MOVE DVG-COD-COOC IN DV2 TO WSS-COD-COOC IN WSS-VARI.
           MOVE DVG-FEC-FCOL IN DV2 TO WSS-FEC-FCOL IN WSS-VARI.

       FIN-MOVER-DV2-WSS.
           EXIT.

       MOVER-TVP-WSS SECTION.
      *---------------------*
       INI-MOVER-TVP-WSS.
           MOVE '0'                 TO WSS-CLASIFICACION.
           MOVE '0'                 TO WSS-IND-PASO IN WSS-VARI.
           MOVE TVP-VAL-CAPI IN TVP TO WSS-VAL-CAPI IN WSS-VARI.
           MOVE TVP-VAL-REAJ IN TVP TO WSS-VAL-REAJ IN WSS-VARI.
           MOVE TVP-VAL-INTE IN TVP TO WSS-VAL-INTE IN WSS-VARI.
           MOVE TVP-VAL-CUAT IN TVP TO WSS-VAL-CUAT IN WSS-VARI.
      *
      *  Codigo de plzo.
           MOVE TVP-IND-SCTB IN TVP TO WSS-IND-SCTB IN WSS-VARI.
           MOVE TVP-COD-OFOC IN TVP TO WSS-COD-OFOC IN WSS-VARI.
           MOVE TVP-FEC-FVEN IN TVP TO WSS-FEC-FVEN IN WSS-VARI.
      *ALD-BCI-INI/16.04.1999
           MOVE TVP-FEC-FVEN IN TVP TO WSS-FEC-FIMV IN WSS-VARI.
      *ALD-BCI-FIN/16.04.1999
           MOVE TVP-CIC-IOPC IN TVP TO WSS-CIC-IOPC IN WSS-VARI.
           MOVE TVP-CIC-CCSB IN TVP TO WSS-CIC-CCSB IN WSS-VARI.

       FIN-MOVER-DVG-WSS.
           EXIT.

       LEE-TAB-AUX SECTION.
      *-------------------*
       INI-LEE-TAB-AUX.
      *BUS-TAB busca glosa de codigo en tabla
           MOVE 'N' TO WSS-IND-CONSUMO IN WSS-VARI.
           MOVE 'COL'                     TO TAB-COD-SIST.
           MOVE 'AUX'                     TO TAB-COD-TTAB IN TAB.
           MOVE  WSS-COD-COOC IN WSS-VARI TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           MOVE TAB-CIC-CTAB IN TAB TO WSS-COD-CAUX IN WSS-VARI.
           IF WSS-COD-TOOC IN WSS-VARI = 'CON'
              MOVE 'S' TO WSS-IND-CONSUMO IN WSS-VARI.
      *     DISPLAY 'COD-COOC ' WSS-COD-COOC IN WSS-VARI.
      *     DISPLAY ' INDICADOR ' WSS-IND-CONSUMO IN WSS-VARI.
       FIN-LEE-TAB-AUX.
           EXIT.

       OBT-CIC-ICLI SECTION.
      *--------------------*
       INI-OBT-CIC-ICLI.
           MOVE '0'  TO WSS-IND-ERROR IN WSS-VARI.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE  WSS-CIC-IOPC IN WSS-VARI TO RDC-CIC-IOPC IN RDC.
           MOVE  ZEROES                   TO RDC-NUM-IDLC IN RDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
              WSS-CIC-IOPC IN WSS-VARI = RDC-CIC-IOPC IN RDC )
              MOVE '1'  TO WSS-IND-ERROR IN WSS-VARI
              GO TO RES-OBT-CIC-ICLI.


           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
              MOVE '2'  TO WSS-IND-ERROR IN WSS-VARI
              GO TO RES-OBT-CIC-ICLI.

       RES-OBT-CIC-ICLI.
              IF  WSS-IND-ERROR IN WSS-VARI = '1'
                  DISPLAY 'ERROR LECTURA COLRDC: '
                           WSS-CIC-IOPC IN WSS-VARI
              ELSE
              IF  WSS-IND-ERROR IN WSS-VARI = '2'
                  DISPLAY 'ERROR LECTURA SGCDBC: '
                           RDC-CIC-ICLI IN RDC ' ' FIO-STAT.
       FIN-OBT-CIC-ICLI.
           EXIT.

       BUS-FECHA-CAS SECTION.
      *----------------------*
       INI-BUS-FECHA-CAS.
      * Calcular diferencia entre FVEN y FPRO en MES.
           MOVE '0' TO WSS-IND-ERROR IN WSS-VARI.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO FEC-ITM1.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO FEC-ITM2.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO FEC-ITM3.
           MOVE WSS-NUM-APRO IN WSS-VARI TO FEC-ITM4.
      *DMSA.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-MES  TO FEC-CMND.
           MOVE 12           TO FEC-NMES.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
              MOVE '1'  TO WSS-IND-ERROR IN WSS-VARI
              GO TO FIN-BUS-FECHA-CAS.

           MOVE FEC-ITM1  TO  WSS-NUM-DINP IN WSS-VARI.
           MOVE FEC-ITM2  TO  WSS-NUM-MINP IN WSS-VARI.
           MOVE FEC-ITM3  TO  WSS-NUM-SINP IN WSS-VARI.
           MOVE FEC-ITM4  TO  WSS-NUM-AINP IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI  = 'S'
              DISPLAY 'FECHA CAS (-12) '
              WSS-FEC-FINP IN WSS-VARI.

       FIN-BUS-FECHA-CAS.
           EXIT.

       BUS-CLASIFICA SECTION.
      *----------------------*
       INI-BUS-CLASIFICA.
      * Calcular diferencia entre FVEN y FPRO en MES.
           MOVE '0' TO WSS-CLASIFICACION.
           MOVE '0' TO WSS-IND-ERROR IN WSS-VARI.
      *ALD-BCI-INI/16.04.1999
           MOVE ZEROES TO FEC-NDIA.
      *ALD-BCI-FIN/16.04.1999
      *Credito al dia.
           IF WSS-FEC-FAUX IN WSS-VARI  < WSS-FEC-FVEN IN WSS-VARI
              MOVE '1' TO WSS-CLASIFICACION
              GO TO FIN-BUS-CLASIFICA.


           MOVE WSS-NUM-DVEN IN WSS-VARI TO FEC-DEC1.
           MOVE WSS-NUM-MVEN IN WSS-VARI TO FEC-MEC1.
           MOVE WSS-NUM-SVEN IN WSS-VARI TO FEC-SEC1.
           MOVE WSS-NUM-AVEN IN WSS-VARI TO FEC-AEC1.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-APRO IN WSS-VARI TO FEC-AEC2.

           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
              MOVE '1'  TO WSS-IND-ERROR IN WSS-VARI
              GO TO RES-BUS-CLASIFICA.

            IF WSS-IND-SU90 IN WSS-VARI = 'S'
                COMPUTE FEC-NDIA = FEC-NDIA + 90.


      *     IF FEC-NDIA = ZEROES
      *        MOVE '1' TO WSS-CLASIFICACION
      *     ELSE
      *     IF FEC-NDIA > ZEROES  AND

           IF  FEC-NDIA < 31
              MOVE '2' TO WSS-CLASIFICACION
           ELSE
           IF FEC-NDIA > 30  AND
              FEC-NDIA < 61
              MOVE '3' TO WSS-CLASIFICACION
           ELSE
           IF FEC-NDIA > 60  AND
              FEC-NDIA < 121
              MOVE '4' TO WSS-CLASIFICACION
           ELSE
           IF FEC-NDIA > 120
              MOVE '5' TO WSS-CLASIFICACION.
      *ALD-BCI-INI/16.04.1999
      * SI MISMO DIA DE PROCESO MUEVE 1 FEN-NDIA PARA CONSIDERAR UNA
      * CUOTA MOROSA Y 1 DIA DE MORA EN FC4
           IF FEC-NDIA = ZEROES
               MOVE 1 TO FEC-NDIA.
      *ALD-BCI-FIN/16.04.1999

       RES-BUS-CLASIFICA.
              IF  WSS-IND-ERROR IN WSS-VARI = '1'
                  DISPLAY 'FEC ERR : ' WSS-FEC-FVEN '/' WSS-FEC-FPRO.
       FIN-BUS-CLASIFICA.
           EXIT.



       LEE-REG-DVG SECTION.
      *-------------------*
       INI-LEE-REG-DVG.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           IF FIO-STAT-OKS
              ADD 1 TO WSS-NUM-LDVG IN WSS-VARI
              GO TO FIN-LEE-REG-DVG.
           IF NOT FIO-STAT-OKS  AND
              WSS-NUM-LDVG IN WSS-VARI = ZEROES
              DISPLAY 'ARCHIVO DVG NO EXISTEN DATOS '.
       FIN-LEE-REG-DVG.
           EXIT.

       LEE-REG-DV2 SECTION.
      *-------------------*
       INI-LEE-REG-DV2.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DV2.
           IF FIO-STAT-OKS
              ADD 1 TO WSS-NUM-LDV2 IN WSS-VARI
              GO TO FIN-LEE-REG-DV2.
           IF NOT FIO-STAT-OKS  AND
              WSS-NUM-LDV2 IN WSS-VARI = ZEROES
              DISPLAY 'ARCHIVO DV2 NO EXISTEN DATOS '.
       FIN-LEE-REG-DV2.
           EXIT.

       LEE-REG-TVP SECTION.
      *-------------------*
      * INI-LEE-REG-TVP.
             MOVE FIO-GET-NXT TO FIO-CMND.
             PERFORM COL-FIO-TVP.
           IF FIO-STAT-OKS
              ADD 1 TO WSS-NUM-LTVP IN WSS-VARI
              GO TO FIN-LEE-REG-TVP.
           IF NOT FIO-STAT-OKS  AND
              WSS-NUM-LTVP IN WSS-VARI = ZEROES
              DISPLAY 'ARCHIVO TVP NO EXISTEN DATOS '.
       FIN-LEE-REG-TVP.
           EXIT.

       GRABA-REG-DC4 SECTION.
      *---------------------*
       INI-GRAB-REG-DC4.
      *    FECHA DE PROCESO O CONTABLE
      *
           MOVE WSS-FEC-FAUX IN WSS-VARI TO DC4-FEC-FPRO IN DC4.
           MOVE WSS-CIC-IOPC IN WSS-VARI TO DC4-CIC-IOPC IN DC4.
           MOVE RDC-CIC-ICLI IN RDC      TO DC4-CIC-ICLI IN DC4.
           MOVE WSS-CIC-CCSB IN WSS-VARI TO DC4-CIC-CCSB IN DC4.
           MOVE WSS-VAL-CAPI IN WSS-VARI TO DC4-VAL-CAPI IN DC4.
           MOVE WSS-VAL-REAJ IN WSS-VARI TO DC4-VAL-REAJ IN DC4.
           MOVE WSS-VAL-INTE IN WSS-VARI TO DC4-VAL-INTE IN DC4.
           MOVE WSS-VAL-CUAT IN WSS-VARI TO DC4-VAL-CUAT IN DC4.
           MOVE WSS-IND-SCTB IN WSS-VARI TO DC4-IND-SCTB IN DC4.
           MOVE WSS-CLASIFICACION        TO DC4-COD-CLAS IN DC4.
           MOVE FIO-PUT  TO FIO-CMND.
           PERFORM COL-FIO-DC4.
           IF FIO-STAT-OKS
              ADD 1 TO WSS-NUM-GDC4 IN WSS-VARI.
       FIN-GRAB-REG-DC4.
           EXIT.

       GRABA-REG-FC4 SECTION.
      *---------------------*
       INI-GRAB-REG-FC4.
      *    FECHA DE PROCESO O CONTABLE

           COMPUTE FC4-VAL-SCRE IN FC4 ROUNDED = WSS-VAL-CAPI +
                                                 WSS-VAL-REAJ +
                                                 WSS-VAL-INTE.

      *ALD-BCI-INI/15.12.1998...SE INCLUYEN CASTIGOS, LUEGO SE
      *SE EXCLUYEN PARA ENTREGA EDUARDO S.
      *    IF FC4-VAL-SCRE IN FC4 = ZEROES
      *        GO TO FIN-GRAB-REG-FC4.
      *ALD-BCI-FIN/15.12.1998

           MOVE '|'                      TO FC4-GLS-FIL1 IN FC4.
           MOVE '|'                      TO FC4-GLS-FIL2 IN FC4.
           MOVE '|'                      TO FC4-GLS-FIL3 IN FC4.
           MOVE '|'                      TO FC4-GLS-FIL4 IN FC4.
           MOVE '|'                      TO FC4-GLS-FIL5 IN FC4.
           MOVE '|'                      TO FC4-GLS-FIL6 IN FC4.
      *ALD-BCI-INI/12.01.1999
           MOVE '|'                      TO FC4-GLS-FIL7 IN FC4.
           MOVE '|'                      TO FC4-GLS-FIL8 IN FC4.
           MOVE WSS-CIC-CCSB IN WSS-VARI TO FC4-CIC-CCSB IN FC4.
           MOVE WSS-GLS-CCSB IN WSS-VARI TO FC4-GLS-CCSB IN FC4.
      *ALD-BCI-FIN/12.01.1999
      *ALD-BCI-INI/16.04.1999
           MOVE '|'                      TO FC4-GLS-FIL9 IN FC4.
           MOVE '|'                      TO FC4-GLS-FI10 IN FC4.
           MOVE WSS-NUM-DMOR IN WSS-VARI TO FC4-NUM-NDIA IN FC4.
           MOVE WSS-NUM-DIMV IN WSS-VARI TO FC4-NUM-DIMV IN FC4.
           MOVE WSS-NUM-MIMV IN WSS-VARI TO FC4-NUM-MIMV IN FC4.
           MOVE WSS-NUM-SIMV IN WSS-VARI TO FC4-NUM-SIMV IN FC4.
           MOVE WSS-NUM-AIMV IN WSS-VARI TO FC4-NUM-AIMV IN FC4.
      *ALD-BCI-FIN/16.04.1999

           MOVE WSS-CIC-IOPC IN WSS-VARI TO FC4-CIC-IOPC IN FC4.
           MOVE DBC-NUM-ICLI IN DBC      TO FC4-NUM-ICLI IN FC4.

           MOVE DBC-GLS-NOMC IN DBC      TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN                 TO DBC-GLS-NOMC IN DBC.

           MOVE DBC-GLS-NOMC IN DBC      TO FC4-GLS-NOMC IN FC4.
           MOVE WSS-COD-OFOC IN WSS-VARI TO FC4-COD-OFOC IN FC4.


           IF  WSS-NUM-DIA
               MOVE 'A'  TO FC4-COD-CLAS IN FC4
           ELSE
              IF  WSS-NUM-UNMES
                  MOVE 'B' TO FC4-COD-CLAS IN FC4
              ELSE
                 IF WSS-NUM-DOSMES
                  MOVE 'B-' TO FC4-COD-CLAS IN FC4
                 ELSE
                   IF WSS-NUM-CUATROMES
                      MOVE 'C' TO FC4-COD-CLAS IN FC4
                   ELSE
                    IF WSS-NUM-MASCUATRO
                       MOVE 'D' TO FC4-COD-CLAS IN FC4.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-FC4.
           IF FIO-STAT-OKS
              ADD 1 TO WSS-NUM-GFC4 IN WSS-VARI.
       FIN-GRAB-REG-FC4.
           EXIT.

       BUSCA-COD-PESO SECTION.
      *---------------------*
       INI-BUSCA-COD-PESO.
      *Rescata el codigo de PESOS
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
              MOVE 'COL'          TO MSG-COD-SIST
              MOVE 'CICPESONEX' TO MSG-COD-MENS IN MSG
              PERFORM GET-MSG
              DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
              PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
       FIN-BUSCA-COD-PESO.
           EXIT.

       OBT-CUENTA-DCC SECTION.
      *----------------------*
       INI-OBT-CUENTA-DCC.
           MOVE SPACES   TO WSS-CIC-CCSB IN WSS-VARI.
           MOVE '0'      TO WSS-IND-ERROR IN WSS-VARI.
           IF WSS-IND-SCTB IN WSS-VARI = 'MUL'
               MOVE SPACES TO DCC-IND-SCTB IN DCC
           ELSE
               MOVE WSS-IND-SCTB IN WSS-VARI TO DCC-IND-SCTB IN DCC.

           MOVE WSS-IND-SCBZ IN WSS-VARI TO DCC-IND-SCBZ IN DCC.
           MOVE WSS-IND-SCRT IN WSS-VARI TO DCC-IND-SCRT IN DCC.
           MOVE 'DCC'                    TO DCC-COD-TTAB IN DCC.
           MOVE 'CAP'                    TO DCC-MSC-TCCT IN DCC.
           MOVE WSS-COD-VCOC IN WSS-VARI TO DCC-COD-VCAM IN DCC.
           MOVE WSS-COD-COOC IN WSS-VARI TO DCC-COD-COCR IN DCC.
           MOVE WSS-COD-PLZO IN WSS-VARI TO DCC-COD-PLZO IN DCC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           IF NOT FIO-STAT-OKS
              MOVE '* ' TO DCC-COD-PLZO IN DCC
              MOVE FIO-GET-KEY TO FIO-CMND
              PERFORM COL-FIO-DCC
              IF NOT FIO-STAT-OKS
                 MOVE '*     ' TO DCC-COD-COCR IN DCC
                 MOVE FIO-GET-KEY TO FIO-CMND
                 PERFORM COL-FIO-DCC
                 IF NOT FIO-STAT-OKS
                    MOVE '1' TO WSS-IND-ERROR IN WSS-VARI
                    GO TO RES-OBT-CUENTA-DCC.

           MOVE DCC-GLS-CCSB IN DCC  TO WSS-CIC-CCSB IN WSS-VARI.
      *ALD-BCI-INI/12.01.1999
           MOVE SPACES TO WSS-GLS-CCSB IN WSS-VARI.
           IF WSS-CIC-CCSB IN WSS-VARI > SPACES
             MOVE 'TAB'                    TO TAB-COD-SIST
             MOVE 'PCC'                    TO TAB-COD-TTAB IN TAB
             MOVE WSS-CIC-CCSB IN WSS-VARI TO TAB-COD-CTAB IN TAB
             PERFORM BUS-TAB
             MOVE TAB-GLS-DESC IN TAB      TO WSS-GLS-CCSB IN WSS-VARI.
      *ALD-BCI-FIN/12.01.1999

       RES-OBT-CUENTA-DCC.
              IF  WSS-IND-ERROR IN WSS-VARI = '1'
                  DISPLAY 'ERROR CUENTA CAPITAL '.
       FIN-OBT-CUENTA-DCC.
           EXIT.

       DET-SALDO-EN-PESOS SECTION.
      *--------------------------*
       INI-DET-SALDO-EN-PESOS.
           MOVE '0' TO WSS-IND-ERROR IN WSS-VARI.
           IF WSS-COD-VCOC IN WSS-VARI = WSS-COD-PESO IN WSS-VARI
              GO TO FIN-DET-SALDO-EN-PESOS.

           MOVE WSS-COD-VCOC IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE WSS-FEC-FCOL IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1)
           MOVE WSS-FEC-FCOL IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).

           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS AND
              CAM-IND-VIGE NOT = 'S'
              MOVE '1' TO WSS-IND-ERROR IN WSS-VARI
              GO TO RES-DET-SALDO-EN-PESOS.


           MULTIPLY CAM-SGV-VCAM IN CAM BY WSS-VAL-CAPI  IN WSS-VARI
                                    GIVING WSS-VAL-CAPI  IN WSS-VARI.

           MOVE WSS-VAL-CAPI IN WSS-VARI TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO WSS-VAL-CAPI IN WSS-VARI.

       RES-DET-SALDO-EN-PESOS.
              IF  WSS-IND-ERROR IN WSS-VARI = '1'
                  MOVE 'COL'        TO MSG-COD-SIST
                  MOVE 'CAMVCAMNEX' TO MSG-COD-MENS
                  PERFORM GET-MSG
                  DISPLAY MSG-GLS-DESC    CAM-KEY-CAMB IN CAM.
       FIN-DET-SALDO-EN-PESOS.
           EXIT.


       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-DVG.
           PERFORM COL-FIO-DV2.
           PERFORM COL-FIO-TVP.
           PERFORM COL-FIO-VES.

      *Archivos de salida.
           PERFORM COL-FIO-FC4.
           PERFORM COL-FIO-DC4.
       FIN-CLOSE-FILE.
           EXIT.

       MUS-ESTADISTICA SECTION.
       INI-MUS-ESTADISTICA.
           DISPLAY 'ESTADISTICA COLPGFC4        '.
           DISPLAY '============================'.
           DISPLAY 'REG. LEIDOS DEL DVG          :' WSS-NUM-LDVG.
           DISPLAY 'REG. LEIDOS EN CASTIGO (DV2) :' WSS-NUM-LDV2.
           DISPLAY 'REG. LEIDOS DE LAS T.VISA    :' WSS-NUM-LTVP.
           DISPLAY 'REG. LEIDOS DE VENCIMIENTOS  :' WSS-NUM-LVES.
      *
           DISPLAY 'REG. SELECCIONADOS DEL DVG   :' WSS-NUM-SDVG.
           DISPLAY 'REG. SELECCIONADOS DEL DV2   :' WSS-NUM-SDV2.
           DISPLAY 'REG. SELECCIONADOS DEL TVP   :' WSS-NUM-STVP.
      *
           DISPLAY 'REG. GRABADOS DC4            :' WSS-NUM-GDC4.
           DISPLAY 'REG. GRABADOS FC4            :' WSS-NUM-GFC4.
      *
       FIN-MUS-ESTADISTICA.
           EXIT.

       PRO-CUOTAS-ATRASADAS SECTION.
       INI-PRO-CUOTAS-ATRASADAS .
           MOVE ZEROES TO WSS-VAL-CUAT.
       LUP-PRO-CUOTAS-ATRASADAS.
           IF WSS-EXISTE-VES = 'N'
               GO TO FIN-PRO-CUOTAS-ATRASADAS.

      *     DISPLAY 'VEN-CIC-IOPC : ' VEN-CIC-IOPC IN VEN.
      *     DISPLAY 'WSS-CIC-IOPC : ' WSS-CIC-IOPC IN WSS-VARI.

           IF VEN-CIC-IOPC IN VEN > WSS-CIC-IOPC IN WSS-VARI
      *         DISPLAY 'MAYOR VEN > WSS '
               GO TO FIN-PRO-CUOTAS-ATRASADAS.
           IF VEN-CIC-IOPC IN VEN < WSS-CIC-IOPC IN WSS-VARI
      *         DISPLAY 'MAYOR VEN < WSS '
               GO TO NXT-PRO-CUOTAS-ATRASADAS.

      *      DISPLAY 'MAYOR VEN = WSS '.

           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FAUX
               GO TO NXT-PRO-CUOTAS-ATRASADAS.

           IF WSS-COD-VCOC = WSS-COD-PESO IN WSS-VARI
              GO TO CUO-DET-SALDO-EN-PESOS.

           MOVE WSS-COD-VCOC             TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE VEN-NUM-DVEN IN VEN      TO CAM-NUM-DCAM IN CAM(1)
           MOVE VEN-NUM-MVEN IN VEN      TO CAM-NUM-MCAM IN CAM(1)
           MOVE VEN-NUM-SVEN IN VEN      TO CAM-NUM-SCAM IN CAM(1)
           MOVE VEN-NUM-AVEN IN VEN      TO CAM-NUM-ACAM IN CAM(1)
           MOVE CAM-FEC-FCAM IN CAM(1)   TO CAM-FEC-FCAM IN CAM(2).

           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS AND
              CAM-IND-VIGE NOT = 'S'
               DISPLAY 'NEX V. CAMBIO FVEN : ' CAM-KEY-CAMB IN CAM
               GO TO NXT-PRO-CUOTAS-ATRASADAS.


           MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SFIN IN VEN
                                    GIVING VEN-VAL-SFIN IN VEN.

           MOVE VEN-VAL-SFIN IN VEN TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO VEN-VAL-SFIN IN VEN.

       CUO-DET-SALDO-EN-PESOS.

           COMPUTE WSS-VAL-CUAT ROUNDED = WSS-VAL-CUAT +
                                          VEN-VAL-SFIN IN VEN.

       NXT-PRO-CUOTAS-ATRASADAS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VES
           IF FIO-STAT-OKS
               ADD 1 TO WSS-NUM-LVES IN WSS-VARI
               MOVE VES TO VEN
               GO TO LUP-PRO-CUOTAS-ATRASADAS.


           MOVE 'N' TO WSS-EXISTE-VES.

       FIN-PRO-CUOTAS-ATRASADAS.
           EXIT.


       COPY COLBFDVG.
       COPY COLBFDV2.
       COPY COLBFTVP.
       COPY COLBFVES.
       COPY COLBFFC4.
       COPY COLBFDC4.
       COPY COLBFRDC.
       COPY COLBFDCC.

       COPY GNSBFTAB.
       COPY SGCBFDBC.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGPES.
       COPY GNSBBTAB.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBBMSG.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBTABT.
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY TABBFCAM.
       COPY GNSBGCPT.

