       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     COLPB515.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  27/02/91.
      *REMARKS.        SISTEMA DE COLOCACIONES
      *                ANALISTA CARRASCO PATRICIO.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT COLVCRE  ASSIGN TO COLVCRE
                  ORGANIZATION IS INDEXED
                  ACCESS IS SEQUENTIAL
                  RECORD KEY IS CRE-CLAVE
                  FILE STATUS IS WR-FS-COLVCRE.
           SELECT COLVCUO  ASSIGN TO COLVCUO
                  ORGANIZATION IS INDEXED
                  ACCESS IS RANDOM
                  RECORD KEY IS CUO-CLAVE
                  FILE STATUS IS WR-FS-COLVCUO.
           SELECT COLVATR ASSIGN TO SYS010-DA-3340-S-COLVATR
                  FILE STATUS IS WR-FS-COLVATR.
           SELECT COLVTAR  ASSIGN TO COLVTAR
                  FILE STATUS IS WR-FS-COLVTAR.
           SELECT COLL515  ASSIGN TO UT-S-COLL515.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  COLVCRE
           RECORD CONTAINS 377 CHARACTERS
           LABEL RECORD STANDARD.
      ******************************************************************
      *             DESCRIPCION DE REGISTRO (CREM002)
      *             ----------------------------------
      *
      *    ARCHIVO   : BCVO.MAESTRO.CREDITOS
      *              : MAESTRO DE CREDITOS
      *    FD        : CREM002
      *    PREFIJO   : 'CRE'
      *    LARGO REG.: 377
      *    ORGANIZAC.: VSAM-KSDS
      *    BLOQUEO   : NO
      *    CLAVE PRIM: CRE-CLAVE
      *    LARGO CL.1: 21
      *    CLAVE SEC.: CRE-RUT-DD
      *    LARGO CL.1: 10
      *
      ******************************************************************
      *
       01  CRE-CREDITOS-REG.

           05  CRE-CLAVE.
               10  CRE-SUCURSAL                PIC  9(03).
               10  CRE-CODIGO-OPER.
                   20  CRE-COD-OPER            PIC  9(03).
                       88 CRE-CREDITO-AL-PERSONAL VALUE
                       109 125 610 611 614 620 712 713 722 725.
                   20  CRE-SERIAL-OPER         PIC  9(05).
               10  CRE-RUT-DD.
                   20  CRE-RUT-DD-NUM          PIC  9(09).
                   20  CRE-RUT-DD-VER          PIC  X(01).

           05  CRE-RUT-DI.
               10  CRE-RUT-DI-DIG              PIC  9(09).
               10  CRE-RUT-DI-VER              PIC  X(01).
           05  CRE-RUT-EMP.
               10  CRE-RUT-EMP-DIG             PIC  9(09).
               10  CRE-RUT-EMP-VER             PIC  X(01).
           05  CRE-CTA-CTE                     PIC  X(10).

           05  CRE-TOTAL-CUOTAS                PIC  9(03).
           05  CRE-NUM-CUOT-PAGADAS            PIC  9(03).
           05  CRE-NUM-CUOT-MOROSAS            PIC  9(03).
           05  CRE-NUM-CUOT-PENDIENTES         PIC  9(03).

           05  CRE-FECH-CURSE.
               10  CRE-FECH-CURSE-AA           PIC  9(02).
               10  CRE-FECH-CURSE-MM           PIC  9(02).
               10  CRE-FECH-CURSE-DD           PIC  9(02).

           05  CRE-FECH-PAGARE.
               10  CRE-FECH-PAGARE-AA          PIC  9(02).
               10  CRE-FECH-PAGARE-MM          PIC  9(02).
               10  CRE-FECH-PAGARE-DD          PIC  9(02).
           05  CRE-FECH-PRIMER-VENC.
               10  CRE-FECH-PRIMER-VENC-AA     PIC  9(02).
               10  CRE-FECH-PRIMER-VENC-MM     PIC  9(02).
               10  CRE-FECH-PRIMER-VENC-DD     PIC  9(02).
           05  CRE-FECH-VENC-ANTERIOR.
               10  CRE-FECH-VENC-ANTERIOR-DD   PIC  9(02).
               10  CRE-FECH-VENC-ANTERIOR-MM   PIC  9(02).
               10  CRE-FECH-VENC-ANTERIOR-AA   PIC  9(02).

           05  CRE-FECH-PROX-VENC.
               10  CRE-FECH-PROX-VENC-AA       PIC  9(02).
               10  CRE-FECH-PROX-VENC-MM       PIC  9(02).
               10  CRE-FECH-PROX-VENC-DD       PIC  9(02).

           05  CRE-FECH-ULTIMO-MOV.
               10  CRE-FECH-ULTIMO-MOV-DD      PIC  9(02).
               10  CRE-FILLER-1                PIC  X(01).
               10  CRE-FECH-ULTIMO-MOV-MM      PIC  9(02).
               10  CRE-FILLER-2                PIC  X(01).
               10  CRE-FECH-ULTIMO-MOV-AA      PIC  9(02).

           05  CRE-TIPO-OPER                   PIC  X(03).
           05  CRE-TIPO-GARANTIA               PIC  X(01).
           05  CRE-TIPO-INTERES                PIC  X(01).
           05  CRE-TIPO-CLIENTE                PIC  9(03).
           05  CRE-PERIODICIDAD                PIC  9(01).
           05  CRE-REAJUSTABILIDAD             PIC  X(01).
           05  CRE-DIAS-PLAZO                  PIC  9(03).
           05  CRE-MESES-DIFERIDOS             PIC  X(03).
           05  CRE-TIEMPO-GRACIA               PIC  9(02).
           05  CRE-COD-DISTRIBUIDOR            PIC  X(02).
           05  CRE-AGENTE                      PIC  9(03).
           05  CRE-LINEA-CRED                  PIC  9(02).
           05  CRE-DESTINO                     PIC  X(02).
           05  CRE-TIPO-COBRANZA               PIC  9(01).
           05  CRE-INDIC-MONEDA                PIC  9(01).
           05  CRE-VIGENCIA                    PIC  X(01).
           05  CRE-OTRO-DD                     PIC  X(01).
           05  CRE-OTRO-DI                     PIC  X(01).

           05  CRE-MONTO-COLOC                 PIC S9(12)V9(03)  COMP-3.
           05  CRE-INTERES-COLOC               PIC S9(12)V9(03)  COMP-3.
           05  CRE-CUOTA-BASE                  PIC S9(10)V9(03)  COMP-3.
           05  CRE-TASA-VIGENTE                PIC S9(02)V9(02)  COMP-3.

           05  CRE-MONTO-PAGADO                PIC S9(12)V9(03)  COMP-3.
           05  CRE-INTERES-PAGADO              PIC S9(12)V9(03)  COMP-3.

           05  CRE-ULT-MONTO-CANC              PIC S9(10)V9(03)  COMP-3.
           05  CRE-ULT-INTERES-CANC            PIC S9(10)V9(03)  COMP-3.

           05  CRE-CUOTA-PROX-VENC             PIC S9(10)V9(03)  COMP-3.
           05  CRE-INTERES-PROX-VENC           PIC S9(10)V9(03)  COMP-3.

           05  CRE-SALDO-COLOCAC               PIC S9(12)V9(03)  COMP-3.
           05  CRE-TOT-INT-PAG-MES             PIC S9(12)V9(03)  COMP-3.

           05  CRE-REAJUSTES-PAG               PIC S9(10)V9(03)  COMP-3.
           05  CRE-VAL-IMPU                    PIC S9(11)V9(04)  COMP-3.
           05  CRE-HABER                       PIC S9(11)V9(04)  COMP-3.

           05  CRE-AJUSTE                      PIC S9(06)V9(02)  COMP-3.
           05  CRE-GASTO-NOTARIAL              PIC S9(06)V9(02)  COMP-3.
           05  CRE-OPERAC-ANTERIOR             PIC  X(08).
           05  CRE-COD-HIPOTECARIO             PIC  X(08).
           05  CRE-CLAVE-LINEA                 PIC  X(11).
           05  CRE-TASA-SPREAD-COLOC           PIC S9(02)V9(02).
           05  CRE-SALDO-CAPIT-REAJ            PIC S9(12)V9(03)  COMP-3.
           05  CRE-CUOTAS-ORIGINALES           PIC  9(03).
           05  CRE-NUM-OPERATIVAS              PIC  9(03).
           05  CRE-CONT-CAPITALIZ              PIC  9(02).
           05  CRE-CUOTAS-CURSE                PIC  9(03).
           05  CRE-PERIODICIDAD-INTERES        PIC  9(01).
           05  CRE-FECHA-CANCELACION.
               10  CRE-FECHA-CANCELACION-AA    PIC  9(02).
               10  CRE-FECHA-CANCELACION-MM    PIC  9(02).
               10  CRE-FECHA-CANCELACION-DD    PIC  9(02).
           05  CRE-FECH-PROX-VCTO-INT.
               10  CRE-FECH-PROX-VENC-INT-AA   PIC  9(02).
               10  CRE-FECH-PROX-VENC-INT-MM   PIC  9(02).
               10  CRE-FECH-PROX-VENC-INT-DD   PIC  9(02).
           05  CRE-FECH-ULT-PAGO-INT.
               10  CRE-FECH-ULT-PAGO-INT-AA    PIC  9(02).
               10  CRE-FECH-ULT-PAGO-INT-MM    PIC  9(02).
               10  CRE-FECH-ULT-PAGO-INT-DD    PIC  9(02).
           05  CRE-MTO-INTERES-CAPITALIZADO    PIC S9(08)V9(02)  COMP-3.
           05  CRE-TASA-INTERES-2              PIC S9(02)V9(02)  COMP-3.
           05  CRE-TASA-INTERES-3              PIC S9(02)V9(02)  COMP-3.
           05  CRE-TIPO-CUOTA                  PIC  9(01).
           05  CRE-IND-I8                      PIC  9(01).
           05  CRE-IND-I9                      PIC  9(01).
           05  CRE-IND-I11                     PIC  9(01).
           05  CRE-IND-I14                     PIC  9(02).
           05  CRE-IND-I16                     PIC  9(02).
           05  CRE-IND-I17                     PIC  9(01).
           05  CRE-IND-I18                     PIC  9(01).
           05  CRE-FECHA-SUSPEN.
               10  CRE-FECHA-SUSPEN-AA         PIC  9(02).
               10  CRE-FECHA-SUSPEN-MM         PIC  9(02).
               10  CRE-FECHA-SUSPEN-DD         PIC  9(02).
           05  CRE-VCTOS-EN-MES                PIC S9(12)V9(03)  COMP-3.
           05  CRE-VCTOS-PAG-MES               PIC S9(12)V9(03)  COMP-3.
           05  CRE-TASA-MAX-CONV               PIC  X(1).
           05  CRE-ESTADO-PRO                  PIC  X(1).
           05  CRE-NRO-PREPAGOS                PIC  9(03).
           05  CRE-CLASIF-RIESGO               PIC  X(02).
           05  CRE-IND-RENO                    PIC  X(01).
           05  CRE-IND-SUS-COB                 PIC  X(01).
           05  CRE-FILLER                      PIC  X(03).
           SKIP1
       FD  COLVCUO
           RECORD CONTAINS 090 CHARACTERS
           LABEL RECORD STANDARD.
      ******************************************************************
      *             DESCRIPCION DE REGISTRO (CREM009)
      *             ---------------------------------
      *
      *    ARCHIVO   : BCVO.MAESTRO.CUOTAS
      *              : MAESTRO DE CUOTAS
      *    FD        : CREM009
      *    PREFIJO   : 'CUO'
      *    LARGO REG.: 90
      *    ORGANIZAC.: VSAM-KSDS
      *    BLOQUEO   : NO
      *    CLAVE PRIM: CUO-CLAVE
      *    LARGO PRIM: 21
      *
      ******************************************************************
      *
       01  CUO-CUOTAS-REG.

           05  CUO-CLAVE.
               10  CUO-RUT-DD.
                   20  CUO-RUT-DD-DIG         PIC  9(09).
                   20  CUO-RUT-DD-VER         PIC  X(01).
               10  CUO-CODIGO-OPER.
                   20  CUO-COD-OPER           PIC  9(03).
                   20  CUO-SERIAL-OPER        PIC  9(05).
               10  CUO-NUM-ABONO              PIC  9(03).

           05  CUO-FECHA-VENC.
               10  CUO-FECHA-VENC-DD          PIC  9(02).
               10  CUO-FECHA-VENC-MM          PIC  9(02).
               10  CUO-FECHA-VENC-AA          PIC  9(02).
           05  CUO-CUOTA                      PIC S9(10)V9(03)  COMP-3.
           05  CUO-AMORTIZACION               PIC S9(10)V9(03)  COMP-3.
           05  CUO-INTERES                    PIC S9(10)V9(03)  COMP-3.
           05  CUO-SUCURSAL                   PIC  9(03).
           05  CUO-INDIC-DE-CUOTA             PIC  9(01).
           05  CUO-DIAS-ENTRE-VCTOS           PIC S9(03)        COMP-3.
           05  CUO-DIFEREN-INTERES            PIC S9(08)V9(02)  COMP-3.
           05  CUO-COMISION                   PIC S9(08)V9(02)  COMP-3.
           05  CUO-SEGURO-INCENDIO            PIC S9(08)V9(02)  COMP-3.
           05  CUO-SEGURO-DESGRAV             PIC S9(08)V9(02)  COMP-3.
           05  CUO-CODIGO-CUOTA               PIC  9(01).
           05  CUO-ESTADO-PRO                 PIC  X.
           05  CUO-FILLER                     PIC  9(10).
      *
      *
      ******************************************************************
           SKIP1
       FD  COLVATR
           RECORD CONTAINS 080 CHARACTERS
           LABEL RECORD STANDARD.
       01  COLRATR.
           05 ATR-SUCURSAL      PIC X(03).
           05 ATR-CODIGO-OPER   PIC X(08).
           05 ATR-RUT-DD        PIC X(10).
           05 ATR-NUM-ABONO     PIC X(03).
           05 ATR-FECHA-VENC    PIC X(06).
           05 ATR-AMORTIZACION  PIC S9(10)V9(03) SIGN LEADING SEPARATE.
           05 ATR-INTERES       PIC S9(10)V9(03) SIGN LEADING SEPARATE.
           05 ATR-TIP-MND       PIC X(01).
           05 ATR-FILLER        PIC X(21).
           SKIP1
       FD  COLVTAR
           RECORD CONTAINS 080 CHARACTERS
           LABEL RECORD OMITTED.
       01  COLRTAR.
           02 TAR-COD           PIC X(03).
           02 FILLER            PIC X(77).
           SKIP1
       FD  COLL515
           RECORD CONTAINS 132 CHARACTERS
           LABEL RECORD OMITTED.
       01  COLR515              PIC X(132).
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-PARAMETROS.
           05 FILLER                 PIC X(28).
           05 WP-FECHA.
              10 WP-DIA              PIC X(02).
              10 FILLER              PIC X(01).
              10 WP-MES              PIC X(02).
              10 FILLER              PIC X(01).
              10 WP-ANO              PIC X(02).
           05 FILLER                 PIC X(44).
           SKIP1
       01  W-TABLAS.
           05 WT-TABLA-COD OCCURS 300 TIMES INDEXED BY WI-I.
              10 WT-COD               PIC X(03).
           SKIP1
       01  W-SWITCHES.
           05 WS-PROCESO             PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-NO-OK    VALUE 1.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-TAR               PIC 9(06) VALUE 0.
           05 WA-R-TAR               PIC 9(06) VALUE 0.
           05 WA-G-TAR               PIC 9(06) VALUE 0.
           05 WA-L-CRE               PIC 9(06) VALUE 0.
           05 WA-R-CRE               PIC 9(06) VALUE 0.
           05 WA-G-CRE               PIC 9(06) VALUE 0.
           05 WA-L-CUO               PIC 9(06) VALUE 0.
           05 WA-R-CUO               PIC 9(06) VALUE 0.
           05 WA-G-CUO               PIC 9(06) VALUE 0.
           05 WA-L-ATR               PIC 9(06) VALUE 0.
           05 WA-R-ATR               PIC 9(06) VALUE 0.
           05 WA-G-ATR               PIC 9(06) VALUE 0.
           05 WA-PAGINA              PIC 9(04) VALUE 0.
           05 WA-LINEA               PIC 9(03) VALUE 100.
           05 WA-AMORTIZ             PIC S9(10)V9(03) COMP-3 VALUE +0.
           05 WA-INTERES             PIC S9(10)V9(03) COMP-3 VALUE +0.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA           PIC X(02).
              10 WR-HOY-MM           PIC X(02).
              10 WR-HOY-DD           PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION          PIC X(15).
              10 WR-PARRAFO          PIC X(15).
              10 WR-ARCHIVO          PIC X(10).
              10 WR-OPERACI          PIC X(10).
              10 WR-FILE-ST          PIC X(02).
              10 WR-MENSAJE          PIC X(35).
           05 WR-FS-COLVCRE          PIC 9(02) VALUE 0.
           05 WR-FS-COLVCUO          PIC 9(02) VALUE 0.
           05 WR-FS-COLVTAR          PIC 9(02) VALUE 0.
           05 WR-FS-COLVATR          PIC 9(02) VALUE 0.
           05 WR-FECHA-PARAM.
              10 WR-ANO-PARAM        PIC X(02).
              10 WR-MES-PARAM        PIC X(02).
              10 WR-DIA-PARAM        PIC X(02).
           05 WR-FECHA-VENC.
              10 WR-VENC-AA          PIC X(02).
              10 WR-VENC-MM          PIC X(02).
              10 WR-VENC-DD          PIC X(02).
           05 WR-PARAM-RUTUFECH.
              10 WR-RUTU-FECH.
      ***********15 WR-RUTU-SIG      PIC 99 VALUE  19.
                 15 WR-RUTU-SIG      PIC 99.
                 15 WR-RUTU-ANO      PIC 99.
                 15 WR-RUTU-MES      PIC 99.
                 15 WR-RUTU-DIA      PIC 99.
              10 WR-RUTU-SN          PIC X.
              10 WR-RUTU-HABANT      PIC 9(08) VALUE 0.
              10 WR-RUTU-HABPOS      PIC 9(08) VALUE 0.
              10 WR-RUTU-CODRET      PIC 9     VALUE 0.
           05 WR-LINEA01.
              10 FILLER         PIC X(020) VALUE 'BANCO DE CREDITO E I'.
              10 FILLER         PIC X(003) VALUE 'NV.'.
              10 FILLER         PIC X(090) VALUE ALL ' '.
              10 FILLER         PIC X(012) VALUE 'PAGINA :'.
              10 WR-LA01-PAG    PIC Z.ZZ9.
           05 WR-LINEA02.
              10 FILLER         PIC X(020) VALUE 'SISTEMA COLOCACIONES'.
              10 FILLER         PIC X(020) VALUE ALL ' '.
              10 FILLER         PIC X(020) VALUE 'CUOTAS ATRASADAS INF'.
              10 FILLER         PIC X(020) VALUE 'ORMADAS EN CINTA AL'.
              10 WR-LA02-PDD    PIC X(002).
              10 FILLER         PIC X(001) VALUE ALL '/'.
              10 WR-LA02-PMM    PIC X(002).
              10 FILLER         PIC X(001) VALUE ALL '/'.
              10 WR-LA02-PAA    PIC X(002).
              10 FILLER         PIC X(026) VALUE ALL ' '.
              10 FILLER         PIC X(008) VALUE 'FECHA :'.
              10 WR-LA02-DIA    PIC X(002).
              10 FILLER         PIC X(001) VALUE ALL '/'.
              10 WR-LA02-MES    PIC X(002).
              10 FILLER         PIC X(001) VALUE ALL '/'.
              10 WR-LA02-ANO    PIC X(002).
           05 WR-LINEA03.
              10 FILLER         PIC X(130) VALUE ALL '-'.
           05 WR-LINEA04.
              10 FILLER         PIC X(020) VALUE ' COD.        NUMERO'.
              10 FILLER         PIC X(020) VALUE 'DE              R.U.'.
              10 FILLER         PIC X(017) VALUE 'T.         NUMERO'.
              10 FILLER         PIC X(013) VALUE '        FECHA'.
              10 FILLER         PIC X(023) VALUE ALL ' '.
              10 FILLER         PIC X(005) VALUE 'VALOR'.
              10 FILLER         PIC X(020) VALUE '                   V'.
              10 FILLER         PIC X(004) VALUE 'ALOR'.
           05 WR-LINEA05.
              10 FILLER         PIC X(020) VALUE ' SUC.         CREDIT'.
              10 FILLER         PIC X(020) VALUE 'O            DEUDOR'.
              10 FILLER         PIC X(017) VALUE 'DIR.        CUOTA'.
              10 FILLER         PIC X(014) VALUE '       VENCTO.'.
              10 FILLER         PIC X(021) VALUE ALL ' '.
              10 FILLER         PIC X(007) VALUE 'CAPITAL'.
              10 FILLER         PIC X(020) VALUE '                 INT'.
              10 FILLER         PIC X(004) VALUE 'ERES'.
           05 WR-LINEA06.
              10 FILLER         PIC X(001) VALUE ALL ' '.
              10 WR-LA06-SUC    PIC X(003).
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA06-OPE    PIC X(003).
              10 FILLER         PIC X(001) VALUE ALL ' '.
              10 WR-LA06-SER    PIC X(008).
              10 FILLER         PIC X(009) VALUE ALL ' '.
              10 WR-LA06-RUT    PIC ZZZZZZZZ9.
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA06-DV     PIC X(001).
              10 FILLER         PIC X(009) VALUE ALL ' '.
              10 WR-LA06-CUO    PIC ZZ9.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA06-DIA    PIC X(002).
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA06-MES    PIC X(002).
              10 FILLER         PIC X(001) VALUE ALL '-'.
              10 WR-LA06-ANO    PIC X(002).
              10 FILLER         PIC X(014) VALUE ALL ' '.
              10 WR-LA06-CAP    PIC -----.---.--9,999.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA06-INT    PIC -----.---.--9,999.
           05 WR-LINEA07.
              10 FILLER         PIC X(086) VALUE ALL ' '.
              10 FILLER         PIC X(017) VALUE ALL '-'.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 FILLER         PIC X(017) VALUE ALL '-'.
           05 WR-LINEA08.
              10 FILLER         PIC X(052) VALUE ALL ' '.
              10 FILLER         PIC X(009) VALUE 'TOTAL DE'.
              10 WR-LA08-CUO    PIC ZZZ.ZZ9.
              10 FILLER         PIC X(018) VALUE ' CUOTAS'.
              10 WR-LA08-CAP    PIC -----.---.--9,999.
              10 FILLER         PIC X(008) VALUE ALL ' '.
              10 WR-LA08-INT    PIC -----.---.--9,999.
      *

           COPY RLOVR001.

       77  Y2K-CRE-FECH-PROX-VENC
                         PIC 9(6).
       77  Y2K-WR-FECHA-VENC
                         PIC 9(6).
       77  Y2K-WR-FECHA-PARAM
                         PIC 9(6).
           EJECT
       PROCEDURE DIVISION.
      *===================
           EJECT
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-PROCESO.
           STOP RUN.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-ABRIR-ARCHIVOS.
      *------------------
           OPEN INPUT COLVCRE COLVCUO COLVTAR
               OUTPUT COLVATR COLL515.
           IF WR-FS-COLVCRE NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'     TO WR-SECCION
              MOVE 'BA-ABRIR-ARCHIVOS' TO WR-PARRAFO
              MOVE 'COLVCRE    '       TO WR-ARCHIVO
              MOVE 'OPEN       '       TO WR-OPERACI
              MOVE WR-FS-COLVCRE       TO WR-FILE-ST
              MOVE 1                   TO WS-PROCESO
              GO BZ-FIN-DE-SECCION.
           IF WR-FS-COLVCUO NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'     TO WR-SECCION
              MOVE 'BA-ABRIR-ARCHIVOS' TO WR-PARRAFO
              MOVE 'COLVCUO    '       TO WR-ARCHIVO
              MOVE 'OPEN       '       TO WR-OPERACI
              MOVE WR-FS-COLVCUO       TO WR-FILE-ST
              MOVE 1                   TO WS-PROCESO
              GO BZ-FIN-DE-SECCION.
           IF WR-FS-COLVTAR NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'     TO WR-SECCION
              MOVE 'BA-ABRIR-ARCHIVOS' TO WR-PARRAFO
              MOVE 'COLVTAR    '       TO WR-ARCHIVO
              MOVE 'OPEN       '       TO WR-OPERACI
              MOVE WR-FS-COLVTAR       TO WR-FILE-ST
              MOVE 1                   TO WS-PROCESO
              GO BZ-FIN-DE-SECCION.
           IF WR-FS-COLVATR NOT = 0 AND NOT = 97
              MOVE 'B-INICIALIZAR'     TO WR-SECCION
              MOVE 'BA-ABRIR-ARCHIVOS' TO WR-PARRAFO
              MOVE 'COLVATR    '       TO WR-ARCHIVO
              MOVE 'OPEN       '       TO WR-OPERACI
              MOVE WR-FS-COLVATR       TO WR-FILE-ST
              MOVE 1                   TO WS-PROCESO
              GO BZ-FIN-DE-SECCION.
           SKIP1
       BB-VALIDA-PARAMETRO.
      *--------------------
           ACCEPT W-PARAMETROS.
           IF (WP-DIA NOT NUMERIC OR WP-MES NOT NUMERIC OR
               WP-ANO NOT NUMERIC)
               MOVE 1 TO WS-PROCESO
               DISPLAY '*** FECHA NO NUMERICA *** : ' WP-FECHA
           ELSE
              MOVE WP-ANO TO WR-RUTU-ANO  WR-ANO-PARAM WR-LA02-PAA
              IF WR-RUTU-ANO < ROLL-YY
                 MOVE 20 TO WR-RUTU-SIG
              ELSE
                 MOVE 19 TO WR-RUTU-SIG
              END-IF
              MOVE WP-MES TO WR-RUTU-MES  WR-MES-PARAM WR-LA02-PMM
              MOVE WP-DIA TO WR-RUTU-DIA  WR-DIA-PARAM WR-LA02-PDD
              MOVE ZEROS TO WR-RUTU-HABANT WR-RUTU-HABPOS WR-RUTU-CODRET
              MOVE ' '    TO WR-RUTU-SN
              CALL 'RTNUFECH' USING  WR-RUTU-FECH   WR-RUTU-SN
                                     WR-RUTU-HABANT WR-RUTU-HABPOS
                                     WR-RUTU-CODRET
              IF WR-RUTU-CODRET NOT = 0
                 MOVE 1 TO WS-PROCESO.
           IF (WS-PROCESO = 1)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BB-VALIDA-PARAMET' TO WR-PARRAFO
               MOVE 'SYSIN      '       TO WR-ARCHIVO
               MOVE '**FECHA INVALIDA*' TO WR-MENSAJE
               MOVE WR-RUTU-CODRET      TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO BZ-FIN-DE-SECCION.
           ACCEPT WR-FECHA-HOY FROM DATE.
           MOVE WR-HOY-AA TO WR-LA02-ANO
           MOVE WR-HOY-MM TO WR-LA02-MES
           MOVE WR-HOY-DD TO WR-LA02-DIA.
           SKIP1
       BC-CARGA-CODIGOS.
      *-----------------
           SET WI-I TO 1.
           PERFORM BD-LEER-COLVTAR.
           PERFORM BE-LLEVA-A-TABLA
              UNTIL (WR-FS-COLVTAR NOT = 0 OR WI-I = 300)
           IF (WI-I = 300)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BC-CARGA-CODIGOS ' TO WR-PARRAFO
               MOVE 'COLVTAR    '       TO WR-ARCHIVO
               MOVE '**SE LLENO TABLA*' TO WR-MENSAJE
               MOVE 1                   TO WS-PROCESO
               GO BZ-FIN-DE-SECCION.
           GO BZ-FIN-DE-SECCION.
           SKIP1
       BD-LEER-COLVTAR.
      *----------------
           READ COLVTAR.
           IF (WR-FS-COLVTAR NOT = 0 AND NOT = 10)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BD-LEER-COLVTAR'   TO WR-PARRAFO
               MOVE 'COLVTAR    '       TO WR-ARCHIVO
               MOVE 'READ       '       TO WR-OPERACI
               MOVE WR-FS-COLVTAR       TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO BZ-FIN-DE-SECCION.
           SKIP1
       BE-LLEVA-A-TABLA.
      *------------------
           ADD 1    TO WA-L-TAR
           MOVE TAR-COD TO WT-COD(WI-I)
           PERFORM BD-LEER-COLVTAR.
           SET WI-I UP BY 1.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GRAL.
      *----------------
           PERFORM CB-LEER-CREDITO
           PERFORM CC-SELECCIONA-REGS
              UNTIL (WR-FS-COLVCRE NOT =  0   OR
                     WS-PROCESO-NO-OK)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-CREDITO.
      *----------------
           READ COLVCRE NEXT
           IF WR-FS-COLVCRE NOT = 0 AND NOT = 10
              MOVE 'C-PROCESAR'        TO WR-SECCION
              MOVE 'CB-LEER-CREDITO'   TO WR-PARRAFO
              MOVE 'COLVCRE    '       TO WR-ARCHIVO
              MOVE 'READ NEXT  '       TO WR-OPERACI
              MOVE WR-FS-COLVCRE       TO WR-FILE-ST
              MOVE 1                   TO WS-PROCESO
              GO CZ-FIN-DE-SECCION.
           SKIP1
       CC-SELECCIONA-REGS.
      *-------------------
           ADD 1 TO WA-L-CRE
      *****IF (CRE-FECH-PROX-VENC > WR-FECHA-PARAM)
           MOVE CRE-FECH-PROX-VENC TO Y2K-CRE-FECH-PROX-VENC
           ADD ROLLDIFF-1-YYMMDD TO Y2K-CRE-FECH-PROX-VENC
           MOVE WR-FECHA-PARAM TO Y2K-WR-FECHA-PARAM
           ADD ROLLDIFF-1-YYMMDD TO Y2K-WR-FECHA-PARAM
           IF (Y2K-CRE-FECH-PROX-VENC > Y2K-WR-FECHA-PARAM)
               ADD 1 TO WA-R-CRE
           ELSE
               SET WI-I TO 1
               SEARCH WT-TABLA-COD
                AT END ADD 1 TO WA-R-CRE
                WHEN WT-COD(WI-I) = CRE-COD-OPER
                 PERFORM CD-CARGA-CUOTAS.
           PERFORM CB-LEER-CREDITO.
           SKIP1
       CD-CARGA-CUOTAS.
      *----------------
           MOVE CRE-RUT-DD       TO CUO-RUT-DD
           MOVE CRE-CODIGO-OPER  TO CUO-CODIGO-OPER
           ADD  1 CRE-NUM-CUOT-PAGADAS GIVING CUO-NUM-ABONO
           MOVE 0 TO WR-FS-COLVCUO
           PERFORM CE-GRABAR-CUOTAS
             UNTIL (CUO-NUM-ABONO > CRE-TOTAL-CUOTAS OR
                    WR-FS-COLVCUO NOT = 0).
           SKIP1
       CE-GRABAR-CUOTAS.
      *-----------------
           PERFORM CG-LEER-CUOTA.
           IF (WR-FS-COLVCUO = 0)
              MOVE CUO-FECHA-VENC-DD  TO WR-VENC-DD
              MOVE CUO-FECHA-VENC-MM  TO WR-VENC-MM
              MOVE CUO-FECHA-VENC-AA  TO WR-VENC-AA
      ********IF (WR-FECHA-VENC > WR-FECHA-PARAM)
              MOVE WR-FECHA-VENC TO Y2K-WR-FECHA-VENC
              ADD ROLLDIFF-1-YYMMDD TO Y2K-WR-FECHA-VENC
              MOVE WR-FECHA-PARAM TO Y2K-WR-FECHA-PARAM
              ADD ROLLDIFF-1-YYMMDD TO Y2K-WR-FECHA-PARAM
              IF (Y2K-WR-FECHA-VENC > Y2K-WR-FECHA-PARAM)
                  MOVE 10 TO WR-FS-COLVCUO
              ELSE
                  ADD 1 TO WA-L-CUO
                  MOVE CRE-SUCURSAL      TO ATR-SUCURSAL
                  MOVE CRE-RUT-DD        TO ATR-RUT-DD
                  MOVE CRE-CODIGO-OPER   TO ATR-CODIGO-OPER
                  MOVE CUO-NUM-ABONO     TO ATR-NUM-ABONO
                  MOVE CUO-FECHA-VENC    TO ATR-FECHA-VENC
                  MOVE CUO-AMORTIZACION  TO ATR-AMORTIZACION
                  MOVE CUO-INTERES       TO ATR-INTERES
                  MOVE CRE-IND-I9        TO ATR-TIP-MND
                  MOVE SPACES            TO ATR-FILLER
                  PERFORM CH-GRABAR-COLVATR
                  PERFORM CI-LISTA-REGISTRO.
            ADD 1 TO CUO-NUM-ABONO.
           SKIP1
       CG-LEER-CUOTA.
      *--------------
           READ COLVCUO
           IF WR-FS-COLVCUO = 23
              DISPLAY '** NO SE ENCONTRO CUOTA : ' CUO-CLAVE
           ELSE
           IF WR-FS-COLVCUO NOT = 0
              MOVE 'C-PROCESAR'        TO WR-SECCION
              MOVE 'CG-LEER-CUOTA'     TO WR-PARRAFO
              MOVE 'COLVCUO    '       TO WR-ARCHIVO
              MOVE 'READ       '       TO WR-OPERACI
              MOVE WR-FS-COLVCUO       TO WR-FILE-ST
              MOVE 1                   TO WS-PROCESO
              GO CZ-FIN-DE-SECCION.
           SKIP1
       CH-GRABAR-COLVATR.
      *------------------
           WRITE COLRATR
           IF (WR-FS-COLVATR NOT = 0)
               MOVE 'C-PROCESAR'        TO WR-SECCION
               MOVE 'CH-GRABAR-COLVATR' TO WR-PARRAFO
               MOVE 'COLVATR    '       TO WR-ARCHIVO
               MOVE 'WRITE      '       TO WR-OPERACI
               MOVE WR-FS-COLVATR       TO WR-FILE-ST
               MOVE 1                   TO WS-PROCESO
               GO CZ-FIN-DE-SECCION
           ELSE
               ADD 1 TO WA-G-ATR.
           SKIP1
       CI-LISTA-REGISTRO.
      *------------------
           MOVE  CRE-SUCURSAL      TO WR-LA06-SUC
           MOVE  CRE-COD-OPER      TO WR-LA06-OPE
           MOVE  CRE-SERIAL-OPER   TO WR-LA06-SER
           MOVE  CRE-RUT-DD-NUM    TO WR-LA06-RUT
           MOVE  CRE-RUT-DD-VER    TO WR-LA06-DV
           MOVE  CUO-NUM-ABONO     TO WR-LA06-CUO
           MOVE  WR-VENC-DD        TO WR-LA06-DIA
           MOVE  WR-VENC-MM        TO WR-LA06-MES
           MOVE  WR-VENC-AA        TO WR-LA06-ANO
           MOVE  CUO-AMORTIZACION  TO WR-LA06-CAP
           MOVE  CUO-INTERES       TO WR-LA06-INT
           ADD   CUO-AMORTIZACION  TO WA-AMORTIZ
           ADD   CUO-INTERES       TO WA-INTERES
           IF (WA-LINEA > 50)
               PERFORM CJ-TITULOS.
           WRITE COLR515 FROM WR-LINEA06.
           ADD 1 TO WA-LINEA.
           SKIP1
       CJ-TITULOS.
      *-----------
           ADD 1 TO WA-PAGINA
           MOVE WA-PAGINA TO WR-LA01-PAG
           WRITE COLR515 FROM WR-LINEA01 AFTER PAGE
           WRITE COLR515 FROM WR-LINEA02 AFTER 1
           WRITE COLR515 FROM WR-LINEA03 AFTER 2
           WRITE COLR515 FROM WR-LINEA04 AFTER 1
           WRITE COLR515 FROM WR-LINEA05 AFTER 1
           WRITE COLR515 FROM WR-LINEA03 AFTER 1
           MOVE SPACES TO COLR515.
           WRITE COLR515                 AFTER 1
           MOVE 0 TO WA-LINEA.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-TOT-GENERALES.
      *-----------------
           IF WS-PROCESO-OK
              MOVE WA-G-ATR   TO WR-LA08-CUO
              MOVE WA-AMORTIZ TO WR-LA08-CAP
              MOVE WA-INTERES TO WR-LA08-INT
              IF (WA-LINEA > 47)
                  PERFORM CJ-TITULOS
                  WRITE COLR515 FROM WR-LINEA07 AFTER 3
                  WRITE COLR515 FROM WR-LINEA08 AFTER 1
              ELSE
                  WRITE COLR515 FROM WR-LINEA07 AFTER 3
                  WRITE COLR515 FROM WR-LINEA08 AFTER 1.
           SKIP1
       DB-ESTADISTICAS.
      *----------------
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           IF WS-PROCESO-NO-OK
              DISPLAY 'COLL515, NO EMITIDO O INCOMPLETO'
              DISPLAY ' '
              DISPLAY ' '
           ELSE
              DISPLAY 'COLL515, SE EMITIO PARA LAS SIGUIENTES OFICINAS'
              DISPLAY ' '
              DISPLAY '090 L'.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA COLPB515 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '    GENERA CINTA CON CUOTAS ATRASADAS'
           DISPLAY ' '
           DISPLAY 'ARCHIVO  LEIDOS  RECHAZADOS   GRABADOS'
           DISPLAY ' '
           DISPLAY 'COLVTAR: ' WA-L-TAR '     ' WA-R-TAR '    ' WA-G-TAR
           DISPLAY 'COLVCRE: ' WA-L-CRE '     ' WA-R-CRE '    ' WA-G-CRE
           DISPLAY 'COLVCUO: ' WA-L-CUO '     ' WA-R-CUO '    ' WA-G-CUO
           DISPLAY 'COLVATR: ' WA-L-ATR '     ' WA-R-ATR '    ' WA-G-ATR
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY W-PARAMETROS
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DC-TERMINO.
      *-----------
           CLOSE COLVCRE COLVCUO COLVTAR COLVATR COLL515.
           IF WS-PROCESO-NO-OK
              MOVE 100 TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY ' '
              DISPLAY 'EL ERROR FUE EN LA SECCION          ' WR-SECCION
              DISPLAY '                   PARRAFO          ' WR-PARRAFO
              DISPLAY '                   ARCHIVO          ' WR-ARCHIVO
              DISPLAY '                   OPERACION        ' WR-OPERACI
              DISPLAY '                   FILE STATUS      ' WR-FILE-ST
              DISPLAY ' '
              DISPLAY 'CLAVES EN PROCESO : '
              DISPLAY '                   MAESTRO CREDITOS ' CRE-CLAVE
              DISPLAY '                   MAESTRO CUOTAS   ' CUO-CLAVE
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.

