       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLP1D08.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    27-JAN-2000 15:56:36.

      * Programa Auxiliar Generador Interfaz D08 Super-Bancos de COL.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCC-REQA==.
      *COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RVO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.

       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY TABBRCAM.
       COPY TABBRTIO.
       COPY SGCBRDBC.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRRDC.
       COPY COLBRDCC.
      *COPY COLBRRVO.
       COPY COLBRROV.
       COPY COLBRD00.
       COPY SUPBRD01.
       COPY SUPBRDE8.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       COPY GNSWGPES.
       COPY COLBRTOC.
       COPY COLWGCTO.

       01  WSS-VARI.
           03 WSS-IND-FINMES         VALUE 'N'         PIC X(01).
           03 WSS-IND-ORIGINA        VALUE 'N'         PIC X(01).
           03 WSS-FEC-FINF.
              05 WSS-NUM-DINF                          PIC 9(02).
              05 WSS-NUM-MINF                          PIC 9(02).
              05 WSS-NUM-SINF                          PIC 9(02).
              05 WSS-NUM-AINF                          PIC 9(02).
           03 WSS-FEC-FIN1.
              05 WSS-NUM-SIN1                          PIC 9(02).
              05 WSS-NUM-AIN1                          PIC 9(02).
              05 WSS-NUM-MIN1                          PIC 9(02).
              05 WSS-NUM-DIN1                          PIC 9(02).
           03 WSS-COD-COOC                             PIC X(06).
           03 WSS-COD-PESO                             PIC X(06).
           03 WSS-COD-UEFE                             PIC X(06).
           03 WSS-SGV-UEFE                             PIC S9(11)V9999.
           03 WSS-IND-STAT         VALUE 0             PIC 9(01).
           03 WSS-IDD-GAUX                             PIC X(01).
           03 WSS-NUM-ICLI.
              05 WSS-IND-FILL                          PIC X(01).
              05 WSS-COD-RUTC                          PIC 9(08).
           03 WSS-GLS-NOMC.
              05 WSS-GLS-APAT                          PIC X(25).
              05 FILLER            VALUE '/'           PIC X(01).
              05 WSS-GLS-AMAT                          PIC X(25).
              05 FILLER            VALUE '/'           PIC X(01).
              05 WSS-GLS-NOMB                          PIC X(25).
           03 WSS-CIC-IOPC.
              05 WSS-CAI-IOPC                          PIC X(04).
              05 WSS-IIC-IOPC                          PIC X(08).
           03 WSS-COD-ID00.
              05 WSS-COD-OFOC                          PIC X(03).
              05 WSS-NUM-CRED.
                 07 WSS-CIC-TCCT                       PIC X(03).
                 07 WSS-NUM-IOPC                       PIC X(05).
              05 WSS-COD-IRUT.
                 07 WSS-IND-IRUT   VALUE '0'           PIC X(01).
                 07 WSS-NUM-IRUT                       PIC X(08).
              05 WSS-VRF-IRUT                          PIC X(01).
      *
           03 WSS-CIC-IROV                             PIC X(12).
           03 WSS-COD-D00O                             PIC X(21).
           03 WSS-COD-D00V                             PIC X(21).
           03 WSS-NUM-CCAT                             PIC 9(03).
           03 WSS-NUM-CMAA                             PIC 9(03).
           03 WSS-VAL-SDCA                             PIC 9(14).
           03 WSS-VAL-MCAT                             PIC 9(14).
           03 WSS-FEC-FCOL.
              05 WSS-NUM-SCOL                          PIC 9(02).
              05 WSS-NUM-ACOL                          PIC 9(02).
              05 WSS-NUM-MCOL                          PIC 9(02).
              05 WSS-NUM-DCOL                          PIC 9(02).
           03 WSS-FEC-FMAV.
              05 WSS-NUM-SMAV                          PIC 9(02).
              05 WSS-NUM-AMAV                          PIC 9(02).
              05 WSS-NUM-MMAV                          PIC 9(02).
              05 WSS-NUM-DMAV                          PIC 9(02).
           03 WSS-SGV-FINF                             PIC S9(11)V9999.
           03 WSS-IND-MNAC         VALUE ZEROES        PIC 9(01).
           03 WSS-VAL-MNAC         VALUE ZEROES        PIC 9(15).
           03 WSS-IND                                  PIC 9(02).
           03 WSS-NUM-LROV         VALUE 0             PIC 9(07).
           03 WSS-NUM-D00L         VALUE 0             PIC 9(07).
           03 WSS-NUM-D00S         VALUE 0             PIC 9(07).
           03 WSS-NUM-D00P         VALUE 0             PIC 9(07).
           03 WSS-NUM-D00R         VALUE 0             PIC 9(07).
           03 WSS-NUM-RVOL         VALUE 0             PIC 9(07).
           03 WSS-NUM-RVOS         VALUE 0             PIC 9(07).
           03 WSS-NUM-RVOP         VALUE 0             PIC 9(07).
           03 WSS-NUM-RVOR         VALUE 0             PIC 9(07).
           03 WSS-NUM-DE8G         VALUE 0             PIC 9(07).
           03 WSS-NUM-DE8M         VALUE 0             PIC 9(07).
           03 WSS-DE8                                  PIC X(200).
           03 WSS-COD-IRUT-O.
              05 WSS-IND-IRUT-O    VALUE '0'           PIC X(01).
              05 WSS-NUM-IRUT-O                        PIC X(08).
           03 WSS-VRF-IRUT-O                           PIC X(01).
           03 WSS-COD-IRUT-V.
              05 WSS-IND-IRUT-V    VALUE '0'           PIC X(01).
              05 WSS-NUM-IRUT-V                        PIC X(08).
           03 WSS-VRF-IRUT-V                           PIC X(01).
           03 WSS-NUM-DIVP         VALUE ZEROES        PIC 9(02).
           03 WSS-COD-NIOO         VALUE SPACES        PIC X(30).
           03 WSS-FEC-FAUX.
              05 WSS-NUM-SAUX                          PIC 9(02).
              05 WSS-NUM-AAUX                          PIC 9(02).
              05 WSS-NUM-MAUX                          PIC 9(02).
              05 WSS-NUM-DAUX                          PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                          PIC 9(02).
              05 WSS-NUM-APRO                          PIC 9(02).
              05 WSS-NUM-MPRO                          PIC 9(02).
              05 WSS-NUM-DPRO                          PIC 9(02).
           03 WSS-IND-SU90         VALUE SPACES        PIC X(01).
           03 WSS-IND-OK           VALUE SPACES        PIC X(01).
           03 WSS-IND-TCR          VALUE SPACES        PIC X(01).
           03 WSS-GLS-INGR.
              05 FILLER                                PIC X(25).
              05 WSS-COD-CCAS                          PIC X(09).
           03 WSS-ARR-CCTB OCCURS 40 TIMES.
              05 WSS-COD-CCTB                          PIC X(09).
           03 WSS-Y                VALUE ZEROES        PIC 9(02).
           03 WSS-NUM-DIFM         VALUE ZEROES        PIC 9(04).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLP1D08' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           PERFORM LEE-PARAM.
           PERFORM ABRE-ARCH.

      ******************************************************************
       INI-MAIN-D00.

      *SE VALIDA QUE FECHA DE PROCESO SEA O NO, FIN DE MES
           PERFORM VAL-FINF-FINMES.

      * Primera Lectura de Creditos en D00
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-D00.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR : NO HAY REGISTROS D00 '
               GO TO FIN-MAIN.

       LUP-MAIN-D00.
      * Proceso de Seleccion y Clasificacion a Partir del Registro D00
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN-D00.
           ADD  1  TO WSS-NUM-D00L IN WSS-VARI.

      * Seleccion del Registro D00 Leido para ser Procesado
           PERFORM SEL-REG-PRO-D00.
           IF WSS-IND-STAT IN WSS-VARI = 1
               GO TO NXT-MAIN-D00.
           ADD  1  TO WSS-NUM-D00S IN WSS-VARI.

      * Proceso de Carga del Registro DE8 con el D00 Seleccionado
           PERFORM FIL-REG-DE8-D00.
           IF WSS-IND-STAT IN WSS-VARI = 1
               ADD  1  TO WSS-NUM-D00R IN WSS-VARI
               GO TO NXT-MAIN-D00.
           ADD  1  TO WSS-NUM-D00P IN WSS-VARI.

      * Graba el Registro DE8 con informacion del D00 Seleccionado
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM SUP-FIO-DE8.
           IF NOT FIO-STAT-OKS
               DISPLAY ' ERROR AL GRABAR DE8, FIO-STAT : ' FIO-STAT
               DISPLAY ' LLAVE : ' DE8-COD-IDE8
               PERFORM GNS-PRO-ABT
           ELSE
               ADD  1  TO WSS-NUM-DE8G.
       NXT-MAIN-D00.
      * Siguiente lectura de Creditos por Llave Primaria en D00
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-D00.
           GO TO LUP-MAIN-D00.
       FIN-MAIN-D00.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SUP-FIO-DE8.

       FIN-MAIN.
           PERFORM CIERRA-ARCH.
           PERFORM MUESTRA-ESTAD.

      * Termino del Proceso
           PERFORM GNS-PRO-END.

      *<<<< EOF
      ******************************************************************
       LEE-PARAM SECTION.
       INI-LEE-PARAM.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      * Captura Fecha de Proceso
           DISPLAY 'INGRESE FECHA DEL INFORME (DDMMSSAA) '.
           ACCEPT WSS-FEC-FINF IN WSS-VARI.
           DISPLAY WSS-FEC-FINF IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Informe
           DISPLAY 'FECHA DEL INFORME : ' WSS-FEC-FINF IN WSS-VARI.
           MOVE WSS-FEC-FINF    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DINF IN WSS-VARI TO WSS-NUM-DIN1 IN WSS-VARI.
           MOVE WSS-NUM-MINF IN WSS-VARI TO WSS-NUM-MIN1 IN WSS-VARI.
           MOVE WSS-NUM-SINF IN WSS-VARI TO WSS-NUM-SIN1 IN WSS-VARI.
           MOVE WSS-NUM-AINF IN WSS-VARI TO WSS-NUM-AIN1 IN WSS-VARI.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

           MOVE ZEROES TO WSS-Y.
       LUP-LEE-CCTB-CAST.
           ADD 1 TO WSS-Y.
           IF WSS-Y > 40
              DISPLAY '***********************************************'
              DISPLAY 'HAY MAS CTAS. CONTABLES DE CASTIGO POR LEER.'
              DISPLAY '  '
              GO TO FIN-LUP-LEE-CCTB-CAST.
           ACCEPT WSS-GLS-INGR IN WSS-VARI.
           MOVE WSS-COD-CCAS IN WSS-VARI TO
                                    WSS-COD-CCTB IN WSS-ARR-CCTB(WSS-Y).
           IF WSS-COD-CCTB IN WSS-ARR-CCTB(WSS-Y) NOT = '999999999'
              GO TO LUP-LEE-CCTB-CAST.
       FIN-LUP-LEE-CCTB-CAST.

      * Obtiene el Codigo Valor de Cambio de Pesos mediante el CIC de la
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB IN TAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

      * Obtiene el Codigo Valor de Cambio de la UF mediante el CIC de la
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0998  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0998  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB IN TAB
               PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-UEFE IN WSS-VARI.

      * Obtiene Valor de Cambio en Pesos para la UF a Fecha del Informe
           MOVE WSS-FEC-FIN1 IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FIN1 IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE WSS-COD-UEFE IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A FECHA INFORME : '
                        WSS-FEC-FINF IN WSS-VARI      ' PARA LA U.F.'
               PERFORM GNS-PRO-ABT
           ELSE
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY 'IND. VIGENCIA NO VALIDO A FECHA INFORME : '
                        WSS-FEC-FINF IN WSS-VARI    ' PARA LA U.F.'
               PERFORM GNS-PRO-ABT
           ELSE
           MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-UEFE IN WSS-VARI.

      * Proceso para Cargar las tablas  TAB/TIO  y  COL/TOC  en Memoria
           PERFORM COL-CRG-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
               PERFORM GNS-PRO-ABT.
       FIN-LEE-PARAM.
           EXIT.
      ******************************************************************

       ABRE-ARCH SECTION.
       INI-ABRE-ARCH.
      * Apertura de Registros
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *    PERFORM COL-FIO-RVO.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-RDC.
           PERFORM COL-FIO-DCC.
           PERFORM SGC-FIO-DBC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-D00.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR : INPUT  D00 '
               PERFORM GNS-PRO-ABT.
      *
           PERFORM SUP-FIO-D01.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR : INPUT  D01 '
               PERFORM GNS-PRO-ABT.
      *
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM SUP-FIO-DE8.
       FIN-ABRE-ARCH.
           EXIT.

      ******************************************************************

       CIERRA-ARCH SECTION.
       INI-CIERRA-ARCH.
      * Cierre de Registros
           MOVE FIO-CLO TO FIO-CMND.
      *    PERFORM COL-FIO-RVO.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-RDC.
           PERFORM COL-FIO-DCC.
           PERFORM SGC-FIO-DBC.
           PERFORM COL-FIO-D00.
           PERFORM SUP-FIO-D01.
           PERFORM SUP-FIO-DE8.

       FIN-CIERRA-ARCH.
           EXIT.

      ******************************************************************
      * Seleccion del Registro D00 Leido para ser Procesado
       SEL-REG-PRO-D00 SECTION.
       INI-SEL-REG-PRO-D00.
           MOVE  1  TO WSS-IND-STAT IN WSS-VARI.

           MOVE ZEROES TO WSS-Y.

       INI-FILTRA-CCTB.
           ADD 1 TO WSS-Y.
           IF WSS-Y > 40 OR
                      WSS-COD-CCTB IN WSS-ARR-CCTB(WSS-Y) = '999999999'
              GO TO FIN-FILTRA-CCTB.
           IF D00-COD-CCTB IN D00 = WSS-COD-CCTB IN WSS-ARR-CCTB(WSS-Y)
              GO TO FIN-SEL-REG-PRO-D00
           ELSE
              GO TO INI-FILTRA-CCTB.
       FIN-FILTRA-CCTB.
           IF NOT ((D00-COD-SIST IN D00 = '01' OR = '05') AND
                    D00-IND-TCRE IN D00 = '04')
               GO TO FIN-SEL-REG-PRO-D00.

           MOVE  0  TO WSS-IND-STAT IN WSS-VARI.
       FIN-SEL-REG-PRO-D00.
           EXIT.
      ******************************************************************
      * Proceso de Carga del Registro DE8 con el D00 Seleccionado
       FIL-REG-DE8-D00 SECTION.
       INI-FIL-REG-DE8-D00.
           MOVE  1  TO WSS-IND IN WSS-VARI.
           MOVE  0  TO WSS-IND-STAT IN WSS-VARI.

       DEU-FIL-REG-DE8-D00.

           IF D00-IND-TDEU IN D00(WSS-IND) = 'D'
               MOVE D00-VRF-RUTD IN D00(WSS-IND) TO DE8-VRF-RUTD IN DE8
               MOVE D00-NUM-RUTD IN D00(WSS-IND) TO DE8-NUM-RUTD IN DE8
               MOVE D00-NUM-RUTD IN D00(WSS-IND) TO D01-NUM-RUTD IN D01
               MOVE D00-VRF-RUTD IN D00(WSS-IND) TO D01-VRF-RUTD IN D01
               MOVE '2'                          TO D01-IND-TREG IN D01
               MOVE FIO-GET-NLS TO FIO-CMND
               PERFORM SUP-FIO-D01
      * Transforma Nombre a Formato Super
               IF FIO-STAT-OKS AND
                 D00-NUM-RUTD IN D00(WSS-IND) = D01-NUM-RUTD IN D01
                   MOVE D01-GLS-NOMD IN D01 TO DE8-GLS-NOMD IN DE8
                   GO TO CRD-FIL-REG-DE8-D00
               ELSE
                   DISPLAY 'DEUDOR DIRECTO DEL D01 CON ERROR : '
                            D01-COD-ID01 IN D01
                   MOVE  1  TO WSS-IND-STAT IN WSS-VARI
                   GO TO FIN-FIL-REG-DE8-D00.
           ADD 1 TO WSS-IND.
           IF WSS-IND < 10
               GO TO DEU-FIL-REG-DE8-D00.
           DISPLAY 'EN VECTOR DEUDORES DEL D00 NO HAY DIRECTOS; ERROR '
                   'KEY : ' D00-COD-ID00 IN D00.
           MOVE  1  TO WSS-IND-STAT IN WSS-VARI.
           GO TO FIN-FIL-REG-DE8-D00.
       CRD-FIL-REG-DE8-D00.
           MOVE SPACES TO WSS-IND-SU90.
           MOVE D00-COD-NIID IN D00  TO DE8-COD-NIIO IN DE8.
           IF D00-COD-SIST IN D00 = '05'
               MOVE '11' TO DE8-IND-TCRE IN DE8
           ELSE
               PERFORM BUSCA-EN-DCC-TCR
               IF WSS-IND-TCR IN WSS-VARI = 'S'
                  MOVE 'S' TO WSS-IND-SU90
                  MOVE '11' TO DE8-IND-TCRE IN DE8
               ELSE
                  MOVE '12' TO DE8-IND-TCRE IN DE8.

           IF DE8-IND-TCRE IN DE8 = '11'
               MOVE ZEROES TO DE8-VAL-MCRE IN DE8
               MOVE ZEROES TO DE8-DMA-FCRE IN DE8
               MOVE ZEROES TO DE8-NUM-PCRE IN DE8
               GO TO CONT-CALC.
           MOVE D00-VAL-MOOP IN D00  TO DE8-VAL-MCRE IN DE8.
           MOVE D00-AMD-FOTO IN D00  TO DE8-DMA-FCRE IN DE8.

      * Obtiene Plazo del Credito en Meses
           MOVE D00-NUM-DOTO IN D00  TO WSS-NUM-DCOL IN WSS-VARI.
           MOVE D00-NUM-MOTO IN D00  TO WSS-NUM-MCOL IN WSS-VARI.
           IF D00-NUM-AOTO IN D00 < 60
               MOVE  20  TO WSS-NUM-SCOL IN WSS-VARI
           ELSE
               MOVE  19  TO WSS-NUM-SCOL IN WSS-VARI.
           MOVE D00-NUM-AOTO IN D00  TO WSS-NUM-ACOL IN WSS-VARI.
           MOVE D00-NUM-DEXT IN D00  TO WSS-NUM-DMAV IN WSS-VARI.
           MOVE D00-NUM-MEXT IN D00  TO WSS-NUM-MMAV IN WSS-VARI.
           IF D00-NUM-AEXT IN D00 < 60
               MOVE  20  TO WSS-NUM-SMAV IN WSS-VARI
           ELSE
               MOVE  19  TO WSS-NUM-SMAV IN WSS-VARI.
           MOVE D00-NUM-AEXT IN D00  TO WSS-NUM-AMAV IN WSS-VARI.
           PERFORM DIF-MES-FCOL-FMAV.
           IF WSS-IND-STAT IN WSS-VARI = 1
               DISPLAY 'ERROR AL CALCULAR PLAZO DEL CREDITO D00 : '
                       'KEY : ' D00-COD-ID00 IN D00
               DISPLAY 'STATUS FEC : ' FEC-STAT ' / ' FEC-MENS
                       ' FEC1 : ' FEC-FEC1 ' FEC2 : ' FEC-FEC2
               GO TO FIN-FIL-REG-DE8-D00.
           MOVE FEC-NMES IN FEC-VARI TO DE8-NUM-PCRE IN DE8.
           IF DE8-NUM-PCRE IN DE8 = ZEROES
               MOVE 1 TO DE8-NUM-PCRE IN DE8.

           IF D00-NUM-VCIM IN D00 = ZEROES AND D00-IND-ESTC IN D00 = 3
              MOVE 1 TO D00-NUM-VCIM IN D00.
           MOVE D00-NUM-VCIM IN D00  TO DE8-NUM-CCAT IN DE8.


      * Obtiene Morosidad del Credito en Meses

           IF D00-NUM-VCIM IN D00 NOT > ZEROES
               MOVE ZEROES TO DE8-NUM-CMAA IN DE8
               GO TO TCR-FIL-REG-DE8-D00.
           PERFORM DIF-MES-FVEN-FINF.
           IF WSS-IND-STAT IN WSS-VARI = 1
               MOVE ZEROES TO DE8-NUM-CMAA IN DE8
               MOVE 0 TO WSS-IND-STAT IN WSS-VARI
               DISPLAY 'ERROR AL CALCULAR PLAZO DE MOROSIDAD : '
                       'KEY : ' D00-COD-ID00 IN D00
               DISPLAY 'STATUS FEC : ' FEC-STAT ' / ' FEC-MENS
                       ' FEC1 : ' FEC-FEC1 ' FEC2 : ' FEC-FEC2
           ELSE
               MOVE FEC-NMES IN FEC-VARI TO DE8-NUM-CMAA IN DE8.

           MOVE DE8-NUM-DCRE IN DE8  TO FEC-ITM1.
           MOVE DE8-NUM-MCRE IN DE8  TO FEC-ITM2.
           IF DE8-NUM-ACRE IN DE8 < 60
               MOVE  20  TO FEC-ITM3
           ELSE
               MOVE  19  TO FEC-ITM3.
           MOVE DE8-NUM-ACRE IN DE8  TO FEC-ITM4.
           MOVE DE8-NUM-PCRE IN DE8  TO FEC-NMES.
           MOVE FEC-SUM-MES          TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
              GO TO TCR-FIL-REG-DE8-D00.

           MOVE FEC-ITM1 TO WSS-NUM-DAUX IN WSS-VARI.
           MOVE FEC-ITM2 TO WSS-NUM-MAUX IN WSS-VARI.
           MOVE FEC-ITM3 TO WSS-NUM-SAUX IN WSS-VARI.
           MOVE FEC-ITM4 TO WSS-NUM-AAUX IN WSS-VARI.

           IF WSS-FEC-FAUX IN WSS-VARI < WSS-FEC-FIN1 IN WSS-VARI
              PERFORM DIF-MES-FAUX-FIN1
              IF WSS-IND-STAT IN WSS-VARI = 1
                 DISPLAY 'ERROR AL CALCULAR PLAZO DE MOROSIDAD : '
                         'KEY : ' D00-COD-ID00 IN D00
                 DISPLAY 'STATUS FEC : ' FEC-STAT ' / ' FEC-MENS
                         ' FEC1 : ' FEC-FEC1 ' FEC2 : ' FEC-FEC2
                 MOVE ZEROES TO WSS-NUM-DIFM IN WSS-VARI
              ELSE
                 MOVE FEC-NMES IN FEC-VARI TO WSS-NUM-DIFM IN WSS-VARI
              END-IF
              IF WSS-NUM-DIFM IN WSS-VARI > DE8-NUM-CMAA IN DE8
                 MOVE WSS-NUM-DIFM IN WSS-VARI TO DE8-NUM-CMAA IN DE8.
           GO TO TCR-FIL-REG-DE8-D00.

       CONT-CALC.
      *LA "CANTIDAD CUOTAS ATRASADAS"(DE8-NUM-CCAT) Y LA "CANTIDAD DE
      * MESES DE ATRASO DE LA CUOTA MAS ANTIGUA"(DE8-NUM-CMAA)
      * SE CALCULAN IGUAL, PARA EL CASO DEL SIST '05'
      *Y EL CALCULO SE HARA EN EL SIGUIENTE PERFORM:
           PERFORM DIF-MES-FVEN-FPRO.
           IF WSS-IND-OK IN WSS-VARI = '1'
               DISPLAY 'ERROR AL CALCULAR CANTIDAD DE CUOTAS Y/O MESES'
                       ' DE ATRASO'
                       ' KEY : ' D00-COD-ID00 IN D00
               MOVE ZEROES TO DE8-NUM-CCAT IN DE8
                              DE8-NUM-CMAA IN DE8
           ELSE
               MOVE WSS-NUM-DIVP TO DE8-NUM-CCAT IN DE8
                                    DE8-NUM-CMAA IN DE8.

       TCR-FIL-REG-DE8-D00.

      *AQUI COMIENSAN LOS CALCULOS COMUNES A AMBOS SISTEMAS '01' Y '05'

           ADD  D00-VAL-SMO1 IN D00
                D00-VAL-SMO2 IN D00
                D00-VAL-SCVC IN D00  GIVING DE8-VAL-MCAT IN DE8.

           IF DE8-NUM-CCAT IN DE8 > ZEROES OR
              DE8-VAL-MCAT IN DE8 > ZEROES OR
              DE8-NUM-CMAA IN DE8 > ZEROES
              IF DE8-NUM-CCAT IN DE8 NOT > ZEROES OR
                 DE8-VAL-MCAT IN DE8 NOT > ZEROES OR
                 DE8-NUM-CMAA IN DE8 NOT > ZEROES
                  DISPLAY '********************************************'
                  DISPLAY 'ERROR DE INCONSISTENCIA EN LOS CAMPOS:'
                  DISPLAY '-CANTIDAD DE CUOTAS ATRASADAS'
                  DISPLAY '-MONTO DE CUOTAS ATRASADAS '
                  DISPLAY '-CANTIDAD DE MESES DE ATRASO DE LA CUOTA MAS'
                          ' ANTIGUA'
                  DISPLAY '  '
                  DISPLAY 'KEY : ' D00-COD-ID00 IN D00
                  DISPLAY '*******************************************'.

           ADD  D00-VAL-SCON IN D00
                D00-VAL-RDEV IN D00
                D00-VAL-IPCO IN D00
                D00-VAL-IPCR IN D00
                D00-VAL-RDER IN D00 GIVING DE8-VAL-SDCA IN DE8.
           MOVE ZEROES              TO DE8-VAL-PRCR IN DE8.
           MOVE ZEROES              TO DE8-VAL-SDCO IN DE8.
           MOVE '0'                 TO DE8-IND-CJUD IN DE8.
           MOVE D00-IND-ESTC IN D00 TO DE8-IND-ESTC IN DE8.
           MOVE SPACES              TO DE8-GLS-FILL IN DE8.

      *BUSCA CREDITO PRINCIPAL
           MOVE DE8-COD-NIIO IN DE8 TO WSS-COD-NIOO IN WSS-VARI.
           MOVE DE8-COD-NIIO IN DE8 TO ROV-CIC-IOPC IN ROV.
           MOVE DE8-COD-NIIO IN DE8 TO WSS-CIC-IROV IN WSS-VARI.
       LEE-REG-ROV.
           MOVE SPACES               TO ROV-CIC-IOPV IN ROV.
           MOVE ZEROES               TO ROV-NUM-IVCT IN ROV.
           MOVE FIO-GET-NLS          TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT (FIO-STAT-OKS AND
                   WSS-CIC-IROV IN WSS-VARI = ROV-CIC-IOPC IN ROV)
              MOVE WSS-COD-NIOO IN WSS-VARI TO DE8-COD-NIOO IN DE8
              GO TO FIN-FIL-REG-DE8-D00.

           MOVE ROV-CIC-IOPC IN ROV TO DLC-CIC-IOPC IN DLC.
           MOVE  1                  TO DLC-NUM-IDLC IN DLC.
           MOVE WSS-IDD-GAUX IN WSS-VARI TO IDD-GAUX.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'DLC    NEX'   TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' / KEY : ' ROV-CIC-IOPC IN ROV
               MOVE  1  TO WSS-IND-STAT IN WSS-VARI
               GO TO FIN-FIL-REG-DE8-D00.

           IF ( DLC-IND-SCTB IN DLC = 'CAS'  OR
                DLC-IND-SCTB IN DLC = 'VCD' )
              MOVE ROV-CIC-IOPV IN ROV TO ROV-CIC-IOPC IN ROV
              MOVE ROV-CIC-IOPV IN ROV TO WSS-CIC-IROV IN WSS-VARI
              MOVE WSS-CIC-IROV IN WSS-VARI TO WSS-COD-NIOO
              GO TO LEE-REG-ROV
           ELSE
              MOVE ROV-CIC-IOPC IN ROV TO WSS-CIC-IROV IN WSS-VARI.

           MOVE WSS-CIC-IROV IN WSS-VARI TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF ( FIO-STAT-OKS AND OPC-VAL-SCRE IN OPC > ZEROES AND
                OPC-MSC-STAT IN OPC = 'A' )
              IF ROV-CIC-IOPC IN ROV = WSS-CIC-IROV IN WSS-VARI
                 MOVE ROV-CIC-IOPC IN ROV TO DE8-COD-NIOO IN DE8
                 GO TO FIN-FIL-REG-DE8-D00
              ELSE
                 MOVE WSS-CIC-IROV IN WSS-VARI TO ROV-CIC-IOPC IN ROV
                 MOVE WSS-CIC-IROV IN WSS-VARI TO WSS-COD-NIOO
                 GO TO LEE-REG-ROV
           ELSE
              MOVE WSS-COD-NIOO IN WSS-VARI TO DE8-COD-NIOO IN DE8
              GO TO FIN-FIL-REG-DE8-D00.

       FIN-FIL-REG-DE8-D00.
           EXIT.

      *-----------------------------------------------------------------
      * Calcula Diferencia en Meses entre Fechas Curse y Vencimiento (vi
       DIF-MES-FCOL-FMAV SECTION.
       INI-DIF-MES-FCOL-FMAV.
           MOVE  0  TO WSS-IND-STAT IN WSS-VARI.
           MOVE SPACES                   TO FEC-FEC1.
           MOVE SPACES                   TO FEC-FEC2.
           MOVE WSS-NUM-DCOL IN WSS-VARI TO FEC-DEC1.
           MOVE WSS-NUM-MCOL IN WSS-VARI TO FEC-MEC1.
           MOVE WSS-NUM-SCOL IN WSS-VARI TO FEC-SEC1.
           MOVE WSS-NUM-ACOL IN WSS-VARI TO FEC-AEC1.
           MOVE WSS-NUM-DMAV IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MMAV IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SMAV IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-AMAV IN WSS-VARI TO FEC-AEC2.
      *ESTO ES PARA QUE DE8-NUM-PCRE NO QUEDE EN 0 CUANDO FEC1 Y FEC2
      *SON IGUALES, YA QUE SBIF, DICE QUE MINIMO DEBE SER 1
           IF FEC-FEC1 = FEC-FEC2
               MOVE 1 TO FEC-NMES IN FEC-VARI
               GO TO FIN-DIF-MES-FCOL-FMAV.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-MES  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE  1  TO WSS-IND-STAT IN WSS-VARI.
           IF FEC-NDIA IN FEC-VARI > 0
               ADD  1  TO FEC-NMES IN FEC-VARI.
       FIN-DIF-MES-FCOL-FMAV.
           EXIT.
      *-----------------------------------------------------------------
      * Calcula Diferencia en Meses entre Fechas Vencimiento e Informe (
       DIF-MES-FVEN-FINF SECTION.
       INI-DIF-MES-FVEN-FINF.
           MOVE  0  TO WSS-IND-STAT IN WSS-VARI.
           MOVE SPACES                   TO FEC-FEC1.
           MOVE SPACES                   TO FEC-FEC2.
           MOVE D00-NUM-DPVC IN D00      TO FEC-DEC1.
           MOVE D00-NUM-MPVC IN D00      TO FEC-MEC1.
           IF D00-NUM-APVC IN D00 > 80
               MOVE  19  TO FEC-SEC1
           ELSE
               MOVE  20  TO FEC-SEC1.
           MOVE D00-NUM-APVC IN D00      TO FEC-AEC1.

           MOVE WSS-NUM-DINF IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MINF IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SINF IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-AINF IN WSS-VARI TO FEC-AEC2.

      *ESTO ES PARA QUE DE8-NUM-CMAA NO QUEDE EN 0 CUANDO FEC1 Y FEC2
      *SON IGUALES, YA QUE SBIF, DICE QUE MINIMO DEBE SER 1
           IF FEC-FEC1 = FEC-FEC2
               MOVE 1 TO FEC-NMES IN FEC-VARI
               GO TO FIN-DIF-MES-FVEN-FINF.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-MES  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE  1  TO WSS-IND-STAT IN WSS-VARI.

           IF WSS-IND-FINMES IN WSS-VARI = 'S' AND FEC-STAT-OKS
               ADD 1 TO FEC-NMES IN FEC-VARI
               GO TO FIN-DIF-MES-FVEN-FINF.

           IF FEC-NDIA IN FEC-VARI > 0
               ADD  1  TO FEC-NMES IN FEC-VARI.
       FIN-DIF-MES-FVEN-FINF.
           EXIT.
      ******************************************************************
       VAL-FINF-FINMES SECTION.
       INI-VAL-FINF-FINMES.
           MOVE WSS-NUM-MINF IN WSS-VARI TO FEC-ITM2.
           MOVE WSS-NUM-SINF IN WSS-VARI TO FEC-ITM3.
           MOVE WSS-NUM-AINF IN WSS-VARI TO FEC-ITM4.
           MOVE 01                       TO FEC-ITM1.
           IF FEC-ITM2 = 12
               MOVE 01                   TO FEC-ITM2
               ADD  01                   TO FEC-ITM4
               IF FEC-ITM4 = 0
                   ADD 01                TO FEC-ITM3
               ELSE
                   NEXT SENTENCE
           ELSE
               ADD  01                   TO FEC-ITM2.

           MOVE FEC-FORM-FEC        TO FEC-FORM.
           MOVE FEC-RST-DIA         TO FEC-CMND.
           MOVE 1                   TO FEC-NDIA.
           PERFORM CAL-FEC.
           IF ( FEC-STAT-OKS                        AND
                WSS-NUM-DINF IN WSS-VARI = FEC-ITM1 AND
                WSS-NUM-MINF IN WSS-VARI = FEC-ITM2 AND
                WSS-NUM-SINF IN WSS-VARI = FEC-ITM3 AND
                WSS-NUM-AINF IN WSS-VARI = FEC-ITM4 )
                 MOVE 'S' TO WSS-IND-FINMES IN WSS-VARI
           ELSE
                 MOVE 'N' TO WSS-IND-FINMES IN WSS-VARI.
       FIN-VAL-FINF-FINMES.
           EXIT.
      ******************************************************************
       DIF-MES-FVEN-FPRO SECTION.
      *-------------------------*
       INI-DIF-MES-FVEN-FPRO.
      * CALCULAR DIFERENCIA ENTRE FVEN Y FPRO EN MES.
           MOVE ZEROES TO WSS-NUM-DIVP.
           MOVE SPACES TO WSS-IND-OK IN WSS-VARI.

           MOVE D00-NUM-DPVC IN D00  TO WSS-NUM-DAUX IN WSS-VARI.
           MOVE D00-NUM-MPVC IN D00  TO WSS-NUM-MAUX IN WSS-VARI.
           MOVE D00-NUM-APVC IN D00  TO WSS-NUM-AAUX IN WSS-VARI.
           IF D00-NUM-APVC IN D00 > 80
               MOVE  19  TO WSS-NUM-SAUX IN WSS-VARI
           ELSE
               MOVE  20  TO WSS-NUM-SAUX IN WSS-VARI.

      *CREDITO AL DIA.
           IF WSS-FEC-FIN1 IN WSS-VARI  < WSS-FEC-FAUX IN WSS-VARI
              MOVE ZEROES TO WSS-NUM-DIVP
              GO TO FIN-DIF-MES-FVEN-FPRO.

           MOVE D00-NUM-DPVC IN D00      TO FEC-DEC1.
           MOVE D00-NUM-MPVC IN D00      TO FEC-MEC1.
           IF D00-NUM-APVC IN D00 > 80
              MOVE  19  TO FEC-SEC1
           ELSE
              MOVE  20  TO FEC-SEC1.
           MOVE D00-NUM-APVC IN D00      TO FEC-AEC1.

           MOVE WSS-NUM-DINF IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MINF IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SINF IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-AINF IN WSS-VARI TO FEC-AEC2.

           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
              MOVE '1'  TO WSS-IND-OK IN WSS-VARI
              GO TO FIN-DIF-MES-FVEN-FPRO.

           IF WSS-IND-SU90 IN WSS-VARI = 'S'
               COMPUTE FEC-NDIA = FEC-NDIA + 90.

           IF  FEC-NDIA < 31
              MOVE 1 TO WSS-NUM-DIVP
           ELSE
           IF FEC-NDIA > 30  AND
              FEC-NDIA < 61
              MOVE 2 TO WSS-NUM-DIVP
           ELSE
           IF FEC-NDIA > 60  AND
              FEC-NDIA < 91
              MOVE 3 TO WSS-NUM-DIVP
           ELSE
           IF FEC-NDIA > 90 AND
              FEC-NDIA < 121
              MOVE 4 TO WSS-NUM-DIVP
           ELSE
           IF FEC-NDIA > 120
              MOVE 5 TO WSS-NUM-DIVP.

       FIN-DIF-MES-FVEN-FPRO.
           EXIT.
      ******************************************************************
       BUSCA-EN-DCC-TCR SECTION.
      *-------------------------*
       INI-BUSCA-EN-DCC-TCR.
           MOVE SPACES              TO WSS-IND-TCR IN WSS-VARI.
           MOVE SPACES              TO DCC-CIC-TABL IN DCC.
           MOVE D00-COD-TOPE IN D00 TO DCC-CIC-TCCT IN DCC.
           MOVE 'DCC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-DCC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL BUSCAR CODIGO EN DCC, CON TIPO DE OPER'
                      'ACION ' D00-COD-TOPE IN D00
              GO TO FIN-BUSCA-EN-DCC-TCR.
           IF DCC-COD-TOCR IN DCC = 'TCR'
              MOVE 'S' TO WSS-IND-TCR IN WSS-VARI.
       FIN-BUSCA-EN-DCC-TCR.
           EXIT.
      ******************************************************************
       DIF-MES-FAUX-FIN1 SECTION.
      *-------------------------*
       INI-DIF-MES-FAUX-FIN1.
           MOVE  0  TO WSS-IND-STAT IN WSS-VARI.
           MOVE SPACES                   TO FEC-FEC1.
           MOVE SPACES                   TO FEC-FEC2.

           MOVE WSS-NUM-DAUX IN WSS-VARI TO FEC-DEC1.
           MOVE WSS-NUM-MAUX IN WSS-VARI TO FEC-MEC1.
           MOVE WSS-NUM-SAUX IN WSS-VARI TO FEC-SEC1.
           MOVE WSS-NUM-AAUX IN WSS-VARI TO FEC-AEC1.

           MOVE WSS-NUM-DIN1 IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MIN1 IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SIN1 IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-AIN1 IN WSS-VARI TO FEC-AEC2.

           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-MES  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE  1  TO WSS-IND-STAT IN WSS-VARI.
           IF FEC-NDIA IN FEC-VARI > 0
              ADD  1  TO FEC-NMES IN FEC-VARI.
       FIN-DIF-MES-FAUX-FIN1.
           EXIT.
      ******************************************************************
       MUESTRA-ESTAD SECTION.
       INI-MUESTRA-ESTAD.
      * Despliega Totales de Control Proceso
           DISPLAY 'TOTAL D00 LEIDOS         : '
                               WSS-NUM-D00L IN WSS-VARI.
           DISPLAY 'TOTAL D00 SELECCIONADOS  : '
                               WSS-NUM-D00S IN WSS-VARI.
           DISPLAY 'TOTAL D00 PROCESADOS     : '
                               WSS-NUM-D00P IN WSS-VARI.
           DISPLAY 'TOTAL D00 RECHAZADOS     : '
                               WSS-NUM-D00R IN WSS-VARI.
           DISPLAY 'TOTAL RVO LEIDOS         : '
                               WSS-NUM-RVOL IN WSS-VARI.
           DISPLAY 'TOTAL ROV LEIDOS         : '
                               WSS-NUM-LROV IN WSS-VARI.
           DISPLAY 'TOTAL RVO SELECCIONADOS  : '
                               WSS-NUM-RVOS IN WSS-VARI.
           DISPLAY 'TOTAL RVO PARA PROCESO   : '
                               WSS-NUM-RVOP IN WSS-VARI.
           DISPLAY 'TOTAL RVO RECHAZADOS     : '
                               WSS-NUM-RVOR IN WSS-VARI.
           DISPLAY 'TOTAL DE8 GRABADOS       : '
                               WSS-NUM-DE8G IN WSS-VARI.
           DISPLAY 'TOTAL DE8 MODIFICADOS    : '
                               WSS-NUM-DE8M IN WSS-VARI.
       FIN-MUESTRA-ESTAD.
           EXIT.

       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.

       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY TABBFCAM.
       COPY SGCBFDBC.
       COPY COLBFOPC.
       COPY COLBFDLC.
       COPY COLBFRDC.
      *RRM-INI 25-MAR-1999 SE EXPANDE EL COPY  COLBFDCC.
      *COPY COLBFDCC.
      * Modulo FIO para registro DCC en DATACOM, 25-MAR-1993 09:59:22.64
       COL-FIO-DCC SECTION.
       INI-COL-FIO-DCC.
           MOVE 'DCC' TO DCC-COD-TTAB IN DCC
                         DCC-CIC-TTAB IN DCC
                         DCC-EXT-TTAB IN DCC.

           MOVE ADR-DCC-REQA TO ADR-REQA.
           MOVE 'DCC' TO ADR-TABL IN ADR-REQA.
      *
           MOVE 'COL'  TO IDD-SAUX.
           MOVE 'C'    TO IDD-GAUX.
           MOVE SPACES TO IDD-RAUX.
           MOVE 1 TO IDD-IAUX.
           SET IDD-ITBL TO 1.
           SEARCH IDD-VSIS VARYING IDD-IAUX
             AT END
               GO TO GRP-COL-FIO-DCC
             WHEN IDD-DENT( IDD-ITBL ) = IDD-DAUX
               MOVE IDD-IENT( IDD-ITBL) TO ADR-DBID IN ADR-REQA
               GO TO FSR-COL-FIO-DCC.
       GRP-COL-FIO-DCC.
           MOVE SPACES TO IDD-GAUX.
           MOVE 1      TO IDD-IAUX.
           SET IDD-ITBL TO 1.
           SEARCH IDD-VSIS VARYING IDD-IAUX
             AT END
               MOVE 'En COL-FIO-DCC, NO existe en IDD:' TO FIO-MEN1
               MOVE IDD-DAUX                            TO FIO-MEN2
               PERFORM PRG-ABT
             WHEN IDD-DENT( IDD-ITBL ) = IDD-DAUX
               MOVE IDD-IENT( IDD-ITBL) TO ADR-DBID IN ADR-REQA.
       FSR-COL-FIO-DCC.
      *
           MOVE 'DCC00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-DCC.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'DCC-COD-TABL' OR FIO-AKEY NOT > SPACES )
               MOVE DCC-COD-TABL IN DCC TO ADR-VKEY IN ADR-REQA
               MOVE 'DCC00' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'DCC-STP-ITRN'
               MOVE DCC-STP-ITRN IN DCC TO ADR-VKEY IN ADR-REQA
               MOVE 'DCC04' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'DCC-CIC-TABL'
               MOVE DCC-CIC-TABL IN DCC TO ADR-VKEY IN ADR-REQA
               MOVE 'DCC01' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'DCC-EXT-TABL'
               MOVE DCC-EXT-TABL IN DCC TO ADR-VKEY IN ADR-REQA
               MOVE 'DCC02' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-DCC, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               PERFORM PRG-ABT.

           IF FIO-CMND = FIO-PUT OR FIO-MOD
              MOVE DCC-COD-TTAB IN DCC TO DCC-CIC-TTAB IN DCC
                                          DCC-EXT-TTAB IN DCC.

       CON-COL-FIO-DCC.
           MOVE DCC TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO DCC
           ELSE
               MOVE 'COLDCC' TO FIO-MEN2
               IF FIO-STAT-FTL
                   PERFORM PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-DCC-REQA.
       FIN-COL-FIO-DCC.
           EXIT.
      *COPY COLBFRVO.
       COPY COLBFROV.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY GNSBGPES.
       COPY COLBFTOC.
       COPY COLBGCTO.
      *
       COPY COLBFD00.
      *CAH POR MIENTRAS ASTERISQUEO SUPIOD01
      *COPY SUPIOD01.
       SUP-FIO-D01 SECTION.
       INI-SUP-FIO-D01.
           MOVE '00'       TO FIO-STAT.
           MOVE 'SUPPFD01' TO D01-GLS-NOMD IN D01.
       FIN-SUP-FIO-D01.
           EXIT.
      *CAH POR MIENTRAS ASTERISQUEO SUPIOD01
       COPY SUPBFDE8.
      *>>>>
