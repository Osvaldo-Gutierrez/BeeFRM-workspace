       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGACU.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.
      * Acumulacion del movimiento contable diario
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LMC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ACU-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-NWA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
      *<<<< WSS
      *
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
      *
       COPY COLBRLMC.
       COPY COLBRACU.
       COPY COLBRNWA.
       COPY COLBRDPC.
       COPY GNSWGFEC.
      *
       COPY GNSBRMSG.
       COPY GNSWGHOY.
       COPY GNSBRTAB.
      *COPY GNSBRMSC.
      *
       01  WSS-VARI.
           03 WSS-FEC-FCTB                       PIC X(08).
           03 WSS-FEC-FTAR.
              05  WSS-FIL-1                      PIC X(01).
              05  WSS-FEC-FTA1.
                  07  WSS-NUM-DTAR               PIC 9(02).
                  07  WSS-FIL-SLA1               PIC X(01).
                  07  WSS-NUM-MTAR               PIC 9(02).
                  07  WSS-FIL-SLA2               PIC X(01).
                  07  WSS-NUM-ATAR               PIC 9(02).
           03 WSS-PARAM-PERM.
              05  WSS-NUM-PERM                   PIC X(01).
              05  WSS-MES-PERM                   PIC 9(03).
              05  WSS-MES-PERM-R REDEFINES WSS-MES-PERM PIC X(03).
           03 WSS-IND-INIC                       PIC X(01).
           03 WSS-FEC-FPRC.
              05 WSS-NUM-SPRC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DPRC        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DFEC        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FTOP.
              05 WSS-NUM-SATOP       VALUE ZEROS  PIC 9(04).
              05 WSS-NUM-R-SATOP REDEFINES WSS-NUM-SATOP.
                 07 WSS-NUM-STOP                  PIC 9(02).
                 07 WSS-NUM-ATOP                  PIC 9(02).
              05 WSS-NUM-MTOP        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DTOP        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FANT.
              05 WSS-NUM-SANT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AANT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MANT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DANT        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AINI        VALUE ZEROS  PIC 9(02).
           03 WSS-MESES              VALUE 0      PIC 9(06).
           03 WSS-ANOS               VALUE 0      PIC 9(06).
           03 CNT-LEI-ACU            VALUE 0      PIC 9(09).
           03 MNT-LEI-ACU            VALUE 0      PIC 9(14)V9(4).
           03 CNT-GRA-ACU            VALUE 0      PIC 9(09).
           03 MNT-GRA-ACU            VALUE 0      PIC 9(14)V9(4).
           03 CNT-ELI-ACU            VALUE 0      PIC 9(09).
           03 MNT-ELI-ACU            VALUE 0      PIC 9(14)V9(4).
           03 CNT-LEI-LMC            VALUE 0      PIC 9(09).
           03 MNT-LEI-LMC            VALUE 0      PIC 9(14)V9(4).
           03 WSS-EOF-ACU            VALUE 'N'    PIC X(01).
              88 EOF-ACU             VALUE 'S'.
           03 WSS-EOF-LMC            VALUE 'N'    PIC X(01).
              88 EOF-LMC             VALUE 'S'.
           03 WSS-STAT-PGM           VALUE 0      PIC 9(01).
              88 PGM-STAT-OKS        VALUE 0.
              88 PGM-STAT-INF        VALUE 1.
              88 PGM-STAT-MAL        VALUE 2.
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *============
       INI-MAIN.
      *--------
       COPY GNSBGEDB.
           MOVE 'COLPGACU' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM LEE-PARAM.
           PERFORM LEE-PRO-ANT.
           PERFORM PRO-ACU.
           PERFORM PRO-LMC.
           PERFORM ESTADISTICAS.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
      *--------
           PERFORM GNS-PRO-END.
       OPEN-FILE SECTION.
      *=================
       INI-OPEN-FILE.
      *-------------
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-LMC.
           PERFORM COL-FIO-ACU.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-NWA.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-DPC.
       FIN-OPEN-FILE.
      *-------------
           EXIT.
       LEE-PARAM SECTION.
      *=================
       INI-LEE-PARAM.
      *-------------
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
       FECHA-PROCESO.
      *-------------
      *    CAPTURA FECHA PROCESO
      * ini-jgm 1-04-1998.
      *  formato de la fecha de proceso, ya que aceptaba solo
      * el ano (2 digitos).
      *Captura Fecha de Proceso
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FEC DE PROC (DDMMSSAA) : ' WSS-FEC-FPRO IN WSS-VARI.
      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT.
      **********************************
      *    CAPTURA FECHA PROCESO
      *     DISPLAY 'FECHA DE PROCESO                 (*DD/MM/AA) : '.
      *     ACCEPT  WSS-FEC-FTAR IN WSS-VARI.
      *     DISPLAY WSS-FEC-FTA1 IN WSS-VARI.
      *     MOVE WSS-NUM-DTAR IN WSS-VARI TO WSS-NUM-DPRO IN WSS-VARI.
      *     MOVE WSS-NUM-MTAR IN WSS-VARI TO WSS-NUM-MPRO IN WSS-VARI.
      *     MOVE WSS-NUM-ATAR IN WSS-VARI TO WSS-NUM-APRO IN WSS-VARI.
      *     IF WSS-NUM-APRO IN WSS-VARI < 50
      *        MOVE 20                    TO WSS-NUM-SPRO IN WSS-VARI
      *     ELSE
      *        MOVE 19                    TO WSS-NUM-SPRO IN WSS-VARI.
      *    VAL-FEC Valida Fecha de Proceso
      *     MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
      *     MOVE FEC-FORM-FEC TO FEC-FORM.
      *     MOVE FEC-FBLK-N   TO FEC-FBLK.
      *     MOVE FEC-CHOY-LE  TO FEC-CHOY.
      *     MOVE FEC-VHBL-N   TO FEC-VHBL.
      *     MOVE FEC-VAL-FEC  TO FEC-CMND.
      *     PERFORM PRO-FEC.
      *     IF NOT FEC-STAT-OKS
      *         DISPLAY FEC-MENS
      *         DISPLAY '---------------------------------------------'
      *         DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
      *         DISPLAY '---------------------------------------------'
      *         PERFORM ESTADISTICAS
      *         PERFORM GNS-PRO-ABT.
      **********************************
      * fin-jgm 13-04-1998
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DPRC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MPRC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SPRC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-APRC IN WSS-VARI.

           DISPLAY 'BUSCA-HABIL-ANTERIOR'.
           DISPLAY '===================='.
           DISPLAY 'FECHA DE PARTIDA : ' WSS-FEC-FFEC.

       BUS-DIA-HBL-ANT.
      *---------------
      *BUSCA HABIL ANTERIOR
           MOVE WSS-NUM-DFEC TO FEC-ITM1.
           MOVE WSS-NUM-MFEC TO FEC-ITM2.
           MOVE WSS-NUM-SFEC TO FEC-ITM3.
           MOVE WSS-NUM-AFEC TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM CAL-FEC.
           MOVE FEC-FECH TO WSS-FEC-FINI.
           DISPLAY 'FECHA OBTENIDA : ' WSS-FEC-FINI.
      *VAL-FEC Validar Fecha
           MOVE WSS-FEC-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
              DISPLAY 'FECHA INHABIL : ' WSS-FEC-FINI
              MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI
              MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI
              MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI
              MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI
              GO TO BUS-DIA-HBL-ANT.
           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DANT IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MANT IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SANT IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AANT IN WSS-VARI.
           DISPLAY 'FECHA HABIL ANTERIOR : ' WSS-FEC-FANT.

       PERMANENCIA.
      *-----------
      *    CAPTURA FLAG DE LIMPIEZA DE ARCHIVO ACUMULADO
           DISPLAY 'PERIODO DE PERMANENCIA EN ACUMULADO (*999)   : '.
           ACCEPT   WSS-PARAM-PERM IN WSS-VARI.
           DISPLAY  WSS-MES-PERM   IN WSS-VARI.

           IF WSS-MES-PERM-R IS NOT NUMERIC
              MOVE 1 TO WSS-MES-PERM
              DISPLAY 'NO NUMERICO.......ASUME 1'.

           IF WSS-MES-PERM < 1
              MOVE 1 TO WSS-MES-PERM
              DISPLAY 'MENOR QUE 1.......ASUME 1'.

           MOVE WSS-FEC-FPRC TO WSS-FEC-FTOP.
           MOVE 1            TO WSS-NUM-DTOP.

           DIVIDE WSS-MES-PERM BY 12 GIVING  WSS-ANOS
                                   REMAINDER WSS-MESES.
           COMPUTE WSS-NUM-SATOP = WSS-NUM-SATOP - WSS-ANOS.

           IF WSS-MESES NOT < WSS-NUM-MTOP
              COMPUTE WSS-MESES     = WSS-MESES - WSS-NUM-MTOP
              COMPUTE WSS-NUM-MTOP  = 12 - WSS-MESES
              COMPUTE WSS-NUM-SATOP = WSS-NUM-SATOP - 1
           ELSE
              COMPUTE WSS-NUM-MTOP = WSS-NUM-MTOP - WSS-MESES.

           DISPLAY 'FECHA TOPE PERMANENCIA EN ACUMULADO AAAAMMDD : '.
           DISPLAY WSS-FEC-FTOP.

       FLAG-PRIMER-PROCESO.
      *-------------------
      *    CAPTURA FLAG DE INICIO
      *    EL PRIMER PROCESO => 'S'; DESPUES SIEMPRE FIJO EN 'N'
           DISPLAY 'PARAMETRO RESERVADO                          : '.
           ACCEPT  WSS-IND-INIC IN WSS-VARI.
           DISPLAY WSS-IND-INIC IN WSS-VARI.
           IF WSS-IND-INIC NOT = 'S' AND NOT = 'N'
              MOVE 'N' TO WSS-IND-INIC IN WSS-VARI
              DISPLAY 'DISTINTO DE "S" O "N"...ASUME N'.
           DISPLAY 'FIN LECTURA Y VALIDACION DE PARAMETROS'.
       FIN-LEE-PARAM.
      *-------------
           EXIT.
       LEE-PRO-ANT SECTION.
      *===================
       INI-LEE-PRO-ANT.
      *---------------
           MOVE '4'           TO DPC-TIP-REGI IN DPC.
           MOVE 'ACU_CTRL'    TO DPC-COD-CTRL IN DPC.
           MOVE SPACES        TO DPC-FIL-FIL4 IN DPC.
           IF WSS-IND-INIC IN WSS-VARI NOT = 'S'
              MOVE FIO-GET-KEY-UPD   TO FIO-CMND
              PERFORM COL-FIO-DPC
              IF NOT (FIO-STAT-OKS)
                 DISPLAY '---------------------------------------------'
                 DISPLAY ' ERROR EN EL PROCESO                         '
                 DISPLAY ' AL LEER REGISTRO 4  DPC                     '
                 DISPLAY ' FILE STATUS DPC => ' FIO-STAT
                 DISPLAY '---------------------------------------------'
                 DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
                 DISPLAY '---------------------------------------------'
                 PERFORM ESTADISTICAS
                 PERFORM GNS-PRO-ABT.
           IF WSS-IND-INIC IN WSS-VARI = 'S'
              MOVE WSS-FEC-FANT  TO DPC-FEC-FPR4 IN DPC
              MOVE ZEROES        TO DPC-FEC-FELI IN DPC
              MOVE ZEROES        TO DPC-NUM-RACU IN DPC
              MOVE ZEROES        TO DPC-VAL-MACU IN DPC
              MOVE ZEROES        TO DPC-NUM-RELI IN DPC
              MOVE ZEROES        TO DPC-VAL-MELI IN DPC
              MOVE SPACES        TO DPC-GLS-DIS4 IN DPC.
           DISPLAY '----------------------------------------------'.
           DISPLAY 'DATOS DEL PROCESO ANTERIOR                    '.
           DISPLAY '----------------------------------------------'.
           DISPLAY 'FECHA PROCESO ANTERIOR     : ' DPC-FEC-FPR4 IN DPC.
           DISPLAY 'ACUMULADOS (NRO.)          : ' DPC-NUM-RACU IN DPC.
           DISPLAY 'ACUMULADOS (MTO.)          : ' DPC-VAL-MACU IN DPC.
           DISPLAY 'FECHA PERMANENCIA          : ' DPC-FEC-FELI IN DPC.
           DISPLAY 'ELIMINADOS (NRO.)          : ' DPC-NUM-RELI IN DPC.
           DISPLAY 'ELIMINADOS (MTO.)          : ' DPC-VAL-MELI IN DPC.
           DISPLAY '----------------------------------------------'.
           IF WSS-FEC-FANT NOT = DPC-FEC-FPR4 IN DPC
               DISPLAY '----------------------------------------------'
               DISPLAY 'ERROR EN EL PROCESO                           '
               DISPLAY 'FECH.ANT.NO CORRESPONDE A PROCESO -1 DIA HABIL'
               DISPLAY '----------------------------------------------'
               DISPLAY 'FECHA PROCESO ANTERIOR   : ' DPC-FEC-FPR4 IN DPC
               DISPLAY 'FECHA PROCESO            : ' WSS-FEC-FPRC
               DISPLAY 'FECHA PROC.ANT.CALCULADA : ' WSS-FEC-FANT
               DISPLAY '----------------------------------------------'
               DISPLAY '********** PROCESO AUTOCANCELADO *************'
               DISPLAY '----------------------------------------------'
               PERFORM ESTADISTICAS
               PERFORM GNS-PRO-ABT.
       FIN-LEE-PRO-ANT.
      *---------------
           EXIT.
       PRO-ACU SECTION.
      *===============
       INI-PRO-ACU.
      *-----------
           MOVE FIO-GET-NXT     TO FIO-CMND.
           PERFORM COL-FIO-ACU.
           IF NOT (FIO-STAT-OKS) AND WSS-IND-INIC IN WSS-VARI NOT = 'S'
               DISPLAY '---------------------------------------------'
               DISPLAY 'ERROR EN EL PROCESO                          '
               DISPLAY 'ARCHIVO VACIO "MOVIMIENTO ACUMULADO" ACU     '
               DISPLAY '---------------------------------------------'
               DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
               DISPLAY '---------------------------------------------'
               PERFORM ESTADISTICAS
               PERFORM GNS-PRO-ABT.
           IF NOT (FIO-STAT-OKS)
              MOVE 'S' TO WSS-EOF-ACU
           ELSE
              MOVE 'N' TO WSS-EOF-ACU.
           PERFORM LUP-PRO-ACU UNTIL EOF-ACU.
           IF MNT-LEI-ACU IN WSS-VARI NOT = DPC-VAL-MACU IN DPC OR
              CNT-LEI-ACU IN WSS-VARI NOT = DPC-NUM-RACU IN DPC
               DISPLAY '---------------------------------------------'
               DISPLAY ' ERROR EN EL PROCESO                         '
               DISPLAY ' MVTO LEIDO DESDE ACUMULADO NO CUADRA CON LO '
               DISPLAY ' GRABADO EN EL PROCESO DEL DIA ANTERIOR      '
               DISPLAY '---------------------------------------------'
               DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
               DISPLAY '---------------------------------------------'
               PERFORM ESTADISTICAS
               PERFORM GNS-PRO-ABT.
           GO TO FIN-PRO-ACU.
       LUP-PRO-ACU.
      *-----------
      *INI-JGM 13-04-1998.
      *    IF ACU-FEC-FCTB < WSS-FEC-FTOP
           IF ACU-FEC-FPRO < WSS-FEC-FTOP
      *INI-JGM 13-04-1998.
              ADD 1            TO CNT-LEI-ACU IN WSS-VARI
              ADD 1            TO CNT-ELI-ACU IN WSS-VARI
              ADD ACU-SGV-ICTB TO MNT-LEI-ACU IN WSS-VARI
              ADD ACU-SGV-ICTB TO MNT-ELI-ACU IN WSS-VARI
           ELSE
              ADD 1            TO CNT-LEI-ACU IN WSS-VARI
              ADD 1            TO CNT-GRA-ACU IN WSS-VARI
              ADD ACU-SGV-ICTB TO MNT-LEI-ACU IN WSS-VARI
              ADD ACU-SGV-ICTB TO MNT-GRA-ACU IN WSS-VARI
              MOVE ACU         TO NWA
              MOVE FIO-PUT     TO FIO-CMND
              PERFORM COL-FIO-NWA
              IF NOT (FIO-STAT-OKS)
                 DISPLAY '---------------------------------------------'
                 DISPLAY ' ERROR EN EL PROCESO                         '
                 DISPLAY ' AL GRABAR REGISTRO NWA DESDE ACU            '
                 DISPLAY ' FILE STATUS NWA => ' FIO-STAT
                 DISPLAY   NWA
                 DISPLAY '---------------------------------------------'
                 DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
                 DISPLAY '---------------------------------------------'
                 PERFORM ESTADISTICAS
                 PERFORM GNS-PRO-ABT.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-ACU.
           IF NOT (FIO-STAT-OKS)
              MOVE 'S' TO WSS-EOF-ACU.
       FIN-PRO-ACU.
      *-----------
           EXIT.
       PRO-LMC SECTION.
      *===============
       INI-PRO-LMC.
      *-----------
           MOVE FIO-GET-NXT     TO FIO-CMND.
           PERFORM COL-FIO-LMC.
           IF NOT (FIO-STAT-OKS)
               DISPLAY '---------------------------------------------'
               DISPLAY 'ERROR EN EL PROCESO                          '
               DISPLAY 'ARCHIVO VACIO "MOVIMIENTO CONTABLE" LMC      '
               DISPLAY '---------------------------------------------'
               DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
               DISPLAY '---------------------------------------------'
               PERFORM ESTADISTICAS
               PERFORM GNS-PRO-ABT.
           MOVE 'N' TO WSS-EOF-LMC.
           PERFORM LUP-PRO-LMC UNTIL EOF-LMC.
           MOVE '4'           TO DPC-TIP-REGI IN DPC.
           MOVE 'ACU_CTRL'    TO DPC-COD-CTRL IN DPC.
           MOVE SPACES        TO DPC-FIL-FIL4 IN DPC.
           MOVE WSS-FEC-FPRC  TO DPC-FEC-FPR4 IN DPC.
           MOVE WSS-FEC-FTOP  TO DPC-FEC-FELI IN DPC.
           MOVE CNT-GRA-ACU   TO DPC-NUM-RACU IN DPC.
           MOVE MNT-GRA-ACU   TO DPC-VAL-MACU IN DPC.
           MOVE CNT-ELI-ACU   TO DPC-NUM-RELI IN DPC.
           MOVE MNT-ELI-ACU   TO DPC-VAL-MELI IN DPC.
           MOVE SPACES        TO DPC-GLS-DIS4 IN DPC.
           IF WSS-IND-INIC IN WSS-VARI NOT = 'S'
              MOVE FIO-MOD  TO FIO-CMND
           ELSE
              MOVE FIO-PUT  TO FIO-CMND.
           PERFORM COL-FIO-DPC.
           IF NOT (FIO-STAT-OKS)
               DISPLAY '---------------------------------------------'
               DISPLAY ' ERROR EN EL PROCESO                         '
               DISPLAY ' AL GRABAR REGISTRO DPC                      '
               DISPLAY ' FILE STATUS DPC => ' FIO-STAT
               DISPLAY '---------------------------------------------'
               DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
               DISPLAY '---------------------------------------------'
               PERFORM ESTADISTICAS
               PERFORM GNS-PRO-ABT.
           GO TO FIN-PRO-LMC.
       LUP-PRO-LMC.
      *-----------
      *INI-JGM 15-04-1998
      *    IF LMC-FEC-FCTB NOT = WSS-FEC-FPRC
      *FIN-JGM 15-04-1998
           IF LMC-FEC-FPRO NOT = WSS-FEC-FPRC
              DISPLAY '---------------------------------------------'
              DISPLAY ' ERROR EN EL PROCESO                         '
              DISPLAY ' SE DETECTA INFORMACION EN LMC CON FECHA     '
              DISPLAY ' DISTINTA A LA DE PROCESO                    '
              DISPLAY ' FECHA EN REGISTRO LMC => ' LMC-FEC-FPRO
              DISPLAY '---------------------------------------------'
              DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
              DISPLAY '---------------------------------------------'
              PERFORM ESTADISTICAS
              PERFORM GNS-PRO-ABT
           ELSE
              ADD 1            TO CNT-LEI-LMC IN WSS-VARI
              ADD 1            TO CNT-GRA-ACU IN WSS-VARI
              ADD LMC-SGV-ICTB TO MNT-LEI-LMC IN WSS-VARI
              ADD LMC-SGV-ICTB TO MNT-GRA-ACU IN WSS-VARI
              MOVE LMC         TO NWA
              MOVE FIO-PUT     TO FIO-CMND
              PERFORM COL-FIO-NWA
              IF NOT (FIO-STAT-OKS)
                 DISPLAY '---------------------------------------------'
                 DISPLAY ' ERROR EN EL PROCESO                         '
                 DISPLAY ' AL GRABAR REGISTRO NWA DESDE LMC            '
                 DISPLAY ' FILE STATUS NWA => ' FIO-STAT
                 DISPLAY NWA
                 DISPLAY '---------------------------------------------'
                 DISPLAY ' ******** PROCESO AUTOCANCELADO *************'
                 DISPLAY '---------------------------------------------'
                 PERFORM ESTADISTICAS
                 PERFORM GNS-PRO-ABT.
              MOVE FIO-GET-NXT    TO FIO-CMND
              PERFORM COL-FIO-LMC
              IF NOT (FIO-STAT-OKS)
                 MOVE 'S' TO WSS-EOF-LMC.
       FIN-PRO-LMC.
      *-----------
           EXIT.
       ESTADISTICAS SECTION.
      *====================
       INI-ESTAD.
      *---------
           DISPLAY '-----------------------------------------------'.
           DISPLAY '        ESTADISTICAS DEL PROCESO '.
           DISPLAY '-----------------------------------------------'.
           DISPLAY '-----------------------------------------------'.
           DISPLAY 'FECHA PROCESADA                  : ' WSS-FEC-FPRC.
           DISPLAY 'ELIMINA ACUMULADOS ANTERIORES A  : ' WSS-FEC-FTOP.
           DISPLAY '-----------------------------------------------'.
           DISPLAY 'LEIDOS DESDE ACUMULADO   (NRO.)  : ' CNT-LEI-ACU .
           DISPLAY 'LEIDOS DESDE ACUMULADO   (MTO.)  : ' MNT-LEI-ACU.
           DISPLAY '-----------------------------------------------'.
           DISPLAY 'LEIDOS LMC (DIARIO)      (NRO.)  : ' CNT-LEI-LMC.
           DISPLAY 'LEIDOS LMC (DIARIO)      (MTO.)  : ' MNT-LEI-LMC.
           DISPLAY '-----------------------------------------------'.
           DISPLAY 'ELIMINADOS DEL ACUMULADO (NRO.)  : ' CNT-ELI-ACU.
           DISPLAY 'ELIMINADOS DEL ACUMULADO (MTO.)  : ' MNT-ELI-ACU.
           DISPLAY '-----------------------------------------------'.
           DISPLAY 'GRABADOS NUEVO ACUMULADO (NRO.)  : ' CNT-GRA-ACU.
           DISPLAY 'GRABADOS NUEVO ACUMULADO (MTO.)  : ' MNT-GRA-ACU.
           DISPLAY '-----------------------------------------------'.
       FIN-ESTAD.
           EXIT.
       CLOSE-FILE SECTION.
      *==================
       INI-CLOSE-FILE.
      *--------------
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-LMC.
           PERFORM COL-FIO-ACU.
           PERFORM COL-FIO-NWA.
           PERFORM COL-FIO-DPC.
       FIN-CLOSE-FILE.
      *--------------
           EXIT.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
      *
       COPY COLBFACU.
       COPY COLBFNWA.
       COPY COLBFDPC.
       COPY COLBFLMC.
      *
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
      *
       COPY GNSBFMSG.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBFTAB.
      *COPY GNSBFMSC.
      *COPY GNSBBMSC.
      *COPY GNSBBTAB.
