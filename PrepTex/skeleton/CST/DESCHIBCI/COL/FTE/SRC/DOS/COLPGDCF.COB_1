       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGDCF.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    30-Jun-93 02:18 PM.
000000*COMPILED SITE 2000          02.09.98

      ******************************************************************
      *  INTERSOFT                                                     *
      *  MODIFICACION: CONVERTIR REPORT-WRITER A LISTADO, MIGRAR A     *
      *                A COBOL FOR MVS Y CONVERSION 2000               *
      *  FECHA: ABRIL 1998                                             *
      ******************************************************************

      * Programa Listador de Reporte DCF con Sort.


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTDCF         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTDCF         ASSIGN TO        UT-S-RPTDCF.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.

      ***** CRW-COMIENZO
      *    SELECT RPTDCF         ASSIGN TO        UT-S-RPTDCF.
           SELECT RPTDCF         ASSIGN TO        RPTDCF.
      ***** CRW-FINAL

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRDCF.

      ***** CRW-COMIENZO
      *FD  RPTDCF LABEL RECORDS OMITTED
      *    REPORT IS RPT-DCF
      *    .
       FD  RPTDCF
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD STANDARD.

       01  RPTDCF-REG           PIC X(133).
      ***** CRW-FINAL

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRVEN.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWDCF.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       01  WSS-VARI.
      *DAD ini  04-MAR-1992   (Memo.27-02-92 Pto.1.1)
           03 WSS-KEY-IVEN.
              05 WSS-CIC-IOPC                     PIC X(12).
              05 WSS-NUM-IVEN                     PIC 9(03).
           03 WSS-IND-OTRO          VALUE SPACES  PIC X(01).
      *DAD fin  04-MAR-1992
           03 WSS-FEC-FVEI.
              05 WSS-NUM-SVEI                     PIC 9(02).
              05 WSS-NUM-AVEI                     PIC 9(02).
              05 WSS-NUM-MVEI                     PIC 9(02).
              05 WSS-NUM-DVEI                     PIC 9(02).
           03 WSS-FEC-FVEF.
              05 WSS-NUM-SVEF                     PIC 9(02).
              05 WSS-NUM-AVEF                     PIC 9(02).
              05 WSS-NUM-MVEF                     PIC 9(02).
              05 WSS-NUM-DVEF                     PIC 9(02).
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FFIN.
              05 WSS-NUM-DFIN                     PIC 9(02).
              05 WSS-NUM-MFIN                     PIC 9(02).
              05 WSS-NUM-SFIN                     PIC 9(02).
              05 WSS-NUM-AFIN                     PIC 9(02).
           03 WSS-K                 VALUE ZEROS   PIC 9(04).
           03 WSS-COD-COOC          VALUE SPACES  PIC X(06).
           03 WSS-COD-OFOC          VALUE SPACES  PIC X(03).
           03 WSS-COD-CDES          VALUE ZEROS   PIC 9(03).
           03 WSS-SUM-VDES          VALUE ZEROS   PIC 9(09)V99.
           03 WSS-NUM-MFOL          VALUE ZEROS   PIC 9(05).
           03 WSS-NUM-RLEI          VALUE ZEROS   PIC 9(09).
           03 WSS-NUM-RERR          VALUE ZEROS   PIC 9(09).
           03 WSS-NUM-RPRO          VALUE ZEROS   PIC 9(09).
           03 WSS-VAL-SFIN          VALUE ZEROS   PIC 9(11).
           03 WSS-COD-PESO          VALUE SPACES  PIC X(06).
           03 WSS-COD-UFES          VALUE SPACES  PIC X(06).
           03 WSS-NUM-CHBL          VALUE ZEROS   PIC 9(02).
       01  WSS-IND-COOC.
           03 WSS-OCU-COOC OCCURS 1 TO 100 TIMES
              DEPENDING ON WSS-K INDEXED BY WSS-I.
              05 WSS-TAB-COOC                     PIC X(06).

       COPY GNSWGFEC.
       COPY GNSWGCPT.
       COPY GNSWGHOY.
       COPY COLBROPC.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY TABBROFI.
       COPY COLBRDLC.
       COPY TABBRCAM.
       COPY COLBRDCF.
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *>>>>

      ***** CRW-COMIENZO

      *REPORT SECTION.
      *--------------
      *COPY COLRFDCF.
      *    .
      *COPY COLRTDCF.

       01  RPT-VARIABLES.
           02 RPT-VARIABLES-VARIAS.
               03 RPT-VARI-LINEA              PIC 9(03) VALUE 60.
               03 RPT-VARI-PAGE-COUNTER       PIC 9(03).
           02 RPT-VARIABLES-CONTROL.
               03 RPT-CTRL-COD-OFOC           PIC X(03) VALUE SPACES.
               03 RPT-CTRL-COD-COOC           PIC X(06) VALUE SPACES.
           02 RPT-VARIABLES-TOTALES.
               03 RPT-TOT-VAL-SFIN            PIC 9(12)V9(04) VALUE 0.
               03 RPT-TOT-NUM-REG             PIC 9(05)       VALUE 0.

       01  PH-DCF.

           03  PH-DCF-01.
               05  RPT-HEAD-DUSU              PIC X(40).
               05  FILLER                     PIC X(082) VALUE SPACES.
               05  RPT-HEAD-FDMA              PIC 99B99B9999.

           03  PH-DCF-02.
               05  RPT-HEAD-DSIS              PIC X(40).
               05  FILLER                     PIC X(082) VALUE SPACES.
               05  FILLER                     PIC X(6)
                   VALUE 'PAGINA'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-PAGE-COUNTER      PIC ZZZ.

           03  PH-DCF-03.
               05  FILLER                     PIC X(030) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'LISTADO DE DESCUENTOS A FUNCIONARIOS B.'.
               05  FILLER                     PIC X(8)
                   VALUE 'C.I. DEL'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-NUM-DINI          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-HEAD-NUM-MINI          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-HEAD-NUM-SINI          PIC 9(2).
               05  RPT-HEAD-NUM-AINI          PIC 9(2).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  FILLER                     PIC X(2)
                   VALUE 'AL'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-NUM-DFIN          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-HEAD-NUM-MFIN          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-HEAD-NUM-SFIN          PIC 9(2).
               05  RPT-HEAD-NUM-AFIN          PIC 9(2).

           03  PH-DCF-04.
               05  FILLER                     PIC X(118)
                   VALUE ALL '-'.

           03  PH-DCF-05.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'CLASE  SUC                 NOMBRE      '.
               05  FILLER                     PIC X(39)
                   VALUE '                             NUMERO   N'.
               05  FILLER                     PIC X(29)
                   VALUE 'UMERO      FECHA        MONTO'.

           03  PH-DCF-06.
               05  FILLER                     PIC X(39)
                   VALUE 'OPERAC                                 '.
               05  FILLER                     PIC X(39)
                   VALUE '                 R.U.T.      OPERACION '.
               05  FILLER                     PIC X(16)
                   VALUE ' CUOTA      VCTO'.

           03  PH-DCF-07.
               05  FILLER                     PIC X(118)
                   VALUE ALL '-'.

       01  DL-DCF.

           03  DL-DCF-08.
               05  RPT-DETA-COD-COOC          PIC X(6).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-COD-OFOC          PIC X(3).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-GLS-NOMC          PIC X(40).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-NUM-ICLI          PIC X(8).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-VRF-ICLI          PIC X.
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-CIC-IOPC          PIC X(12).
               05  FILLER                     PIC X(003) VALUE SPACES.
               05  RPT-DETA-NUM-NCUO          PIC ZZZ.
               05  FILLER                     PIC X(003) VALUE SPACES.
               05  RPT-DETA-NUM-DVEN          PIC X(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-NUM-MVEN          PIC X(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-NUM-SVEN          PIC X(2).
               05  RPT-DETA-NUM-AVEN          PIC X(2).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-VAL-SFIN          PIC ZZ.ZZZ.ZZZ.ZZ9,9999.

       01  CF-DCF-OPC-COD-OFOC.

           03  CF-DCF-09.
               05  FILLER                     PIC X(51)
                   VALUE ALL '-'.

           03  CF-DCF-10.
               05  FILLER                     PIC X(21)
                   VALUE 'NUMERO DE REGISTROS :'.
               05  FILLER                     PIC X(025) VALUE SPACES.
               05  RPT-CTRL-NUM-NUNO          PIC ZZZZZ.

           03  CF-DCF-11.
               05  FILLER                     PIC X(21)
                   VALUE 'TOTAL DESCUENTO     :'.
               05  FILLER                     PIC X(017) VALUE SPACES.
               05  RPT-CTRL-VAL-SFIN          PIC --.---.--9,99.
               05  RPT-CTRL-RESTO.
                   07  RPT-CTRL-FEC-FVEN      PIC 9(8).

      ***** CRW-FINAL

       PROCEDURE DIVISION.
      *==================

      ***** CRW-COMIENZO
      *DECLARATIVES.
      ***** CRW-FINAL

       DEC-CF-DCF-OPC-COD-COOC SECTION.

      ***** CRW-COMIENZO
      *    USE BEFORE REPORTING CF-DCF-OPC-COD-COOC.
      ***** CRW-FINAL

       INI-CF-DCF-OPC-COD-COOC.
       FIN-CF-DCF-OPC-COD-COOC.
           EXIT.
       DEC-CF-DCF-OPC-COD-OFOC SECTION.

      ***** CRW-COMIENZO
      *    USE BEFORE REPORTING CF-DCF-OPC-COD-OFOC.
      ***** CRW-FINAL

       INI-CF-DCF-OPC-COD-OFOC.
       FIN-CF-DCF-OPC-COD-OFOC.
           EXIT.
       DEC-PH-DCF SECTION.

      ***** CRW-COMIENZO
      *    USE BEFORE REPORTING PH-DCF.
      ***** CRW-FINAL

       INI-PH-DCF.
       FIN-PH-DCF.
           EXIT.

      ***** CRW-COMIENZO
      *END DECLARATIVES.
      ***** CRW-FINAL

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPGDCF' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
      *<<<< INI
      * Abrir Archivos input sort
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-RDC.
           PERFORM SGC-FIO-DBC.
           PERFORM COL-FIO-DLC.
           PERFORM TAB-FIO-CAM.
      * Rutina para Capturar y Validar Parametros
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      * Captura Fecha de vencimiento inicial
           ACCEPT WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FEC INICIO (DDMMAAAA)  : ' WSS-FEC-FINI IN WSS-VARI.
      * Captura Fecha de vencimiento final
           ACCEPT WSS-FEC-FFIN IN WSS-VARI.
           DISPLAY 'FEC FINAL  (DDMMAAAA)  : ' WSS-FEC-FFIN IN WSS-VARI.
      * Captura Oficina
           ACCEPT WSS-COD-OFOC IN WSS-VARI.
           DISPLAY 'OFICINA                : ' WSS-COD-OFOC IN WSS-VARI.
      * Captura Codigo de Descuento
           ACCEPT WSS-COD-CDES IN WSS-VARI.
           DISPLAY 'CODIGO DE DESCUENTO    : ' WSS-COD-CDES IN WSS-VARI.
      * Captura Clase Operacion
       LUP-CLS-OPE.
           ACCEPT WSS-COD-COOC IN WSS-VARI.
           DISPLAY 'Clase de operacion     : ' WSS-COD-COOC IN WSS-VARI.
           IF WSS-COD-COOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-COOC IN WSS-VARI
               GO TO VAL-FEC.
           IF WSS-COD-COOC IN WSS-VARI = '#' OR
              WSS-COD-COOC IN WSS-VARI = '*'
               GO TO VAL-FEC.
      *VAL-TAB Valida Clase de Operacion en TABLAS
           MOVE 'COL'                    TO TAB-COD-SIST
           MOVE 'AUX'                    TO TAB-COD-TTAB IN TAB
           MOVE WSS-COD-COOC IN WSS-VARI TO TAB-COD-CTAB IN TAB
           PERFORM VAL-TAB
           IF MSG-COD-MENS > SPACES
      *DPM ini  15-NOV-1996
               DISPLAY 'CLASE OPERACION ' WSS-COD-COOC
                           ' ' MSG-GLS-DESC IN MSG
               MOVE PGM-SMAL TO PGM-STAT-SRT
      *DPM fin  15-NOV-1996
           ELSE
               ADD 1                         TO WSS-K
               MOVE WSS-COD-COOC IN WSS-VARI TO WSS-TAB-COOC(WSS-K)
               GO TO LUP-CLS-OPE.

           DISPLAY 'FIN CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

       VAL-FEC.
      *VAL-FEC Valida Fecha de vencimiento inicial
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-GE              TO FEC-CHOY.
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.

           IF NOT FEC-STAT-OKS
               DISPLAY 'FECHA VENCIMIENTO INICIAL ' FEC-MENS
               MOVE PGM-SMAL TO PGM-STAT-SRT.

      *VAL-FEC Valida Fecha de vencimiento final
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-GE              TO FEC-CHOY.
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.

           IF NOT FEC-STAT-OKS
               DISPLAY 'FECHA VENCIMIENTO FINAL ' FEC-MENS
               MOVE PGM-SMAL TO PGM-STAT-SRT.

      *CMP-FEC Compara Fecha inicial con Fecha final
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FFIN IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-CMP-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               DISPLAY FEC-MENS
               MOVE PGM-SMAL TO PGM-STAT-SRT.

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY 'CODIGO OFICINA ' WSS-COD-OFOC
                           ' ' MSG-GLS-DESC IN MSG
                   MOVE PGM-SMAL TO PGM-STAT-SRT.

      *VAL-CDES Valida codigo de Descuento
           IF WSS-COD-CDES IN WSS-VARI NOT = 214 AND 572
               DISPLAY 'CODIGO DE DESCUENTO ERRONEO : '
                               WSS-COD-CDES IN WSS-VARI
                       ', DEBE SER 214 O 572'
               MOVE PGM-SMAL TO PGM-STAT-SRT.

           IF PGM-STAT-SRT-MAL
               GO TO FIN-MAIN.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DVEI IN WSS-VARI
                                            WSS-NUM-DINI IN WSS-DCF.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MVEI IN WSS-VARI
                                            WSS-NUM-MINI IN WSS-DCF.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SVEI IN WSS-VARI
                                            WSS-NUM-SINI IN WSS-DCF.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AVEI IN WSS-VARI
                                            WSS-NUM-AINI IN WSS-DCF.
           MOVE WSS-NUM-DFIN IN WSS-VARI TO WSS-NUM-DVEF IN WSS-VARI
                                            WSS-NUM-DFIN IN WSS-DCF.
           MOVE WSS-NUM-MFIN IN WSS-VARI TO WSS-NUM-MVEF IN WSS-VARI
                                            WSS-NUM-MFIN IN WSS-DCF.
           MOVE WSS-NUM-SFIN IN WSS-VARI TO WSS-NUM-SVEF IN WSS-VARI
                                            WSS-NUM-SFIN IN WSS-DCF.
           MOVE WSS-NUM-AFIN IN WSS-VARI TO WSS-NUM-AVEF IN WSS-VARI
                                            WSS-NUM-AFIN IN WSS-DCF.

      *VAL-FEC Obtiene penultimo dia habil de quincena o mes considenado
      *        la fecha de inicio
           IF WSS-COD-CDES IN WSS-VARI = 572
               MOVE 15 TO WSS-NUM-DINI IN WSS-VARI
           ELSE
               IF WSS-NUM-MINI IN WSS-VARI = 2
                   DIVIDE WSS-NUM-AINI IN WSS-VARI BY 4 GIVING FEC-REST
                                                    REMAINDER FEC-REST
                   IF FEC-REST = ZEROS
                       MOVE 29 TO WSS-NUM-DINI IN WSS-VARI
                   ELSE
                       MOVE 28 TO WSS-NUM-DINI IN WSS-VARI
               ELSE
                   MOVE FEC-DMES (WSS-NUM-MINI IN WSS-VARI) TO
                                              WSS-NUM-DINI IN WSS-VARI.

           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
       LUP-PEN-DIA-HBL.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-N               TO FEC-CHOY.
           MOVE FEC-VHBL-S               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-OKS
               ADD 1 TO WSS-NUM-CHBL IN WSS-VARI
               IF WSS-NUM-CHBL IN WSS-VARI = 2
                   MOVE FEC-FECH TO WSS-FEC-FINI IN WSS-VARI
                   GO TO BUS-VLR-UF.
           MOVE 1           TO FEC-NDIA.
           MOVE FEC-RST-DIA TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           GO TO LUP-PEN-DIA-HBL.

       BUS-VLR-UF.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB.
           MOVE CAM-COD-MNAC   TO TAB-CIC-CTAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'        TO MSG-COD-SIST
               MOVE 'CODTVLRNEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-MAIN.
           MOVE TAB-COD-CTAB TO WSS-COD-PESO IN WSS-VARI.

           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB.
           MOVE '0998  '       TO TAB-CIC-CTAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'        TO MSG-COD-SIST
               MOVE 'CODTVLRNEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-MAIN.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-UFES IN WSS-VARI.

           MOVE WSS-NUM-DINI IN WSS-VARI TO CAM-NUM-DCAM IN CAM (1)
           MOVE WSS-NUM-MINI IN WSS-VARI TO CAM-NUM-MCAM IN CAM (1)
           MOVE WSS-NUM-SINI IN WSS-VARI TO CAM-NUM-SCAM IN CAM (1)
           MOVE WSS-NUM-AINI IN WSS-VARI TO CAM-NUM-ACAM IN CAM (1)
           MOVE WSS-NUM-DINI IN WSS-VARI TO CAM-NUM-DCAM IN CAM (2)
           MOVE WSS-NUM-MINI IN WSS-VARI TO CAM-NUM-MCAM IN CAM (2)
           MOVE WSS-NUM-SINI IN WSS-VARI TO CAM-NUM-SCAM IN CAM (2)
           MOVE WSS-NUM-AINI IN WSS-VARI TO CAM-NUM-ACAM IN CAM (2)
           MOVE WSS-COD-UFES IN WSS-VARI TO CAM-COD-VCAM IN CAM (1)
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM (2)
           MOVE FIO-GET-KEY TO FIO-CMND
           PERFORM TAB-FIO-CAM
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR, NO EXISTE VALOR UF AL ' WSS-FEC-FINI
                                                        IN WSS-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-MAIN.

           DISPLAY 'VALOR DE UF SE TOMARA A FECHA : ' WSS-FEC-FINI.
           IF CAM-IND-VIGE IN CAM NOT = 'S'
               DISPLAY 'INDICADOR DE VIGENCIA NO ACTIVO, VALOR : '
                        CAM-IND-VIGE IN CAM
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-MAIN.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
      *>>>>
           SORT GNSSRT
       COPY COLRSDCF.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-VEN.
      *<<<< FIN_FST_INP
           MOVE WSS-FEC-FVEI IN WSS-VARI TO VEN-FEC-FVEN IN VEN
           MOVE 'VEN-FEC-FVEN'           TO FIO-AKEY
           MOVE FIO-GET-NLS              TO FIO-CMND
           PERFORM COL-FIO-VEN
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN VENCIMIENTOS PARA FECHA INICIAL : '
                                               WSS-FEC-FINI IN WSS-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
      *>>>>
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Obtencion y seleccion de registros a procesar
      * Valida que fecha de vencimiento final sea menor que la ingresada
      * parametro
           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FVEF IN WSS-VARI
              GO TO FIN-INP-SRT.

           IF VEN-VAL-SFIN IN VEN NOT > ZEROS
               GO TO NXT-INP-SRT.

      * Busca VEN en OPC
           MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE REGISTRO VEN EN OPC : ' VEN-CIC-IOPC
                                                                IN VEN
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-INP-SRT.

      * Verifica que OPC este activada. Valida que el credito no este ca
           IF (OPC-MSC-STAT IN OPC NOT = 'A') OR
              (OPC-VAL-SCRE IN OPC NOT > ZEROS)
               GO TO NXT-INP-SRT.

      *DAD ini  04-MAR-1992   (Memo.27-02-92 Pto.1.1)
      ** Busca vencimiento en DLC para revisar que no existan
      ** vencimientos morosos
      *     MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
      *     MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
      *     MOVE FIO-GET-KEY TO FIO-CMND.
      *     PERFORM COL-FIO-DLC.
      *     IF FIO-STAT-OKS
      ** Revisa que no existan vencimientos morosos
      *         IF DLC-IND-SCTB IN DLC NOT > SPACES OR
      *            DLC-IND-SCTB IN DLC = 'VIG'
      *             NEXT SENTENCE
      *         ELSE
      *             GO TO NXT-INP-SRT
      *     ELSE
      *         DISPLAY 'NO EXISTE REGISTRO DLC EN VEN : '
      *                  VEN-CIC-IOPC IN VEN
      *         ADD 1 TO WSS-NUM-RERR IN WSS-VARI
      *         GO TO NXT-INP-SRT.
      * Se reemplaza lectura del DLC por el VEN anterior y se examina pa
      * si el esta cancelado
           IF VEN-NUM-IVEN IN VEN NOT > 1
               GO TO CON-LUP-PUT-VEN.
           MOVE 'N'                 TO WSS-IND-OTRO IN WSS-VARI.
           MOVE VEN-KEY-IVEN IN VEN TO WSS-KEY-IVEN IN WSS-VARI.
           SUBTRACT 1 FROM VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = WSS-CIC-IOPC IN WSS-VARI
               IF VEN-VAL-SFIN IN VEN > ZEROES OR
                  VEN-VAL-SCAP IN VEN > ZEROES
                   MOVE 'S' TO WSS-IND-OTRO IN WSS-VARI.
           MOVE WSS-KEY-IVEN IN WSS-VARI TO VEN-KEY-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF WSS-IND-OTRO IN WSS-VARI = 'S'
               GO TO NXT-INP-SRT.
       CON-LUP-PUT-VEN.
           IF VEN-NUM-IVCT IN VEN NOT = OPC-NUM-IVEN IN OPC
               DISPLAY 'ERROR DE INCONSISTENCIA: NO CORRESPONDE EL '
                       'OPC-NUM-IVEN CON EL VENCIMIENTO A CARGAR '
                       'EN CREDITO : '    OPC-CIC-IOPC IN OPC
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-INP-SRT.
      *DAD fin  04-MAR-1992

      * Valida que clase operacion corresponda a alguna ingresada por
      * parametro
           IF WSS-COD-COOC IN WSS-VARI NOT = '*' AND
              WSS-COD-COOC IN WSS-VARI NOT = SPACES
               SET WSS-I TO 1
               SEARCH WSS-OCU-COOC AT END
                      GO TO NXT-INP-SRT
               WHEN WSS-TAB-COOC(WSS-I) = OPC-COD-COOC IN OPC
                     NEXT SENTENCE.

      * Valida que codigo oficina corresponda al ingresado por parametro
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*' AND
              WSS-COD-OFOC IN WSS-VARI NOT = SPACES
               IF OPC-COD-OFOC IN OPC NOT = WSS-COD-OFOC IN WSS-VARI
                   GO TO NXT-INP-SRT.

      * Busca OPC en RDC para obtener rut cliente
           MOVE OPC-CIC-IOPC IN OPC TO RDC-CIC-IOPC IN RDC
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC
           MOVE 'RDC-KEY-IDLC'      TO FIO-AKEY
           MOVE FIO-GET-KEY         TO FIO-CMND
           PERFORM COL-FIO-RDC
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE REGISTRO OPC EN RDC : ' RDC-KEY-IDLC
                                                                IN RDC
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-INP-SRT
           ELSE
      * Con RDC busca nombre  cliente en DBC
               MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC
               MOVE FIO-GET-KEY         TO FIO-CMND
               PERFORM SGC-FIO-DBC
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE CLIENTE EN DBC : '
                                                     DBC-CIC-ICLI IN DBC
                   ADD 1 TO WSS-NUM-RERR IN WSS-VARI
                   GO TO NXT-INP-SRT.

           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
           PERFORM CPT-BLKS
           MOVE CPT-STRN TO SRT-GLS-NOMC IN SRT

           IF OPC-COD-VCOC IN OPC = WSS-COD-UFES IN WSS-VARI
               MULTIPLY VEN-VAL-SFIN IN VEN BY CAM-SGV-VCAM IN CAM
                        GIVING WSS-VAL-SFIN ROUNDED
               ADD  WSS-VAL-SFIN IN WSS-VARI TO WSS-SUM-VDES IN WSS-VARI
               MOVE WSS-VAL-SFIN IN WSS-VARI TO VEN-VAL-SFIN IN VEN
           ELSE
               ADD VEN-VAL-SFIN IN VEN TO WSS-SUM-VDES IN WSS-VARI.

           IF OPC-IND-VSTR IN OPC = 'S'
               MOVE VEN-NUM-IVCT IN VEN TO SRT-NUM-NCUO IN SRT
           ELSE
               MOVE VEN-NUM-IVEN IN VEN TO SRT-NUM-NCUO IN SRT.

           ADD  1                   TO WSS-NUM-MFOL IN WSS-VARI.
           MOVE 1                   TO SRT-NUM-NUNO IN SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMDCF.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
      *<<<< FIN_INP
      * Cerrar Archivos input sort
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-RDC.
           PERFORM SGC-FIO-DBC.
           PERFORM COL-FIO-DLC.
           PERFORM TAB-FIO-CAM.
      *>>>>
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-VEN.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
      *<<<< INI_OUT
      * Abrir Archivos de salida y grabar
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-DCF.
      *>>>>
           OPEN OUTPUT RPTDCF.
           CALL 'GNSPLHDR' USING RPT-VARI.

      ***** CRW-COMIENZO
      *    INITIATE RPT-DCF.
      ***** CRW-FINAL

           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.

      ***** CRW-COMIENZO
           MOVE OPC-COD-OFOC IN SRT TO RPT-CTRL-COD-OFOC
           MOVE OPC-COD-COOC IN SRT TO RPT-CTRL-COD-COOC.
      ***** CRW-FINAL

      *<<<< FST_OUT
      * Grabar registro cabecera
      *DAD ini  24-JUL-1991
           MOVE WSS-COD-CDES IN WSS-VARI TO DCF-NUM-CFOL IN DCF
      *DAD fin  24-JUL-1991
           MOVE ZEROS                    TO DCF-NUM-CLIN IN DCF
           MOVE ZEROS                    TO DCF-VAL-SBAS IN DCF
           MOVE ZEROS                    TO DCF-VAL-IBAS IN DCF
           MOVE WSS-NUM-MFOL IN WSS-VARI TO DCF-NUM-MFOL IN DCF
           MOVE ZEROS                    TO DCF-COD-EFOL IN DCF
           MOVE WSS-SUM-VDES IN WSS-VARI TO DCF-VAL-TDES IN DCF
           MOVE WSS-SUM-VDES IN WSS-VARI TO DCF-VAL-IDES IN DCF
           MOVE 'COLO'                   TO DCF-COD-CSIS IN DCF
           MOVE SPACES                   TO DCF-COD-ACTU IN DCF
           MOVE FIO-PUT TO FIO-CMND
           PERFORM COL-FIO-DCF
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL GRABAR CABECERA DCF : ' DCF
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-OUT-SRT.
           MOVE ZEROS                    TO DCF-NUM-NLIN IN DCF.
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      * Obtencion de registros SORT
      *DAD ini  24-JUL-1991
           MOVE WSS-COD-CDES IN WSS-VARI TO DCF-NUM-NFOL IN DCF.
      *DAD fin  24-JUL-1991
           ADD 1                         TO DCF-NUM-NLIN IN DCF.
           MOVE 'I'                      TO DCF-IND-TMOV IN DCF.
           MOVE DBC-NUM-ICLI IN SRT      TO DCF-NUM-ICLI IN DCF.
           MOVE DBC-VRF-ICLI IN SRT      TO DCF-VRF-ICLI IN DCF.
           MOVE WSS-COD-CDES IN WSS-VARI TO DCF-COD-CDES IN DCF.
           MOVE ZEROS                    TO DCF-NUM-NPRI IN DCF.
           MOVE ZEROS                    TO DCF-NUM-BCUO IN DCF.
           MOVE VEN-VAL-SFIN IN SRT      TO DCF-VAL-SFIN IN DCF.
           MOVE ZEROS                    TO DCF-NUM-CMOV IN DCF.
           MOVE SPACES                   TO DCF-GLS-DISP IN DCF.
           MOVE OPC-CIC-IOPC IN SRT      TO DCF-CIC-IOPC IN DCF.
           MOVE VEN-NUM-DVEN IN SRT      TO DCF-NUM-DVEN IN DCF.
           MOVE VEN-NUM-MVEN IN SRT      TO DCF-NUM-MVEN IN DCF.
           MOVE VEN-NUM-SVEN IN SRT      TO DCF-NUM-SVEN IN DCF.
           MOVE VEN-NUM-AVEN IN SRT      TO DCF-NUM-AVEN IN DCF.
           MOVE SRT-NUM-NCUO IN SRT      TO DCF-NUM-IVEN IN DCF.

           MOVE FIO-PUT TO FIO-CMND
           PERFORM COL-FIO-DCF
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL GRABAR DCF : ' DCF
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-OUT-SRT.
           ADD 1 TO WSS-NUM-RPRO IN WSS-VARI.
      *>>>>
       GEN-OUT-SRT.

      ***** CRW-COMIENZO
      *    GENERATE DL-DCF.

           MOVE RPT-DUSU                       TO RPT-HEAD-DUSU
           MOVE RPT-FDMA                       TO RPT-HEAD-FDMA
           MOVE RPT-DSIS                       TO RPT-HEAD-DSIS
           MOVE WSS-NUM-DINI IN WSS-DCF        TO RPT-HEAD-NUM-DINI
           MOVE WSS-NUM-MINI IN WSS-DCF        TO RPT-HEAD-NUM-MINI
           MOVE WSS-NUM-SINI IN WSS-DCF        TO RPT-HEAD-NUM-SINI
           MOVE WSS-NUM-AINI IN WSS-DCF        TO RPT-HEAD-NUM-AINI
           MOVE WSS-NUM-DFIN IN WSS-DCF        TO RPT-HEAD-NUM-DFIN
           MOVE WSS-NUM-MFIN IN WSS-DCF        TO RPT-HEAD-NUM-MFIN
           MOVE WSS-NUM-SFIN IN WSS-DCF        TO RPT-HEAD-NUM-SFIN
           MOVE WSS-NUM-AFIN IN WSS-DCF        TO RPT-HEAD-NUM-AFIN
           MOVE OPC-COD-COOC IN SRT            TO RPT-DETA-COD-COOC
           MOVE OPC-COD-OFOC IN SRT            TO RPT-DETA-COD-OFOC
           MOVE SRT-GLS-NOMC IN SRT            TO RPT-DETA-GLS-NOMC
           MOVE DBC-NUM-ICLI IN SRT            TO RPT-DETA-NUM-ICLI
           MOVE DBC-VRF-ICLI IN SRT            TO RPT-DETA-VRF-ICLI
           MOVE OPC-CIC-IOPC IN SRT            TO RPT-DETA-CIC-IOPC
           MOVE SRT-NUM-NCUO IN SRT            TO RPT-DETA-NUM-NCUO
           MOVE VEN-NUM-DVEN IN SRT            TO RPT-DETA-NUM-DVEN
           MOVE VEN-NUM-MVEN IN SRT            TO RPT-DETA-NUM-MVEN
           MOVE VEN-NUM-SVEN IN SRT            TO RPT-DETA-NUM-SVEN
           MOVE VEN-NUM-AVEN IN SRT            TO RPT-DETA-NUM-AVEN
           MOVE VEN-VAL-SFIN IN SRT            TO RPT-DETA-VAL-SFIN
           MOVE VEN-FEC-FVEN IN SRT            TO RPT-CTRL-FEC-FVEN

           IF RPT-VARI-LINEA > 59 OR RPT-VARI-PAGE-COUNTER = 0 OR
              OPC-COD-OFOC IN SRT NOT = RPT-CTRL-COD-OFOC      OR
              OPC-COD-COOC IN SRT NOT = RPT-CTRL-COD-COOC
              IF OPC-COD-OFOC IN SRT NOT = RPT-CTRL-COD-OFOC   OR
                 OPC-COD-COOC IN SRT NOT = RPT-CTRL-COD-COOC
                 MOVE OPC-COD-OFOC IN SRT TO RPT-CTRL-COD-OFOC
                 MOVE OPC-COD-COOC IN SRT TO RPT-CTRL-COD-COOC
                 MOVE RPT-TOT-NUM-REG TO RPT-CTRL-NUM-NUNO
                 MOVE RPT-TOT-VAL-SFIN TO RPT-CTRL-VAL-SFIN
                 MOVE SPACES           TO RPT-CTRL-RESTO
                 WRITE RPTDCF-REG FROM  CF-DCF-09
                 WRITE RPTDCF-REG FROM  CF-DCF-10
                 WRITE RPTDCF-REG FROM  CF-DCF-11
                 MOVE 0 TO RPT-TOT-NUM-REG RPT-TOT-VAL-SFIN
                 ADD 3 TO RPT-VARI-LINEA
              END-IF
              ADD 1 TO RPT-VARI-PAGE-COUNTER
              MOVE RPT-VARI-PAGE-COUNTER TO RPT-HEAD-PAGE-COUNTER
              MOVE SPACES TO RPTDCF-REG
              WRITE RPTDCF-REG AFTER PAGE
              WRITE RPTDCF-REG FROM  PH-DCF-01
              WRITE RPTDCF-REG FROM  PH-DCF-02
              WRITE RPTDCF-REG FROM  PH-DCF-03 AFTER 2
              WRITE RPTDCF-REG FROM  PH-DCF-04
              WRITE RPTDCF-REG FROM  PH-DCF-05
              WRITE RPTDCF-REG FROM  PH-DCF-06
              WRITE RPTDCF-REG FROM  PH-DCF-07
              MOVE 10 TO RPT-VARI-LINEA
           END-IF.
           ADD  SRT-NUM-NUNO IN SRT            TO RPT-TOT-NUM-REG
           ADD  VEN-VAL-SFIN IN SRT            TO RPT-TOT-VAL-SFIN
           WRITE RPTDCF-REG FROM  DL-DCF-08.
           ADD 1 TO RPT-VARI-LINEA.
      ***** CRW-FINAL

       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      *<<<< FIN_OUT
      * Cerrar Archivos de salida
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-DCF.
           DISPLAY 'ESTADISTICAS DE PROCESO'.
           DISPLAY '============ == ======='.
           DISPLAY 'REGISTROS LEIDOS     : ' WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'REGISTROS PROCESADOS : ' WSS-NUM-RPRO IN WSS-VARI.
           DISPLAY 'REGISTROS ERRONEOS   : ' WSS-NUM-RERR IN WSS-VARI.
      *>>>>

      ***** CRW-COMIENZO
      *    TERMINATE RPT-DCF.
           MOVE RPT-TOT-NUM-REG TO RPT-CTRL-NUM-NUNO
           MOVE RPT-TOT-VAL-SFIN TO RPT-CTRL-VAL-SFIN
           MOVE SPACES           TO RPT-CTRL-RESTO
           WRITE RPTDCF-REG FROM  CF-DCF-09
           WRITE RPTDCF-REG FROM  CF-DCF-10
           WRITE RPTDCF-REG FROM  CF-DCF-11.
      ***** CRW-FINAL

           CLOSE RPTDCF.
       EXT-OUT-SRT.
           EXIT.
       COPY COLBFVEN.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY GNSBBMSG.
       COPY GNSBVTAB.
       COPY TABBVOFI.
       COPY COLBFDLC.
       COPY TABBFCAM.
       COPY COLBFOPC.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY TABBFOFI.
       COPY COLBFDCF.
      *>>>>

