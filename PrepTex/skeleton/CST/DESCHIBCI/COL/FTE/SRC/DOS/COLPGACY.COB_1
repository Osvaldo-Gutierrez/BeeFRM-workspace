       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGACY.
       AUTHOR.          BEE TEAM (EX-CONSIST).
       DATE-WRITTEN.    23-ENE-1997.
      *
      * Lee Archivo MAN, activando CAN para movimientos OK.
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES. DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY GNSBRERR.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ERR-REQA==.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY GNSWGPES.
       COPY GNSWVIDD.

       COPY COLBROPC.
       COPY COLBRCAN.
       COPY COLBRDLC.
       COPY COLBRRCV.
       COPY COLBRVEN.
       COPY COLBRVEN REPLACING ==VEN== BY ==VEN-BKP==.
       COPY COLBRTOC.
       COPY COLBREVC.
       COPY COLBRRDT.
       COPY COLBRICG.
       COPY COLBRMAN.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MAN-REQA==.
       COPY COLWGVEN.
       COPY COLWICAN.

       COPY TABBRCAM.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *
       01  SCR-VARI.
           03 SCR-CMND               VALUE SPACES PIC X(03).
      *
       01  FRM-VARI.
           03  FRM-MENS.
               05 FRM-MEN1           VALUE SPACES PIC X(40).
               05 FRM-MEN2           VALUE SPACES PIC X(40).
      *
       01  WSS-FEC-FTER-COMP-3       PIC 9(08) COMP-3.
      *
       01  WSS-VARI.
      *ALD-BCI-INI/22-11-1998
           03 WSS-FEC-FDVA.
              05 WSS-NUM-SDVA        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-ADVA        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MDVA        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DDVA        VALUE ZEROS  PIC 9(02).
      *ALD-BCI-INI/22-11-1998
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AINI        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-STER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-ATER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MTER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DTER        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FTER-R REDEFINES
              WSS-FEC-FTER                        PIC 9(08).
           03  WSS-IDLC.
               05  WSS-VEC-IDLC OCCURS 500.
                   07  WSS-SGV-CAPD  COMP-3       PIC S9(11)V9(04).
                   07  WSS-SGV-FINA  COMP-3       PIC S9(11)V9(04).
           03  WSS-LIM-IDLC          VALUE 500    PIC 9(03).
           03  WSS-NUM-IDLC          VALUE ZEROS  PIC 9(04).
           03  WSS-NUM-VCVG          VALUE ZEROS  PIC S9(03).
           03  WSS-NUM-VCCM          VALUE ZEROS  PIC S9(03).
           03  WSS-NUM-VCCO          VALUE ZEROS  PIC S9(03).
           03  WSS-NUM-VCCV          VALUE ZEROS  PIC S9(03).
           03  WSS-NUM-IEVC          VALUE ZEROS  PIC 9(03).
           03  WSS-AUX-IVEN          VALUE ZEROS  PIC 9(03).
           03  WSS-IND-SCTB                        PIC X(03).
           03  WSS-IND-SCBZ                        PIC X(05).
           03  WSS-IND-EXIS          VALUE 'N'    PIC X(01).
           03  WSS-VAL-CAPI-GENE     COMP-3       PIC 9(11)V9(04).
           03  WSS-VAL-VENC  COMP-3  VALUE ZEROS  PIC 9(11)V9(04).
           03  WSS-FEC-FAMT.
               05  WSS-NUM-SAMT      VALUE ZEROS  PIC 9(02).
               05  WSS-NUM-AAMT      VALUE ZEROS  PIC 9(02).
               05  WSS-NUM-MAMT      VALUE ZEROS  PIC 9(02).
               05  WSS-NUM-DAMT      VALUE ZEROS  PIC 9(02).
           03  WSS-VAL-SMO1  COMP-3  VALUE ZEROS  PIC S9(11)V9(5).
           03  WSS-VAL-SMO2  COMP-3  VALUE ZEROS  PIC S9(11)V9(5).
           03  WSS-IND-CIER          VALUE SPACES PIC X(01).
           03  WSS-RCAN-SACT         VALUE ZEROES PIC 9(04).
           03  WSS-RCAN-NACT         VALUE ZEROES PIC 9(04).
           03  WSS-ROPC-SACT         VALUE ZEROES PIC 9(04).
           03  WSS-ROPC-NACT         VALUE ZEROES PIC 9(04).
           03  WSS-CMSG              VALUE SPACES PIC X(12).
           03  WSS-MENS.
               05 WSS-MEN2           VALUE SPACES PIC X(40).
           03  WSS-GLS-POPC                       PIC X(40).
           03  WSS-DCO-POPC                       PIC X(12).
           03  WSS-GLS-PCAN                       PIC X(40).
           03  WSS-DCO-PCAN                       PIC X(12).
           03  WSS-NUM-PASO          VALUE ZEROES PIC 9(01).
           03  WSS-MAXV              VALUE ZEROS  PIC 9(03).
           03  WSS-MINV              VALUE ZEROS  PIC 9(03).
           03  WSS-MAXD              VALUE ZEROS  PIC 9(03).
           03  WSS-MIND              VALUE ZEROS  PIC 9(03).
           03  WSS-IND-DISP          VALUE SPACES PIC X(01).

      *
       01  PGM-VARI.
           03 PGM-STAT.
              05 PGM-STAT-VEN           VALUE '23'      PIC X(02).
                 88 PGM-STAT-VEN-OKS    VALUE '00'.
                 88 PGM-STAT-VEN-NUL    VALUE '  '.


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGACY' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OBT-PARAM.
           PERFORM OPEN-FILE.
           PERFORM LEE-REG-MAN.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
      *COPY GNSBGGBK.
           PERFORM GNS-PRO-END.

      ******************************************************************
       OPEN-FILE SECTION.
      ******************
       INI-OPEN-FILE.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-EVC.
           PERFORM COL-FIO-RDT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-TOC.
           PERFORM COL-FIO-MAN.
           IF NOT FEC-STAT-OKS
              DISPLAY 'ERROR ARCHIVO MAN ' FIO-STAT
               PERFORM GNS-PRO-ABT.
      *
       FIN-OPEN-FILE.
           EXIT.

      ******************************************************************
       OBT-PARAM SECTION.
      ******************
       INI-OBT-PARAM.
      *Captura Fecha de Proceso
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO ( DDMMAAAA ) : ' WSS-FEC-FPRO
                                                      IN WSS-VARI.
           ACCEPT  WSS-IND-DISP IN WSS-VARI.
           DISPLAY 'INDICADOR DE DISPLAY          : ' WSS-IND-DISP
                                                      IN WSS-VARI.
           DISPLAY 'Fin    Captura    de Parametros'.
           DISPLAY 'Inicio Validacion de Parametros'.

      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
      *    MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT.

           IF WSS-IND-DISP NOT > SPACES
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI
           ELSE
           IF NOT ( WSS-IND-DISP = 'S' OR 'N' )
               DISPLAY 'INDICADOR DE DISPLAY DEBE SER: S O N'
               PERFORM GNS-PRO-ABT.
           DISPLAY WSS-IND-DISP IN WSS-VARI.

           DISPLAY 'Fin Validacion de Parametros'.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO WSS-FEC-FINI IN WSS-VARI.
       BUS-DIA-HBL.
      *Buscar el dia anterior
           MOVE WSS-FEC-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
      *Ver si el dia resultante es feriado
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
                MOVE FEC-FECH TO WSS-FEC-FINI IN WSS-VARI
                GO TO BUS-DIA-HBL.
           DISPLAY 'FECHA INICIO  : ' WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FECHA TERMINO : ' WSS-FEC-FPRO IN WSS-VARI.

      * Obtener mensajes parametricos de proceso para MVO desde COL o CA
      *GET-MSG Busca mensaje en tabla
           MOVE 'COL'        TO MSG-COD-SIST.
           MOVE 'ACAPROCOPC' TO MSG-COD-MENS.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC IN MSG TO WSS-GLS-POPC IN WSS-VARI.
           MOVE MSG-GLS-DCOR IN MSG TO WSS-DCO-POPC IN WSS-VARI.

      *GET-MSG Busca mensaje en tabla
           MOVE 'COL'        TO MSG-COD-SIST.
           MOVE 'ACAPROCCAN' TO MSG-COD-MENS.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC IN MSG TO WSS-GLS-PCAN IN WSS-VARI.
           MOVE MSG-GLS-DCOR IN MSG TO WSS-DCO-PCAN IN WSS-VARI.
       FIN-OBT-PARAM.
           EXIT.

      ******************************************************************
       LEE-REG-MAN SECTION.
      ********************
       INI-LEE-REG-MAN.
      * Leo el primer registro del archivo MAN
           MOVE FIO-GET-FST TO FIO-CMND.
       LUP-LEE-REG-MAN.
           PERFORM COL-FIO-MAN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LEE-REG-MAN.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'FECHA ING. MOV.: 'MAN-FEC-FIMV IN MAN
               DISPLAY 'FECHA PROCESO  : 'WSS-FEC-FTER IN WSS-VARI.

           MOVE WSS-FEC-FTER-R IN WSS-VARI TO WSS-FEC-FTER-COMP-3.
      *    IF MAN-FEC-FIMV IN MAN > WSS-FEC-FTER IN WSS-VARI
           IF MAN-FEC-FIMV IN MAN > WSS-FEC-FTER-COMP-3
               GO TO FIN-LEE-REG-MAN.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'SISTEMA ORIGEN MOV.: 'MAN-COD-TORG IN MAN.

           IF MAN-COD-TORG IN MAN NOT = 'COL'
               GO TO NXT-LEE-REG-MAN.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'COD. MOTIVO RECAHZO: 'MAN-COD-MRCH IN MAN.

           IF MAN-COD-MRCH IN MAN NOT = '00'
               GO TO NXT-LEE-REG-MAN.

      * Limpia el codigo de mensaje
           MOVE SPACES TO WSS-CMSG.
           PERFORM PRO-ACT-CAN.
      *
      * Analiza codigo de mensaje para hacer CHK-PNT o BCK-OUT
           IF WSS-CMSG > SPACES
               GO TO BKO-LEE-REG-MAN.
      *
           IF WSS-CMSG > SPACES
               GO TO BKO-LEE-REG-MAN.
           MOVE FIO-CHK-PNT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           GO TO NXT-LEE-REG-MAN.
       BKO-LEE-REG-MAN.
           MOVE 'COL'    TO MSG-COD-SIST.
           MOVE WSS-CMSG TO MSG-COD-MENS.
           PERFORM GET-MSG.
           DISPLAY MSG-GLS-DESC WSS-MEN2.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *
       NXT-LEE-REG-MAN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-LEE-REG-MAN.
       FIN-LEE-REG-MAN.
           EXIT.
      ******************************************************************
      *Proceso de activacion de una Cancelacion
       PRO-ACT-CAN SECTION.
      ********************
       INI-PRO-ACT-CAN.
      *Lectura de OPC y carga de los campos buffer ( CAN-FLD )
           MOVE MAN-CIC-IOPC IN MAN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NEX'        TO WSS-CMSG
               MOVE OPC-CIC-IOPC IN OPC TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
      *
           PERFORM PUT-OPC-CAN.
      *Lectura de CAN y carga de los campos buffer ( CAN-FLD )
           MOVE MAN-CIC-IOPC IN MAN TO CAN-CIC-IOPC IN CAN.
           MOVE MAN-NUM-ICAN IN MAN TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NEX'        TO WSS-CMSG
               MOVE CAN-KEY-ICAN IN CAN TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
      *
           IF CAN-MSC-STAT IN CAN = 'A'
               GO TO FIN-PRO-ACT-CAN.
      *
           PERFORM PUT-CAN-CAN.
           PERFORM OBT-TSA-OPC.
           PERFORM OBT-TSA-CAN.
      *
      *Lectura de RCV y carga de los campos buffer ( CAN-FLD )
           MOVE CAN-CIC-IOPC IN CAN TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN TO RCV-NUM-ICAN IN RCV.
           MOVE SPACES              TO RCV-CIC-IOPV IN RCV.
           MOVE ZEROES              TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS AND
                    CAN-CIC-IOPC IN CAN = RCV-CIC-IOPC IN RCV AND
                    CAN-NUM-ICAN IN CAN = RCV-NUM-ICAN IN RCV )
               MOVE 'RCVINEXICAN:'      TO WSS-CMSG
               MOVE RCV-KEY-ICAN IN RCV TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
      *
      *INI-JGM 28-07-1999.
      *Valida que el vencimiento asociado a la cancelacion,
      *se a el mismo a rebajar.
      *
           MOVE OPC-CIC-IOPC IN OPC     TO VEN-CIC-IOPC IN VEN.
           IF OPC-IND-VSTR IN OPC = 'S'
               MOVE 1                   TO VEN-NUM-IVEN IN VEN
           ELSE
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN.
      *
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENINEXPAG:'       TO WSS-CMSG
               MOVE OPC-CIC-IOPC IN OPC TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
      *
           IF RCV-NUM-IVCT IN RCV NOT = VEN-NUM-IVCT IN VEN
               MOVE 'VENPAGADO :'       TO WSS-CMSG
               MOVE RCV-KEY-ICAN IN RCV TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
      *FIN-JGM 28-07-1999.
      *
      *
           PERFORM PUT-RCV-CAN.
      *
      *Lectura de VEN y carga de los campos buffer ( CAN-FLD )
      *
           MOVE RCV-CIC-IOPV IN RCV     TO VEN-CIC-IOPC IN VEN.
           IF RCV-NUM-IVCT IN RCV > ZEROES
               MOVE RCV-NUM-IVCT IN RCV TO VEN-NUM-IVEN IN VEN
           ELSE
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN.
      *
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM COL-OBT-VEN
               IF VEN-STAT-OKS
                   MOVE '00' TO FIO-STAT
               ELSE
                   MOVE '23' TO FIO-STAT
           ELSE
               PERFORM COL-FIO-VEN.
      *
           IF NOT FIO-STAT-OKS
               MOVE 'VENINEXRCV:'       TO WSS-CMSG
               MOVE RCV-KEY-IRCV IN RCV TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
      *
           PERFORM PUT-VEN-CAN.
      *
      *Despues de tener buffer cargado ( CAN-FLD ) realiza el mismo
      *proceso del programa interactivo ( ver CMN-ACC )
      *
           MOVE 'CAN' TO SCR-CMND.
           PERFORM INI-CAN.
      *ALD-BCI-INI/22-11-1998
      *    IF CAN-MSC-FCAN IN CAN = 'T' AND
      *       CAN-MSC-ECAN IN CAN = 'T'
           IF ( CAN-MSC-FCAN IN CAN = 'T' AND
                CAN-MSC-ECAN IN CAN = 'T' ) OR
              ( CAN-MSC-FCAN IN CAN = 'T' AND
                CAN-MSC-ECAN IN CAN = 'E' AND
                OPC-NUM-VCOR IN OPC = 1 )
      *ALD-BCI-FIN/22-11-1998
               PERFORM TOT-CUO
           ELSE
               PERFORM PRO-RCV.
      *
           IF MSG-COD-MENS > SPACES
               MOVE MSG-COD-MENS TO WSS-CMSG
               MOVE FRM-MEN2     TO WSS-MEN2
               GO TO ERR-PRO-ACT-CAN.
      *
      *ALD-BCI-INI/22-11-1998
           MOVE CAN-FEC-FDVA IN CAN TO WSS-FEC-FDVA.
      *ALD-BCI-FIN/22-11-1998
           MOVE MAN-CIC-IOPC IN MAN TO CAN-CIC-IOPC IN CAN.
           MOVE MAN-NUM-ICAN IN MAN TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NOUPD'      TO WSS-CMSG
               MOVE CAN-KEY-ICAN IN CAN TO WSS-MEN2
               GO TO ERR-PRO-ACT-CAN.
      *
      *ALD-BCI-INI/22-11-1998
           MOVE WSS-FEC-FDVA TO CAN-FEC-FDVA IN CAN.
      *ALD-BCI-FIN/22-11-1998
           MOVE 'A' TO CAN-MSC-STAT IN CAN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-RCAN-SACT
               GO TO FIN-PRO-ACT-CAN.
      *
           MOVE 'CAN    NOMOD'      TO WSS-CMSG.
           MOVE CAN-KEY-ICAN IN CAN TO WSS-MEN2.
       ERR-PRO-ACT-CAN.
           ADD 1 TO WSS-RCAN-NACT.
       FIN-PRO-ACT-CAN.
           EXIT.

      ******************************************************************
       OBT-TSA-OPC SECTION.
      ********************
       INI-OBT-TSA-OPC.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPC IN ICG.
           MOVE ZEROES              TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES              TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF FIO-STAT-OKS AND
              ICG-CIC-IOPC IN ICG = CAN-CIC-IOPC IN CAN AND
              ICG-NUM-ICAN IN ICG = ZEROES              AND
              ICG-CIC-IOPV IN ICG = SPACES              AND
              ICG-NUM-IVCT IN ICG = ZEROES              AND
              ICG-IND-TTAS IN ICG = 'INT'
               IF ICG-COD-CVTI IN ICG > SPACES
                   MOVE ICG-SGV-SPRD IN ICG TO FRM-SGV-TINT IN CAN-FLD
               ELSE
                   MOVE ICG-SGV-TINT IN ICG TO FRM-SGV-TINT IN CAN-FLD
           ELSE
               MOVE ZEROES TO FRM-SGV-TINT IN CAN-FLD.
       FIN-OBT-TSA-OPC.
           EXIT.

      ******************************************************************
       OBT-TSA-CAN SECTION.
      ********************
       INI-OBT-TSA-CAN.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPC IN ICG.
           MOVE CAN-NUM-ICAN IN CAN TO ICG-NUM-ICAN IN ICG.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF FIO-STAT-OKS AND
              ICG-CIC-IOPC IN ICG = CAN-CIC-IOPC IN CAN AND
              ICG-NUM-ICAN IN ICG = CAN-NUM-ICAN IN CAN AND
              ICG-CIC-IOPV IN ICG = CAN-CIC-IOPC IN CAN AND
              ICG-NUM-IVCT IN ICG = ZEROES              AND
              ICG-IND-TTAS IN ICG = 'INT'
               MOVE ICG-SGV-TINT IN ICG TO ICG-SGV-TINT IN CAN-FLD
           ELSE
               MOVE ZEROES TO ICG-SGV-TINT IN CAN-FLD.
       FIN-OBT-TSA-CAN.
           EXIT.
      ******************************************************************
       PUT-OPC-CAN SECTION.
      ********************
       INI-PUT-OPC-CAN.
           MOVE OPC-MSC-TVOC IN OPC TO OPC-MSC-TVOC IN CAN-FLD.
           MOVE OPC-COD-TOOC IN OPC TO OPC-COD-TOOC IN CAN-FLD.
           MOVE OPC-COD-OFOC IN OPC TO OPC-COD-OFOC IN CAN-FLD.
           MOVE OPC-COD-AOOC IN OPC TO OPC-COD-AOOC IN CAN-FLD.
           MOVE OPC-NUM-VCVG IN OPC TO OPC-NUM-VCVG IN CAN-FLD.
           MOVE OPC-COD-VCOC IN OPC TO OPC-COD-VCOC IN CAN-FLD.
           MOVE OPC-VAL-SCRE IN OPC TO OPC-VAL-SCRE IN CAN-FLD.
       FIN-PUT-OPC-CAN.
           EXIT.

      ******************************************************************
       PUT-CAN-CAN SECTION.
      ********************
       INI-PUT-CAN-CAN.
           MOVE CAN-CAI-IOPC IN CAN TO CAN-CAI-IOPC IN CAN-FLD.
           MOVE CAN-IIC-IOPC IN CAN TO CAN-IIC-IOPC IN CAN-FLD.
           MOVE CAN-NUM-ICAN IN CAN TO CAN-NUM-ICAN IN CAN-FLD.
           MOVE CAN-COD-RCAN IN CAN TO CAN-COD-RCAN IN CAN-FLD.
           MOVE CAN-MSC-STAT IN CAN TO CAN-MSC-STAT IN CAN-FLD.
           MOVE CAN-MSC-CANC IN CAN TO CAN-MSC-CANC IN CAN-FLD.
           MOVE CAN-NUM-DOUT IN CAN TO CAN-NUM-DOUT IN CAN-FLD.
           MOVE CAN-NUM-MOUT IN CAN TO CAN-NUM-MOUT IN CAN-FLD.
           MOVE CAN-NUM-SOUT IN CAN TO CAN-NUM-SOUT IN CAN-FLD.
           MOVE CAN-NUM-AOUT IN CAN TO CAN-NUM-AOUT IN CAN-FLD.
           MOVE CAN-NUM-DCAN IN CAN TO CAN-NUM-DCAN IN CAN-FLD.
           MOVE CAN-NUM-MCAN IN CAN TO CAN-NUM-MCAN IN CAN-FLD.
           MOVE CAN-NUM-SCAN IN CAN TO CAN-NUM-SCAN IN CAN-FLD.
           MOVE CAN-NUM-ACAN IN CAN TO CAN-NUM-ACAN IN CAN-FLD.
           MOVE CAN-NUM-DEGR IN CAN TO CAN-NUM-DEGR IN CAN-FLD.
           MOVE CAN-NUM-MEGR IN CAN TO CAN-NUM-MEGR IN CAN-FLD.
           MOVE CAN-NUM-SEGR IN CAN TO CAN-NUM-SEGR IN CAN-FLD.
           MOVE CAN-NUM-AEGR IN CAN TO CAN-NUM-AEGR IN CAN-FLD.
           MOVE CAN-COD-OFCA IN CAN TO CAN-COD-OFCA IN CAN-FLD.
           MOVE CAN-SGV-GAST IN CAN TO CAN-SGV-GAST IN CAN-FLD.
           MOVE CAN-SGV-TREN IN CAN TO CAN-SGV-TREN IN CAN-FLD.
           MOVE CAN-SGV-TPAG IN CAN TO CAN-SGV-TPAG IN CAN-FLD.
           MOVE CAN-SGV-DIFE IN CAN TO CAN-SGV-DIFE IN CAN-FLD.
       FIN-PUT-CAN-CAN.
           EXIT.

      ******************************************************************
       PUT-RCV-CAN SECTION.
      ********************
       INI-PUT-RCV-CAN.
           MOVE RCV-SGV-CAPI IN RCV TO RCV-SGV-CAPI IN CAN-FLD.
           MOVE RCV-SGV-FINA IN RCV TO RCV-SGV-FINA IN CAN-FLD.
       FIN-PUT-RCV-CAN.
           EXIT.

      ******************************************************************
       PUT-VEN-CAN SECTION.
      ********************
       INI-PUT-VEN-CAN.
           MOVE VEN-NUM-DVEN IN VEN TO VEN-NUM-DVEN IN CAN-FLD.
           MOVE VEN-NUM-MVEN IN VEN TO VEN-NUM-MVEN IN CAN-FLD.
           MOVE VEN-NUM-SVEN IN VEN TO VEN-NUM-SVEN IN CAN-FLD.
           MOVE VEN-NUM-AVEN IN VEN TO VEN-NUM-AVEN IN CAN-FLD.
           MOVE VEN-NUM-DIMV IN VEN TO VEN-NUM-DIMV IN CAN-FLD.
           MOVE VEN-NUM-MIMV IN VEN TO VEN-NUM-MIMV IN CAN-FLD.
           MOVE VEN-NUM-SIMV IN VEN TO VEN-NUM-SIMV IN CAN-FLD.
           MOVE VEN-NUM-AIMV IN VEN TO VEN-NUM-AIMV IN CAN-FLD.
           MOVE VEN-IND-SCTB IN VEN TO VEN-IND-SCTB IN CAN-FLD.
           MOVE VEN-VAL-SCAP IN VEN TO VEN-VAL-SCAP IN CAN-FLD.
           MOVE VEN-VAL-SFIN IN VEN TO VEN-VAL-SFIN IN CAN-FLD.
           MOVE VEN-NUM-IVEN IN VEN TO VEN-NUM-IVEN IN CAN-FLD.
           MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVCT IN CAN-FLD.
       FIN-PUT-VEN-CAN.
           EXIT.

      ******************************************************************
       INI-CAN SECTION.
      ****************
       INI-INI-CAN.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE ZEROES TO WSS-NUM-VCVG IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-VCCM IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-VCCO IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-VCCV IN WSS-VARI.
           PERFORM RST-DLC VARYING WSS-NUM-IDLC FROM 1 BY 1
                           UNTIL   WSS-NUM-IDLC > WSS-LIM-IDLC.
       FIN-INI-CAN.
           EXIT.

      ******************************************************************
       RST-DLC SECTION.
      ****************
       INI-RST-DLC.
           MOVE ZEROES TO WSS-SGV-CAPD(WSS-NUM-IDLC)
                          WSS-SGV-FINA(WSS-NUM-IDLC).
       FIN-RST-DLC.
           EXIT.

      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *****************************
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'OPERACIONES   ACTIVADAS OK   : ' WSS-ROPC-SACT.
           DISPLAY 'OPERACIONES   ACTIVADAS NOOK : ' WSS-ROPC-NACT.
           DISPLAY 'CANCELACIONES ACTIVADAS OK   : ' WSS-RCAN-SACT.
           DISPLAY 'CANCELACIONES ACTIVADAS NOOK : ' WSS-RCAN-NACT.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.

      ******************************************************************
       CLOSE-FILE SECTION.
      *******************
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-EVC.
           PERFORM COL-FIO-RDT.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-TOC.
           PERFORM COL-FIO-MAN.
       FIN-CLOSE-FILE.
           EXIT.

      ******************************************************************
      * Copys necesarios para la ejecucion de este programa.

       COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY COLBGINT.
       COPY GNSBGPES.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
      *
       COPY COLB4CAN.
       COPY COLB5CAN.
       COPY COLBGVEN.
       COPY COLBFOPC.
       COPY COLBFCAN.
       COPY COLBFDLC.
       COPY COLBFRCV.
       COPY COLBFVEN.
       COPY COLBFTOC.
       COPY COLBFEVC.
       COPY COLBFRDT.
       COPY COLBFICG.
       COPY COLBFMAN.
      *
       COPY TABBFCAM.
