       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGCAN.
       AUTHOR.          CONSIST.
      * Genera cancelaciones a partir de los vencimientos que ocurran
      * en la fecha de vencimiento ingresada por parametro y que tengan
      * cargo automatico en cuenta corriente.

       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AUT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *<<<< WSS
       COPY COLBROPC.
       COPY COLBRCYA.
       COPY COLBRAUT.
       COPY COLBRVEN.
       COPY COLBRRCV.
       COPY COLBRCAN.
       COPY COLBRTOC.
       COPY COLBRRDT.
       COPY COLBRDLC.
       COPY COLBREVC.
       COPY COLBRICG.
       COPY COLWGVEN.
       COPY TABBRCAM.
       COPY TABBROFI.
       COPY TABBRTIO.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
       COPY GNSWGINT.
       COPY GNSWGCVI.

       01  WSS-VARI.
      *DAD ini  01-AGO-1991
           03 WSS-OPC-KEEP                        PIC X(218).
      *DAD fin  01-AGO-1991
      *DAD ini  09-JUL-1991
           03 WSS-MSC-CCAA           VALUE SPACES PIC X(05).
      *DAD fin  09-JUL-1991
           03 WSS-COD-OFIC.
              05 WSS-IND-OFIC        OCCURS 10    PIC X(03).
           03 WSS-VAL-IDNI COMP-3    VALUE +0     PIC S9(11)V9(4).
           03 WSS-OFI-NFND           VALUE ZEROS  PIC 9(02).
           03 WSS-NUM-RLEI           VALUE ZEROS  PIC 9(04).
           03 WSS-NUM-RPRO           VALUE ZEROS  PIC 9(04).
           03 WSS-NUM-RERR           VALUE ZEROS  PIC 9(04).
           03 WSS-FEC-FMIN.
              05 WSS-NUM-DMIN        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MMIN        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SMIN        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AMIN        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AINI        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-STER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-ATER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MTER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DTER        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROS  PIC 9(02).
           03 WSS-COD-CMSC           VALUE SPACES PIC X(01).
           03 WSS-SGV-PROC COMP-3    VALUE +0     PIC S9(11)V9(04).
           03 WSS-SGV-COLO COMP-3    VALUE +0     PIC S9(11)V9(04).
           03 WSS-I                               PIC 9(03).
           03 WSS-GLS-MEN2           VALUE SPACES PIC X(40).
           03 WSS-RCV-KEY-ICAN.
              05 WSS-RCV-CIC-IOPC    VALUE SPACES PIC X(12).
              05 WSS-RCV-NUM-ICAN    VALUE ZEROS  PIC 9(03).
           03 WSS-VEN-KEY-IVEN.
              05 WSS-VEN-CIC-IOPC                 PIC X(12).
              05 WSS-VEN-NUM-IVEN                 PIC 9(03).
           03 WSS-IND-OTRO                        PIC X(01).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGCAN' TO FIO-PROG.
           PERFORM BUS-FSIS.
           PERFORM OPEN-FILE.
           PERFORM PRM-CAN.
           PERFORM INI-CAN.
           PERFORM LUP-PUT-VEN.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-VEN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-CAN.
       FIN-OPEN-FILE.
           EXIT.

       PRM-CAN SECTION.
      *-----------------
       INI-PRM-CAN.
      *DAD ini  09-JUL-1991
      * Busca Codigo con CIC de Cargo Automatico en Cta.Cte. desde GCAN
           MOVE 'TAB'        TO MSC-COD-SIST FIO-SIST.
           MOVE 'TCYACCGCAN' TO MSC-CIC-MSCL IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY 'NO EXISTE CODIGO DE CARGO AUTOMATICO EN CUENTA '
                       'CORRIENTE PARA GCAN'
               DISPLAY 'EN MISCELANEO TCYA DEL SISTEMA DE TABLAS, CON '
                       'EL CIC = TCYACCGCAN'
               PERFORM GNS-PRO-END.
           MOVE MSC-COD-CMSC IN MSC TO WSS-MSC-CCAA IN WSS-VARI.
      *DAD fin  09-JUL-1991

           MOVE 1 TO WSS-I.
           PERFORM LIM-OFI-PRMT UNTIL WSS-I > 10.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      *Captura Fecha de Proceso
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FEC DE PROC (DDMMAAAA) : ' WSS-FEC-FPRO IN WSS-VARI.
      *Captura Oficinas del credito
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'OFICINA                : ' WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO WSS-FEC-FMIN IN WSS-VARI.
       BUS-DIA-HBL.
           MOVE WSS-FEC-FMIN TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM PRO-FEC.
           MOVE FEC-FECH TO WSS-FEC-FINI.

      *VAL-FEC Validar Fecha
           MOVE WSS-FEC-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
                MOVE WSS-FEC-FINI IN WSS-VARI TO WSS-FEC-FMIN
                     IN WSS-VARI
                GO TO BUS-DIA-HBL.
           MOVE WSS-FEC-FMIN IN WSS-VARI TO WSS-FEC-FINI IN WSS-VARI.
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-IND-OFIC IN WSS-VARI(1) NOT = '*'
               MOVE 1 TO WSS-I
               PERFORM VAL-OFI-PRMT UNTIL WSS-I > 10.
               IF WSS-OFI-NFND IN WSS-VARI = 10
                   DISPLAY 'NO EXISTE NINGUNA OFICINA INGRESADA'
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
           DISPLAY 'FECHA INICIO  : ' WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FECHA TERMINO : ' WSS-FEC-FPRO IN WSS-VARI.
       FIN-PRM-CAN.
           EXIT.

       LIM-OFI-PRMT SECTION.
      *--------------------
       INI-LIM-OFI-PRMT.
           MOVE SPACES TO WSS-IND-OFIC IN WSS-VARI(WSS-I)
           ADD 1       TO WSS-I IN WSS-VARI.
       FIN-LIM-OFI-PRMT.
           EXIT.

       VAL-OFI-PRMT SECTION.
      *--------------------
       INI-VAL-OFI-PRMT.
           IF WSS-IND-OFIC IN WSS-VARI(WSS-I) NOT = SPACES
               MOVE WSS-IND-OFIC IN WSS-VARI(WSS-I) TO
                                    OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF (MSG-COD-MENS = 'COD    NEX') OR
                  (MSG-COD-MENS = 'COD    NVG')
                    MOVE 'COL' TO MSG-COD-SIST
                    PERFORM GET-MSG
                    DISPLAY 'OFICINA ' WSS-IND-OFIC IN WSS-VARI(WSS-I)
                    ' ' MSG-GLS-DESC
                    ADD 1 TO WSS-OFI-NFND IN WSS-VARI.
           ADD 1 TO WSS-I IN WSS-VARI.
       FIN-VAL-OFI-PRMT.
           EXIT.

       INI-CAN SECTION.
      *---------------
       INI-INI-CAN.
      * Inicializacion de Variables
           MOVE 'TVAL'         TO MSC-CIC-TMSC IN MSC.
           MOVE '0'            TO MSC-CIC-CMSC IN MSC.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'EN MISCELANEO TVAL NO EXISTE CODIGO 0'
               PERFORM GNS-PRO-END.
           MOVE MSC-COD-CMSC IN MSC TO WSS-COD-CMSC IN WSS-VARI.
       FIN-INI-CAN.
           EXIT.

       LUP-PUT-VEN SECTION.
      *-------------------
       INI-LUP-VEN.
      * Programacion de Ciclo para Registro VEN
           MOVE WSS-NUM-DINI IN WSS-VARI TO VEN-NUM-DVEN IN VEN.
           MOVE WSS-NUM-MINI IN WSS-VARI TO VEN-NUM-MVEN IN VEN.
           MOVE WSS-NUM-SINI IN WSS-VARI TO VEN-NUM-SVEN IN VEN.
           MOVE WSS-NUM-AINI IN WSS-VARI TO VEN-NUM-AVEN IN VEN.
           MOVE FIO-GET-NLS              TO FIO-CMND.
       LUP-BUS-VEN.
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LUP-PUT-VEN.

           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

      * Si la fecha de vencimiento es mayor que la ingresada por paramet
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-LUP-PUT-VEN.

      * Si el vencimiento no esta completo
           IF VEN-IND-CALC IN VEN = 'N'
               GO TO OTRO-VEN.

      * Si el vencimiento esta cancelado
           IF VEN-VAL-SCAP IN VEN = ZEROS AND
              VEN-VAL-SFIN IN VEN = ZEROS
               GO TO OTRO-VEN.

      * Si existe la OPC
           MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'        TO MSG-COD-SIST
               MOVE 'OPC    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO OTRO-VEN
           ELSE
      * Si el credito no esta activado
               IF OPC-MSC-STAT IN OPC NOT = 'A'
                   GO TO OTRO-VEN.

      * Valida que el credito no este cancelado
           IF OPC-VAL-SCRE IN OPC NOT > ZEROES
               GO TO OTRO-VEN.

      * Mediante BUS-TAB Busca Sistema Valido en Tabla TIO para Operacio
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'TIO'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-TOOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY MSG-GLS-DESC ' : ' TAB-COD-TABL
               GO TO OTRO-VEN.

      * Obtiene Sistema Valido de la Operacion de Credito
           MOVE TAB TO TIO.
           IF TIO-GLS-SIST IN TIO NOT = 'COL'
               GO TO OTRO-VEN.

      * Valida que pertenezca a la oficina
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-IND-OFIC IN WSS-VARI(1) NOT = '*'
               MOVE 1 TO WSS-I
               PERFORM SEL-OFI-PRMT UNTIL WSS-I > 10 OR
               OPC-COD-OFOC IN OPC = WSS-IND-OFIC IN WSS-VARI(WSS-I)
               IF WSS-I > 10
                   GO TO OTRO-VEN.

      ** Busca vencimiento en DLC para revisar que no existan
      ** vencimientos morosos
      *     MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
      *     MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
      *     MOVE FIO-GET-KEY         TO FIO-CMND.
      *     PERFORM COL-FIO-DLC.
      *     IF FIO-STAT-OKS
      ** Revisa que no existan vencimientos morosos
      *         IF DLC-IND-SCTB IN DLC NOT > SPACES OR
      *            DLC-IND-SCTB IN DLC = 'VIG'
      *             NEXT SENTENCE
      *         ELSE
      *             GO TO OTRO-VEN
      *     ELSE
      *         DISPLAY 'NO EXISTE REGISTRO DLC EN VEN : ' VEN-CIC-IOPC
      *                                                          IN VEN
      *         ADD 1 TO WSS-NUM-RERR IN WSS-VARI
      *         GO TO OTRO-VEN.
      *SE REEMPLAZA LA LECTURA DEL DLC POR EL VEN ANTERIOR PARA VER SI E
      *CANCELADO
           IF VEN-NUM-IVEN IN VEN NOT > 1
               GO TO CON-LUP-PUT-VEN.
           MOVE 'N'                 TO WSS-IND-OTRO.
           MOVE VEN-KEY-IVEN IN VEN TO WSS-VEN-KEY-IVEN.
           SUBTRACT 1 FROM VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = WSS-VEN-CIC-IOPC
               IF VEN-VAL-SFIN IN VEN > ZEROES OR
                  VEN-VAL-SCAP IN VEN > ZEROES
                   MOVE 'S' TO WSS-IND-OTRO
               ELSE
                   MOVE 'N' TO WSS-IND-OTRO
           ELSE
               MOVE 'N' TO WSS-IND-OTRO.
           MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF WSS-IND-OTRO = 'S'
               GO TO OTRO-VEN.
       CON-LUP-PUT-VEN.
      *

      * Valida que el credito no tenga cargo automatico en cuenta corrie
           PERFORM BUS-CYA-AUT.
           IF NOT (FIO-STAT-OKS AND VEN-IND-CAUT IN VEN NOT = 'N')
               GO TO OTRO-VEN.

           MOVE SPACES TO WSS-GLS-MEN2.
           PERFORM BUS-REG-RCV.
           IF MSG-COD-MENS > SPACES
               GO TO DIS-MEN-ERR.

           PERFORM GEN-REG-CAN.
           IF MSG-COD-MENS > SPACES
               GO TO DIS-MEN-ERR.

           PERFORM GEN-REG-RCV.
           IF MSG-COD-MENS > SPACES
               GO TO DIS-MEN-ERR.

           PERFORM GEN-REG-CYA.
           IF MSG-COD-MENS > SPACES
               GO TO DIS-MEN-ERR.

       DIS-MEN-ERR.
           IF MSG-COD-MENS > SPACES
               MOVE 'COL' TO MSG-COD-SIST
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC WSS-GLS-MEN2
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               PERFORM BCK-OUT
           ELSE
               ADD 1 TO WSS-NUM-RPRO IN WSS-VARI
               PERFORM CHK-PNT.

       OTRO-VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-BUS-VEN.
       FIN-LUP-PUT-VEN.
           EXIT.

        SEL-OFI-PRMT SECTION.
      *--------------------
        INI-SEL-OFI-PRMT.
           ADD 1 TO WSS-I.
        FIN-SEL-OFI-PRMT.
           EXIT.

       BUS-CYA-AUT SECTION.
       INI-BUS-CYA-AUT.
           MOVE OPC-CAI-IOPC IN OPC TO AUT-CAI-IOPC IN AUT.
           MOVE OPC-IIC-IOPC IN OPC TO AUT-IIC-IOPC IN AUT.
           MOVE 'AUT'               TO AUT-NUM-ICAN IN AUT.
           MOVE 1                   TO AUT-NUM-ICYA IN AUT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-AUT.
           IF FIO-STAT-OKS
               MOVE AUT TO CYA.
       FIN-BUS-CYA-AUT.
           EXIT.
      ******************************************************************
       GEN-REG-CAN SECTION.
       INI-GEN-REG-CAN.
           MOVE SPACES TO MSG-COD-MENS.
      * Programacion de Generacion Cancelacion
           PERFORM BUS-ULT-CAN.
           PERFORM INI-REG-CAN.
           IF MSG-COD-MENS > SPACES
               GO TO FIN-GEN-REG-CAN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-CAN.
           EXIT.
      *-----------------------------------------------------------------
       BUS-ULT-CAN SECTION.
       INI-BUS-ULT-CAN.
      *Busca Correlativo Ultima Cancelacion.
           MOVE VEN-CIC-IOPC IN VEN TO CAN-CIC-IOPC IN CAN.
           MOVE 999                 TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = CAN-CIC-IOPC IN CAN
               ADD 1 TO CAN-NUM-ICAN IN CAN
               GO TO FIN-BUS-ULT-CAN.

           MOVE VEN-CIC-IOPC IN VEN TO CAN-CIC-IOPC IN CAN.
           MOVE 1                   TO CAN-NUM-ICAN IN CAN.
           MOVE '00'                TO FIO-STAT.
       FIN-BUS-ULT-CAN.
           EXIT.
      *-----------------------------------------------------------------
       INI-REG-CAN SECTION.
       INI-INI-REG-CAN.
           MOVE VEN-CIC-IOPC IN VEN      TO CAN-CIC-IOPC IN CAN.
           PERFORM GET-FHOY.
           MOVE HOY-STP-ITRN IN HOY-VARI TO CAN-STP-ITRN IN CAN.
           MOVE SPACES                   TO CAN-MSC-TACC IN CAN.
           MOVE 'G'                      TO CAN-MSC-STAT IN CAN.
           MOVE SPACES                   TO CAN-COD-OTRN IN CAN.
           MOVE SPACES                   TO CAN-COD-ATRN IN CAN.
           MOVE OPC-COD-OFOC IN OPC      TO CAN-COD-OFCA IN CAN.
           MOVE OPC-COD-RMOC IN OPC      TO CAN-COD-RCAN IN CAN.
           MOVE 'CTE'                    TO CAN-MSC-CANC IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DCAN IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MCAN IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SCAN IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-ACAN IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DEGR IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MEGR IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SEGR IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-AEGR IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DOUT IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MOUT IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SOUT IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-AOUT IN CAN.
           MOVE ZEROES                   TO CAN-SGV-GAST IN CAN.
      *Calcular VEN-VAL-SFIN * VCAM_FPRO si es reajustable
           PERFORM CAL-SGV-TPAG.
           MOVE ZEROES                   TO CAN-SGV-TREN IN CAN.
           MOVE 1                        TO CAN-NUM-TVCC IN CAN.
           MOVE ZEROES                   TO CAN-FEC-FDVA IN CAN.
           MOVE ZEROES                   TO CAN-SGV-DIFE IN CAN.
           MOVE SPACES                   TO CAN-GLS-DISP IN CAN.
       FIN-INI-REG-CAN.
           EXIT.
      *-----------------------------------------------------------------
       CAL-SGV-TPAG SECTION.
       INI-CAL-SGV-TPAG.
           MOVE SPACES                       TO MSG-COD-MENS.
      *Calcular VEN-VAL-SFIN * VCAM_FPRO si es reajustable
           MOVE VEN-VAL-SFIN IN VEN          TO CAN-SGV-TPAG IN CAN.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC      TO TAB-COD-CTAB IN TAB
               MOVE '0999  '                 TO TAB-CIC-CTAB IN TAB
               MOVE WSS-NUM-DPRO IN WSS-VARI TO TAB-NUM-DTRN IN TAB
               MOVE WSS-NUM-MPRO IN WSS-VARI TO TAB-NUM-MTRN IN TAB
               MOVE WSS-NUM-SPRO IN WSS-VARI TO TAB-NUM-STRN IN TAB
               MOVE WSS-NUM-APRO IN WSS-VARI TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SFIN IN
                          VEN GIVING PES-SGV-DECI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE TO CAN-SGV-TPAG IN CAN
               ELSE
                   IF MSG-COD-MENS IN MSG = 'CODVCAMEQL'
                       MOVE SPACES TO MSG-COD-MENS
                   ELSE
                       MOVE CAM-KEY-CAMB IN CAM TO WSS-GLS-MEN2.
       FIN-CAL-SGV-TPAG.
           EXIT.
      ******************************************************************
       GEN-REG-RCV SECTION.
       INI-GEN-REG-RCV.
           MOVE SPACES TO MSG-COD-MENS.
      * Programacion de Generacion Relacion Cancelacion Vencimiento
           MOVE '00' TO FIO-STAT.
           PERFORM INI-REG-RCV.
           IF MSG-COD-MENS > SPACES
               GO TO FIN-GEN-REG-RCV.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS
               MOVE 'RCV    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-RCV.
           EXIT.
      *-----------------------------------------------------------------
       INI-REG-RCV SECTION.
       INI-INI-REG-RCV.
           MOVE SPACES                   TO RCV-GLS-FLAG IN RCV.
           MOVE CAN-CIC-IOPC IN CAN      TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN      TO RCV-NUM-ICAN IN RCV.
           MOVE VEN-CIC-IOPC IN VEN      TO RCV-CIC-IOPV IN RCV.
           MOVE VEN-NUM-IVCT IN VEN      TO RCV-NUM-IVCT IN RCV.
           PERFORM GET-FHOY.
           MOVE HOY-STP-ITRN IN HOY-VARI TO RCV-STP-ITRN IN RCV.
           MOVE SPACES                   TO RCV-MSC-TACC IN RCV.
           MOVE SPACES                   TO RCV-MSC-STAT IN RCV.
           MOVE SPACES                   TO RCV-COD-OTRN IN RCV.
           MOVE SPACES                   TO RCV-COD-ATRN IN RCV.
           MOVE OPC-CIC-IOPC IN OPC      TO RCV-CIC-IOPD IN RCV.
           MOVE VEN-NUM-IDLC IN VEN      TO RCV-NUM-IDLC IN RCV.
           MOVE VEN-NUM-IEVC IN VEN      TO RCV-NUM-IEVC IN RCV.
           MOVE 'F'                      TO RCV-MSC-MCAN IN RCV.
           MOVE VEN-VAL-SCAP IN VEN      TO RCV-SGV-CAPI IN RCV.
           MOVE VEN-VAL-IDNI IN VEN      TO RCV-SGV-INTE IN RCV.
           MOVE VEN-VAL-SFIN IN VEN      TO RCV-SGV-FINA IN RCV.
           MOVE VEN-IND-SCTB IN VEN      TO RCV-IND-SCTB IN RCV.
           MOVE VEN-IND-SCBZ IN VEN      TO RCV-IND-SCBZ IN RCV.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO RCV-NUM-DCAN IN RCV.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO RCV-NUM-MCAN IN RCV.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO RCV-NUM-SCAN IN RCV.
           MOVE WSS-NUM-APRO IN WSS-VARI TO RCV-NUM-ACAN IN RCV.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO RCV-NUM-DOUT IN RCV.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO RCV-NUM-MOUT IN RCV.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO RCV-NUM-SOUT IN RCV.
           MOVE WSS-NUM-APRO IN WSS-VARI TO RCV-NUM-AOUT IN RCV.
      *Calculo de reajustes SCAP (VCAM_FPRO - VCAM_FCOL)
           PERFORM CAL-RCV-REAJ.
           MOVE SPACES                   TO RCV-GLS-DISP IN RCV.
       FIN-INI-REG-RCV.
           EXIT.
      *-----------------------------------------------------------------
       CAL-RCV-REAJ SECTION.
       INI-CAL-RCV-REAJ.
           MOVE SPACES                       TO MSG-COD-MENS.
      *Calculo de reajustes SCAP (VCAM_FPRO - VCAM_FCOL)
           MOVE ZEROES                       TO RCV-SGV-REAJ IN RCV.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC      TO TAB-COD-CTAB IN TAB
               MOVE '0999  '                 TO TAB-CIC-CTAB IN TAB
               MOVE WSS-NUM-DPRO IN WSS-VARI TO TAB-NUM-DTRN IN TAB
               MOVE WSS-NUM-MPRO IN WSS-VARI TO TAB-NUM-MTRN IN TAB
               MOVE WSS-NUM-SPRO IN WSS-VARI TO TAB-NUM-STRN IN TAB
               MOVE WSS-NUM-APRO IN WSS-VARI TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN
                          VEN GIVING PES-SGV-DECI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE TO WSS-SGV-PROC IN WSS-VARI
               ELSE
                   IF MSG-COD-MENS IN MSG = 'CODVCAMEQL'
                       MOVE SPACES TO MSG-COD-MENS
                       GO TO FIN-CAL-RCV-REAJ
                   ELSE
                       MOVE CAM-KEY-CAMB IN CAM TO WSS-GLS-MEN2
                       GO TO FIN-CAL-RCV-REAJ.

      *DAD ini  01-AGO-1991
           IF OPC-IND-RENO IN OPC NOT = 'PRO'
               MOVE OPC-NUM-DCOL IN OPC TO TAB-NUM-DTRN IN TAB
               MOVE OPC-NUM-MCOL IN OPC TO TAB-NUM-MTRN IN TAB
               MOVE OPC-NUM-SCOL IN OPC TO TAB-NUM-STRN IN TAB
               MOVE OPC-NUM-ACOL IN OPC TO TAB-NUM-ATRN IN TAB
           ELSE
               PERFORM DET-OPC-ORIG
               IF MSG-COD-MENS IN MSG > SPACES
                   GO TO FIN-CAL-RCV-REAJ.
      *DAD fin  01-AGO-1991

           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB
               MOVE '0999  '            TO TAB-CIC-CTAB IN TAB
      *DAD ini  01-AGO-1991
      *         MOVE OPC-NUM-DCOL IN OPC TO TAB-NUM-DTRN IN TAB
      *         MOVE OPC-NUM-MCOL IN OPC TO TAB-NUM-MTRN IN TAB
      *         MOVE OPC-NUM-SCOL IN OPC TO TAB-NUM-STRN IN TAB
      *         MOVE OPC-NUM-ACOL IN OPC TO TAB-NUM-ATRN IN TAB
      *DAD fin  01-AGO-1991
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN
                          VEN GIVING PES-SGV-DECI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE TO WSS-SGV-COLO IN WSS-VARI
               ELSE
                   IF MSG-COD-MENS IN MSG = 'CODVCAMEQL'
                       MOVE SPACES TO MSG-COD-MENS
                   ELSE
                       MOVE CAM-KEY-CAMB IN CAM TO WSS-GLS-MEN2
                       GO TO FIN-CAL-RCV-REAJ.

           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               SUBTRACT WSS-SGV-COLO IN WSS-VARI FROM
                        WSS-SGV-PROC IN WSS-VARI GIVING
                        RCV-SGV-REAJ IN RCV.
       FIN-CAL-RCV-REAJ.
           EXIT.
      *-----------------------------------------------------------------
      *DAD ini  01-AGO-1991
      * Con el Documento asociado a la Operacion Prorrogada, Determinar
      * la Operacion Original y Leerla para Obtener la Fecha de Curse Or
       DET-OPC-ORIG SECTION.
       INI-DET-OPC-ORIG.
      * Guardo el OPC que este leido anteriormente (REGISTRO)
           MOVE OPC TO WSS-OPC-KEEP IN WSS-VARI.

      * Busca el Documento asociado a la Operacion Prorrogada
           MOVE SPACES TO MSG-COD-MENS IN MSG
                          WSS-GLS-MEN2 IN WSS-VARI.
           MOVE RCV-KEY-IDLC IN RCV TO DLC-KEY-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLC    NOHAY'      TO MSG-COD-MENS IN MSG
               MOVE DLC-KEY-IDLC IN DLC TO WSS-GLS-MEN2 IN WSS-VARI
               GO TO FIN-DET-OPC-ORIG.

      * Determina cual es la Operacion Original y la Lee
           IF DLC-CIC-IOCO IN DLC > SPACES AND
              DLC-CIC-IOCO IN DLC NOT = OPC-CIC-IOPC IN OPC
               MOVE DLC-CIC-IOCO IN DLC TO OPC-CIC-IOPC IN OPC
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-OPC
               IF NOT FIO-STAT-OKS
                   MOVE 'OPC    NOHAY'      TO MSG-COD-MENS IN MSG
                   MOVE OPC-KEY-IOPC IN OPC TO WSS-GLS-MEN2 IN WSS-VARI
                   GO TO FIN-DET-OPC-ORIG.

      * Obtiene la Fecha de Curse Original
           MOVE OPC-NUM-DCOL IN OPC TO TAB-NUM-DTRN IN TAB.
           MOVE OPC-NUM-MCOL IN OPC TO TAB-NUM-MTRN IN TAB.
           MOVE OPC-NUM-SCOL IN OPC TO TAB-NUM-STRN IN TAB.
           MOVE OPC-NUM-ACOL IN OPC TO TAB-NUM-ATRN IN TAB.

       FIN-DET-OPC-ORIG.
      * Restauro el OPC que traia el programa
           MOVE WSS-OPC-KEEP IN WSS-VARI TO OPC.
       EXT-DET-OPC-ORIG.
           EXIT.
      *DAD fin  01-AGO-1991
      ******************************************************************
       GEN-REG-CYA SECTION.
       INI-GEN-REG-CYA.
           MOVE SPACES TO MSG-COD-MENS.
      * Programacion de Generacion Cargos y Abonos
           PERFORM INI-REG-CYA.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS
               MOVE 'CYA    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-CYA.
           EXIT.
      *-----------------------------------------------------------------
       INI-REG-CYA SECTION.
       INI-INI-REG-CYA.
           MOVE CAN-NUM-ICAN IN CAN      TO CYA-NUM-ICAN IN CYA.
           MOVE 1                        TO CYA-NUM-ICYA IN CYA.
           PERFORM GET-FHOY.
           MOVE HOY-STP-ITRN IN HOY-VARI TO CYA-STP-ITRN IN CYA.
      *DAD ini  09-JUL-1991
      *     MOVE 'CCMA'                   TO CYA-IND-TCYA IN CYA.
           MOVE WSS-MSC-CCAA IN WSS-VARI TO CYA-IND-TCYA IN CYA.
      *DAD fin  09-JUL-1991
           MOVE CAN-SGV-TPAG IN CAN      TO CYA-SGV-LIQU IN CYA.
           MOVE 'S'                      TO CYA-IND-MAUT IN CYA.
           MOVE SPACES                   TO CYA-IND-FREE IN CYA.
           MOVE SPACES                   TO CYA-GLS-DISP IN CYA.
       FIN-INI-REG-CYA.
           EXIT.
      ******************************************************************
       BCK-OUT SECTION.
       INI-BCK-OUT.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               DISPLAY 'BACK-OUT ERRONEO'.
       FIN-BCK-OUT.
           EXIT.
      ******************************************************************
       CHK-PNT SECTION.
       INI-CHK-PNT.
           MOVE FIO-CHK-PNT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               DISPLAY 'CHECK-PNT ERRONEO'.
       FIN-CHK-PNT.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTRO VEN LEIDOS     : ' WSS-NUM-RLEI IN WSS-VARI
           DISPLAY 'REGISTRO VEN PROCESADOS : ' WSS-NUM-RPRO IN WSS-VARI
           DISPLAY 'REGISTRO VEN ERRONEOS   : ' WSS-NUM-RERR IN WSS-VARI
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-CAN.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
       BUS-REG-RCV SECTION.
       INI-BUS-REG-RCV.
           MOVE SPACES              TO MSG-COD-MENS.
           MOVE SPACES              TO WSS-RCV-KEY-ICAN IN WSS-VARI.
           MOVE VEN-CIC-IOPC IN VEN TO RCV-CIC-IOPV IN RCV.
           MOVE VEN-NUM-IVCT IN VEN TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS         TO FIO-CMND.
       LUP-BUS-REG.
           MOVE 'RCV-KEY-IVCT'      TO FIO-AKEY.
           PERFORM COL-FIO-RCV.
           IF (FIO-STAT-OKS AND
                   RCV-CIC-IOPV IN RCV = VEN-CIC-IOPC IN VEN AND
                   RCV-NUM-IVCT IN RCV = VEN-NUM-IVCT IN VEN)
               MOVE RCV-KEY-ICAN IN RCV TO WSS-RCV-KEY-ICAN IN WSS-VARI
               MOVE FIO-GET-NXT         TO FIO-CMND
               GO TO LUP-BUS-REG.

           IF WSS-RCV-KEY-ICAN IN WSS-VARI NOT > SPACES
               GO TO FIN-BUS-REG-RCV.

           MOVE WSS-RCV-KEY-ICAN IN WSS-VARI TO CAN-KEY-ICAN IN CAN
           MOVE FIO-GET-KEY                  TO FIO-CMND
           PERFORM COL-FIO-CAN
           IF FIO-STAT-OKS AND (CAN-MSC-STAT IN CAN = 'A' OR
                                CAN-MSC-STAT IN CAN = 'G' OR
                                CAN-MSC-STAT IN CAN = 'H' OR
                                CAN-MSC-STAT IN CAN = 'P' OR
                                CAN-MSC-STAT IN CAN = 'S' OR
                                CAN-MSC-STAT IN CAN = 'W')
               MOVE 'CAN    YAEXI'      TO MSG-COD-MENS
               MOVE CAN-KEY-ICAN IN CAN TO WSS-GLS-MEN2.
       FIN-BUS-REG-RCV.
           EXIT.
      ******************************************************************

       COPY COLBFOPC.
       COPY COLBFCYA.
       COPY COLBFAUT.
       COPY COLBFVEN.
       COPY COLBFRCV.
       COPY COLBFCAN.
       COPY COLBFTOC.
       COPY COLBFRDT.
       COPY COLBFDLC.
       COPY COLBFEVC.
       COPY COLBFICG.
       COPY COLBGVEN.
       COPY TABBFCAM.
       COPY TABBVOFI.
       COPY TABBFOFI.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBGCAM.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGPES.
       COPY GNSBGINT.
       COPY GNSBGCVI.
