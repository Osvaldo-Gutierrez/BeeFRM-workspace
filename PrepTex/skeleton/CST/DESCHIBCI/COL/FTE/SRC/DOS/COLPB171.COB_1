       IDENTIFICATION DIVISION.
      *........................
       PROGRAM-ID.    COLPB171.
      *
      *  AUTOR: HUGO ELADIO ZAPATA FUENTES.
      *         ===========================
      *
      *   PAREA MAESTRO DE CREDITOS CON MAECOL GENERANDO ARCHIVO D00
      *   MONEDA NACIONAL VIGENTE
      *
      *     ARCHIVOS : MAECOL - CREDITOS - CUOTAS - OPERACIONES
      *              : CIR5MDOO.
      *
      *
      *  MODIFICACIONES:
      *
      *  - SE ELIMINA EL USO DEL ARCHIVO MAECOL, REEMPLAZANDO SUS
      *    CAMPOS POR LOS EQUVALENTES DEL MAESTRO DE CREDITOS.
      *    SENC. 31/08/88.
      *
      *  - SE CORRIGE COMPARACION DE FECHAS PARA DECIDIR SI EL CRED.
      *    ESTA MOROSO.
      *    SENC. 15/12/88.
      *
      *  - SE CORRIGE RESCATAR REAJUSTES E INTERESES RIESGOSOS.
      *    HEZF. 30/05/89.
      *
      *  - NO CONSIDERA LOS EXCLUIBLES.
      *    PHJH. 16/04/90.
      *  - SE DEBE CONSIDERAR CRE-IND-I9 EN VEZ DE OPE-I9
      *    ODJ   25/02/91.
      *
      *  - SE DENE CONSIDERAR LAS BOLETAS DE GARANTIA SIEMPRE VIGENTES Y
      *    NO MOROSAS.        ODJ   30/06/94.
      *
       ENVIRONMENT DIVISION.
      *.....................

       CONFIGURATION SECTION.
      *......................

       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
      *.....................

       FILE-CONTROL.

           SELECT TAR-TARJETA     ASSIGN  UT-S-TARJETA.

           SELECT CRE-CREDITOS    ASSIGN TO CREDIT
                  ORGANIZATION    IS INDEXED
                  ACCESS MODE     IS SEQUENTIAL
                  RECORD KEY      IS CRE-CLAVE
                  FILE STATUS     IS CRE-STATUS.

           SELECT CUO-CUOTAS      ASSIGN TO CUOTAS
                  ORGANIZATION    IS INDEXED
                  ACCESS MODE     IS DYNAMIC
                  RECORD KEY      IS CUO-CLAVE
                  FILE STATUS     IS CUO-STATUS.

           SELECT DEV-DEVFME      ASSIGN TO DEVFME
                  ORGANIZATION    IS INDEXED
                  ACCESS MODE     IS RANDOM
                  RECORD KEY      IS DEV-CLAVE
                  FILE STATUS     IS DEV-STATUS.

           SELECT OPE-OPERACIONES ASSIGN OPERAC
                                  ORGANIZATION INDEXED
                                  ACCESS MODE  RANDOM
                                  RECORD KEY   OPESC-CLAVE
                                  FILE STATUS  OPE-STATUS.

           SELECT UF-UEFES           ASSIGN UEFES
                  ORGANIZATION       IS INDEXED
                  ACCESS MODE        IS RANDOM
                  RECORD KEY         IS UF-CLAVE
                  FILE STATUS        IS UF-STATUS.

           SELECT CIR5-CIR5MD00      ASSIGN AS-CIR5D00
                  ORGANIZATION       IS SEQUENTIAL
                  ACCESS MODE        IS SEQUENTIAL
                  FILE STATUS        IS D00-STATUS.

       DATA DIVISION.
      *..............

       FILE SECTION.

       FD  CRE-CREDITOS
                         RECORD   CONTAINS 377 CHARACTERS.
                         COPY CREM002.

       FD  DEV-DEVFME
                         RECORD   CONTAINS 128 CHARACTERS.
                         COPY CREM110.

       FD  CUO-CUOTAS
                         RECORD   CONTAINS  90 CHARACTERS.
                         COPY CREM009.

       FD  OPE-OPERACIONES
                           RECORD   CONTAINS 128 CHARACTERS.
                           COPY CRET131.

       FD  UF-UEFES
                           RECORD   CONTAINS  12 CHARACTERS.
                           COPY CREM021.

        FD  TAR-TARJETA
            RECORDING MODE F
            RECORD CONTAINS 80 CHARACTERS.
        01  TAR-REG-TARJETAS.
            05  TAR-FECHA.
                15  TAR-FECHA-AA        PIC 9(02).
                15  TAR-FECHA-MM        PIC 9(02).
                88  TRIMESTRE                 VALUE 03 06 09 12.
                15  TAR-FECHA-DD        PIC 9(02).
            05  TAR-FILLER1             PIC X(01).
            05  TAR-CODIGOS.
                15  TAR-COD-EXCLUIBLES OCCURS 20 TIMES
                    INDEXED BY IND-TAR.
                    25  TAR-COD-EXC     PIC 9(03).
            05  TAR-FILLER              PIC X(13).

       FD  CIR5-CIR5MD00
                           RECORD   CONTAINS 360 CHARACTERS.
           COPY CYDV000C.

       WORKING-STORAGE SECTION.
      *-----------------------
      *    TABLA PARA CONVERTIR CODIGOS BASIC EN CODIGOS IBM
       01  TABLA-D.
              05 VALORES.
                 10 FILLER  PIC X(39) VALUE
                   'A01B02C03D04E05F06G07H08I09J10K11L12M13'.
                 10 FILLER  PIC X(39) VALUE
                   'N14O15P16Q17R18S19T20U21V22W23X24Y25Z26'.
              05 TABLA-CODIGOS REDEFINES VALORES
                               OCCURS 26 TIMES
                               ASCENDING KEY NUMERO-T
                               INDEXED BY IND4.
                 10 LETRA-T   PIC X(01).
                 10 NUMERO-T  PIC X(02).
       01  CUOTA-LECTURA.
           10  CUOTA-LEC-SUC                 PIC 9(03).
           10  CUOTA-LEC-COD                 PIC X(08).
           10  CUOTA-LEC-RUT                 PIC X(10).
       01  CODIGO.
           10  CODIGO1                       PIC X(01).
           10  CODIGO2                       PIC X(02).
       01  WOR-CUENTA.
           05  WOR-CODIGO         PIC 9(04).
           05  WOR-RESTO-COD      PIC 9(05).
       01  WOR-CLAVE-LINEA.
           05  WOR-LETRA          PIC X(01)  VALUE 'D'.
           05  WOR-RESTO-LIN      PIC X(11).

       01  INDICES-Y-SWITCHS.
           05  CRE-STATUS         PIC 9(02)    VALUE 0.
           05  DEV-STATUS         PIC 9(02)    VALUE 0.
           05  CUO-STATUS         PIC 9(02)    VALUE 0.
           05  OPE-STATUS         PIC 9(02)    VALUE 0.
           05  UF-STATUS          PIC 9(02)    VALUE 0.
           05  D00-STATUS         PIC 9(02)    VALUE 0.
           05  FIN-CREDITOS       PIC X(02)    VALUE 'NO'.
           05  FIN-CUOTAS         PIC X(02)    VALUE 'NO'.
           05  WS-ENCONTRADO      PIC X(02)    VALUE 'NO'.
           05  I                  PIC 9(03)    VALUE 0.
           05  J                  PIC 9(02)    VALUE 0.
           05  CUOTA-INIC         PIC 9(03)    VALUE 0.
           05  WORK-CLAVE.
               10  WORK-SUC       PIC 9(03).
               10  WORK-OPERAC.
                   20  WORK-OPERAC-COD   PIC 9(03).
                   20  WORK-OPERAC-SER   PIC 9(05).
               10  WORK-RUT       PIC X(10).

           05  INICIO             PIC X(02)    VALUE 'NO'.
           05  PRIMER             PIC X(02)    VALUE 'NO'.
           05  CONTINUA-PROC      PIC X(02)    VALUE 'NO'.

       01  AREA-TRAB.
           05  CHEQUEA-CODIGO     PIC XXX.
           88  NO-PROCESAR        VALUE '026' '126'
                                        '217' '220' '223'
                                        '224' '225' '226'
      ****************** MAYO 95        '326' '426' '526'
                                                    '526'
                                        '626' '726' '826'
                                        '926' '601' '701'
      ****************** MAYO 95        '801' '901' '925'
                                        '801'       '925'
                                        '811'.
       01  CONTADORES.
           05  MAXIMO             PIC 9(08)   VALUE 300.
           05  TOPE               PIC 9(08)   VALUE 0.
           05  CONT-LEIDOS-CU     PIC 9(08)   VALUE 0.
           05  CONT-LEIDOS-DEV    PIC 9(08)   VALUE 0.
           05  CONT-LEIDOS-UF     PIC 9(08)   VALUE 0.
           05  CONT-LEIDOS-OP     PIC 9(08)   VALUE 0.
           05  CONT-LEIDOS-CR     PIC 9(08)   VALUE 0.
           05  CONT-OPE-ERR       PIC 9(08)   VALUE 0.
           05  CONT-UF-ERR        PIC 9(08)   VALUE 0.
           05  CONT-ERROR-CR      PIC 9(08)   VALUE 0.
           05  CONT-GRABADOS-C5   PIC 9(08)   VALUE 0.
           05  CONT-PAREADOS      PIC 9(08)   VALUE 0.
           05  CONT-CRE-MAS       PIC 9(08)   VALUE 0.
           05  CONT-PROBLEMAS     PIC 9(08)   VALUE 0.
           05  CONT-NO-PROC       PIC 9(08)   VALUE 0.

       01  VARIABLES-PROGRAMA.

           05  WSS-IND-TTAS.
               07  DIGITO-1       PIC 9(01).
               07  DIGITO-2       PIC 9(01).
               07  DIGITO-3       PIC 9(01).

           05  WSS-OLD-IOPC       PIC X(12).
           05  WSS-FEC-NDIA       PIC 9(06).
           05  WSS-FEC-NDIA-AUX   PIC 9(06).
           05  WSS-FEC-FPO2.
               07  WSS-NUM-SPO2   PIC 9(02).
               07  WSS-NUM-APO2   PIC 9(02).
               07  WSS-NUM-MPO2   PIC 9(02).
               07  WSS-NUM-DPO2   PIC 9(02).
           05  DIAS               PIC 9(07) COMP-3 VALUE 0.
           05  DIF-DIAS           PIC 9(04)        VALUE 0.
           05  ANT-CLAVE          PIC 9(03)        VALUE 0.
           05  WORK-UF-CURSE      PIC 9(08)V99 COMP-3.
           05  WORK-UF-PARAM      PIC 9(08)V99 COMP-3.
           05  WORK-UF            PIC 9(08)V99 COMP-3.
           05  SALDO-CONTABLE     PIC S9(13)   COMP-3 VALUE +0.
           05  SALDO-MOROSO       PIC S9(13)   COMP-3 VALUE +0.
           05  SALDO-MAECOL       PIC S9(13)   COMP-3 VALUE +0.
           05  REAJUSTE           PIC S9(13)   COMP-3 VALUE +0.
           05  C-CUOTA            PIC S9(13)V999   COMP-3 VALUE +0.
           05  C-INTERES          PIC S9(13)V999   COMP-3 VALUE +0.
           05  C-AMORTIZACION     PIC S9(13)V999   COMP-3 VALUE +0.
           05  W-PLAZO-PROM-PONDERADO PIC S9(13)V999   COMP-3 VALUE +0.
           05  INTERES-PASO       PIC S9(13)V999   COMP-3 VALUE +0.
           05  INTERES            PIC S9(13)   COMP-3 VALUE +0.
           05  SALDO-PASO         PIC S9(10)V999  COMP-3 VALUE +0.
           05  SALDO-PROM         PIC S9(10)V999  COMP-3 VALUE +0.
           05  SALDO-UF           PIC S9(10)V999  COMP-3 VALUE +0.
           05  INTER-UF           PIC S9(10)V999  COMP-3 VALUE +0.
           05  TASA               PIC S9(07)V9(10) COMP-3 VALUE +0.

       01  FECHA-DATE                 PIC 9(06).
       01  FECHA-DATER                REDEFINES FECHA-DATE.
           03  AA-DATE                PIC 9(02).
           03  MM-DATE                PIC 9(02).
           03  DD-DATE                PIC 9(02).
       01  FECHA-CURRENTX.
           03  MM-DATE                PIC 9(02).
           03  FILLER                 PIC X(01) VALUE '/'.
           03  DD-DATE                PIC 9(02).
           03  FILLER                 PIC X(01) VALUE '/'.
           03  AA-DATE                PIC 9(02).
       01  FECHAS-DE-LARGO8-Y-6.
           03 FINAL-FECHA8.
           05 FINAL-FECHA-SS         PIC 9(02) VALUE 0.
           05 FINAL-FECHA.
              10 FINAL-FECHA-AA      PIC 9(02) VALUE 0.
              10 FINAL-FECHA-MM      PIC 9(02) VALUE 0.
              10 FINAL-FECHA-DD      PIC 9(02) VALUE 0.
           03 FEC-CALDIAS-INI.
              10 FEC-CALDIAS-INI-DD  PIC 9(02) VALUE 0.
              10 FEC-CALDIAS-INI-MM  PIC 9(02) VALUE 0.
              10 FEC-CALDIAS-INI-SS  PIC 9(02).
              10 FEC-CALDIAS-INI-AA  PIC 9(02) VALUE 0.
           03 FEC-CALDIAS-FIN.
              10 FEC-CALDIAS-FIN-DD  PIC 9(02) VALUE 0.
              10 FEC-CALDIAS-FIN-MM  PIC 9(02) VALUE 0.
              10 FEC-CALDIAS-FIN-SS  PIC 9(02).
              10 FEC-CALDIAS-FIN-AA  PIC 9(02) VALUE 0.
           03 INICIAL-FECHA8.
           05 INICIAL-FECHA-SS       PIC 9(02) VALUE 0.
           05 INICIAL-FECHA.
              10 INICIAL-FECHA-AA    PIC 9(02) VALUE 0.
              10 INICIAL-FECHA-MM    PIC 9(02) VALUE 0.
              10 INICIAL-FECHA-DD    PIC 9(02) VALUE 0.

           03 FECHA-PASO8.
           05 FECHA-PASO-SS          PIC 9(02) VALUE 0.
           05 FECHA-PASO.
              10 FECHA-PASO-AA       PIC 9(02) VALUE 0.
              10 FECHA-PASO-MM       PIC 9(02) VALUE 0.
              10 FECHA-PASO-DD       PIC 9(02) VALUE 0.
           03 FECHA-WORK8.
           05 FECHA-WORK-SS          PIC 9(02) VALUE 0.
           05 FECHA-WORK.
              10 FECHA-WORK-AA       PIC 9(02) VALUE 0.
              10 FECHA-WORK-MM       PIC 9(02) VALUE 0.
              10 FECHA-WORK-DD       PIC 9(02) VALUE 0.
           03 FECHA-XXXX8.
           05 FECHA-XXXX-SS          PIC 9(02) VALUE 0.
           05 FECHA-XXXX.
              10 FECHA-XXXX-AA       PIC 9(02) VALUE 0.
              10 FECHA-XXXX-MM       PIC 9(02) VALUE 0.
              10 FECHA-XXXX-DD       PIC 9(02) VALUE 0.
           03 FECHA-YYYY8.
           05 FECHA-YYYY-SS          PIC 9(02) VALUE 0.
           05 FECHA-YYYY.
              10 FECHA-YYYY-AA       PIC 9(02) VALUE 0.
              10 FECHA-YYYY-MM       PIC 9(02) VALUE 0.
              10 FECHA-YYYY-DD       PIC 9(02) VALUE 0.
           03 FECHA-TAR8.
           05 FECHA-TAR-SS          PIC 9(02) VALUE 0.
           05 FECHA-TAR.
              10 FECHA-TAR-AA       PIC 9(02) VALUE 0.
              10 FECHA-TAR-MM       PIC 9(02) VALUE 0.
              10 FECHA-TAR-DD       PIC 9(02) VALUE 0.
           03 WOR-FECH-PROX-VENC8.
           05 WOR-FECH-PROX-VENC-SS        PIC 9(02) VALUE 0.
           05 WOR-FECH-PROX-VENC.
              10 WOR-FECH-PROX-VENC-AA     PIC 9(02) VALUE 0.
              10 WOR-FECH-PROX-VENC-MM     PIC 9(02) VALUE 0.
              10 WOR-FECH-PROX-VENC-DD     PIC 9(02) VALUE 0.

           03 FECHA-CURRENT.
              10 FECHA-CURRENT-MM       PIC 9(02) VALUE 0.
              10 FECHA-CURRENT-1        PIC X(01).
              10 FECHA-CURRENT-DD       PIC 9(02) VALUE 0.
              10 FECHA-CURRENT-2        PIC X(01).
              10 FECHA-CURRENT-AA       PIC 9(02) VALUE 0.

       01  AUX1-FECH-ULTIMO-MOV8.
           03  AUX1-FECH-ULTIMO-MOV-SS     PIC 9(02) VALUE 0.
           03  AUX1-FECH-ULTIMO-MOV.
           05  AUX1-FECH-ULTIMO-MOV-DD     PIC 9(02) VALUE 0.
           05  AUX1-FECH-ULTIMO-MOV-MM     PIC 9(02) VALUE 0.
           05  AUX1-FECH-ULTIMO-MOV-AA     PIC 9(02) VALUE 0.

       01  AUX2-FECH-ULTIMO-MOV8.
           03  AUX2-FECH-ULTIMO-MOV-SS     PIC 9(02) VALUE 0.
           03  AUX2-FECH-ULTIMO-MOV.
           05  AUX2-FECH-ULTIMO-MOV-AA     PIC 9(02) VALUE 0.
           05  AUX2-FECH-ULTIMO-MOV-MM     PIC 9(02) VALUE 0.
           05  AUX2-FECH-ULTIMO-MOV-DD     PIC 9(02) VALUE 0.

       01  CAMPOS-MENSAJES-ERROR-VSAM.
           05  MENS-PROC          PIC X(50)   VALUE SPACES.
           05  MENS-ARCH          PIC X(30)   VALUE SPACES.
           05  MENS-STAT          PIC XX      VALUE SPACES.
           05  MENS-CLAVE         PIC X(70)   VALUE SPACES.
           05  MENS-SW            PIC 9       VALUE 1.
           05  BLANCO             PIC X(05)   VALUE SPACES.

       01  WOR-FECH-UDHDM8.
           03 WOR-FECH-UDHDM-SS   PIC 9(02).
           03 WOR-FECH-UDHDM.
           05 WOR-FECH-UDHDM-AA   PIC 9(02).
           05 WOR-FECH-UDHDM-MM   PIC 9(02).
           05 WOR-FECH-UDHDM-DD   PIC 9(02).

       01  UIB.
           05 FILLER               PIC X(32) VALUE 'COLPB171'.


      ***************************************************************
       01  REQ-AREA-EVC.
           03 EVC-COMMAND          PIC  X(05).
           03 EVC-TABLE            PIC  X(03) VALUE 'EVC'.
           03 EVC-KEY              PIC  X(05) VALUE 'EVC00'.
           03 EVC-RET-COD          PIC  X(02) VALUE SPACES.
           03 EVC-INT-RC           PIC  X(01).
           03 EVC-DB-ID            PIC S9(04) COMP VALUE +160.
           03 EVC-REC-ID           PIC  X(07).
           03 FILLER               PIC  X(25).
           03 EVC-CONT-MAX         PIC S9(04) COMP.
           03 EVC-CONT-IO          PIC S9(04) COMP.
           03 FILLER               PIC  X(22).
           03 EVC-VAL-KEY          PIC  X(15).
           03 EVC-VAL-KEY-R REDEFINES EVC-VAL-KEY.
              05 EVC-VAL-KEY-1     PIC X(12).
              05 EVC-VAL-KEY-2     PIC 9(03).

       01  REG-EVC.
      *    COPYDD COL-R-EVC.COL-E-EVC00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO EVC
           02  COL-E-EVC00.
      *                             ESTRUCTURA VENCIMIENTOS
               03  EVC.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  EVC-GLS-FLAG PICTURE X(1).
      *                             CLAVE PRIMARIA REGISTRO
                   04  EVC-KEY-IREG.
      *                             KEY     IDENTIFICACION ESTRUCTURA VE
                       05  EVC-KEY-IEVC.
      *                             COD. INT. COMP. OPERACION CREDITO
                       06  EVC-CIC-IOPC.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       07  EVC-CAI-IOPC.
                       08  EVC-COD-TOPC
                                    PICTURE X(1).
                       08  EVC-COD-COPC
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       07  EVC-IIC-IOPC.
                       08  EVC-COD-OOPC.
                       09  EVC-COD-SOPC
                                    PICTURE X(2).
                       09  EVC-IND-SOPC
                                    PICTURE X(1).
                       08  EVC-GLS-NOPC
                                    PICTURE X(5).
      *                             NUM IDENT ESTRUCTURA VENCIMIENTOS
                       06  EVC-NUM-IEVC
                                    PICTURE 9(3).
      *                             TIME STAMP TRANSACCION
                       05  EVC-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  EVC-FEC-FTRN.
                       07  EVC-NUM-STRN
                                    PICTURE 9(2).
                       07  EVC-NUM-ATRN
                                    PICTURE 9(2).
                       07  EVC-NUM-MTRN
                                    PICTURE 9(2).
                       07  EVC-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  EVC-HRA-HRTR.
                       07  EVC-NUM-HHTR
                                    PICTURE 9(2).
                       07  EVC-NUM-MMTR
                                    PICTURE 9(2).
                       07  EVC-NUM-SSTR
                                    PICTURE 9(2).
      *                             REQ INDICADOR TIPO ACCION
                   04  EVC-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  EVC-MSC-STAT PICTURE X(1).
      *                             REQ CODIGO ORIGEN TRANSACCION
                   04  EVC-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  EVC-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  EVC-COD-TTRN
                                    PICTURE X(3).
      *                             REQ AUTOR TRANSACCION
                   04  EVC-COD-ATRN PICTURE X(12).
      *                             REQ NUMERO VENCIMIENTOS ESTRUCTURA
                   04  EVC-NUM-VIGU PICTURE 9(3).
      *                             REQ NUMERO VENCIMIENTOS CANCELADOS
                   04  EVC-NUM-VCAN PICTURE 9(3).
      *                             REQ SEPARACION ENTRE VENCIMIENTOS
                   04  EVC-NUM-SEVE PICTURE 9(4)
                                        COMPUTATIONAL.
      *                             REQ UNIDAD DE TIEMPO SEPARACION ENTR
                   04  EVC-IND-UTSV PICTURE X(1).
      *                             REQ FECHA PROXIMO VENCIMIENTO
                   04  EVC-FEC-FPVC.
                       05  EVC-NUM-SPVC
                                    PICTURE 9(2).
                       05  EVC-NUM-APVC
                                    PICTURE 9(2).
                       05  EVC-NUM-MPVC
                                    PICTURE 9(2).
                       05  EVC-NUM-DPVC
                                    PICTURE 9(2).
      *                             DESFASE EN DIAS
                   04  EVC-NUM-DESD PICTURE 9(4)
                                        COMPUTATIONAL.
      *                             FACTOR VARIACION ( CRECIMIENTO/DECRE
                   04  EVC-SGV-FVAR PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             IND. FACTOR VARIACION ( MONTO/PORCEN
                   04  EVC-IND-FVAR PICTURE X(1).
      *                             REQ VALOR VENCIMIENTOS ( CAPITAL O F
                   04  EVC-VAL-VENC PICTURE 9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             TIPO CALCULO VENCIMIENTOS
                   04  EVC-MSC-TCVE.
      *                             BASE CALCULO CREDITO
                       05  EVC-MSC-BCCR
                                    PICTURE X(1).
      *                             VALORES INFORMADOS CREDITO
                       05  EVC-MSC-VICR
                                    PICTURE X(1).
      *                             REQ NUMERO IDENTIFICACION DOCUMENTO
                   04  EVC-NUM-IDLC PICTURE 9(3).
      *                             REQ DIA PRIMER VENCIMIENTO
                   04  EVC-NUM-DPVE PICTURE 9(2).
      *                             INDICADOR CARGO AUTOMATICO
                   04  EVC-IND-CAUT PICTURE X(1).
      *                             DURACION VENCIMIENTOS
                   04  EVC-NUM-DURV PICTURE 9(4)
                                        COMPUTATIONAL.
      *                             UNIDAD DE TIEMPO DURACION VENCIMIENT
                   04  EVC-IND-UTDV PICTURE X(1).
      *                             REQ NUMERO IDENTIFICACION VENCIMIENT
                   04  EVC-NUM-IVCT PICTURE 9(3).
      *                             REQ VALOR CAPITAL O VALOR FINAL
                   04  EVC-VAL-VCVF PICTURE 9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             GLOSA DISPONIBLE
                   04  EVC-GLS-DISP PICTURE X(11).

       01  ELEM-LIST-EVC.
           03 ELEM-1-EVC     PIC X(5) VALUE 'EVC00'.
           03 ELEM-EVC       PIC X(1).
           03 FILLER         PIC X(5) VALUE SPACES.


      ***************************************************************
       01  REQ-AREA-ICG.
           03 ICG-COMMAND          PIC  X(05).
           03 ICG-TABLE            PIC  X(03) VALUE 'ICG'.
           03 ICG-KEY              PIC  X(05) VALUE 'ICG00'.
           03 ICG-RET-COD          PIC  X(02) VALUE SPACES.
           03 ICG-INT-RC           PIC  X(01).
           03 ICG-DB-ID            PIC S9(04) COMP VALUE +160.
           03 ICG-REC-ID           PIC  X(07).
           03 FILLER               PIC  X(25).
           03 ICG-CONT-MAX         PIC S9(04) COMP.
           03 ICG-CONT-IO          PIC S9(04) COMP.
           03 FILLER               PIC  X(22).
           03 ICG-VAL-KEY          PIC  X(41).
           03 ICG-VAL-KEY-R REDEFINES ICG-VAL-KEY.
              05 ICG-VAL-KEY-1     PIC  X(12).
              05 ICG-VAL-KEY-2     PIC  9(03).
              05 ICG-VAL-KEY-3     PIC  X(12).
              05 ICG-VAL-KEY-4     PIC  9(03).
              05 ICG-VAL-KEY-5     PIC  X(03).
              05 ICG-VAL-KEY-6     PIC  9(08).

       01  REG-ICG.
      *    COPYDD COL-R-ICG.COL-E-ICG00(PROD),01,D
      *                             EQUIVALENTE AL REGISTRO ICG
           02  COL-E-ICG00.
      *                             INTERES COMISIONES GASTOS
               03  ICG.
      *                             FILLER COMPATIBILIDAD VSAM/MVS
                   04  ICG-GLS-FLAG PICTURE X(1).
      *                             CLAVE PRIMARIA REGISTRO
                   04  ICG-KEY-IREG.
      *                             KEY     IDENTIFICACION TASAS
                       05  ICG-KEY-IICG.
      *                             IDENTIFICACION CANCELACION
                       06  ICG-KEY-ICAN.
      *                             COD. INT. COMP. OPERACION CREDITO
                       07  ICG-CIC-IOPC.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       08  ICG-CAI-IOPC.
                       09  ICG-COD-TOPC
                                    PICTURE X(1).
                       09  ICG-COD-COPC
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       08  ICG-IIC-IOPC.
                       09  ICG-COD-OOPC.
                       10  ICG-COD-SOPC
                                    PICTURE X(2).
                       10  ICG-IND-SOPC
                                    PICTURE X(1).
                       09  ICG-GLS-NOPC
                                    PICTURE X(5).
      *                             NUM IDENT. CANCELACION
                       07  ICG-NUM-ICAN
                                    PICTURE 9(3).
      *                             IDENTIFICACION VENCIMIENTO
                       06  ICG-KEY-IVCT.
      *                             COD. INT. COMP. OPERACION CREDITO
                       07  ICG-CIC-IOPV.
      *                             CENTRO ASIGNACION IDENTIFICACION
                       08  ICG-CAI-IOPV.
                       09  ICG-COD-TOPV
                                    PICTURE X(1).
                       09  ICG-COD-COPV
                                    PICTURE X(3).
      *                             IDENT. INTERNA COMPUTACIONAL
                       08  ICG-IIC-IOPV.
                       09  ICG-COD-OOPV.
                       10  ICG-COD-SOPV
                                    PICTURE X(2).
                       10  ICG-IND-SOPV
                                    PICTURE X(1).
                       09  ICG-GLS-NOPV
                                    PICTURE X(5).
      *                             NUM IDENT. VENCIMIENTO
                       07  ICG-NUM-IVCT
                                    PICTURE 9(3).
      *                             TIPO TASA
                       06  ICG-IND-TTAS
                                    PICTURE X(3).
      *                             FECHA INICIO VIGENCIA TASA
                       06  ICG-FEC-FINI.
                       07  ICG-NUM-SINI
                                    PICTURE 9(2).
                       07  ICG-NUM-AINI
                                    PICTURE 9(2).
                       07  ICG-NUM-MINI
                                    PICTURE 9(2).
                       07  ICG-NUM-DINI
                                    PICTURE 9(2).
      *                             TIME STAMP TRANSACCION
                       05  ICG-STP-ITRN.
      *                             FECHA TRANSACCION
                       06  ICG-FEC-FTRN.
                       07  ICG-NUM-STRN
                                    PICTURE 9(2).
                       07  ICG-NUM-ATRN
                                    PICTURE 9(2).
                       07  ICG-NUM-MTRN
                                    PICTURE 9(2).
                       07  ICG-NUM-DTRN
                                    PICTURE 9(2).
      *                             HORA TRANSACCION
                       06  ICG-HRA-HRTR.
                       07  ICG-NUM-HHTR
                                    PICTURE 9(2).
                       07  ICG-NUM-MMTR
                                    PICTURE 9(2).
                       07  ICG-NUM-SSTR
                                    PICTURE 9(2).
      *                             REQ INDICADOR TIPO ACCION
                   04  ICG-MSC-TACC PICTURE X(1).
      *                             INDICADOR STATUS
                   04  ICG-MSC-STAT PICTURE X(1).
      *                             REQ CODIGO ORIGEN TRANSACCION
                   04  ICG-COD-OTRN.
      *                             INDICADOR TIPO ORIGEN
                       05  ICG-MSC-TTRN
                                    PICTURE X(1).
      *                             CODIGO TIPO ORIGEN
                       05  ICG-COD-TTRN
                                    PICTURE X(3).
      *                             REQ AUTOR TRANSACCION
                   04  ICG-COD-ATRN PICTURE X(12).
      *                             REQ CODIGO TIPO TASA INTERES
                   04  ICG-COD-TINT.
      *                             CODIGO TIPO TASA
                       05  ICG-COD-TIPT.
      *                             BASE CALCULO TASA INTERES
                       06  ICG-MSC-BCTI
                                    PICTURE X(1).
      *                             PERIODO BASE TASA INTERES
                       06  ICG-MSC-PBTI
                                    PICTURE X(1).
      *                             MODALIDAD CALCULO TASA INTERES
                       06  ICG-MSC-MCTI.
      *                             BAJO PERIODO BASE TASA           COL
                       07  ICG-MSC-BPBT
                                    PICTURE X(1).
      *                             SOBRE PERIODO BASE TASA          COL
                       07  ICG-MSC-SPBT
                                    PICTURE X(1).
      *                             CODIGO VALOR TASA INTERES
                       05  ICG-COD-CVTI
                                    PICTURE X(3).
      *                             TIPO FECHA PERIODO REPACTO
                       05  ICG-MSC-TFPR
                                    PICTURE X(1).
      *                             NUMERO PERIODO REPACTO
                       05  ICG-NUM-PREP
                                    PICTURE 9(3).
      *                             UNIDAD TIEMPO PERIODO REPACTO
                       05  ICG-MSC-UTPR
                                    PICTURE X(1).
      *                             FECHA TERMINO VIGENCIA TASA
                   04  ICG-FEC-FTER.
                       05  ICG-NUM-STER
                                    PICTURE 9(2).
                       05  ICG-NUM-ATER
                                    PICTURE 9(2).
                       05  ICG-NUM-MTER
                                    PICTURE 9(2).
                       05  ICG-NUM-DTER
                                    PICTURE 9(2).
      *                             REQ VALOR SPREAD
                   04  ICG-SGV-SPRD PICTURE S9(3)V9(4)
                                        COMPUTATIONAL-3.
      *                             REQ VALOR TASA
                   04  ICG-SGV-TINT PICTURE S9(3)V9(4)
                                        COMPUTATIONAL-3.
      *                             VALOR EN MONTO
                   04  ICG-SGV-MNTO PICTURE S9(11)V9(4)
                                        COMPUTATIONAL-3.
      *                             GLOSA DISPONIBLE
                   04  ICG-GLS-DISP PICTURE X(7).

       01  ELEM-LIST-ICG.
           03 ELEM-1-ICG     PIC X(5) VALUE 'ICG00'.
           03 ELEM-ICG       PIC X(1).
           03 FILLER         PIC X(5) VALUE SPACES.
      ***************************************************************


      ******************************************************************
      *    DEFINICION DE AREAS PARA RUTINA CALENDAR.                   *
      ******************************************************************
           COPY CALEAREA.

       PROCEDURE DIVISION.
      *...................
       PRINCIPAL.
      *..........
           ENTRY 'DBMSCBL'.
           PERFORM  ABRIR.
           PERFORM  LEER-TARJETA.
           PERFORM  LEER-UF-PARAMETRO.
           PERFORM  LEER-CREDITOS.
           DISPLAY 'PARTIO....'     UPON  CONSOLE.
           PERFORM  PROCESAR   UNTIL   FIN-CREDITOS =  'SI'.
           PERFORM  CERRAR.
           PERFORM  ESTADISTICAS.
           STOP RUN.
           EJECT

       ABRIR.
      *......
           OPEN  INPUT   CRE-CREDITOS
                         CUO-CUOTAS
                         DEV-DEVFME
                         TAR-TARJETA
                         UF-UEFES
                         OPE-OPERACIONES
                 OUTPUT  CIR5-CIR5MD00.
           IF (CRE-STATUS  NOT = 0 AND NOT = 35 AND NOT = 97)
           THEN  MOVE  'ABRIR'          TO  MENS-PROC
                 MOVE  'CRE-CREDITOS  ' TO  MENS-ARCH
                 MOVE  CRE-STATUS       TO  MENS-STAT
                 PERFORM  ERROR-VSAM.
           IF (DEV-STATUS  NOT = 0 AND NOT = 35 AND NOT = 97)
           THEN  MOVE  'ABRIR'          TO  MENS-PROC
                 MOVE  'DEV-DEVFME    ' TO  MENS-ARCH
                 MOVE  DEV-STATUS       TO  MENS-STAT
                 PERFORM  ERROR-VSAM.
           IF (CUO-STATUS  NOT = 0 AND NOT = 35 AND NOT = 97)
           THEN  MOVE  'ABRIR'        TO  MENS-PROC
                 MOVE  'CUO-CUOTAS  ' TO  MENS-ARCH
                 MOVE  CUO-STATUS     TO  MENS-STAT
                 PERFORM  ERROR-VSAM.
           IF (OPE-STATUS  NOT = 0 AND NOT = 35 AND NOT = 97)
           THEN  MOVE  'ABRIR'          TO  MENS-PROC
                 MOVE  'OPE-OPERACIONE' TO  MENS-ARCH
                 MOVE  OPE-STATUS       TO  MENS-STAT
                 PERFORM  ERROR-VSAM.
           IF (UF-STATUS  NOT = 0 AND NOT = 35 AND NOT = 97)
           THEN  MOVE  'ABRIR'        TO  MENS-PROC
                 MOVE  'UF-UEFES    ' TO  MENS-ARCH
                 MOVE  UF-STATUS      TO  MENS-STAT
                 PERFORM  ERROR-VSAM.
           IF (D00-STATUS  NOT = 0 AND NOT = 35 AND NOT = 97)
           THEN  MOVE  'ABRIR'          TO  MENS-PROC
                 MOVE  'D00-CIR5MD00  ' TO  MENS-ARCH
                 MOVE  D00-STATUS       TO  MENS-STAT
                 PERFORM  ERROR-VSAM.
       CERRAR.
      *.......
           CLOSE    CRE-CREDITOS
                    TAR-TARJETA
                    DEV-DEVFME
                    CUO-CUOTAS
                    OPE-OPERACIONES
                    UF-UEFES
                    CIR5-CIR5MD00.

       PROCESAR.
      *.........
           PERFORM VALIDAR-CODIGO.
           IF D00-IND-DEUDA-EXCLUIBLE = 'E'
           THEN ADD 1 TO CONT-NO-PROC
           ELSE IF NO-PROCESAR
                THEN ADD 1  TO  CONT-NO-PROC
                ELSE IF CRE-SALDO-COLOCAC = +0
                     THEN ADD 1 TO CONT-NO-PROC
                     ELSE PERFORM PROCESO-OK.
           PERFORM LEER-CREDITOS.
           ADD  1    TO    TOPE
           IF TOPE > MAXIMO
           THEN DISPLAY 'VAN PROCESADOS :' TOPE UPON CONSOLE
                ADD      MAXIMO       TO   MAXIMO.

       LEER-DEV.
      *----------
           READ DEV-DEVFME.
           IF DEV-STATUS = 23
           THEN MOVE 'NO'         TO  WS-ENCONTRADO
           ELSE IF DEV-STATUS NOT = 00
                THEN MOVE 'NO'                        TO  WS-ENCONTRADO
                     MOVE  '   LEER-DEVEN         '   TO  MENS-PROC
                     MOVE  'DEV-DEVENGOS'             TO  MENS-ARCH
                     MOVE  DEV-STATUS                 TO  MENS-STAT
                     PERFORM  ERROR-VSAM
                ELSE ADD 1 TO CONT-LEIDOS-DEV
                     MOVE 'SI'                        TO  WS-ENCONTRADO.
       PROCESO-OK.
      *----------
           MOVE   CRE-FECH-CURSE-DD      TO      FECHA-PASO-DD
           MOVE   CRE-FECH-CURSE-MM      TO      FECHA-PASO-MM
           MOVE   CRE-FECH-CURSE-AA      TO      FECHA-PASO-AA
           PERFORM MUEVE-FECHA-PASO
           MOVE CRE-FECHA-SUSPEN TO FECHA-WORK.
           PERFORM MUEVE-FECHA-WORK.
           IF CRE-FECHA-SUSPEN > ZEROES    AND
      *       CRE-FECHA-SUSPEN < FECHA-PASO
              FECHA-WORK8      < FECHA-PASO8
                MOVE FECHA-PASO            TO    CRE-FECHA-SUSPEN.

           IF CRE-FECHA-SUSPEN > ZEROES    AND
      *       CRE-FECHA-SUSPEN > TAR-FECHA
              FECHA-WORK8      > FECHA-TAR8
           THEN MOVE TAR-FECHA             TO    CRE-FECHA-SUSPEN.

           MOVE 'SI'             TO         CONTINUA-PROC.
           PERFORM LEER-ARCHIVOS-PROCESO.



           IF CONTINUA-PROC   =  'SI'
           THEN
                PERFORM LLENAR-SALIDA
                PERFORM GRABAR-REGISTRO-C5
           ELSE PERFORM PROBLEMA.

       LLENAR-SALIDA.
      *...............
           PERFORM  LLENAR-CAMPOS-DIRECTOS.
           PERFORM  LLENAR-CAMPOS-CALCULOS.
      **MOD. ENE97. PRP
           IF D00-ESTADO-CREDITO IS NUMERIC
            IF D00-ESTADO-CREDITO = 1 AND
              (D00-SALDO-MOROSO1 NOT = 0 OR
               D00-SALDO-MOROSO2 NOT = 0)
              DISPLAY '** CREDITO ERRONEO. ESTADO VIG CON SALDO MOR **'
                      'NRO: ' D00-CLAVE '** SE CONSIDERA VIGENTE'
              DISPLAY D00-FECHA-PROX-VCTO 'D0W ' WOR-FECH-PROX-VENC8
                      ' TAR ' FECHA-TAR8 'UH ' WOR-FECH-UDHDM
              MOVE ZEROES     TO D00-SALDO-MOROSO1
              MOVE ZEROES     TO D00-SALDO-MOROSO2.
      **FIN MOD

       LEER-ARCHIVOS-PROCESO.
      *......................
            IF CRE-COD-OPER  NOT = ANT-CLAVE
            THEN MOVE CRE-COD-OPER         TO  ANT-CLAVE    OPESC-CLAVE
                 PERFORM LEER-OPERACIONES
                 MOVE OPESC-CTA-DE-CAJA      TO  WOR-CUENTA.
            IF CRE-IND-I9 = 2
            THEN  PERFORM LEER-UEFES.

       LEER-UEFES.
      *...........
           MOVE  CRE-FECH-CURSE-DD    TO          UF-FECHA-DD.
           MOVE  CRE-FECH-CURSE-MM    TO          UF-FECHA-MM.
           MOVE  CRE-FECH-CURSE-AA    TO          UF-FECHA-AA.
           PERFORM  LEER-UF.
           MOVE   UF-VALOR               TO          WORK-UF-CURSE.
           IF  CRE-FECHA-SUSPEN       >  ZEROES
           THEN   MOVE   CRE-FECHA-SUSPEN            TO   UF-CLAVE
                  PERFORM  LEER-UF
                  MOVE   UF-VALOR                    TO   WORK-UF
      *    ELSE  IF  WOR-FECH-PROX-VENC     <  TAR-FECHA     AND
      *              WOR-FECH-PROX-VENC-AA  >  50
           ELSE  IF  WOR-FECH-PROX-VENC8 <  FECHA-TAR8
                 THEN     MOVE   WOR-FECH-PROX-VENC  TO   UF-CLAVE
                          PERFORM  LEER-UF
                          MOVE   UF-VALOR            TO   WORK-UF
                 ELSE     MOVE   WORK-UF-PARAM       TO   WORK-UF.

       LEER-UF-PARAMETRO.
      *..................
           MOVE   TAR-FECHA           TO          UF-CLAVE.
           PERFORM  LEER-UF.
           MOVE   UF-VALOR            TO          WORK-UF-PARAM.

       LEER-CUOT-CRED.
      *....................
           MOVE CUO-FECHA-VENC-AA  TO FECHA-PASO-AA
           MOVE CUO-FECHA-VENC-MM  TO FECHA-PASO-MM
           MOVE CUO-FECHA-VENC-DD  TO FECHA-PASO-DD.
           PERFORM MUEVE-FECHA-PASO
           PERFORM PLAZO-PROM-POND-RESTO.
      *    IF (  FECHA-PASO   NOT >   TAR-FECHA
      *    AND   FECHA-PASO-AA    > 60
      *    AND   FECHA-PASO       < WOR-FECH-UDHDM)
           IF (  FECHA-PASO8  NOT >   FECHA-TAR8
AHORA *    AND   FECHA-PASO8      < WOR-FECH-UDHDM8)
AHORA      AND   FECHA-PASO8  NOT > WOR-FECH-UDHDM8)
           THEN   ADD   1                 TO      D00-NRO-CTAS-ATRAS
                  MOVE FECHA-PASO         TO   INICIAL-FECHA
                  MOVE TAR-FECHA          TO   FINAL-FECHA
                  PERFORM CALCULAR-DIAS
                  PERFORM SUMA-SALDO-MOROSO.
           IF CUO-NUM-ABONO = CRE-TOTAL-CUOTAS
           THEN MOVE FECHA-PASO    TO      D00-FECHA-DE-EXTINCION.
           IF CUO-AMORTIZACION > ZEROES
           THEN ADD   1            TO      D00-NRO-CTAS-AMORT-PEND.
           PERFORM LEER-CUOTAS.

       PLAZO-PROM-POND-RESTO.
      *......................
           MOVE   CUO-CUOTA             TO    C-CUOTA.
           MOVE   CUO-AMORTIZACION      TO    C-AMORTIZACION.
           MOVE   CUO-INTERES           TO    C-INTERES.

           IF   OPESC-I14   = ( 7 OR 9 OR 11 )
           AND  C-INTERES =   0
           THEN PERFORM CALCULAR-INT-PASO
                COMPUTE C-AMORTIZACION =  C-CUOTA     -  INTERES-PASO
                COMPUTE SALDO-PROM     =  SALDO-PROM  -  C-AMORTIZACION
                MOVE    INTERES-PASO   TO C-INTERES.

           IF CUO-DIAS-ENTRE-VCTOS IS NOT NUMERIC
            OR CUO-DIAS-ENTRE-VCTOS =  ZEROES
           THEN  MOVE  30             TO CUO-DIAS-ENTRE-VCTOS
                 IF CRE-PERIODICIDAD = 9
                 THEN COMPUTE CUO-DIAS-ENTRE-VCTOS ROUNDED =
                              CUO-DIAS-ENTRE-VCTOS * 12
                 ELSE IF CRE-PERIODICIDAD > 1
                      THEN COMPUTE CUO-DIAS-ENTRE-VCTOS ROUNDED =
                               CUO-DIAS-ENTRE-VCTOS * CRE-PERIODICIDAD.

      *    IF (  FECHA-PASO    < TAR-FECHA
      *    AND   FECHA-PASO-AA > 60  )
           IF (  FECHA-PASO8   < FECHA-TAR8)
                 MOVE   1                     TO   DIF-DIAS
           ELSE  IF   PRIMER   = 'SI'
                 THEN MOVE TAR-FECHA          TO   INICIAL-FECHA
                      MOVE FECHA-PASO         TO   FINAL-FECHA
                      MOVE 'NO'               TO   PRIMER
                      PERFORM CALCULAR-DIAS
                 ELSE ADD  CUO-DIAS-ENTRE-VCTOS TO DIF-DIAS.

           COMPUTE W-PLAZO-PROM-PONDERADO ROUNDED  =
                   W-PLAZO-PROM-PONDERADO          +
                   C-AMORTIZACION  *  DIF-DIAS.

       CALCULAR-INT-PASO.
      *..................
           COMPUTE INTERES-PASO ROUNDED  = SALDO-PROM   *  TASA  *
                                         ( CUO-DIAS-ENTRE-VCTOS / 30 ).

       SUMA-SALDO-MOROSO.
      *..................
           IF CRE-IND-I9 = 3
           THEN COMPUTE SALDO-MOROSO = C-AMORTIZACION + C-INTERES
           ELSE IF CRE-IND-I9 = 2
                THEN COMPUTE SALDO-MOROSO ROUNDED =
                                    C-AMORTIZACION * WORK-UF   +
                                    C-INTERES      * WORK-UF
                ELSE IF CRE-IND-I9 = 1
                     THEN COMPUTE SALDO-PASO ROUNDED =
                                  C-AMORTIZACION / WORK-UF-CURSE +
                                  C-INTERES      / WORK-UF-CURSE
                          COMPUTE SALDO-MOROSO ROUNDED =
                                          SALDO-PASO * WORK-UF.
           PERFORM ASIGNA-SALDO-MOROSO.

       ASIGNA-SALDO-MOROSO.
      *--------------------
           IF  DIF-DIAS < 30
           THEN COMPUTE D00-SALDO-MOROSO1 = D00-SALDO-MOROSO1 +
                                             SALDO-MOROSO
           ELSE COMPUTE D00-SALDO-MOROSO2 = D00-SALDO-MOROSO2 +
                                             SALDO-MOROSO.

       LLENAR-CAMPOS-DIRECTOS.
      *.......................
            MOVE 01                 TO     D00-SISTEMA.
      *     MOVE CRE-CLAVE          TO     D00-NUM-INTERNO-IDENT.
            MOVE CRE-CLAVE-LINEA    TO     WOR-RESTO-LIN.
            MOVE WOR-CLAVE-LINEA    TO     D00-NUM-INTERNO-IDENT.
            MOVE CRE-CODIGO-OPER    TO     D00-TIPO-OPERACION.
            MOVE ZEROES             TO     D00-MONTO-RENOV-MES.
            MOVE 'S'                TO     D00-AFECTA-MARGENES-S-N.
            MOVE ZEROES             TO     D00-INT-POR-COBRAR-RIESGO
                                           D00-REAJ-DEVEN-RIESGO
                                           D00-INT-POR-COBRAR-VCDOS
                                           D00-MONTO-RENOV-MES.
            MOVE CRE-VCTOS-PAG-MES  TO     D00-MONTO-PAG-EN-MES.
            MOVE CRE-VCTOS-EN-MES   TO     D00-VENC-EN-MES.
            MOVE CRE-TIPO-GARANTIA  TO     D00-TIPO-GARANTIA.
                 MOVE '1'           TO     D00-SITUAC-OPERACION.

            MOVE CRE-SUCURSAL       TO     D00-OFICINA.


            MOVE 0000               TO     D00-TIPO-CRED-CART-ORIGEN.
            MOVE ZEROES             TO     D00-SALDO-REPROGRAMADO.

            IF CRE-VIGENCIA = 'N'  AND
               WOR-FECH-PROX-VENC-AA = TAR-FECHA-AA   AND
               WOR-FECH-PROX-VENC-MM = TAR-FECHA-MM   AND
               CRE-FECH-CURSE-AA     = TAR-FECHA-AA   AND
               CRE-FECH-CURSE-MM     = TAR-FECHA-MM
            THEN MOVE  4            TO     D00-ESTADO-CREDITO
            ELSE IF ( OPESC-I16 = 24 )
                 THEN MOVE  3       TO     D00-ESTADO-CREDITO
                 ELSE IF CRE-VIGENCIA = 'S'
                      THEN IF WOR-FECH-PROX-VENC8 > FECHA-TAR8
                           OR CRE-IND-I11 = 3
                           OR WOR-FECH-PROX-VENC8 >     WOR-FECH-UDHDM8
                           THEN MOVE  1    TO     D00-ESTADO-CREDITO
                           ELSE MOVE  2    TO     D00-ESTADO-CREDITO
                      ELSE MOVE 0   TO     D00-ESTADO-CREDITO.

            MOVE CRE-DESTINO        TO     D00-ACT-DESTINO-CRED
            MOVE CRE-FECH-CURSE-AA  TO     D00-FECHA-OTORGAM-AA.
            MOVE CRE-FECH-CURSE-MM  TO     D00-FECHA-OTORGAM-MM.
            MOVE CRE-FECH-CURSE-DD  TO     D00-FECHA-OTORGAM-DD.

            IF CRE-IND-I9 = 3
            THEN  MOVE 999              TO     D00-MONEDA
                  MOVE CRE-TASA-VIGENTE TO     D00-TASA-INTERES
            ELSE  MOVE 998              TO     D00-MONEDA
                  COMPUTE D00-TASA-INTERES ROUNDED =
                          CRE-TASA-VIGENTE * 12.

            IF CRE-IND-I9 = 2
            THEN COMPUTE  D00-MONTO-ORIGINAL     ROUNDED =
                          CRE-MONTO-COLOC     * WORK-UF-CURSE
                 COMPUTE  D00-ULT-INTERES-PAGADO ROUNDED =
                          CRE-TOT-INT-PAG-MES * WORK-UF-CURSE
            ELSE MOVE CRE-TOT-INT-PAG-MES  TO   D00-ULT-INTERES-PAGADO
                 MOVE CRE-MONTO-COLOC      TO     D00-MONTO-ORIGINAL.

            MOVE CRE-FECH-PROX-VENC-AA  TO D00-FECHA-PROX-VCTO-AA.
            MOVE CRE-FECH-PROX-VENC-MM  TO D00-FECHA-PROX-VCTO-MM.
            MOVE CRE-FECH-PROX-VENC-DD  TO D00-FECHA-PROX-VCTO-DD.
            IF (CRE-IND-I14 NOT = 08 AND CRE-IND-I14 NOT = 13) OR
               (CRE-FECH-ULT-PAGO-INT  =  ZEROES  OR  SPACES)
            THEN IF CRE-FECH-VENC-ANTERIOR = ZEROS OR
                    CRE-FECH-VENC-ANTERIOR  NOT NUMERIC

                 THEN MOVE CRE-FECH-CURSE TO D00-FECHA-ULT-PAGO-INT
                 ELSE MOVE CRE-FECH-VENC-ANTERIOR-DD  TO
                                           D00-FECHA-ULT-PAGO-INT-DD
                      MOVE CRE-FECH-VENC-ANTERIOR-MM  TO
                                           D00-FECHA-ULT-PAGO-INT-MM
                      MOVE CRE-FECH-VENC-ANTERIOR-AA  TO
                                           D00-FECHA-ULT-PAGO-INT-AA

            ELSE MOVE CRE-FECH-ULT-PAGO-INT-DD
                                        TO D00-FECHA-ULT-PAGO-INT-DD
                 MOVE CRE-FECH-ULT-PAGO-INT-MM
                                        TO D00-FECHA-ULT-PAGO-INT-MM
                 MOVE CRE-FECH-ULT-PAGO-INT-AA
                                        TO D00-FECHA-ULT-PAGO-INT-AA.

      ** CRE-IND-I11 = 3 ==> BOL.DE GARANT. (CR. CONTINGENTES EN GRAL)

           IF CRE-IND-I11 = 3
           THEN IF TAR-FECHA-MM = 01  OR 02
                THEN MOVE 31  TO D00-FECHA-ULT-PAGO-INT-DD
                     MOVE 12  TO D00-FECHA-ULT-PAGO-INT-MM
                     IF TAR-FECHA-AA = 0
                        MOVE 99   TO D00-FECHA-ULT-PAGO-INT-AA
                     ELSE
                     SUBTRACT 1 FROM TAR-FECHA-AA GIVING
                                 D00-FECHA-ULT-PAGO-INT-AA

                ELSE IF TAR-FECHA-MM = 04  OR 05
                     THEN MOVE 31  TO D00-FECHA-ULT-PAGO-INT-DD
                          MOVE 03  TO D00-FECHA-ULT-PAGO-INT-MM
                          MOVE TAR-FECHA-AA  TO
                                      D00-FECHA-ULT-PAGO-INT-AA

                     ELSE IF TAR-FECHA-MM = 07  OR 08
                          THEN MOVE 30  TO D00-FECHA-ULT-PAGO-INT-DD
                               MOVE 06  TO D00-FECHA-ULT-PAGO-INT-MM
                               MOVE TAR-FECHA-AA
                                        TO D00-FECHA-ULT-PAGO-INT-AA

                          ELSE IF TAR-FECHA-MM = 10  OR 11
                               THEN MOVE 30  TO
                                         D00-FECHA-ULT-PAGO-INT-DD
                                    MOVE 09  TO
                                         D00-FECHA-ULT-PAGO-INT-MM
                                    MOVE TAR-FECHA-AA  TO
                                         D00-FECHA-ULT-PAGO-INT-AA.


           IF CRE-FECHA-CANCELACION IS NOT NUMERIC
           OR CRE-FECHA-CANCELACION = ZEROES
           OR CRE-FECHA-CANCELACION = SPACES
           THEN NEXT SENTENCE
           ELSE MOVE CRE-FECHA-CANCELACION TO D00-FECHA-ULT-PAGO-INT.
           MOVE D00-FECHA-ULT-PAGO-INT TO FECHA-XXXX
           PERFORM MUEVE-FECHA-XXXX.
           MOVE CRE-FECH-CURSE         TO FECHA-YYYY
           PERFORM MUEVE-FECHA-YYYY.
           IF FECHA-XXXX8            < FECHA-YYYY8
      *    IF D00-FECHA-ULT-PAGO-INT < CRE-FECH-CURSE
           THEN MOVE CRE-FECH-CURSE  TO  D00-FECHA-ULT-PAGO-INT.

            IF OPESC-I11 = 3
            THEN MOVE  2            TO     D00-TIPO-CREDITO
            ELSE IF OPESC-I11 = 2
                 THEN MOVE  3            TO     D00-TIPO-CREDITO
                 ELSE IF OPESC-I11 = 1
                      THEN MOVE  4            TO     D00-TIPO-CREDITO
                      ELSE IF OPESC-I11 = 4
                           THEN MOVE  5       TO     D00-TIPO-CREDITO
                           ELSE MOVE  0       TO     D00-TIPO-CREDITO.

            MOVE  0                 TO     D00-NRO-CTAS-ATRAS
            MOVE  OPESC-CTA-COLOCAC TO     D00-CUENTA-CONTABLE
            MOVE CRE-TOTAL-CUOTAS   TO     D00-NRO-CTAS-ORIG
            MOVE WOR-CODIGO         TO     D00-TIPO-CRED-CART-ORIGEN.
            MOVE TAR-FECHA          TO     D00-FECHA-DE-CIERRE.
            ACCEPT FECHA-DATE FROM DATE.
            MOVE CORRESPONDING FECHA-DATER TO FECHA-CURRENTX
            MOVE  FECHA-CURRENTX    TO     FECHA-CURRENT.
      *     MOVE CURRENT-DATE       TO     FECHA-CURRENT.
            MOVE FECHA-CURRENT-AA   TO     D00-FECHA-CREAC-REG-AA
                                           D00-FECHA-ULT-MOD-REG-AA.
            MOVE FECHA-CURRENT-MM   TO     D00-FECHA-CREAC-REG-MM
                                           D00-FECHA-ULT-MOD-REG-MM.
            MOVE FECHA-CURRENT-DD   TO     D00-FECHA-CREAC-REG-DD
                                           D00-FECHA-ULT-MOD-REG-DD.
            MOVE SPACES             TO     D00-AUTOR-CREC-REG
            MOVE SPACES             TO     D00-AUTOR-ULT-MOD-REG
            MOVE ZEROES             TO     D00-FILLER
            PERFORM LIMPIAR VARYING J FROM 1 BY 1 UNTIL J > 9.
            MOVE CRE-RUT-DD         TO     D00-ARR-RUT-DD (1).
            MOVE 'D'                TO     D00-TIPO-DEUDOR(1).
            IF  ( CRE-RUT-DI  = SPACES OR ZEROES )
             OR ( CRE-RUT-DD  = CRE-RUT-DI )
            THEN NEXT SENTENCE
            ELSE MOVE CRE-RUT-DI         TO     D00-ARR-RUT-DD (2)
                 MOVE 'A'                TO     D00-TIPO-DEUDOR(2).
       VALIDAR-CODIGO.
      *----------------
            MOVE CRE-COD-OPER       TO CHEQUEA-CODIGO.
            SET IND-TAR             TO     1
            SEARCH  TAR-COD-EXCLUIBLES
            AT END  MOVE  ' '    TO           D00-IND-DEUDA-EXCLUIBLE
            WHEN CRE-COD-OPER = TAR-COD-EXC (IND-TAR)
                    MOVE  'E'    TO           D00-IND-DEUDA-EXCLUIBLE.
       LIMPIAR.
      *--------
            MOVE SPACES             TO     D00-ARR-RUT-DD  (J).
            MOVE SPACES             TO     D00-TIPO-DEUDOR (J).

       LLENAR-CAMPOS-CALCULOS.
      *.......................
            MOVE CRE-CLAVE               TO  DEV-CLAVE.
            PERFORM LEER-DEV.
            IF WS-ENCONTRADO = 'NO'
            THEN PERFORM CALCULAR-SALDO-CONT
                 PERFORM CALCULAR-REAJ-DEVENGADO
                 MOVE REAJUSTE            TO   D00-REAJ-DEVEN
                 COMPUTE D00-SALDO-CONTABLE = SALDO-CONTABLE - REAJUSTE
            ELSE MOVE DEV-SALDO-FIN-MES   TO  D00-SALDO-CONTABLE
                 MOVE DEV-FECH-CLASIF  TO FECHA-XXXX
                 PERFORM MUEVE-FECHA-XXXX
                 MOVE DEV-FECH-DEVENGO TO FECHA-YYYY
                 PERFORM MUEVE-FECHA-YYYY
                 IF ((DEV-CLASIF = 'C ' OR 'D ') AND
                     (FECHA-XXXX8     < FECHA-YYYY8))
      *              (DEV-FECH-CLASIF < DEV-FECH-DEVENGO))
                 THEN
                     MOVE DEV-REAJUSTES-C-Y-D TO D00-REAJ-DEVEN
                     MOVE DEV-INTERES-C-Y-D   TO D00-INT-POR-COBRAR
                     COMPUTE D00-INT-POR-COBRAR-RIESGO =
                             DEV-INTERES-VENCIDO - DEV-INTERES-C-Y-D
                     COMPUTE D00-REAJ-DEVEN-RIESGO =
                             DEV-REAJUSTE-GANADO - DEV-REAJUSTES-C-Y-D
                 ELSE
                     MOVE DEV-REAJUSTE-GANADO TO  D00-REAJ-DEVEN
                     MOVE DEV-INTERES-VENCIDO TO  D00-INT-POR-COBRAR
                     MOVE ZEROES           TO D00-INT-POR-COBRAR-RIESGO
                     MOVE ZEROES           TO D00-REAJ-DEVEN-RIESGO.
            IF ( OPESC-I16 = 24 )
            THEN MOVE D00-SALDO-CONTABLE TO   D00-SALDO-EN-CART-VENC
            ELSE MOVE ZEROES             TO   D00-SALDO-EN-CART-VENC.

            IF OPESC-I14 = 07 OR 08 OR 09 OR 13
            THEN
                 MOVE  999               TO   D00-BASE-CALCULO-TASA-INT
            ELSE
                 MOVE  100               TO   D00-BASE-CALCULO-TASA-INT.
            IF WS-ENCONTRADO = 'NO'
            THEN IF CRE-FECHA-SUSPEN > ZEROES
                 THEN MOVE ZEROES        TO   D00-INT-POR-COBRAR
                 ELSE PERFORM CALCULAR-INTERES-DEV
                      MOVE INTERES       TO   D00-INT-POR-COBRAR.

            MOVE ZEROES                  TO   D00-INT-POR-COBRAR-VCDOS
                                              D00-SALDO-MOROSO1
                                              D00-SALDO-MOROSO2.
            IF OPESC-I14 = 5
            THEN MOVE WOR-FECH-PROX-VENC TO   D00-FECHA-DE-EXTINCION
      *          IF  WOR-FECH-PROX-VENC  NOT >  TAR-FECHA
      *          AND WOR-FECH-PROX-VENC-AA   > 50
                 IF  WOR-FECH-PROX-VENC8 NOT >  FECHA-TAR8
                 AND CRE-IND-I11         NOT = 3
      *          AND WOR-FECH-PROX-VENC      < WOR-FECH-UDHDM
AHORA *          AND WOR-FECH-PROX-VENC8     < WOR-FECH-UDHDM8
AHORA            AND WOR-FECH-PROX-VENC8 NOT > WOR-FECH-UDHDM8
                 THEN MOVE ZEROES          TO  D00-PLAZO-PROM-PONDERADO
                      MOVE WOR-FECH-PROX-VENC  TO   INICIAL-FECHA
                      MOVE TAR-FECHA           TO   FINAL-FECHA
                      PERFORM CALCULAR-DIAS
                      COMPUTE SALDO-MOROSO ROUNDED =
                                                   D00-SALDO-CONTABLE +
                                                   D00-REAJ-DEVEN     +
                                                   D00-INT-POR-COBRAR
                      PERFORM ASIGNA-SALDO-MOROSO
                      MOVE ZEROES          TO  D00-NRO-CTAS-AMORT-PEND
                                               D00-NRO-CTAS-ATRAS
                 ELSE MOVE ZEROES          TO  D00-NRO-CTAS-ATRAS
                                               D00-NRO-CTAS-AMORT-PEND
                      PERFORM CALC-PLAZOS-PROM-I14-5
            ELSE ADD 1 CRE-NUM-CUOT-PAGADAS GIVING CUOTA-INIC
                 MOVE   0                TO   D00-NRO-CTAS-ATRAS
                                              W-PLAZO-PROM-PONDERADO
                                              DIF-DIAS
                                              SALDO-PASO
                                              D00-NRO-CTAS-AMORT-PEND
                 MOVE  CRE-SALDO-COLOCAC TO   SALDO-PROM
                 MOVE  CRE-RUT-DD        TO   CUO-RUT-DD
                 MOVE  CRE-CODIGO-OPER   TO   CUO-CODIGO-OPER
                 COMPUTE TASA = CRE-TASA-VIGENTE / 100
                 PERFORM PROCESO-DE-CUOTAS.

            IF (( CRE-VIGENCIA = 'N'  AND
                 WOR-FECH-PROX-VENC-AA = TAR-FECHA-AA   AND
                 WOR-FECH-PROX-VENC-MM = TAR-FECHA-MM   AND
                 CRE-FECH-CURSE-AA     = TAR-FECHA-AA   AND
                 CRE-FECH-CURSE-MM     = TAR-FECHA-MM )
                 OR ( OPESC-I16 = 24 ))
                 THEN MOVE ZEROES   TO     D00-PLAZO-PROM-PONDERADO
                                           D00-INT-POR-COBRAR
                                           D00-NRO-CTAS-AMORT-PEND.

            IF WS-ENCONTRADO = 'NO'
            THEN IF OPESC-I16 = 27 OR OPESC-I14 = 3
                 THEN MOVE ZEROES     TO     D00-INT-POR-COBRAR.

            IF  D00-ESTADO-CREDITO  =   3
            THEN  MOVE   ZEROES     TO     D00-SALDO-MOROSO1
                                           D00-SALDO-MOROSO2.
            IF WS-ENCONTRADO = 'NO'
            THEN IF (( OPESC-I11 = 3 )    AND
                     ( TAR-FECHA-MM = 03 OR 06 OR 09 OR 12 ))
                 THEN  MOVE ZEROES       TO   D00-INT-POR-COBRAR.

       PROCESO-DE-CUOTAS.
      *-----------------
           MOVE 'NO'               TO   FIN-CUOTAS
           MOVE 'SI'               TO   PRIMER
           PERFORM START-CUOTAS
           IF  FIN-CUOTAS  =   'NO'
           THEN PERFORM LEER-CUOTAS
                PERFORM LEER-CUOT-CRED
                       UNTIL   CRE-CLAVE  NOT = CUOTA-LECTURA
                       OR       FIN-CUOTAS = 'SI'.

           COMPUTE D00-PLAZO-PROM-PONDERADO ROUNDED  =
                      W-PLAZO-PROM-PONDERADO / CRE-SALDO-COLOCAC.

       CALC-PLAZOS-PROM-I14-5.
      *-----------------------
            MOVE TAR-FECHA             TO     INICIAL-FECHA.
            MOVE WOR-FECH-PROX-VENC    TO     FINAL-FECHA.
            PERFORM CALCULAR-DIAS.
            MOVE  DIF-DIAS             TO     D00-PLAZO-PROM-PONDERADO.

       CALCULAR-REAJ-DEVENGADO.
      *-----------------------
            MOVE CRE-FECH-VENC-ANTERIOR-DD TO FECHA-PASO-DD
            MOVE CRE-FECH-VENC-ANTERIOR-MM TO FECHA-PASO-MM
            MOVE CRE-FECH-VENC-ANTERIOR-AA TO FECHA-PASO-AA
            PERFORM MUEVE-FECHA-PASO
            IF CRE-IND-I9 = 3 OR
      *        FECHA-PASO  >  WOR-FECH-PROX-VENC
               FECHA-PASO8  >  WOR-FECH-PROX-VENC8
            THEN  MOVE ZEROES                 TO   REAJUSTE
            ELSE  COMPUTE REAJUSTE  ROUNDED   =
                              SALDO-CONTABLE  -    SALDO-MAECOL.

       CALCULAR-SALDO-CONT.
      *--------------------

            MOVE CRE-SALDO-COLOCAC  TO SALDO-MAECOL

            IF CRE-IND-I9 = 1
            THEN COMPUTE SALDO-PASO ROUNDED =
                                    SALDO-MAECOL / WORK-UF-CURSE.

            IF CRE-IND-I9 = 2
            THEN COMPUTE SALDO-MAECOL ROUNDED =
                         CRE-SALDO-COLOCAC * WORK-UF-CURSE
                 MOVE CRE-SALDO-COLOCAC  TO SALDO-PASO.

            MOVE CRE-FECH-VENC-ANTERIOR-DD TO FECHA-PASO-DD
            MOVE CRE-FECH-VENC-ANTERIOR-MM TO FECHA-PASO-MM
            MOVE CRE-FECH-VENC-ANTERIOR-AA TO FECHA-PASO-AA
            PERFORM MUEVE-FECHA-PASO

            IF CRE-IND-I9 = 3
      *     OR FECHA-PASO  >  WOR-FECH-PROX-VENC
            OR FECHA-PASO8 >  WOR-FECH-PROX-VENC8
            THEN MOVE SALDO-MAECOL       TO     SALDO-CONTABLE
            ELSE IF CRE-IND-I9 = 1 OR 2
                 THEN COMPUTE SALDO-CONTABLE  ROUNDED =
                              SALDO-PASO      * WORK-UF.

       CALCULAR-INTERES-DEV.
      *---------------------
            MOVE CRE-FECH-VENC-ANTERIOR-DD TO FECHA-PASO-DD
            MOVE CRE-FECH-VENC-ANTERIOR-MM TO FECHA-PASO-MM
            MOVE CRE-FECH-VENC-ANTERIOR-AA TO FECHA-PASO-AA
            PERFORM MUEVE-FECHA-PASO
      *     IF FECHA-PASO  >  TAR-FECHA   OR
      *        FECHA-PASO  >  WOR-FECH-PROX-VENC OR
AHORA *     IF FECHA-PASO8 >  FECHA-TAR8  OR
AHORA *        FECHA-PASO8 >  WOR-FECH-PROX-VENC8 OR
AHORA       IF FECHA-PASO8 NOT < FECHA-TAR8  OR
AHORA          FECHA-PASO8 NOT < WOR-FECH-PROX-VENC8 OR
               CRE-TIPO-INTERES        =  'A'    OR
               (CRE-IND-I11 = 3  AND TRIMESTRE)

            THEN MOVE ZEROES   TO    INTERES
AHORA *     ELSE IF WOR-FECH-PROX-VENC8 > FECHA-TAR8
AHORA       ELSE IF WOR-FECH-PROX-VENC8 NOT < FECHA-TAR8
      *     ELSE IF WOR-FECH-PROX-VENC > TAR-FECHA
      *          OR WOR-FECH-PROX-VENC-AA < 50
                 THEN PERFORM HASTA-FEC-PROCESO
                 ELSE PERFORM HASTA-FEC-PVCTO.

       MUEVE-FECHA-PASO.
      *-----------------
            IF FECHA-PASO-AA < 60
               MOVE 20 TO FECHA-PASO-SS
            ELSE
               MOVE 19 TO FECHA-PASO-SS
            END-IF.
            IF FECHA-PASO = ZEROES MOVE 0 TO FECHA-PASO-SS.
       MUEVE-FECHA-WORK.
      *-----------------
            IF FECHA-WORK-AA < 60
               MOVE 20 TO FECHA-WORK-SS
            ELSE
               MOVE 19 TO FECHA-WORK-SS
            END-IF.
            IF FECHA-WORK = ZEROES MOVE 0 TO FECHA-WORK-SS.
       MUEVE-FECHA-XXXX.
      *-----------------
            IF FECHA-XXXX-AA < 60
               MOVE 20 TO FECHA-XXXX-SS
            ELSE
               MOVE 19 TO FECHA-XXXX-SS
            END-IF.
            IF FECHA-XXXX = ZEROES MOVE 0 TO FECHA-XXXX-SS.
       MUEVE-FECHA-YYYY.
      *-----------------
            IF FECHA-YYYY-AA < 60
               MOVE 20 TO FECHA-YYYY-SS
            ELSE
               MOVE 19 TO FECHA-YYYY-SS
            END-IF.
            IF FECHA-YYYY = ZEROES MOVE 0 TO FECHA-YYYY-SS.
       MUEVE-FECHA-TAR.
      *-----------------
            IF FECHA-TAR-AA < 60
               MOVE 20 TO FECHA-TAR-SS
            ELSE
               MOVE 19 TO FECHA-TAR-SS
            END-IF.
            IF FECHA-TAR = ZEROES MOVE 0 TO FECHA-TAR-SS.
       MUEVE-FECHA-PROX.
      *-----------------
            IF WOR-FECH-PROX-VENC-AA < 60
               MOVE 20 TO WOR-FECH-PROX-VENC-SS
            ELSE
               MOVE 19 TO WOR-FECH-PROX-VENC-SS
            END-IF.
            IF WOR-FECH-PROX-VENC = ZEROES
               MOVE 0 TO WOR-FECH-PROX-VENC-SS.
       MUEVE-FECHA-AUX1.
      *-----------------
            IF AUX1-FECH-ULTIMO-MOV-AA < 60
               MOVE 20 TO AUX1-FECH-ULTIMO-MOV-SS
            ELSE
               MOVE 19 TO AUX1-FECH-ULTIMO-MOV-SS
            END-IF.
            IF AUX1-FECH-ULTIMO-MOV = ZEROES
               MOVE 0 TO AUX1-FECH-ULTIMO-MOV-SS.
       MUEVE-FECHA-AUX2.
      *-----------------
            IF AUX2-FECH-ULTIMO-MOV-AA < 60
               MOVE 20 TO AUX2-FECH-ULTIMO-MOV-SS
            ELSE
               MOVE 19 TO AUX2-FECH-ULTIMO-MOV-SS
            END-IF.
            IF AUX2-FECH-ULTIMO-MOV = ZEROES
               MOVE 0 TO AUX2-FECH-ULTIMO-MOV-SS.

       CALCULAR-DIAS.
      *-------------
           IF INICIAL-FECHA-AA < 60
              MOVE  20               TO    FEC-CALDIAS-INI-SS
                                           INICIAL-FECHA-SS
           ELSE
              MOVE  19               TO    FEC-CALDIAS-INI-SS
                                           INICIAL-FECHA-SS
           END-IF.
           MOVE INICIAL-FECHA-AA     TO    FEC-CALDIAS-INI-AA
           MOVE INICIAL-FECHA-MM     TO    FEC-CALDIAS-INI-MM
           MOVE INICIAL-FECHA-DD     TO    FEC-CALDIAS-INI-DD

           IF FINAL-FECHA-AA     <    60
                 MOVE  20            TO    FEC-CALDIAS-FIN-SS
                                           FINAL-FECHA-SS
           ELSE  MOVE  19            TO    FEC-CALDIAS-FIN-SS
                                           FINAL-FECHA-SS.

           MOVE FINAL-FECHA-AA       TO    FEC-CALDIAS-FIN-AA
           MOVE FINAL-FECHA-MM       TO    FEC-CALDIAS-FIN-MM
           MOVE FINAL-FECHA-DD       TO    FEC-CALDIAS-FIN-DD
           MOVE   ZEROES             TO    DIAS.
      *    IF  FINAL-FECHA > INICIAL-FECHA
      *    OR   FINAL-FECHA-AA < 60
           IF  FINAL-FECHA8 > INICIAL-FECHA8
           CALL 'RUTUCALD' USING FEC-CALDIAS-INI FEC-CALDIAS-FIN DIAS
           ELSE  MOVE    ZEROES                  TO              DIAS.

           IF WOR-FECH-PROX-VENC8 NOT > FECHA-TAR8
      *    IF WOR-FECH-PROX-VENC  NOT > TAR-FECHA
      *    AND WOR-FECH-PROX-VENC-AA > 50
           THEN  MOVE       DIAS               TO     DIF-DIAS
           ELSE  ADD     1  DIAS               GIVING DIF-DIAS.

       HASTA-FEC-PVCTO.
      *----------------
           MOVE WOR-FECH-PROX-VENC              TO FINAL-FECHA.

           IF  CRE-FECH-VENC-ANTERIOR  = ZEROES
           THEN MOVE CRE-FECH-CURSE             TO INICIAL-FECHA
      ***  ELSE IF  CRE-FECH-VENC-ANTERIOR-AA < 40
      ***       THEN
      ***       MOVE CRE-FECH-VENC-ANTERIOR-DD  TO INICIAL-FECHA-AA
      ***       MOVE CRE-FECH-VENC-ANTERIOR-MM  TO INICIAL-FECHA-MM
      ***       MOVE CRE-FECH-VENC-ANTERIOR-AA  TO INICIAL-FECHA-DD
                ELSE
                MOVE CRE-FECH-VENC-ANTERIOR-AA  TO INICIAL-FECHA-AA
                MOVE CRE-FECH-VENC-ANTERIOR-MM  TO INICIAL-FECHA-MM
                MOVE CRE-FECH-VENC-ANTERIOR-DD  TO INICIAL-FECHA-DD.

           IF CRE-IND-I11 = 3
           THEN IF TAR-FECHA-MM = 01  OR 02
                THEN MOVE 31  TO INICIAL-FECHA-DD
                     MOVE 12  TO INICIAL-FECHA-MM
                     IF TAR-FECHA-AA = 0
                        MOVE 99   TO INICIAL-FECHA-AA
                     ELSE
                     SUBTRACT 1 FROM TAR-FECHA-AA GIVING
                                 INICIAL-FECHA-AA

                ELSE IF TAR-FECHA-MM = 04  OR 05
                     THEN MOVE 31  TO INICIAL-FECHA-DD
                          MOVE 03  TO INICIAL-FECHA-MM
                          MOVE TAR-FECHA-AA  TO INICIAL-FECHA-AA

                     ELSE IF TAR-FECHA-MM = 07  OR 08
                          THEN MOVE 30  TO INICIAL-FECHA-DD
                               MOVE 06  TO INICIAL-FECHA-MM
                               MOVE TAR-FECHA-AA  TO INICIAL-FECHA-AA

                          ELSE IF TAR-FECHA-MM = 10  OR 11
                               THEN MOVE 30  TO INICIAL-FECHA-DD
                                    MOVE 09  TO INICIAL-FECHA-MM
                                    MOVE TAR-FECHA-AA
                                             TO INICIAL-FECHA-AA.


           IF CRE-IND-I14 = 13
           THEN MOVE CRE-FECH-ULT-PAGO-INT-DD  TO INICIAL-FECHA-DD
                MOVE CRE-FECH-ULT-PAGO-INT-MM  TO INICIAL-FECHA-MM
                MOVE CRE-FECH-ULT-PAGO-INT-AA  TO INICIAL-FECHA-AA.

           MOVE INICIAL-FECHA      TO FECHA-XXXX
           PERFORM MUEVE-FECHA-XXXX
           MOVE CRE-FECH-CURSE     TO FECHA-YYYY
           PERFORM MUEVE-FECHA-YYYY
           IF FECHA-XXXX8    < FECHA-YYYY8
      *    IF INICIAL-FECHA < CRE-FECH-CURSE
           THEN MOVE CRE-FECH-CURSE   TO  INICIAL-FECHA.

           PERFORM CALCULAR-DIAS.
           PERFORM CALCULAR-INT-DEVEN.

       HASTA-FEC-PROCESO.
      *------------------
      *    MOVE TAR-FECHA                       TO FINAL-FECHA.
           MOVE FECHA-TAR8 TO FINAL-FECHA8.
           IF  CRE-FECH-VENC-ANTERIOR = ZEROES
           THEN MOVE CRE-FECH-CURSE             TO INICIAL-FECHA
      ***  ELSE IF  CRE-FECH-VENC-ANTERIOR-AA < 40
      ***       THEN
      ***       MOVE CRE-FECH-VENC-ANTERIOR-DD  TO INICIAL-FECHA-AA
      ***       MOVE CRE-FECH-VENC-ANTERIOR-MM  TO INICIAL-FECHA-MM
      ***       MOVE CRE-FECH-VENC-ANTERIOR-AA  TO INICIAL-FECHA-DD
                ELSE
                MOVE CRE-FECH-VENC-ANTERIOR-AA  TO INICIAL-FECHA-AA
                MOVE CRE-FECH-VENC-ANTERIOR-MM  TO INICIAL-FECHA-MM
                MOVE CRE-FECH-VENC-ANTERIOR-DD  TO INICIAL-FECHA-DD.

           IF CRE-IND-I11 = 3
           THEN IF TAR-FECHA-MM = 01  OR 02
                THEN MOVE 31  TO INICIAL-FECHA-DD
                     MOVE 12  TO INICIAL-FECHA-MM
                     IF TAR-FECHA-AA = 0
                        MOVE 99   TO INICIAL-FECHA-AA
                     ELSE
                     SUBTRACT 1 FROM TAR-FECHA-AA GIVING
                                 INICIAL-FECHA-AA

                ELSE IF TAR-FECHA-MM = 04  OR 05
                     THEN MOVE 31  TO INICIAL-FECHA-DD
                          MOVE 03  TO INICIAL-FECHA-MM
                          MOVE TAR-FECHA-AA  TO INICIAL-FECHA-AA

                     ELSE IF TAR-FECHA-MM = 07  OR 08
                          THEN MOVE 30  TO INICIAL-FECHA-DD
                               MOVE 06  TO INICIAL-FECHA-MM
                               MOVE TAR-FECHA-AA  TO INICIAL-FECHA-AA

                          ELSE IF TAR-FECHA-MM = 10  OR 11
                               THEN MOVE 30  TO INICIAL-FECHA-DD
                                    MOVE 09  TO INICIAL-FECHA-MM
                                    MOVE TAR-FECHA-AA
                                             TO INICIAL-FECHA-AA.


           IF CRE-IND-I14 = 13
           THEN MOVE CRE-FECH-ULT-PAGO-INT-DD  TO INICIAL-FECHA-DD
                MOVE CRE-FECH-ULT-PAGO-INT-MM  TO INICIAL-FECHA-MM
                MOVE CRE-FECH-ULT-PAGO-INT-AA  TO INICIAL-FECHA-AA.
           IF INICIAL-FECHA-AA < 60
               MOVE 20 TO INICIAL-FECHA-SS
           ELSE
               MOVE 19 TO INICIAL-FECHA-SS
           END-IF.
           IF INICIAL-FECHA = ZEROES MOVE 00 TO INICIAL-FECHA-SS.
           MOVE CRE-FECH-CURSE TO FECHA-XXXX
           PERFORM MUEVE-FECHA-XXXX
           IF INICIAL-FECHA8 < FECHA-XXXX8
      *    IF INICIAL-FECHA < CRE-FECH-CURSE
           THEN MOVE CRE-FECH-CURSE   TO  INICIAL-FECHA.

           PERFORM CALCULAR-DIAS.
           PERFORM CALCULAR-INT-DEVEN.

       CALCULAR-INT-DEVEN.
      *-------------------
           COMPUTE TASA ROUNDED = CRE-TASA-VIGENTE / 100
           COMPUTE TASA ROUNDED = (DIF-DIAS / 30) * TASA

           COMPUTE INTERES ROUNDED = TASA * SALDO-CONTABLE.
       PROBLEMA.
      *---------
           ADD   1            TO        CONT-PROBLEMAS.

       GRABAR-REGISTRO-C5.
      *...................
            IF D00-BASE-CALCULO-TASA-INT  = 999
                PERFORM BASE-CALCULO-TASA-INT-VAR
                 MOVE WSS-IND-TTAS       TO   D00-BASE-CALCULO-TASA-INT
            ELSE
                PERFORM BASE-CALCULO-TASA-INT-FIJ
                 MOVE WSS-IND-TTAS       TO   D00-BASE-CALCULO-TASA-INT.
           IF   D00-SALDO-CONTABLE        = ZEROES
           AND  D00-SALDO-MOROSO1         = ZEROES
           AND  D00-SALDO-MOROSO2         = ZEROES
           AND  D00-SALDO-EN-CART-VENC    = ZEROES
           AND  D00-SALDO-REPROGRAMADO    = ZEROES
           AND  D00-INT-POR-COBRAR        = ZEROES
           AND  D00-INT-POR-COBRAR-VCDOS  = ZEROES
           AND  D00-INT-POR-COBRAR-RIESGO = ZEROES
           AND  D00-REAJ-DEVEN            = ZEROES
           AND  D00-REAJ-DEVEN-RIESGO     = ZEROES
           THEN NEXT SENTENCE
           ELSE WRITE D00-REGISTRO
                IF ( D00-STATUS  NOT = 0 )
                THEN  MOVE  '   GRABAR CIRC5       '  TO  MENS-PROC
                      MOVE  'D00-CIR5MD00'            TO  MENS-ARCH
                      MOVE  D00-STATUS                  TO  MENS-STAT
                      PERFORM  ERROR-VSAM
                ELSE  ADD 1 TO CONT-GRABADOS-C5.

       LEER-CREDITOS.
      *..............
           READ CRE-CREDITOS NEXT.
           IF ( CRE-STATUS  = 10 )
           THEN MOVE 'SI' TO   FIN-CREDITOS
           ELSE IF ( CRE-STATUS  NOT = 0 )
                      MOVE  'A1-LEER-CREDITOS      '  TO  MENS-PROC
                      MOVE  'CRE-CREDITOS'            TO  MENS-ARCH
                      MOVE  CRE-STATUS                TO  MENS-STAT
                      PERFORM  ERROR-VSAM
                ELSE  ADD 1 TO CONT-LEIDOS-CR
                      MOVE CRE-FECH-PROX-VENC-DD  TO
                                               WOR-FECH-PROX-VENC-DD
                      MOVE CRE-FECH-PROX-VENC-MM  TO
                                               WOR-FECH-PROX-VENC-MM
                      MOVE CRE-FECH-PROX-VENC-AA  TO
                                               WOR-FECH-PROX-VENC-AA
                      PERFORM MUEVE-FECHA-PROX
                      IF CRE-FECH-VENC-ANTERIOR NOT NUMERIC
                      THEN MOVE CRE-FECH-ULTIMO-MOV-DD
                                      TO AUX1-FECH-ULTIMO-MOV-DD
                                         AUX2-FECH-ULTIMO-MOV-DD
                           MOVE CRE-FECH-ULTIMO-MOV-MM
                                      TO AUX1-FECH-ULTIMO-MOV-MM
                                         AUX2-FECH-ULTIMO-MOV-MM
                           MOVE CRE-FECH-ULTIMO-MOV-AA
                                      TO AUX1-FECH-ULTIMO-MOV-AA
                                         AUX2-FECH-ULTIMO-MOV-AA
                           PERFORM MUEVE-FECHA-AUX1
                           PERFORM MUEVE-FECHA-AUX2

      *                    IF AUX2-FECH-ULTIMO-MOV >
      *                       CRE-FECH-PROX-VENC
                           IF AUX2-FECH-ULTIMO-MOV8 >
                              WOR-FECH-PROX-VENC8
                           THEN MOVE CRE-FECH-PROX-VENC-DD
                                     TO CRE-FECH-VENC-ANTERIOR-DD
                                MOVE CRE-FECH-PROX-VENC-MM
                                     TO CRE-FECH-VENC-ANTERIOR-MM
                                MOVE CRE-FECH-PROX-VENC-AA
                                     TO CRE-FECH-VENC-ANTERIOR-AA
                           ELSE MOVE AUX1-FECH-ULTIMO-MOV
                                     TO CRE-FECH-VENC-ANTERIOR.


       LEER-OPERACIONES.
      *.................
           READ OPE-OPERACIONES.
           IF (OPE-STATUS = 23 )
           THEN MOVE ZEROES               TO   OPESC-CTA-COLOCAC
                                               OPESC-CTA-DE-CAJA
                ADD   1                   TO   CONT-OPE-ERR
           ELSE IF ( OPE-STATUS  NOT = 0 )
                THEN  MOVE  '   LEER-OPERACIONES   '  TO  MENS-PROC
                MOVE  'OPE-OPERACIO'                  TO  MENS-ARCH
                MOVE  OPE-STATUS                      TO  MENS-STAT
                PERFORM  ERROR-VSAM
                ELSE  ADD 1 TO CONT-LEIDOS-OP.


       LEER-UF.
      *........
           READ UF-UEFES.
           IF (UF-STATUS = 23 )
           THEN MOVE ZEROES               TO   UF-VALOR
                ADD   1                   TO   CONT-UF-ERR
           ELSE IF ( UF-STATUS  NOT = 0 )
                THEN  MOVE  '   LEER-UEFES         '  TO  MENS-PROC
                MOVE  'UF-UEFES    '                  TO  MENS-ARCH
                MOVE  UF-STATUS                       TO  MENS-STAT
                PERFORM  ERROR-VSAM
                ELSE  ADD 1 TO CONT-LEIDOS-UF.

       LEER-TARJETA.
      *.............
           READ TAR-TARJETA.
           DISPLAY 'FECHA AAMMDD  ' TAR-FECHA
           DISPLAY 'FECHA AAMMDD  ' TAR-FECHA UPON CONSOLE.

           MOVE 'CARGAR'                TO CALEAREA-FUNCION.
           MOVE ALL '0'                 TO CALEAREA-FECHA-1.
           PERFORM BC-CALL-RTNUCALE.
           IF (CALEAREA-COD-RET = 0 AND CALEAREA-FUNCION NOT = 'CANCEL')
               IF TAR-FECHA-AA < 60
                  MOVE 20 TO CALEAREA-FECHA-1-AM
               ELSE
                  MOVE 19 TO CALEAREA-FECHA-1-AM
               END-IF
               MOVE TAR-FECHA-AA        TO CALEAREA-FECHA-1-AA
               MOVE TAR-FECHA-MM        TO CALEAREA-FECHA-1-M
               MOVE TAR-FECHA-DD        TO CALEAREA-FECHA-1-D
               MOVE 'BUDHM'             TO CALEAREA-FUNCION
               MOVE ALL '0'             TO CALEAREA-FECHA-2
               PERFORM BC-CALL-RTNUCALE
               IF (CALEAREA-COD-RET = 0)
                   MOVE CALEAREA-FECHA-2-AA TO WOR-FECH-UDHDM-AA
                   MOVE CALEAREA-FECHA-2-M  TO WOR-FECH-UDHDM-MM
                   MOVE CALEAREA-FECHA-2-D  TO WOR-FECH-UDHDM-DD.
           MOVE TAR-FECHA        TO FECHA-TAR.
           IF FECHA-TAR-AA < 60
              MOVE 20 TO FECHA-TAR-SS
           ELSE
              MOVE 19 TO FECHA-TAR-SS
           END-IF.
           IF FECHA-TAR = ZEROES MOVE 0 TO FECHA-TAR-SS.
           IF WOR-FECH-UDHDM-AA < 60
              MOVE 20 TO WOR-FECH-UDHDM-SS
           ELSE
              MOVE 19 TO WOR-FECH-UDHDM-SS
           END-IF.
           IF WOR-FECH-UDHDM = ZEROES MOVE 0 TO WOR-FECH-UDHDM-SS.
           DISPLAY 'ULTIMO DIA HABIL DEL MES : ' WOR-FECH-UDHDM8.
           DISPLAY 'FECHA DEL PROCESO        : ' FECHA-TAR8.

       BC-CALL-RTNUCALE.
      *-----------------
           CALL 'RUTUCALE' USING CALEAREA.
           IF (CALEAREA-COD-RET NOT = 0 OR CALEAREA-FUNCION = 'CANCEL')
               MOVE 'BC-CALL-RTNUCALE' TO MENS-PROC
               MOVE 'RUTUCALE'         TO MENS-ARCH
               MOVE 'ERROR EN RUTINA'  TO MENS-CLAVE
               MOVE CALEAREA-COD-RET   TO MENS-STAT
               MOVE 1                  TO MENS-SW
               PERFORM  ERROR-VSAM.

       START-CUOTAS.
      *.............
           MOVE  CRE-SUCURSAL               TO CUO-SUCURSAL.
           MOVE  CRE-CODIGO-OPER            TO CUO-CODIGO-OPER.
           MOVE  CRE-RUT-DD                 TO CUO-RUT-DD.
           MOVE  ZEROES                     TO CUO-NUM-ABONO.
           START CUO-CUOTAS KEY IS NOT < CUO-CLAVE.
           IF ( CUO-STATUS  NOT = 0 )
           MOVE 'SI'                        TO FIN-CUOTAS.


       LEER-CUOTAS.
      *............
           READ CUO-CUOTAS NEXT RECORD.
           IF ( CUO-STATUS  = 10 )
           THEN  MOVE   'SI'         TO      FIN-CUOTAS
           ELSE    IF ( CUO-STATUS  NOT = 0 )
                    THEN  MOVE  '   LEER-CUOTAS ' TO  MENS-PROC
                          MOVE  'CUO-CUOTAS  '    TO  MENS-ARCH
                          MOVE  CUO-STATUS        TO  MENS-STAT
                          PERFORM  ERROR-VSAM
                    ELSE  ADD 1 TO CONT-LEIDOS-CU
                          MOVE CUO-SUCURSAL       TO CUOTA-LEC-SUC
                          MOVE CUO-CODIGO-OPER    TO CUOTA-LEC-COD
                          MOVE CUO-RUT-DD         TO CUOTA-LEC-RUT.




       ERROR-VSAM.
      *...........
           DISPLAY  BLANCO
           DISPLAY  BLANCO '*** ERROR VSAM  ERROR VSAM ***'
           DISPLAY  BLANCO
           DISPLAY  BLANCO 'PROCEDIMIENTO   :  ' MENS-PROC
           DISPLAY  BLANCO 'ARCHIVO         :  ' MENS-ARCH
           DISPLAY  BLANCO 'FILE-STATUS     :  ' MENS-STAT

           DISPLAY  BLANCO
           IF (MENS-SW = 0)
           THEN DISPLAY BLANCO  '*** EL PROCESO CONTINUA ***'
           ELSE DISPLAY  BLANCO '*** SE CANCELA EL PROGRAMA ***'
                DISPLAY  BLANCO '*** ERROR VSAM  ERROR VSAM ***'
                DISPLAY  BLANCO '*** SE CANCELA EL PROGRAMA ***'
                                                 UPON CONSOLE
                DISPLAY  BLANCO '*** ERROR VSAM  ERROR VSAM ***'
                                                 UPON CONSOLE
                MOVE  1   TO   MENS-SW
                MOVE  100   TO RETURN-CODE
                IF (MENS-PROC  NOT =  'CERRAR')
                THEN  PERFORM  CERRAR
                PERFORM  ESTADISTICAS
                STOP RUN.
           EJECT
      *

       ESTADISTICAS.
      *.............
           DISPLAY BLANCO
           DISPLAY BLANCO    ' ESTADISTICAS PROCESO'
           DISPLAY BLANCO '_____________________________'
           DISPLAY BLANCO
           DISPLAY BLANCO 'LEIDOS CRE-CREDITOS  : ' CONT-LEIDOS-CR.
           DISPLAY BLANCO 'CRED. NO PROCESADOS  : ' CONT-NO-PROC.
           DISPLAY BLANCO 'LEIDOS DEV-DEVEN     : ' CONT-LEIDOS-DEV.
           DISPLAY BLANCO 'LEIDOS MAE-OPERAC    : ' CONT-LEIDOS-OP.
           DISPLAY BLANCO 'LEIDOS UF-UEFES      : ' CONT-LEIDOS-UF.
           DISPLAY BLANCO 'ERROR OPERAC         : ' CONT-OPE-ERR.
           DISPLAY BLANCO 'ERROR UF             : ' CONT-UF-ERR.
           DISPLAY BLANCO 'ERROR CREDITOS       : ' CONT-ERROR-CR.
           DISPLAY BLANCO 'PROBLEMAS            : ' CONT-PROBLEMAS.

       BASE-CALCULO-TASA-INT-FIJ.

           MOVE 0 TO DIGITO-2.
           MOVE 1 TO DIGITO-1.
           PERFORM DIF-DIA-FCOL-FMAV.
           PERFORM DET-DIGITO-3.

       BASE-CALCULO-TASA-INT-VAR.

           MOVE 2 TO DIGITO-1.
           PERFORM LEER-ICG.
           IF NOT ( ICG-RET-COD = SPACES AND
              ICG-VAL-KEY-1 = WSS-OLD-IOPC )
               MOVE 8 TO DIGITO-2
               MOVE 8 TO DIGITO-3
           ELSE
           IF ICG-COD-CVTI IN ICG = 'TAB' OR
              ICG-COD-CVTI IN ICG = 'TBA' OR
              ICG-COD-CVTI IN ICG = 'TBS' OR
              ICG-COD-CVTI IN ICG = 'TBT'
               MOVE 1 TO DIGITO-2
           ELSE
           IF ICG-COD-CVTI IN ICG = 'TIP'
               MOVE 2 TO DIGITO-2
           ELSE
           IF ICG-COD-CVTI IN ICG = 'LIB'
               MOVE 3 TO DIGITO-2
           ELSE
           IF ICG-COD-CVTI IN ICG = 'PRI' OR
              ICG-COD-CVTI IN ICG = 'PRA' OR
              ICG-COD-CVTI IN ICG = 'PRB' OR
              ICG-COD-CVTI IN ICG = 'PRC' OR
              ICG-COD-CVTI IN ICG = 'PRD'
               MOVE 4 TO DIGITO-2
           ELSE
               MOVE 9 TO DIGITO-2.

           IF ICG-RET-COD = SPACES AND
              ICG-VAL-KEY-1 = WSS-OLD-IOPC
               IF ICG-MSC-TFPR IN ICG = 'F'
                   PERFORM DET-DIG-TRES-V-F
               ELSE
                   PERFORM DET-DIG-TRES-V-C.

       DET-DIG-TRES-V-F.

           PERFORM LEER-EVC.
           IF NOT ( EVC-RET-COD = SPACES AND
              EVC-VAL-KEY-1 = WSS-OLD-IOPC )
               MOVE 8 TO DIGITO-3
           ELSE
           PERFORM LEER-NXT-EVC UNTIL NOT (EVC-RET-COD = SPACES AND
                                  EVC-VAL-KEY-1 = WSS-OLD-IOPC).

           IF WSS-FEC-NDIA > ZEROES AND
              WSS-FEC-NDIA NOT = 999999
               PERFORM DET-DIGITO-3
           ELSE
               PERFORM DIF-DIA-FCOL-FMAV
               PERFORM DET-DIGITO-3.

       DET-DIG-TRES-V-C.

           IF ICG-MSC-UTPR IN ICG =  'M'
               COMPUTE ICG-NUM-PREP = ICG-NUM-PREP * 30
           ELSE
           IF ICG-MSC-UTPR IN ICG =  'A'
               COMPUTE ICG-NUM-PREP = ICG-NUM-PREP * 365.
           PERFORM SUMA-DIAS.
           PERFORM DIF-DIA-FCOL-FPO2.
           PERFORM DET-DIGITO-3.

       DET-DIGITO-3.

           IF WSS-FEC-NDIA < 30
               MOVE 1 TO DIGITO-3
           ELSE
           IF WSS-FEC-NDIA < 90
               MOVE 2 TO DIGITO-3
           ELSE
           IF WSS-FEC-NDIA NOT < 90 AND
              WSS-FEC-NDIA < 180
               MOVE 3 TO DIGITO-3
           ELSE
           IF WSS-FEC-NDIA NOT < 180 AND
              WSS-FEC-NDIA < 365
               MOVE 4 TO DIGITO-3
           ELSE
           IF WSS-FEC-NDIA NOT < 365 AND
              WSS-FEC-NDIA < 1095
               MOVE 5 TO DIGITO-3
           ELSE
               MOVE 6 TO DIGITO-3.

       LEER-EVC.
      *--------*
           MOVE 999999       TO WSS-FEC-NDIA.
           MOVE D00-NUM-INTERNO-IDENT TO WSS-OLD-IOPC.
           MOVE LOW-VALUE    TO EVC-VAL-KEY.
           MOVE WSS-OLD-IOPC TO EVC-VAL-KEY-1.
           MOVE 'REDKG'      TO EVC-COMMAND.
           PERFORM LEER-EVC-DTC.
           IF EVC-RET-COD   = SPACES AND
              EVC-VAL-KEY-1 = WSS-OLD-IOPC
               PERFORM DET-SEPA
               IF WSS-FEC-NDIA-AUX < WSS-FEC-NDIA AND
                  WSS-FEC-NDIA-AUX > ZEROES
                   MOVE WSS-FEC-NDIA-AUX TO WSS-FEC-NDIA.
       LEER-NXT-EVC.
      *--------*
           MOVE 'REDNX'      TO EVC-COMMAND.
           PERFORM LEER-EVC-DTC.
           IF EVC-RET-COD   = SPACES AND
              EVC-VAL-KEY-1 = WSS-OLD-IOPC
               PERFORM DET-SEPA
               IF WSS-FEC-NDIA-AUX < WSS-FEC-NDIA AND
                  WSS-FEC-NDIA-AUX > ZEROES
                   MOVE WSS-FEC-NDIA-AUX TO WSS-FEC-NDIA.
       DET-SEPA.
      *--------*
           MOVE ZEROES       TO WSS-FEC-NDIA-AUX.
           IF EVC-IND-UTSV = 'D'
               MOVE EVC-NUM-SEVE TO WSS-FEC-NDIA-AUX
           ELSE
           IF EVC-IND-UTSV = 'M'
               COMPUTE WSS-FEC-NDIA-AUX = EVC-NUM-SEVE * 30
           ELSE
           IF EVC-IND-UTSV = 'A'
               COMPUTE WSS-FEC-NDIA-AUX = EVC-NUM-SEVE * 365.

       LEER-EVC-DTC.
      *------------*
           CALL 'DBNTRY' USING UIB REQ-AREA-EVC REG-EVC ELEM-LIST-EVC.

       LEER-ICG.
      *--------*

           MOVE D00-NUM-INTERNO-IDENT TO WSS-OLD-IOPC.
           MOVE LOW-VALUE TO ICG-VAL-KEY
           MOVE WSS-OLD-IOPC TO ICG-VAL-KEY-1.
           MOVE ZEROES       TO ICG-VAL-KEY-2.
           MOVE SPACES       TO ICG-VAL-KEY-3.
           MOVE ZEROES       TO ICG-VAL-KEY-4.
           MOVE 'INT'        TO ICG-VAL-KEY-5.
           MOVE 'REDKG' TO ICG-COMMAND
           CALL 'DBNTRY' USING UIB REQ-AREA-ICG REG-ICG ELEM-LIST-ICG.



       SUMA-DIAS.
      *--------*

           MOVE 'AVNDH'         TO CALEAREA-FUNCION.

           MOVE D00-FECHA-OTORGAM-AA TO CALEAREA-FECHA-1-AA.
           MOVE D00-FECHA-OTORGAM-MM TO CALEAREA-FECHA-1-M.
           MOVE D00-FECHA-OTORGAM-DD TO CALEAREA-FECHA-1-D.

           IF D00-FECHA-OTORGAM-AA > 60
               MOVE 19 TO CALEAREA-FECHA-1-AM
           ELSE
               MOVE 20 TO CALEAREA-FECHA-1-AM.
           MOVE  ICG-NUM-PREP   TO CALEAREA-NUM-DIA.

           CALL 'RUTUCALE' USING CALEAREA.

           MOVE CALEAREA-FECHA-2-AM TO WSS-NUM-SPO2.
           MOVE CALEAREA-FECHA-2-AA TO WSS-NUM-APO2.
           MOVE CALEAREA-FECHA-2-M  TO WSS-NUM-MPO2.
           MOVE CALEAREA-FECHA-2-D  TO WSS-NUM-DPO2.

       DIF-DIA-FCOL-FVEN.
      *-----------------*

           MOVE 'CDCEF' TO CALEAREA-FUNCION.

           MOVE D00-FECHA-OTORGAM-AA TO CALEAREA-FECHA-1-AA.
           MOVE D00-FECHA-OTORGAM-MM TO CALEAREA-FECHA-1-M.
           MOVE D00-FECHA-OTORGAM-DD TO CALEAREA-FECHA-1-D.

           IF D00-FECHA-OTORGAM-AA > 60
               MOVE 19 TO CALEAREA-FECHA-1-AM
           ELSE
               MOVE 20 TO CALEAREA-FECHA-1-AM.



           CALL 'RUTUCALE' USING CALEAREA.

           IF CALEAREA-COD-RET = 0
               MOVE CALEAREA-NUM-DIA TO WSS-FEC-NDIA.

       DIF-DIA-FCOL-FPO2.
      *-----------------*

           MOVE 'CDCEF'      TO CALEAREA-FUNCION.

           MOVE D00-FECHA-OTORGAM-AA TO CALEAREA-FECHA-1-AA.
           MOVE D00-FECHA-OTORGAM-MM TO CALEAREA-FECHA-1-M.
           MOVE D00-FECHA-OTORGAM-DD TO CALEAREA-FECHA-1-D.

           IF D00-FECHA-OTORGAM-AA > 60
               MOVE 19 TO CALEAREA-FECHA-1-AM
           ELSE
               MOVE 20 TO CALEAREA-FECHA-1-AM.

           MOVE WSS-NUM-DPO2 TO CALEAREA-FECHA-2-D.
           MOVE WSS-NUM-MPO2 TO CALEAREA-FECHA-2-M.
           MOVE WSS-NUM-SPO2 TO CALEAREA-FECHA-2-AM.
           MOVE WSS-NUM-APO2 TO CALEAREA-FECHA-2-AA.

           CALL 'RUTUCALE' USING CALEAREA.

           IF CALEAREA-COD-RET = 0
               MOVE CALEAREA-NUM-DIA TO WSS-FEC-NDIA.

       DIF-DIA-FCOL-FMAV.
      *-----------------*

           MOVE 'CDCEF'      TO CALEAREA-FUNCION.

           MOVE D00-FECHA-OTORGAM-AA TO CALEAREA-FECHA-1-AA.
           MOVE D00-FECHA-OTORGAM-MM TO CALEAREA-FECHA-1-M.
           MOVE D00-FECHA-OTORGAM-DD TO CALEAREA-FECHA-1-D.

           IF D00-FECHA-OTORGAM-AA > 60
               MOVE 19 TO CALEAREA-FECHA-1-AM
           ELSE
               MOVE 20 TO CALEAREA-FECHA-1-AM.


           MOVE D00-FECHA-DE-EXTINCION-AA TO CALEAREA-FECHA-2-AA.
           MOVE D00-FECHA-DE-EXTINCION-MM TO CALEAREA-FECHA-2-M.
           MOVE D00-FECHA-DE-EXTINCION-DD TO CALEAREA-FECHA-2-D.

           IF D00-FECHA-DE-EXTINCION-AA > 60
               MOVE 19 TO CALEAREA-FECHA-2-AM
           ELSE
               MOVE 20 TO CALEAREA-FECHA-2-AM.

           CALL 'RUTUCALE' USING CALEAREA.
           IF CALEAREA-COD-RET = 0
               MOVE CALEAREA-NUM-DIA TO WSS-FEC-NDIA.
