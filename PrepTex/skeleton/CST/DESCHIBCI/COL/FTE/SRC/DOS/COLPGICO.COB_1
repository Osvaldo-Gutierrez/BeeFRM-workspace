       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGICO.
       AUTHOR.          BEE TEAM (EX-CONSIST).
       DATE-WRITTEN.
      * Programa Seleccionador de Cancelaciones para el Archivo ICO.
      * Autor: RBG / 13-DEC-1996.
      *
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-COM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.

      *<<<< WSS
       COPY COLWGINT.
       COPY COLWGVEN.
       COPY COLBRCOM.
       COPY COLBRICG.
       COPY SGCBRDBC.
       COPY COLBRDLC.
       COPY COLBRRCV.
       COPY COLBRRDC.
       COPY COLBRCYA.
       COPY SGCBRDCP.
       COPY SGCBRDCE.
       COPY COLBROPC.
       COPY COLBRVEN.
       COPY COLBRCAN.
       COPY COLBRICO.
       COPY COLBRICV.
       COPY COLBRTOC.
       COPY TABBRTIO.
       COPY TABBRCAM.
       COPY TABBRUSR.
       COPY COLBREVC.
       COPY GNSWGCPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.

       01  WSS-VARI.
           03  WSS-NUM-RLEI           VALUE ZEROES   PIC 9(06).
           03  WSS-NUM-RGRA           VALUE ZEROES   PIC 9(06).
           03  WSS-OPEC                              PIC X(01).
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO                      PIC 9(02).
               05  WSS-NUM-MPRO                      PIC 9(02).
               05  WSS-NUM-SPRO                      PIC 9(02).
               05  WSS-NUM-APRO                      PIC 9(02).
           03  WSS-FEC-FPRI.
               05  WSS-NUM-SPRI                      PIC 9(02).
               05  WSS-NUM-APRI                      PIC 9(02).
               05  WSS-NUM-MPRI                      PIC 9(02).
               05  WSS-NUM-DPRI                      PIC 9(02).
           03  WSS-FEC-FTOP.
               05  WSS-NUM-STOP       VALUE ZEROES   PIC 9(02).
               05  WSS-NUM-ATOP       VALUE ZEROES   PIC 9(02).
               05  WSS-NUM-MTOP       VALUE ZEROES   PIC 9(02).
               05  WSS-NUM-DTOP       VALUE ZEROES   PIC 9(02).
      * Variable para desplegar o no los Display del Programa.
           03  WSS-IND-DISP           VALUE 'S'       PIC X(01).
      *
           03  WSS-MSC-TVOC                           PIC X(06).
           03  WSS-COD-PESO                           PIC X(06).
           03  WSS-GLS-ANO.
               05  WSS-GLS-ANO1                       PIC X(01).
               05  WSS-GLS-ANO2                       PIC X(01).
           03  WSS-GLS-DATA.
               05 WSS-GLS-DIAS                        PIC X(03).
               05  WSS-NUM-DIAS       REDEFINES
                   WSS-GLS-DIAS                       PIC 9(03).
               05 WSS-COD-MONE                        PIC X(06).
               05 WSS-GLS-MOUF                        PIC X(07).
               05  WSS-VAL-MOUF       REDEFINES
                   WSS-GLS-MOUF                       PIC 9(03)V9(04).
               05 WSS-GLS-TOPE                        PIC X(03).
               05  WSS-NUM-TOPE       REDEFINES
                   WSS-GLS-TOPE                       PIC 9(03).
               05 WSS-COD-CCOD        OCCURS 08       PIC X(05).
           03  WSS-GLS-DAT3           VALUE SPACES    PIC X(60).
           03  WSS-TBAN REDEFINES
                   WSS-GLS-DAT3       OCCURS 20       PIC X(03).
           03  MOV-I                                  PIC 9(02).
           03  WSS-COD-BANC                           PIC X(03).
           03  WSS-IND-MORA                           PIC X(01).
           03  WSS-IND-EBAN                           PIC X(01).
           03  WSS-IND-EXIS                           PIC X(01).
           03  WSS-SGV-VCAM                           PIC S9(11)V9(04).
           03  WSS-GLS-DAT2.
               05 WSS-GLS-PIVA                         PIC X(10).
               05 WSS-NUM-PIVA        REDEFINES
                  WSS-GLS-PIVA                         PIC  9(06)V9(04).
           03  WSS-VAL-IVA     COMP-3 VALUE ZEROES     PIC  9(02)V9(04).
           03  WSS-SGV-TCOM    COMP-3 VALUE ZEROES     PIC S9(11)V9(05).
           03  WSS-COD-CCCC           VALUE SPACES     PIC X(02).
           03  WSS-SGV-NETO    COMP-3 VALUE ZEROES     PIC S9(11)V9(04).
           03  WSS-SGV-MIVA    COMP-3 VALUE ZEROES     PIC S9(09)V9(04).
           03  WSS-NUM-TOP1                            PIC 9(03).
           03  WSS-NUM-1HBL           VALUE ZEROES     PIC 9(02).
           03  WSS-IND-FERI                            PIC X(01).
           03  WSS-NUM-CONT           VALUE ZEROES     PIC 9(02).
           03  WSS-NUM-CON1           VALUE ZEROES     PIC 9(06).
           03  WSS-COD-CAN                             PIC X(03).
           03  WSS-COD-SVCD           VALUE LOW-VALUES PIC X(08).
           03  WSS-IND-CHEK           VALUE 'N'        PIC X(01).
           03  WSS-CIC-IOPC           VALUE SPACES     PIC X(12).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGICO' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM INI-VAR.
           PERFORM PRO-CAN.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
      ******************************************************************
       OPEN-FILE SECTION.
      *******************
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-ICV.
           MOVE 'COL' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE 'TAB' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-ICO.
       FIN-OPEN-FILE.
           EXIT.

      ******************************************************************
       INI-VAR SECTION.
      *****************
       INI-INI-VAR.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS '.
           DISPLAY '============================ '.
      * Captura Fecha de Proceso.
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY ' '.
           DISPLAY 'FECHA DE PROCESO (*DDMMSSAA): ' WSS-FEC-FPRO
                                                            IN WSS-VARI.
      * Captura Indicador para desplegar los Display en el programa.
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY 'INDICADOR DE DISPLAY (S/N)  : ' WSS-IND-DISP
                                                            IN WSS-VARI.
      * Valida codigo de display (S/N).
           IF WSS-IND-DISP IN WSS-VARI NOT = ('S' AND 'N')
               DISPLAY 'Lo siento, codigo de Display no es valido, '
               DISPLAY 'Debe Ingresar S o N '
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

      * VAL-FEC VALIDA FECHA PROCESO.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'COL'         TO MSG-COD-SIST
               MOVE 'FECPROCNEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.
      *
      * Muevo Fechas DD-MM-SSAA a Formato SSAA-MM-DD.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DPRI IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MPRI IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SPRI IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-APRI IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO            : 'WSS-FEC-FPRO IN
                                                            WSS-VARI.
           DISPLAY ' '.
      *
      * Obtiene COD movimiento mediante el CIC.
           MOVE 'COL'          TO FIO-SIST.
           MOVE 'MVT'          TO TAB-CIC-TTAB IN TAB.
           MOVE 'CAN'          TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'       TO MSG-COD-SIST
               MOVE 'CICCANNEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CAN IN WSS-VARI.
      *
      * Busco el CIC de la moneda.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CICPESONEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'         TO MSG-COD-SIST
               MOVE 'CICTVAL0NEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

           MOVE MSC-COD-CMSC IN MSC TO WSS-MSC-TVOC IN WSS-VARI.
      *
      * Busco codigo de situacion contable "VENCIDA".
           MOVE 'COL'          TO MSC-COD-SIST.
           MOVE 'SCTB'         TO MSC-CIC-TMSC IN MSC.
           MOVE 'VCD'          TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS = SPACES
               MOVE MSC-COD-CMSC IN MSC TO WSS-COD-SVCD
           ELSE
               DISPLAY 'ABORTO : SIS = COL, MSC = SCTB, '
                       'MSC-CIC-CMSC = VCD'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

      * Voy a buscar a la tabla COBRO COMISION BANCA los tipos de Banca.
           MOVE 'COL'       TO FIO-SIST.
           MOVE 'CCB'       TO TAB-COD-TTAB IN TAB.
           MOVE SPACES      TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND TAB-IND-VIGE IN TAB = 'S' )
               DISPLAY 'ERROR: NO EXISTE O NO VIGENTE TABLA CCB'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.
           MOVE TAB-GLS-DATA IN TAB TO WSS-GLS-DAT3 IN WSS-VARI.
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'TABLA CCB: ' WSS-GLS-DAT3 IN WSS-VARI.

      * Voy a buscar el Monto Neto, Moneda, No. de Dias Inicio y Tope.
           MOVE 'COL'       TO FIO-SIST.
           MOVE 'CMO'       TO TAB-COD-TTAB IN TAB.
           MOVE SPACES      TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND TAB-IND-VIGE IN TAB = 'S' )
               DISPLAY 'ERROR: NO EXISTE O NO VIGENTE TABLA CMO'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.
           MOVE TAB-GLS-DATA IN TAB TO WSS-GLS-DATA IN WSS-VARI.
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'TABLA CMO: ' WSS-GLS-DATA IN WSS-VARI.

           IF WSS-NUM-DIAS IN WSS-VARI IS NOT NUMERIC
               DISPLAY 'ERROR: INICIO DE RANGO TABLA CMO NO NUMERICO'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

           IF WSS-NUM-TOPE IN WSS-VARI IS NOT NUMERIC
               DISPLAY 'ERROR: TOPE DE RANGO TABLA CMO NO NUMERICO'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

           IF WSS-VAL-MOUF IN WSS-VARI NOT NUMERIC
               DISPLAY 'ERROR: MONTO MONEDA TABLA CMO NO NUMERICO'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

           MOVE 'TAB'                    TO TAB-COD-SIST FIO-SIST.
           MOVE 'VLR'                    TO TAB-CIC-TTAB IN TAB.
           MOVE WSS-COD-MONE IN WSS-VARI TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: CODIGO MONEDA NO EXISTE EN TABLA VLR : '
                   WSS-COD-MONE IN WSS-VARI
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

      * Voy a buscar el Iva.
           MOVE 'TAB'       TO FIO-SIST.
           MOVE 'IVA'       TO TAB-COD-TTAB IN TAB.
           MOVE SPACES      TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT (FIO-STAT-OKS AND TAB-IND-VIGE IN TAB = 'S')
               DISPLAY 'ERROR: NO EXISTE TABLA IVA'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.
           MOVE TAB-GLS-DATA IN TAB TO WSS-GLS-DAT2 IN WSS-VARI.
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'TABLA IVA: ' WSS-GLS-DAT2 IN WSS-VARI.

           IF WSS-NUM-PIVA IN WSS-VARI NOT NUMERIC
               DISPLAY 'ERROR: PORCENTAJE NO NUMERICO EN TABLA IVA'
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT.

      *INICIALIZO ICO
           MOVE SPACES TO ICO.
           MOVE ZEROES TO ICO-SGV-MNTO IN ICO.
           MOVE ZEROES TO ICO-FEC-FVEN IN ICO.
           MOVE ZEROES TO ICO-FEC-FCAN IN ICO.

       FIN-INI-VAR.
           EXIT.

      ******************************************************************
       PRO-CAN SECTION.
      *****************
       INI-PRO-CAN.
           MOVE SPACES TO WSS-CIC-IOPC IN WSS-VARI.
           MOVE 'N' TO WSS-IND-CHEK IN WSS-VARI.
           MOVE FIO-GET-FST TO FIO-CMND.
       LUP-BUS-CAN.
           PERFORM COL-FIO-ICV.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-CAN.

           MOVE ICV TO CAN.
           IF CAN-FEC-FOUT IN CAN NOT = WSS-FEC-FPRI IN WSS-VARI
              GO TO NXT-PRO-CAN.

           IF CAN-CIC-IOPC IN CAN = WSS-CIC-IOPC IN WSS-VARI AND
              WSS-IND-CHEK IN WSS-VARI = 'S'
               GO TO NXT-PRO-CAN
           ELSE
               MOVE 'N' TO WSS-IND-CHEK IN WSS-VARI.

      * Esto es para las estadisticas.
           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'CANCELACION LEIDA       : 'CAN-CIC-IOPC IN CAN
                       '  NUMERO: 'CAN-NUM-ICAN IN CAN.

      * Verifica estado de las Cancelaciones.
           IF CAN-MSC-STAT IN CAN NOT = 'A'
               GO TO NXT-PRO-CAN.

      * Esto lo puse para chequear que la can no haya sido reversada. 
           IF (CAN-SGV-TPAG IN CAN < ZEROES) OR
              (CAN-SGV-TREN IN CAN < ZEROES)
                GO TO NXT-PRO-CAN.

      * Verifico si es con Cargo en Cuenta Corriente.
           PERFORM BUS-CIC-CTA.

           IF MSG-COD-MENS IN MSG > SPACES
               GO TO NXT-PRO-CAN.

      * Acceso a la OPC.
           PERFORM LEE-OPC.
           IF MSG-COD-MENS IN MSG > SPACES
               DISPLAY 'FALLO LECTURA OPC: '
               GO TO NXT-PRO-CAN.

      * Busca Dato Opera Como 'COMO OPERA = WSS-OPEC'.
           PERFORM BUS-DAT-OPEC.
           IF MSG-COD-MENS IN MSG > SPACES
               DISPLAY 'FALLO LECTURA TOC'
               GO TO NXT-PRO-CAN.
           IF WSS-OPEC = 'B' OR 'D'
               GO TO NXT-PRO-CAN.

      * Leo el VEN.
           PERFORM LEE-VEN.
           IF MSG-COD-MENS IN MSG > SPACES
               DISPLAY 'FALLO LECTURA VEN: '
               GO TO NXT-PRO-CAN.

      * Verifica si se le cobra Comision para grabarlo en el archivo ICO
           PERFORM CHK-EXI-COMI.

           IF WSS-IND-MORA IN WSS-VARI NOT = 'S'
               GO TO NXT-PRO-CAN.

      * Esto es para las Estadisticas.
           ADD 1 TO WSS-NUM-CON1 IN WSS-VARI.

      * Muestra las Cancelaciones segun indicador DISPLAY.
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'CANCELACION SELECCIONADA: 'CAN-CIC-IOPC IN CAN
                       '  NUMERO: 'CAN-NUM-ICAN IN CAN.
      * Graba Registros en Archivo ICO.
           PERFORM GRA-REG-ICO.
       NXT-PRO-CAN.
           MOVE FIO-GET-NXT TO FIO-CMND.
      *    PERFORM COL-FIO-CAN.
           GO TO LUP-BUS-CAN.
       FIN-PRO-CAN.
           EXIT.

      ******************************************************************
       BUS-DAT-OPEC SECTION.
      *********************
       INI-BUS-DAT-OPEC.
      * BUSCA DATO 'COMO OPERA=WSS-OPEC'
           MOVE SPACES TO MSG-COD-MENS IN MSG.
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'CANTOOCNEX'
               MOVE 'ERROR' TO MSG-COD-MENS IN MSG
               GO TO FIN-BUS-DAT-OPEC.
           MOVE TOC-MSC-OPEC IN TOC TO WSS-OPEC.
       FIN-BUS-DAT-OPEC.
           EXIT.

      ******************************************************************
       CHK-EXI-COMI SECTION.
      *********************
       INI-CHK-EXI-COMI.
           MOVE 'N'    TO WSS-IND-MORA IN WSS-VARI.
           MOVE SPACES TO WSS-IND-EBAN IN WSS-VARI.

           MOVE CAN-CIC-IOPC IN CAN TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           PERFORM COL-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
                    CAN-CIC-IOPC IN CAN = RDC-CIC-IOPC IN RDC )
                     DISPLAY 'ERROR: NO EXISTE RDC ASOCIADA A CAN : '
                            CAN-CIC-IOPC IN CAN
                     GO TO END-CHK-EXI-COMI.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: NO EXISTE DBC ASOCIADA A RDC : '
                      RDC-CIC-ICLI IN RDC
               GO TO END-CHK-EXI-COMI.
      *    IF WSS-IND-DISP IN WSS-VARI = 'S'
      *        DISPLAY 'TIPO DE CLIENTE: ' DBC-IND-TICL IN DBC.

           IF DBC-IND-TICL IN DBC = 'P'
               MOVE DBC-CIC-ICLI IN DBC TO DCP-CIC-ICLI IN DCP
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM SGC-FIO-DCP
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR: NO EXISTE DCP ASOCIADA A DBC : '
                          DBC-CIC-ICLI IN DBC
                   GO TO END-CHK-EXI-COMI
               ELSE
                   MOVE DCP-COD-TBAN IN DCP TO WSS-COD-BANC IN WSS-VARI
                   PERFORM CHK-EXI-TBAN
                   IF WSS-IND-EBAN IN WSS-VARI = 'S'
                       GO TO SIG-CON-BANC
                   ELSE
                       GO TO END-CHK-EXI-COMI
           ELSE
              IF DBC-IND-TICL IN DBC = 'E'
                 MOVE DBC-CIC-ICLI IN DBC TO DCE-CIC-ICLI IN DCE
                 MOVE FIO-GET-KEY TO FIO-CMND
                 PERFORM SGC-FIO-DCE
                 IF NOT FIO-STAT-OKS
                      DISPLAY 'ERROR: NO EXISTE DCE ASOCIADA A DBC : '
                              DBC-CIC-ICLI IN DBC
                      GO TO END-CHK-EXI-COMI
                 ELSE
                      MOVE DCE-COD-TBAN IN DCE TO WSS-COD-BANC
                                                             IN WSS-VARI
                      PERFORM CHK-EXI-TBAN
                      IF WSS-IND-EBAN IN WSS-VARI = 'S'
                          GO TO SIG-CON-BANC
                      ELSE
                          GO TO END-CHK-EXI-COMI.

       SIG-CON-BANC.
           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.
      ****
           PERFORM RST-DIA-HBLS.
           IF WSS-IND-MORA IN WSS-VARI NOT = 'S'
               GO TO END-CHK-EXI-COMI.
      ****
           IF WSS-IND-DISP IN WSS-VARI = 'S'
           AND WSS-IND-MORA = 'N'
               DISPLAY 'CANCELACION NO SELECIONADA: 'CAN-CIC-IOPC
                                                               IN CAN
                       '  NUMERO :'CAN-NUM-ICAN IN CAN.
           IF WSS-COD-MONE IN WSS-VARI = '0999  '
               MOVE 1 TO WSS-SGV-VCAM IN WSS-VARI
               GO TO CONT-SIN-VCAM.

           MOVE WSS-COD-MONE IN WSS-VARI TO CAM-COD-VCAM IN CAM(1).
           MOVE '0999  '                 TO CAM-COD-VCAM IN CAM(2).
           MOVE CAN-NUM-DOUT IN CAN      TO CAM-NUM-DCAM IN CAM(1).
           MOVE CAN-NUM-MOUT IN CAN      TO CAM-NUM-MCAM IN CAM(1).
           MOVE CAN-NUM-SOUT IN CAN      TO CAM-NUM-SCAM IN CAM(1).
           MOVE CAN-NUM-AOUT IN CAN      TO CAM-NUM-ACAM IN CAM(1).
           MOVE CAM-FEC-FCAM IN CAM(1)   TO CAM-FEC-FCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT ( FIO-STAT-OKS AND CAM-IND-VIGE = 'S')
               DISPLAY 'ERROR: NO EXISTE VALOR DE CAMBIO : '
                   CAM-KEY-CAMB IN CAM
               MOVE ZEROES TO WSS-SGV-VCAM
               MOVE 'N'    TO WSS-IND-MORA IN WSS-VARI
               GO TO END-CHK-EXI-COMI
           ELSE
               MOVE CAM-SGV-VCAM IN CAM TO WSS-SGV-VCAM.

       CONT-SIN-VCAM.
           MOVE 'COM-KEY-IICG' TO FIO-AKEY.
           MOVE OPC-CIC-IOPC IN OPC     TO COM-CIC-IOPC IN COM.
           MOVE ZEROES                  TO COM-NUM-ICAN IN COM.
           MOVE SPACES                  TO COM-CIC-IOPV IN COM.
           MOVE ZEROES                  TO COM-NUM-IVCT IN COM.
           MOVE SPACES                  TO COM-IND-TTAS IN COM.
           MOVE ZEROES                  TO COM-FEC-FINI IN COM.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-FEC-COM.
           PERFORM COL-FIO-COM.
           IF NOT ( FIO-STAT-OKS AND
               COM-CIC-IOPC IN COM = OPC-CIC-IOPC IN OPC )
               NEXT SENTENCE
           ELSE
               IF COM-IND-TTAS IN COM NOT = 'CMO'
                   MOVE FIO-GET-NXT TO FIO-CMND
                   GO TO LUP-FEC-COM
               ELSE
                   IF CAN-NUM-SOUT IN CAN = COM-NUM-SINI IN COM AND
                      CAN-NUM-AOUT IN CAN = COM-NUM-AINI IN COM AND
                      CAN-NUM-MOUT IN CAN = COM-NUM-MINI IN COM AND
                      CAN-NUM-DOUT IN CAN = COM-NUM-DINI IN COM
                       IF COM-NUM-ICAN IN COM = CAN-NUM-ICAN IN CAN
                           MOVE 'N' TO WSS-IND-MORA IN WSS-VARI
                           GO TO END-CHK-EXI-COMI
                       ELSE
                           MOVE 'N' TO WSS-IND-MORA IN WSS-VARI
                           GO TO END-CHK-EXI-COMI
      *                     NEXT SENTENCE
                   ELSE
                       MOVE FIO-GET-NXT TO FIO-CMND
                       GO TO LUP-FEC-COM.

      * Multiplico por el Valor de Cambio.
           MULTIPLY WSS-VAL-MOUF IN WSS-VARI BY WSS-SGV-VCAM IN
                           WSS-VARI GIVING WSS-SGV-NETO IN WSS-VARI.

           DIVIDE WSS-NUM-PIVA IN WSS-VARI BY 100 GIVING WSS-VAL-IVA IN
                                                               WSS-VARI.
           MULTIPLY WSS-SGV-NETO IN WSS-VARI BY WSS-VAL-IVA IN WSS-VARI
                                        GIVING WSS-SGV-MIVA IN WSS-VARI.

           MOVE WSS-SGV-MIVA IN WSS-VARI TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO WSS-SGV-MIVA IN WSS-VARI.

           MOVE WSS-SGV-NETO IN WSS-VARI TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO WSS-SGV-NETO IN WSS-VARI.

           COMPUTE WSS-SGV-TCOM IN WSS-VARI = WSS-SGV-MIVA IN WSS-VARI +
                                               WSS-SGV-NETO IN WSS-VARI.
       END-CHK-EXI-COMI.
           EXIT.

      ******************************************************************
       LIM-TBAN SECTION.
      ****************
       INI-LIM-TBAN.
           MOVE 1 TO MOV-I.
       LUP-LIM-TBAN.
           IF MOV-I > 20
               GO TO FIN-LIM-TBAN.
           MOVE SPACES TO WSS-TBAN (MOV-I).
       SIG-LIM-TBAN.
           ADD 1 TO MOV-I.
           GO TO LUP-LIM-TBAN.
       FIN-LIM-TBAN.
           EXIT.

      ******************************************************************
       CHK-EXI-TBAN SECTION.
      *********************
       INI-CHK-EXT-TBAN.
           MOVE 1 TO MOV-I.
      *    IF WSS-IND-DISP IN WSS-VARI = 'S'
      *        DISPLAY 'EXI-TBAN : ' WSS-COD-BANC IN WSS-VARI.
       LUP-VEC-TBAN.
      *    IF WSS-IND-DISP IN WSS-VARI = 'S'
      *        DISPLAY 'TBAN-SUB : ' WSS-TBAN (MOV-I).
           IF WSS-COD-BANC IN WSS-VARI = WSS-TBAN (MOV-I) AND
              WSS-COD-BANC IN WSS-VARI > SPACES
               MOVE 'S' TO WSS-IND-EBAN IN WSS-VARI
               GO TO FIN-CHK-EXT-TBAN.
           IF MOV-I = 20
               MOVE 'N' TO WSS-IND-EBAN IN WSS-VARI
               GO TO FIN-CHK-EXT-TBAN
           ELSE
               ADD 1 TO MOV-I
               GO TO LUP-VEC-TBAN.
       FIN-CHK-EXT-TBAN.
           EXIT.

      ******************************************************************
       RST-DIA-HBLS SECTION.
      **********************
       INI-RST-DIA-HBLS.
      * Calculo el numero de dias que hay entre la FCAN y FVEN
           MOVE VEN-NUM-DVEN IN VEN TO FEC-DEC1.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-MEC1.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-SEC1.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-AEC1.
           MOVE CAN-NUM-DOUT IN CAN TO FEC-DEC2.
           MOVE CAN-NUM-MOUT IN CAN TO FEC-MEC2.
           MOVE CAN-NUM-SOUT IN CAN TO FEC-SEC2.
           MOVE CAN-NUM-AOUT IN CAN TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE ZEROES TO FEC-NDIA.
           MOVE FEC-NDIA TO WSS-NUM-TOP1 IN WSS-VARI.
      *    IF WSS-IND-DISP IN WSS-VARI = 'S'
      *     DISPLAY 'RST-DIA WSS-NUM-TOP1 : ' WSS-NUM-TOP1 IN WSS-VARI
      *     DISPLAY 'RST-DIA WSS-NUM-TOPE : ' WSS-NUM-TOPE IN WSS-VARI.
           IF WSS-NUM-TOP1 IN WSS-VARI > WSS-NUM-TOPE IN WSS-VARI
               MOVE 'N' TO WSS-IND-MORA IN WSS-VARI
               GO TO FIN-RST-DIA-HBLS.

      *VAL-FEC Va restando un dia habil hasta acumular TOPE TABLA
           MOVE 'N' TO WSS-IND-MORA IN WSS-VARI.
           MOVE 1   TO WSS-NUM-CONT IN WSS-VARI.
           MOVE CAN-NUM-DOUT IN CAN TO WSS-NUM-DTOP IN WSS-VARI.
           MOVE CAN-NUM-MOUT IN CAN TO WSS-NUM-MTOP IN WSS-VARI.
           MOVE CAN-NUM-SOUT IN CAN TO WSS-NUM-STOP IN WSS-VARI.
           MOVE CAN-NUM-AOUT IN CAN TO WSS-NUM-ATOP IN WSS-VARI.
      * Resta  dias habiles y se salta los no habiles.
       REST-DIA-HBLS.
           MOVE WSS-NUM-DTOP TO FEC-ITM1.
           MOVE WSS-NUM-MTOP TO FEC-ITM2.
           MOVE WSS-NUM-STOP TO FEC-ITM3.
           MOVE WSS-NUM-ATOP TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM CAL-FEC.
           MOVE FEC-ITM1 TO WSS-NUM-DTOP.
           MOVE FEC-ITM2 TO WSS-NUM-MTOP.
           MOVE FEC-ITM3 TO WSS-NUM-STOP.
           MOVE FEC-ITM4 TO WSS-NUM-ATOP.
      *VAL-FEC Validar Fecha.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
              GO TO REST-DIA-HBLS.

      *    IF WSS-IND-DISP IN WSS-VARI = 'S'
      *        DISPLAY 'RST-DIA VEN-FEC-FVEN: 'VEN-FEC-FVEN IN VEN
      *        DISPLAY 'RST-DIA WSS-FEC-FTOP: 'WSS-FEC-FTOP IN WSS-VARI.
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTOP IN WSS-VARI
               MOVE 'N' TO WSS-IND-MORA IN WSS-VARI
               GO TO FIN-RST-DIA-HBLS.

      *    IF WSS-IND-DISP IN WSS-VARI = 'S'
      *     DISPLAY 'RST-DIA WSS-NUM-CONT : ' WSS-NUM-CONT IN WSS-VARI
      *     DISPLAY 'RST-DIA WSS-NUM-DIAS : ' WSS-NUM-DIAS IN WSS-VARI.
           IF (WSS-NUM-CONT IN WSS-VARI = WSS-NUM-DIAS IN WSS-VARI) AND
              (VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FTOP IN WSS-VARI)
                 MOVE 'S' TO WSS-IND-MORA IN WSS-VARI
                 GO TO FIN-RST-DIA-HBLS
           ELSE
               ADD 1 TO WSS-NUM-CONT IN WSS-VARI
               GO TO REST-DIA-HBLS.
       FIN-RST-DIA-HBLS.
           EXIT.

      ******************************************************************
       BUS-CIC-CTA SECTION.
      ********************
       INI-BUS-CIC-CTA.
           MOVE SPACES TO MSG-COD-MENS IN MSG.
           MOVE CAN-CIC-IOPC IN CAN TO CYA-CIC-IOPC IN CYA.
           MOVE CAN-NUM-ICAN IN CAN TO CYA-NUM-ICAN IN CYA.
           MOVE ZEROES              TO CYA-NUM-ICYA IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT ( FIO-STAT-OKS AND
                CAN-CIC-IOPC IN CAN = CYA-CIC-IOPC IN CYA AND
                CAN-NUM-ICAN IN CAN = CYA-NUM-ICAN IN CYA )
                 IF CYA-IND-TCYA IN CYA NOT = 'C'
                     MOVE 'NOEXISTECYA' TO MSG-COD-MENS IN MSG
                     GO TO FIN-BUS-CIC-CTA.
           MOVE 1 TO MOV-I.
       LUP-VEC-MOV.
           IF CYA-IND-TCYA IN CYA = WSS-COD-CCOD (MOV-I) AND
              WSS-COD-CCOD (MOV-I) > SPACES
                MOVE 'TAB'               TO MSC-COD-SIST
                MOVE 'TCYA'              TO MSC-COD-TMSC IN MSC
                MOVE CYA-IND-TCYA IN CYA TO MSC-COD-CMSC IN MSC
                PERFORM BUS-MSC
                IF FIO-STAT-OKS AND MSC-IND-VIGE IN MSC = 'S' AND
                   MSC-CIC-CMSC IN MSC = 'CTBL'
                    MOVE CAN-CIC-IOPC IN CAN TO CYA-CIC-IOPC IN CYA
                    MOVE ZEROES              TO CYA-NUM-ICAN IN CYA
                    MOVE ZEROES              TO CYA-NUM-ICYA IN CYA
                    MOVE FIO-GET-NLS TO FIO-CMND
                    PERFORM COL-FIO-CYA
                    IF NOT ( FIO-STAT-OKS AND
                       CAN-CIC-IOPC IN CAN = CYA-CIC-IOPC IN CYA AND
                       ZEROES = CYA-NUM-ICAN IN CYA )
                        MOVE 'CTACTBL  NEX' TO MSG-COD-MENS IN MSG
                    ELSE
                        MOVE 'TAB'               TO MSC-COD-SIST
                        MOVE 'TCYA'              TO MSC-COD-TMSC IN MSC
                        MOVE CYA-IND-TCYA IN CYA TO MSC-COD-CMSC IN MSC
                        PERFORM BUS-MSC
                        IF FIO-STAT-OKS
                            MOVE MSC-CIC-CMSC IN MSC TO WSS-COD-CCCC IN
                                                               WSS-VARI
                            IF WSS-COD-CCCC IN WSS-VARI NOT = 'CC'
                                NEXT SENTENCE
                            ELSE
                                MOVE 'NOEXISTECYA' TO MSG-COD-MENS IN
                                                                    MSG
                        ELSE
                            MOVE 'NOEXISTECYA' TO MSG-COD-MENS IN MSG
                ELSE
                    MOVE 'NOEXISTECYA' TO MSG-COD-MENS IN MSG
           ELSE
               IF MOV-I = 8
                   MOVE 'NOESCC' TO MSG-COD-MENS IN MSG
               ELSE
                   ADD 1 TO MOV-I
                   GO TO LUP-VEC-MOV.
       FIN-BUS-CIC-CTA.
           EXIT.

      ******************************************************************
       LEE-OPC SECTION.
      *****************
       INI-LEE-OPC.
           MOVE SPACES TO MSG-COD-MENS IN MSG.
           MOVE CAN-CIC-IOPC IN CAN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
              DISPLAY ' NO ENCONTRE OPERACION: '
                        OPC-CIC-IOPC IN OPC
              MOVE 'ERROR' TO MSG-COD-MENS IN MSG
              GO TO FIN-LEE-OPC.
       FIN-LEE-OPC.
           EXIT.

      ******************************************************************
       LEE-VEN SECTION.
      ****************
       INI-LEE-VEN.
           MOVE SPACES TO MSG-COD-MENS IN MSG.
           MOVE CAN-CIC-IOPC IN CAN TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN TO RCV-NUM-ICAN IN RCV.
           MOVE SPACES              TO RCV-CIC-IOPV IN RCV.
           MOVE ZEROES              TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS
                AND CAN-CIC-IOPC IN CAN = RCV-CIC-IOPC IN RCV
                AND CAN-NUM-ICAN IN CAN = RCV-NUM-ICAN IN RCV )
                 DISPLAY ' NO EXISTE RCV PARA CAN '
                 MOVE 'ERROR' TO MSG-COD-MENS IN MSG
                 GO TO FIN-LEE-VEN.

           IF RCV-NUM-IVCT IN RCV = ZEROES
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN
           ELSE
               MOVE RCV-NUM-IVCT IN RCV TO VEN-NUM-IVEN IN VEN.

           MOVE RCV-CIC-IOPC IN RCV TO VEN-CIC-IOPC IN VEN.

           IF OPC-IND-VSTR IN OPC = 'S'
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS
                   DISPLAY 'ERROR NO EXISTE VEN PARA RCV EN OBTVEN '
                   MOVE 'OBTVENERR'         TO MSG-COD-MENS IN MSG
               ELSE
                   GO TO FIN-LEE-VEN
           ELSE
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'OBTVENERR'         TO MSG-COD-MENS IN MSG
                   DISPLAY 'ERROR NO EXISTE VEN PARA RCV EN FIOVEN '.
       FIN-LEE-VEN.
           EXIT.

      ******************************************************************
      * Graba archivo ICO.
       GRA-REG-ICO SECTION.
      ********************
       INI-GRA-MOV.
           MOVE DBC-NUM-ICLI IN DBC      TO ICO-NUM-ICLI IN ICO.
           MOVE DBC-VRF-ICLI IN DBC      TO ICO-VRF-ICLI IN ICO.
           MOVE DBC-GLS-NOMC IN DBC      TO ICO-GLS-NOMC IN ICO.
           MOVE OPC-COD-RMOC IN OPC      TO ICO-COD-RMOC IN ICO.
           MOVE OPC-COD-RMOC IN OPC      TO USR-COD-USER IN USR.
           PERFORM BUS-USR.
           MOVE USR-GLS-DESC IN USR TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO USR-GLS-DESC IN USR.
           MOVE USR-GLS-DESC IN USR      TO ICO-GLS-RMOC IN ICO.
           MOVE OPC-CIC-IOPC IN OPC      TO ICO-CIC-IOPC IN ICO.
           MOVE VEN-NUM-DVEN IN VEN      TO ICO-NUM-DVEN IN ICO.
           MOVE VEN-NUM-MVEN IN VEN      TO ICO-NUM-MVEN IN ICO.
           MOVE VEN-NUM-SVEN IN VEN      TO ICO-NUM-SVEN IN ICO.
           MOVE VEN-NUM-AVEN IN VEN      TO ICO-NUM-AVEN IN ICO.
           MOVE CAN-NUM-DOUT IN CAN      TO ICO-NUM-DCAN IN ICO.
           MOVE CAN-NUM-MOUT IN CAN      TO ICO-NUM-MCAN IN ICO.
           MOVE CAN-NUM-SOUT IN CAN      TO ICO-NUM-SCAN IN ICO.
           MOVE CAN-NUM-AOUT IN CAN      TO ICO-NUM-ACAN IN ICO.
           MOVE WSS-SGV-TCOM IN WSS-VARI TO ICO-SGV-MNTO IN ICO.

           MOVE 'TAB'               TO MSC-COD-SIST.
           MOVE 'TCYA'              TO MSC-COD-TMSC IN MSC.
           MOVE CYA-IND-TCYA IN CYA TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF ( MSC-CIC-CMSC IN MSC = 'CAJA')
               MOVE SPACES TO ICO-CIC-ICYA IN ICO
           ELSE
               MOVE CYA-CIC-ICYA IN CYA  TO ICO-CIC-ICYA IN ICO.

           MOVE CYA-IND-TCYA IN CYA      TO ICO-IND-TCYA IN ICO.
           ADD 1 TO WSS-NUM-RGRA IN WSS-VARI.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-ICO.
           IF FIO-STAT-OKS
               IF WSS-IND-DISP IN WSS-VARI = 'S'
                   DISPLAY 'CANCELACION GRABADA OK  : 'CAN-CIC-IOPC
                                                                IN CAN
                           '  NUMERO :'CAN-NUM-ICAN IN CAN
                   DISPLAY '-------------------------------------------'
                           '---------'
                   DISPLAY '                                           '
                           '         '
               ELSE
                   NEXT SENTENCE
           ELSE
               DISPLAY 'ERROR AL GRABAR REGISTRO AL ARCHIVO ICO'
               DISPLAY 'CANCELACION NO GRABADA  : 'CAN-CIC-IOPC IN CAN
               GO TO FIN-GRA-REG-ICO.
           MOVE 'S'                 TO WSS-IND-CHEK IN WSS-VARI.
           MOVE CAN-CIC-IOPC IN CAN TO WSS-CIC-IOPC IN WSS-VARI.
       FIN-GRA-REG-ICO.
           EXIT.

      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *****************************
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY ' '.
           DISPLAY ' ESTADISTICAS DEL PROCESO.'.
           DISPLAY ' -------------------------'.
           DISPLAY ' '.
           DISPLAY 'REGISTROS LEIDOS DE TABLA CAN   : '
                                        WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'CANCELACIONES CON MORA          : '
                                        WSS-NUM-CON1 IN WSS-VARI.
           DISPLAY 'REGISTROS GRABADOS EN TABLA ICO : '
                                        WSS-NUM-RGRA IN WSS-VARI.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.

      ******************************************************************
       CLOSE-FILE SECTION.
      ********************
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-ICO.
           PERFORM COL-FIO-ICV.
           MOVE 'COL' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE 'TAB' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
       FIN-CLOSE-FILE.
           EXIT.

      ******************************************************************
      * Copys necesarios para la ejecucion del programa.

       COPY COLBFDLC.
       COPY COLBFOPC.
       COPY COLBFICG.
       COPY COLBFVEN.
       COPY COLBFRCV.
       COPY COLBFRDC.
       COPY COLBFCAN.
       COPY COLBFCOM.
       COPY COLBFCYA.
       COPY COLBFICO.
       COPY COLBFICV.
       COPY COLBFEVC.
       COPY COLBGINT.
       COPY COLBGVEN.
       COPY COLBFTOC.
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY SGCBFDCE.
       COPY TABBFCAM.
       COPY TABBBUSR.
       COPY TABBFUSR.
       COPY GNSBGCPT.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBGPES.

