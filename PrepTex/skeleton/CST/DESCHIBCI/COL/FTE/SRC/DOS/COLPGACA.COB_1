       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGACA.
       AUTHOR.          CONSIST(JSS).
       DATE-WRITTEN.    20-OCT-1992     ( Ultima Modificacion ).

      *Lee transacciones de MVO, activando CAN para movimientos OK

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES. DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY GNSBRERR.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ERR-REQA==.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY GNSWGPES.
       COPY GNSWVIDD.

       COPY COLBROPC.
       COPY COLBRCAN.
       COPY COLBRDLC.
       COPY COLBRRCV.
       COPY COLBRVEN.
      *DAD ini  31-MAY-1993
       COPY COLBRVEN REPLACING ==VEN== BY ==VEN-BKP==.
      *DAD fin  31-MAY-1993
       COPY COLBRTOC.
       COPY COLBREVC.
       COPY COLBRRDT.
       COPY COLBRICG.
       COPY COLBRMVO.
       COPY COLBRRCH.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MVO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCH-REQA==.
       COPY COLWGVEN.
       COPY COLWICAN.

       COPY TABBRCAM.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
      *
       01  SCR-VARI.
           03 SCR-CMND               VALUE SPACES PIC X(03).
      *
       01  FRM-VARI.
           03  FRM-MENS.
               05 FRM-MEN1           VALUE SPACES PIC X(40).
               05 FRM-MEN2           VALUE SPACES PIC X(40).
      *
       01  WSS-VARI.
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AINI        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-STER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-ATER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MTER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DTER        VALUE ZEROS  PIC 9(02).
           03  WSS-MAXV              VALUE ZEROS  PIC 9(03).
           03  WSS-MINV              VALUE ZEROS  PIC 9(03).
           03  WSS-MAXD              VALUE ZEROS  PIC 9(03).
           03  WSS-MIND              VALUE ZEROS  PIC 9(03).
           03  WSS-IDLC.
               05  WSS-VEC-IDLC OCCURS 500.
                   07  WSS-SGV-CAPD  COMP-3       PIC S9(11)V9(04).
                   07  WSS-SGV-FINA  COMP-3       PIC S9(11)V9(04).
           03  WSS-LIM-IDLC          VALUE 500    PIC 9(03).
           03  WSS-NUM-IDLC          VALUE ZEROS  PIC 9(04).
      *DAD ini  22-OCT-1991
           03  WSS-NUM-VCVG          VALUE ZEROS  PIC S9(03).
           03  WSS-NUM-VCCM          VALUE ZEROS  PIC S9(03).
           03  WSS-NUM-VCCO          VALUE ZEROS  PIC S9(03).
           03  WSS-NUM-VCCV          VALUE ZEROS  PIC S9(03).
      *DAD fin  22-OCT-1991
           03  WSS-NUM-IEVC          VALUE ZEROS  PIC 9(03).
           03  WSS-AUX-IVEN          VALUE ZEROS  PIC 9(03).
      *DAD ini  20-OCT-1992   (Memo.08-10-92)
           03  WSS-IND-SCTB                        PIC X(03).
           03  WSS-IND-SCBZ                        PIC X(05).
      *DAD fin  20-OCT-1992
      *DAD ini  31-MAY-1993
           03  WSS-IND-EXIS          VALUE 'N'    PIC X(01).
           03  WSS-VAL-CAPI-GENE     COMP-3       PIC 9(11)V9(04).
      *     03  VEN-BKP               VALUE SPACES PIC X(137).
      *DAD fin  31-MAY-1993
           03  WSS-VAL-VENC  COMP-3  VALUE ZEROS  PIC 9(11)V9(04).
           03  WSS-FEC-FAMT.
               05  WSS-NUM-SAMT      VALUE ZEROS  PIC 9(02).
               05  WSS-NUM-AAMT      VALUE ZEROS  PIC 9(02).
               05  WSS-NUM-MAMT      VALUE ZEROS  PIC 9(02).
               05  WSS-NUM-DAMT      VALUE ZEROS  PIC 9(02).
           03  WSS-VAL-SMO1  COMP-3  VALUE ZEROS  PIC S9(11)V9(5).
           03  WSS-VAL-SMO2  COMP-3  VALUE ZEROS  PIC S9(11)V9(5).
           03  WSS-IND-CIER          VALUE SPACES PIC X(01).
           03  WSS-RMVO-PROC         VALUE ZEROES PIC 9(04).
           03  WSS-RMVO-ERRO         VALUE ZEROES PIC 9(04).
           03  WSS-RMVO-INGR         VALUE ZEROES PIC 9(04).
           03  WSS-RMVO-RECH         VALUE ZEROES PIC 9(04).
           03  WSS-RMVO-SCIE         VALUE ZEROES PIC 9(04).
           03  WSS-RMVO-NCIE         VALUE ZEROES PIC 9(04).
           03  WSS-RMVO-SCIR         VALUE ZEROES PIC 9(04).
           03  WSS-RMVO-NCIR         VALUE ZEROES PIC 9(04).
      *DAD ini  04-OCT-1991
           03  WSS-RMVO-OSTT         VALUE ZEROES PIC 9(04).
      *DAD fin  04-OCT-1991
           03  WSS-RCAN-SACT         VALUE ZEROES PIC 9(04).
           03  WSS-RCAN-NACT         VALUE ZEROES PIC 9(04).
           03  WSS-ROPC-SACT         VALUE ZEROES PIC 9(04).
           03  WSS-ROPC-NACT         VALUE ZEROES PIC 9(04).
           03  WSS-IRCH              VALUE SPACES PIC X(01).
           03  WSS-CMSG              VALUE SPACES PIC X(12).
           03  WSS-MENS.
               05 WSS-MEN2           VALUE SPACES PIC X(40).
      *DAD ini  11-NOV-1991
           03  WSS-GLS-POPC                       PIC X(40).
           03  WSS-DCO-POPC                       PIC X(12).
           03  WSS-GLS-PCAN                       PIC X(40).
           03  WSS-DCO-PCAN                       PIC X(12).
      *DAD fin  11-NOV-1991
      *
       01  PGM-VARI.
           03 PGM-STAT.
              05 PGM-STAT-VEN           VALUE '23'      PIC X(02).
                 88 PGM-STAT-VEN-OKS    VALUE '00'.
                 88 PGM-STAT-VEN-NUL    VALUE '  '.


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGACA' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM OBT-PARAM.
           PERFORM LEE-REG-MVO.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
       COPY GNSBGGBK.

       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-EVC.
           PERFORM COL-FIO-RDT.
           PERFORM COL-FIO-MVO.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-RCH.
           PERFORM COL-FIO-TOC.
       FIN-OPEN-FILE.
           EXIT.

       OBT-PARAM SECTION.
       INI-OBT-PARAM.
      *Captura Fecha de Proceso
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO ( DDMMAAAA ) : ' WSS-FEC-FPRO
                                                      IN WSS-VARI.
           ACCEPT  WSS-IND-CIER IN WSS-VARI.
           DISPLAY 'INDICADOR DE CIERRE           : ' WSS-IND-CIER
                                                      IN WSS-VARI.
           DISPLAY 'Fin    Captura    de Parametros'.
           DISPLAY 'Inicio Validacion de Parametros'.

      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
      *    MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992

           IF WSS-IND-CIER NOT > SPACES
               MOVE 'N' TO WSS-IND-CIER IN WSS-VARI
           ELSE
           IF NOT ( WSS-IND-CIER = 'S' OR 'N' )
               DISPLAY 'INDICADOR DE CIERRE DEBE SER: S o N'
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           DISPLAY WSS-IND-CIER IN WSS-VARI.

           DISPLAY 'Fin Validacion de Parametros'.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO WSS-FEC-FINI IN WSS-VARI.
       BUS-DIA-HBL.
      *Buscar el dia anterior
           MOVE WSS-FEC-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
      *Ver si el dia resultante es feriado
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
                MOVE FEC-FECH TO WSS-FEC-FINI IN WSS-VARI
                GO TO BUS-DIA-HBL.
           DISPLAY 'FECHA INICIO  : ' WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FECHA TERMINO : ' WSS-FEC-FPRO IN WSS-VARI.

      *DAD ini  11-NOV-1991
      * Obtener mensajes parametricos de proceso para MVO desde COL o CA
      *GET-MSG Busca mensaje en tabla
           MOVE 'COL'        TO MSG-COD-SIST.
           MOVE 'ACAPROCOPC' TO MSG-COD-MENS.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC IN MSG TO WSS-GLS-POPC IN WSS-VARI.
           MOVE MSG-GLS-DCOR IN MSG TO WSS-DCO-POPC IN WSS-VARI.

      *GET-MSG Busca mensaje en tabla
           MOVE 'COL'        TO MSG-COD-SIST.
           MOVE 'ACAPROCCAN' TO MSG-COD-MENS.
           PERFORM GET-MSG.
           MOVE MSG-GLS-DESC IN MSG TO WSS-GLS-PCAN IN WSS-VARI.
           MOVE MSG-GLS-DCOR IN MSG TO WSS-DCO-PCAN IN WSS-VARI.
      *DAD fin  11-NOV-1991
       FIN-OBT-PARAM.
           EXIT.

       LEE-REG-MVO SECTION.
       INI-LEE-REG-MVO.
           MOVE SPACES                   TO MVO-KEY-IMVO IN MVO.
           MOVE WSS-NUM-DINI IN WSS-VARI TO MVO-NUM-DIMV IN MVO.
           MOVE WSS-NUM-MINI IN WSS-VARI TO MVO-NUM-MIMV IN MVO.
           MOVE WSS-NUM-SINI IN WSS-VARI TO MVO-NUM-SIMV IN MVO.
           MOVE WSS-NUM-AINI IN WSS-VARI TO MVO-NUM-AIMV IN MVO.
           MOVE 'COL'                    TO MVO-COD-TORG IN MVO.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-LEE-REG-MVO.
      *JSS para probar en CST la FIO COL-FIO-MVO accesa un VSAM
           PERFORM COL-FIO-MVO.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LEE-REG-MVO.

           IF MVO-FEC-FIMV IN MVO > WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-LEE-REG-MVO.

           IF MVO-COD-TORG IN MVO NOT = 'COL'
               GO TO NXT-LEE-REG-MVO.

      *Si el movimiento ya fue procesado por COL ( ya esta cerrado )
      *busca el siguiente
           IF MVO-COD-TCUA IN MVO = 'C'
               GO TO NXT-LEE-REG-MVO.

      *Si el movimiento esta solamente ingresado(I), suma contador respe
           IF MVO-COD-EPRC IN MVO = 'I'
      *DAD ini  04-OCT-1991
               ADD 1 TO WSS-RMVO-INGR
               GO TO NXT-LEE-REG-MVO.

      *Si fue rechazado(F) confirma el rechazo ( CON-RECH, suma contador
      *corresponde el rechazo )
      *Luego si esta ingresado(I) o rechazado(f) cofirmado(WSS-IRCH), lo
      *con cierre si corresponde y luego busca el siguiente
           IF MVO-COD-EPRC IN MVO = 'F'
               PERFORM CON-RECH
               IF WSS-IRCH = 'S'
                   IF WSS-IND-CIER = 'S'
                       PERFORM RCH-REG-MVO
                       GO TO NXT-LEE-REG-MVO
                   ELSE
                       GO TO NXT-LEE-REG-MVO
               ELSE
               IF WSS-IRCH = 'N'
                   NEXT SENTENCE
               ELSE
                   ADD 1 TO WSS-RMVO-OSTT
                   GO TO NXT-LEE-REG-MVO
           ELSE
           IF MVO-COD-EPRC IN MVO NOT = 'A'
               ADD 1 TO WSS-RMVO-OSTT
               GO TO NXT-LEE-REG-MVO.
      *DAD fin  04-OCT-1991

      *Pregunta por campo MVO-COD-TAUT debe ser ZEROES ( esto indica
      *Operacion ) o > ZEROES ( indica Nro. de Cancelacion )
           IF ( MVO-COD-TAUT IN MVO IS NOT NUMERIC )
               DISPLAY 'EN REGISTRO MVO:' MVO-KEY-IMVO IN MVO ', CAMPO'
               ' MVO-COD-TAUT NO ES NUMERICO: ' MVO-COD-TAUT IN MVO
               GO TO NXT-LEE-REG-MVO.
           ADD 1 TO WSS-RMVO-PROC.

      *Limpia el codigo de mensaje
           MOVE SPACES TO WSS-CMSG.
      *Analiza que tipo de registro es el MVO para activar OPC o CAN
      *(por el momento maneja solo cancelaciones y movimientos no relaci
      *    Si el MVO corresponde a una OPERACION
      *JSS  IF MVO-CIC-NUOP IN MVO     > SPACES AND
      *        MVO-COD-TAUT IN MVO     = '000'  AND
      *        MVO-GLS-TAB1 IN MVO NOT > SPACES
      *         DISPLAY 'PRO-ACT-OPC:' MVO-CIC-NUOP IN MVO
      *         PERFORM PRO-ACT-OPC
      *     ELSE
      *    Si el MVO corresponde a una CANCELACION
           IF MVO-CIC-NUOP IN MVO     > SPACES AND
              MVO-COD-TAUT IN MVO     > '000'  AND
              MVO-GLS-TAB1 IN MVO NOT > SPACES
      *DAD ini  11-NOV-1991
               DISPLAY WSS-GLS-PCAN IN WSS-VARI MVO-CIC-NUOP IN MVO
                       '/' MVO-COD-TAUT IN MVO ' '
                       WSS-DCO-PCAN IN WSS-VARI MVO-KEY-IMVO IN MVO
      *DAD fin  11-NOV-1991
               PERFORM PRO-ACT-CAN
           ELSE
      *JSS     DISPLAY 'MVO NO corresponde a OPC o CAN:' MVO-KEY-IMVO
      *DAD ini  11-NOV-1991
               DISPLAY WSS-GLS-POPC IN WSS-VARI MVO-CIC-NUOP IN MVO
                       '/' MVO-COD-TAUT IN MVO ' '
                       WSS-DCO-POPC IN WSS-VARI MVO-KEY-IMVO IN MVO
      *DAD fin  11-NOV-1991
               ADD 1 TO WSS-RMVO-ERRO
               GO TO NXT-LEE-REG-MVO.

      *Analiza codigo de mensaje para hacer CHK-PNT o BCK-OUT
           IF WSS-CMSG > SPACES
               GO TO BKO-LEE-REG-MVO.
       CHP-LEE-REG-MVO.
      *Cierra registro MVO para no procesarlo nuevamente
           PERFORM CIE-REG-MVO.
           IF WSS-CMSG > SPACES
               GO TO BKO-LEE-REG-MVO.
           MOVE FIO-CHK-PNT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           GO TO NXT-LEE-REG-MVO.
       BKO-LEE-REG-MVO.
           MOVE 'COL'    TO MSG-COD-SIST.
           MOVE WSS-CMSG TO MSG-COD-MENS.
           PERFORM GET-MSG.
           DISPLAY MSG-GLS-DESC WSS-MEN2.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *
       NXT-LEE-REG-MVO.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-LEE-REG-MVO.
       FIN-LEE-REG-MVO.
           EXIT.

      *Para confirmar rechazos : se considera rechazado si existe regist
      *en tabla RCH y estado del proceso no es igual a '14'
       CON-RECH SECTION.
       INI-CON-RECH.
           MOVE MVO-KEY-IMVO IN MVO TO RCH-KEY-IRCH IN RCH.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RCH.
      *DAD ini  04-OCT-1991
           IF FIO-STAT-OKS
               IF RCH-COD-EPRC IN RCH NOT = '14'
                   MOVE 'S' TO WSS-IRCH
                   ADD 1 TO WSS-RMVO-RECH
               ELSE
                   MOVE 'N' TO WSS-IRCH
           ELSE
               MOVE 'X' TO WSS-IRCH.
      *DAD fin  04-OCT-1991
       FIN-CON-RECH.
           EXIT.

      *Proceso de activacion de una Operacion
       PRO-ACT-OPC SECTION.
       INI-PRO-ACT-OPC.
           MOVE MVO-CIC-NUOP IN MVO TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPCGKEYUPDNO'      TO WSS-CMSG
               MOVE OPC-CIC-IOPC IN OPC TO WSS-MEN2
               ADD 1 TO WSS-ROPC-NACT
               GO TO FIN-PRO-ACT-OPC.
           MOVE 'A' TO OPC-MSC-STAT IN OPC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-ROPC-SACT
           ELSE
               MOVE 'OPC    MODNO'      TO WSS-CMSG
               MOVE OPC-CIC-IOPC IN OPC TO WSS-MEN2
               ADD 1 TO WSS-ROPC-NACT.
       FIN-PRO-ACT-OPC.
           EXIT.

      *Proceso de activacion de una Cancelacion
       PRO-ACT-CAN SECTION.
       INI-PRO-ACT-CAN.
      *Lectura de OPC y carga de los campos buffer ( CAN-FLD )
           MOVE MVO-CIC-NUOP IN MVO TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NEX'        TO WSS-CMSG
               MOVE OPC-CIC-IOPC IN OPC TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
           PERFORM PUT-OPC-CAN.
      *Lectura de CAN y carga de los campos buffer ( CAN-FLD )
           MOVE MVO-CIC-NUOP IN MVO TO CAN-CIC-IOPC IN CAN.
           MOVE MVO-COD-TAUT IN MVO TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NEX'        TO WSS-CMSG
               MOVE CAN-KEY-ICAN IN CAN TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
           IF CAN-MSC-STAT IN CAN = 'A'
               GO TO FIN-PRO-ACT-CAN.
           PERFORM PUT-CAN-CAN.
      *JSS-INI 23-FEB-1996
           PERFORM OBT-TSA-OPC.
           PERFORM OBT-TSA-CAN.
      *JSS-FIN 23-FEB-1996
      *Lectura de RCV y carga de los campos buffer ( CAN-FLD )
           MOVE CAN-CIC-IOPC IN CAN TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN TO RCV-NUM-ICAN IN RCV.
           MOVE SPACES              TO RCV-CIC-IOPV IN RCV.
           MOVE ZEROES              TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT ( FIO-STAT-OKS AND
                    CAN-CIC-IOPC IN CAN = RCV-CIC-IOPC IN RCV AND
                    CAN-NUM-ICAN IN CAN = RCV-NUM-ICAN IN RCV )
               MOVE 'RCVINEXICAN:'      TO WSS-CMSG
               MOVE RCV-KEY-ICAN IN RCV TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
           PERFORM PUT-RCV-CAN.
      *Lectura de VEN y carga de los campos buffer ( CAN-FLD )
           MOVE RCV-CIC-IOPV IN RCV     TO VEN-CIC-IOPC IN VEN.
           IF RCV-NUM-IVCT IN RCV > ZEROES
               MOVE RCV-NUM-IVCT IN RCV TO VEN-NUM-IVEN IN VEN
           ELSE
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM COL-OBT-VEN
               IF VEN-STAT-OKS
                   MOVE '00' TO FIO-STAT
               ELSE
                   MOVE '23' TO FIO-STAT
           ELSE
               PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENINEXRCV:'       TO WSS-CMSG
               MOVE RCV-KEY-IRCV IN RCV TO WSS-MEN2
               GO TO FIN-PRO-ACT-CAN.
           PERFORM PUT-VEN-CAN.
      *
      *Despues de tener buffer cargado ( CAN-FLD ) realiza el mismo
      *proceso del programa interactivo ( ver CMN-ACC )
      *
           MOVE 'CAN' TO SCR-CMND.
           PERFORM INI-CAN.
           IF CAN-MSC-FCAN IN CAN = 'T' AND
              CAN-MSC-ECAN IN CAN = 'T'
               PERFORM TOT-CUO
           ELSE
               PERFORM PRO-RCV.
           IF MSG-COD-MENS > SPACES
               MOVE MSG-COD-MENS TO WSS-CMSG
               MOVE FRM-MEN2     TO WSS-MEN2
               GO TO ERR-PRO-ACT-CAN.
           MOVE MVO-CIC-NUOP IN MVO TO CAN-CIC-IOPC IN CAN.
           MOVE MVO-COD-TAUT IN MVO TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NOUPD'      TO WSS-CMSG
               MOVE CAN-KEY-ICAN IN CAN TO WSS-MEN2
               GO TO ERR-PRO-ACT-CAN.
           MOVE 'A' TO CAN-MSC-STAT IN CAN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-RCAN-SACT
               GO TO FIN-PRO-ACT-CAN.
           MOVE 'CAN    NOMOD'      TO WSS-CMSG.
           MOVE CAN-KEY-ICAN IN CAN TO WSS-MEN2.
       ERR-PRO-ACT-CAN.
           ADD 1 TO WSS-RCAN-NACT.
       FIN-PRO-ACT-CAN.
           EXIT.

      *JSS-INI 23-FEB-1996
       OBT-TSA-OPC SECTION.
       INI-OBT-TSA-OPC.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPC IN ICG.
           MOVE ZEROES              TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES              TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF FIO-STAT-OKS AND
              ICG-CIC-IOPC IN ICG = CAN-CIC-IOPC IN CAN AND
              ICG-NUM-ICAN IN ICG = ZEROES              AND
              ICG-CIC-IOPV IN ICG = SPACES              AND
              ICG-NUM-IVCT IN ICG = ZEROES              AND
              ICG-IND-TTAS IN ICG = 'INT'
               IF ICG-COD-CVTI IN ICG > SPACES
                   MOVE ICG-SGV-SPRD IN ICG TO FRM-SGV-TINT IN CAN-FLD
               ELSE
                   MOVE ICG-SGV-TINT IN ICG TO FRM-SGV-TINT IN CAN-FLD
           ELSE
               MOVE ZEROES TO FRM-SGV-TINT IN CAN-FLD.
       FIN-OBT-TSA-OPC.
           EXIT.

       OBT-TSA-CAN SECTION.
       INI-OBT-TSA-CAN.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPC IN ICG.
           MOVE CAN-NUM-ICAN IN CAN TO ICG-NUM-ICAN IN ICG.
           MOVE CAN-CIC-IOPC IN CAN TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF FIO-STAT-OKS AND
              ICG-CIC-IOPC IN ICG = CAN-CIC-IOPC IN CAN AND
              ICG-NUM-ICAN IN ICG = CAN-NUM-ICAN IN CAN AND
              ICG-CIC-IOPV IN ICG = CAN-CIC-IOPC IN CAN AND
              ICG-NUM-IVCT IN ICG = ZEROES              AND
              ICG-IND-TTAS IN ICG = 'INT'
               MOVE ICG-SGV-TINT IN ICG TO ICG-SGV-TINT IN CAN-FLD
           ELSE
               MOVE ZEROES TO ICG-SGV-TINT IN CAN-FLD.
       FIN-OBT-TSA-CAN.
           EXIT.
      *JSS-FIN 23-FEB-1996

       PUT-OPC-CAN SECTION.
       INI-PUT-OPC-CAN.
           MOVE OPC-MSC-TVOC IN OPC TO OPC-MSC-TVOC IN CAN-FLD.
           MOVE OPC-COD-TOOC IN OPC TO OPC-COD-TOOC IN CAN-FLD.
           MOVE OPC-COD-OFOC IN OPC TO OPC-COD-OFOC IN CAN-FLD.
           MOVE OPC-COD-AOOC IN OPC TO OPC-COD-AOOC IN CAN-FLD.
           MOVE OPC-NUM-VCVG IN OPC TO OPC-NUM-VCVG IN CAN-FLD.
           MOVE OPC-COD-VCOC IN OPC TO OPC-COD-VCOC IN CAN-FLD.
           MOVE OPC-VAL-SCRE IN OPC TO OPC-VAL-SCRE IN CAN-FLD.
       FIN-PUT-OPC-CAN.
           EXIT.

       PUT-CAN-CAN SECTION.
       INI-PUT-CAN-CAN.
           MOVE CAN-CAI-IOPC IN CAN TO CAN-CAI-IOPC IN CAN-FLD.
           MOVE CAN-IIC-IOPC IN CAN TO CAN-IIC-IOPC IN CAN-FLD.
           MOVE CAN-NUM-ICAN IN CAN TO CAN-NUM-ICAN IN CAN-FLD.
           MOVE CAN-COD-RCAN IN CAN TO CAN-COD-RCAN IN CAN-FLD.
           MOVE CAN-MSC-STAT IN CAN TO CAN-MSC-STAT IN CAN-FLD.
           MOVE CAN-MSC-CANC IN CAN TO CAN-MSC-CANC IN CAN-FLD.
           MOVE CAN-NUM-DOUT IN CAN TO CAN-NUM-DOUT IN CAN-FLD.
           MOVE CAN-NUM-MOUT IN CAN TO CAN-NUM-MOUT IN CAN-FLD.
           MOVE CAN-NUM-SOUT IN CAN TO CAN-NUM-SOUT IN CAN-FLD.
           MOVE CAN-NUM-AOUT IN CAN TO CAN-NUM-AOUT IN CAN-FLD.
           MOVE CAN-NUM-DCAN IN CAN TO CAN-NUM-DCAN IN CAN-FLD.
           MOVE CAN-NUM-MCAN IN CAN TO CAN-NUM-MCAN IN CAN-FLD.
           MOVE CAN-NUM-SCAN IN CAN TO CAN-NUM-SCAN IN CAN-FLD.
           MOVE CAN-NUM-ACAN IN CAN TO CAN-NUM-ACAN IN CAN-FLD.
           MOVE CAN-NUM-DEGR IN CAN TO CAN-NUM-DEGR IN CAN-FLD.
           MOVE CAN-NUM-MEGR IN CAN TO CAN-NUM-MEGR IN CAN-FLD.
           MOVE CAN-NUM-SEGR IN CAN TO CAN-NUM-SEGR IN CAN-FLD.
           MOVE CAN-NUM-AEGR IN CAN TO CAN-NUM-AEGR IN CAN-FLD.
           MOVE CAN-COD-OFCA IN CAN TO CAN-COD-OFCA IN CAN-FLD.
           MOVE CAN-SGV-GAST IN CAN TO CAN-SGV-GAST IN CAN-FLD.
           MOVE CAN-SGV-TREN IN CAN TO CAN-SGV-TREN IN CAN-FLD.
           MOVE CAN-SGV-TPAG IN CAN TO CAN-SGV-TPAG IN CAN-FLD.
           MOVE CAN-SGV-DIFE IN CAN TO CAN-SGV-DIFE IN CAN-FLD.
       FIN-PUT-CAN-CAN.
           EXIT.

       PUT-RCV-CAN SECTION.
       INI-PUT-RCV-CAN.
           MOVE RCV-SGV-CAPI IN RCV TO RCV-SGV-CAPI IN CAN-FLD.
           MOVE RCV-SGV-FINA IN RCV TO RCV-SGV-FINA IN CAN-FLD.
       FIN-PUT-RCV-CAN.
           EXIT.

       PUT-VEN-CAN SECTION.
       INI-PUT-VEN-CAN.
           MOVE VEN-NUM-DVEN IN VEN TO VEN-NUM-DVEN IN CAN-FLD.
           MOVE VEN-NUM-MVEN IN VEN TO VEN-NUM-MVEN IN CAN-FLD.
           MOVE VEN-NUM-SVEN IN VEN TO VEN-NUM-SVEN IN CAN-FLD.
           MOVE VEN-NUM-AVEN IN VEN TO VEN-NUM-AVEN IN CAN-FLD.
           MOVE VEN-NUM-DIMV IN VEN TO VEN-NUM-DIMV IN CAN-FLD.
           MOVE VEN-NUM-MIMV IN VEN TO VEN-NUM-MIMV IN CAN-FLD.
           MOVE VEN-NUM-SIMV IN VEN TO VEN-NUM-SIMV IN CAN-FLD.
           MOVE VEN-NUM-AIMV IN VEN TO VEN-NUM-AIMV IN CAN-FLD.
           MOVE VEN-IND-SCTB IN VEN TO VEN-IND-SCTB IN CAN-FLD.
           MOVE VEN-VAL-SCAP IN VEN TO VEN-VAL-SCAP IN CAN-FLD.
           MOVE VEN-VAL-SFIN IN VEN TO VEN-VAL-SFIN IN CAN-FLD.
           MOVE VEN-NUM-IVEN IN VEN TO VEN-NUM-IVEN IN CAN-FLD.
           MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVCT IN CAN-FLD.
       FIN-PUT-VEN-CAN.
           EXIT.

       INI-CAN SECTION.
       INI-INI-CAN.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE ZEROES TO WSS-NUM-VCVG IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-VCCM IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-VCCO IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-VCCV IN WSS-VARI.
           PERFORM RST-DLC VARYING WSS-NUM-IDLC FROM 1 BY 1
                           UNTIL   WSS-NUM-IDLC > WSS-LIM-IDLC.
       FIN-INI-CAN.
           EXIT.

       RST-DLC SECTION.
       INI-RST-DLC.
           MOVE ZEROES TO WSS-SGV-CAPD(WSS-NUM-IDLC)
                          WSS-SGV-FINA(WSS-NUM-IDLC).
       FIN-RST-DLC.
           EXIT.

      *Marca registro MVO cerrado
       CIE-REG-MVO SECTION.
       INI-CIE-REG-MVO.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-MVO.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO GET-KEY-UPD, MVO:' MVO-KEY-IMVO
               ADD 1 TO WSS-RMVO-NCIE
               GO TO FIN-CIE-REG-MVO.
           MOVE 'C' TO MVO-COD-TCUA IN MVO.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-MVO.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-RMVO-SCIE
           ELSE
               DISPLAY 'NO MOD, MVO:' MVO-KEY-IMVO
               ADD 1 TO WSS-RMVO-NCIE.
       FIN-CIE-REG-MVO.
           EXIT.

      *Marca registro MVO rechazado
       RCH-REG-MVO SECTION.
       INI-RCH-REG-MVO.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-MVO.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO GET-KEY-UPD, MVO:' MVO-KEY-IMVO
               ADD 1 TO WSS-RMVO-NCIR
               GO TO FIN-RCH-REG-MVO.
      * RBG/INI/8-OCT-1996 Se pregunta por la transaccion para saber si es
      *                    cargo o abono.
           IF MVO-COD-TRCR = '0670 '
               MOVE 'Y' TO MVO-COD-EPRC IN MVO.
      *    MOVE 'Y' TO MVO-COD-EPRC IN MVO.
      * RBG/FIN/8-OCT-1996
           MOVE 'C' TO MVO-COD-TCUA IN MVO.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-MVO.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-RMVO-SCIR
           ELSE
               DISPLAY 'NO MOD, MVO:' MVO-KEY-IMVO
               ADD 1 TO WSS-RMVO-NCIR.
       FIN-RCH-REG-MVO.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-RCH.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-TOC.
           PERFORM COL-FIO-EVC.
           PERFORM COL-FIO-RDT.
           PERFORM COL-FIO-MVO.
      *
           DISPLAY 'MOVIMIENTOS   EN INGRESO     : ' WSS-RMVO-INGR.
           DISPLAY 'MOVIMIENTOS   EN RECHAZO     : ' WSS-RMVO-RECH.
           DISPLAY 'MOVIMIENTOS   DE CURSE       : ' WSS-RMVO-ERRO.
           DISPLAY 'MOVIMIENTOS   PROCESADOS     : ' WSS-RMVO-PROC.
           DISPLAY 'MOVIMIENTOS   MARC. COL OK   : ' WSS-RMVO-SCIE.
           DISPLAY 'MOVIMIENTOS   MARC. COL NOOK : ' WSS-RMVO-NCIE.
      *DAD ini  04-OCT-1991
           DISPLAY 'MOVIMIENTOS   OTRO STATUS    : ' WSS-RMVO-OSTT.
      *DAD fin  04-OCT-1991
           DISPLAY 'MOVIMIENTOS   RECH CERR OK   : ' WSS-RMVO-SCIR.
           DISPLAY 'MOVIMIENTOS   RECH CERR NOOK : ' WSS-RMVO-NCIR.
           DISPLAY 'OPERACIONES   ACTIVADAS OK   : ' WSS-ROPC-SACT.
           DISPLAY 'OPERACIONES   ACTIVADAS NOOK : ' WSS-ROPC-NACT.
           DISPLAY 'CANCELACIONES ACTIVADAS OK   : ' WSS-RCAN-SACT.
           DISPLAY 'CANCELACIONES ACTIVADAS NOOK : ' WSS-RCAN-NACT.
       FIN-CLOSE-FILE.
           EXIT.

       COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBBSYS.
       COPY GNSBGEND.
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
       COPY GNSBGABT.
      *DAD fin  29-MAY-1992
      *SMC-INI 09.09.1993
       COPY COLBGINT.
      *SMC-FIN 09.09.1993
       COPY GNSBGPES.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
      *
       COPY COLB4CAN.
       COPY COLB5CAN.
       COPY COLBGVEN.
       COPY COLBFOPC.
       COPY COLBFCAN.
       COPY COLBFDLC.
       COPY COLBFRCV.
       COPY COLBFVEN.
       COPY COLBFTOC.
       COPY COLBFEVC.
       COPY COLBFRDT.
       COPY COLBFICG.
      *JSS PARA PROBAR EN CST EXISTE EL COPY COLBFMVO QUE ACCESA
      *UN VSAM CON LA ESTRUCTURA DE LA TABLA MVO
       COPY COLBFMVO.
      *JSS PARA PROBAR EN CST EXISTE EL COPY COLBFRCH VACIO
       COPY COLBFRCH.
      *
       COPY TABBFCAM.
