       IDENTIFICATION DIVISION.
       PROGRAM-ID. COLPGCCT.
      *AUTHOR.         ZAPATA FUENTES HUGO.
      *                DATE-WRITTEN,  01/06/92.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  SISTEMA  : SISTEMA COLOCACIONES                               *
      *  PROGRAM. : HUGO ZAPATA                                        *
      *  OBJETIVO :GENERAR MOV. A CUENTAS CORRIENTES POR CONCEPTO      *
      *            DE COBRO DE IMPUESTOS.                              *
      *  Archivos :                                                    *
      *   ENTRADA : LDA-390, LINEAS DE AVANCE.                         *
      *             COLVIMO,  ARCHIVO DE IMPUESTOS. SAM                *
      *   SALIDA  : COLVMCC   ARCHIVO PARA CUENTAS CORRIENTES.         *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: O.Donoso...................            *
      *            Fecha      : 15/11/93                               *
      *            Cambio     : Se cambia codigo de campo              *
      *                         CCT-COD-MVTO por "G" en vez de "I"     *
      *                                                                *
      *            PROGRAMADOR: P.RETAMAL..................            *
      *            FECHA      : 18/07/95                               *
      *            CAMBIO     : SE AGREGA POBLAMIENTO DE CAMPO         *
      *                         CCT-SUCUR CON LA SUC DE LA CTA.CTE     *
      *                         OBTENIDO DESDE LA TABLA OPE.           *
      ******************************************************************
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
           C01 IS CANAL-1
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT COLVIMO  ASSIGN TO DA-S-COLVIMO.
           SELECT COLVCCT  ASSIGN TO DA-S-COLVCCT.
           SELECT SOR-SORT ASSIGN TO SYS001-DA-S-SORTWK1.
       DATA DIVISION.
       FILE SECTION.
       FD  COLVIMO
           RECORD  CONTAINS 128 CHARACTERS
           BLOCK   CONTAINS 0   RECORDS
           LABEL RECORDS IS STANDARD.
      ******************************************************************
      *             DESCRIPCION DE REGISTRO (COLVIMO)
      *             ----------------------------------
      *
      *    ARCHIVO   : MOVTOS. DE CARGOS, ABONOS E IMPUESTO
      *              : GENERADO POR PGM COLPGIMP.
      *    FD        : COLVIMO
      *    PREFIJO   : 'IMO'
      *    LARGO REG.: 128
      *    ORGANIZAC.: SAM
      *    BLOQUEO   : NO
      *
      ******************************************************************
      *
       01  IMO-REGISTRO.

           05  IMO-CLAVE-LDA                   PIC  X(12).
           05  IMO-CIC-IOPC                    PIC  X(12).
           05  IMO-TIPO-MOV                    PIC  X(01).
           05  IMO-FEC-MOV.
               10  IMO-FEC-MOV-SS              PIC  X(02).
               10  IMO-FEC-MOV-AA              PIC  X(02).
               10  IMO-FEC-MOV-MM              PIC  X(02).
               10  IMO-FEC-MOV-DD              PIC  X(02).
           05  IMO-MONTO                       PIC S9(11)V9999.
           05  IMO-INTERES                     PIC S9(11)V9999.
           05  IMO-REAJUSTES                   PIC S9(11)V9999.
           05  IMO-SDO-AVANCE                  PIC S9(11)V9999.
           05  IMO-TASA                        PIC S9(04)V9999.
           05  IMO-IND-VENCIDA                 PIC  X(01).
           05  IMO-FILLER                      PIC  X(26).

       FD  COLVCCT
           RECORD  CONTAINS  80 CHARACTERS
           BLOCK   CONTAINS 0   RECORDS
           LABEL RECORDS IS STANDARD.
      ******************************************************************
      *             DESCRIPCION DE REGISTRO (COLVCCT)
      *             ----------------------------------
      *
      *    ARCHIVO   : MOVIMIENTOS A CUENTAS CORRIENTES X IMPTO
      *              : GENERADO POR PGM. COLPGCCT.
      *    FD        : COLVCCT
      *    PREFIJO   : 'CCT'
      *    LARGO REG.: 80
      *    ORGANIZAC.: SAM
      *    BLOQUEO   : 10 REG. X BLOQUE
      *
      ******************************************************************
      *
       01  CCT-REGISTRO.
           05 CCT-TIP-REGI             PIC 9(01).
           05 CCT-CTA-CTE              PIC 9(08).
           05 CCT-IMPUT                PIC 9(01).
           05 FILLER                   PIC X(07).
           05 CCT-VALOR                PIC 9(10)V99.
           05 CCT-COD-MVTO             PIC X(01).
           05 CCT-CAJA-BCO             PIC 9(02).
           05 FILLER                   PIC X(04).
           05 CCT-FEC-MVTO             PIC 9(05).
           05 CCT-CAJ-AUTO             PIC X(01).
           05 FILLER                   PIC X(13).
           05 CCT-SUC-ORIG             PIC X(03).
           05 CCT-COD-SIST             PIC X(04).
           05 CCT-SUCUR                PIC X(03).
           05 FILLER                   PIC X(11).
           05 CCT-MAR-MCAR             PIC X(01).
           05 FILLER                   PIC X(03).


       SD  SOR-SORT
           RECORD CONTAINS 31 CHARACTERS.
       01  SORT-REG.
           10 SOR-CTA-CTE       PIC X(08).
           10 SOR-MONTO         PIC S9(11)V9999.
           10 SOR-FECHA.
              20  SOR-FECHA-DD  PIC X(02).
              20  SOR-FECHA-MM  PIC X(02).
              20  SOR-FECHA-AA  PIC X(01).
           10 SOR-OFICINA       PIC X(03).
       WORKING-STORAGE SECTION.
      *-----------------------
       01  PAR-FECHA.
           10 PAR-FEC-AA             PIC 9(02).
           10 PAR-FEC-MM             PIC 9(02).
           10 PAR-FEC-DD             PIC 9(02).
           10 PAR-NADA               PIC X(74).

       01  W-FECHAS.
           10 W-FECHA-PROCESO        PIC X(08).
           10 WOR-FECHA-AA.
              20 WOR-FECHA-AA-1      PIC X.
              20 WOR-FECHA-AA-2      PIC X.
       01  SW-SWITCHES-OPC.
           20 SW-LDA                 PIC 9(01) VALUE 0.
              88 CON-LDA             VALUE 0.
              88 SIN-LDA             VALUE 1.
           20 SW-SORT                PIC 9(01) VALUE 0.
              88 SORT-OK             VALUE 0.
              88 FIN-SORT            VALUE 1.
           20 SW-COLVIMO             PIC 9(01) VALUE 0.
              88 COLVIMO-OK          VALUE 0.
              88 FIN-COLVIMO         VALUE 1.
       01  W-STATUS-ERRORES.
           20 WR-SECCION             PIC X(30).
           20 WR-PARRAFO             PIC X(30).
           20 WR-COMANDO             PIC X(30).
           20 WR-ARCHIVO             PIC X(30).
           20 WR-STATUS              PIC X(30).
           20 WR-TERMINAR            PIC X(01)  VALUE SPACES.
       01  W-ACUMULADORES.
           10 WA-SEL                 PIC 9(06)    VALUE 0.
           10 WA-GRA                 PIC 9(06)    VALUE 0.
           10 WA-SIN-LDA             PIC 9(06)    VALUE 0.
      *****************************************************************
      *          USER INFORMATION BLOCK
      *****************************************************************
       01  UIB.
           05 FILLER                 PIC X(32) VALUE 'COLPGCCT'.
      *****************************************************************
      *           REQUEST AREA
      *****************************************************************
       01  REQ-AREA-LDA.
           03 LDA-COMMAND         PIC X(5)     VALUE 'REDKY'.
           03 LDA-TABLE           PIC X(3)     VALUE 'LDA'.
           03 LDA-KEY             PIC X(5)     VALUE 'LDA01'.
           03 LDA-RET-COD         PIC X(2)     VALUE SPACES.
           03 LDA-INT-RC          PIC X(1).
           03 LDA-DB-ID           PIC S9(4)    COMP VALUE +390.
           03 LDA-REC-ID          PIC X(7).
           03 FILLER              PIC X(25).
           03 LDA-CONT-MAX        PIC S9(4)    COMP.
           03 LDA-CONT-ID         PIC S9(4)    COMP.
           03 FILLER              PIC X(22).
           03 LDA-VALUE           PIC X(50).
      *01  REG-LDA.
      *    COPYDD COL-R-LDA.COL-E-LDA00(PROD),01,D
      *                             LINEA DE AVANCES
       01  LDA.
      *                   CLAVE
                   04  LDA-CLV-LDA-1.
      *                             CIC CLIENTE
                       05  CLI-CIC      PICTURE X(12).
      *                             CODIGO OPERACION (CLAVE PRIMARIA)
                       05  LDA-COP.
      *                             NUMERO SISTEMA
                           06  LDA-NUM-SIS
                                    PICTURE X(4).
      *                             CODIGO CORRELATIVO
                           06  LDA-COD-CRR
                                    PICTURE X(8).
      *                             CODIGO OFICINA
                   04  LDA-COD-OFI  PICTURE X(3).
      *                             ESTADO OPERACION
                   04  LDA-COD-EST  PICTURE X(3).
      *                             VALOR DE CAMBIO (MONEDA)
                   04  LDA-VCB      PICTURE X(6).
      *                             CODIGO EJECUTIVO
                   04  LDA-COD-EJE  PICTURE X(12).
      *                             FECHA INGRESO COMPUTACIONAL
                   04  LDA-FEC-ING-CMP.
      *                             FECHA (ANO) INGRESO COMPUT.
                       05  LDA-FA-ING-CMP
                                    PICTURE 9(4).
      *                             FECHA (MES) INGRESO COMPUT.
                       05  LDA-FM-ING-CMP
                                    PICTURE 9(2).
      *                             FECHA (MES) INGRESO COMPUT.
                       05  LDA-FD-ING-CMP
                                    PICTURE 9(2).
      *                             FECHA CURSE REAL
                   04  LDA-FEC-CUR-REA.
      *                             FECHA (ANO) CURSE REAL
                       05  LDA-FA-CUR-REA
                                    PICTURE 9(4).
      *                             FECHA (MES) CURSE REAL
                       05  LDA-FM-CUR-REA
                                    PICTURE 9(2).
      *                             FECHA (MES) CURSE REAL
                       05  LDA-FD-CUR-REA
                                    PICTURE 9(2).
      *                             FECHA RENOVACION
                   04  LDA-FEC-REN-LIN.
      *                             FECHA (ANO) RENOVACION
                       05  LDA-FA-REN-LIN
                                    PICTURE 9(4).
      *                             FECHA (MES) RENOVACION
                       05  LDA-FM-REN-LIN
                                    PICTURE 9(2).
      *                             FECHA (DIA) RENOVACION
                       05  LDA-FD-REN-LIN
                                    PICTURE 9(2).
      *                             FECHA VENCIMIENTO LINEA
                   04  LDA-FEC-VTO-LIN.
      *                             FECHA (ANO) VENCIMIENTO LINEA
                       05  LDA-FA-VTO-LIN
                                    PICTURE 9(4).
      *                             FECHA (MES) VENCIMIENTO LINEA
                       05  LDA-FM-VTO-LIN
                                    PICTURE 9(2).
      *                             FECHA (DIA) VENCIMIENTO LINEA
                       05  LDA-FD-VTO-LIN
                                    PICTURE 9(2).
      *                             MONTO AUTORIZADO LINEA
                   04  LDA-VAL-AUT  PICTURE S9(11)V9(4).
      *                             FECHA ULTIMA ACTUALIZACION
                   04  LDA-FEC-ULT-ACT.
      *                             FECHA (ANO) ULTIMA ACTUALIZ.
                       05  LDA-FA-ULT-ACT
                                    PICTURE 9(4).
      *                             FECHA (MES) ULTIMA ACTUALIZ.
                       05  LDA-FM-ULT-ACT
                                    PICTURE 9(2).
      *                             FECHA (DIA) ULTIMA ACTUALIZ.
                       05  LDA-FD-ULT-ACT
                                    PICTURE 9(2).
      *                             HORA ULTIMA ACTUALIZACION
                   04  LDA-HOR-ACT  PICTURE 9(6).
      *                             TERMINAL ULTIMA ACTUALIZACION
                   04  LDA-TML-ACT  PICTURE X(4).
      *                             ID. USUARIO ULT ACTUALIZACION
                   04  LDA-IDN-USU-ACT
                                    PICTURE X(12).
      *                             CUENTA CORRIENTE
                   04  CCT-COP.
      *                             NUMERO DE SISTEMA
                       05  CCT-NUM-SIS
                                    PICTURE X(4).
      *                             CODIGO CORRELATIVO
                       05  CCT-COD-CRR
                                    PICTURE X(8).
                   04  FILLER1      PICTURE X(20).


       01  REQ-AREA-OPE.
           03 OPE-COMMAND         PIC X(5)     VALUE 'REDKY'.
           03 OPE-TABLE           PIC X(3)     VALUE 'OPE'.
           03 OPE-KEY             PIC X(5)     VALUE 'OPE00'.
           03 OPE-RET-COD         PIC X(2)     VALUE SPACES.
           03 OPE-INT-RC          PIC X(1).
           03 OPE-DB-ID           PIC S9(4)    COMP VALUE +100.
           03 OPE-REC-ID          PIC X(7).
           03 FILLER              PIC X(25).
           03 OPE-CONT-MAX        PIC S9(4)    COMP.
           03 OPE-CONT-ID         PIC S9(4)    COMP.
           03 FILLER              PIC X(22).
           03 OPE-VALUE           PIC X(50).

           COPY SGCBROPE.

      ******************************************************************
      *                     ELEMENT LIST
      ******************************************************************


       01  ELEM-LIST-LDA.
           05 ELEM-1          PIC X(5) VALUE 'LDA01'.
           05 ELEM-SEC        PIC X(1).
           05 FILLER          PIC X(5) VALUE SPACES.

       01  ELEM-LIST-OPE.
           05 ELEM-1          PIC X(5) VALUE 'OPE00'.
           05 ELEM-SEC        PIC X(1).
           05 FILLER          PIC X(5) VALUE SPACES.

       PROCEDURE DIVISION.
      *------------------
       A-CONTROL SECTION.
           ENTRY  'DBMSCBL'.
       AA-INICIALIZACION.
           PERFORM B-INICIO.
       AB-PROCESAMIENTO.
           PERFORM C-PROCESAR.
       AC-TERMINACION.
           PERFORM D-TERMINAR.
       AZ-FIN-DE-PROCESO.
           GOBACK.

       B-INICIO SECTION.

       B0-CONTROL.
      *-----------
           PERFORM BA-ABRIR-ARCHIVOS.
           GO TO   BZ-FIN-SECCION.

       BA-ABRIR-ARCHIVOS.
      *-----------------
             ACCEPT PAR-FECHA     FROM SYSIN.
             OPEN INPUT    COLVIMO
                  OUTPUT   COLVCCT.


       BZ-FIN-SECCION.
      *---------------
           EXIT.

       C-PROCESAR SECTION.
      *-----------------
       C0-CONTROL.
      *-----------
           IF WR-TERMINAR = 'S'
              GO TO CZ-FIN-SECCION
           END-IF.
           SORT SOR-SORT ON ASCENDING  KEY SOR-OFICINA
           INPUT  PROCEDURE E-ENTRADA
           OUTPUT PROCEDURE S-SALIDA.
           GO TO CZ-FIN-SECCION.

       CZ-FIN-SECCION.
      *---------------
           EXIT.


       E-ENTRADA SECTION.
      *------------------
           PERFORM EA-LEER-COLVIMO.
           PERFORM EB-LLENAR-SORT UNTIL FIN-COLVIMO.
           GO TO EZ-FIN-SECCION.

       EA-LEER-COLVIMO.
      *------------------
           READ COLVIMO AT END SET FIN-COLVIMO TO TRUE.

       EB-LLENAR-SORT.
      *------------------
           IF IMO-TIPO-MOV = 'I'
           AND IMO-MONTO > 0
               PERFORM EC-SELECCINADO.
           PERFORM EA-LEER-COLVIMO.

       EC-SELECCINADO.
      *------------------
           MOVE  IMO-CLAVE-LDA                  TO LDA-VALUE.
           PERFORM ED-ACCESAR-LDA.
           IF CON-LDA
              ADD  1                            TO WA-SEL
              PERFORM EE-LLENAR-CAMPOS
              RELEASE SORT-REG
           ELSE
              ADD  1                            TO WA-SIN-LDA
           END-IF.

       ED-ACCESAR-LDA.
      *---------------
           CALL 'DBNTRY' USING UIB REQ-AREA-LDA LDA ELEM-LIST-LDA
           IF        LDA-RET-COD = 14
                     SET SIN-LDA         TO   TRUE
           ELSE      IF     LDA-RET-COD = SPACES
                            SET CON-LDA         TO   TRUE
                     ELSE
                            MOVE 'CA-ACCESAR-LDA'      TO WR-PARRAFO
                            MOVE 'C-PROCESAR        '  TO WR-SECCION
                            MOVE LDA-COMMAND           TO WR-COMANDO
                            MOVE 'TABLA LDA         '  TO WR-ARCHIVO
                            MOVE LDA-RET-COD           TO WR-STATUS
                            MOVE 'S'                   TO WR-TERMINAR
                     END-IF
           END-IF.

       EE-LLENAR-CAMPOS.
      *------------------
           MOVE IMO-FEC-MOV      TO W-FECHA-PROCESO.
           MOVE IMO-MONTO        TO SOR-MONTO.
           MOVE CCT-COD-CRR      TO SOR-CTA-CTE.
           MOVE PAR-FEC-DD       TO SOR-FECHA-DD.
           MOVE PAR-FEC-MM       TO SOR-FECHA-MM.
           MOVE PAR-FEC-AA       TO WOR-FECHA-AA.
           MOVE WOR-FECHA-AA-2   TO SOR-FECHA-AA.
           MOVE LDA-COD-OFI      TO SOR-OFICINA.

       EZ-FIN-SECCION.
      *------------------
           EXIT.

       S-SALIDA SECTION.
      *------------------
           PERFORM SA-LEER-SORT
           PERFORM SB-PROCESO-SORT UNTIL FIN-SORT.
           GO TO SZ-FIN-SECCION.

       SA-LEER-SORT.
      *----------
           RETURN SOR-SORT AT END SET  FIN-SORT   TO TRUE.

       SB-PROCESO-SORT.
      *----------------
           PERFORM SC-GRABAR.
           PERFORM SA-LEER-SORT.

       SC-GRABAR.
      *----------
           MOVE  7                     TO CCT-TIP-REGI.
           MOVE SOR-CTA-CTE            TO CCT-CTA-CTE.
           MOVE 9                      TO CCT-IMPUT.
           MOVE SOR-MONTO              TO CCT-VALOR.
           MOVE 'G'                    TO CCT-COD-MVTO.
           MOVE ZEROES                 TO CCT-CAJA-BCO.
           MOVE SOR-FECHA              TO CCT-FEC-MVTO.
           MOVE SPACES                 TO CCT-CAJ-AUTO.
           MOVE SOR-OFICINA            TO CCT-SUC-ORIG.
           MOVE SPACES                 TO CCT-COD-SIST.
           PERFORM SD-BUSCA-OPE
      *****MOVE ZEROES                 TO CCT-SUCUR.
           MOVE SPACES                 TO CCT-MAR-MCAR.
           WRITE CCT-REGISTRO.
           ADD   1                 TO  WA-GRA.

       SD-BUSCA-OPE.
      *------------
           MOVE '0000'                   TO OPE-CAI-IOPE
           MOVE SOR-CTA-CTE              TO OPE-IIC-IOPE
           MOVE OPE-CIC-IOPE             TO OPE-VALUE
           CALL 'DBNTRY' USING UIB REQ-AREA-OPE OPE ELEM-LIST-OPE
           IF OPE-RET-COD NOT = '  '
              MOVE 'CD-BUSCA-OPE'        TO WR-PARRAFO
              MOVE 'C-PROCESAR        '  TO WR-SECCION
              MOVE OPE-COMMAND           TO WR-COMANDO
              MOVE 'TABLA OPE         '  TO WR-ARCHIVO
              MOVE OPE-RET-COD           TO WR-STATUS
              MOVE 'S'                   TO WR-TERMINAR
              MOVE ZEROES                TO CCT-SUCUR
           ELSE
              MOVE OPE-COD-OFOP          TO CCT-SUCUR
           END-IF.

       SZ-FIN-SECCION.
      *---------------
           EXIT.

       D-TERMINAR SECTION.
      *------------------
           DISPLAY '*'
           IF WR-TERMINAR = 'S'
              DISPLAY  'PARRAFO            '     WR-PARRAFO
              DISPLAY  'SECCION            '     WR-SECCION
              DISPLAY  'PARRAFO            '     WR-COMANDO
              DISPLAY  'ARCHIVO            '     WR-ARCHIVO
              DISPLAY  'STATUS             '     WR-STATUS
              MOVE 100                       TO RETURN-CODE
           END-IF.
           DISPLAY '*           ESTADISTICAS'
           DISPLAY '*'
           DISPLAY '*  PROCESO AL: '  PAR-FEC-DD PAR-FEC-MM PAR-FEC-AA
           DISPLAY '*'
           DISPLAY '*'
           DISPLAY '* REGISTROS SELECCIONADOS.........' WA-SEL.
           DISPLAY '* REGISTROS GRABADOS..............' WA-GRA.
           DISPLAY '* REGISTROS SIN LDA...............' WA-SIN-LDA.
      *
           CLOSE COLVIMO COLVCCT.
      *
       DZ-FIN-SECCION.
      *---------------
           EXIT.
      *
      *
