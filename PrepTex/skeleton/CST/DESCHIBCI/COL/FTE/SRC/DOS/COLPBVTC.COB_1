       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPBVTC.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    14-Apr-89 05:50 PM.

      * Programa de traspaso Vencimientos a Cartera Vencida.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY COLBRVEN.
       COPY COLBREVC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
      *<<<< WSS
       01  WSS-VARI.

      * OFICINA
           03  WSS-COD-OFIC                          PIC X(03).

      * FECHA INGRESADA COMO PARAMETRO INICIO DEL RANGO
           03  WSS-FEC-FMIN.
               05  WSS-NUM-DMIN                      PIC 9(02).
               05  WSS-NUM-MMIN                      PIC 9(02).
               05  WSS-NUM-SMIN                      PIC 9(02).
               05  WSS-NUM-AMIN                      PIC 9(02).

      * FECHA INICIO RANGO PARA BUSCAR EN ARCHIVO VEN
      * O CCB
           03  WSS-FEC-FINI.
               05  WSS-NUM-DINI                      PIC 9(02).
               05  WSS-NUM-MINI                      PIC 9(02).
               05  WSS-NUM-SINI                      PIC 9(02).
               05  WSS-NUM-AINI                      PIC 9(02).

      * FECHA INGRESADA COMO PARAMETRO FIN DEL RANGO
           03  WSS-FEC-FMAX.
               05  WSS-NUM-DMAX                      PIC 9(02).
               05  WSS-NUM-MMAX                      PIC 9(02).
               05  WSS-NUM-SMAX                      PIC 9(02).
               05  WSS-NUM-AMAX                      PIC 9(02).

      * FECHA FINAL RANGO PARA BUSCAR EN ARCHIVO VEN
      * O CCB
           03  WSS-FEC-FTER.
               05  WSS-NUM-DTER                      PIC 9(02).
               05  WSS-NUM-MTER                      PIC 9(02).
               05  WSS-NUM-STER                      PIC 9(02).
               05  WSS-NUM-ATER                      PIC 9(02).

      * FECHA CAMBIO SITUACION CONTABLE
           03  WSS-FEC-FCCT.
               05  WSS-NUM-DCCT                      PIC 9(02).
               05  WSS-NUM-MCCT                      PIC 9(02).
               05  WSS-NUM-SCCT                      PIC 9(02).
               05  WSS-NUM-ACCT                      PIC 9(02).

      * FECHA INICIO RANGO ACELERACION
           03  WSS-FEC-FIRA.
               05  WSS-NUM-DIRA                      PIC 9(02).
               05  WSS-NUM-MIRA                      PIC 9(02).
               05  WSS-NUM-SIRA                      PIC 9(02).
               05  WSS-NUM-AIRA                      PIC 9(02).


      * FECHA TERMINO RANGO ACELERACION
           03  WSS-FEC-FTRA.
               05  WSS-NUM-DTRA                      PIC 9(02).
               05  WSS-NUM-MTRA                      PIC 9(02).
               05  WSS-NUM-STRA                      PIC 9(02).
               05  WSS-NUM-ATRA                      PIC 9(02).

      * TOTAL DIAS NO HABILES
           03  WSS-NUM-TDNH         VALUE ZEROES     PIC 9(03).

      * TOTAL
      * DIAS NECESARIOS PARA PASAR UN VENCIMIENTO A
      * CARTERA
      * VENCIDA
           03  WSS-NUM-TDCV         VALUE ZEROES     PIC 9(05).
           03  WSS-NUM-TDCV-RED
                          REDEFINES WSS-NUM-TDCV     PIC X(05).

      * TOTAL DE VENCIMIENTOS EN CARTERA VENCIDA
      *                  QUE IMPLICAN ACELERACION
           03  WSS-NUM-TACV         VALUE ZEROES     PIC 9(05).
           03  WSS-NUM-TACV-RED
                          REDEFINES WSS-NUM-TACV     PIC X(05).

      * INDICADOR DE ACELERACION PARA CCT
           03  WSS-IND-ACEL         VALUE LOW-VALUES PIC X(01).
      * SITUACION CONTABLE PARA DLC
           03  WSS-DLC-IND-SCTB     VALUE LOW-VALUES PIC X(03).
           03  WSS-COD-SVCD         VALUE LOW-VALUES PIC X(08).
           03  WSS-COD-SCAS         VALUE LOW-VALUES PIC X(08).
           03  WSS-COD-SMOR         VALUE LOW-VALUES PIC X(08).
           03  WSS-COD-SIDJ         VALUE LOW-VALUES PIC X(08).
           03  WSS-COD-VCOC         VALUE LOW-VALUES PIC X(06).
           03  WSS-CIC-IOPC         VALUE LOW-VALUES PIC X(12).
           03  WSS-COD-RMOC         VALUE LOW-VALUES PIC X(12).
           03  WSS-VAL-SCAP         COMP-3     PIC 9(11)V9(04).
      * SALDO DEL DOCUMENTO PARA CCT
           03  WSS-VAL-SRDT         COMP-3     PIC 9(11)V9(04).

      * TOTAL DE REGISTROS VEN SELECCIONADOS
           03  WSS-NUM-SVEN         VALUE ZEROES     PIC 9(05).
      * TOTAL DE REGISTROS VEN PROCESADOS
           03  WSS-NUM-PVEN         VALUE ZEROES     PIC 9(05).

           03  WSS-NUM-TRDT         VALUE ZEROES     PIC 9(05).
           03  WSS-NUM-TCCT         VALUE ZEROES     PIC 9(05).

       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSBRMSC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY SGCBRDBC.
       COPY COLBRRDC.
       COPY TABBROFI.
       COPY TABBRVLR.
       COPY TABBRCAM.
       COPY TABBRUSR.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRCCT.
       COPY COLBRRDT.
       COPY COLBRCCB.
       COPY GNSWGCPT.
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CCT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CCB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VLR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
      *>>>>

       PROCEDURE DIVISION.
      *==================

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPBVTC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM INI.
           PERFORM PRO-VEN.
           PERFORM PRO-ACEL.
           PERFORM FIN.
       FIN-MAIN.
           PERFORM GNS-PRO-END.


       INI SECTION.
       INI-INI.

      *Captura Oficina
           DISPLAY 'OFICINA ( */XXX ) :'.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY ' OFICINA : ' WSS-COD-OFIC IN WSS-VARI.
           DISPLAY ' '.

      *Captura Fecha de Inicio
      *     DISPLAY 'FECHA DE INICIO ( DDMMSSAA ) :'.
      *     ACCEPT  WSS-FEC-FMIN IN WSS-VARI.
      *     DISPLAY ' RANGO DE SOLICITUD :'
      *             ' FECHA INICIAL          = ' WSS-FEC-FMIN.
      *     DISPLAY ' '.
           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO WSS-NUM-DMIN IN WSS-FEC-FMIN.
           MOVE HOY-MHOY TO WSS-NUM-MMIN IN WSS-FEC-FMIN.
           MOVE HOY-SHOY TO WSS-NUM-SMIN IN WSS-FEC-FMIN.
           MOVE HOY-AHOY TO WSS-NUM-AMIN IN WSS-FEC-FMIN.

      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMSSAA ) : '
           ACCEPT  WSS-FEC-FMAX IN WSS-VARI.
           DISPLAY ' RANGO DE SOLICITUD :'
                   ' FECHA FINAL            = ' WSS-FEC-FMAX.
           DISPLAY ' '.

      *VAL-OFI Valida Oficina
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS NOT = SPACES
                   DISPLAY 'ABORTO : ' WSS-COD-OFIC
                   DISPLAY MSG-GLS-DESC
                   PERFORM GNS-PRO-END.

      *VAL-FEC Valida Fecha de Inicio
      *     MOVE WSS-FEC-FMIN IN WSS-VARI TO FEC-FECH.
      *     MOVE FEC-FORM-FEC TO FEC-FORM.
      *     MOVE FEC-FBLK-N TO FEC-FBLK.
      *     MOVE FEC-CHOY-N TO FEC-CHOY.
      *     MOVE FEC-VHBL-N TO FEC-VHBL.
      *     MOVE FEC-VAL-FEC TO FEC-CMND.
      *     PERFORM PRO-FEC.
      *     IF NOT FEC-STAT-OKS
      *         DISPLAY 'ABORTO : ' WSS-FEC-FMIN
      *         DISPLAY FEC-MENS
      *         PERFORM GNS-PRO-END.

      *VAL-FEC Valida Fecha de Termino
           MOVE WSS-FEC-FMAX IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N TO FEC-FBLK.
           MOVE FEC-CHOY-N TO FEC-CHOY.
           MOVE FEC-VHBL-N TO FEC-VHBL.
           MOVE FEC-VAL-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ABORTO : ' WSS-FEC-FMAX
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FMIN IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FMAX IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB' TO MSG-COD-SIST
               MOVE 'FECFMINFMAX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY 'ABORTO : ' WSS-FEC-FMIN '  ' WSS-FEC-FMAX
               DISPLAY MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-END.


      * BUSCA CODIGO DE SITUACION CONTABLE "VENCIDA"

           MOVE 'COL'  TO MSC-COD-SIST.
           MOVE 'SCTB' TO MSC-CIC-TMSC IN MSC.
           MOVE 'VCD'  TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS = SPACES
               MOVE MSC-COD-CMSC IN MSC TO WSS-COD-SVCD
           ELSE
               DISPLAY 'ABORTO : SIS = COL, MSC = SCTB, '
                       'MSC-CIC-CMSC = VCD'
               DISPLAY ' ' MSG-GLS-DESC
               PERFORM GNS-PRO-END.


      * BUSCA CODIGO DE SITUACION CONTABLE "CASTIGADA"

           MOVE 'COL'  TO MSC-COD-SIST.
           MOVE 'SCTB' TO MSC-CIC-TMSC IN MSC.
           MOVE 'CAS'  TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS = SPACES
               MOVE MSC-COD-CMSC IN MSC TO WSS-COD-SCAS
           ELSE
               DISPLAY 'ABORTO : SIS = COL, MSC = SCTB, '
                       'MSC-CIC-CMSC = CAS'
               DISPLAY ' ' MSG-GLS-DESC
               PERFORM GNS-PRO-END.

      * BUSCA CODIGO DE SITUACION CONTABLE "MOROSA"

           MOVE 'COL'  TO MSC-COD-SIST.
           MOVE 'SCTB' TO MSC-CIC-TMSC IN MSC.
           MOVE 'MOR'  TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS = SPACES
               MOVE MSC-COD-CMSC IN MSC TO WSS-COD-SMOR
           ELSE
               DISPLAY 'ABORTO : SIS = COL, MSC = SCTB, '
                       'MSC-CIC-CMSC = MOR'
               DISPLAY ' ' MSG-GLS-DESC
               PERFORM GNS-PRO-END.

      * BUSCA CODIGO DE SITUACION COBRANZA "INICIO DEMANDA JUDICIAL"

           MOVE 'COL'  TO MSC-COD-SIST.
           MOVE 'SCBZ' TO MSC-CIC-TMSC IN MSC.
           MOVE 'IDJ'  TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS = SPACES
               MOVE MSC-COD-CMSC IN MSC TO WSS-COD-SIDJ
           ELSE
               DISPLAY 'ABORTO : SIS = COL, MSC = SCBZ, '
                       'MSC-CIC-CMSC = IDJ'
               DISPLAY ' ' MSG-GLS-DESC
               PERFORM GNS-PRO-END.

      * BUSCA PARAMETRO "CANTIDAD DE DIAS PARA PASAR UN VENCIMIENTO
      *                  A CARTERA VENCIDA"

           MOVE 'COL' TO TAB-COD-SIST.
           MOVE 'PCV' TO TAB-COD-TTAB IN TAB.
           MOVE 'DCV' TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY 'ABORTO : SIS = COL, TAB = PCV, COD = DCV'
               DISPLAY MSG-GLS-DESC
               PERFORM GNS-PRO-END.

           MOVE TAB-GLS-ABRV TO WSS-NUM-TDCV-RED.
           IF WSS-NUM-TDCV-RED NOT NUMERIC
               DISPLAY 'ABORTO : SIS = COL, TAB = PCV, COD = DCV'
               DISPLAY 'ABREVIACION NO NUMERICA'
               PERFORM GNS-PRO-END.

      * BUSCA PARAMETRO "CANTIDAD DE DIAS DESPUES QUE SE INICIA
      * LA DEMANDA JUDICIAL QUE IMPLICA ACELERACION

           MOVE 'COL' TO TAB-COD-SIST.
           MOVE 'PCV' TO TAB-COD-TTAB IN TAB.
           MOVE 'ACV' TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY 'ABORTO : SIS = COL, TAB = PCV, COD = ACV'
               DISPLAY MSG-GLS-DESC
               PERFORM GNS-PRO-END.

           MOVE TAB-GLS-ABRV TO WSS-NUM-TACV-RED.
           IF WSS-NUM-TACV-RED NOT NUMERIC
               DISPLAY 'ABORTO : SIS = COL, TAB = PCV, COD = ACV'
               DISPLAY 'ABREVIACION NO NUMERICA'
               PERFORM GNS-PRO-END.

           DISPLAY 'Fin de Validacion de Parametros'.

      * OBTENER FECHA INICIO RANGO DE SELECCION
      *    WSS-FEC-FINI = WSS-FEC-FMIN - WSS-NUM-DTVC
           MOVE WSS-FEC-FMIN TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA TO FEC-CMND.
           MOVE WSS-NUM-TDCV TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-FECH TO WSS-FEC-FINI.
           DISPLAY ' RANGO DE SELECCION :'
                   ' FECHA INICIAL          = ' WSS-FEC-FINI.
           DISPLAY ' '.

      * SI EL WSS-FEC-FMIN NO ES HABIL, WSS-FEC-FINI
      * DEBE QUEDAR COMO  WSS-FEC-FINI + EL TOTAL DE DIAS NO HABILES
      * CONTIGUOS HACIA ADELANTE DE WSS-FEC-FMIN

      *VAL-FEC Validar Fecha
           MOVE WSS-FEC-FMIN TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-HBL
                GO TO NOT-BUS-PDH.
      * BUS-PRIMER-DIA-HABIL
           MOVE 1 TO WSS-NUM-TDNH.
           MOVE WSS-FEC-FMIN TO FEC-FECH.
       BUS-PDH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-SUM-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
      *VAL-FEC Validar Fecha
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-HBL
                ADD 1 TO WSS-NUM-TDNH
                GO TO BUS-PDH.
       NOT-BUS-PDH.

           IF WSS-NUM-TDNH > ZEROES
               MOVE WSS-FEC-FINI        TO FEC-FECH
               MOVE FEC-FORM-FEC        TO FEC-FORM
               MOVE FEC-SUM-DIA         TO FEC-CMND
               MOVE WSS-NUM-TDNH        TO FEC-NDIA
      *        PERFORM PRO-FEC
               PERFORM CAL-FEC
               MOVE FEC-FECH TO WSS-FEC-FINI.

           DISPLAY ' RANGO DE SELECCION :'
                   ' FECHA INICIAL EFECTIVA = ' WSS-FEC-FINI.
           DISPLAY ' '.

      * OBTENER FECHA FINAL RANGO DE SELECCION
      *    WSS-FEC-FTER = WSS-FEC-FMAX - WSS-NUM-DTVC
           MOVE WSS-FEC-FMAX TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA TO FEC-CMND.
           MOVE WSS-NUM-TDCV TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-FECH TO WSS-FEC-FTER.
           DISPLAY ' RANGO DE SELECCION :'
                   ' FECHA FINAL            = ' WSS-FEC-FTER.
           DISPLAY ' '.

      * SI EL DIA SIGUIENTE A WSS-FEC-FMAX ES NO HABIL, WSS-FEC-FTER
      * DEBE QUEDAR COMO  WSS-FEC-FTER + EL TOTAL DE DIAS NO HABILES
      * CONTIGUOS HACIA ADELANTE DE WSS-FEC-FMAX

      * BUS-ULTIMO-DIA-NO-HABIL
           MOVE ZEROES              TO WSS-NUM-TDNH.
           MOVE WSS-FEC-FMAX        TO FEC-FECH.
       BUS-UDNH.
           MOVE FEC-FORM-FEC        TO FEC-FORM.
           MOVE FEC-SUM-DIA         TO FEC-CMND.
           MOVE 1                   TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
      *VAL-FEC Validar Fecha
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-HBL
                ADD 1 TO WSS-NUM-TDNH
                GO TO BUS-UDNH.

           IF WSS-NUM-TDNH > ZEROES
               MOVE WSS-FEC-FTER        TO FEC-FECH
               MOVE FEC-FORM-FEC        TO FEC-FORM
               MOVE FEC-SUM-DIA         TO FEC-CMND
               MOVE WSS-NUM-TDNH        TO FEC-NDIA
      *        PERFORM PRO-FEC
               PERFORM CAL-FEC
               MOVE FEC-FECH TO WSS-FEC-FTER.

           DISPLAY ' RANGO DE SELECCION :'
                   ' FECHA FINAL EFECTIVA   = ' WSS-FEC-FTER.
           DISPLAY ' '.

       FIN-INI.
           EXIT.

       PRO-VEN SECTION.
       INI-PRO-VEN.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-VEN.

      *    OBTIENE PRIMER REGISTRO VEN SEGUN RANGO

           MOVE FIO-GET-NLS TO FIO-CMND.
           MOVE WSS-NUM-DINI TO VEN-NUM-DVEN IN VEN.
           MOVE WSS-NUM-MINI TO VEN-NUM-MVEN IN VEN.
           MOVE WSS-NUM-SINI TO VEN-NUM-SVEN IN VEN.
           MOVE WSS-NUM-AINI TO VEN-NUM-AVEN IN VEN.
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
           PERFORM COL-FIO-VEN.

       LUP-INP-VEN.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-VEN.

      *    VERIFICA QUE FECHA DE VENCIMIENTO NO SOBREPASE LIMITE MAXIMO
           MOVE WSS-FEC-FTER        TO FEC-FEC1.
           MOVE VEN-NUM-DVEN IN VEN TO FEC-DEC2.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-MEC2.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-SEC2.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-AEC2.
           MOVE FEC-FORM-FEC        TO FEC-FORM.
           MOVE FEC-CMP-FEC         TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-LES
              GO TO FIN-INP-VEN.

           ADD 1 TO WSS-NUM-SVEN.
           DISPLAY 'VENCIMIENTO DENTRO DE RANGO FECHAS '
                   VEN-KEY-IVEN.

           IF WSS-CIC-IOPC IN WSS-VARI NOT = VEN-CIC-IOPC IN VEN
               MOVE VEN-CIC-IOPC IN VEN TO WSS-CIC-IOPC IN WSS-VARI
               PERFORM GET-CUR-OPC
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR : '
                   DISPLAY 'VENCIMIENTO SIN OPERACION '
                           'ASOCIADA : ' VEN-CIC-IOPC IN VEN
                   DISPLAY ' '
                   GO TO NXT-INP-VEN.

           IF VEN-VAL-SFIN IN VEN NOT > ZEROES
               DISPLAY VEN-KEY-IVEN IN VEN ' VCTO RECHAZADO CANCELADO'
               GO TO NXT-INP-VEN.

           IF VEN-IND-SCTB IN VEN = 'M  '
               DISPLAY VEN-KEY-IVEN IN VEN ' VCTO RECHAZADO SITUACION'
                                        ' CONTABLE DE EXCEPCION M'
               GO TO NXT-INP-VEN.

      *     IF VEN-NUM-IVEN IN VEN < OPC-NUM-IVEN IN OPC
      *         DISPLAY 'VENCIMIENTO RECHAZADO: '
      *                 'VEN-NUM-IVEN < OPC-NUM-IVEN ' VEN-KEY-IVEN
      *         DISPLAY ' '
      *         GO TO NXT-INP-VEN.

           IF OPC-MSC-STAT NOT = 'A'
               DISPLAY 'VENCIMIENTO RECHAZADO: '
                       'CREDITO NO ACTIVADO         ' VEN-KEY-IVEN
               DISPLAY ' '
               GO TO NXT-INP-VEN.

           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFIC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                    DISPLAY 'VENCIMIENTO RECHAZADO: '
                            'OFICINA OPC NO CORRESPONDE  ' VEN-KEY-IVEN
                   GO TO NXT-INP-VEN.

      * SI CODIGO DE SITUACION CONTABLE DE VENCIMIENTO = "VENCIDA" O
      *    "CASTIGADO" ==> NO CONSIDERA VENCIMIENTO

           MOVE 'COL'  TO MSC-COD-SIST.
           MOVE 'SCTB' TO MSC-CIC-TMSC IN MSC.
           MOVE VEN-IND-SCTB IN VEN TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS NOT = SPACES
               DISPLAY 'VENCIMIENTO RECHAZADO: '
                       'SITUACION CONTABLE INEXISTENTE '
                       'O NO VIGENTE ' VEN-KEY-IVEN
               DISPLAY ' '
               GO TO NXT-INP-VEN.

           IF MSC-COD-CMSC IN MSC = WSS-COD-SVCD OR
              MSC-COD-CMSC IN MSC = WSS-COD-SCAS
               DISPLAY 'VENCIMIENTO RECHAZADO: '
                       'CASTIGADO O VENCIDO ' MSC-CIC-CMSC ' '
                       VEN-KEY-IVEN
               DISPLAY ' '
               GO TO NXT-INP-VEN.

           IF VEN-IND-SCTB IN VEN NOT = WSS-COD-SMOR
               DISPLAY VEN-KEY-IVEN ' VENCIMIENTO RECHAZADO: '
                       'NO MOROSO ==> ' VEN-IND-SCTB
               GO TO NXT-INP-VEN.

      * OBTIENE FECHA CANDIDATA EN QUE EL VENCIMIENTO SE PASARA
      * A CARTERA VENCIDA, PARA GRABAR EN CCT FECHA CAMBIO SITUACION CON

           MOVE VEN-NUM-DVEN IN VEN TO FEC-ITM1.
           MOVE VEN-NUM-MVEN IN VEN TO FEC-ITM2.
           MOVE VEN-NUM-SVEN IN VEN TO FEC-ITM3.
           MOVE VEN-NUM-AVEN IN VEN TO FEC-ITM4.
           MOVE FEC-FORM-FEC        TO FEC-FORM.
           MOVE FEC-SUM-DIA         TO FEC-CMND.
           MOVE WSS-NUM-TDCV        TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.

      * SI LA FECHA EN QUE EL VENCIMIENTO SE PASARA A CARTERA VENCIDA
      * ES UNA FECHA NO HABIL, SE BUSCA EL PRIMER DIA HABIL ANTERIOR

      * BUS-PRIMER-DIA-HABIL-BACK.
       BUS-PDH-BACK.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-HBL
               MOVE FEC-FORM-FEC        TO FEC-FORM
               MOVE FEC-RST-DIA         TO FEC-CMND
               MOVE 1                   TO FEC-NDIA
      *        PERFORM PRO-FEC
               PERFORM CAL-FEC
               GO TO BUS-PDH-BACK.

      * CARGA FECHA DE CAMBIO DE SITUACION CONTABLE ( CARTERA VENCIDA )
           MOVE FEC-ITM1 TO WSS-NUM-DCCT.
           MOVE FEC-ITM2 TO WSS-NUM-MCCT.
           MOVE FEC-ITM3 TO WSS-NUM-SCCT.
           MOVE FEC-ITM4 TO WSS-NUM-ACCT.

      * ASUME QUE SE PASARA A CARTERA VENCIDA EL VALOR FINAL
      * DEL VENCIMIENTO
           MOVE VEN-VAL-SFIN IN VEN TO WSS-VAL-SRDT.

      * ASUME QUE NO ACELERARA
           MOVE 'N' TO WSS-IND-ACEL.

           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB.
           MOVE '0999  '       TO TAB-CIC-CTAB.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               IF TAB-COD-CTAB NOT = OPC-COD-VCOC IN OPC
                   MOVE OPC-FEC-FING IN OPC TO CAM-FEC-FCAM IN CAM(1)
                   MOVE OPC-FEC-FING IN OPC TO CAM-FEC-FCAM IN CAM(2)
                   MOVE OPC-COD-VCOC IN OPC TO CAM-COD-VCAM IN CAM(1)
                   MOVE TAB-COD-CTAB IN TAB TO CAM-COD-VCAM IN CAM(2)
                   MOVE FIO-GET-KEY  TO FIO-CMND
                   PERFORM TAB-FIO-CAM
                   IF NOT FIO-STAT-OKS
                       MOVE 'COL' TO MSG-COD-SIST
                       MOVE 'CODVCAMNEX' TO MSG-COD-MENS
                       PERFORM GET-MSG
                       DISPLAY MSG-GLS-DESC
                   ELSE
                       IF CAM-IND-VIGE NOT = 'S'
                           MOVE 'COL' TO MSG-COD-SIST
                           MOVE 'VALCAMNVG' TO MSG-COD-MENS
                           PERFORM GET-MSG
                           DISPLAY MSG-GLS-DESC
                       ELSE
                           MULTIPLY CAM-SGV-VCAM IN CAM
                           BY       VEN-VAL-SCAP IN VEN
                           GIVING   WSS-VAL-SCAP IN WSS-VARI
               ELSE
                   MOVE VEN-VAL-SCAP IN VEN  TO
                        WSS-VAL-SCAP IN WSS-VARI
           ELSE
               MOVE 'COL' TO MSG-COD-SIST
               MOVE 'CODTVLRNEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY 'OPERACION : ' OPC-CIC-IOPC IN OPC
               DISPLAY MSG-GLS-DESC IN MSG
               DISPLAY ' '.

           MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR : '
                   DISPLAY 'VENCIMIENTO RECHAZADO: '
                           'SIN DOCUMENTO '
                           'ASOCIADO : ' VEN-KEY-IVEN
                   DISPLAY ' '
                   GO TO NXT-INP-VEN.

           PERFORM PRO-RDT.
           IF FIO-STAT-OKS
                DISPLAY RDT-KEY-IRDT ' REGISTRO RDT PROCESADO'
                ADD 1 TO WSS-NUM-TRDT
           ELSE
                DISPLAY 'ERROR : '
                DISPLAY 'ACTUALIZANDO REGISTRO RDT '
                       'ASOCIADO : ' FIO-STAT ' '
                       VEN-CIC-IOPC IN VEN.

      *
           MOVE WSS-DLC-IND-SCTB TO DLC-IND-SCTB IN DLC.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.

           PERFORM PRO-CCT.
           IF FIO-STAT-OKS
                DISPLAY 'REGISTRO CCT PROCESADO '
                        CCT-KEY-ICCT
                DISPLAY ' '
                ADD 1 TO WSS-NUM-TCCT
           ELSE
                DISPLAY 'ERROR : '
                DISPLAY 'ACTUALIZANDO REGISTRO CCT '
                        'ASOCIADO : ' FIO-STAT ' '
                        VEN-CIC-IOPC IN VEN.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE WSS-COD-SVCD TO VEN-IND-SCTB IN VEN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-NUM-PVEN
               DISPLAY VEN-KEY-IVEN ' REGISTRO VEN PROCESADO '
           ELSE
               DISPLAY 'ABORTO : ERROR EN MODIFICACION REGISTRO VEN'
               DISPLAY ' ' VEN-KEY-IVEN
               PERFORM GNS-PRO-END.

       NXT-INP-VEN.
      * SETEA LLAVE PARA GET-NXT
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           GO TO LUP-INP-VEN.
       FIN-INP-VEN.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       FIN-PRO-VEN.
           EXIT.

       PRO-ACEL SECTION.
       INI-PRO-ACEL.
           MOVE WSS-FEC-FMIN TO WSS-FEC-FIRA.

           MOVE WSS-FEC-FIRA TO FEC-FECH.
       PXM-HBL.
           MOVE FEC-FECH TO WSS-FEC-FTRA.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-SUM-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
      *VAL-FEC Validar Fecha
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-HBL
                GO TO PXM-HBL.

      * OBTENER FECHA INICIO RANGO DE SELECCION
      *    WSS-FEC-FINI = WSS-FEC-FIRA - WSS-NUM-TACV
           MOVE WSS-FEC-FIRA TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA TO FEC-CMND.
           MOVE WSS-NUM-TACV TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-FECH TO WSS-FEC-FINI.
           DISPLAY ' RANGO DE SELECCION : CCB'
                   ' FECHA INICIAL          = ' WSS-FEC-FINI.
           DISPLAY ' '.

      * OBTENER FECHA TERMINO RANGO DE SELECCION
      *    WSS-FEC-FTER = WSS-FEC-FTRA - WSS-NUM-TACV
           MOVE WSS-FEC-FTRA TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA TO FEC-CMND.
           MOVE WSS-NUM-TACV TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-FECH TO WSS-FEC-FTER.
           DISPLAY ' RANGO DE SELECCION : CCB'
                   ' FECHA FINAL = ' WSS-FEC-FTER.
           DISPLAY ' '.

      *    OBTIENE PRIMER REGISTRO CCB SEGUN RANGO

           MOVE WSS-NUM-DINI TO CCB-NUM-DCCB IN CCB.
           MOVE WSS-NUM-MINI TO CCB-NUM-MCCB IN CCB.
           MOVE WSS-NUM-SINI TO CCB-NUM-SCCB IN CCB.
           MOVE WSS-NUM-AINI TO CCB-NUM-ACCB IN CCB.
           MOVE FIO-GET-NLS TO FIO-CMND.
       CIC-PRO-ACEL.
           MOVE 'CCB-FEC-FCCB' TO FIO-AKEY.
           PERFORM COL-FIO-CCB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'FIN CCB'
               GO TO FIN-PRO-ACEL.

      *    VERIFICA QUE FECHA DE CAMBIO SITUACION COBRANZA NO
      *    SOBREPASE LIMITE MAXIMO
           DISPLAY CCB-KEY-ICCB IN CCB ' CCB CANDIDATO'.
           MOVE WSS-FEC-FTER        TO FEC-FEC1.
           MOVE CCB-NUM-DCCB IN CCB TO FEC-DEC2.
           MOVE CCB-NUM-MCCB IN CCB TO FEC-MEC2.
           MOVE CCB-NUM-SCCB IN CCB TO FEC-SEC2.
           MOVE CCB-NUM-ACCB IN CCB TO FEC-AEC2.
           MOVE FEC-FORM-FEC        TO FEC-FORM.
           MOVE FEC-CMP-FEC         TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-LES
               DISPLAY ' NO EXISTEN MAS REGISTROS CCB'
               GO TO FIN-PRO-ACEL.
           IF CCB-IND-SCBZ NOT = WSS-COD-SIDJ
               DISPLAY CCB-KEY-ICCB IN CCB ' ' CCB-IND-SCBZ IN CCB
                       ' RECHAZADO POR SIT. COBRANZA NO VALIDA'
               GO TO CIC-PRO-ACEL.
           PERFORM DLC-ACEL.
           GO TO CIC-PRO-ACEL.
       FIN-PRO-ACEL.
           EXIT.

       DLC-ACEL SECTION.
       INI-DLC-ACEL.
           PERFORM VEN-ACEL.
           MOVE CCB-CIC-IOPC TO DLC-CIC-IOPC.
           MOVE ZEROES TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
       CIC-DLC-ACEL.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               GO TO FIN-DLC-ACEL.

           IF DLC-CIC-IOPC IN DLC NOT = CCB-CIC-IOPC IN CCB
                  MOVE FIO-GET-NXT TO FIO-CMND
                  GO TO CIC-DLC-ACEL.

           IF DLC-VAL-SDLC IN DLC NOT > ZEROES
                  MOVE FIO-GET-NXT TO FIO-CMND
                  GO TO CIC-DLC-ACEL.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           MOVE WSS-COD-SVCD TO DLC-IND-SCTB.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO CIC-DLC-ACEL.

       FIN-DLC-ACEL.
           EXIT.

       VEN-ACEL SECTION.
       INI-VEN-ACEL.
           MOVE CCB-CIC-IOPC TO VEN-CIC-IOPC IN VEN.
           MOVE ZEROES TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NLS TO FIO-CMND.
       CIC-VEN-ACEL.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-VEN-ACEL.

           IF VEN-CIC-IOPC IN VEN NOT = CCB-CIC-IOPC IN CCB
                  MOVE FIO-GET-NXT TO FIO-CMND
                  GO TO CIC-VEN-ACEL.

           IF VEN-VAL-SFIN IN VEN NOT > ZEROES
                  MOVE FIO-GET-NXT TO FIO-CMND
                  GO TO CIC-VEN-ACEL.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE WSS-COD-SVCD TO VEN-IND-SCTB.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO CIC-VEN-ACEL.

       FIN-VEN-ACEL.
           EXIT.

       COPY COLBFVEN.
       COPY COLBFEVC.
       COPY COLBFDLC.
       COPY COLBFCCT.
       COPY COLBFRDT.
       COPY COLBFCCB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.

      *<<<< EOF

       GET-CUR-OPC SECTION.
       INI-GET-CUR-OPC.
           MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       FIN-GET-CUR-OPC.
           EXIT.

       PRO-CCT SECTION.
       INI-PRO-CCT.
           MOVE DLC-KEY-IDLC IN DLC TO CCT-KEY-IDLC IN CCT.
           MOVE 99                  TO CCT-NUM-ICCT IN CCT.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-CCT.
           IF FIO-STAT-OKS AND
              DLC-KEY-IDLC IN DLC = CCT-KEY-IDLC IN CCT
               ADD 1 TO CCT-NUM-ICCT IN CCT
           ELSE
              MOVE DLC-KEY-IDLC IN DLC TO CCT-KEY-IDLC IN CCT
              MOVE 1                   TO CCT-NUM-ICCT IN CCT.
           MOVE SPACES TO CCT-GLS-FLAG IN CCT.
      * AGREGAR RUTINA HEADER REGISTROS, POR MIENTRAS BLANCOS
           MOVE SPACES TO CCT-STP-ITRN IN CCT.
           MOVE SPACES TO CCT-MSC-TACC IN CCT.
           MOVE SPACES TO CCT-MSC-STAT IN CCT.
           MOVE SPACES TO CCT-COD-OTRN IN CCT.
           MOVE SPACES TO CCT-COD-ATRN IN CCT.
           MOVE SPACES TO CCT-COD-RCCT IN CCT.
           MOVE WSS-NUM-DCCT TO CCT-NUM-DCCT IN CCT.
           MOVE WSS-NUM-MCCT TO CCT-NUM-MCCT IN CCT.
           MOVE WSS-NUM-SCCT TO CCT-NUM-SCCT IN CCT.
           MOVE WSS-NUM-ACCT TO CCT-NUM-ACCT IN CCT.
           MOVE SPACES TO CCT-FEC-FINP IN CCT.
           MOVE WSS-COD-SVCD TO CCT-IND-SCTB IN CCT.
           MOVE ZEROES TO CCT-SGV-GAST IN CCT.
           MOVE SPACES TO CCT-IND-SCTA IN CCT.
           MOVE VEN-KEY-IVEN IN VEN TO CCT-KEY-IVCT IN CCT.
           MOVE WSS-IND-ACEL TO CCT-IND-ACEL.
           MOVE SPACES TO CCT-GLS-DISP IN CCT.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CCT.
       FIN-PRO-CCT.
           EXIT.

       PRO-RDT SECTION.
       INI-PRO-RDT.
      * INCREMENTA RDT CON SCTB = VCD
           MOVE DLC-CIC-IOPC IN DLC TO RDT-CIC-IOPC IN RDT.
           MOVE DLC-NUM-IDLC IN DLC TO RDT-NUM-IDLC IN RDT.
           MOVE WSS-COD-SVCD        TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF NOT FIO-STAT-OKS
               MOVE FIO-PUT TO FIO-CMND
               MOVE SPACES TO RDT-GLS-FLAG IN RDT
      * AGREGAR RUTINA HEADER REGISTROS, POR MIENTRAS BLANCOS
               MOVE SPACES TO RDT-STP-ITRN IN RDT
               MOVE SPACES TO RDT-MSC-TACC IN RDT
               MOVE SPACES TO RDT-MSC-STAT IN RDT
               MOVE SPACES TO RDT-COD-OTRN IN RDT
               MOVE SPACES TO RDT-COD-ATRN IN RDT
               MOVE WSS-VAL-SRDT TO RDT-VAL-SRDT IN RDT
               MOVE SPACES TO RDT-GLS-DISP IN RDT
           ELSE
               ADD  VEN-VAL-SFIN IN VEN TO RDT-VAL-SRDT IN RDT
               MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-RDT.

      * DECREMENTA RDT CON SCTB = MOR
           MOVE DLC-CIC-IOPC IN DLC TO RDT-CIC-IOPC IN RDT.
           MOVE DLC-NUM-IDLC IN DLC TO RDT-NUM-IDLC IN RDT.
           MOVE WSS-COD-SMOR        TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF NOT FIO-STAT-OKS
               DISPLAY RDT-KEY-IRDT ' ERROR NO HAY RDT PARA '
                                    'SCTB MOROSA'
           ELSE
               SUBTRACT VEN-VAL-SFIN IN VEN FROM RDT-VAL-SRDT IN RDT
               IF RDT-VAL-SRDT IN RDT NOT > ZEROES
                  MOVE FIO-DEL TO FIO-CMND
                  PERFORM COL-FIO-RDT
               ELSE
                  MOVE FIO-MOD TO FIO-CMND
                  PERFORM COL-FIO-RDT.

           MOVE SPACES TO WSS-DLC-IND-SCTB.
           MOVE DLC-CIC-IOPC IN DLC TO RDT-CIC-IOPC IN RDT.
           MOVE DLC-NUM-IDLC IN DLC TO RDT-NUM-IDLC IN RDT.
           MOVE SPACES              TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-NLS TO FIO-CMND.
       CIC-PRO-RDT.
           PERFORM COL-FIO-RDT.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-RDT.
           IF DLC-CIC-IOPC IN DLC NOT = RDT-CIC-IOPC IN RDT OR
              DLC-NUM-IDLC IN DLC NOT = RDT-NUM-IDLC IN RDT
               GO TO FIN-PRO-RDT.
           IF WSS-DLC-IND-SCTB = SPACES
               MOVE RDT-IND-SCTB IN RDT TO WSS-DLC-IND-SCTB
           ELSE
           IF WSS-DLC-IND-SCTB NOT = RDT-IND-SCTB IN RDT
               MOVE 'MUL'               TO WSS-DLC-IND-SCTB
               GO TO FIN-PRO-RDT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO CIC-PRO-RDT.
       FIN-PRO-RDT.
           EXIT.

       FIN SECTION.
       INI-FIN.
           DISPLAY '      T O T A L E S   D E  C O N T R O L'.
           DISPLAY '      = = = = = = =   = =  = = = = = = ='.
           DISPLAY ' '
           DISPLAY ' REGISTROS VEN SELECCIONADOS = ' WSS-NUM-SVEN.
           DISPLAY ' '
           DISPLAY ' REGISTROS VEN PROCESADOS    = ' WSS-NUM-PVEN.
           DISPLAY ' '
           DISPLAY ' REGISTROS CCT PROCESADOS    = ' WSS-NUM-TCCT.
           DISPLAY ' '
           DISPLAY ' REGISTROS RDT PROCESADOS    = ' WSS-NUM-TRDT.
       FIN-FIN.
           EXIT.

       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBFTAB.
       COPY SGCBFDBC.
       COPY COLBFRDC.
       COPY COLBFOPC.
       COPY TABBFOFI.
       COPY TABBFVLR.
       COPY TABBFCAM.
       COPY TABBBUSR.
       COPY TABBFUSR.

       COPY SGCBGCLI.
       COPY GNSBGCPT.
       COPY GNSBBTAB.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY GNSBVTAB.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBBMSC.
       COPY GNSBFMSC.
      *
      *>>>>
