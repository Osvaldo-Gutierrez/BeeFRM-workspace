       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGADC.
       AUTHOR.          BEE (DPM).
       DATE-WRITTEN.    21-JUL-1997.

      * Programa Generador de Archivo ADC con deudas completas.

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DSQ-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRADC.
       COPY COLBRDSQ.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGGRC.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-REC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ESC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-FIA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AMF-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.

       COPY COLBROPC.
       COPY COLBRRDC.
       COPY COLBRDLC.
       COPY COLBRRDT.
       COPY SGCBRDBC.
       COPY COLBRRDE.
       COPY COLWGDEU.

       COPY GNSBRMSC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
       COPY TABBRCAM.
       COPY SGCBRREC.
       COPY DEUBRESC.
       COPY DEUBRFIA.
       COPY DEUBRAMF.
       COPY GNSWGCPT.
       COPY GNSWGSYS.

       01  WSS-VARI.
      * Variables para estadisticas
           03  WSS-NUM-RLEI       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RGRA       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RRCH       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-REXI       VALUE ZEROES      PIC 9(06).

           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO               PIC 9(02).
               05  WSS-NUM-MPRO               PIC 9(02).
               05  WSS-NUM-SPRO               PIC 9(02).
               05  WSS-NUM-APRO               PIC 9(02).
           03  WSS-VAL-DEUT    VALUE ZEROES   PIC 9(14).
      *     03  WSS-VAL-MPAR    VALUE ZEROES   PIC 9(14).

           03  WSS-VAL-DEDI    VALUE ZEROES   PIC 9(14).
           03  WSS-VAL-DEIN    VALUE ZEROES   PIC 9(14).
           03  WSS-VAL-DECO    VALUE ZEROES   PIC 9(14).
           03  WSS-VAL-DEFI    VALUE ZEROES   PIC 9(14).

           03  WSS-VAL-TPES    VALUE ZEROES         PIC 9(13)V9(02).
           03  WSS-NUM-ICLI                         PIC ZZZZZZZ9.
           03  WSS-IND-FLAG    VALUE ZEROES   PIC 9(01).
           03  WSS-IND-DISP    VALUE SPACES         PIC X(01).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGADC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM OBT-PARAM.
           PERFORM PROCESA-DSQ.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-DSQ.
           PERFORM SGC-FIO-DBC.

           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-ADC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA COLADC...' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-OPEN-FILE.
           EXIT.


      ******************************************************************
       OBT-PARAM SECTION.
      *-----------------
       INI-OBT-PARAM.
           DISPLAY 'ACEPTA PARAMETROS.'
           DISPLAY '------------------'
           DISPLAY ' '.
      *     ACCEPT WSS-VAL-MPAR IN WSS-VARI.
      *     DISPLAY 'MONTO PARAMETRO: ' WSS-VAL-MPAR IN WSS-VARI.

           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO WSS-NUM-DPRO IN WSS-VARI
                            ADC-NUM-DPRO IN ADC.
           MOVE HOY-MHOY TO WSS-NUM-MPRO IN WSS-VARI
                            ADC-NUM-MPRO IN ADC.
           MOVE HOY-SHOY TO WSS-NUM-SPRO IN WSS-VARI
                            ADC-NUM-SPRO IN ADC.
           MOVE HOY-AHOY TO WSS-NUM-APRO IN WSS-VARI
                            ADC-NUM-APRO IN ADC.
           DISPLAY 'FECHA DE PROCESO: ' WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY ' '.
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY 'INDICADOR DE DISPLAY (S/N): '
                                 WSS-IND-DISP IN WSS-VARI.
           DISPLAY ' '.
           DISPLAY 'VALIDACION DE PARAMETROS.'.
           DISPLAY '-------------------------'.
           DISPLAY ' '.

      *     DISPLAY 'VALIDA MONTO DE PARAMETRO:'.
      *     IF WSS-VAL-MPAR IN WSS-VARI NOT NUMERIC
      *         DISPLAY 'ERROR PARAMETRO NUMERICO: '
      *                        WSS-VAL-MPAR IN WSS-VARI
      *         PERFORM GNS-PRO-ABT.

           DISPLAY ' '.
           DISPLAY 'VALIDA INDICADOR DE DISPLAY'.
           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
                                       NOT = 'N'
               DISPLAY 'ASUME IND. DE DISPLAY = N'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI.
       FIN-OBT-PARAM.
           EXIT.


      ******************************************************************
       PROCESA-DSQ SECTION.
      *-------------------
       INI-PROCESA-DSQ.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-DSQ.

       LUP-PROCESA-DSQ.
           MOVE ZEROES TO WSS-IND-FLAG IN WSS-VARI.

           IF NOT FIO-STAT-OKS
              GO TO FIN-PROCESA-DSQ.

           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY ' '
               DISPLAY 'REG. DSQ LEIDO: ' DSQ.

           PERFORM CAL-DEUDA-TOTAL.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
               GO TO NXT-PROCESA-DSQ.

      * Como las deudas en el archivo D01 y en el parametro son en
      * miles de pesos, se debe calcular la deuda tambien en miles
      * de pesos, pero en el reporte debe ir en pesos
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'DEUDA TOTAL: ' WSS-VAL-DEUT IN WSS-VARI.

      *     DIVIDE WSS-VAL-DEUT IN WSS-VARI BY 1000
      *                GIVING WSS-VAL-DEUT IN WSS-VARI.

      *     IF WSS-VAL-DEUT IN WSS-VARI < WSS-VAL-MPAR IN WSS-VARI
      *         ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
      *         GO TO NXT-PROCESA-DSQ.

           IF DSQ-CIC-DEUD IN DSQ > SPACES
               MOVE DSQ-CIC-DEUD IN DSQ TO DBC-CIC-ICLI IN DBC
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM SGC-FIO-DBC
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR LECTURA SGCDBC: '
                                           DSQ-CIC-DEUD IN DSQ
                   ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
                   GO TO NXT-PROCESA-DSQ.

           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.

           MOVE DSQ-CIC-DEUD IN DSQ      TO ADC-CIC-DEUD IN ADC.
           MOVE DBC-GLS-NOMC IN DBC      TO ADC-GLS-NOMC IN ADC.
           MOVE DBC-NUM-ICLI IN DBC      TO WSS-NUM-ICLI IN WSS-VARI.
           MOVE WSS-NUM-ICLI IN WSS-VARI TO ADC-NUM-ICLI IN ADC.
           MOVE DBC-VRF-ICLI IN DBC      TO ADC-VRF-ICLI IN ADC.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'DEUDA DIRECTA       : '
                                    WSS-VAL-DEDI IN WSS-VARI
               DISPLAY 'DEUDA INDIRECTA     : '
                                    WSS-VAL-DEIN IN WSS-VARI
               DISPLAY 'DEUDA COMPLEMENTARIA: '
                                    WSS-VAL-DECO IN WSS-VARI
               DISPLAY 'DEUDA SOLIDARIA     : '
                                    WSS-VAL-DEFI IN WSS-VARI
               DISPLAY 'REG. DSQ GRABADO.'.

           MOVE WSS-VAL-DEDI IN WSS-VARI TO ADC-VAL-DEDI IN ADC.
           MOVE WSS-VAL-DEIN IN WSS-VARI TO ADC-VAL-DEIN IN ADC.
           MOVE WSS-VAL-DECO IN WSS-VARI TO ADC-VAL-DECO IN ADC.
           MOVE WSS-VAL-DEFI IN WSS-VARI TO ADC-VAL-DEFI IN ADC.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-ADC.
           ADD 1 TO WSS-NUM-RGRA IN WSS-VARI.

       NXT-PROCESA-DSQ.
           MOVE ZEROES TO WSS-VAL-DEDI IN WSS-VARI
                          WSS-VAL-DEIN IN WSS-VARI
                          WSS-VAL-DECO IN WSS-VARI
                          WSS-VAL-DEFI IN WSS-VARI
                          WSS-VAL-TPES IN WSS-VARI
                          WSS-VAL-DEUT IN WSS-VARI.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DSQ.
           GO TO LUP-PROCESA-DSQ.
       FIN-PROCESA-DSQ.
           EXIT.


      ******************************************************************
       CAL-DEUDA-TOTAL SECTION.
      *------------------------
       INI-CAL-DEUDA-TOTAL.
      * Llama a rutina de choque de deudas
           MOVE HOY-FHOY TO DEU-FEC-FPRO.
           PERFORM INI-CAM.
           MOVE 'R' TO DEU-FPRO IN DEU.
           MOVE DSQ-CIC-DEUD IN DSQ TO DEU-CIC-ICLI IN DEU.
           PERFORM CAL-DEU.
           IF DEU-STAT NOT = DEU-STAT-OKS
               IF WSS-IND-DISP IN WSS-VARI = 'S'
                   DISPLAY 'ERROR RUTINA CHOQUE DEUDA: '
                                             DEU-STAT ' ' DEU-MEN2
                   MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
                   GO TO FIN-CAL-DEUDA-TOTAL
               ELSE
                   MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
                   GO TO FIN-CAL-DEUDA-TOTAL.

      * Obtiene valores de rutina de choque mediante registro RDE
           MOVE 0 TO DEU-R.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DEU-FIO-RDE.
           IF NOT ( FIO-STAT-OKS AND
              RDE-CIC-ICLI IN RDE = DSQ-CIC-DEUD IN DSQ )
               MOVE ZEROES TO WSS-VAL-DEUT IN WSS-VARI
               GO TO FIN-CAL-DEUDA-TOTAL.

       LUP-CAL-DEUDA-TOTAL.
           IF RDE-COD-VCCR IN RDE NOT > SPACES
               MOVE RDE-VAL-DEUD IN RDE TO WSS-VAL-TPES IN WSS-VARI
           ELSE
           IF RDE-COD-VCCR IN RDE = CAM-COD-MNAC
               MOVE RDE-VAL-DEUD IN RDE TO WSS-VAL-TPES IN WSS-VARI
           ELSE
               MOVE RDE-COD-VCCR IN RDE TO CAM-COD-VCAM IN CAM(1)
               PERFORM GET-CAM
               IF NOT FIO-STAT-OKS
                   MOVE ZEROES TO WSS-VAL-TPES IN WSS-VARI
               ELSE
                   IF CAM-IND-VIGE IN CAM = 'N'
                       MOVE ZEROES TO WSS-VAL-TPES IN WSS-VARI
                   ELSE
                       MULTIPLY RDE-VAL-DEUD IN RDE BY
                                CAM-SGV-VCAM IN CAM
                       GIVING   WSS-VAL-TPES IN WSS-VARI.

           IF RDE-IND-TIPD IN RDE = 'DIR'
               ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DEDI IN WSS-VARI
           ELSE
           IF RDE-IND-TIPD IN RDE = 'IND'
               ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DEIN IN WSS-VARI
           ELSE
           IF RDE-IND-TIPD IN RDE = 'COM'
               ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DECO IN WSS-VARI
           ELSE
           IF RDE-IND-TIPD IN RDE = 'FIA'
              ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DEFI IN WSS-VARI.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DEU-FIO-RDE.
           IF FIO-STAT-OKS AND
              RDE-CIC-ICLI IN RDE = DSQ-CIC-DEUD IN DSQ
               GO TO LUP-CAL-DEUDA-TOTAL.

      *        Calcula Deuda Total = D. Dir + D. Ind + (D. Com + D. Fia)
      * La deuda en el archivo D01 esta en miles de pesos, igual
      * que el parametro, por lo que la deuda total debe quedar
      * en miles de pesos
           COMPUTE WSS-VAL-DEUT IN WSS-VARI ROUNDED =
                              ( WSS-VAL-DEDI IN WSS-VARI +
                                WSS-VAL-DEIN IN WSS-VARI +
                                WSS-VAL-DECO IN WSS-VARI +
                                WSS-VAL-DEFI IN WSS-VARI ).
       FIN-CAL-DEUDA-TOTAL.
           EXIT.


      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-RDC.
           PERFORM COL-FIO-DSQ.
           PERFORM COL-FIO-ADC.
       FIN-CLOSE-FILE.
           EXIT.


      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS DEL PROCESO.'.
           DISPLAY '-------------------------'.
           DISPLAY ' '.
           DISPLAY 'REGISTROS OPC LEIDOS        : '
                                  WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'REGISTROS GRABADOS DSQ      : '
                                  WSS-NUM-RGRA IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS        : '
                                  WSS-NUM-RRCH IN WSS-VARI.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.


      * COPY DEUBFRDE.
       DEU-FIO-RDE SECTION.
       INI-DEU-FIO-RDE.
           PERFORM GET-RDE.
       FIN-DEU-FIO-RDE.
           EXIT.

       GET-RDE SECTION.
       INI-GET-RDE.
           MOVE '00' TO FIO-STAT.
           IF FIO-CMND = FIO-GET-PRV  OR
              FIO-CMND = FIO-GET-LEQ
               SUBTRACT 1 FROM DEU-R
               IF DEU-R NOT > ZEROES
                   MOVE '10' TO FIO-STAT
                   GO TO FIN-GET-RDE
               ELSE
                   NEXT SENTENCE
           ELSE
               ADD 1 TO DEU-R
               IF DEU-R > DEU-RMAX
                   MOVE '10' TO FIO-STAT
                   GO TO FIN-GET-RDE.
           IF DEU-R > 50
               MOVE '10' TO FIO-STAT
               GO TO FIN-GET-RDE.
           MOVE DEU-CIC-ICLI IN DEU        TO RDE-CIC-ICLI IN RDE.
           MOVE RDE-IND-TIPD IN DEU(DEU-R) TO RDE-IND-TIPD IN RDE.
           MOVE RDE-IND-SCRT IN DEU(DEU-R) TO RDE-IND-SCRT IN RDE.
           MOVE RDE-IND-SCTB IN DEU(DEU-R) TO RDE-IND-SCTB IN RDE.
           MOVE RDE-IND-SCBZ IN DEU(DEU-R) TO RDE-IND-SCBZ IN RDE.
           MOVE RDE-COD-COCR IN DEU(DEU-R) TO RDE-COD-COCR IN RDE.
           MOVE RDE-COD-VCCR IN DEU(DEU-R) TO RDE-COD-VCCR IN RDE.
           MOVE RDE-COD-OFCR IN DEU(DEU-R) TO RDE-COD-OFCR IN RDE.
           MOVE RDE-VAL-DEUD IN DEU(DEU-R) TO RDE-VAL-DEUD IN RDE.
      *    MOVE RDE-VAL-DPES IN DEU(DEU-R) TO RDE-VAL-DPES IN RDE.
       FIN-GET-RDE.
           EXIT.

       COPY COLBFADC.
       COPY COLBFDSQ.
       COPY COLBFOPC.
       COPY COLBFRDC.
       COPY COLBFDLC.
       COPY COLBFRDT.

       COPY DEUBCDEU.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBPFEC.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGPES.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBGCPT.
       COPY TABBFCAM.
       COPY TABBGCAM.
       COPY SGCBFREC.
       COPY SGCBFDBC.
       COPY DEUBFESC.
       COPY DEUBFFIA.
       COPY DEUBFAMF.

