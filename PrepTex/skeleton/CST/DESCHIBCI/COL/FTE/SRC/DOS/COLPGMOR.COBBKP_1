       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGMOR.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    29-MAY-1992.
      * Programa Generador de Archivo CAN.
      * Marca como morosos los vencimientos con fecha de vencimiento
      * anterior a la fecha de proceso (VEN). Acepta como fecha de
      * proceso una fecha menor o igual a la fecha actual.
      * Genera proximo vencimiento (CAN).
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AUT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-RCE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *<<<< WSS
       COPY COLBROPC.
       COPY COLBRCYA.
       COPY COLBRAUT.
       COPY COLBRCAN.
       COPY COLBRVEN.
       COPY COLBRRCV.
       COPY COLBRRCE.
       COPY COLBRVEN REPLACING ==VEN.== BY ==VEN-INC.==.
       COPY COLBRTOC.
       COPY COLBRRDT.
       COPY COLBRDLC.
       COPY COLBREVC.
       COPY COLBRICG.
       COPY COLWGVEN.
       COPY COLWGVIN.
       COPY TABBRCAM.
       COPY TABBROFI.
      *DAD ini  21-NOV-1991 / 24-ABR-1991
       COPY COLWGCTO.
       COPY TABBRTIO.
      *DAD fin  21-NOV-1991 / 24-ABR-1991
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
       COPY GNSWGINT.
       COPY GNSWGCVI.

       01  WSS-VARI.
           03  WSS-COD-OFIC          VALUE SPACES PIC X(03).
           03  WSS-VAL-IDNI            COMP-3     PIC S9(11)V9(4).
           03  WSS-NUM-IVCT                       PIC 9(04).
           03  WSS-NUM-PVEN                       PIC 9(04).
           03  WSS-FEC-FAMT.
               05  WSS-NUM-SAMT                   PIC 9(02).
               05  WSS-NUM-AAMT                   PIC 9(02).
               05  WSS-NUM-MAMT                   PIC 9(02).
               05  WSS-NUM-DAMT                   PIC 9(02).
           03  WSS-FEC-FANT.
               05  WSS-NUM-SANT                   PIC 9(02).
               05  WSS-NUM-AANT                   PIC 9(02).
               05  WSS-NUM-MANT                   PIC 9(02).
               05  WSS-NUM-DANT                   PIC 9(02).
           03 WSS-NUM-RLEI                        PIC 9(04).
           03 WSS-NUM-RPRO                        PIC 9(04).
           03 WSS-NUM-RERR                        PIC 9(04).
           03 WSS-REG-VEN                         PIC X(137).
           03 WSS-NUM-DIAS                        PIC 9(04).
           03 WSS-FEC-FMIN.
              05 WSS-NUM-SMIN                     PIC 9(02).
              05 WSS-NUM-AMIN                     PIC 9(02).
              05 WSS-NUM-MMIN                     PIC 9(02).
              05 WSS-NUM-DMIN                     PIC 9(02).
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-STER                     PIC 9(02).
              05 WSS-NUM-ATER                     PIC 9(02).
              05 WSS-NUM-MTER                     PIC 9(02).
              05 WSS-NUM-DTER                     PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO                     PIC 9(02).
              05 WSS-NUM-MPRO                     PIC 9(02).
              05 WSS-NUM-SPRO                     PIC 9(02).
              05 WSS-NUM-APRO                     PIC 9(02).
      *DAD ini  30-OCT-1991
           03 WSS-NUM-RCOM                        PIC 9(04).
           03 WSS-NUM-RRCE                        PIC 9(04).
           03 WSS-FEC-FRCE.
              05 WSS-NUM-SRCE                     PIC 9(02).
              05 WSS-NUM-ARCE                     PIC 9(02).
              05 WSS-NUM-MRCE                     PIC 9(02).
              05 WSS-NUM-DRCE                     PIC 9(02).
      *DAD fin  30-OCT-1991
           03 WSS-COD-CMSC                        PIC X(01).
           03 WSS-SGV-PROC          COMP-3        PIC S9(11)V9(04).
           03 WSS-SGV-COLO          COMP-3        PIC S9(11)V9(04).
           03 WSS-I                               PIC 9(03).
           03 WSS-VAL-SCRE          COMP-3        PIC 9(11)V9(04).
           03 WSS-VAL-SCR1          COMP-3        PIC 9(11)V9(04).
           03 WSS-VAL-SMON          COMP-3        PIC S9(11)V9(04).
           03 WSS-IND-FICA                        PIC X(01).
           03 WSS-IND-ULTC                        PIC X(01).
           03 WSS-IND-ACC                         PIC X(01).
           03  WSS-FEC-FMAX.
               05  WSS-NUM-SMAX                   PIC 9(02).
               05  WSS-NUM-AMAX                   PIC 9(02).
               05  WSS-NUM-MMAX                   PIC 9(02).
               05  WSS-NUM-DMAX                   PIC 9(02).
           03  WSS-FEC-FVAN.
               05  WSS-NUM-DVAN                   PIC 9(02).
               05  WSS-NUM-MVAN                   PIC 9(02).
               05  WSS-NUM-SVAN                   PIC 9(02).
               05  WSS-NUM-AVAN                   PIC 9(02).
           03  WSS-FEC-FUNO.
               05  WSS-NUM-DUNO                   PIC 9(02).
               05  WSS-NUM-MUNO                   PIC 9(02).
               05  WSS-NUM-SUNO                   PIC 9(02).
               05  WSS-NUM-AUNO                   PIC 9(02).
           03  WSS-FEC-FDOS.
               05  WSS-NUM-DDOS                   PIC 9(02).
               05  WSS-NUM-MDOS                   PIC 9(02).
               05  WSS-NUM-SDOS                   PIC 9(02).
               05  WSS-NUM-ADOS                   PIC 9(02).
           03 WSS-ERR.
              05 WSS-GLS-EVEN                     PIC X(15).
              05 WSS-GLS-ERR                      PIC X(50).
           03 WSS-GLS-TRC.
              05 WSS-GLS-TVEN                     PIC X(15).
              05 WSS-GLS-TRACE                    PIC X(50).
           03 WSS-VAL-INTE          COMP-3        PIC S9(11)V9(04).
           03 WSS-SGV-CMOR          COMP-3        PIC S9(11)V9(04).
           03 WSS-SGV-SCRE-REAL     COMP-3        PIC S9(11)V9(04).
           03 WSS-VEN-SGV-IDNI      COMP-3        PIC S9(11)V9(04).
      * 
           03 WSS-NUM-BVEN                        PIC 9(03).
           03 SIGUIENTE-VEZ     VALUE 1           PIC 9(01).
           03 WSS-IND-PVEZ      VALUE 0           PIC 9(01).
              88 PRIMERA-VEZ                      VALUE 0.
           03 WSS-VEN-KEY-IVEN                    PIC X(15).
      *JSS 16-DEC-1991 (Memo.06-12-91/2.1)
      *( PARA INDICAR SI COMPLETA VALOR CAPITAL - EVC )
           03 WSS-IND-CCVC                        PIC 9(01).
      *DAD  ini 29-ABR-1991 
           03 WSS-IND-TRACE     VALUE 'N'         PIC X(01).
      *DAD  fin 29-ABR-1991 
           03 WSS-CMSG                            PIC X(12).
           03 WSS-MENS.
              05 WSS-MEN1                         PIC X(40).
              05 WSS-MEN2                         PIC X(40).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           MOVE 'TRACE MAIN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           COPY GNSBGEDB.

           MOVE 'COLPGMOR' TO FIO-PROG.
           PERFORM BUS-FSIS.
           PERFORM OPEN-FILE.
           PERFORM PRM-VEN.
           PERFORM INIC-VEN.
           PERFORM LUP-PUT-VEN.
           MOVE ZEROES TO WSS-IND-PVEZ.
           PERFORM LUP-PUT-RCE.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE 'TRACE OPEN-FILE ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-RCV.
       FIN-OPEN-FILE.
           EXIT.

       PRM-VEN SECTION.
       INI-PRM-VEN.
           MOVE 'TRACE PRM-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE ZEROES TO WSS-NUM-RLEI IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-RPRO IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-RERR IN WSS-VARI.
      *DAD ini  30-OCT-1991
           MOVE ZEROES TO WSS-NUM-RCOM IN WSS-VARI.
      *DAD fin  30-OCT-1991
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      *Captura Fecha de Proceso
           DISPLAY 'FECHA DE PROCESO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
      *Captura Oficina del credito
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.

           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
      *JSS MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI
                                            WSS-NUM-DMIN IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI
                                            WSS-NUM-MMIN IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI
                                            WSS-NUM-SMIN IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI
                                            WSS-NUM-AMIN IN WSS-VARI.
       BUS-DIA-HBL.
           MOVE WSS-NUM-DMIN TO FEC-ITM1.
           MOVE WSS-NUM-MMIN TO FEC-ITM2.
           MOVE WSS-NUM-SMIN TO FEC-ITM3.
           MOVE WSS-NUM-AMIN TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM PRO-FEC.
           MOVE FEC-FECH TO WSS-FEC-FINI.
      *VAL-FEC Validar Fecha
           MOVE WSS-FEC-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
                MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DMIN
                MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MMIN
                MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SMIN
                MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AMIN
                     IN WSS-VARI
                GO TO BUS-DIA-HBL.
           MOVE WSS-NUM-DMIN IN WSS-VARI TO WSS-NUM-DINI IN WSS-VARI.
           MOVE WSS-NUM-MMIN IN WSS-VARI TO WSS-NUM-MINI IN WSS-VARI.
           MOVE WSS-NUM-SMIN IN WSS-VARI TO WSS-NUM-SINI IN WSS-VARI.
           MOVE WSS-NUM-AMIN IN WSS-VARI TO WSS-NUM-AINI IN WSS-VARI.

           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS = 'COD    NEX'
                   DISPLAY MSG-GLS-MENS
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
                   PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
           DISPLAY 'FECHA INICIO  VEN : ' WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FECHA TERMINO VEN : ' WSS-FEC-FPRO IN WSS-VARI.
       FIN-PRM-VEN.
           EXIT.

       INIC-VEN SECTION.
       INI-INIC-VEN.
           MOVE 'TRACE INIC-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * Inicializacion de Variables
           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'EN MISCELANEO TVAL NO EXISTE CODIGO 0'
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           MOVE MSC-COD-CMSC IN MSC TO WSS-COD-CMSC IN WSS-VARI.
      *DAD ini  21-NOV-1991
      * Proceso para Cargar las tablas  TAB/TIO  y  COL/TOC  en Memoria
           PERFORM COL-CRG-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
      *DAD fin  21-NOV-1991
       FIN-INIC-VEN.
           EXIT.

       LUP-PUT-VEN SECTION.
       INI-LUP-PUT-VEN.
           DISPLAY '---------- INICIO PROCESO DE VEN ----------'.
           MOVE 'TRACE LUP-PUT-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * Programacion de Ciclo para Registro VEN
       LUP-BUS-VEN.
           MOVE SPACES TO WSS-CMSG WSS-MEN1 WSS-MEN2.
           IF PRIMERA-VEZ
               MOVE FIO-GET-NLS TO FIO-CMND
               MOVE 'VEN-FEC-FVEN' TO FIO-AKEY
               MOVE WSS-NUM-DMIN IN WSS-VARI  TO VEN-NUM-DVEN IN VEN
               MOVE WSS-NUM-MMIN IN WSS-VARI  TO VEN-NUM-MVEN IN VEN
               MOVE WSS-NUM-SMIN IN WSS-VARI  TO VEN-NUM-SVEN IN VEN
               MOVE WSS-NUM-AMIN IN WSS-VARI  TO VEN-NUM-AVEN IN VEN
               MOVE SIGUIENTE-VEZ TO WSS-IND-PVEZ
           ELSE
               MOVE FIO-GET-NXT TO FIO-CMND
               MOVE FIO-KDEF-S TO FIO-KDEF
               MOVE AUX-VEN-REQA TO ADR-VEN-REQA.

           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LUP-PUT-VEN.

      *     DISPLAY 'REGISTRO VEN LEIDO : ' VEN-KEY-IVEN IN VEN ' / '
      *             VEN-FEC-FVEN IN VEN ' / ' VEN-IND-CALC IN VEN.

           ADD 1 TO WSS-NUM-RLEI.
           MOVE ADR-VEN-REQA TO AUX-VEN-REQA.
           MOVE VEN-KEY-IVEN IN VEN TO WSS-VEN-KEY-IVEN.
           MOVE 'VCTO RESPALDADO' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-LUP-PUT-VEN.
           MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
           PERFORM BUS-REG-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPERACION INEXISTENTE' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO LUP-BUS-VEN.

           IF MSG-COD-MENS = 'OPCNOACA'
               MOVE 'OPERACION NO ACTIVADA ' TO WSS-GLS-TRACE
               PERFORM DSP-TRACE
               GO TO LUP-BUS-VEN.

           IF WSS-COD-OFIC IN WSS-VARI     > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFIC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   MOVE 'OFICINA NO VALIDA' TO WSS-GLS-TRACE
                   PERFORM DSP-TRACE
                   GO TO LUP-BUS-VEN.

           IF OPC-VAL-SCRE  IN OPC NOT > ZEROES
               MOVE 'OPERACION CANCELADA' TO WSS-GLS-TRACE
               PERFORM DSP-TRACE
               GO TO LUP-BUS-VEN.

      *DAD ini  21-NOV-1991 / 24-ABR-1991 
      * Busca con el Codigo Tipo Operacion en la Tabla cargada en Memoria el
      * 'Sistema que lo Maneja'
           MOVE OPC-COD-TOOC IN OPC TO CTO-TOOC IN CTO-VARI.
           PERFORM COL-BUS-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
                       'CON CODIGO : '     CTO-TOOC IN CTO-VARI
                       ' EN CREDITO : '    OPC-CIC-IOPC IN OPC
               GO TO LUP-BUS-VEN.

      * Valida el 'Sistema que lo Maneja' de la Operacion de Credito
           IF CTO-SIST IN CTO-VARI NOT = 'COL'
               GO TO LUP-BUS-VEN.
      *DAD fin  21-NOV-1991 / 24-ABR-1991 

      *JSS Si el vencimiento ESTA CANCELADO ( cambie la pregunta, ya que puede
      *ser un vencimiento que quedo incompleto con SFIN=0, al preguntar
      *si VEN-NUM-IVEN < OPC-NUM-IVEN me aseguro que el vencimiento se cancelo)
      *     IF VEN-VAL-SFIN IN VEN = ZEROES
            IF VEN-NUM-IVCT IN VEN < OPC-NUM-IVEN IN OPC
               PERFORM PRC-PRX-VEN
               MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'VCTO NO ENCONTRADO' TO WSS-GLS-ERR
                   PERFORM DSP-ERR
                   IF WSS-CMSG NOT > SPACES
                       GO TO LUP-BUS-VEN
                   ELSE
                       MOVE WSS-CMSG TO MSG-COD-MENS
                       GO TO DIS-MEN-ERR
               ELSE
                   MOVE 'VCTO ENCONTRADO' TO WSS-GLS-TRACE
                   PERFORM DSP-TRACE
                   IF WSS-CMSG NOT > SPACES
                       GO TO LUP-BUS-VEN
                   ELSE
                       MOVE WSS-CMSG TO MSG-COD-MENS
                       GO TO DIS-MEN-ERR.

      *Aqui solo llega cuando el vencimiento NO ESTA CANCELADO
           IF VEN-IND-CALC IN VEN = 'N'
      *         MOVE 'VENCIMIENTO NO COMPLETO ' TO WSS-GLS-ERR
      *         PERFORM DSP-ERR
      *         DISPLAY 'ERROR:VENCIMIENTO: ' VEN-KEY-IVEN IN VEN
      *                 ', DEBERIA ESTAR COMPLETO'
      *         DISPLAY '      YA QUE VENCE EL : ' VEN-FEC-FVEN IN VEN
               PERFORM CMP-VEN
               MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'VCTO NO ENCONTRADO' TO WSS-GLS-ERR
                   PERFORM DSP-ERR
                   IF WSS-CMSG NOT > SPACES
                       GO TO LUP-BUS-VEN
                   ELSE
                       MOVE WSS-CMSG TO MSG-COD-MENS
                       GO TO DIS-MEN-ERR
               ELSE
                   MOVE 'VCTO ENCONTRADO' TO WSS-GLS-TRACE
                   PERFORM DSP-TRACE
                   IF WSS-CMSG NOT > SPACES
                       NEXT SENTENCE
                   ELSE
                       MOVE WSS-CMSG TO MSG-COD-MENS
                       GO TO DIS-MEN-ERR.
      *
           IF VEN-VAL-SCAP IN VEN < OPC-VAL-SCRE IN OPC
               IF OPC-IND-VSTR IN OPC = 'S'
                   PERFORM EXP-VEN
                   IF WSS-CMSG > SPACES
                       MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN
                       MOVE FIO-GET-KEY TO FIO-CMND
                       PERFORM COL-FIO-VEN
                       MOVE WSS-CMSG TO MSG-COD-MENS
                       GO TO DIS-MEN-ERR
                   ELSE
                       NEXT SENTENCE
               ELSE
                   PERFORM CMP-VEN
                   IF WSS-CMSG > SPACES
                       MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN
                       MOVE FIO-GET-KEY TO FIO-CMND
                       PERFORM COL-FIO-VEN
                       MOVE WSS-CMSG TO MSG-COD-MENS
                       GO TO DIS-MEN-ERR.

           MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VCTO NO ENCONTRADO' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO LUP-BUS-VEN
           ELSE
               MOVE 'VCTO ENCONTRADO' TO WSS-GLS-TRACE
               PERFORM DSP-TRACE.

      * AAA Marca moroso independiente de cargo automatico en
      *     cuenta corriente 15-APR-1991 19:10:45
      *     ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.
      *     PERFORM BUS-CYA-AUT.
      *     MOVE SPACES TO MSG-COD-MENS.
      *     IF NOT ( FIO-STAT-OKS AND
      *              VEN-IND-CAUT IN VEN NOT = 'N' )

               ADD 1 TO WSS-NUM-RPRO IN WSS-VARI
               MOVE VEN TO WSS-REG-VEN IN WSS-VARI
               MOVE SPACES TO MSG-COD-MENS
               PERFORM PRO-VEN-MORO
               IF MSG-COD-MENS > SPACES
                   GO TO DIS-MEN-ERR
               ELSE
                   GO TO LUP-BUS-VEN.
           GO TO LUP-BUS-VEN.
       DIS-MEN-ERR.
           ADD 1 TO WSS-NUM-RERR IN WSS-VARI.
      *     PERFORM BCK-OUT.
           MOVE 'COL' TO MSG-COD-SIST.
           PERFORM GET-MSG.
           DISPLAY 'VCTO :' VEN-KEY-IVEN IN VEN.
           DISPLAY MSG-GLS-DESC WSS-MEN1.
           DISPLAY WSS-MEN2.
           GO TO LUP-BUS-VEN.
       FIN-LUP-PUT-VEN.
           EXIT.

       LUP-PUT-RCE SECTION.
       INI-LUP-PUT-RCE.
           DISPLAY '---------- INICIO PROCESO DE RCE ----------'.
           MOVE 'TRACE LUP-PUT-RCE ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *DAD ini  30-OCT-1991
      * Obtiene Fecha del Ultimo Siguiente Dia NO Habil
           MOVE WSS-NUM-DPRO TO FEC-ITM1.
           MOVE WSS-NUM-MPRO TO FEC-ITM2.
           MOVE WSS-NUM-SPRO TO FEC-ITM3.
           MOVE WSS-NUM-APRO TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-SIG-HBL  TO FEC-CMND.
           PERFORM PRO-FEC.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
           PERFORM PRO-FEC.
           MOVE FEC-ITM1 TO WSS-NUM-DRCE IN WSS-VARI.
           MOVE FEC-ITM2 TO WSS-NUM-MRCE IN WSS-VARI.
           MOVE FEC-ITM3 TO WSS-NUM-SRCE IN WSS-VARI.
           MOVE FEC-ITM4 TO WSS-NUM-ARCE IN WSS-VARI.
      *JSS 16-DEC-1991 11:43:22 (Memo.06-12-91/2.1)
           MOVE FEC-ITM1 TO WSS-NUM-DTER IN WSS-VARI.
           MOVE FEC-ITM2 TO WSS-NUM-MTER IN WSS-VARI.
           MOVE FEC-ITM3 TO WSS-NUM-STER IN WSS-VARI.
           MOVE FEC-ITM4 TO WSS-NUM-ATER IN WSS-VARI.
      *
           DISPLAY 'FECHA INICIO  RCE : ' WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FECHA TERMINO RCE : ' WSS-NUM-DRCE IN WSS-VARI
                                          WSS-NUM-MRCE IN WSS-VARI
                                          WSS-NUM-SRCE IN WSS-VARI
                                          WSS-NUM-ARCE IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-RRCE IN WSS-VARI.
      *DAD fin  30-OCT-1991
      * Programacion de Ciclo para Registro RCE
       LUP-BUS-RCE.
           MOVE SPACES TO WSS-CMSG WSS-MEN1 WSS-MEN2.
           IF PRIMERA-VEZ
               MOVE FIO-GET-NLS TO FIO-CMND
               MOVE 'RCE-KEY-DISP' TO FIO-AKEY
               MOVE 'FECH'                   TO RCE-CAI-FVEN IN RCE
      *DAD ini  30-OCT-1991
               MOVE WSS-NUM-DPRO IN WSS-VARI TO RCE-NUM-DVEN IN RCE
               MOVE WSS-NUM-MPRO IN WSS-VARI TO RCE-NUM-MVEN IN RCE
               MOVE WSS-NUM-SPRO IN WSS-VARI TO RCE-NUM-SVEN IN RCE
               MOVE WSS-NUM-APRO IN WSS-VARI TO RCE-NUM-AVEN IN RCE
      *DAD fin  30-OCT-1991
               MOVE SIGUIENTE-VEZ TO WSS-IND-PVEZ
           ELSE
               MOVE FIO-KDEF-S TO FIO-KDEF
               MOVE AUX-RCE-REQA TO ADR-RCE-REQA
               MOVE FIO-GET-NXT TO FIO-CMND.

      *JSS DISPLAY 'RCE : ' RCE-KEY-DISP IN RCE.

           PERFORM COL-FIO-RCE.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LUP-PUT-RCE.
           MOVE ADR-RCE-REQA TO AUX-RCE-REQA.

      *DAD ini  30-OCT-1991
           IF ( RCE-FEC-FVEN IN RCE > WSS-FEC-FRCE IN WSS-VARI ) OR
      *DAD fin  30-OCT-1991
              ( RCE-CAI-FVEN IN RCE NOT = 'FECH' )
               GO TO FIN-LUP-PUT-RCE.

           MOVE RCE-CIC-IOPC IN RCE TO OPC-CIC-IOPC IN OPC.
           PERFORM BUS-REG-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPERACION INEXISTENTE' TO WSS-GLS-ERR
      *DAD ini  02-DIC-1991   (Memo.27-11-91)
               DISPLAY RCE-CIC-IOPC IN RCE ' ' WSS-GLS-ERR
      *DAD fin  02-DIC-1991
               GO TO LUP-BUS-RCE.

           IF MSG-COD-MENS = 'OPCNOACA'
               MOVE 'OPERACION NO ACTIVADA ' TO WSS-GLS-TRACE
               PERFORM DSP-TRACE
               GO TO LUP-BUS-RCE.

           IF WSS-COD-OFIC IN WSS-VARI     > SPACES AND
              WSS-COD-OFIC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFIC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   MOVE 'OFICINA NO VALIDA' TO WSS-GLS-TRACE
                   PERFORM DSP-TRACE
                   GO TO LUP-BUS-RCE.

           IF OPC-VAL-SCRE  IN OPC NOT > ZEROES
               MOVE 'OPERACION CANCELADA' TO WSS-GLS-TRACE
               PERFORM DSP-TRACE
               GO TO LUP-BUS-RCE.

           PERFORM CMP-VEN.
           IF WSS-CMSG > SPACES
               GO TO MSG-LUP-PUT-RCE.

      *DAD ini  02-DIC-1991
      * Cuando el VEN esta calculado y completo, rutina CMP-VEN no lee el ICG
      * Leo ICG de la Operacion para obtener el Codigo Tipo tasa de interes
           IF OPC-CIC-IOPC IN OPC NOT = ICG-CIC-IOPC IN ICG
               MOVE OPC-CIC-IOPC IN OPC TO VIN-CIC-IOPC IN VIN-VARI
               MOVE ZEROES              TO VIN-NUM-IDLC IN VIN-VARI
               PERFORM BUS-VIN
               IF NOT FIO-STAT-OKS
                   MOVE 'BUSVIN ERROR'           TO WSS-CMSG
                   MOVE 'ERROR AL LEER ICG  MEDIANTE BUS-VIN Y LLAVE : '
                                                 TO WSS-MEN1
                   MOVE VIN-KEY-IDLC IN VIN-VARI TO WSS-MEN2
                   GO TO MSG-LUP-PUT-RCE.
      *DAD fin  02-DIC-1991

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-RCE.

           PERFORM OBT-FEC-FTC.

      *DAD ini  07-ENE-1992   (Memo.06-12-91 Pto.2.2)
      * Obtiene el primer DLC de la OPC para determinar si es Ultima 
           IF DLC-CIC-IOPC IN DLC NOT = OPC-CIC-IOPC IN OPC
               MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC
               MOVE  0                  TO DLC-NUM-IDLC IN DLC
               MOVE FIO-GET-NLS TO FIO-CMND
               PERFORM COL-FIO-DLC
               IF FIO-STAT-OKS AND
                  DLC-CIC-IOPC IN DLC = OPC-CIC-IOPC IN OPC
                   IF RCE-FEC-FVEN IN RCE NOT < DLC-FEC-FMAV IN DLC
                       MOVE 'RCEFVEN>FMAV' TO MSG-COD-MENS
                   ELSE
                       NEXT SENTENCE
               ELSE
                   DISPLAY 'NO EXISTE DLC : ' OPC-CIC-IOPC IN OPC
                           ' EN CICLO RCE'.
      *DAD fin  07-ENE-1992

           IF MSG-COD-MENS NOT > SPACES
               MOVE FIO-MOD     TO FIO-CMND
           ELSE
               MOVE FIO-ULK-REC TO FIO-CMND.
           PERFORM COL-FIO-RCE.

      *DAD ini  30-OCT-1991
           ADD 1 TO WSS-NUM-RRCE IN WSS-VARI.
      *DAD fin  30-OCT-1991
           GO TO LUP-BUS-RCE.
       MSG-LUP-PUT-RCE.
           MOVE 'COL'    TO MSG-COD-SIST.
           MOVE WSS-CMSG TO MSG-COD-MENS.
           PERFORM GET-MSG.
           DISPLAY 'RRCE : ' RCE-KEY-IRCE IN RCE.
           DISPLAY MSG-GLS-DESC.
           DISPLAY WSS-MEN1 WSS-MEN2.
           GO TO LUP-BUS-RCE.
       FIN-LUP-PUT-RCE.
           EXIT.

       PRC-PRX-VEN SECTION.
       INI-PRC-PRX-VEN.
      *JSS debe revisar eL VEN(N+1) ( el VEN fisico ya sea con VEN o EVC )
           MOVE 'TRACE PRC-PRX-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *Primero pregunta para saber si el VEN(N+1) esta cancelado y asi no
      *hacer nada
           ADD 1 TO VEN-NUM-IVEN IN VEN.
           ADD 1 TO VEN-NUM-IVCT IN VEN.
           IF VEN-NUM-IVCT IN VEN < OPC-NUM-IVEN IN OPC
               GO TO FIN-PRC-PRX-VEN.
      *Aqui llega si el VEN(N+1) no esta cancelado
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VENCIMIENTO INEXISTENTE ' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               MOVE 'VENSIGUNEX'        TO WSS-CMSG
               MOVE VEN-KEY-IVEN IN VEN TO WSS-MEN1
               MOVE '/PRC-PRX-VEN/'     TO WSS-MEN2
               GO TO FIN-PRC-PRX-VEN.
      *Completa VEN cuando no esta cancelado e incompleto
           IF VEN-IND-CALC IN VEN = 'N'
               PERFORM CMP-VEN.
       FIN-PRC-PRX-VEN.
           EXIT.

       CMP-VEN SECTION.
       INI-CMP-VEN.
           MOVE 'TRACE CMP-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * OBTIENE VEN A COMPLETAR
           PERFORM GET-VEN-INC.
           IF WSS-NUM-PVEN = ZEROES
               MOVE 'GET-VEN-INC NO OK ' TO WSS-GLS-TRACE
               PERFORM DSP-TRACE
               GO TO FIN-CMP-VEN.
      * SI VEN COMPLETO, NADA HACE
           IF VEN-IND-CALC IN VEN-INC NOT = 'N'
               GO TO FIN-CMP-VEN.

           DISPLAY 'VENCIMIENTO A COMPLETAR : ' VEN-KEY-IVEN IN VEN-INC.

      * CARGA TODOS LOS PARAMETROS NECESARIOS PARA LA RUTINA DE INTERESES
           MOVE VEN-NUM-DVEN IN VEN-INC TO WSS-NUM-DDOS.
           MOVE VEN-NUM-MVEN IN VEN-INC TO WSS-NUM-MDOS.
           MOVE VEN-NUM-SVEN IN VEN-INC TO WSS-NUM-SDOS.
           MOVE VEN-NUM-AVEN IN VEN-INC TO WSS-NUM-ADOS.
           PERFORM OBT-WSS-FEC-FUNO.
           IF WSS-CMSG > SPACES
               GO TO FIN-CMP-VEN.
           PERFORM OBT-NUM-DIAS.
           IF WSS-CMSG > SPACES
               GO TO FIN-CMP-VEN.
           MOVE WSS-FEC-FUNO        TO INT-FUNO.
           MOVE WSS-FEC-FDOS        TO INT-FDOS.
           MOVE OPC-COD-VCOC IN OPC TO INT-VCAM.
           MOVE OPC-NUM-DCOL IN OPC TO INT-DCOL.
           MOVE OPC-NUM-MCOL IN OPC TO INT-MCOL.
           MOVE OPC-NUM-SCOL IN OPC TO INT-SCOL.
           MOVE OPC-NUM-ACOL IN OPC TO INT-ACOL.
           MOVE OPC-NUM-DCOL IN OPC TO INT-DBTV.
           MOVE OPC-NUM-MCOL IN OPC TO INT-MBTV.
           MOVE OPC-NUM-SCOL IN OPC TO INT-SBTV.
           MOVE OPC-NUM-ACOL IN OPC TO INT-ABTV.
      * OBTIENE LA TASA DE INTERES DEL DOCUMENTO O DE LA OPERACION
      * SEGUN CORRESPONDA
           MOVE OPC-CIC-IOPC IN OPC TO VIN-CIC-IOPC.
           MOVE VEN-NUM-IDLC IN VEN TO VIN-NUM-IDLC.
           MOVE 'PARAMETROS BUS-VIN CARGADOS' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           PERFORM BUS-VIN.
           IF NOT FIO-STAT-OKS
               MOVE 'BUSVIN ERROR'     TO WSS-CMSG
               MOVE '/CMP-VEN/'        TO WSS-MEN1
               MOVE 'ERROR EN BUS-VIN' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO FIN-CMP-VEN.
           IF ICG-COD-CVTI IN ICG > SPACES
               MOVE ICG-SGV-SPRD IN ICG TO INT-TINT
      *JSS, 20/05/92 (Memo.13/05/92)
               IF ICG-MSC-TFPR IN ICG = 'C' AND
                  ICG-SGV-TINT IN ICG > 0   AND
                  VEN-NUM-IVCT IN VEN-INC = 1
                   MOVE 'COL'       TO FIO-SIST
                   MOVE FIO-GET-KEY TO FIO-CMND
                   MOVE 'VSNLIQTMXCBCI' TO TAB-COD-TABL IN TAB
                   PERFORM GNS-FIO-TAB
                   IF FIO-STAT-OKS AND TAB-MSC-STAT IN TAB = 'S'
                       MOVE ICG-SGV-TINT IN ICG TO INT-SPR2
                   ELSE
                       NEXT SENTENCE
               ELSE
                   NEXT SENTENCE
      *
           ELSE
               MOVE ICG-SGV-TINT IN ICG TO INT-TINT.
           MOVE ICG-COD-TINT IN ICG TO INT-TIPT.
           MOVE 'PARAMETROS GNS-CAL-INT CARGADOS' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           IF WSS-IND-TRACE = 'S'
               DISPLAY ' INT-FUNO ' INT-FUNO
               DISPLAY ' INT-FDOS ' INT-FDOS
               DISPLAY ' INT-VCAM ' INT-VCAM
               DISPLAY ' INT-FCOL ' INT-FCOL
               DISPLAY ' INT-FBTV ' INT-FBTV
               DISPLAY ' INT-TINT ' INT-TINT
               DISPLAY ' INT-TIPT ' INT-TIPT.
           PERFORM GNS-CAL-INT.
           IF INT-CMSG > SPACES
               MOVE 'ERROR EN GNS-CAL-INT' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               MOVE INT-CMSG    TO WSS-CMSG
               MOVE INT-MENS    TO WSS-MEN1
               MOVE '/CMP-VEN/' TO WSS-MEN2
               GO TO FIN-CMP-VEN.

           MOVE ' GNS-CAL-INT OK' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.

           IF INT-IETB > SPACES
               GO TO FIN-CMP-VEN.
      * OBTIENE EL DOCUMENTO ASOCIADO AL VENCIMIENTO PARA DETERMINAR
      * SI ES ULTIMA CUOTA
           MOVE VEN-CIC-IOPC IN VEN-INC TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN-INC TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLC    NEX'        TO WSS-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO WSS-MEN1
               MOVE 'CMP-VEN/'          TO WSS-MEN2
               GO TO FIN-CMP-VEN.
           IF VEN-FEC-FVEN IN VEN-INC = DLC-FEC-FMAV IN DLC
               MOVE 'U' TO WSS-IND-ULTC IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-IND-ULTC IN WSS-VARI.

      * CALCULA EL INTERES DEL PERIODO COMO
      *
      *       WSS-VAL-INTE = TIPE * ( SCRE - CMOR + IDNI ) + IDNI
      *                               
      *
           SUBTRACT WSS-SGV-CMOR FROM OPC-VAL-SCRE IN OPC GIVING
                    WSS-SGV-SCRE-REAL.
           ADD WSS-SGV-SCRE-REAL WSS-VEN-SGV-IDNI GIVING
               WSS-VAL-INTE.
           MULTIPLY WSS-VAL-INTE BY INT-TIPE GIVING WSS-VAL-INTE.
           ADD WSS-VAL-INTE WSS-VEN-SGV-IDNI GIVING WSS-VAL-INTE.

      * REDONDEA EL INTERES SIN CENTAVOS PARA PESOS
           IF OPC-COD-VCOC IN OPC = CAM-COD-MNAC IN CAM-VARI
               MOVE WSS-VAL-INTE TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO WSS-VAL-INTE.

      * REALIZA PROCESOS ESPECIFICOS DE "COMPLETAR VENCIMIENTOS"
      * DEPENDIENDO DE OPERACION ESTRUCTURADA O NO
           MOVE 0 TO WSS-IND-CCVC.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM CMP-VEN-EVC
           ELSE
               PERFORM CMP-VEN-VEN.

           IF WSS-CMSG > SPACES
               GO TO FIN-CMP-VEN.

      *JSS 16-DEC-1991 11:33:57
      *Solo debe actualizar el SCPA cuando completa el valor capital
      *de la cuota y esta ademas debe ser la cuota vigente
           IF NOT ( WSS-IND-CCVC = 1 AND
                    VEN-NUM-IVCT IN VEN-INC = OPC-NUM-IVEN IN OPC )
               GO TO CON-CMP-VEN.

           MOVE VEN-CIC-IOPC IN VEN-INC TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN-INC TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLC    NOGKU'      TO WSS-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO WSS-MEN1
               MOVE 'CMP-VEN/'          TO WSS-MEN2
               GO TO FIN-CMP-VEN.
           SUBTRACT VEN-VAL-CAPI IN VEN-INC FROM DLC-VAL-SCPA IN DLC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'DLC    NOMOD'      TO WSS-CMSG
               MOVE DLC-KEY-IDLC IN DLC TO WSS-MEN1
               MOVE 'CMP-VEN/'          TO WSS-MEN2
               GO TO FIN-CMP-VEN.

       CON-CMP-VEN.
      * REGRABA VENCIMIENTO COMPLETO
           MOVE VEN-VAL-FINA IN VEN-INC TO VEN-VAL-SFIN IN VEN-INC.
           MOVE VEN-VAL-CAPI IN VEN-INC TO VEN-VAL-SCAP IN VEN-INC.
           MOVE SPACES                  TO VEN-IND-CALC IN VEN-INC.
           IF OPC-NUM-IVEN IN OPC = VEN-NUM-IVCT IN VEN-INC
                MOVE 'PGS'              TO VEN-IND-SCTB IN VEN-INC
           ELSE
                MOVE SPACES             TO VEN-IND-SCTB IN VEN-INC.
           MOVE VEN-KEY-IVEN IN VEN-INC TO VEN-KEY-IVEN IN VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    NOGKU'      TO WSS-CMSG
               MOVE VEN-KEY-IVEN IN VEN TO WSS-MEN1
               MOVE 'CMP-VEN/'          TO WSS-MEN2
               GO TO FIN-CMP-VEN.

           MOVE VEN-INC TO VEN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    NOMOD'      TO WSS-CMSG
               MOVE VEN-KEY-IVEN IN VEN TO WSS-MEN1
               MOVE 'CMP-VEN/'          TO WSS-MEN2
               GO TO FIN-CMP-VEN.

      *DAD ini  30-OCT-1991
           ADD 1 TO WSS-NUM-RCOM IN WSS-VARI.
      *DAD fin  30-OCT-1991
       FIN-CMP-VEN.
           EXIT.

       GET-VEN-INC SECTION.
       INI-GET-VEN-INC.
           MOVE 'TRACE GET-VEN-INC ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * OBTIENE EL PRIMER VCTO DE LA OPERACION QUE CUMPLE :
      * A) ( FVEN >= FMIN AND FVEN <= FTER ) ( INICIO - TERMINO DE PROCESO )
      *    NO CANCELADO Y QUE ESTE INCOMPLETO, SI TAL VEN NO EXISTE ENTONCES
      *    BUSCA AQUEL QUE CUMPLA :
      * B) FVEN > FTER ( TERMINO DE PROCESO ) NO CANCELADO
      * ACUMULA LOS SALDOS DE CAPITALES DE LOS ANTERIORES A EL
      * PARA OBTENER EL SALDO "REAL" DEL CREDITO Y CALCULAR LOS
      * INTERESES
           MOVE ZEROES TO WSS-NUM-PVEN.
           MOVE ZEROES TO WSS-SGV-CMOR.
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           IF OPC-IND-VSTR IN OPC = 'S'
               MOVE 1 TO VEN-NUM-IVEN IN VEN
           ELSE
               MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NLS TO FIO-CMND.
       CIC-GET-VEN-INC.
           PERFORM COL-FIO-VEN.
           IF NOT ( FIO-STAT-OKS AND
                    OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN )
      *JSS 16-DEC-1991 11:34:26
      *Restaura el CIC del vencimiento (Memo.06-12-91/2.1)
                  MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN
                  GO TO FIN-GET-VEN-INC.
      *
           IF VEN-FEC-FVEN IN VEN NOT < WSS-FEC-FMIN AND
              VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FTER
               IF VEN-NUM-IVCT IN VEN NOT < OPC-NUM-IVEN IN OPC
                   IF VEN-IND-CALC IN VEN = 'N'
                       MOVE VEN-NUM-IVCT IN VEN TO WSS-NUM-PVEN
                       MOVE VEN TO VEN-INC
                       GO TO FIN-GET-VEN-INC
                   ELSE
                       ADD VEN-VAL-SCAP IN VEN TO WSS-SGV-CMOR
               ELSE
                   NEXT SENTENCE
           ELSE
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTER AND
              VEN-NUM-IVCT IN VEN NOT < OPC-NUM-IVEN IN OPC
               MOVE VEN-NUM-IVCT IN VEN TO WSS-NUM-PVEN
               MOVE VEN TO VEN-INC
               GO TO FIN-GET-VEN-INC
           ELSE
               ADD VEN-VAL-SCAP IN VEN TO WSS-SGV-CMOR.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO CIC-GET-VEN-INC.
       FIN-GET-VEN-INC.
           EXIT.

       OBT-WSS-FEC-FUNO SECTION.
       INI-OBT-WSS-FEC-FUNO.
           MOVE 'TRACE OBT-WSS-FEC-FUNO ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *Obtiene fecha de devengo =
      *MIN( MAX(DLC-FDEV,VEN-FVEN anterior),VEN-FVEN actual)
           MOVE VEN-CIC-IOPC IN VEN-INC TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN-INC TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF FIO-STAT-OKS
               MOVE DLC-NUM-DDEV IN DLC TO WSS-NUM-DUNO
               MOVE DLC-NUM-MDEV IN DLC TO WSS-NUM-MUNO
               MOVE DLC-NUM-SDEV IN DLC TO WSS-NUM-SUNO
               MOVE DLC-NUM-ADEV IN DLC TO WSS-NUM-AUNO
               MOVE DLC-FEC-FDEV IN DLC TO WSS-FEC-FMAX
           ELSE
               MOVE 'DLC    NEX'         TO WSS-CMSG
               MOVE DLC-KEY-IDLC IN DLC  TO WSS-MEN1
               MOVE '/OBT-WSS-FEC-FUNO/' TO WSS-MEN2
               MOVE 'ERROR EN RECUPERACION DE DLC ASOCIADO'
                           TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO FIN-OBT-WSS-FEC-FUNO.
      *
      * OBTIENE DESDE EL VENCIMIENTO ANTERIOR AL ENCONTRADO
      * EN GET-VEN-INC SU FECHA DE VENCIMIENTO Y SUS INTERESES
      * DEVENGADOS NO INCLUIDOS
           IF VEN-NUM-IVCT IN VEN-INC = 1
                MOVE OPC-NUM-DCOL IN OPC TO WSS-NUM-DVAN
                MOVE OPC-NUM-MCOL IN OPC TO WSS-NUM-MVAN
                MOVE OPC-NUM-SCOL IN OPC TO WSS-NUM-SVAN
                MOVE OPC-NUM-ACOL IN OPC TO WSS-NUM-AVAN
                MOVE ZEROES TO WSS-VEN-SGV-IDNI
                GO TO CMP-OBT-WSS-FEC-FUNO.
           SUBTRACT 1 FROM VEN-NUM-IVCT IN VEN GIVING
                           VEN-NUM-IVEN IN VEN.
           PERFORM GET-KEY-VEN.
           IF WSS-CMSG > SPACES
               GO TO FIN-OBT-WSS-FEC-FUNO.
           MOVE VEN-NUM-DVEN IN VEN TO WSS-NUM-DVAN.
           MOVE VEN-NUM-MVEN IN VEN TO WSS-NUM-MVAN.
           MOVE VEN-NUM-SVEN IN VEN TO WSS-NUM-SVAN.
           MOVE VEN-NUM-AVEN IN VEN TO WSS-NUM-AVAN.
           MOVE VEN-VAL-IDNI IN VEN TO WSS-VEN-SGV-IDNI.
      *Debe tomar FECHA DE DEVENGO = MAX(DLC-FDEV,VEN-FVEN anterior )
           IF WSS-FEC-FMAX < VEN-FEC-FVEN IN VEN
               MOVE VEN-NUM-DVEN IN VEN TO WSS-NUM-DUNO
               MOVE VEN-NUM-MVEN IN VEN TO WSS-NUM-MUNO
               MOVE VEN-NUM-SVEN IN VEN TO WSS-NUM-SUNO
               MOVE VEN-NUM-AVEN IN VEN TO WSS-NUM-AUNO
               MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FMAX.
      *
      * RESTAURA EL VENCIMIENTO OBTENIDO EN GET-FST-VEN
           ADD 1 TO VEN-NUM-IVEN IN VEN.
           PERFORM GET-KEY-VEN.
           IF WSS-CMSG > SPACES
               MOVE 'ERROR EN RECUPERACION DE VCTO A COMPLETAR'
                           TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO FIN-OBT-WSS-FEC-FUNO.
       CMP-OBT-WSS-FEC-FUNO.
      *Debe tomar FECHA DE DEVENGO = MIN(DLC-FDEV,VEN-FVEN actual )
           IF WSS-FEC-FMAX > VEN-FEC-FVEN IN VEN-INC
               MOVE VEN-NUM-DVEN IN VEN-INC TO WSS-NUM-DUNO
               MOVE VEN-NUM-MVEN IN VEN-INC TO WSS-NUM-MUNO
               MOVE VEN-NUM-SVEN IN VEN-INC TO WSS-NUM-SUNO
               MOVE VEN-NUM-AVEN IN VEN-INC TO WSS-NUM-AUNO.
       FIN-OBT-WSS-FEC-FUNO.
           EXIT.
           
       CMP-VEN-EVC SECTION.
       INI-CMP-VEN-EVC.
           MOVE 'TRACE CMP-VEN-EVC ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * COMPLETA VENCIMIENTOS PARA OPERACIONES ESTRUCTURADAS

      * COMPLETA VALOR FINAL
            IF VEN-VAL-FINA IN VEN-INC NOT > ZEROES
                ADD VEN-VAL-CAPI IN VEN-INC WSS-VAL-INTE GIVING
                    VEN-VAL-FINA IN VEN-INC
                MOVE ZEROES TO VEN-VAL-IDNI IN VEN-INC
            ELSE
      * COMPLETA VALOR CAPITAL CONSIDERANDO QUE ES POSIBLE NO PAGAR
      * TODOS LOS INTERESES ( JSS  16-DEC-1991 11:35:21, MARCA INDICADOR
      * QUE INDICA QUE COMPLETA CAPITAL )
            MOVE 1 TO WSS-IND-CCVC
            IF VEN-VAL-FINA IN VEN-INC < WSS-VAL-INTE
                MOVE ZEROES TO VEN-VAL-CAPI IN VEN-INC
                SUBTRACT VEN-VAL-FINA IN VEN-INC FROM
                         WSS-VAL-INTE GIVING VEN-VAL-IDNI IN VEN-INC
            ELSE
                SUBTRACT WSS-VAL-INTE FROM VEN-VAL-FINA IN VEN-INC
                         GIVING VEN-VAL-CAPI IN VEN-INC
                MOVE ZEROES TO VEN-VAL-IDNI IN VEN-INC.
       FIN-CMP-VEN-EVC.
           EXIT.
           
       CMP-VEN-VEN SECTION.
       INI-CMP-VEN-VEN.
           MOVE 'TRACE CMP-VEN-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * COMPLETA VENCIMIENTOS PARA OPERACIONES NO ESTRUCTURADAS
      * REALIZA PROCESOS ESPECIFICOS DEPENDIENDO SI EL VALOR FINAL ESTA EN CERO
      * O NO Y SI ES ULTIMA CUOTA O NO

            IF VEN-VAL-FINA IN VEN-INC NOT > ZEROES
                IF WSS-IND-ULTC IN WSS-VARI NOT = 'U'
                    PERFORM CMP-VFIN-ITM
                ELSE
                    PERFORM CMP-VFIN-ULT
           ELSE
                IF WSS-IND-ULTC IN WSS-VARI NOT = 'U'
                    PERFORM CMP-VCAP-ITM
                ELSE
                    PERFORM CMP-VCAP-ULT.
       FIN-CMP-VEN-VEN.
           EXIT.
           
       CMP-VFIN-ITM SECTION.
       INI-CMP-VFIN-ITM.
           MOVE 'TRACE CMP-VFIN-ITM ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * COMPLETA VALOR FINAL PARA CUOTA INTERMEDIA
           IF VEN-VAL-CAPI IN VEN-INC > WSS-SGV-SCRE-REAL
               MOVE WSS-SGV-SCRE-REAL TO VEN-VAL-CAPI IN VEN-INC
               ADD WSS-VAL-INTE VEN-VAL-CAPI IN VEN-INC GIVING
                        VEN-VAL-FINA IN VEN-INC
               MOVE ZEROES TO VEN-VAL-IDNI IN VEN-INC
      * ACORTA EL CREDITO; ELIMINA VENCIMIENTOS SIGUIENTES
               PERFORM DEL-VEN-SIG
           ELSE
               ADD WSS-VAL-INTE VEN-VAL-CAPI IN VEN-INC GIVING
                        VEN-VAL-FINA IN VEN-INC
               MOVE ZEROES TO VEN-VAL-IDNI IN VEN-INC.
       FIN-CMP-VFIN-ITM.
           EXIT.

       CMP-VFIN-ULT SECTION.
       INI-CMP-VFIN-ULT.
           MOVE 'TRACE CMP-VFIN-ULT ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * COMPLETA VALOR FINAL PARA ULTIMA CUOTA
           MOVE WSS-SGV-SCRE-REAL TO VEN-VAL-CAPI IN VEN-INC.
           ADD WSS-VAL-INTE VEN-VAL-CAPI IN VEN-INC GIVING
                VEN-VAL-FINA IN VEN-INC.
           MOVE ZEROES TO VEN-VAL-IDNI IN VEN-INC.
       FIN-CMP-VFIN-ULT.
           EXIT.

       CMP-VCAP-ITM SECTION.
       INI-CMP-VCAP-ITM.
           MOVE 'TRACE CMP-VCAP-ITM ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * COMPLETA VALOR CAPITAL PARA CUOTA INTERMEDIA
      
           IF VEN-VAL-FINA IN VEN-INC NOT < WSS-VAL-INTE
      *    SI ALCANZA A PAGAR LOS INTERESES
               SUBTRACT WSS-VAL-INTE FROM VEN-VAL-FINA IN VEN-INC
                  GIVING VEN-VAL-CAPI IN VEN-INC
               MOVE ZEROES TO VEN-VAL-IDNI IN VEN-INC
      *        CON EL CAPITAL CALCULADO PAGA EL SALDO DEL CREDITO
               IF VEN-VAL-CAPI IN VEN-INC > WSS-SGV-SCRE-REAL
                   MOVE WSS-SGV-SCRE-REAL TO VEN-VAL-CAPI IN VEN-INC
                   ADD VEN-VAL-CAPI IN VEN-INC WSS-VAL-INTE GIVING
                       VEN-VAL-FINA IN VEN-INC
      *            ACORTA EL CREDITO; ELIMINA VENCIMIENTOS SIGUIENTES
                   PERFORM DEL-VEN-SIG
               ELSE
                   NEXT SENTENCE
           ELSE
      *    SI NO ALCANZA A PAGAR LOS INTERESES
               MOVE ZEROES TO VEN-VAL-CAPI IN VEN-INC
               SUBTRACT VEN-VAL-FINA IN VEN-INC FROM
                    WSS-VAL-INTE GIVING VEN-VAL-IDNI IN VEN-INC.
       FIN-CMP-VCAP-ITM.
           EXIT.

       CMP-VCAP-ULT SECTION.
       INI-CMP-VCAP-ULT.
           MOVE 'TRACE CMP-VCAP-ULT ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * COMPLETA VALOR CAPITAL PARA ULTIMA CUOTA
           IF VEN-VAL-FINA IN VEN NOT < WSS-VAL-INTE
      *    SI ALCANZA A PAGAR LOS INTERESES
               SUBTRACT WSS-VAL-INTE FROM VEN-VAL-FINA IN VEN-INC
                   GIVING VEN-VAL-CAPI IN VEN-INC
               MOVE ZEROES TO VEN-VAL-IDNI IN VEN-INC
               IF VEN-VAL-CAPI IN VEN-INC > WSS-SGV-SCRE-REAL
      *        CON EL CAPITAL CALCULADO PAGA EL SALDO DEL CREDITO
                   MOVE WSS-SGV-SCRE-REAL TO VEN-VAL-CAPI IN VEN-INC
                   ADD VEN-VAL-CAPI IN VEN-INC WSS-VAL-INTE GIVING
                       VEN-VAL-FINA IN VEN-INC
               ELSE
               IF VEN-VAL-CAPI IN VEN-INC < WSS-SGV-SCRE-REAL
      *            GENERAR UN VEN ADIC(I +1) CON
      *            VEN-VAL-CAPI = 0
      *            FINA = VEN-VAL-FINA IN VEN
      *            VEN-IND-CALC = "N"
      *            RESPECTO AL FLUJO 
      *              FVEN = VEN-FEC-FVEN + ( WSS-FEC-FDOS - WSS-FEC-FVAN )
      *            RESPECTO AL CURSE
                   PERFORM GEN-VEN-ADC
               ELSE
                   NEXT SENTENCE
           ELSE
      *    NO ALCANZA A PAGAR LOS INTERESES
               MOVE ZEROES TO VEN-VAL-CAPI IN VEN
               SUBTRACT VEN-VAL-FINA IN VEN-INC FROM WSS-VAL-INTE GIVING
                        VEN-VAL-IDNI IN VEN-INC
      *            GENERAR UN VEN ADIC(I +1) CON
      *            VEN-VAL-CAPI = 0
      *            FINA = VEN-VAL-FINA IN VEN
      *            VEN-IND-CALC = "N"
      *            RESPECTO AL FLUJO
      *              FVEN = VEN-FEC-FVEN + ( WSS-FEC-FDOS - WSS-FEC-FVAN )
      *              RESPECTO AL CURSE
                   PERFORM GEN-VEN-ADC.
       FIN-CMP-VCAP-ULT.
           EXIT.

       GET-KEY-VEN SECTION.
       INI-GET-KEY-VEN.
           MOVE 'TRACE GET-KEY-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS
                   MOVE 'VEN    NEX'    TO WSS-CMSG
                   MOVE '/GET-KEY-VEN/' TO WSS-MEN1
                   MOVE VEN-STAT        TO WSS-MEN2
               ELSE
                   NEXT SENTENCE
           ELSE
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'VEN    NEX'    TO WSS-CMSG
                   MOVE '/GET-KEY-VEN/' TO WSS-MEN1
                   MOVE FIO-STAT        TO WSS-MEN2.
       FIN-GET-KEY-VEN.
           EXIT.

       EXP-VEN SECTION.
       INI-EXP-VEN.
      *GENERA PROXIMO VENCIMIENTO ( EVC ) PARA UNA OPERACION
           MOVE 'TRACE EXP-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *DAD ini  07-ENE-1992   (Memo.27-12-91)
      * Obtiene el DLC asociado al VEN para determinar si es Ultima Cuota
           IF DLC-CIC-IOPC IN DLC = VEN-CIC-IOPC IN VEN AND
              VEN-NUM-IDLC IN VEN > 0                   AND
              VEN-NUM-IDLC IN VEN = DLC-NUM-IDLC IN DLC
               GO TO CHK-EXP-VEN.
           MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
           IF VEN-NUM-IDLC IN VEN = 0
               MOVE  1  TO DLC-NUM-IDLC IN DLC
           ELSE
               MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE DLC : ' DLC-KEY-IDLC IN DLC 
                       ' EN CICLO : EXP-VEN'
               GO TO FIN-EXP-VEN.
       CHK-EXP-VEN.
           IF VEN-FEC-FVEN IN VEN NOT < DLC-FEC-FMAV IN DLC
               GO TO FIN-EXP-VEN.
      *DAD fin  07-ENE-1992
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE VEN-NUM-IVEN IN VEN TO WSS-NUM-BVEN.
           ADD 1 TO WSS-NUM-BVEN.
           MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF VEN-STAT-TVX
               DISPLAY VEN-STAT '/OPERACION : ' OPC-CIC-IOPC
                     '/VENCIMIENTO : ' VEN-NUM-IVEN IN VEN
               GO TO FIN-EXP-VEN.
           IF NOT VEN-STAT-OKS
      *DAD ini  02-DIC-1991   (Memo.08-11-91 Pto.2 / Memo.21-10-91)
               IF CTO-OPEC IN CTO-VARI = 'D'
                   GO TO FIN-EXP-VEN
               ELSE
      *DAD fin  02-DIC-1991
                   MOVE 'VEN-STAT NO OK' TO WSS-GLS-ERR
                   PERFORM DSP-ERR
      *DAD ini  27-DIC-1991
                   DISPLAY VEN-STAT ' : ' VEN-MENS
      *DAD fin  27-DIC-1991
                   GO TO FIN-EXP-VEN.
           MOVE WSS-NUM-BVEN        TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'PUT VEN-STAT NO OK' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO FIN-EXP-VEN.
       FIN-EXP-VEN.
           EXIT.

       DEL-VEN-SIG SECTION.
       INI-DEL-VEN-SIG.
           MOVE 'TRACE DEL-VEN-SIG ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *ISP BORRA VENCIMIENTOS SIGUIENTES
           MOVE VEN-NUM-IVEN IN VEN TO WSS-NUM-IVCT IN WSS-VARI.
       NXT-DEL-VEN.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT (FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = OPC-CIC-IOPC IN OPC)
               GO TO LEE-CUR-VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO LEE-CUR-VEN.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO LEE-CUR-VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           SUBTRACT 1 FROM OPC-NUM-VCVG IN OPC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           GO TO NXT-DEL-VEN.
       LEE-CUR-VEN.
           MOVE OPC-CIC-IOPC IN OPC      TO VEN-CIC-IOPC IN VEN.
           MOVE WSS-NUM-IVCT IN WSS-VARI TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       FIN-DEL-VEN-SIG.
           EXIT.

       GEN-VEN-ADC SECTION.
       INI-GEN-VEN-ADC.
           MOVE 'TRACE GEN-VEN-ADC ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           ADD 1 TO VEN-NUM-IVEN IN VEN.
      *         05  VEN-STP-ITRN.
           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FAMT IN WSS-VARI.
           MOVE SPACES TO VEN-MSC-TACC IN VEN.
           MOVE SPACES TO VEN-MSC-STAT IN VEN.
           MOVE SPACES TO VEN-COD-OTRN IN VEN.
           MOVE SPACES TO VEN-COD-ATRN IN VEN.
           PERFORM OBT-FEC-VCIM.
           MOVE ZEROES TO VEN-VAL-CAPI IN VEN.
           MOVE ZEROES TO VEN-VAL-SCAP IN VEN.
           MOVE ZEROES TO VEN-VAL-IDNI IN VEN.
           MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN.
           MOVE SPACES TO VEN-IND-SCTB IN VEN.
           MOVE VEN-NUM-IVEN IN VEN TO VEN-NUM-IVCT IN VEN.
           MOVE SPACES              TO VEN-IND-SCBZ IN VEN.
           MOVE ZEROES              TO VEN-VAL-MTSC IN VEN.
           MOVE SPACES              TO VEN-IND-MTSC IN VEN.
           MOVE SPACES              TO VEN-GLS-DISP IN VEN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC
           ADD 1 TO OPC-NUM-VCVG IN OPC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           MOVE VEN-NUM-IDLC IN VEN TO DLC-CIC-IOPC IN DLC.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           MOVE VEN-FEC-FVEN IN VEN TO DLC-FEC-FMAV IN DLC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
       FIN-GEN-VEN-ADC.
           EXIT.
      *-------------------------------------------------------------------------
       OBT-FEC-FTC SECTION.
       INI-OBT-FEC-FTC.
           MOVE 'TRACE OBT-FEC-FTC ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *DAD ini  30-OCT-1991
      *   Se Elimino Analisis Feriado para Tasa Var. c/r al Curse en Despertador
           IF ICG-COD-CVTI IN ICG > SPACES
               IF ICG-MSC-TFPR = 'C'
                   PERFORM GET-FEC-CRS
               ELSE
                   PERFORM GET-FEC-FLJ
                   IF MSG-COD-MENS NOT > SPACES
                       PERFORM FER-FTC.
      *DAD fin  30-OCT-1991
       FIN-OBT-FEC-FTC.
           EXIT.

       GET-FEC-CRS SECTION.
       INI-GET-FEC-CRS.
           MOVE 'TRACE GET-FEC-CRS ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE RCE-NUM-DVEN IN RCE TO FEC-ITM1.
           MOVE RCE-NUM-MVEN IN RCE TO FEC-ITM2.
           MOVE RCE-NUM-SVEN IN RCE TO FEC-ITM3.
           MOVE RCE-NUM-AVEN IN RCE TO FEC-ITM4.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           IF WSS-IND-TRACE = 'S'
               DISPLAY ' RCE : ' RCE
               DISPLAY ' ICG : ' ICG.

           IF ICG-MSC-UTPR = 'D'
               MOVE FEC-SUM-DIA  TO FEC-CMND
               MOVE ICG-NUM-PREP TO FEC-NDIA
           ELSE
           IF ICG-MSC-UTPR = 'M'
               MOVE FEC-SUM-MES  TO FEC-CMND
               MOVE ICG-NUM-PREP TO FEC-NMES
           ELSE
      *JSS asume ano
      *    IF ICG-MSC-UTPR = 'A'
               MOVE FEC-SUM-ANO  TO FEC-CMND
               MOVE ICG-NUM-PREP TO FEC-NANO.
           PERFORM PRO-FEC.
           MOVE FEC-ITM1 TO RCE-NUM-DVEN IN RCE.
           MOVE FEC-ITM2 TO RCE-NUM-MVEN IN RCE.
           MOVE FEC-ITM3 TO RCE-NUM-SVEN IN RCE.
           MOVE FEC-ITM4 TO RCE-NUM-AVEN IN RCE.
           IF WSS-IND-TRACE = 'S'
               DISPLAY ' RCE : ' RCE.
       FIN-GET-FEC-CRS.
           EXIT.

       GET-FEC-FLJ SECTION.
       INI-GET-FEC-FLJ.
      *Si es respecto al flujo solo las OPC con EVC deben tener despertador
      *(en el caso de VEN con flujo los propios VEN son los despertadores)
      *por lo tanto para asegurarse pregunta para abandonar
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               GO TO FIN-GET-FEC-FLJ.
      *
           MOVE 'TRACE GET-FEC-FLJ ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE 'VEN    NEX' TO MSG-COD-MENS.
           MOVE 1 TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF VEN-FEC-FVEN IN VEN NOT > WSS-FEC-FTER
               PERFORM NXT-OBT-VEN
           ELSE
               PERFORM PRV-OBT-VEN.
           MOVE 'TRACE FIN-GET-FEC-FLJ ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           IF MSG-COD-MENS NOT > SPACES
               MOVE VEN-FEC-FVEN IN VEN TO RCE-FEC-FVEN IN RCE.
       FIN-GET-FEC-FLJ.
           EXIT.

       NXT-OBT-VEN SECTION.
       INI-NXT-OBT-VEN.
           MOVE 'TRACE NXT-OBT-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
       CIC-NXT-OBT-VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF VEN-STAT-OKS
               IF VEN-FEC-FVEN IN VEN > RCE-FEC-FVEN IN RCE
                   MOVE SPACES TO MSG-COD-MENS
               ELSE
                   GO TO CIC-NXT-OBT-VEN.
       FIN-NXT-OBT-VEN.
           EXIT.

       PRV-OBT-VEN SECTION.
       INI-PRV-OBT-VEN.
           MOVE 'TRACE PRV-OBT-VEN ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
       CIC-PRV-OBT-VEN.
           IF VEN-NUM-IVCT IN VEN = 1
               IF VEN-FEC-FVEN IN VEN > RCE-FEC-FVEN IN RCE
                   MOVE SPACES TO MSG-COD-MENS
                   GO TO FIN-PRV-OBT-VEN
               ELSE
                   GO TO FIN-PRV-OBT-VEN.

           SUBTRACT 1 FROM VEN-NUM-IVCT IN VEN
                    GIVING VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF VEN-STAT-OKS
               IF VEN-FEC-FVEN IN VEN NOT > RCE-FEC-FVEN IN RCE
                   MOVE SPACES TO MSG-COD-MENS
                   MOVE FIO-GET-NXT TO FIO-CMND
                   PERFORM COL-OBT-VEN
               ELSE
                   GO TO CIC-PRV-OBT-VEN.
       FIN-PRV-OBT-VEN.
           EXIT.

       FER-FTC SECTION.
       INI-FER-FTC.
           MOVE 'TRACE FER-FTC ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           IF OPC-IND-AFER IN OPC = 'S'
               MOVE RCE-NUM-DVEN IN RCE TO FEC-ITM1
               MOVE RCE-NUM-MVEN IN RCE TO FEC-ITM2
               MOVE RCE-NUM-SVEN IN RCE TO FEC-ITM3
               MOVE RCE-NUM-AVEN IN RCE TO FEC-ITM4
               MOVE FEC-FORM-FEC TO FEC-FORM
               MOVE FEC-FBLK-S   TO FEC-FBLK
               MOVE FEC-CHOY-N   TO FEC-CHOY
               MOVE FEC-VHBL-S   TO FEC-VHBL
               MOVE FEC-VAL-FEC  TO FEC-CMND
               PERFORM PRO-FEC
               IF FEC-STAT-FER
                   MOVE FEC-SIG-HBL  TO FEC-CMND
                   PERFORM PRO-FEC
                   MOVE FEC-ITM1 TO RCE-NUM-DVEN IN RCE
                   MOVE FEC-ITM2 TO RCE-NUM-MVEN IN RCE
                   MOVE FEC-ITM3 TO RCE-NUM-SVEN IN RCE
                   MOVE FEC-ITM4 TO RCE-NUM-AVEN IN RCE.
       FIN-FER-FTC.
           EXIT.
      *-------------------------------------------------------------------------
       DSP-TRACE SECTION.
       INI-DSP-TRACE.
           IF WSS-IND-TRACE = 'S'
               DISPLAY VEN-KEY-IVEN IN VEN ' ' WSS-GLS-TRACE.
       FIN-DSP-TRACE.
           EXIT.

       DSP-ERR SECTION.
       INI-DSP-ERR.
           ADD 1 TO WSS-NUM-RERR.
           DISPLAY VEN-KEY-IVEN IN VEN ' ' WSS-GLS-ERR.
       FIN-DSP-ERR.
           EXIT.

       COPY COLBFOPC.
       COPY COLBFCYA.
       COPY COLBFAUT.
       COPY COLBFCAN.
       COPY COLBFVEN.
       COPY COLBFRCV.
       COPY COLBFRCE.
       COPY COLBFTOC.
       COPY COLBFRDT.
       COPY COLBFDLC.
       COPY COLBFEVC.
       COPY COLBFICG.
       COPY COLBGVEN.
       COPY COLBGVIN.
      *DAD ini  21-NOV-1991
       COPY COLBGCTO.
      *DAD fin  21-NOV-1991
       COPY TABBFCAM.
       COPY TABBVOFI.
       COPY TABBFOFI.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
      *DAD  ini 24-ABR-1991 
       COPY GNSBBTAB.
      *DAD  fin 24-ABR-1991 
       COPY GNSBBMSG.
       COPY GNSBGCAM.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
       COPY GNSBGABT.
      *DAD fin  29-MAY-1992
       COPY GNSBGPES.
       COPY GNSBGINT.
       COPY GNSBGCVI.

       BUS-REG-OPC SECTION.
       INI-BUS-REG-OPC.
           MOVE 'TRACE BUS-REG-OPC ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NEX' TO MSG-COD-MENS
               GO TO FIN-BUS-REG-OPC.
           IF OPC-MSC-STAT IN OPC NOT = 'A'
               MOVE 'OPCNOACA' TO MSG-COD-MENS.
       FIN-BUS-REG-OPC.
           EXIT.

       BUS-CYA-AUT SECTION.
       INI-BUS-CYA-AUT.
           MOVE 'TRACE BUS-AUT-CYA' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE OPC-CAI-IOPC IN OPC TO AUT-CAI-IOPC IN AUT.
           MOVE OPC-IIC-IOPC IN OPC TO AUT-IIC-IOPC IN AUT.
           MOVE 'AUT'               TO AUT-NUM-ICAN IN AUT.
           MOVE 1                   TO AUT-NUM-ICYA IN AUT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-AUT.
           IF NOT FIO-STAT-OKS
               MOVE 'CYAAUTNEX' TO MSG-COD-MENS
           ELSE
               MOVE AUT TO CYA.
       FIN-BUS-CYA-AUT.
           EXIT.

       GEN-REG-CAN SECTION.
       INI-GEN-REG-CAN.
           MOVE 'TRACE GEN-REG-CAN' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * Programacion de Generacion Cancelacion
           PERFORM BUS-ULT-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 1 TO CAN-NUM-ICAN IN CAN.
           MOVE '00' TO FIO-STAT.
           PERFORM INI-REG-CAN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-GEN-REG-CAN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-CAN.
           EXIT.

       BUS-ULT-CAN SECTION.
       INI-BUS-ULT-CAN.
           MOVE 'TRACE BUS-ULT-CAN' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *Busca Correlativo Ultima Cancelacion.
           MOVE VEN-CIC-IOPC IN VEN TO CAN-CIC-IOPC IN CAN.
           MOVE 999                 TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = CAN-CIC-IOPC IN CAN
               ADD 1 TO CAN-NUM-ICAN IN CAN
               GO TO FIN-BUS-ULT-CAN.
           MOVE FIO-STAT-NEX TO FIO-STAT.
       FIN-BUS-ULT-CAN.
           EXIT.

       INI-REG-CAN SECTION.
       INI-INI-REG-CAN.
           MOVE 'TRACE INI-REG-CAN' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE VEN-CIC-IOPC IN VEN TO CAN-CIC-IOPC IN CAN.
           MOVE ZEROES              TO CAN-STP-ITRN IN CAN.
           MOVE SPACES              TO CAN-MSC-TACC IN CAN.
           MOVE 'G'                 TO CAN-MSC-STAT IN CAN.
           MOVE SPACES              TO CAN-COD-OTRN IN CAN.
           MOVE SPACES              TO CAN-COD-ATRN IN CAN.
           MOVE CYA-COD-OFIC IN CYA TO CAN-COD-OFCA IN CAN.
           MOVE OPC-COD-RMOC IN OPC TO CAN-COD-RCAN IN CAN.
           MOVE 'CTE'               TO CAN-MSC-CANC IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DCAN IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MCAN IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SCAN IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-ACAN IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DEGR IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MEGR IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SEGR IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-AEGR IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DOUT IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MOUT IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SOUT IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-AOUT IN CAN.
           MOVE ZEROES                   TO CAN-SGV-GAST IN CAN.
      *Calcular VEN-VAL-SFIN * VCAM_FPRO si es reajustable
           PERFORM CAL-SGV-TPAG.
           MOVE ZEROES                   TO CAN-SGV-TREN IN CAN.
           MOVE ZEROES                   TO CAN-SGV-DIFE IN CAN.
           MOVE SPACES                   TO CAN-GLS-DISP IN CAN.
       FIN-INI-REG-CAN.
           EXIT.

       CAL-SGV-TPAG SECTION.
       INI-CAL-SGV-TPAG.
           MOVE 'TRACE CAL-SGV-TPAG' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *Calcular VEN-VAL-SFIN * VCAM_FPRO si es reajustable
           MOVE VEN-VAL-SFIN IN VEN TO CAN-SGV-TPAG IN CAN.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC     TO TAB-COD-CTAB IN TAB
               MOVE '0999  '                TO TAB-CIC-CTAB IN TAB
               MOVE WSS-NUM-DPRO IN WSS-VARI TO TAB-NUM-DTRN IN TAB
               MOVE WSS-NUM-MPRO IN WSS-VARI TO TAB-NUM-MTRN IN TAB
               MOVE WSS-NUM-SPRO IN WSS-VARI TO TAB-NUM-STRN IN TAB
               MOVE WSS-NUM-APRO IN WSS-VARI TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SFIN IN
                          VEN GIVING CAN-SGV-TPAG IN CAN
               ELSE
                   IF MSG-COD-MENS IN MSG NOT = 'CODVCAMEQL'
                       MOVE FIO-STAT-NEX TO FIO-STAT.
       FIN-CAL-SGV-TPAG.
           EXIT.
       GEN-REG-RCV SECTION.
       INI-GEN-REG-RCV.
           MOVE 'TRACE GEN-REG-RCV' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * Programacion de Generacion Relacion Cancelacion Vencimiento
           MOVE '00' TO FIO-STAT.
           PERFORM INI-REG-RCV.
           IF NOT FIO-STAT-OKS
               GO TO FIN-GEN-REG-RCV.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS
               MOVE 'RCV    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-RCV.
           EXIT.
       INI-REG-RCV SECTION.
       INI-INI-REG-RCV.
           MOVE 'TRACE INI-REG-RCV' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE SPACES              TO RCV-GLS-FLAG IN RCV.
           MOVE CAN-CIC-IOPC IN CAN TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN TO RCV-NUM-ICAN IN RCV.
           MOVE VEN-CIC-IOPC IN VEN TO RCV-CIC-IOPV IN RCV.
           MOVE VEN-NUM-IVEN IN VEN TO RCV-NUM-IVCT IN RCV.
           MOVE ZEROES              TO RCV-STP-ITRN IN RCV.
           MOVE SPACES              TO RCV-MSC-TACC IN RCV.
           MOVE SPACES              TO RCV-MSC-STAT IN RCV.
           MOVE SPACES              TO RCV-COD-OTRN IN RCV.
           MOVE SPACES              TO RCV-COD-ATRN IN RCV.
           MOVE OPC-CIC-IOPC IN OPC TO RCV-CIC-IOPD IN RCV.
           MOVE VEN-NUM-IDLC IN VEN TO RCV-NUM-IDLC IN RCV.
           MOVE VEN-NUM-IEVC IN VEN TO RCV-NUM-IEVC IN RCV.
           MOVE 'F'                 TO RCV-MSC-MCAN IN RCV.
           MOVE VEN-VAL-SCAP IN VEN TO RCV-SGV-CAPI IN RCV.
           MOVE VEN-VAL-IDNI IN VEN TO RCV-SGV-INTE IN RCV.
           MOVE VEN-VAL-SFIN IN VEN TO RCV-SGV-FINA IN RCV.
           MOVE VEN-IND-SCTB IN VEN TO RCV-IND-SCTB IN RCV.
           MOVE VEN-IND-SCBZ IN VEN TO RCV-IND-SCBZ IN RCV.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO RCV-NUM-DCAN IN RCV.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO RCV-NUM-MCAN IN RCV.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO RCV-NUM-SCAN IN RCV.
           MOVE WSS-NUM-APRO IN WSS-VARI TO RCV-NUM-ACAN IN RCV.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO RCV-NUM-DOUT IN RCV.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO RCV-NUM-MOUT IN RCV.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO RCV-NUM-SOUT IN RCV.
           MOVE WSS-NUM-APRO IN WSS-VARI TO RCV-NUM-AOUT IN RCV.
      *Calculo de reajustes SCAP (VCAM_FPRO - VCAM_FCOL)
           PERFORM CAL-RCV-REAJ.
           MOVE SPACES                   TO RCV-GLS-DISP IN RCV.
       FIN-INI-REG-RCV.
           EXIT.
       CAL-RCV-REAJ SECTION.
       INI-CAL-RCV-REAJ.
           MOVE 'TRACE CAL-REAJ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      *Calculo de reajustes SCAP (VCAM_FPRO - VCAM_FCOL)
           MOVE ZEROES                      TO RCV-SGV-REAJ IN RCV.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC     TO TAB-COD-CTAB IN TAB
               MOVE '0999  '                TO TAB-CIC-CTAB IN TAB
               MOVE WSS-NUM-DPRO IN WSS-VARI TO TAB-NUM-DTRN IN TAB
               MOVE WSS-NUM-MPRO IN WSS-VARI TO TAB-NUM-MTRN IN TAB
               MOVE WSS-NUM-SPRO IN WSS-VARI TO TAB-NUM-STRN IN TAB
               MOVE WSS-NUM-APRO IN WSS-VARI TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN
                          VEN GIVING WSS-SGV-PROC IN WSS-VARI
               ELSE
                   IF MSG-COD-MENS IN MSG NOT = 'CODVCAMEQL'
                       MOVE FIO-STAT-NEX TO FIO-STAT
                       GO TO FIN-CAL-RCV-REAJ
                   ELSE
                       GO TO FIN-CAL-RCV-REAJ.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC     TO TAB-COD-CTAB IN TAB
               MOVE '0999  '                TO TAB-CIC-CTAB IN TAB
               MOVE OPC-NUM-DCOL IN OPC     TO TAB-NUM-DTRN IN TAB
               MOVE OPC-NUM-MCOL IN OPC     TO TAB-NUM-MTRN IN TAB
               MOVE OPC-NUM-SCOL IN OPC     TO TAB-NUM-STRN IN TAB
               MOVE OPC-NUM-ACOL IN OPC     TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN
                          VEN GIVING WSS-SGV-COLO IN WSS-VARI
               ELSE
                   IF MSG-COD-MENS IN MSG NOT = 'CODVCAMEQL'
                       MOVE FIO-STAT-NEX TO FIO-STAT
                       GO TO FIN-CAL-RCV-REAJ.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               SUBTRACT WSS-SGV-COLO IN WSS-VARI FROM
                        WSS-SGV-PROC IN WSS-VARI GIVING
                        RCV-SGV-REAJ IN RCV.
       FIN-CAL-RCV-REAJ.
           EXIT.

       GEN-REG-CYA SECTION.
       INI-GEN-REG-CYA.
           MOVE 'TRACE GEN-REG-CYA' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
      * Programacion de Generacion Cargos y Abonos
           PERFORM INI-REG-CYA.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS
               MOVE 'CYA    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-CYA.
           EXIT.
       INI-REG-CYA SECTION.
       INI-INI-REG-CYA.
           MOVE 'TRACE INI-REG-CYA' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE CAN-NUM-ICAN IN CAN TO CYA-NUM-ICAN IN CYA.
           MOVE 1                   TO CYA-NUM-ICYA IN CYA.
           MOVE CAN-SGV-TPAG IN CAN TO CYA-SGV-LIQU IN CYA.
           MOVE 'S'                 TO CYA-IND-MAUT IN CYA.
           MOVE SPACES              TO CYA-IND-FREE IN CYA.
           MOVE SPACES              TO CYA-GLS-DISP IN CYA.
       FIN-INI-REG-CYA.
           EXIT.

       BCK-OUT SECTION.
       INI-BCK-OUT.
           MOVE 'TRACE BCK-OUT ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'BCKOUTN' TO MSG-COD-MENS.
       FIN-BCK-OUT.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE 'TRACE CLOSE-FILE ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           DISPLAY 'REGISTRO VEN LEIDOS     : ' WSS-NUM-RLEI IN WSS-VARI
           DISPLAY 'REGISTRO VEN PROCESADOS : ' WSS-NUM-RPRO IN WSS-VARI
      *DAD ini  30-OCT-1991
           DISPLAY 'REGISTRO RCE PROCESADOS : ' WSS-NUM-RRCE IN WSS-VARI
           DISPLAY 'REGISTRO VEN COMPLETADOS: ' WSS-NUM-RCOM IN WSS-VARI
      *DAD fin  30-OCT-1991
           DISPLAY 'REGISTRO VEN ERRONEOS   : ' WSS-NUM-RERR IN WSS-VARI
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-CAN.
       FIN-CLOSE-FILE.
           EXIT.

       PRO-VEN-MORO SECTION.
       INI-PRO-VEN-MORO.
           MOVE 'TRACE PRO-VEN-MORO ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           IF VEN-IND-SCTB IN VEN     > SPACES AND
              VEN-IND-SCTB IN VEN NOT = 'VIG'
               GO TO FIN-PRO-VEN-MORO.
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-PRO-VEN-MORO.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM EVC-VEN-MORO
           ELSE
               PERFORM VEN-VEN-MORO.
       FIN-PRO-VEN-MORO.
           EXIT.

       EVC-VEN-MORO SECTION.
       INI-EVC-VEN-MORO.
           MOVE 'TRACE EVC-VEN-MORO ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN.
           MOVE 'MOROSO PARA EVC' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VCTO NO ENCONTRADO PARA MOROSO EVC' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO FIN-EVC-VEN-MORO.

           MOVE 'MOR' TO VEN-IND-SCTB IN VEN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-EVC-VEN-MORO.

           PERFORM PRO-RDT.
           IF NOT FIO-STAT-OKS
               MOVE 'ERROR EN PRO-RDT' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO FIN-EVC-VEN-MORO.

           PERFORM PRO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'ERROR EN PRO-DLC' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO FIN-EVC-VEN-MORO.

           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.

           IF NOT FIO-STAT-OKS
               GO TO RST-EVC-VEN-MORO.

           IF VEN-CIC-IOPC IN VEN NOT = OPC-CIC-IOPC IN OPC
               GO TO RST-EVC-VEN-MORO.

           IF VEN-IND-SCTB IN VEN NOT > SPACES OR
              VEN-IND-SCTB IN VEN = 'VIG'
               GO TO RST-EVC-VEN-MORO.

           PERFORM PRO-RDT.
           IF NOT FIO-STAT-OKS
               MOVE 'ERROR EN PRO-RDT' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO RST-EVC-VEN-MORO.

           PERFORM PRO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'ERROR EN PRO-DLC' TO WSS-GLS-ERR
               PERFORM DSP-ERR
               GO TO RST-EVC-VEN-MORO.
       RST-EVC-VEN-MORO.
           MOVE WSS-VEN-KEY-IVEN TO VEN-KEY-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       FIN-EVC-VEN-MORO.
           EXIT.

       VEN-VEN-MORO SECTION.
       INI-VEN-VEN-MORO.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-VEN-VEN-MORO.
           MOVE 'MOR' TO VEN-IND-SCTB IN VEN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-VEN-VEN-MORO.
           PERFORM PRO-RDT.
           IF NOT FIO-STAT-OKS
               GO TO FIN-VEN-VEN-MORO.
           PERFORM PRO-DLC.
       FIN-VEN-VEN-MORO.
           EXIT.

       PRO-RDT SECTION.
       INI-PRO-RDT.
           MOVE 'TRACE PRO-RDT ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           MOVE 'COL' TO MSC-COD-SIST FIO-SIST.
           MOVE 'OPEC' TO MSC-COD-TMSC IN MSC.
           MOVE TOC-MSC-OPEC IN TOC TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           MOVE VEN-CIC-IOPC IN VEN TO RDT-CIC-IOPC IN RDT.
           MOVE VEN-NUM-IDLC IN VEN TO RDT-NUM-IDLC IN RDT.
           MOVE VEN-IND-SCTB IN VEN TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF FIO-STAT-OKS
               GO TO CON-PRO-RDT.
           IF MSC-CIC-CMSC IN MSC = 'D'
               MOVE VEN-VAL-FINA IN VEN TO RDT-VAL-SRDT IN RDT
           ELSE
               MOVE VEN-VAL-CAPI IN VEN TO RDT-VAL-SRDT IN RDT.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           GO TO FIN-PRO-RDT.
       CON-PRO-RDT.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-RDT.
           IF MSC-CIC-CMSC IN MSC = 'D'
               ADD VEN-VAL-FINA IN VEN TO RDT-VAL-SRDT IN RDT
           ELSE
               ADD VEN-VAL-CAPI IN VEN TO RDT-VAL-SRDT IN RDT.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-RDT.
       FIN-PRO-RDT.
           EXIT.

       PRO-DLC SECTION.
       INI-PRO-DLC.
           MOVE 'TRACE PRO-DLC ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE VEN-CIC-IOPC IN VEN TO RDT-CIC-IOPC IN RDT.
           MOVE VEN-NUM-IDLC IN VEN TO RDT-NUM-IDLC IN RDT.
           MOVE SPACES              TO RDT-IND-SCTB IN RDT.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RDT.
           IF NOT ( FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = RDT-CIC-IOPC IN RDT AND
              VEN-NUM-IDLC IN VEN = RDT-NUM-IDLC IN RDT )
               GO TO CO1-PRO-DLC.
      *     MOVE FIO-GET-NXT TO FIO-CMND.
      *     PERFORM COL-FIO-RDT.
      *     IF NOT ( FIO-STAT-OKS AND
      *        VEN-CIC-IOPC IN VEN = RDT-CIC-IOPC IN RDT AND
      *        VEN-NUM-IDLC IN VEN = RDT-NUM-IDLC IN RDT )
      *         GO TO CO2-PRO-DLC.
           MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               GO TO FIN-PRO-DLC.
           MOVE 'MUL' TO DLC-IND-SCTB IN DLC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           GO TO FIN-PRO-DLC.
       CO1-PRO-DLC.
      *     MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
      *     MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
      *     MOVE FIO-GET-KEY-UPD TO FIO-CMND.
      *     PERFORM COL-FIO-DLC.
      *     IF NOT FIO-STAT-OKS
      *         GO TO FIN-PRO-DLC.
      *     IF DLC-IND-SCTB IN DLC NOT > SPACES OR
      *        DLC-IND-SCTB IN DLC = 'VIG'
      *         MOVE 'MOR' TO DLC-IND-SCTB IN DLC
      *         MOVE FIO-MOD TO FIO-CMND
      *         PERFORM COL-FIO-DLC.
      *     GO TO FIN-PRO-DLC.
       CO2-PRO-DLC.
      *     MOVE VEN-CIC-IOPC IN VEN TO DLC-CIC-IOPC IN DLC.
      *     MOVE VEN-NUM-IDLC IN VEN TO DLC-NUM-IDLC IN DLC.
      *     MOVE FIO-GET-KEY-UPD TO FIO-CMND.
      *     PERFORM COL-FIO-DLC.
      *     IF NOT FIO-STAT-OKS
      *         GO TO FIN-PRO-DLC.
      *     IF DLC-IND-SCTB IN DLC NOT > SPACES OR
      *        DLC-IND-SCTB IN DLC = 'VIG'
      *         MOVE 'MOR' TO DLC-IND-SCTB IN DLC
      *         MOVE FIO-MOD TO FIO-CMND
      *         PERFORM COL-FIO-DLC.
       FIN-PRO-DLC.
           EXIT.
       BUS-REG-RCV SECTION.
       INI-BUS-REG-RCV.
           MOVE 'TRACE BUS-REG-RCV ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE VEN-CIC-IOPC IN VEN TO RCV-CIC-IOPV IN RCV.
           MOVE VEN-NUM-IVEN IN VEN TO RCV-NUM-IVCT IN RCV.
           MOVE 'RCV-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF FIO-STAT-OKS
               MOVE 'CAN    YAEXI' TO MSG-COD-MENS.
       FIN-BUS-REG-RCV.
           EXIT.

       OBT-NUM-DIAS SECTION.
       INI-OBT-NUM-DIAS.
           MOVE 'TRACE OBT-NUM-DIAS ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           IF OPC-IND-VSTR IN OPC = 'S'
               MOVE ZEROES TO WSS-NUM-DIAS
               GO TO FIN-OBT-NUM-DIAS.
           MOVE WSS-NUM-DVAN TO FEC-DEC1.
           MOVE WSS-NUM-MVAN TO FEC-MEC1.
           MOVE WSS-NUM-SVAN TO FEC-SEC1.
           MOVE WSS-NUM-AVAN TO FEC-AEC1.
           MOVE WSS-NUM-DDOS TO FEC-DEC2.
           MOVE WSS-NUM-MDOS TO FEC-MEC2.
           MOVE WSS-NUM-SDOS TO FEC-SEC2.
           MOVE WSS-NUM-ADOS TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'FECSTATNOOK'    TO WSS-CMSG
               MOVE 'FEC-DIF-DIA'    TO WSS-MEN1
               MOVE '/OBT-NUM-DIAS/' TO WSS-MEN2
           ELSE
               MOVE FEC-NDIA TO WSS-NUM-DIAS.
       FIN-OBT-NUM-DIAS.
           EXIT.

       OBT-FEC-VCIM SECTION.
       INI-OBT-FEC-VCIM.
           MOVE 'TRACE OBT-FEC-VCIM ' TO WSS-GLS-TRACE.
           PERFORM DSP-TRACE.
           MOVE VEN-NUM-DVEN IN VEN     TO FEC-ITM1.
           MOVE VEN-NUM-MVEN IN VEN     TO FEC-ITM2.
           MOVE VEN-NUM-SVEN IN VEN     TO FEC-ITM3.
           MOVE VEN-NUM-AVEN IN VEN     TO FEC-ITM4.
           MOVE FEC-FORM-FEC            TO FEC-FORM.
           MOVE FEC-SUM-DIA             TO FEC-CMND.
           MOVE WSS-NUM-DIAS IN WSS-VARI TO FEC-NDIA.
           PERFORM PRO-FEC.
           MOVE FEC-ITM1                TO VEN-NUM-DVEN IN VEN.
           MOVE FEC-ITM2                TO VEN-NUM-MVEN IN VEN.
           MOVE FEC-ITM3                TO VEN-NUM-SVEN IN VEN.
           MOVE FEC-ITM4                TO VEN-NUM-AVEN IN VEN.
       BUS-NXT-HBL-VEN.
           IF OPC-IND-AFER IN OPC = 'S'
               MOVE VEN-NUM-DVEN IN VEN TO FEC-ITM1
               MOVE VEN-NUM-MVEN IN VEN TO FEC-ITM2
               MOVE VEN-NUM-SVEN IN VEN TO FEC-ITM3
               MOVE VEN-NUM-AVEN IN VEN TO FEC-ITM4
               MOVE FEC-FORM-FEC TO FEC-FORM
               MOVE FEC-FBLK-S   TO FEC-FBLK
               MOVE FEC-CHOY-N   TO FEC-CHOY
               MOVE FEC-VHBL-S   TO FEC-VHBL
               MOVE FEC-VAL-FEC  TO FEC-CMND
               PERFORM PRO-FEC
               IF FEC-STAT-FER
                   MOVE FEC-SIG-HBL  TO FEC-CMND
                   PERFORM PRO-FEC
                   MOVE FEC-ITM1 TO VEN-NUM-DVEN IN VEN
                   MOVE FEC-ITM2 TO VEN-NUM-MVEN IN VEN
                   MOVE FEC-ITM3 TO VEN-NUM-SVEN IN VEN
                   MOVE FEC-ITM4 TO VEN-NUM-AVEN IN VEN.

           MOVE VEN-FEC-FVEN IN VEN TO VEN-FEC-FIMV IN VEN.

           IF OPC-IND-AFER IN OPC = 'N'
               MOVE VEN-NUM-DIMV IN VEN TO FEC-ITM1
               MOVE VEN-NUM-MIMV IN VEN TO FEC-ITM2
               MOVE VEN-NUM-SIMV IN VEN TO FEC-ITM3
               MOVE VEN-NUM-AIMV IN VEN TO FEC-ITM4
               MOVE FEC-FBLK-S   TO FEC-FBLK
               MOVE FEC-CHOY-N   TO FEC-CHOY
               MOVE FEC-VHBL-S   TO FEC-VHBL
               MOVE FEC-VAL-FEC  TO FEC-CMND
               PERFORM PRO-FEC
               IF FEC-STAT-FER
                   MOVE VEN-NUM-DIMV IN VEN TO FEC-ITM1
                   MOVE VEN-NUM-MIMV IN VEN TO FEC-ITM2
                   MOVE VEN-NUM-SIMV IN VEN TO FEC-ITM3
                   MOVE VEN-NUM-AIMV IN VEN TO FEC-ITM4
                   MOVE FEC-FORM-FEC TO FEC-FORM
                   MOVE FEC-SIG-HBL  TO FEC-CMND
                   PERFORM PRO-FEC
                   MOVE FEC-ITM1 TO VEN-NUM-DIMV IN VEN
                   MOVE FEC-ITM2 TO VEN-NUM-MIMV IN VEN
                   MOVE FEC-ITM3 TO VEN-NUM-SIMV IN VEN
                   MOVE FEC-ITM4 TO VEN-NUM-AIMV IN VEN.
       FIN-OBT-FEC-VCIM.
           EXIT.
