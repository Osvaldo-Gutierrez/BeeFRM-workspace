       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLSEC.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    06-Mar-98 03:06 PM.

      * Programa Listador de Reporte SEC con Sort.
      *
      * V 1.1.2
      *
      *<<<< CMT_ID
      *>>>>

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            C01 IS SALTO
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTSEC         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTSEC         ASSIGN TO        UT-S-RPTSEC.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTSEC         ASSIGN TO        UT-S-RPTSEC.
      *<<<< IOS
      *>>>>

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRSEC.
      *<<<< RR
      *>>>>
       FD  RPTSEC LABEL RECORDS OMITTED
           RECORD CONTAINS 132 CHARACTERS.
       01  GRC-LINEA-SEC     PIC X(132).
      *<<<< FD_RPT
      *>>>>
      *<<<< FLS
      *>>>>

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBROPC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGGRC.

       COPY GNSWGPES.

       COPY GNSBRTAB.
       COPY TABBRCAM.
       COPY GNSBRMSG.
       COPY COLRWSEC.
      *<<<< RW
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY COLBRRDC.
       COPY COLBRDLC.
       COPY SGCBRDBC.
       COPY SGCBRDCP.
       COPY GNSWGFEC.
       COPY GNSWGCPT.
       COPY GNSWGHOY.
       COPY GNSBRMSC.
       COPY COLBRSEC.
      *>>>>
       COPY COLRNSEC.
       COPY COLROSEC.
       COPY GNSWGSYS.
       01  PGM-HRSR-SEC   VALUE 'N'       PIC X(01).
           88 HAY-REGISTROS-EN-SORT-SEC  VALUE 'S'.

       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       01  WSS-VARI.
      *INI-JGM
           03  WSS-GLS-DAT3       VALUE SPACES     PIC X(60).
           03  WSS-TTIO           REDEFINES
               WSS-GLS-DAT3       OCCURS 20     PIC X(03).

           03  MOV-I           VALUE ZEROES     PIC 9(02).
           03  WSS-IND-ETIO    VALUE 'N'        PIC X(01).
           03  WSS-IND-NOCA    VALUE 'S'        PIC X(01).
           03  WSS-NUM-IDLC                     PIC 9(03).
           03  WSS-NUM-NANO                     PIC 9(02).
           03  WSS-VAL-SCRE                     PIC 9(11)V9(04).
           03  WSS-RUT-ICLI    VALUE '50000000' PIC X(08).
           03  WSS-NUM-ICLI    REDEFINES WSS-RUT-ICLI
                                                PIC 9(08).
           03  WSS-COD-TCLI    VALUE SPACES     PIC X(03).
           03  WSS-FEC-FMAV.
               05  WSS-NUM-SMAV                 PIC 9(02).
               05  WSS-NUM-AMAV                 PIC 9(02).
               05  WSS-NUM-MMAV                 PIC 9(02).
               05  WSS-NUM-DMAV                 PIC 9(02).

           03  WSS-FEC-FCOL.
               05  WSS-NUM-SCOL                 PIC 9(02).
               05  WSS-NUM-ACOL                 PIC 9(02).
               05  WSS-NUM-MCOL                 PIC 9(02).
               05  WSS-NUM-DCOL                 PIC 9(02).
           03  WSS-FMIN.
               05  WSS-SMIN                     PIC 9(02).
               05  WSS-AMIN                     PIC 9(02).
               05  WSS-MMIN                     PIC 9(02).
               05  WSS-DMIN                     PIC 9(02).

           03  WSS-GLS-DDIA                     PIC X(04).
           03  WSS-NUM-DDIA REDEFINES WSS-GLS-DDIA
                                                PIC 9(04).

           03  WSS-GLS-DMES                     PIC X(03).
           03  WSS-NUM-DMES REDEFINES WSS-GLS-DMES
                                                PIC 9(03).
           03  WSS-GLS-DANO                     PIC X(03).
           03  WSS-NUM-DANO REDEFINES WSS-GLS-DANO
                                                PIC 9(03).
           03  WSS-VAL-SEGD                     PIC 9(11)V9(04).
           03  WSS-VAL-SEGP                     PIC 9(11)V9(04).

           03  WSS-GLS-DATA.
               05  WSS-GLS-CLIE                 PIC X(09).
               05  WSS-VAL-CLIE REDEFINES WSS-GLS-CLIE
                                                PIC 9(03)V9(06).
               05  FILL1                        PIC X(01).
               05  WSS-GLS-NCLI                 PIC X(09).
               05  WSS-VAL-NCLI REDEFINES WSS-GLS-NCLI
                                                PIC 9(03)V9(06).
               05  FILL2                        PIC X(41).
      *FIN-JGM

           03  WSS-GLS-MES1           PIC X(03).
           03  WSS-NUM-MES1  REDEFINES WSS-GLS-MES1 PIC 9(03).
           03  WSS-GLS-MES2           PIC X(03).
           03  WSS-NUM-MES2  REDEFINES WSS-GLS-MES2 PIC 9(03).
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO       PIC 9(02).
               05  WSS-NUM-MPRO       PIC 9(02).
               05  WSS-NUM-SPRO       PIC 9(02).
               05  WSS-NUM-APRO       PIC 9(02).
           03  WSS-FEC-FAUX.
               05  WSS-NUM-SAUX       PIC 9(02).
               05  WSS-NUM-AAUX       PIC 9(02).
               05  WSS-NUM-MAUX       PIC 9(02).
               05  WSS-NUM-DAUX       PIC 9(02).
           03  WSS-FEC-FEC2.
               05  WSS-NUM-SEC2       PIC 9(02).
               05  WSS-NUM-AEC2       PIC 9(02).
               05  WSS-NUM-MEC2       PIC 9(02).
               05  WSS-NUM-DEC2       PIC 9(02).
           03  WSS-VEC-TRMO.
               05  WSS-GLS-TRMO  OCCURS 10  PIC X(04).
           03  WSS-VEC-TRM02 REDEFINES WSS-VEC-TRMO.
               05  WSS-NUM-TRMO  OCCURS 10  PIC 9(04).
           03  WSS-CNTD          VALUE 0    PIC 9(02).
           03  WSS-CMAX          VALUE 0    PIC 9(02).

      * RRM-INI 11-ENE-1999
      *    03  WSS-NUM-DIA      VALUE 0    PIC 9(04).
      *    03  WSS-NUM-FNAC      VALUE 0    PIC 9(04).
      *    03  WSS-NUM-FMAV      VALUE 0    PIC 9(04).
           03  WSS-NUM-DIA       VALUE 0    PIC 9(06).
           03  WSS-NUM-FNAC      VALUE 0    PIC 9(06).
           03  WSS-NUM-FMAV      VALUE 0    PIC 9(06).
      * RRM-FIN 11-ENE-1999
           03  WSS-GLS-DAT2-MINMAX.
               05  WSS-AGNO-NUM-MINMAX.
                   07  WSS-MIN-AGNO         PIC X(02).
                   07  WSS-MAX-AGNO         PIC X(02).
               05  FILLER                   PIC X(16).
           03  WSS-GLS-DAT3-MINMAX-ID.
               05  FILLER                   PIC X(10).
               05  WSS-AGNO-NUM-MINMAX-ID.
                   07  WSS-MIN-AGNO-ID       PIC X(02).
                   07  WSS-MAX-AGNO-ID       PIC X(02).
               05  FILLER                   PIC X(16).
           03  WSS-COD-MNAC                 PIC X(01).
           03  WSS-COD-PESO                 PIC X(06).
      *>>>>
      *<<<< WSS_DTC
      *>>>>

      * AQUI EXISTIA LABEL RF INI
      *<<<< RF
      *>>>>
      * AQUI EXISTIA LABEL RF FIN
       COPY COLRTSEC.
      *<<<< RT
      *>>>>

       PROCEDURE DIVISION.
      *==================

      * AQUI EXISTIA LABEL FIN_DEC INI
      *<<<< FIN_DEC
      *>>>>
      * AQUI EXISTIA LABEL FIN_DEC FIN

       MAIN SECTION.
      *------------
       INI-MAIN.
      *    MOVE SYS-MXFL TO SYS-CMND.
      *    PERFORM GNS-PRO-SYS.
       COPY GNSBGEDB.
      *    MOVE 'COL' TO RPT-SIST.
           MOVE 'COLPLSEC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
      *>>>>
           SORT GNSSRT
       COPY COLRSSEC.
      *<<<< RS
      *>>>>
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
      *<<<< FIN
      *>>>>
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
      * Abrir archivo SEC de salida
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-SEC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR EN APERTURA DE ARCHIVO COLSEC'
               GO TO FIN-INP-SRT.

      * Limpiar vector de tramos
           PERFORM LIMPIA-VECTOR.
      * Guardar tramos del Seguro en vector para posterior uso
           MOVE 'COL' TO TAB-COD-SIST FIO-SIST.
           MOVE 'SDM' TO TAB-COD-TTAB IN TAB.
           MOVE SPACES TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT (FIO-STAT-OKS AND
                   TAB-COD-TTAB IN TAB = 'SDM')
               DISPLAY 'ERROR EN LECTURA DE TRAMOS DE TABLA SDV'
               GO TO FIN-INP-SRT.

       LUP-TRMO.
           IF TAB-IND-VIGE IN TAB NOT = 'S' OR
              TAB-COD-CTAB IN TAB = SPACES
               GO TO SIG-TRMO.
           ADD 1                    TO WSS-CNTD IN WSS-VARI.
           MOVE TAB-CIC-CTAB IN TAB TO WSS-GLS-TRMO(WSS-CNTD).
       SIG-TRMO.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'SDM'
               GO TO LUP-TRMO.

       NXT-TRMO.
      * Captura fecha de Proceso
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO: ' WSS-FEC-FPRO IN WSS-VARI.
      * Validacion de parametro
      *VAL-FEC Valida fecha proceso
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-N               TO FEC-CHOY.
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN VALIDACION F. DE PROCESO : '
                        WSS-FEC-FPRO IN WSS-VARI '  ' FEC-MENS
              GO TO FIN-INP-SRT.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO FEC-ITM1.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO FEC-ITM2.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO FEC-ITM3.
           MOVE WSS-NUM-APRO IN WSS-VARI TO FEC-ITM4.
           MOVE 01                       TO FEC-NMES.
           MOVE FEC-RST-MES              TO FEC-CMND.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           PERFORM CAL-FEC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL RESTAR 1 A FECHA PROCESO'
               GO TO FIN-INP-SRT.

           MOVE 01                       TO WSS-NUM-DAUX IN WSS-VARI.
           MOVE FEC-ITM2                 TO WSS-NUM-MAUX IN WSS-VARI.
           MOVE FEC-ITM3                 TO WSS-NUM-SAUX IN WSS-VARI.
           MOVE FEC-ITM4                 TO WSS-NUM-AAUX IN WSS-VARI.

           MOVE 'COL' TO TAB-COD-SIST FIO-SIST.
           MOVE 'SDG' TO TAB-COD-TTAB IN TAB.
           MOVE 'SEDECO' TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'COD. SEDECO NO EXISTE EN TABLA COLSDG'
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT
           ELSE
               MOVE TAB-GLS-DAT2 IN TAB TO WSS-GLS-DAT2-MINMAX
                                                        IN WSS-VARI
               MOVE TAB-GLS-DAT2 IN TAB TO WSS-GLS-DAT3-MINMAX-ID
                                                        IN WSS-VARI.
           IF WSS-AGNO-NUM-MINMAX IN WSS-VARI NOT NUMERIC
               DISPLAY 'DATOS ANEXOS NO NUMERICOS: '
                                     WSS-AGNO-NUM-MINMAX IN WSS-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           IF WSS-AGNO-NUM-MINMAX-ID IN WSS-VARI NOT NUMERIC
               DISPLAY 'DATOS ANEXOS NO NUMERICOS: '
                                     WSS-AGNO-NUM-MINMAX-ID IN WSS-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.

      * DPM ini  6-MAR-1998
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CICPESONEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT
               GO TO FIN-INP-SRT.

           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

           MOVE 'TAB'  TO FIO-SIST.
           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CICTVAL0NEX' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-ABT
               GO TO FIN-INP-SRT.

           MOVE MSC-COD-CMSC IN MSC TO WSS-COD-MNAC IN WSS-VARI.
      * DPM fin  6-MAR-1998
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       FST-INP-SRT.
      *<<<< INI_FST_INP
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR EN APERTURA DE OPC'.
           MOVE WSS-FEC-FAUX IN WSS-VARI TO OPC-FEC-FINP IN OPC.
           MOVE 'OPC-FEC-FINP' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *<<<< FIN_FST_INP
      *>>>>
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *INI-JGM
           IF  NOT(OPC-NUM-MINP IN OPC  = WSS-NUM-MAUX IN WSS-VARI AND
                   OPC-NUM-SINP IN OPC  = WSS-NUM-SAUX IN WSS-VARI AND
                   OPC-NUM-AINP IN OPC  = WSS-NUM-AAUX IN WSS-VARI AND
                   OPC-MSC-STAT IN OPC = 'A' AND
                   OPC-VAL-SEGD IN OPC > ZEROES )
                     GO TO NXT-INP-SRT.
      *
           IF  OPC-COD-SEGD IN OPC  NOT = 'SEDECO' AND
               OPC-COD-SEGD IN OPC  NOT = 'SECOCE'
               GO TO NXT-INP-SRT.
      *
            MOVE ZEROES TO SEC.
            MOVE OPC-CIC-IOPC IN OPC TO OPC-CIC-IOPC IN SRT.
            MOVE OPC-VAL-CRED IN OPC TO OPC-VAL-CRED IN SRT.
            MOVE OPC-VAL-SEGD IN OPC TO WSS-SGV-SEGD IN SRT.

      * Llena archivo SEG con CIC operacion, valor credito,
      * monto prima cancelada
            MOVE OPC-CIC-IOPC IN SRT TO SEC-CIC-IOPC IN SEC.
            MOVE OPC-VAL-CRED IN SRT TO SEC-VAL-CRED IN SEC.
            MOVE WSS-SGV-SEGD IN SRT TO SEC-VAL-SEGD IN SEC.

      * Obtiene glosa de moneda para archivo SEG
            PERFORM OBT-GLS-MONE.
      *Busca si Credito Tiene deudor Indirecto.
           MOVE  1   TO WSS-NUM-IDLC IN WSS-VARI.
           PERFORM BUS-REG-RDC.
           IF MSG-COD-MENS IN MSG = 'OPC    NORDC'
              MOVE 'D'  TO SEC-IND-IDLC IN SEC
              MOVE 'D'  TO SEC-IND-IDLC IN SRT
              MOVE ZEROES  TO WSS-NUM-IDLC IN WSS-VARI
              PERFORM BUS-REG-RDC
              PERFORM BUSCA-RUT-NOMBRE-FECHA
      *       Determina tramo de dif. de meses
              PERFORM CALCULA-TRAMO
              PERFORM GRABA-REG-SEC
              RELEASE SRT
              GO TO NXT-INP-SRT.
      *

            PERFORM  CALC-SEG-SEDECO.
            IF WSS-VAL-SEGD IN WSS-VARI = ZEROES
               MOVE 'I'  TO SEC-IND-IDLC IN SEC
               MOVE 'I'  TO SEC-IND-IDLC IN SRT
               MOVE 1       TO WSS-NUM-IDLC IN WSS-VARI
               PERFORM BUS-REG-RDC
               PERFORM BUSCA-RUT-NOMBRE-FECHA
      *        Determina tramo de dif. de meses
               PERFORM CALCULA-TRAMO
               PERFORM GRABA-REG-SEC
               RELEASE SRT
               GO TO NXT-INP-SRT.
      *
            IF WSS-SGV-SEGD IN SRT NOT > WSS-VAL-SEGD IN WSS-VARI OR
               WSS-SGV-SEGD IN SRT NOT >
                            WSS-VAL-SEGD IN WSS-VARI + 10
               MOVE 'D'  TO SEC-IND-IDLC IN SEC
               MOVE 'D'  TO SEC-IND-IDLC IN SRT
               MOVE ZEROES  TO WSS-NUM-IDLC IN WSS-VARI
               PERFORM BUS-REG-RDC
               PERFORM BUSCA-RUT-NOMBRE-FECHA
      *        Determina tramo de dif. de meses
               PERFORM CALCULA-TRAMO
               PERFORM GRABA-REG-SEC
               RELEASE SRT
               GO TO NXT-INP-SRT.
      *
            MOVE 'D'  TO SEC-IND-IDLC IN SEC.
            MOVE 'D'  TO SEC-IND-IDLC IN SRT.
      * Deudor directo.
            MOVE WSS-VAL-SEGD IN WSS-VARI TO WSS-SGV-SEGD IN SRT.
            MOVE WSS-VAL-SEGD IN WSS-VARI TO SEC-VAL-CRED IN SEC.
            MOVE ZEROES                   TO WSS-NUM-IDLC IN WSS-VARI.
            PERFORM BUS-REG-RDC.
            PERFORM BUSCA-RUT-NOMBRE-FECHA.
      *     Determina tramo de dif. de meses
            PERFORM CALCULA-TRAMO.
            PERFORM GRABA-REG-SEC.
            RELEASE SRT.

      * Deudor Indirecto.
            MOVE 'I'  TO SEC-IND-IDLC IN SEC.
            MOVE 'I'  TO SEC-IND-IDLC IN SRT.
            COMPUTE
            WSS-SGV-SEGD IN SRT = OPC-VAL-SEGD IN OPC -
                                  WSS-VAL-SEGD IN WSS-VARI.
            MOVE WSS-SGV-SEGD IN SRT TO SEC-VAL-SEGD IN SEC.
            MOVE 1                   TO WSS-NUM-IDLC IN WSS-VARI.
            PERFORM BUS-REG-RDC.
            PERFORM BUSCA-RUT-NOMBRE-FECHA.
      *     Determina tramo de dif. de meses
            PERFORM CALCULA-TRAMO.
            PERFORM GRABA-REG-SEC.
            RELEASE SRT.
            GO TO NXT-INP-SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMSEC.
      *<<<< RM
      *>>>>
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
           MOVE 'OPC-FEC-FINP' TO FIO-AKEY.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *<<<< FIN_NXT_INP
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
      *<<<< FIN_INP
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-SEC.
      *>>>>
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
      *<<<< INI_OUT
      *>>>>
           OPEN OUTPUT RPTSEC.
      *     MOVE 'COLPLSEC' TO RPT-NPGM.
           PERFORM CAL-HDR.
           PERFORM INITIATE-RPTSEC.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           MOVE SRT TO SRT-OLD-SEC.
           MOVE SRT TO SRT-CUR-SEC.
      *<<<< FST_OUT
      *>>>>
      *    PERFORM SEC-FST-PRO-CTL.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *>>>>
       GEN-OUT-SRT.
           PERFORM GENERATE-DL-SEC.
      *<<<< FIN_GEN_OUT
      *>>>>
       NXT-OUT-SRT.
           MOVE SRT TO SRT-OLD-SEC.
       RET-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      *<<<< FIN_OUT
      *>>>>
           IF HAY-REGISTROS-EN-SORT-SEC
                PERFORM TERMINATE-RPTSEC.
      *<<<< FIN_TMT
      *>>>>
           CLOSE RPTSEC.
       EXT-OUT-SRT.
           EXIT.
       COPY COLBFOPC.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGIRH.
       COPY GNSBGPES.
       COPY COLRESEC.
      *    12-FEB-98 11:49 AM     ,PAI= ,CLI= ,SOP=MVS  V 1.1.3
       D-CF-SEC-WSS-VAL-TRMO SECTION.
       INI-CF-SEC-WSS-VAL-TRMO.
      *<<<< CF_SEC_WSS_VAL_TRMO
      *>>>>
       FIN-CF-SEC-WSS-VAL-TRMO.
           EXIT.
       D-PH-SEC SECTION.
       INI-PH-SEC.
      *<<<< PH_SEC
      *>>>>
       FIN-PH-SEC.
           EXIT.
       D-FF-SEC SECTION.
       INI-FF-SEC.
       FIN-FF-SEC.
           EXIT.
       D-DL-SEC SECTION.
       INI-DL-SEC.
       FIN-DL-SEC.
           EXIT.
       COPY GNSBGABT.

      *<<<< EOF
       COPY GNSBGHOY.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBGCPT.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBFMSC.
       COPY GNSBGCAM.

       COPY COLBFSEC.
       COPY COLBFRDC.
       COPY COLBFDLC.
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY TABBFCAM.

       OBT-GLS-MONE SECTION.
       INI-OBT-GLS-MONE.
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'VLR'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS NOT = SPACES
              MOVE MSG-GLS-DCOR TO WSS-GLS-MONE IN SRT
           ELSE
              MOVE TAB-GLS-ABRV TO WSS-GLS-MONE IN SRT.

           IF OPC-MSC-TVOC IN OPC NOT = WSS-COD-MNAC IN WSS-VARI
               MOVE 3 TO SEC-COD-MONE IN SEC
           ELSE
           IF OPC-COD-VCOC IN OPC = WSS-COD-PESO IN WSS-VARI
               MOVE 1 TO SEC-COD-MONE IN SEC
           ELSE
               MOVE 2 TO SEC-COD-MONE IN SEC.
      * DPM fin  6-MAR-1998
       FIN-OBT-GLS-MONE.
           EXIT.


       LIMPIA-VECTOR SECTION.
       INI-LIMPIA-VECTOR.
           MOVE 1 TO WSS-CNTD IN WSS-VARI.
       LUP-NXT.
           IF WSS-CNTD IN WSS-VARI NOT > 10
               MOVE ZEROES TO WSS-NUM-TRMO(WSS-CNTD)
               ADD 1 TO WSS-CNTD IN WSS-VARI
               GO TO LUP-NXT.
           MOVE ZEROES TO WSS-CNTD IN WSS-VARI.
       FIN-LIMPIA-VECTOR.
           EXIT.


       BUSCA-RUT-NOMBRE-FECHA SECTION.
       INI-BUSCA-RUT-NOMBRE-FECHA.


           IF NOT FIO-STAT-OKS OR
              RDC-CAI-IOPC IN RDC NOT = OPC-CAI-IOPC IN OPC OR
              RDC-IIC-IOPC IN RDC NOT = OPC-IIC-IOPC IN OPC OR
              RDC-NUM-IDLC IN RDC NOT = WSS-NUM-IDLC IN WSS-VARI
                MOVE SPACES TO DBC-NUM-ICLI IN DBC
                MOVE SPACES TO DBC-VRF-ICLI IN DBC
                MOVE SPACES TO DBC-GLS-NOMC IN DBC
                MOVE 'OPC    NORDC' TO MSG-COD-MENS IN MSG
                GO TO FIN-BUSCA-RUT-NOMBRE-FECHA.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR EN LECTURA DE DBC'
               GO TO FIN-BUSCA-RUT-NOMBRE-FECHA.

           MOVE DBC-NUM-ICLI IN DBC TO DBC-NUM-ICLI IN SRT.
           MOVE DBC-VRF-ICLI IN DBC TO DBC-VRF-ICLI IN SRT.
           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO DBC-GLS-NOMC IN SRT.
      * Llena rut y nombre de cliente para archivo
      *MFG INI 06-10-95
           MOVE ZEROES              TO SEC-FIL-ICLI IN SEC.
      *MFG FIN 06-10-95
           MOVE DBC-NUM-ICLI IN SRT TO SEC-NUM-ICLI IN SEC.
           MOVE DBC-VRF-ICLI IN SRT TO SEC-VRF-ICLI IN SEC.
           MOVE DBC-GLS-NOMC IN SRT TO SEC-GLS-NOMC IN SEC.

           PERFORM CALCULA-FECHA-NAC.
           PERFORM CALCULA-FECHA-CREDITO.

           IF WSS-NUM-FNAC IN WSS-VARI = ZEROES
               MOVE WSS-NUM-FMAV IN WSS-VARI TO WSS-NUM-PLCO IN SRT
                                                WSS-NUM-DIA IN WSS-VARI

      * Llena fecha de termino de cobertura en archivo SEC
               MOVE DLC-NUM-DMAV IN DLC TO SEC-NUM-DTER IN SEC
                                           SEC-NUM-DTER IN SRT
               MOVE DLC-NUM-MMAV IN DLC TO SEC-NUM-MTER IN SEC
                                           SEC-NUM-MTER IN SRT
               MOVE DLC-NUM-SMAV IN DLC TO SEC-NUM-STER IN SEC
                                           SEC-NUM-STER IN SRT
               MOVE DLC-NUM-AMAV IN DLC TO SEC-NUM-ATER IN SEC
                                           SEC-NUM-ATER IN SRT

           ELSE
           IF WSS-NUM-FMAV IN WSS-VARI = ZEROES
               MOVE WSS-NUM-FNAC IN WSS-VARI TO WSS-NUM-PLCO IN SRT
                                                WSS-NUM-DIA IN WSS-VARI

      * Llena fecha de termino de cobertura en archivo SEC
               MOVE WSS-NUM-DEC2 IN WSS-VARI TO SEC-NUM-DTER IN SEC
                                                SEC-NUM-DTER IN SRT
               MOVE WSS-NUM-MEC2 IN WSS-VARI TO SEC-NUM-MTER IN SEC
                                                SEC-NUM-MTER IN SRT
               MOVE WSS-NUM-SEC2 IN WSS-VARI TO SEC-NUM-STER IN SEC
                                                SEC-NUM-STER IN SRT
               MOVE WSS-NUM-AEC2 IN WSS-VARI TO SEC-NUM-ATER IN SEC
                                                SEC-NUM-ATER IN SRT

           ELSE
           IF WSS-NUM-FNAC IN WSS-VARI < WSS-NUM-FMAV IN WSS-VARI
               MOVE WSS-NUM-FNAC IN WSS-VARI TO WSS-NUM-PLCO IN SRT
                                                WSS-NUM-DIA IN WSS-VARI

      * Llena fecha de termino de cobertura en archivo SEC
               MOVE WSS-NUM-DEC2 IN WSS-VARI TO SEC-NUM-DTER IN SEC
                                                SEC-NUM-DTER IN SRT
               MOVE WSS-NUM-MEC2 IN WSS-VARI TO SEC-NUM-MTER IN SEC
                                                SEC-NUM-MTER IN SRT
               MOVE WSS-NUM-SEC2 IN WSS-VARI TO SEC-NUM-STER IN SEC
                                                SEC-NUM-STER IN SRT
               MOVE WSS-NUM-AEC2 IN WSS-VARI TO SEC-NUM-ATER IN SEC
                                                SEC-NUM-ATER IN SRT

           ELSE
               MOVE WSS-NUM-FMAV IN WSS-VARI TO WSS-NUM-PLCO IN SRT
                                                WSS-NUM-DIA IN WSS-VARI

      * Llena fecha de termino de cobertura en archivo SEC
               MOVE DLC-NUM-DMAV IN DLC TO SEC-NUM-DTER IN SEC
                                           SEC-NUM-DTER IN SRT
               MOVE DLC-NUM-MMAV IN DLC TO SEC-NUM-MTER IN SEC
                                           SEC-NUM-MTER IN SRT
               MOVE DLC-NUM-SMAV IN DLC TO SEC-NUM-STER IN SEC
                                           SEC-NUM-STER IN SRT
               MOVE DLC-NUM-AMAV IN DLC TO SEC-NUM-ATER IN SEC
                                           SEC-NUM-ATER IN SRT.

       FIN-BUSCA-RUT-NOMBRE-FECHA.
           EXIT.


       CALCULA-FECHA-NAC SECTION.
       INI-CALCULA-FECHA-NAC.
           MOVE DBC-CIC-ICLI IN DBC TO DCP-CIC-ICLI IN DCP.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DCP.

           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR EN LECTURA DE DCP'
               GO TO FIN-BUSCA-RUT-NOMBRE-FECHA
               MOVE ZEROES TO DCP.
            IF SEC-IND-IDLC IN SEC = 'D'
               MOVE WSS-MAX-AGNO TO FEC-NANO
            ELSE
               MOVE WSS-MAX-AGNO-ID TO FEC-NANO.
           IF DCP-FEC-FNAC IN DCP > ZEROES
               MOVE DCP-NUM-DNAC IN DCP TO FEC-ITM1
               MOVE DCP-NUM-MNAC IN DCP TO FEC-ITM2
               MOVE DCP-NUM-SNAC IN DCP TO FEC-ITM3
               MOVE DCP-NUM-ANAC IN DCP TO FEC-ITM4
               MOVE FEC-SUM-ANO  TO FEC-CMND
               PERFORM CAL-FEC

               IF FEC-STAT-OKS
                   MOVE FEC-ITM1 TO WSS-NUM-DEC2 IN WSS-VARI
                   MOVE FEC-ITM2 TO WSS-NUM-MEC2 IN WSS-VARI
                   MOVE FEC-ITM3 TO WSS-NUM-SEC2 IN WSS-VARI
                   MOVE FEC-ITM4 TO WSS-NUM-AEC2 IN WSS-VARI

                   IF WSS-FEC-FEC2 IN WSS-VARI >
                     OPC-FEC-FCOL IN OPC
                      PERFORM OBT-DIF-FNAC.

      * Llena fecha de nacimiento en archivo SEC
           MOVE DCP-NUM-DNAC IN DCP TO SEC-NUM-DNAC IN SEC.
           MOVE DCP-NUM-MNAC IN DCP TO SEC-NUM-MNAC IN SEC.
           MOVE DCP-NUM-SNAC IN DCP TO SEC-NUM-SNAC IN SEC.
           MOVE DCP-NUM-ANAC IN DCP TO SEC-NUM-ANAC IN SEC.
       FIN-CALCULA-FECHA-NAC.
           EXIT.


       CALCULA-FECHA-CREDITO SECTION.
       INI-CALCULA-FECHA-CREDITO.
      *Calculo de meses del credito
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE ZEROES TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT (FIO-STAT-OKS AND
              OPC-CIC-IOPC IN OPC = DLC-CIC-IOPC IN DLC)
               GO TO FIN-CALCULA-FECHA-CREDITO.

      * Llena fecha de curse real en archivo SEC
           MOVE OPC-NUM-DCOL IN OPC TO SEC-NUM-DCOL IN SEC
                                       SEC-NUM-DCOL IN SRT.
           MOVE OPC-NUM-MCOL IN OPC TO SEC-NUM-MCOL IN SEC
                                       SEC-NUM-MCOL IN SRT.
           MOVE OPC-NUM-SCOL IN OPC TO SEC-NUM-SCOL IN SEC
                                       SEC-NUM-SCOL IN SRT.
           MOVE OPC-NUM-ACOL IN OPC TO SEC-NUM-ACOL IN SEC
                                       SEC-NUM-ACOL IN SRT.

           PERFORM OBT-DIF-FMAV.
       FIN-CALCULA-FECHA-CREDITO.
           EXIT.


       CALCULA-TRAMO SECTION.
       INI-CALCULA-TRAMO.
           MOVE 1 TO WSS-CNTD IN WSS-VARI.
       LUP-CALC-TRMO.
           IF WSS-CNTD IN WSS-VARI NOT > 10 AND
              WSS-NUM-DIA IN WSS-VARI NOT > WSS-NUM-TRMO(WSS-CNTD)
               MOVE WSS-NUM-TRMO(WSS-CNTD) TO WSS-VAL-TRMO IN SRT
           ELSE
           IF WSS-CNTD IN WSS-VARI NOT > 10
               ADD 1 TO WSS-CNTD IN WSS-VARI
               GO TO LUP-CALC-TRMO.

           IF WSS-CNTD IN WSS-VARI > 10
               DISPLAY 'NO EXISTE TRAMO QUE CONTENGA A DIF. '
                       'DE DIAS ' WSS-NUM-DIA IN WSS-VARI.

      * Llena tramo de cobertura en archivo SEC
           MOVE WSS-VAL-TRMO IN SRT TO SEC-NUM-TRMO IN SEC.
       FIN-CALCULA-TRAMO.
           EXIT.

       OBT-DIF-FNAC SECTION.
       INI-OBT-DIF-FNAC.
           MOVE OPC-NUM-DCOL IN OPC      TO FEC-DEC1.
           MOVE OPC-NUM-MCOL IN OPC      TO FEC-MEC1.
           MOVE OPC-NUM-SCOL IN OPC      TO FEC-SEC1.
           MOVE OPC-NUM-ACOL IN OPC      TO FEC-AEC1.

           MOVE WSS-NUM-DEC2 IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MEC2 IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SEC2 IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-AEC2 IN WSS-VARI TO FEC-AEC2.

           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-DIF-DIA              TO FEC-CMND.
           PERFORM CAL-FEC.
           MOVE FEC-NDIA TO WSS-NUM-FNAC IN WSS-VARI.
       FIN-OBT-DIF-FNAC.
           EXIT.

       OBT-DIF-FMAV SECTION.
       INI-OBT-DIF-FMAV.
           MOVE OPC-NUM-DCOL IN OPC      TO FEC-DEC1.
           MOVE OPC-NUM-MCOL IN OPC      TO FEC-MEC1.
           MOVE OPC-NUM-SCOL IN OPC      TO FEC-SEC1.
           MOVE OPC-NUM-ACOL IN OPC      TO FEC-AEC1.

           MOVE DLC-NUM-DMAV IN DLC      TO FEC-DEC2.
           MOVE DLC-NUM-MMAV IN DLC      TO FEC-MEC2.
           MOVE DLC-NUM-SMAV IN DLC      TO FEC-SEC2.
           MOVE DLC-NUM-AMAV IN DLC      TO FEC-AEC2.

           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-DIF-DIA              TO FEC-CMND.
           PERFORM CAL-FEC.
           MOVE FEC-NDIA TO WSS-NUM-FMAV IN WSS-VARI.
       FIN-OBT-DIF-FMAV.
           EXIT.

      *JGM********************************************************
       CALC-SEG-SEDECO SECTION.
      *************************
       INI-CALC-SEG-SEDECO.
           PERFORM  BUS-REG-DLC-FMAV.
           MOVE 'S'    TO WSS-IND-NOCA IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-SEGD IN WSS-VARI.
           MOVE SPACES      TO MSG-COD-MENS IN MSG.
           MOVE 'COL'       TO TAB-COD-SIST FIO-SIST.
           MOVE 'SDM'       TO TAB-COD-TTAB IN TAB.
           MOVE SPACES      TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TBLSDM  NEX' TO MSG-COD-MENS
               GO TO FIN-CALC-SEG-SEDECO-D
           ELSE
               MOVE TAB-GLS-DATA IN TAB TO WSS-GLS-DAT3 IN WSS-VARI.

      * Chequea que exista la TIO en tabla SDM (Madre).
           PERFORM CHK-EXI-TIO.
           IF WSS-IND-ETIO IN WSS-VARI NOT = 'S'
               MOVE 'TIOTBLSDMNEX' TO MSG-COD-MENS
               GO TO FIN-CALC-SEG-SEDECO-D.

      * Si es moneda nacional transformar a Pesos.
           PERFORM BUS-MONEDA-NAC.

      * Si cliente es empresa, valor del seguro es cero.
      * Buscar fecha nacimiento de deudor natural.
           MOVE OPC-CAI-IOPC IN OPC  TO RDC-CAI-IOPC IN RDC.
           MOVE OPC-IIC-IOPC IN OPC  TO RDC-IIC-IOPC IN RDC.
           MOVE ZEROES                   TO WSS-NUM-IDLC IN WSS-VARI.
           MOVE WSS-NUM-IDLC IN WSS-VARI TO RDC-NUM-IDLC IN RDC.
           PERFORM VAL-ICLI.
           IF WSS-IND-NOCA IN WSS-VARI = 'N'
               GO TO FIN-CALC-SEG-SEDECO-D.

           IF MSG-COD-MENS IN MSG > SPACES
               IF MSG-COD-MENS IN MSG = 'PENACRCOEMP'
                   MOVE SPACES TO MSG-COD-MENS IN MSG
                   GO TO FIN-CALC-SEG-SEDECO-D
               ELSE
                   GO TO FIN-CALC-SEG-SEDECO-D.
      *
      *BUSCA MIN Y MAX SEDECO
           PERFORM BUS-MINMAX-SEGURO.
           IF MSG-COD-MENS IN MSG > SPACES
              GO TO FIN-CALC-SEG-SEDECO-D.

           IF WSS-AGNO-NUM-MINMAX IN WSS-VARI NOT NUMERIC
              MOVE 'SEDECONONUM' TO MSG-COD-MENS
              GO TO FIN-CALC-SEG-SEDECO-D.

      * RINALDO FUENTES/INI/06-FEB-1998.
      * VALIDA QUE LA BANCA DEL CLIENTE ESTA AFECTA A COBRO
      * SEDECO CON INF. TABLA SDG DATOS ADICIONALES POS 31 A 60.
           IF DCP-COD-TBAN IN DCP = SPACES
              GO TO FIN-CALC-SEG-SEDECO-D.

           IF DCP-COD-TBAN IN DCP = TAB-COD-DAT3 IN TAB (1)  OR
                                    TAB-COD-DAT3 IN TAB (2)  OR
                                    TAB-COD-DAT3 IN TAB (3)  OR
                                    TAB-COD-DAT3 IN TAB (4)  OR
                                    TAB-COD-DAT3 IN TAB (5)  OR
                                    TAB-COD-DAT3 IN TAB (6)  OR
                                    TAB-COD-DAT3 IN TAB (7)  OR
                                    TAB-COD-DAT3 IN TAB (8)  OR
                                    TAB-COD-DAT3 IN TAB (9)  OR
                                    TAB-COD-DAT3 IN TAB (10)
              NEXT SENTENCE
            ELSE
              GO TO FIN-CALC-SEG-SEDECO-D.

      * RINALDO FUENTES/FIN/06-FEB-1998.
      * Si cliente tiene mas de 69 aos a FCOL no cobrar seguro
           PERFORM BUS-CLIE-EDAD1.
           IF MSG-COD-MENS IN MSG > SPACES
              IF MSG-COD-MENS IN MSG = '>MINAGNO'
                 MOVE SPACES  TO MSG-COD-MENS IN MSG
                 GO TO FIN-CALC-SEG-SEDECO-D.

      * Si cliente tiene hoy mas de 75 anos no cobrar seguro
           IF DCP-FEC-FNAC IN DCP  > ZEROES
              PERFORM BUS-CLIE-EDAD2
              IF MSG-COD-MENS IN MSG > SPACES
                 IF  MSG-COD-MENS IN MSG = '>FECFCOL'
                     MOVE SPACES  TO MSG-COD-MENS IN MSG
                     GO TO FIN-CALC-SEG-SEDECO-D.

      * Calcular MIN(wss-fec-fmav y wss-fec-fec2)
           PERFORM CALC-MIN-FEC.
           PERFORM OBT-DIFE-MESES.
           IF MSG-COD-MENS IN MSG > SPACES
              MOVE SPACES  TO MSG-COD-MENS IN MSG
              GO TO FIN-CALC-SEG-SEDECO-D.
      * Busco en la tabla SDM el porcentaje a aplicar al credito, desde
      * los datos adicionales, con respecto al plazo en dias.
           MOVE 'COL'        TO TAB-COD-SIST FIO-SIST.
           MOVE 'SDM'        TO TAB-COD-TTAB IN TAB.
           MOVE WSS-NUM-DDIA TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       NXT-SGT-TMO-D.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS AND
               TAB-COD-TTAB IN TAB = 'SDM'
                NEXT SENTENCE
           ELSE
               IF FIO-CMND = FIO-GET-NLS
                   MOVE 'TRAMOSDM NEX'      TO MSG-COD-MENS
                   GO TO FIN-CALC-SEG-SEDECO-D
               ELSE
                   MOVE 'TMOSDMNVIG' TO MSG-COD-MENS
                   GO TO FIN-CALC-SEG-SEDECO-D.

           IF TAB-IND-VIGE IN TAB NOT = 'S'
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO NXT-SGT-TMO-D.

           MOVE TAB-GLS-DATA IN TAB TO WSS-GLS-DATA IN WSS-VARI.

           IF WSS-VAL-CLIE IN WSS-VARI NOT NUMERIC
               MOVE 'PRCNTJSDMNEX' TO MSG-COD-MENS IN MSG
               GO TO FIN-CALC-SEG-SEDECO-D.

      * Multiplico el saldo de credito por el porcentaje asociado al
      * credito y lo dejo en OPC-VAL-SEGD IN OPC.
           MULTIPLY WSS-VAL-SCRE IN WSS-VARI BY WSS-VAL-CLIE IN WSS-VARI
                                GIVING WSS-VAL-SEGD IN WSS-VARI ROUNDED.

           MOVE WSS-VAL-SEGD IN WSS-VARI TO PES-SGV-DECI IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO WSS-VAL-SEGD IN WSS-VARI.
           IF WSS-VAL-SEGD IN WSS-VARI NOT < WSS-VAL-SCRE IN WSS-VARI
               MOVE 'MNTSEG>MNTCR' TO MSG-COD-MENS
               GO TO FIN-CALC-SEG-SEDECO-D.
       FIN-CALC-SEG-SEDECO-D.
           EXIT.
      *
      ******************************************************************
       BUS-REG-DLC-FMAV SECTION.
       INI-BUS-REG-DLC-FMAV.
           MOVE OPC-CAI-IOPC IN OPC TO DLC-CAI-IOPC IN DLC.
           MOVE OPC-IIC-IOPC IN OPC TO DLC-IIC-IOPC IN DLC.
           MOVE ZEROES   TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO  FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF FIO-STAT-OKS AND
              OPC-CAI-IOPC IN OPC = DLC-CAI-IOPC IN DLC AND
              OPC-IIC-IOPC IN OPC = DLC-IIC-IOPC IN DLC
               MOVE  DLC-FEC-FMAV TO WSS-FEC-FMAV IN WSS-VARI.
       FIN-BUS-REG-DLC-FMAV.
           EXIT.
       BUS-MONEDA-NAC SECTION.
       INI-BUS-MONEDA-NAC.
      *Si es moneda Nacional transformar a Pesos
           MOVE 'TVAL' TO MSC-CIC-TMSC IN MSC.
           MOVE '0'    TO MSC-CIC-CMSC IN MSC.
           MOVE 'TAB' TO FIO-SIST.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO MSC-COD-CMSC IN MSC.

           IF OPC-MSC-TVOC IN OPC NOT = MSC-COD-CMSC IN MSC
               MOVE ZEROES TO WSS-VAL-SCRE IN WSS-VARI
               GO TO  BUS-MONEDA-NACS.

           MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB
           MOVE '0999  '                TO TAB-CIC-CTAB IN TAB
           MOVE OPC-NUM-DCOL IN OPC TO TAB-NUM-DTRN IN TAB
           MOVE OPC-NUM-MCOL IN OPC TO TAB-NUM-MTRN IN TAB
           MOVE OPC-NUM-SCOL IN OPC TO TAB-NUM-STRN IN TAB
           MOVE OPC-NUM-ACOL IN OPC TO TAB-NUM-ATRN IN TAB
           PERFORM GET-VCAM
           IF MSG-COD-MENS IN MSG NOT > SPACES
               MULTIPLY CAM-SGV-VCAM IN CAM BY
                        OPC-VAL-CRED IN OPC GIVING
                        WSS-VAL-SCRE IN WSS-VARI
           ELSE
               IF MSG-COD-MENS IN MSG = 'CODVCAMEQL'
                  MOVE 1      TO CAM-SGV-VCAM IN CAM
                  MOVE SPACES TO MSG-COD-MENS IN MSG
                  MOVE OPC-VAL-CRED IN OPC TO WSS-VAL-SCRE
                                                        IN WSS-VARI.

       BUS-MONEDA-NACS.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE OPC-NUM-DCOL IN OPC TO WSS-NUM-DCOL IN WSS-VARI.
           MOVE OPC-NUM-MCOL IN OPC TO WSS-NUM-MCOL IN WSS-VARI.
           MOVE OPC-NUM-SCOL IN OPC TO WSS-NUM-SCOL IN WSS-VARI.
           MOVE OPC-NUM-ACOL IN OPC TO WSS-NUM-ACOL IN WSS-VARI.
       FIN-BUS-MONEDA-NAC.
           EXIT.

       BUS-MINMAX-SEGURO SECTION.
       INI-BUS-MINMAX-SEGURO.
           MOVE 'COL'          TO TAB-COD-SIST  FIO-SIST.
           MOVE 'SDG'          TO TAB-COD-TTAB IN TAB.
           MOVE 'SEDECO'       TO TAB-COD-CTAB IN TAB.

           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'CMODSEDECONEX' TO MSG-COD-MENS
               GO TO FIN-BUS-MINMAX-SEGURO.

           MOVE TAB-GLS-DAT2 IN TAB TO
                                WSS-GLS-DAT2-MINMAX IN WSS-VARI.
       FIN-BUS-MINMAX-SEGURO.
           EXIT.

       BUS-CLIE-EDAD1 SECTION.
       INI-BUS-CLIE-EDAD1.
      * Si cliente tiene mas de 69 aos a FCOL no cobrar seguro
           MOVE 'SPACES' TO MSG-COD-MENS IN MSG.
           IF DCP-FEC-FNAC IN DCP NOT > ZEROES
               MOVE '>MINAGNO' TO MSG-COD-MENS IN MSG
               GO TO FIN-BUS-CLIE-EDAD1.

            MOVE DCP-NUM-DNAC IN DCP      TO FEC-DEC1.
            MOVE DCP-NUM-MNAC IN DCP      TO FEC-MEC1.
            MOVE DCP-NUM-SNAC IN DCP      TO FEC-SEC1.
            MOVE DCP-NUM-ANAC IN DCP      TO FEC-AEC1.

            MOVE WSS-NUM-DCOL IN WSS-VARI TO FEC-DEC2.
            MOVE WSS-NUM-MCOL IN WSS-VARI TO FEC-MEC2.
            MOVE WSS-NUM-SCOL IN WSS-VARI TO FEC-SEC2.
            MOVE WSS-NUM-ACOL IN WSS-VARI TO FEC-AEC2.

            MOVE FEC-DIF-ANO              TO FEC-CMND.
            MOVE FEC-FORM-FEC             TO FEC-FORM.
            PERFORM CAL-FEC.
            IF NOT FEC-STAT-OKS
               MOVE 'FNAC>FCOL' TO MSG-COD-MENS IN MSG
               GO TO FIN-BUS-CLIE-EDAD1.
      *

            MOVE FEC-NANO TO WSS-NUM-NANO IN WSS-VARI
            IF WSS-NUM-NANO IN WSS-VARI > WSS-MIN-AGNO IN WSS-VARI
               MOVE '>MINAGNO' TO MSG-COD-MENS IN MSG
               GO TO FIN-BUS-CLIE-EDAD1.
       FIN-BUS-CLIE-EDAD1.
           EXIT.

       BUS-CLIE-EDAD2 SECTION.
       INI-BUS-CLIE-EDAD2.
      * Si cliente tiene hoy mas de 75 anos no cobrar seguro
           MOVE SPACES TO MSG-COD-MENS IN MSG.
           IF DCP-FEC-FNAC IN DCP NOT > ZEROES
              MOVE WSS-FEC-FMAV IN WSS-VARI TO WSS-FMIN IN WSS-VARI
              GO TO FIN-BUS-CLIE-EDAD2.

      *
            MOVE WSS-MAX-AGNO   TO FEC-NANO
            MOVE DCP-NUM-DNAC IN DCP TO FEC-ITM1
            MOVE DCP-NUM-MNAC IN DCP TO FEC-ITM2
            MOVE DCP-NUM-SNAC IN DCP TO FEC-ITM3
            MOVE DCP-NUM-ANAC IN DCP TO FEC-ITM4
            MOVE FEC-SUM-ANO  TO FEC-CMND
            PERFORM CAL-FEC
            IF NOT FEC-STAT-OKS
               MOVE FEC-STAT TO MSG-COD-MENS IN MSG
               GO TO FIN-BUS-CLIE-EDAD2.

            MOVE FEC-ITM1 TO WSS-NUM-DEC2 IN WSS-VARI
            MOVE FEC-ITM2 TO WSS-NUM-MEC2 IN WSS-VARI
            MOVE FEC-ITM3 TO WSS-NUM-SEC2 IN WSS-VARI
            MOVE FEC-ITM4 TO WSS-NUM-AEC2 IN WSS-VARI

            IF WSS-FEC-FEC2 IN WSS-VARI NOT > WSS-FEC-FCOL IN WSS-VARI
               MOVE '>FECFCOL' TO MSG-COD-MENS IN MSG.
       FIN-BUS-CLIE-EDAD2.
           EXIT.


      * Obtener diferencia de meses entre FMIN y OPC-FEC-FCOL IN OPC-FLD
       OBT-DIFE-MESES SECTION.
       INI-OBT-DIFE-MESES.
           MOVE SPACES TO MSG-COD-MENS IN MSG.
      *
           IF WSS-FMIN IN WSS-VARI NOT > WSS-FEC-FCOL IN WSS-VARI
              IF WSS-FMIN IN WSS-VARI = WSS-FEC-FCOL IN WSS-VARI
                 MOVE'FMIN=FCOL' TO MSG-COD-MENS IN MSG
                 GO TO FIN-OBT-DIFE-MESES
               ELSE
                 GO TO FIN-OBT-DIFE-MESES.
      *
            MOVE FEC-DIF-DIA              TO FEC-CMND.

            MOVE WSS-NUM-DCOL IN WSS-VARI TO FEC-DEC1.
            MOVE WSS-NUM-MCOL IN WSS-VARI TO FEC-MEC1.
            MOVE WSS-NUM-SCOL IN WSS-VARI TO FEC-SEC1.
            MOVE WSS-NUM-ACOL IN WSS-VARI TO FEC-AEC1.

            MOVE WSS-DMIN IN WSS-VARI     TO FEC-DEC2.
            MOVE WSS-MMIN IN WSS-VARI     TO FEC-MEC2.
            MOVE WSS-SMIN IN WSS-VARI     TO FEC-SEC2.
            MOVE WSS-AMIN IN WSS-VARI     TO FEC-AEC2.

            MOVE FEC-FORM-FEC             TO FEC-FORM.
            PERFORM CAL-FEC.
            IF FEC-NDIA NOT < ZEROES
               MOVE FEC-NDIA  TO WSS-NUM-DDIA IN WSS-VARI.
       FIN-OBT-DIFE-MESES.
           EXIT.

       CAL-FINAL-SEGD SECTION.
       INI-CAL-FINAL-SEGD.
           IF (DCP-COD-TBAN IN DCP = 'N' OR
               DCP-COD-TBAN IN DCP = WSS-COD-TCLI IN WSS-VARI)
               MULTIPLY WSS-VAL-SCRE IN WSS-VARI BY
                        WSS-VAL-NCLI IN WSS-VARI GIVING
                        WSS-VAL-SEGD IN WSS-VARI ROUNDED
           ELSE
               MULTIPLY WSS-VAL-SCRE IN WSS-VARI BY
                        WSS-VAL-CLIE IN WSS-VARI GIVING
                        WSS-VAL-SEGD IN WSS-VARI ROUNDED.

           MOVE WSS-VAL-SEGD IN WSS-VARI TO PES-SGV-DECI
                                             IN PES-VARI.
           PERFORM PES-SCTV.
           MOVE PES-SGV-ENTE IN PES-VARI TO
                                WSS-VAL-SEGD IN WSS-VARI.
       FIN-CAL-FINAL-SEGD.
           EXIT.
       CALC-MIN-FEC SECTION.
       INI-CALC-MIN-FEC.
           IF WSS-FEC-FMAV IN WSS-VARI < WSS-FEC-FEC2 IN WSS-VARI
             MOVE WSS-FEC-FMAV IN WSS-VARI TO WSS-FMIN IN WSS-VARI
           ELSE
             MOVE WSS-FEC-FEC2 IN WSS-VARI TO WSS-FMIN IN WSS-VARI.
       FIN-CALC-MIN-FEC.
           EXIT.
       CHK-EXI-TIO SECTION.
      **********************
       INI-CHK-EXI-TIO.
           MOVE 1 TO MOV-I.
       LUP-CHK-EXI-TIO.
           IF OPC-COD-TOOC IN OPC = WSS-TTIO (MOV-I)
               MOVE 'S' TO WSS-IND-ETIO IN WSS-VARI
           ELSE
               IF MOV-I NOT > 20
                   ADD 1 TO MOV-I
                   GO TO LUP-CHK-EXI-TIO
               ELSE
                   MOVE 'TIOTBLSDMNEX' TO MSG-COD-MENS IN MSG
                   MOVE 'N' TO WSS-IND-ETIO IN WSS-VARI.
       FIN-CHK-EXI-TIO.
           EXIT.

       BUS-REG-RDC SECTION.
       INI-BUS-REG-RDC.
           MOVE SPACES  TO MSG-COD-MENS IN MSG.
           MOVE OPC-CAI-IOPC IN OPC  TO RDC-CAI-IOPC IN RDC.
           MOVE OPC-IIC-IOPC IN OPC  TO RDC-IIC-IOPC IN RDC.
           MOVE WSS-NUM-IDLC IN WSS-VARI TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS OR
             RDC-CAI-IOPC IN RDC NOT = OPC-CAI-IOPC IN OPC OR
             RDC-IIC-IOPC IN RDC NOT = OPC-IIC-IOPC IN OPC OR
             RDC-NUM-IDLC IN RDC NOT = WSS-NUM-IDLC IN WSS-VARI
               MOVE 'OPC    NORDC' TO MSG-COD-MENS IN MSG.
       FIN-BUS-REG-RDC.
           EXIT.

       VAL-ICLI SECTION.
      *****************
       INI-VAL-ICLI.
      * Busco fecha de nacimiento del deudor directo o indirecto.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF FIO-STAT-OKS AND
             RDC-CAI-IOPC IN RDC = OPC-CAI-IOPC IN OPC AND
             RDC-IIC-IOPC IN RDC = OPC-IIC-IOPC IN OPC AND
             RDC-NUM-IDLC IN RDC = WSS-NUM-IDLC IN WSS-VARI
               MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM SGC-FIO-DBC
               IF FIO-STAT-OKS
      * Si RUT del cliente es > 50.000.0000 (empresa) no cobrar seguro.
                   IF DBC-NUM-ICLI IN DBC < WSS-NUM-ICLI IN WSS-VARI
                      AND DBC-IND-TICL IN DBC = 'P'
                       MOVE DBC-CIC-ICLI IN DBC TO DCP-CIC-ICLI IN DCP
                       MOVE FIO-GET-KEY TO FIO-CMND
                       PERFORM SGC-FIO-DCP
                       IF FIO-STAT-OKS
                           NEXT SENTENCE
                       ELSE
                           MOVE 'PENACRCOEMP' TO MSG-COD-MENS
                           GO TO FIN-VAL-ICLI
                   ELSE
                       MOVE 'N' TO WSS-IND-NOCA IN WSS-VARI
                       GO TO FIN-VAL-ICLI
               ELSE
                   MOVE 'OPC    NODBC' TO MSG-COD-MENS IN MSG
                   GO TO FIN-VAL-ICLI
           ELSE
               IF WSS-NUM-IDLC IN WSS-VARI = ZEROES
                   MOVE 'OPC    NORDC' TO MSG-COD-MENS IN MSG
               ELSE
                   MOVE 'N' TO WSS-IND-NOCA IN WSS-VARI.
       FIN-VAL-ICLI.
           EXIT.

       GRABA-REG-SEC SECTION.
       INI-GRABA-REG-SEC.
            MOVE FIO-PUT TO FIO-CMND.
            PERFORM COL-FIO-SEC.
       FIN-GRABA-REG-SEC.
           EXIT.

      *>>>>
