       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLCRD.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    28-Apr-94 12:44 PM.

      * Programa Listador de Reporte CRD con Sort.


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTCRD         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTCRD         ASSIGN TO        UT-S-RPTCRD.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTCRD         ASSIGN TO        UT-S-RPTCRD.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRCRD.
       FD  RPTCRD LABEL RECORDS OMITTED
           REPORT IS RPT-CRD
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBROPC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWCRD.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY COLBRTOC.
       COPY COLBRDLC.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY SGCBRRCD.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       01  WSS-VARI.
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO                     PIC 9(02).
              05 WSS-NUM-MPRO                     PIC 9(02).
              05 WSS-NUM-SPRO                     PIC 9(02).
              05 WSS-NUM-APRO                     PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                     PIC 9(02).
              05 WSS-NUM-AFEC                     PIC 9(02).
              05 WSS-NUM-MFEC                     PIC 9(02).
              05 WSS-NUM-DFEC                     PIC 9(02).
           03 WSS-COD-OFOC                        PIC X(03).
           03 WSS-IND-ICLI                        PIC 9(01).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-COD-PLZA.
              05 WSS-IND-OFIC                     PIC X(01).
              05 WSS-COD-OFCB                     PIC X(03).
           03 WSS-COD-OFIS.
              05 WSS-COD-OFI1                     PIC X(03).
              05 WSS-COD-OFI2                     PIC X(03).
           03  WSS-COD-PESO                       PIC X(06).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCD-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY COLRFCRD.
           .
       COPY COLRTCRD.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-CRD-SRT-COD-OFIS SECTION.
           USE BEFORE REPORTING CF-CRD-SRT-COD-OFIS.
       INI-CF-CRD-SRT-COD-OFIS.
      *<<<< CF_CRD_SRT_COD_OFIS
           MOVE ZEROES TO PAGE-COUNTER.
      *>>>>
       FIN-CF-CRD-SRT-COD-OFIS.
           EXIT.
       DEC-PH-CRD SECTION.
           USE BEFORE REPORTING PH-CRD.
       INI-PH-CRD.
      *<<<< PH_CRD
           MOVE SRT-COD-OFIS IN SRT TO WSS-COD-OFIS IN WSS-VARI.
      * Mediante BUS-OFI se Obtine Nombre de la Oficina Operacion de Tab
           IF WSS-COD-OFI1 IN WSS-VARI > SPACES
               MOVE WSS-COD-OFI1 IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFOC IN WSS-CRD
               IF WSS-COD-OFI1 IN WSS-VARI = '090'
                   MOVE 'CREDITOS'    TO WSS-GLS-JOOC IN WSS-CRD
               ELSE
                   MOVE 'OPERACIONES' TO WSS-GLS-JOOC IN WSS-CRD.

      * Mediante BUS-OFI se Obtine Nombre de la Oficina Cobro de Tabla O
           IF WSS-COD-OFI2 IN WSS-VARI > SPACES
               MOVE WSS-COD-OFI2 IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFCB IN WSS-CRD
               IF WSS-COD-OFI2 IN WSS-VARI = '090'
                   MOVE 'CREDITOS'    TO WSS-GLS-JOCB IN WSS-CRD
               ELSE
                   MOVE 'OPERACIONES' TO WSS-GLS-JOCB IN WSS-CRD.
      *>>>>
       FIN-PH-CRD.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLCRD' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           SORT GNSSRT
       COPY COLRSCRD.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      * Captura Fecha de Proceso
           DISPLAY 'FECHA DE PROCESO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFOC IN WSS-VARI.
           DISPLAY WSS-COD-OFOC IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DPRO IN WSS-CRD
                                            WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MPRO IN WSS-CRD
                                            WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SPRO IN WSS-CRD
                                            WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-APRO IN WSS-CRD
                                            WSS-NUM-AFEC IN WSS-VARI.

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFOC IN WSS-VARI
               MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-END.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       FST-INP-SRT.
      *<<<< INI_FST_INP
      *DAD ini  29-Nov-1993   (Memo 23-Nov-1993 Pto.1)
           MOVE WSS-FEC-FFEC IN WSS-VARI TO OPC-FEC-FINP IN OPC.
           MOVE 'OPC-FEC-FINP' TO FIO-AKEY.
      *DAD fin  29-Nov-1993
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY REGISTROS PARA FECHA DE PROCESO : '
                       WSS-FEC-FPRO IN WSS-VARI
               GO TO FIN-INP-SRT.
           GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro OPC
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.

      * Busca los Documentos de Credito para Operacion Determinada
           MOVE SPACES              TO DLC-KEY-IDLC IN DLC.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE FIO-GET-NLS  TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS OR
              DLC-CIC-IOPC IN DLC NOT = OPC-CIC-IOPC IN OPC
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    DLCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   OPC-KEY-IOPC IN OPC
               GO TO NXT-INP-SRT.
       LUP-BUS-OPC-DLC.
           PERFORM BUS-SRT-REG.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS OR
              DLC-CIC-IOPC IN DLC NOT = OPC-CIC-IOPC IN OPC
               GO TO NXT-INP-SRT.
           GO TO LUP-BUS-OPC-DLC.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMCRD.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
      *DAD ini  29-Nov-1993   ( Memo 23-Nov-1993 Pto.1 )
           MOVE 'OPC-FEC-FINP' TO FIO-AKEY.
      *DAD fin  29-Nov-1993
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *<<<< FIN_NXT_INP
      *DAD ini  29-Nov-1993   ( Memo 23-Nov-1993 Pto.1 )
           IF OPC-FEC-FINP IN OPC NOT = WSS-FEC-FFEC IN WSS-VARI
      *DAD fin  29-Nov-1993
               GO TO FIN-INP-SRT.
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTCRD.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-CRD.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
           PERFORM GEN-LIN-OUT.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-CRD.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-CRD.
           CLOSE RPTCRD.
       EXT-OUT-SRT.
           EXIT.
       COPY COLBFOPC.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      ******************************************************************
      * Seleccion del Registro para Proceso Mediante Fecha y Oficina del
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO FIN-SEL-REG-PRO.

           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFOC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   GO TO FIN-SEL-REG-PRO.

      * Mediante el Registro TOC Tipo Operaciones Creditos Obtiene 'Como
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           IF NOT FIO-STAT-OKS
               MOVE 'GNS'        TO MSG-COD-SIST
               MOVE 'COD    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' : ' TOC-COD-TABL
               GO TO FIN-SEL-REG-PRO.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro SRT al Procesar Varios Regis
       BUS-SRT-REG SECTION.
       INI-BUS-SRT-REG.
      * Complementa Formacion del Registro SRT
           MOVE DLC-COD-PLZA IN DLC      TO WSS-COD-PLZA IN WSS-VARI.
           IF WSS-IND-OFIC IN WSS-VARI NOT = 'O'
               GO TO FIN-BUS-SRT-REG.

           MOVE OPC-COD-OFOC IN OPC      TO WSS-COD-OFI1 IN WSS-VARI.
           MOVE WSS-COD-OFCB IN WSS-VARI TO WSS-COD-OFI2 IN WSS-VARI.
           MOVE WSS-COD-OFIS IN WSS-VARI TO SRT-COD-OFIS IN SRT.

           MOVE  1  TO SRT-NUM-DOCU IN SRT.

           IF OPC-COD-VCOC IN OPC NOT = WSS-COD-PESO IN WSS-VARI
               PERFORM CON-VAL-PESOS
               MULTIPLY CAM-SGV-VCAM IN CAM BY DLC-VAL-NDLC IN DLC
                                 GIVING SRT-VAL-NDLC IN SRT ROUNDED
           ELSE
               MOVE DLC-VAL-NDLC IN DLC TO SRT-VAL-NDLC IN SRT.

      * Completa Llenado de Campos y Genera el Registro SRT
           COPY COLRMCRD.
           RELEASE SRT.
       FIN-BUS-SRT-REG.
           EXIT.
      ******************************************************************
      * Busca Valor de Cambio para Operaciones de Credito en Tabla CAM
       CON-VAL-PESOS SECTION.
       INI-CON-VAL-PESOS.
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CAM-FEC-FCAM IN CAM(1).
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CAM-FEC-FCAM IN CAM(2).
           MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A ESTA FECHA : '
                        WSS-FEC-FPRO ' PARA : ' OPC-COD-VCOC IN OPC
               PERFORM GNS-PRO-END.
           IF CAM-IND-VIGE NOT = 'S'
               DISPLAY 'INDICADOR DE VIGENCIA NO VALIDO A FECHA : '
                        WSS-FEC-FPRO ' PARA : ' OPC-COD-VCOC IN OPC
               PERFORM GNS-PRO-END.
       FIN-CON-VAL-PESOS.
           EXIT.
      ******************************************************************
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
       GEN-LIN-OUT SECTION.
       INI-GEN-LIN-OUT.
      * Mediante BUS-TAB Busca Tipo de Moneda de Tabla VLR
           IF OPC-COD-VCOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-VCOC IN WSS-CRD
           ELSE
               MOVE SPACES TO WSS-GLS-VCOC IN WSS-CRD.

           IF TOC-MSC-OPEC IN SRT = 'D'
               MOVE 'IND'               TO WSS-GLS-DDEU IN WSS-CRD
               MOVE 'ACEPTANTE'         TO WSS-GLS-DEUD IN WSS-CRD
               MOVE DLC-NUM-IDLC IN SRT TO RDC-NUM-IDLC IN RDC
           ELSE
               MOVE 'DIR'       TO WSS-GLS-DDEU IN WSS-CRD
               MOVE 'D DIRECTO' TO WSS-GLS-DEUD IN WSS-CRD
               MOVE ZEROES      TO RDC-NUM-IDLC IN RDC.

      *DAD ini  27-ABR-1994   (Memo 25-ABR-1994 Pto.2)
           MOVE SPACES TO DBC-NUM-ICLI IN DBC
                          DBC-VRF-ICLI IN DBC
                          WSS-GLS-NOMC IN WSS-CRD
                          RCD-GLS-DIRE IN RCD.
      *DAD fin  27-ABR-1994

      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro
      * Busca CIC del Cliente con el CIC de Operaciones de Creditos en R
      * RDC-NUM-IDLC > 0 ====> DEU.IND.
      * RDC-NUM-IDLC = 0 ====> DEU.DIR.
           MOVE OPC-CAI-IOPC IN SRT TO RDC-CAI-IOPC IN RDC.
           MOVE OPC-IIC-IOPC IN SRT TO RDC-IIC-IOPC IN RDC.
      *    MOVE                     TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    RDCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RDC-KEY-IDLC IN RDC
               GO TO FIN-GEN-LIN-OUT.

      *CIC-CLI Busca Datos del Cliente Mediante su CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS NOT > SPACES
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO WSS-GLS-NOMC IN WSS-CRD
           ELSE
               MOVE SPACES TO WSS-GLS-NOMC IN WSS-CRD
      *DAD ini  27-ABR-1994   (Memo 25-ABR-1994 Pto.2)
                              DBC-NUM-ICLI IN DBC
                              DBC-VRF-ICLI IN DBC.
      *DAD fin  27-ABR-1994

      * Busca Direccion Cliente en RCD con datos del Cliente Operacion e
           MOVE RDC-CIC-ICLI IN RDC TO RCD-CIC-ICLI IN RCD.
           MOVE RDC-NUM-DIRE IN RDC TO RCD-NUM-DIRE IN RCD.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-RCD.
           IF FIO-STAT-OKS
               MOVE RCD-GLS-DIRE IN RCD TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO RCD-GLS-DIRE IN RCD
           ELSE
               MOVE SPACES TO RCD-GLS-DIRE IN RCD
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'RDC    RCDNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RCD-KEY-IRCD IN RCD.
       FIN-GEN-LIN-OUT.
           EXIT.
      ******************************************************************

       COPY TABBFOFI.
       COPY TABBFCAM.
       COPY COLBFTOC.
       COPY COLBFDLC.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY SGCBFRCD.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY SGCBGCLI.
      *>>>>
