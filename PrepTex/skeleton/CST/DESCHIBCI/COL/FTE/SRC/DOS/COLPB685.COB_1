       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     COLPB685.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  23/10/91.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Colocaciones                                       *
      *  Subsist. :                                                    *
      *  Analista : Carrasco Patricio                                  *
      *  Objetivo : Emitir listado de movimientos de cancelaciones     *
      *             efectuadas por safe y no actualizadas en colocac.  *
      *  Archivos :                                                    *
      *   Entrada : CAN 160: Cancelaciones                             *
      *             OFI 150: Tabla oficinas                            *
      *             COLVJRN: Journal diario de colocaciones            *
      *                                                                *
      *   SALIDA  :                                                    *
      *                                                                *
      *   ENT/SAL :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : ...........................            *
      *                                                                *
      ******************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *======================
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
           EJECT
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT COLVJRN ASSIGN TO AS-COLVJRN
                  FILE STATUS IS WR-FS-COLVJRN.
           SELECT COLVSOR ASSIGN TO SYS010-DA-3340-S-SYSWK1.
           SELECT COLL685 ASSIGN TO UT-S-COLL685.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  COLL685
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD  STANDARD.
       01  COLR685                         PIC X(81).
       SKIP1
       SD  COLVSOR.
       01  COLRSOR.
           05 SOR-SUC-DES                  PIC 9(03).
           05 SOR-SUC-ORI                  PIC 9(03).
           05 SOR-NUM-OPE                  PIC X(12).
           05 SOR-SGV-TPAG                 PIC S9(11) COMP-3.
       SKIP1
       FD  COLVJRN
           LABEL RECORD  STANDARD.
       COPY COLBRJRA.
       SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-INDICES.
           05 WI-I                   PIC 9(03) VALUE 0.
       01  W-SWITCHES.
           05 WS-PROCESO             PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-NO-OK    VALUE 1.
           SKIP1
       01  W-TABLAS.
           05 WT-GLS-OFI             OCCURS 300 TIMES PIC X(30).
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-CAN               PIC 9(06) VALUE 0.
           05 WA-R-CAN               PIC 9(06) VALUE 0.
           05 WA-G-CAN               PIC 9(06) VALUE 0.
           05 WA-L-JRN               PIC 9(06) VALUE 0.
           05 WA-R-JRN               PIC 9(06) VALUE 0.
           05 WA-G-JRN               PIC 9(06) VALUE 0.
           05 WA-L-OFI               PIC 9(06) VALUE 0.
           05 WA-R-OFI               PIC 9(06) VALUE 0.
           05 WA-G-OFI               PIC 9(06) VALUE 0.
           05 WA-L-SOR               PIC 9(06) VALUE 0.
           05 WA-R-SOR               PIC 9(06) VALUE 0.
           05 WA-G-SOR               PIC 9(06) VALUE 0.
           05 WA-PAGINA              PIC 9(06) VALUE 0.
           05 WA-LINEA               PIC 9(03).
           05 WA-TOT-ORI             PIC 9(11).
           05 WA-TOT-DES             PIC 9(11).
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA           PIC X(02).
              10 WR-HOY-MM           PIC X(02).
              10 WR-HOY-DD           PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION          PIC X(15).
              10 WR-PARRAFO          PIC X(15).
              10 WR-ARCHIVO          PIC X(10).
              10 WR-OPERACI          PIC X(10).
              10 WR-FILE-ST          PIC X(02).
              10 WR-MENSAJE          PIC X(35).
           05 WR-FS-COLVJRN          PIC 9(02).
           05 WR-FS-COLVSOR          PIC 9(02)  VALUE 0.
           05 WR-SUC-COMP            PIC 9(04)   COMP-4.
           05 FILLER REDEFINES WR-SUC-COMP.
              10 FILLER              PIC X(01).
              10 WR-SUC-BIN          PIC X(01).
           05 WR-SUC-VAR             PIC 9(03).
           05 WR-SUC-ORI             PIC 9(03).
           05 WR-SUC-DES             PIC 9(03).
           05 WR-NUM-OPE.
              10 FILLER              PIC X(01) VALUE 'D'.
              10 WR-NUM-OPE-N        PIC 9(11).
           05 WR-VAR-COMP            PIC S9(12)V9 COMP-3.
           05 WR-VAR-BCD REDEFINES WR-VAR-COMP.
              10 WR-VAR-BCD-6        PIC X(06).
              10 FILLER              PIC X(01).
           05 WR-FEC-PAG-COMP        PIC S9(06)V9 COMP-3.
           05 FILLER REDEFINES WR-FEC-PAG-COMP.
              10 WR-FEC-PAG-BCD      PIC X(03).
              10 FILLER              PIC X(01).
           05 WR-FEC-PAG             PIC 9(06).
           05 FILLER REDEFINES WR-FEC-PAG.
              10 WR-DIA-PAG          PIC 9(02).
              10 WR-MES-PAG          PIC 9(02).
              10 WR-ANO-PAG          PIC 9(02).
           05 WR-FEC-PAG-AMD.
              10 WR-SIG-PAG-AMD      PIC 9(02).
              10 WR-ANO-PAG-AMD      PIC 9(02).
              10 WR-MES-PAG-AMD      PIC 9(02).
              10 WR-DIA-PAG-AMD      PIC 9(02).
           05 WR-LINEA00          PIC X(002) VALUE '  '.
           05 WR-LINEA01.
              10 FILLER           PIC X(001) VALUE '1'.
              10 FILLER           PIC X(018) VALUE 'BANCO DE CREDITO E'.
              10 FILLER           PIC X(046) VALUE ' INV.'.
              10 FILLER           PIC X(008) VALUE 'FECHA :'.
              10 WR-LA01-DIA      PIC X(002).
              10 FILLER           PIC X(001) VALUE ALL '/'.
              10 WR-LA01-MES      PIC X(002).
              10 FILLER           PIC X(001) VALUE ALL '/'.
              10 WR-LA01-ANO      PIC X(002).
           05 WR-LINEA02.
              10 FILLER           PIC X(001) VALUE ' '.
              10 FILLER           PIC X(012) VALUE 'COLOCACIONES'.
              10 FILLER           PIC X(018) VALUE '             CANCE'.
              10 FILLER           PIC X(018) VALUE 'LACIONES EFECTUADA'.
              10 FILLER           PIC X(018) VALUE 'S POR SAFE     PAG'.
              10 FILLER           PIC X(008) VALUE 'INA :'.
              10 WR-LA02-PAG      PIC ZZ.ZZ9.
           05 WR-LINEA03.
              10 FILLER           PIC X(001) VALUE ' '.
              10 FILLER           PIC X(026) VALUE ALL ' '.
              10 FILLER           PIC X(018) VALUE 'NO ACTUALIZADAS EN'.
              10 FILLER           PIC X(013) VALUE ' COLOCACIONES'.
           05 WR-LINEA04.
              10 FILLER           PIC X(001) VALUE ' '.
              10 FILLER           PIC X(029) VALUE ALL ' '.
              10 FILLER           PIC X(016) VALUE 'FECHA DE PAGO :'.
              10 WR-LA04-DIA      PIC X(002).
              10 FILLER           PIC X(001) VALUE ALL '/'.
              10 WR-LA04-MES      PIC X(002).
              10 FILLER           PIC X(001) VALUE ALL '/'.
              10 WR-LA04-SIG      PIC X(002).
              10 WR-LA04-ANO      PIC X(002).
           05 WR-LINEA05.
              10 FILLER           PIC X(001) VALUE '0'.
              10 FILLER           PIC X(018) VALUE 'OFICINA ORIGEN CRE'.
              10 FILLER           PIC X(007) VALUE 'DITO :'.
              10 WR-LA05-OFI-DES  PIC X(003).
              10 FILLER           PIC X(001) VALUE ALL ' '.
              10 WR-LA05-GLS-OFI  PIC X(030).
           05 WR-LINEA06.
              10 FILLER           PIC X(001) VALUE ' '.
              10 FILLER           PIC X(080) VALUE ALL '-'.
           05 WR-LINEA07.
              10 FILLER           PIC X(001) VALUE ' '.
              10 FILLER           PIC X(018) VALUE 'OFICINA CANCELACIO'.
              10 FILLER           PIC X(001) VALUE ALL 'N'.
              10 FILLER           PIC X(022) VALUE ALL ' '.
              10 FILLER           PIC X(009) VALUE 'OPERACION'.
              10 FILLER           PIC X(018) VALUE '                MO'.
              10 FILLER           PIC X(007) VALUE 'NTO ($)'.
           05 WR-LINEA08.
              10 FILLER           PIC X(001) VALUE ' '.
              10 WR-LA08-OFI-ORI  PIC X(003).
              10 FILLER           PIC X(001) VALUE ALL ' '.
              10 WR-LA08-GLS-OFI  PIC X(030).
              10 FILLER           PIC X(005) VALUE ALL ' '.
              10 WR-LA08-NUM-OPE  PIC X(012).
              10 FILLER           PIC X(008) VALUE ALL ' '.
              10 WR-LA08-MTO-CAN  PIC ZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
           05 WR-LINEA09.
              10 FILLER           PIC X(001) VALUE ' '.
              10 FILLER           PIC X(059) VALUE ALL ' '.
              10 FILLER           PIC X(019) VALUE ALL '-'.
           05 WR-LINEA10.
              10 FILLER           PIC X(001) VALUE ' '.
              10 FILLER           PIC X(035) VALUE ALL ' '.
              10 FILLER           PIC X(014) VALUE 'TOTAL OFICINA '.
              10 WR-LA10-GLS-OFI  PIC X(010).
              10 WR-LA10-TOT-OFI  PIC ZZZ.ZZZ.ZZZ.ZZZ.ZZ9.
           05 WR-LINEA11.
              10 FILLER          PIC X(001) VALUE HIGH-VALUE.
              10 WR-LA11-LIS     PIC X(004) VALUE '0054'.
              10 WR-LA11-OFI     PIC 9(004).
              10 WR-LA11-LIN     PIC X(004) VALUE '0066'.
              10 WR-LA11-CTL     PIC X(002) VALUE '@@'.
              10 WR-LA11-PAG     PIC X(003) VALUE '001'.
              10 FILLER          PIC X(063) VALUE 'REGISTRO DE CONTROL'.
           SKIP1
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  WB-UIB.
           05 FILLER                 PIC X(32) VALUE 'COLPB685'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  WB-REQ-CAN-AREA.
           02 WB-REQ-CAN-CMD          PIC X(5) VALUE 'REDKL'.
           02 FILLER                  PIC X(3) VALUE 'CAN'.
           02 FILLER                  PIC X(5) VALUE 'CAN00'.
           02 WB-REQ-CAN-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +160.
           02 FILLER                  PIC X(58).
           02 WB-REQ-CAN-KEY-VALUE.
              03 WB-REQ-CAN-KEY-1     PIC X(12).
              03 WB-REQ-CAN-KEY-2     PIC X(03).

       01  WB-REQ-OFI-AREA.
           02 WB-REQ-OFI-CMD          PIC X(5) VALUE 'REDKY'.
           02 FILLER                  PIC X(3) VALUE 'OFI'.
           02 FILLER                  PIC X(5) VALUE 'OFI00'.
           02 WB-REQ-OFI-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +150.
           02 FILLER                  PIC X(58).
           02 WB-REQ-OFI-KEY-VALUE    PIC X(03).

      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
      *    Cancelaciones
      *    -------------
      *
      *    Nombre Registro : CAN
      *    Clave(s)        : CAN-KEY-ICAN(UU), CAN-FEC-FCAN(DC),
      *                      CAN-FEC-FEGR(DC), CAN-FEC-FOUT(DC)
      *    Largo           : 158
      *    Bloqueo         : 1
      *    Observaciones   :
      *
      *    01) Modificaciones 06/09/88, LJV
      *        - Eliminacion Clave CAN-STP-ITRN
      *    02) Modificaciones 25/08/89, ISP
      *        - Incorporacion CAN-SGV-DIFE + CAN-GLS-DISP
      *    03) Modificaciones 01/10/90, ALD
      *        - Incorporacion CAN-FEC-FDVA
      *        - Modificacion  CAN-GLS-DISP
      *    04) Modificacion  30/06/93,  DAD
      *        - Modificado Largo/Bloqueo del registro, de 137/2 a 158/1
      *          aumentando la Glosa Disponible.
      *    05) Modificacion  11/10/96,  DPM
      *        - Incorporacion CAN-SGV-IMOK
      *        - Modificacion  CAN-GLS-DISP de 24 a 16
      *
       01  CAN.
      *
      *    %POS 000
      *    Filler Compatibilidad VSAM/MVS
           03  CAN-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  CAN-KEY-IREG.
      *
      *KEY     Identificacion Cancelacion
               05  CAN-KEY-ICAN.
      *
      *            Cod. Int. Comp. Operacion Credito
                   07  CAN-CIC-IOPC.
      *
      *    %POS 001
      *                Centro Asignacion Identificacion
                       09  CAN-CAI-IOPC.
                           11  CAN-COD-TOPC             PIC X(01).
                           11  CAN-COD-COPC             PIC X(03).
      *
      *    %POS 005
      *                Ident. Interna Computacional
                       09  CAN-IIC-IOPC.
                           11  CAN-COD-OOPC.
                               13  CAN-COD-SOPC         PIC X(02).
                               13  CAN-IND-SOPC         PIC X(01).
                           11  CAN-GLS-NOPC             PIC X(05).
      *
      *    %POS 013
      *            Numero Identificacion Cancelacion
                   07  CAN-NUM-ICAN                     PIC 9(03).
      *
      *        Time Stamp Transaccion
               05  CAN-STP-ITRN.
      *
      *    %POS 016
      *            Fecha Transaccion
                   07  CAN-FEC-FTRN.
                       09  CAN-NUM-STRN                 PIC 9(02).
                       09  CAN-NUM-ATRN                 PIC 9(02).
                       09  CAN-NUM-MTRN                 PIC 9(02).
                       09  CAN-NUM-DTRN                 PIC 9(02).
      *
      *    %POS 024
      *            Hora Transaccion
                   07  CAN-HRA-HRTR.
                       09  CAN-NUM-HHTR                 PIC 9(02).
                       09  CAN-NUM-MMTR                 PIC 9(02).
                       09  CAN-NUM-SSTR                 PIC 9(02).
      *
      *    %POS 030
      *REQ Indicador Tipo Accion
           03  CAN-MSC-TACC                             PIC X(01).
      *
      *    %POS 031
      *    Indicador Status
           03  CAN-MSC-STAT                             PIC X(01).
      *
      *REQ Codigo Origen Transaccion
           03  CAN-COD-OTRN.
      *
      *    %POS 032
      *        Indicador Tipo Origen
               05  CAN-MSC-TTRN                         PIC X(01).
      *
      *    %POS 033
      *        Codigo Tipo Origen
               05  CAN-COD-TTRN                         PIC X(03).
      *
      *    %POS 036
      *REQ Autor Transaccion
           03  CAN-COD-ATRN                             PIC X(12).
      *
      *    %POS 048
      *REQ Oficina Cancelacion                          TAB OFI
           03  CAN-COD-OFCA                             PIC X(03).
      *
      *    %POS 051
      *    Codigo Responsable Cancelacion               TAB USR
           03  CAN-COD-RCAN                             PIC X(12).
      *
      *REQ Codigo Cancelacion                           COL CANC
           03  CAN-MSC-CANC.
      *
      *    %POS 063
      *        Tipo Cancelacion                         COL TCAN
               05  CAN-MSC-TCAN                         PIC X(01).
      *
      *    %POS 064
      *        Forma Cancelacion                        COL FCAN
               05  CAN-MSC-FCAN                         PIC X(01).
      *
      *    %POS 065
      *        Entidad Cancelacion                      COL ECAN
               05  CAN-MSC-ECAN                         PIC X(01).
      *
      *    %POS 066
      *REQ Fecha Cancelacion Real
           03  CAN-FEC-FCAN.
               05  CAN-NUM-SCAN                         PIC 9(02).
               05  CAN-NUM-ACAN                         PIC 9(02).
               05  CAN-NUM-MCAN                         PIC 9(02).
               05  CAN-NUM-DCAN                         PIC 9(02).
      *
      *    %POS 074
      *REQ Fecha Cancelacion Contable
           03  CAN-FEC-FEGR.
               05  CAN-NUM-SEGR                         PIC 9(02).
               05  CAN-NUM-AEGR                         PIC 9(02).
               05  CAN-NUM-MEGR                         PIC 9(02).
               05  CAN-NUM-DEGR                         PIC 9(02).
      *
      *    %POS 082
      *REQ Fecha Cancelacion Computacional
           03  CAN-FEC-FOUT.
               05  CAN-NUM-SOUT                         PIC 9(02).
               05  CAN-NUM-AOUT                         PIC 9(02).
               05  CAN-NUM-MOUT                         PIC 9(02).
               05  CAN-NUM-DOUT                         PIC 9(02).
      *
      *    %POS 090
      *    Valor Gastos
           03  CAN-SGV-GAST             COMP-3          PIC S9(11)V9(4).
      *
      *    %POS 098
      *    Valor Total Pagado
           03  CAN-SGV-TPAG             COMP-3          PIC S9(11)V9(4).
      *
      *    %POS 106
      *    Valor Total Renovado
           03  CAN-SGV-TREN             COMP-3          PIC S9(11)V9(4).
      *
      *    %POS 114
      *    Total de Vencimientos CanCelados
           03  CAN-NUM-TVCC                             PIC S9(03).
      *
      *    %POS 117
      *    Diferencia, pago efect - debe pagar
           03  CAN-SGV-DIFE             COMP-3          PIC S9(11)V9(4).
      *MOD-03
      *    %POS 125
      *    Fecha Devengo Anterior
           03  CAN-FEC-FDVA.
               05  CAN-NUM-SDVA                         PIC 9(02).
               05  CAN-NUM-ADVA                         PIC 9(02).
               05  CAN-NUM-MDVA                         PIC 9(02).
               05  CAN-NUM-DDVA                         PIC 9(02).
      *MOD-04
      *    %POS 133
      *    Flag indicador de Contabilizacion
           03 CAN-IND-CTBL                              PIC X(01).
      *
      *MOD-05
      *    %POS 134
      *    Intereses Moratorios Capitalizados
           03  CAN-SGV-IMOK             COMP-3          PIC S9(11)V9(4).
      *
      *MOD-05
      *    %POS 142
      *    Glosa Disponible
           03  CAN-GLS-DISP                             PIC X(16).

      *    Registro de Oficinas
      *    --------------------
      *
      *    Nombre Registro : OFI
      *    Clave(s)        : OFI-COD-OFIC(UU), OFI-CIC-OFIC(UU),
      *                      OFI-EXT-TABL(DC)
      *    Largo           : 297
      *    Bloqueo         : 3
      *    Observaciones   :
      *
      *    01) Modificacion  04/10/94,  DAD
      *        - Se Elimina campo  OFI-GLS-TLEX.
      *        - Se Incorporan campos OFI-NUM-NFAX, OFI-COD-MODE,
      *                               OFI-COD-ZONA, OFI-COD-JPOP,
      *                               OFI-COD-JEOP, OFI-COD-REGP,
      *                               OFI-COD-REGE.
      *        - Se Modifica Tamao del Disponible.
      *
       01  OFI.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OFI-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  OFI-KEY-IREG.
      *
      *KEY     Codigo Oficina ( LLave Primaria )
               05  OFI-COD-OFIC                         PIC X(03).
      *
      *        Time Stamp Transaccion
               05  OFI-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  OFI-FEC-FTRN.
                       09  OFI-NUM-STRN                 PIC 9(02).
                       09  OFI-NUM-ATRN                 PIC 9(02).
                       09  OFI-NUM-MTRN                 PIC 9(02).
                       09  OFI-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  OFI-HRA-HRTR.
                       09  OFI-NUM-HHTR                 PIC 9(02).
                       09  OFI-NUM-MMTR                 PIC 9(02).
                       09  OFI-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  OFI-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  OFI-MSC-STAT                             PIC X(01).
      *
      *    Indicador Vigencia                           GNS VIGE
           03  OFI-IND-VIGE  REDEFINES  OFI-MSC-STAT    PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  OFI-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  OFI-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  OFI-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  OFI-COD-ATRN                             PIC X(12).
      *
      *KEY Codigo Oficina ( Interno Computacional )
           03  OFI-CIC-OFIC                             PIC X(03).
      *
      *    Codigo Oficina Externo
           03  OFI-EXT-TABL.
      *
      *        Codigo Tipo Tabla
               05  OFI-EXT-TTAB                         PIC X(03).
      *
      *        Codigo Tabla
               05  OFI-EXT-CTAB                         PIC X(05).
      *
      *    Abreviacion Oficina
           03  OFI-GLS-ABRV                             PIC X(05).
      *
      *    Descripcion Oficina
           03  OFI-GLS-DESC                             PIC X(38).
      *
      *    Direccion Oficina
           03  OFI-GLS-DIRE                             PIC X(38).
      *
      *    Codigo Comuna Oficina                        TAB COM
           03  OFI-COD-COMU                             PIC X(04).
      *
      *ELI-01
      *    Telex Oficina
      *     03  OFI-GLS-TLEX                             PIC X(38).
      *
      *ING-01
      *    Numero Fax Oficina
           03  OFI-NUM-NFAX                             PIC X(09).
      *
      *ING-01
      *    Codigo Modelo                                TAB MDL
           03  OFI-COD-MODL                             PIC X(01).
      *
      *ING-01
      *    Codigo Zonal                                 TAB ZNL
           03  OFI-COD-ZONL                             PIC X(01).
      *
      *ING-01
      *    Codigo USR jefe banca personas               TAB USR
           03  OFI-COD-JPOP                             PIC X(12).
      *
      *ING-01
      *    Codigo USR jefe banca empresas               TAB USR
           03  OFI-COD-JEOP                             PIC X(12).
      *
      *ING-01
      *    Codigo regional en banca personas            TAB RGN
           03  OFI-COD-REGP                             PIC X(03).
      *
      *    Codigo Plaza Oficina                         TAB PZA
           03  OFI-COD-PLZA                             PIC X(04).
      *
      *    Fonos Oficina
           03  OFI-GLS-FONO.
      *
      *        Fono 1 Oficina
               05  OFI-GLS-FON1                         PIC X(10).
      *
      *        Fono 2 Oficina
               05  OFI-GLS-FON2                         PIC X(10).
      *
      *    Anexos Oficina
           03  OFI-GLS-ANXO.
      *
      *        Anexo 1 Oficina
               05  OFI-GLS-ANX1                         PIC X(04).
      *
      *        Anexo 2 Oficina
               05  OFI-GLS-ANX2                         PIC X(04).
      *
      *    Oficina Regional                             TAB OFI
           03  OFI-COD-OREG                             PIC X(03).
      *
      *    Oficina Centralizacion Operativa             TAB OFI
           03  OFI-COD-OFCO                             PIC X(03).
      *
      *    Clasificacion Operativa Oficina              TAB COPE
           03  OFI-IND-COPE                             PIC X(01).
      *
      *    Clasificacion Comercial Oficina              TAB CCOM
           03  OFI-IND-CCOM                             PIC X(01).
      *
      *    Cuenta Contable Oficina
           03  OFI-NUM-CCON                             PIC X(12).
      *
      *    Digito Verificador Cuenta Contable
           03  OFI-VRF-CCON                             PIC X(01).
      *
      *    Cuenta Corriente Oficina
           03  OFI-NUM-CCTE                             PIC X(12).
      *
      *    Digito Verificador Cuenta Corriente
           03  OFI-VRF-CCTE                             PIC X(01).
      *
      *    Codigo Jefe Oficina                          TAB USR
           03  OFI-COD-JFOF                             PIC X(12).
      *
      *    Fecha desde Jefe Oficina (SSAAMMDD)
           03  OFI-FEC-FJFO.
               05  OFI-NUM-SJFO                         PIC 9(02).
               05  OFI-NUM-AJFO                         PIC 9(02).
               05  OFI-NUM-MJFO                         PIC 9(02).
               05  OFI-NUM-DJFO                         PIC 9(02).
      *
      *    Codigo Jefe Operaciones Oficina              TAB USR
           03  OFI-COD-JOOF                             PIC X(12).
      *
      *    Fecha desde Jefe Operaciones Oficina (SSAAMMDD)
           03  OFI-FEC-FJOO.
               05  OFI-NUM-SJOO                         PIC 9(02).
               05  OFI-NUM-AJOO                         PIC 9(02).
               05  OFI-NUM-MJOO                         PIC 9(02).
               05  OFI-NUM-DJOO                         PIC 9(02).
      *
      *    Fecha Apertura Oficina (SSAAMMDD)
           03  OFI-FEC-FAPE.
               05  OFI-NUM-SAPE                         PIC 9(02).
               05  OFI-NUM-AAPE                         PIC 9(02).
               05  OFI-NUM-MAPE                         PIC 9(02).
               05  OFI-NUM-DAPE                         PIC 9(02).
      *
      *    Fecha Cierre Oficina (SSAAMMDD)
           03  OFI-FEC-FCIE.
               05  OFI-NUM-SCIE                         PIC 9(02).
               05  OFI-NUM-ACIE                         PIC 9(02).
               05  OFI-NUM-MCIE                         PIC 9(02).
               05  OFI-NUM-DCIE                         PIC 9(02).
      *ING-01
      *    Codigo regional en banca empresas            TAB RGN
           03  OFI-COD-REGE                             PIC X(03).
      *
      *MOD-01 X(05).
      *    Disponible
           03  OFI-GLS-DISP                             PIC X(02).
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  WB-ELEM-LIS        PIC X(11).
      *
           COPY RLOVR001.
           EJECT
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           EJECT
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF (WS-PROCESO-OK)
               PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-PROCESO.
           GOBACK.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROCESO-INICIAL.
      *-------------------
           PERFORM BB-ABRIR-ARCHIVOS
           PERFORM BC-FECHAS-LISTADO
           GO BZ-FIN-DE-SECCION.
           SKIP1
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN INPUT COLVJRN
               OUTPUT COLL685.
           IF (WR-FS-COLVJRN NOT = 00 AND NOT = 97)
               MOVE 'B-INICIALIZAR'     TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS' TO WR-PARRAFO
               MOVE 'COLVJRN'           TO WR-ARCHIVO
               MOVE 'OPEN'              TO WR-MENSAJE
               MOVE WR-FS-COLVJRN       TO WR-FILE-ST
               PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       BC-FECHAS-LISTADO.
      *---------------------
           ACCEPT WR-FECHA-HOY FROM DATE.
           MOVE WR-HOY-DD               TO WR-LA01-DIA
           MOVE WR-HOY-MM               TO WR-LA01-MES
           MOVE WR-HOY-AA               TO WR-LA01-ANO.
           SKIP1
       BC-DISPLAY-ERROR.
      *-----------------
           SET WS-PROCESO-NO-OK        TO TRUE.
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GRAL.
      *----------------
           SORT COLVSOR ON ASCENDING KEY SOR-SUC-DES
                                         SOR-SUC-ORI
                                         SOR-NUM-OPE
              INPUT  PROCEDURE IS E-SELECCION
              OUTPUT PROCEDURE IS F-LISTADO.
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'COLPB685 NO EMITE LISTADO'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA COLPB685 FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '   CANCEL. POR SAFE QUE NO ESTAN EN COLOCAC.'
           DISPLAY ' '
           DISPLAY 'ARCHIVO  LEIDOS  RECHAZADOS   REGRABADOS'
           DISPLAY ' '
           DISPLAY 'COLVJRN: ' WA-L-JRN '     ' WA-R-JRN '    ' WA-G-JRN
           DISPLAY 'CAN    : ' WA-L-CAN '     ' WA-R-CAN '    ' WA-G-CAN
           DISPLAY 'OFI    : ' WA-L-OFI '     ' WA-R-OFI '    ' WA-G-OFI
           DISPLAY 'COLVSOR: ' WA-L-SOR '     ' WA-R-SOR '    ' WA-G-SOR
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DC-TERMINO.
      *-----------
           CLOSE COLVJRN COLL685.
           IF WS-PROCESO-NO-OK
              MOVE 100 TO RETURN-CODE
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       E-SELECCION SECTION.
      *--------------------
           PERFORM EB-LEER-COLVJRN
           IF (WR-FS-COLVJRN = 0 AND WS-PROCESO-OK)
               PERFORM EG-FORMA-FECHA
           END-IF
           PERFORM EC-VERIFICA-CANCEL
              UNTIL (WR-FS-COLVJRN NOT = 0 OR WS-PROCESO-NO-OK).
           GO EZ-FIN-DE-SECCION.
           SKIP1
       EB-LEER-COLVJRN.
      *----------------
           READ COLVJRN END-READ
           IF (WR-FS-COLVJRN = 0)
               ADD 1                   TO WA-L-JRN
           ELSE
               IF (WR-FS-COLVJRN NOT = 0 AND NOT = 10)
                   MOVE 'E-SELECCION'       TO WR-SECCION
                   MOVE 'EB-LEER-COLVJRN'   TO WR-PARRAFO
                   MOVE 'COLVJRN'           TO WR-ARCHIVO
                   MOVE 'READ'              TO WR-OPERACI
                   MOVE WR-FS-COLVJRN       TO WR-FILE-ST
                   PERFORM BC-DISPLAY-ERROR
               END-IF
           END-IF.
           SKIP1
       EC-VERIFICA-CANCEL.
      *-------------------
           MOVE +0                      TO WR-VAR-COMP
           MOVE JRN-CUE-NTA             TO WR-VAR-BCD-6
           MOVE WR-VAR-COMP             TO WR-NUM-OPE-N
           MOVE WR-NUM-OPE              TO WB-REQ-CAN-KEY-1
           MOVE HIGH-VALUE              TO WB-REQ-CAN-KEY-2
           MOVE 'REDKL'                 TO WB-REQ-CAN-CMD
           PERFORM ED-LEER-CAN.
           MOVE 'REDBR'                 TO WB-REQ-CAN-CMD
           PERFORM ED-LEER-CAN
             UNTIL (WB-REQ-CAN-RC NOT = '  '            OR
                    CAN-CIC-IOPC  NOT = WR-NUM-OPE      OR
                    CAN-FEC-FOUT      < WR-FEC-PAG-AMD  OR
                   (CAN-MSC-STAT      = 'A' AND
                    CAN-SGV-TPAG  NOT <  0  AND
                    CAN-FEC-FOUT      = WR-FEC-PAG-AMD))

           IF (WB-REQ-CAN-RC NOT = '  '       OR
               CAN-CIC-IOPC  NOT = WR-NUM-OPE OR
               CAN-FEC-FOUT      < WR-FEC-PAG-AMD)
               PERFORM EF-NO-CANCELADA
           ELSE
               ADD 1                   TO WA-L-CAN
           END-IF.
           PERFORM EB-LEER-COLVJRN.
           SKIP1
       ED-LEER-CAN.
      *------------
           MOVE SPACES                 TO WB-REQ-CAN-RC
           MOVE 'CAN00'                TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-CAN-AREA CAN WB-ELEM-LIS
           IF (WB-REQ-CAN-RC NOT = '  ' AND NOT = '14')
               MOVE 'E-SELECCION'        TO WR-SECCION
               MOVE 'ED-LEER-CAN'        TO WR-PARRAFO
               MOVE 'CAN'                TO WR-ARCHIVO
               MOVE WB-REQ-CAN-CMD       TO WR-OPERACI
               MOVE WB-REQ-CAN-RC        TO WR-FILE-ST
               MOVE WB-REQ-CAN-KEY-VALUE TO WR-MENSAJE
               PERFORM BC-DISPLAY-ERROR
           END-IF.
           SKIP1
       EF-NO-CANCELADA.
      *----------------
           MOVE 0                      TO WR-SUC-COMP
           MOVE JRN-SUC-ORI            TO WR-SUC-BIN
           MOVE WR-SUC-COMP            TO SOR-SUC-ORI
           MOVE 0                      TO WR-SUC-COMP
           MOVE JRN-SUC-DES            TO WR-SUC-BIN
           MOVE WR-SUC-COMP            TO SOR-SUC-DES
           MOVE WR-NUM-OPE             TO SOR-NUM-OPE
           MOVE JRN-VAL-2              TO SOR-SGV-TPAG
           RELEASE COLRSOR.
           ADD  1                      TO WA-G-SOR.
           SKIP1
       EG-FORMA-FECHA.
      *---------------
           MOVE +0                     TO WR-FEC-PAG-COMP
           MOVE JRN-FEC                TO WR-FEC-PAG-BCD
           MOVE WR-FEC-PAG-COMP        TO WR-FEC-PAG.
           MOVE WR-DIA-PAG             TO WR-DIA-PAG-AMD
           MOVE WR-MES-PAG             TO WR-MES-PAG-AMD
           MOVE WR-ANO-PAG             TO WR-ANO-PAG-AMD
      *****IF (WR-ANO-PAG > 80)
           IF (WR-ANO-PAG < ROLL-YY)
               MOVE 20                 TO WR-SIG-PAG-AMD
           ELSE
               MOVE 19                 TO WR-SIG-PAG-AMD
           END-IF.
           MOVE WR-DIA-PAG-AMD         TO WR-LA04-DIA
           MOVE WR-MES-PAG-AMD         TO WR-LA04-MES
           MOVE WR-ANO-PAG-AMD         TO WR-LA04-ANO
           MOVE WR-SIG-PAG-AMD         TO WR-LA04-SIG.
           SKIP1
       EZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       F-LISTADO SECTION.
      *==================
       FA-PROCESO-GRAL.
      *----------------
           IF (WS-PROCESO-NO-OK)
               GO FZ-FIN-DE-SECCION
           END-IF
           RETURN COLVSOR AT END MOVE 10 TO WR-FS-COLVSOR END-RETURN
           PERFORM FB-LISTA-POR-SUC-DEST
              UNTIL (WR-FS-COLVSOR NOT =  0 OR
                     WS-PROCESO-NO-OK)
           GO FZ-FIN-DE-SECCION.
           SKIP1
       FB-LISTA-POR-SUC-DEST.
      *----------------------
           MOVE SOR-SUC-DES            TO WR-LA05-OFI-DES
                                          WR-SUC-DES
                                          WR-LA11-OFI
           IF (WT-GLS-OFI(SOR-SUC-DES) > SPACES)
               MOVE WT-GLS-OFI(SOR-SUC-DES) TO WR-LA05-GLS-OFI
           ELSE
               MOVE SOR-SUC-DES        TO WB-REQ-OFI-KEY-VALUE
               PERFORM FG-LEER-OFI
               MOVE OFI-GLS-DESC       TO WT-GLS-OFI(SOR-SUC-DES)
                                          WR-LA05-GLS-OFI
           END-IF
           MOVE 0                      TO WA-TOT-DES
           MOVE 99                     TO WA-LINEA
           WRITE COLR685 FROM WR-LINEA11.
           PERFORM FC-LISTA-POR-ORIGEN
              UNTIL (SOR-SUC-DES   NOT = WR-SUC-DES OR
                     WR-FS-COLVSOR NOT = 0 OR
                     WS-PROCESO-NO-OK)
           MOVE WA-TOT-DES             TO WR-LA10-TOT-OFI
           MOVE 'ORIGEN  : '           TO WR-LA10-GLS-OFI
           WRITE COLR685 FROM WR-LINEA10.
           SKIP1
       FC-LISTA-POR-ORIGEN.
      *--------------------
           MOVE SOR-SUC-ORI            TO WR-LA08-OFI-ORI
                                          WR-SUC-ORI
           IF (WT-GLS-OFI(SOR-SUC-ORI) > SPACES)
               MOVE WT-GLS-OFI(SOR-SUC-ORI) TO WR-LA08-GLS-OFI
           ELSE
               MOVE SOR-SUC-ORI        TO WB-REQ-OFI-KEY-VALUE
               PERFORM FG-LEER-OFI
               MOVE OFI-GLS-DESC       TO WT-GLS-OFI(SOR-SUC-ORI)
                                          WR-LA08-GLS-OFI
           END-IF
           MOVE 0                      TO WA-TOT-ORI
           PERFORM FD-LISTA-DETALLE
              UNTIL (SOR-SUC-ORI   NOT = WR-SUC-ORI OR
                     SOR-SUC-DES   NOT = WR-SUC-DES OR
                     WR-FS-COLVSOR NOT = 0 OR
                     WS-PROCESO-NO-OK)

           MOVE WA-TOT-ORI             TO WR-LA10-TOT-OFI
           MOVE 'CANCEL. : '           TO WR-LA10-GLS-OFI
           WRITE COLR685 FROM WR-LINEA09.
           WRITE COLR685 FROM WR-LINEA10.
           WRITE COLR685 FROM WR-LINEA00.
           ADD  3                      TO WA-LINEA
           ADD  WA-TOT-ORI             TO WA-TOT-DES.
           SKIP1
       FD-LISTA-DETALLE.
      *-----------------
           ADD  1                      TO WA-L-SOR.
           IF (WA-LINEA > 55)
               PERFORM FE-TIT-LISTADO
           END-IF
           MOVE SOR-NUM-OPE            TO WR-LA08-NUM-OPE
           MOVE SOR-SGV-TPAG           TO WR-LA08-MTO-CAN
           ADD  SOR-SGV-TPAG           TO WA-TOT-ORI
           WRITE COLR685 FROM WR-LINEA08.
           ADD  1                      TO WA-LINEA
           MOVE SPACES                 TO WR-LA08-OFI-ORI
                                          WR-LA08-GLS-OFI
           RETURN COLVSOR AT END MOVE 10 TO WR-FS-COLVSOR END-RETURN.
           SKIP1
       FE-TIT-LISTADO.
      *---------------
           ADD 1                   TO WA-PAGINA
           MOVE WA-PAGINA          TO WR-LA02-PAG
           WRITE COLR685 FROM WR-LINEA01.
           WRITE COLR685 FROM WR-LINEA02.
           WRITE COLR685 FROM WR-LINEA03.
           WRITE COLR685 FROM WR-LINEA04.
           WRITE COLR685 FROM WR-LINEA05.
           WRITE COLR685 FROM WR-LINEA06.
           WRITE COLR685 FROM WR-LINEA07.
           WRITE COLR685 FROM WR-LINEA06.
           MOVE 0                  TO WA-LINEA.
           SKIP1
       FG-LEER-OFI.
      *------------
           MOVE SPACES  TO WB-REQ-OFI-RC
           MOVE 'OFI00' TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-OFI-AREA OFI WB-ELEM-LIS
           IF (WB-REQ-OFI-RC = '  ')
               ADD  1                   TO WA-L-OFI
           ELSE
               IF (WB-REQ-OFI-RC = '14')
                   MOVE '*** SIN DESCRIPCION ***' TO OFI-GLS-DESC
               ELSE
                   IF (WB-REQ-OFI-RC NOT = '  ')
                       MOVE 'F-LISTADO'          TO WR-SECCION
                       MOVE 'FG-LEER-OFI'        TO WR-PARRAFO
                       MOVE 'OFI'                TO WR-ARCHIVO
                       MOVE WB-REQ-OFI-CMD       TO WR-OPERACI
                       MOVE WB-REQ-OFI-RC        TO WR-FILE-ST
                       MOVE WB-REQ-OFI-KEY-VALUE TO WR-MENSAJE
                       PERFORM BC-DISPLAY-ERROR
                    END-IF
               END-IF
           END-IF.
           SKIP1
       FZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT

