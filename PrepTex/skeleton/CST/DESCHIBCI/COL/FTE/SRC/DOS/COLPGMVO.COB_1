       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGMVO.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    23-FEB-1993    (Ultima Modificacion).
      * Genera archivo MVO con cargos/abonos a cuentas corrientes.
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MVO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *<<<< WSS
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY COLBROPC.
       COPY COLBRCAN.
       COPY COLBRCYA.
       COPY COLBRTOC.
       COPY SGCBROPE.
       COPY COLBRROV.
       COPY COLBRMVO.
       COPY COLWGMVO.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY TABBROFI.
       01  WSS-VARI.
           03 WSS-COD-OFIC.
              05 WSS-IND-OFIC        OCCURS 10    PIC X(03).
           03 WSS-I                  VALUE 0      PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROS  PIC 9(02).
           03 WSS-IND-TPRO           VALUE SPACES PIC X(01).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DFEC        VALUE ZEROS  PIC 9(02).
           03 WSS-IND-MCYA           VALUE ZEROS  PIC 9(01).
           03 WSS-NUM-RLEI-OPC       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RMOD-OPC       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RERR-OPC       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RLEI-CAN       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RMOD-CAN       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RERR-CAN       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RLEI-CYA       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RERR-CYA       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RGRA-MVO       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RERR-MVO       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RZRO-MVO       VALUE ZEROS  PIC 9(09).
           03 WSS-EDT-RLEI-OPC                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RMOD-OPC                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RERR-OPC                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RLEI-CAN                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RMOD-CAN                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RERR-CAN                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RLEI-CYA                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RERR-CYA                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RGRA-MVO                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RERR-MVO                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RZRO-MVO                    PIC ZZZ.ZZZ.ZZZ.

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGMVO' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM PARAM-MVO.
           PERFORM LUP-PUT-MVO.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-TOC.
           PERFORM SGC-FIO-OPE.
           PERFORM COL-FIO-ROV.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-MVO.
       FIN-OPEN-FILE.
           EXIT.

       PARAM-MVO SECTION.
      *-----------------
       INI-PARAM-MVO.
      * Rutina para Capturar y Validar Parametros de Rango de Fechas
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      *Captura Oficinas del credito
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'OFICINA                : ' WSS-COD-OFIC IN WSS-VARI.
      *Captura Fecha de Proceso
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FEC DE PROC (DDMMAAAA) : ' WSS-FEC-FPRO IN WSS-VARI.
      *Captura Tipo de Proceso
           ACCEPT WSS-IND-TPRO IN WSS-VARI.
           DISPLAY 'TIPO DE PROCESO        : ' WSS-IND-TPRO IN WSS-VARI.

           DISPLAY 'FIN CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *Validacion Oficinas
           IF WSS-COD-OFIC IN WSS-VARI NOT > SPACES
               DISPLAY 'ASUME TODAS LAS OFICINAS (*)'
               MOVE '*' TO WSS-COD-OFIC IN WSS-VARI.
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               MOVE 1 TO WSS-I
               PERFORM VAL-OFIC UNTIL WSS-I > 10.

      *VAL-FEC Valida Fecha de Proceso
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992

      *Valida tipo de proceso
           IF WSS-IND-TPRO IN WSS-VARI NOT > SPACES
               MOVE 'C' TO WSS-IND-TPRO IN WSS-VARI
           ELSE
              IF WSS-IND-TPRO IN WSS-VARI NOT = 'R' AND
                 WSS-IND-TPRO IN WSS-VARI NOT = 'C'
                  DISPLAY 'TIPO DE PROCESO INVALIDO' WSS-IND-TPRO
                                                      IN WSS-VARI
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
                  PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      *DAD ini  23-FEB-1993   (Memo.22-02-93 Pto.2)
      *         16-FEB-1993   (Memo.28-01-93)
      * Se obtiene de tabla la Hora Ingreso Movimiento
           MOVE 'TAB'    TO FIO-SIST.
           MOVE 'PRM'    TO TAB-CIC-TTAB IN TAB.
           MOVE 'MVOCOL' TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DAT1 IN TAB  TO MVO-HRMV IN MVO-VARI
           ELSE
               DISPLAY 'NO EXISTE CODIGO : ' TAB-CIC-TABL IN TAB
               PERFORM GNS-PRO-ABT.
      *DAD fin  23-FEB-1993 / 16-FEB-1993
       FIN-PARAM-MVO.
           EXIT.

       VAL-OFIC SECTION.
       INI-VAL-OFIC.
           IF WSS-IND-OFIC IN WSS-VARI(WSS-I) > SPACES
               MOVE WSS-IND-OFIC IN WSS-VARI(WSS-I) TO
                    OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF ( MSG-COD-MENS = 'COD    NEX' ) OR
                  ( MSG-COD-MENS = 'COD    NVG' )
                    MOVE 'COL' TO MSG-COD-SIST
                    PERFORM GET-MSG
                    DISPLAY MSG-GLS-DESC ',OFICINA:'
                            WSS-IND-OFIC IN WSS-VARI(WSS-I)
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
                    PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           ADD 1 TO WSS-I IN WSS-VARI.
       FIN-VAL-OFIC.
           EXIT.

       LUP-PUT-MVO SECTION.
      *-------------------
       INI-LUP-OPC-MVO.
      * Programacion de Ciclo para Registro OPC
           MOVE WSS-FEC-FFEC IN WSS-VARI TO OPC-FEC-FINP IN OPC.
           MOVE 'OPC-FEC-FINP'           TO FIO-AKEY.
           MOVE FIO-GET-NLS              TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT (FIO-STAT-OKS AND
              OPC-FEC-FINP IN OPC = WSS-FEC-FFEC IN WSS-VARI)
               DISPLAY 'NO HAY REGISTROS OPC PARA FECHA DE PROCESO : '
                                              WSS-FEC-FFEC IN WSS-VARI
               GO TO INI-LUP-CAN-MVO.
       LUP-OPC-MVO.
      * Proceso de Seleccion y Clasificacion a Partir del Registro OPC
           IF WSS-IND-TPRO IN WSS-VARI = 'R'
               IF OPC-MSC-STAT IN OPC = 'G' OR 'B' OR 'H'
                   NEXT SENTENCE
               ELSE
                   GO TO NXT-OPC-MVO
           ELSE
               IF OPC-MSC-STAT IN OPC = 'G' OR 'B'
                   NEXT SENTENCE
               ELSE
                   GO TO NXT-OPC-MVO.

           ADD  1  TO WSS-NUM-RLEI-OPC IN WSS-VARI.

      * Busca los Abonos para Operacion de Credito Determinada
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE OPC-CIC-IOPC IN OPC TO CYA-CIC-IOPC IN CYA.
           MOVE ZEROES              TO CYA-NUM-ICAN IN CYA.
           MOVE FIO-GET-NLS         TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-CIC-IOPC IN CYA NOT = OPC-CIC-IOPC IN OPC OR
              CYA-NUM-ICAN IN CYA NOT = ZEROES
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    CYANX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   OPC-KEY-IOPC IN OPC
               ADD 1               TO WSS-NUM-RERR-OPC IN WSS-VARI
               GO TO NXT-OPC-MVO.
       LUP-OPC-CYA.
           ADD 1    TO WSS-NUM-RLEI-CYA IN WSS-VARI.
           MOVE 'O'                      TO MVO-ORIG IN MVO-VARI.
           MOVE WSS-FEC-FFEC IN WSS-VARI TO MVO-FPRO IN MVO-VARI.
           PERFORM GEN-REG-MVO.
           IF MVO-CMSG IN MVO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE MVO-CMSG IN MVO-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC MVO-MEN2
               MOVE FIO-BCK-OUT          TO FIO-CMND
               PERFORM COL-FIO-OPC
               DISPLAY 'BCK-OUT-OPC : ' OPC-KEY-IOPC IN OPC
               GO TO NXT-OPC-MVO.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF FIO-STAT-OKS AND
              CYA-CIC-IOPC IN CYA = OPC-CIC-IOPC IN OPC AND
              CYA-NUM-ICAN IN CYA = ZEROES
               GO TO LUP-OPC-CYA.

           ON 10 AND EVERY 10
              MOVE FIO-CHK-PNT TO FIO-CMND
              PERFORM COL-FIO-OPC.

       NXT-OPC-MVO.
           MOVE 'OPC-FEC-FINP' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF FIO-STAT-OKS AND
               OPC-FEC-FINP IN OPC = WSS-FEC-FFEC IN WSS-VARI
               GO TO LUP-OPC-MVO.

       INI-LUP-CAN-MVO.
      * Programacion de Ciclo para Registro CAN
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CAN-FEC-FOUT IN CAN.
           MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT (FIO-STAT-OKS AND
              CAN-FEC-FOUT IN CAN = WSS-FEC-FFEC IN WSS-VARI)
               DISPLAY 'NO HAY REGISTROS CAN PARA FECHA DE PROCESO : '
                                              WSS-FEC-FFEC IN WSS-VARI
               GO TO FIN-LUP-REG-MVO.

       LUP-CAN-MVO.
      * Proceso de Seleccion y Clasificacion a Partir del Registro CAN
           IF WSS-IND-TPRO IN WSS-VARI = 'R'
               IF CAN-MSC-STAT IN CAN = 'G' OR 'H'
                   NEXT SENTENCE
               ELSE
                   GO TO NXT-CAN-MVO
           ELSE
               IF CAN-MSC-STAT IN CAN = 'G'
                   NEXT SENTENCE
               ELSE
                   GO TO NXT-CAN-MVO.

           ADD  1  TO WSS-NUM-RLEI-CAN IN WSS-VARI.

      * Busca los Cargos para Cancelaciones de Operacion de Credito Dete
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE CAN-KEY-ICAN IN CAN TO CYA-KEY-ICAN IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-KEY-ICAN IN CYA NOT = CAN-KEY-ICAN IN CAN
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CAN    CYANX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CAN-KEY-ICAN IN CAN
               ADD 1               TO WSS-NUM-RERR-CAN IN WSS-VARI
               GO TO NXT-CAN-MVO.
       LUP-CAN-CYA.
           ADD 1    TO WSS-NUM-RLEI-CYA IN WSS-VARI.
           MOVE 'C'                      TO MVO-ORIG IN MVO-VARI.
           MOVE WSS-FEC-FFEC IN WSS-VARI TO MVO-FPRO IN MVO-VARI.
           PERFORM GEN-REG-MVO.
           IF MVO-CMSG IN MVO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE MVO-CMSG IN MVO-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC MVO-MEN2
               MOVE FIO-BCK-OUT          TO FIO-CMND
               PERFORM COL-FIO-CAN
               DISPLAY 'BCK-OUT-CAN : ' CAN-KEY-ICAN IN CAN
               GO TO NXT-CAN-MVO.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF FIO-STAT-OKS AND
              CYA-KEY-ICAN IN CYA = CAN-KEY-ICAN IN CAN
               GO TO LUP-CAN-CYA.

           ON 10 AND EVERY 10
              MOVE FIO-CHK-PNT TO FIO-CMND
              PERFORM COL-FIO-CAN.

       NXT-CAN-MVO.
           MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS AND
              CAN-FEC-FOUT IN CAN = WSS-FEC-FFEC IN WSS-VARI
               GO TO LUP-CAN-MVO.
       FIN-LUP-REG-MVO.
           EXIT.

       GEN-REG-MVO SECTION.
       INI-GEN-REG-MVO.
      * En el caso de Operaciones solo debe generar MVO para oficinas
      * dadas como parametro
           IF ( MVO-ORIG IN MVO-VARI = 'O' ) AND
              ( WSS-COD-OFIC IN WSS-VARI > SPACES ) AND
              ( WSS-IND-OFIC IN WSS-VARI(1) NOT = '*' )
               MOVE 1 TO WSS-I
               PERFORM SEL-OFIC UNTIL WSS-I > 10 OR
               OPC-COD-OFOC IN OPC = WSS-IND-OFIC IN WSS-VARI(WSS-I)
               IF WSS-I > 10
                   GO TO SKP-GEN-REG-MVO.
      *
      *DAD ini  29-NOV-1991   (Memo.22-11-91)
           MOVE SPACES TO MVO-CMSG IN MVO-VARI.
           IF MVO-ORIG IN MVO-VARI = 'O' AND
              CYA-SGV-LIQU IN CYA = ZEROES
               ADD 1 TO WSS-NUM-RZRO-MVO
               GO TO SKP-GEN-REG-MVO.
      *DAD fin  29-NOV-1991

           PERFORM COL-GEN-MVO.
      *     PERFORM DESPLIEGA-MVO.
           IF MVO-CMSG IN MVO-VARI > SPACES
               ADD 1 TO WSS-NUM-RERR-MVO IN WSS-VARI
               GO TO FIN-GEN-REG-MVO
           ELSE
               ADD 1 TO WSS-NUM-RGRA-MVO IN WSS-VARI.

       SKP-GEN-REG-MVO.
           IF MVO-ORIG IN MVO-VARI = 'C'
               GO TO MAR-DES-CAN.

      * Marca como activado el OPC en diferido
           IF OPC-MSC-STAT IN OPC = 'H'
               GO TO FIN-GEN-REG-MVO.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'OPC    NEX' TO MVO-CMSG IN MVO-VARI
               ADD 1             TO WSS-NUM-RERR-OPC IN WSS-VARI
               GO TO FIN-GEN-REG-MVO.

           IF OPC-MSC-STAT IN OPC = 'B'
               MOVE 'A'     TO OPC-MSC-STAT IN OPC
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-OPC
               IF NOT FIO-STAT-OKS
                   MOVE 'OPC    NOMOD' TO MVO-CMSG IN MVO-VARI
                   ADD 1 TO WSS-NUM-RERR-OPC IN WSS-VARI
               ELSE
                   ADD 1 TO WSS-NUM-RMOD-OPC IN WSS-VARI
           ELSE
      * Marca como procesada la OPC para que no la considere si se repro
               MOVE 'H'     TO OPC-MSC-STAT IN OPC
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-OPC
               IF NOT FIO-STAT-OKS
                   MOVE 'OPC    NOMOD' TO MVO-CMSG IN MVO-VARI
                   ADD 1 TO WSS-NUM-RERR-OPC IN WSS-VARI
               ELSE
                   ADD 1 TO WSS-NUM-RMOD-OPC IN WSS-VARI.

           GO TO FIN-GEN-REG-MVO.

      * Marca como procesada la CAN para que no la considere si se repro
       MAR-DES-CAN.
           IF CAN-MSC-STAT IN CAN = 'H'
               GO TO FIN-GEN-REG-MVO.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NEX' TO MVO-CMSG IN MVO-VARI
               ADD 1 TO WSS-NUM-RERR-CAN IN WSS-VARI
               GO TO FIN-GEN-REG-MVO.

           MOVE 'H'     TO CAN-MSC-STAT IN CAN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NOMOD' TO MVO-CMSG IN MVO-VARI
               ADD 1 TO WSS-NUM-RERR-CAN IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-RMOD-CAN IN WSS-VARI.
       FIN-GEN-REG-MVO.
           EXIT.

       SEL-OFIC SECTION.
       INI-SEL-OFIC.
           ADD 1 TO WSS-I.
       FIN-SEL-OFIC.
           EXIT.

       DESPLIEGA-MVO SECTION.
      *---------------------
       INI-DESPLIEGA-MVO.
           DISPLAY ' '.
           EXHIBIT NAMED MVO-FEC-FIMV IN MVO.
           EXHIBIT NAMED MVO-COD-TORG IN MVO.
           EXHIBIT NAMED MVO-NUM-CORG IN MVO.
           EXHIBIT NAMED MVO-FEC-FMVO IN MVO.
           EXHIBIT NAMED MVO-CIC-NUOP IN MVO.
           EXHIBIT NAMED MVO-COD-AMBT IN MVO.
           EXHIBIT NAMED MVO-COD-MODA IN MVO.
           EXHIBIT NAMED MVO-COD-OPRD IN MVO.
           EXHIBIT NAMED MVO-COD-TERM IN MVO.
           EXHIBIT NAMED MVO-COD-TAUT IN MVO.
           EXHIBIT NAMED MVO-COD-IAUT IN MVO.
           EXHIBIT NAMED MVO-COD-EPRC IN MVO.
           EXHIBIT NAMED MVO-COD-TCUA IN MVO.
           EXHIBIT NAMED MVO-COD-CCUA IN MVO.
           EXHIBIT NAMED MVO-COD-SICR IN MVO.
           EXHIBIT NAMED MVO-COD-TRCR IN MVO.
           EXHIBIT NAMED MVO-COD-SIAB IN MVO.
           EXHIBIT NAMED MVO-COD-TRAB IN MVO.
           EXHIBIT NAMED MVO-IND-OPMV IN MVO.
           EXHIBIT NAMED MVO-COD-SIAL IN MVO.
           EXHIBIT NAMED MVO-COD-TRAL IN MVO.
           EXHIBIT NAMED MVO-COD-TMOV IN MVO.
           EXHIBIT NAMED MVO-COD-PAMV IN MVO.
           EXHIBIT NAMED MVO-COD-VCMV IN MVO.
           EXHIBIT NAMED MVO-SGV-VMVO IN MVO.
           EXHIBIT NAMED MVO-COD-COCR IN MVO.
           EXHIBIT NAMED MVO-COD-OFCR IN MVO.
           EXHIBIT NAMED MVO-CIC-OPCR IN MVO.
           EXHIBIT NAMED MVO-COD-TDCR IN MVO.
           EXHIBIT NAMED MVO-COD-SDCR IN MVO.
           EXHIBIT NAMED MVO-GLS-TCR1 IN MVO.
           EXHIBIT NAMED MVO-GLS-TCR2 IN MVO.
           EXHIBIT NAMED MVO-COD-CVTC IN MVO.
           EXHIBIT NAMED MVO-IND-KESC IN MVO.
           EXHIBIT NAMED MVO-IND-DFCR IN MVO.
           EXHIBIT NAMED MVO-COD-COAB IN MVO.
           EXHIBIT NAMED MVO-COD-OFAB IN MVO.
           EXHIBIT NAMED MVO-CIC-OPAB IN MVO.
           EXHIBIT NAMED MVO-COD-TDAB IN MVO.
           EXHIBIT NAMED MVO-COD-SDAB IN MVO.
           EXHIBIT NAMED MVO-GLS-TAB1 IN MVO.
           EXHIBIT NAMED MVO-GLS-TAB2 IN MVO.
           EXHIBIT NAMED MVO-COD-CVTA IN MVO.
           EXHIBIT NAMED MVO-IND-KESA IN MVO.
           EXHIBIT NAMED MVO-IND-CAIF IN MVO.
           EXHIBIT NAMED MVO-COD-OFRE IN MVO.
           EXHIBIT NAMED MVO-NUM-LCUA IN MVO.
           EXHIBIT NAMED MVO-IND-DHCR IN MVO.
           EXHIBIT NAMED MVO-IND-DHAB IN MVO.
           EXHIBIT NAMED MVO-IND-MTRD IN MVO.
           EXHIBIT NAMED MVO-HRA-HRMV IN MVO.
           EXHIBIT NAMED MVO-GLS-DISP IN MVO.
       FIN-DESPLIEGA-MVO.
           EXIT.

       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           MOVE WSS-NUM-RLEI-OPC TO WSS-EDT-RLEI-OPC.
           MOVE WSS-NUM-RMOD-OPC TO WSS-EDT-RMOD-OPC.
           MOVE WSS-NUM-RERR-OPC TO WSS-EDT-RERR-OPC.
           MOVE WSS-NUM-RLEI-CAN TO WSS-EDT-RLEI-CAN.
           MOVE WSS-NUM-RMOD-CAN TO WSS-EDT-RMOD-CAN.
           MOVE WSS-NUM-RERR-CAN TO WSS-EDT-RERR-CAN.
           MOVE WSS-NUM-RLEI-CYA TO WSS-EDT-RLEI-CYA.
           MOVE WSS-NUM-RERR-CYA TO WSS-EDT-RERR-CYA.
           MOVE WSS-NUM-RGRA-MVO TO WSS-EDT-RGRA-MVO.
           MOVE WSS-NUM-RERR-MVO TO WSS-EDT-RERR-MVO.
           MOVE WSS-NUM-RZRO-MVO TO WSS-EDT-RZRO-MVO.
           DISPLAY 'REG. OPC LEIDOS            : ' WSS-EDT-RLEI-OPC.
           DISPLAY 'REG. OPC MODIFICADOS       : ' WSS-EDT-RMOD-OPC.
           DISPLAY 'REG. OPC ERRONEOS          : ' WSS-EDT-RERR-OPC.
           DISPLAY 'REG. CAN LEIDOS            : ' WSS-EDT-RLEI-CAN.
           DISPLAY 'REG. CAN MODIFICADOS       : ' WSS-EDT-RMOD-CAN.
           DISPLAY 'REG. CAN ERRONEOS          : ' WSS-EDT-RERR-CAN.
           DISPLAY 'REG. CYA LEIDOS            : ' WSS-EDT-RLEI-CYA.
           DISPLAY 'REG. CYA ERRONEOS          : ' WSS-EDT-RERR-CYA.
           DISPLAY 'REG. MVO GRABADOS          : ' WSS-EDT-RGRA-MVO.
           DISPLAY 'REG. MVO NO GRAB.C/ERROR   : ' WSS-EDT-RERR-MVO.
           DISPLAY 'REG. MVO NO GRAB.C/VALOR 0 : ' WSS-EDT-RZRO-MVO.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-TOC.
           PERFORM SGC-FIO-OPE.
           PERFORM COL-FIO-ROV.
           PERFORM COL-FIO-MVO.
       FIN-CLOSE-FILE.
           EXIT.
           COPY GNSBFMSG.
           COPY GNSBFMSC.
           COPY GNSBFTAB.
           COPY COLBFOPC.
           COPY COLBFCAN.
           COPY COLBFCYA.
           COPY COLBFTOC.
           COPY SGCBFOPE.
           COPY COLBFROV.
      *DAD ini  17-MAY-1993   (Memo.12-05-93)
      *     COPY COLBGMVO.
           COPY COLBTMVO.
      *DAD fin  17-MAY-1993
           COPY COLBFMVO.
           COPY TABBVOFI.
           COPY TABBFOFI.
           COPY GNSBBMSG.
           COPY GNSBBMSC.
           COPY GNSBBTAB.
           COPY GNSBGDTC.
           COPY GNSBGIDD.
           COPY GNSBTABT.
           COPY GNSBBSYS.
           COPY GNSBGFEC.
           COPY GNSBGHOY.
           COPY GNSBGEND.
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
           COPY GNSBGABT.
      *DAD fin  29-MAY-1992
           COPY COLBGTCT.
