       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGAHI.
       AUTHOR.          BEE TEAM (JGM).
       DATE-WRITTEN.    27-JUL-1999
      *
      *Actualiza archivo historico de envio a Boletin.
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      *<<<< WSS
      ***************
       COPY COLBRHDI.
       COPY COLBRPDI.
      ***************
      *
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWCVRF.
       COPY GNSWVVRF.
      ***************
       COPY GNSWGSYS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGCPT.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
      *
       01  WSS-VARI.
      *----------------------------------------------------------------*
      *        REGISTRO CABECERA        LARGO REG 201                  *
      *----------------------------------------------------------------*
          03 REG-CABEZA.
             05 RCAB-TIPO-REG               PIC X(02).
             05 RCAB-TIPO-DOC               PIC X(02).
             05 RCAB-COD-INST               PIC 9(03).
             05 RCAB-NOM-INST               PIC X(20).
             05 RCAB-CANT-RGS               PIC 9(05).
             05 RCAB-FECHA-DESDE.
                07 RCAB-FEC-DESDE-S         PIC 9(02).
                07 RCAB-FEC-DESDE-A         PIC 9(02).
                07 RCAB-FEC-DESDE-M         PIC 9(02).
                07 RCAB-FEC-DESDE-D         PIC 9(02).
             05 RCAB-FECHA-HASTA.
                07 RCAB-FEC-HASTA-S         PIC 9(02).
                07 RCAB-FEC-HASTA-A         PIC 9(02).
                07 RCAB-FEC-HASTA-M         PIC 9(02).
                07 RCAB-FEC-HASTA-D         PIC 9(02).
             05 RCAB-NUM-VOL                PIC 9(02).
             05 RCAB-FECHA-GENER.
                07 RCAB-FEC-GENER-S         PIC 9(02).
                07 RCAB-FEC-GENER-A         PIC 9(02).
                07 RCAB-FEC-GENER-M         PIC 9(02).
                07 RCAB-FEC-GENER-D         PIC 9(02).
             05 RCAB-SUMA-RUT               PIC 9(14).
             05 RCAB-SUMA-SFIN              PIC 9(14)V9(02).
             05 RCAB-SUMA-IDENTF            PIC 9(18).
             05 RCAB-SUMA-FVCTO.
                20 RCAB-FEC-VCTO-SSAA       PIC 9(09).
                20 RCAB-FEC-VCTO-MM         PIC 9(07).
                20 RCAB-FEC-VCTO-DD         PIC 9(07).
             10 RCAB-SUMA-FPAGO.
                20 RCAB-FEC-PAGO-SSAA       PIC 9(09).
                20 RCAB-FEC-PAGO-MM         PIC 9(07).
                20 RCAB-FEC-PAGO-DD         PIC 9(07).
             10 FILLER                      PIC X(49) VALUE SPACES.
      *
           03 WSS-PRM-FPR1.
              05 WSS-PRM-DPR1                     PIC 9(02).
              05 WSS-PRM-MPR1                     PIC 9(02).
              05 WSS-PRM-SPR1                     PIC 9(02).
              05 WSS-PRM-APR1                     PIC 9(02).
      *
           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                     PIC 9(02).
              05 WSS-NUM-APRO                     PIC 9(02).
              05 WSS-NUM-MPRO                     PIC 9(02).
              05 WSS-NUM-DPRO                     PIC 9(02).
      *
           03 WSS-FEC-FVEN.
              05 WSS-NUM-SVEN                     PIC X(02).
              05 WSS-NUM-AVEN                     PIC X(02).
              05 WSS-NUM-MVEN                     PIC X(02).
              05 WSS-NUM-DVEN                     PIC X(02).
      *
           03 WSS-FEC-FACL.
              05 WSS-NUM-SACL                     PIC X(02).
              05 WSS-NUM-AACL                     PIC X(02).
              05 WSS-NUM-MACL                     PIC X(02).
              05 WSS-NUM-DACL                     PIC X(02).
      *
           03 WSS-NUM-LCAB       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-EHDI       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-AHDI       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-LPDI       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-PPDI       VALUE ZEROES     PIC 9(08).

           03 WSS-IND-ACTU       VALUE SPACE      PIC X(01).
      *
           03 WSS-COD-ERRO                        PIC X(01).
              88 WSS-OK                           VALUE  '0'.
              88 WSS-NOINFORMADA-BOLETIN          VALUE  '1'.


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGAHI' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           PERFORM OPEN-FILE.
           PERFORM LEE-PARAM.
           PERFORM PRO-REG-PDI.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
      *
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-HDI.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLHDI ' FIO-STAT
              PERFORM GNS-PRO-ABT.
      *
      *
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-PDI.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLPDI ' FIO-STAT
              PERFORM GNS-PRO-ABT.
       FIN-OPEN-FILE.
           EXIT.

       CLOSE-FILE SECTION.
      *-----------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-HDI.
      *
      * Archivo de aclaracion   de documentos Protestados.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-PDI.
           DISPLAY 'CERRAR ARCHIVOS: HDI, PDI'.
      *
       FIN-CLOSE-FILE.
           EXIT.


      ******************************************************************
       PRO-REG-PDI SECTION.
      *------------------*
       INI-PRO-REG-PDI.
      * fecha computacional
      *
      *
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-PDI.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN REGISTROS PDI '
               GO TO FIN-PRO-REG-PDI.
      *
            MOVE 'N'    TO WSS-IND-ACTU  IN WSS-VARI.
      **
       LUP-BUS-PDI.
           ADD 1 TO WSS-NUM-LPDI IN WSS-VARI.
      *JGM-INI/16121999
           MOVE 'S'            TO WSS-IND-ACTU IN WSS-VARI.
      *JGM-FIN/16121999
      *
           IF  PDI-COD-TREG IN PDI = 'RC' AND
               PDI-COD-TDOC IN PDI = 'CM'
               ADD 1 TO WSS-NUM-LCAB IN WSS-VARI
               MOVE PDI              TO REG-CABEZA
               DISPLAY ' FECHA CAB : ' RCAB-FECHA-GENER
      *
      *JGM-INI/16121999
      *        IF RCAB-FECHA-GENER = WSS-FEC-FPRO IN WSS-VARI
      *           MOVE 'S'            TO WSS-IND-ACTU IN WSS-VARI
               MOVE RCAB-FECHA-GENER TO WSS-FEC-FACL IN WSS-VARI.
      *
      *        ELSE
      *           MOVE 'N' TO WSS-IND-ACTU IN WSS-VARI.
      *
      *JGM-FIN/16121999
           IF  WSS-IND-ACTU IN WSS-VARI = 'N'
               GO TO NXT-PRO-PDI.
      *
           IF  PDI-COD-TREG IN PDI NOT = 'RP'
               GO TO NXT-PRO-PDI.
      *
           ADD 1 TO WSS-NUM-PPDI IN WSS-VARI.
           MOVE PDI-FEC-FVEN IN PDI TO HDI-FEC-FVEN IN HDI.
           MOVE PDI-CIC-IOPC IN PDI TO HDI-CIC-IOPC IN HDI.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-HDI.
           IF NOT FIO-STAT-OKS
              ADD   1  TO WSS-NUM-EHDI   IN WSS-VARI
              GO TO NXT-PRO-PDI
           ELSE
            MOVE FIO-GET-KEY-UPD TO FIO-CMND
            PERFORM COL-FIO-HDI.
      *
      *     Indicador Aclarado Boletin.
            MOVE 'A'                      TO HDI-IND-ESTA IN HDI.
      *
      *     Fecha de envio al Boletin.
            MOVE WSS-FEC-FACL IN WSS-VARI TO HDI-FEC-FACL IN HDI.
      *
      *     Fecha de Cancelacion del vencimiento.
            MOVE PDI-FEC-FCAN IN PDI      TO HDI-FEC-FCAN IN HDI.
      *
            MOVE FIO-MOD         TO FIO-CMND.
            PERFORM COL-FIO-HDI.
            IF FIO-STAT-OKS
               ADD   1 TO WSS-NUM-AHDI IN WSS-VARI.
      *
       NXT-PRO-PDI.
      *
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-PDI.
           IF FIO-STAT-OKS
               GO TO LUP-BUS-PDI.
       FIN-PRO-REG-PDI.
           EXIT.
      *
      *
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------*
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS PROCESO: COLPGAHI '.
           DISPLAY '==============================='.
           DISPLAY ' TOTAL DE REG. LEIDOS PDI : ' WSS-NUM-LPDI.
           DISPLAY ' CABECERAS LEIDAS     PDI : ' WSS-NUM-LCAB.
           DISPLAY '----------------------------'.
           DISPLAY ' REG.  PROCESADOS     PDI : ' WSS-NUM-PPDI.
           DISPLAY ' REG.  ACTUALIZADO    HDI : ' WSS-NUM-AHDI.
           DISPLAY ' REG.  NO ENCONTRADOS HDI : ' WSS-NUM-EHDI.
           DISPLAY '==============================='.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.

      *****************************************************
       LEE-PARAM SECTION.
      *-----------------
       INI-LEE-PARAM.
           DISPLAY 'CAPTURA PARAMETROS'.
           DISPLAY '------------------- '.

      * Captura Fecha de Proceso
           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '.
           ACCEPT WSS-PRM-FPR1 IN WSS-VARI.


      * Validacion de parametros
           MOVE WSS-PRM-FPR1 IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ERROR VALIDACION FECHA DE PROCESO: '
                                                    FEC-MENS
               PERFORM GNS-PRO-ABT.
      *
           MOVE WSS-PRM-DPR1 IN WSS-VARI TO WSS-NUM-DPRO IN WSS-VARI.
           MOVE WSS-PRM-MPR1 IN WSS-VARI TO WSS-NUM-MPRO IN WSS-VARI.
           MOVE WSS-PRM-SPR1 IN WSS-VARI TO WSS-NUM-SPRO IN WSS-VARI.
           MOVE WSS-PRM-APR1 IN WSS-VARI TO WSS-NUM-APRO IN WSS-VARI.
      *

           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '
                    WSS-PRM-FPR1 IN WSS-VARI.

       FIN-LEE-PARAM.
           EXIT.
      ***************
       COPY COLBFPDI.
       COPY COLBFHDI.
      ***************
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGABT.

       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
      *
       COPY GNSBFMSC.
       COPY GNSBBMSC.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBBMSG.
       COPY GNSBGCPT.
       COPY GNSBGEND.
