       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPKEVC.
       AUTHOR.          CONSIST.
      * Programa de Carga de Archivo EVC Nuevo.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OLD-REQA==.
      *<<<< WSS
      *COPY GNSBRMSG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY COLBREVC.
      *COPY COLBROLD.         COPIAR DENTRO DEL PROGRAMA.
      *    Estructura Vencimientos
      *    -----------------------
      *
      *    Nombre Registro : OLD
      *    Clave(s)        : OLD-KEY-IOLD(UU), OLD-KEY-IREG(UU)
      *    Largo           : 092
      *    Bloqueo         : 1
      *    Observaciones   :
      *
      *    02) Modificaciones 20/04/88, LJV
      *
       01  OLD.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OLD-GLS-FLAG                             PIC X(01).
      *
      *MOD-01  STR COD-IVEN
      *    Clave Primaria Registro
           03  OLD-KEY-IREG.
      *
      *        Identificacion Invocacion
               05  OLD-KEY-IINV.
      *
      *            Indicador Acceso Invocacion ( I/O )
                   07  OLD-IND-AINV                     PIC X(01).
      *
      *            Time Stamp Invocacion
                   07  OLD-STP-IINV.
      *                Fecha Invocacion
                       09  OLD-FEC-FINV.
                           11  OLD-NUM-SINV             PIC 9(02).
                           11  OLD-NUM-AINV             PIC 9(02).
                           11  OLD-NUM-MINV             PIC 9(02).
                           11  OLD-NUM-DINV             PIC 9(02).
      *
      *                Hora Invocacion
                       09  OLD-HRA-HRIN.
                           11  OLD-NUM-HHIN             PIC 9(02).
                           11  OLD-NUM-MMIN             PIC 9(02).
                           11  OLD-NUM-SSIN             PIC 9(02).
      *
      *            Codigo Origen Invocacion
                   07  OLD-COD-OINV.
      *
      *                Miscelaneo Tipo Origen
                       09  OLD-MSC-TINV                 PIC X(01).
      *
      *                Codigo Tipo Origen
                       09  OLD-COD-TINV                 PIC X(03).
      *
      *KEY     Identificacion Estructura Vencimientos
               05  OLD-KEY-IOLD.
      *
      *            Cod. Int. Comp. Operacion Credito
                   07  OLD-CIC-IOPC.
      *
      *                Centro Asignacion Identificacion
                       09  OLD-CAI-IOPC.
                           11  OLD-COD-TOPC             PIC X(01).
                           11  OLD-COD-COPC             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  OLD-IIC-IOPC.
                           11  OLD-COD-OOPC.
                               13  OLD-COD-SOPC         PIC X(02).
                               13  OLD-IND-SOPC         PIC X(01).
                           11  OLD-GLS-NOPC             PIC X(05).
      *
      *            Num Ident Estructura Vencimientos
                   07  OLD-NUM-IOLD                     PIC 9(03).
      *
      *ELI-01
      *    Digito Verificador Operacion Credito
      *    03  OLD-VRF-IOPC                             PIC X(01).
      *
      *REQ Numero Vencimientos Iguales
           03  OLD-NUM-VIGU                             PIC 9(03).
      *
      *ING-01
      *    Duracion Vencimientos
           03  OLD-NUM-DURV             COMP            PIC 9(04).
      *
      *ING-01
      *    Unidad de Tiempo Duracion Vencimientos       TAB IDMA
           03  OLD-IND-UTDV                             PIC X(01).
      *
      *REQ Numero Vencimientos Cancelados
           03  OLD-NUM-VCAN                             PIC 9(03).
      *
      *    Separacion Primer Vencimiento
           03  OLD-NUM-SPVE             COMP            PIC 9(04).
      *
      *    Unidad de Tiempo Primer Vencimiento          TAB IDMA
           03  OLD-IND-UTPV                             PIC X(01).
      *
      *REQ Separacion entre Vencimientos
           03  OLD-NUM-SEVE             COMP            PIC 9(04).
      *
      *REQ Unidad de Tiempo Separacion entre Venctos    TAB IDMA
           03  OLD-IND-UTSV                             PIC X(01).
      *
      *MOD-01 NOM ttt-nVEN
      *REQ Fecha Proximo Vencimiento
           03  OLD-FEC-FPVC.
               05  OLD-NUM-SPVC                         PIC 9(02).
               05  OLD-NUM-APVC                         PIC 9(02).
               05  OLD-NUM-MPVC                         PIC 9(02).
               05  OLD-NUM-DPVC                         PIC 9(02).
      *
      *    Desfase en Dias
           03  OLD-NUM-DESD             COMP            PIC 9(04).
      *
      *ING-01
      *    Factor Variacion ( Crecimiento/Decrecimiento )
           03  OLD-SGV-FVAR             COMP-3          PIC S9(11)V9(4).
      *
      *ING-01
      *    Ind. Factor Variacion ( Monto/Porcentaje )   COL FVAR
           03  OLD-IND-FVAR                             PIC X(01).
      *
      *ING-01
      *REQ Valor Vencimientos ( Capital o Final )
           03  OLD-VAL-VENC             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-01  NOM COD-TCVE
      *MOD-01  PIC X(03)
      *    Tipo Calculo Vencimientos                    COL TCVE
           03  OLD-MSC-TCVE.
      *
      *ING-01
      *        Base Calculo Credito                     COL BCCR
               05  OLD-MSC-BCCR                         PIC X(01).
      *
      *ING-01
      *        Valores Informados Credito               COL VICR
               05  OLD-MSC-VICR                         PIC X(01).
      *
      *REQ Numero Identificacion Vencimiento
           03  OLD-NUM-IVCT                             PIC 9(03).
      *
      *REQ Numero Identificacion Documento Asociado
           03  OLD-NUM-IDLC                             PIC 9(03).
      *
      *REQ Dia Primer Vencimiento
           03  OLD-NUM-DPVE                             PIC 9(02).
      *
      *ING-02
      *    Indicador Cargo Automatico                   GNS VIGE
           03  OLD-IND-CAUT                             PIC X(01).
      *
      *MOD-01  PIC X(06)
      *MOD-02  PIC X(05)
      *    Glosa Disponible
           03  OLD-GLS-DISP                             PIC X(04).
       01  WSS-VARI.
           03 WSS-NUM-ROLD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RNEW           VALUE 0      PIC 9(05).
           03 WSS-NUM-RMOD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RBAD           VALUE 0      PIC 9(05).
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPKEVC' TO FIO-PROG.
           PERFORM BUS-FSIS.
      *    PERFORM OPEN-FILE.
           PERFORM LUP-PUT-REG.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           COPY GNSBGGBK.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       LUP-PUT-REG SECTION.
      *-------------------
       INI-LUP-REG.
      * Programacion de Ciclo para Registro OLD
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY REGISTROS EN ARCHIVO ANTIGUO '
               GO TO FIN-LUP-REG.
      *         MOVE 'COL'          TO MSG-COD-SIST
      *         MOVE 'EVC    EVCNX' TO MSG-COD-MENS
      *         PERFORM GET-MSG
      *         DISPLAY MSG-GLS-DESC   EVC-KEY-IEVC IN EVC
       LUP-OLD-NEW.
      * Proceso de Seleccion y Clasificacion a Partir del Registro ANTIG
           ADD 1 TO WSS-NUM-ROLD IN WSS-VARI.
           PERFORM BUS-REG-NEW.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF FIO-STAT-OKS
               GO TO LUP-OLD-NEW.
       FIN-LUP-REG.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro NEW al Procesar Registro ANT
       BUS-REG-NEW SECTION.
       INI-BUS-REG-NEW.
      * Formacion del Registro NEW
           MOVE SPACES       TO EVC.
           MOVE OLD-KEY-IOLD TO EVC-KEY-IEVC.
           MOVE OLD-NUM-VIGU TO EVC-NUM-VIGU.
           MOVE OLD-NUM-VCAN TO EVC-NUM-VCAN.
           MOVE OLD-NUM-SEVE TO EVC-NUM-SEVE.
           MOVE OLD-IND-UTSV TO EVC-IND-UTSV.
           MOVE OLD-FEC-FPVC TO EVC-FEC-FPVC.
           MOVE OLD-NUM-DESD TO EVC-NUM-DESD.
           MOVE OLD-SGV-FVAR TO EVC-SGV-FVAR.
           MOVE OLD-IND-FVAR TO EVC-IND-FVAR.
           MOVE OLD-VAL-VENC TO EVC-VAL-VENC.
           MOVE OLD-MSC-TCVE TO EVC-MSC-TCVE.
           MOVE OLD-NUM-IDLC TO EVC-NUM-IDLC.
           MOVE OLD-NUM-DPVE TO EVC-NUM-DPVE.
           MOVE OLD-IND-CAUT TO EVC-IND-CAUT.
           MOVE OLD-NUM-DURV TO EVC-NUM-DURV.
           MOVE OLD-IND-UTDV TO EVC-IND-UTDV.
      * Genera el Registro NEW
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           IF NOT FIO-STAT-OKS
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-EVC
               ADD 1 TO WSS-NUM-RNEW IN WSS-VARI
           ELSE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-EVC
               ADD 1 TO WSS-NUM-RMOD IN WSS-VARI.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RBAD IN WSS-VARI
               DISPLAY 'REGISTRO ERRONEO : '  EVC.
       FIN-BUS-REG-NEW.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OLD LEIDOS   : ' WSS-NUM-ROLD IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-NUM-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS NEW GRABADOS : ' WSS-NUM-RNEW IN WSS-VARI.
           DISPLAY 'REGISTROS MODIFICADOS  : ' WSS-NUM-RMOD IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-EVC.
           PERFORM COL-FIO-OLD.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
      *COPY GNSBFMSG.
      *COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY COLBFEVC.
      *COPY COLBFOLD.            COPIAR DENTRO DEL PROGRAMA.
      * Modulo FIO para registro OLD en DATACOM, 16-SEP-1988 17:55:13.60
       COL-FIO-OLD SECTION.
       INI-COL-FIO-OLD.
           MOVE ADR-OLD-REQA TO ADR-REQA.
           MOVE 'EVC' TO ADR-TABL IN ADR-REQA.
           MOVE IFD-GSIS( 14 ) TO ADR-DBID IN ADR-REQA.
           MOVE 'EVC00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-OLD.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OLD-KEY-IOLD' OR FIO-AKEY NOT > SPACES )
               MOVE OLD-KEY-IOLD IN OLD TO ADR-VKEY IN ADR-REQA
               MOVE 'EVC00' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-OLD, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
       CON-COL-FIO-OLD.
           MOVE OLD TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OLD
           ELSE
               MOVE 'COLOLD' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-OLD-REQA.
       FIN-COL-FIO-OLD.
           EXIT.
