       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGMCC.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    24-MAY-91 16:10 PM.
      * Programa Generador de Archivo MCC.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
      *<<<< WSS
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY TABBROFI.
       COPY COLBROPC.
       COPY COLBRCAN.
       COPY COLBRCYA.
       COPY COLBRMCC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       01  WSS-VARI.
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-DTER                     PIC 9(02).
              05 WSS-NUM-MTER                     PIC 9(02).
              05 WSS-NUM-STER                     PIC 9(02).
              05 WSS-NUM-ATER                     PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                     PIC 9(02).
              05 WSS-NUM-AFEC                     PIC 9(02).
              05 WSS-NUM-MFEC                     PIC 9(02).
              05 WSS-NUM-DFEC                     PIC 9(02).
           03 WSS-FEC-FFET.
              05 WSS-NUM-SFET                     PIC 9(02).
              05 WSS-NUM-AFET                     PIC 9(02).
              05 WSS-NUM-MFET                     PIC 9(02).
              05 WSS-NUM-DFET                     PIC 9(02).
           03 WSS-COD-OFIC                        PIC X(03).
           03 WSS-IND-PROC                        PIC 9(01).
           03 WSS-IND-ROPC           VALUE 0      PIC 9(05).
           03 WSS-IND-RCAN           VALUE 0      PIC 9(05).
           03 WSS-IND-RCYA           VALUE 0      PIC 9(05).
           03 WSS-IND-RBAD           VALUE 0      PIC 9(05).
           03 WSS-IND-ROKS           VALUE 0      PIC 9(05).
           03 WSS-IND-RERR           VALUE 0      PIC 9(05).
           03 WSS-TAB-TCYA.
              05 WSS-IND-TCYA        OCCURS 10    PIC X(05).
           03 WSS-I                  VALUE 0      PIC 9(02).
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGMCC' TO FIO-PROG.
           PERFORM BUS-FSIS.
           PERFORM OPEN-FILE.
           PERFORM PARAM-MCC.
           PERFORM INICIO-MCC.
           PERFORM LUP-PUT-MCC.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY TABBFOFI.
       COPY COLBFOPC.
       COPY COLBFCAN.
       COPY COLBFCYA.
       COPY COLIOMCC.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY TABBVOFI.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CYA.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-MCC.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       PARAM-MCC SECTION.
      *-----------------
       INI-PARAM-MCC.
      * Rutina para Capturar y Validar Parametros de Rango de Fechas
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMAAAA ) : '
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'FIN CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB'         TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-END.
           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DFET IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MFET IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-SFET IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-AFET IN WSS-VARI.
      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFIC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFIC IN WSS-VARI.
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFIC IN WSS-VARI
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.
           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
       FIN-PARAM-MCC.
           EXIT.
      ******************************************************************
       INICIO-MCC SECTION.
      *------------------
       INI-INICIO-MCC.
      * Inicializacion de Variables
           MOVE SPACES TO WSS-TAB-TCYA IN WSS-VARI.
      *BUS-MSC Busca Codigos con CIC Cuenta Corriente en Miscelaneo de T
           MOVE 'TAB'          TO MSC-COD-SIST.
           MOVE 'TCYA'         TO MSC-CIC-TMSC IN MSC.
           MOVE 'CC'           TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC  MSC-CIC-TABL IN MSC
               GO TO FIN-MSC-TCYA.
      *     IF MSC-COD-DAT1 IN MSC(2) NOT = 'S'
           MOVE 1 TO WSS-I.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-TCYA IN WSS-VARI(WSS-I).
       LUP-MSC-TCYA.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS OR
              MSC-CIC-TMSC IN MSC NOT = 'TCYA'
               GO TO FIN-MSC-TCYA.
      *     IF MSC-CIC-MSCL IN MSC = 'TCYACC' AND
      *        MSC-COD-DAT1 IN MSC(2) NOT = 'S'
           IF MSC-CIC-MSCL IN MSC = 'TCYACC'
               ADD 1 TO WSS-I
               MOVE MSC-COD-CMSC IN MSC TO WSS-IND-TCYA
                                                   IN WSS-VARI(WSS-I).
           GO TO LUP-MSC-TCYA.
       FIN-MSC-TCYA.
           EXIT.
      ******************************************************************
       LUP-PUT-MCC SECTION.
      *-------------------
       INI-LUP-OPC-MCC.
      * Programacion de Ciclo para Registro OPC
           MOVE WSS-FEC-FFEC IN WSS-VARI TO OPC-FEC-FCOL IN OPC.
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS OPC EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI ' A '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO INI-LUP-CAN-MCC.
           MOVE 1 TO WSS-IND-PROC IN WSS-VARI.
       LUP-OPC-MCC.
      * Proceso de Seleccion y Clasificacion a Partir del Registro OPC
           IF NOT FIO-STAT-OKS
               GO TO INI-LUP-CAN-MCC.
      * Chequea Estado de la Operacion de Credito
           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO NXT-OPC-MCC.
      * Chequea Oficina del Parametro con Oficina de Credito
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFIC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   GO TO NXT-OPC-MCC.
           ADD  1  TO WSS-IND-ROPC IN WSS-VARI.
      * Busca los Abonos para Operacion de Credito Determinada
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE OPC-CIC-IOPC IN OPC TO CYA-CIC-IOPC IN CYA.
           MOVE ZEROES              TO CYA-NUM-ICAN IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-CIC-IOPC IN CYA NOT = OPC-CIC-IOPC IN OPC OR
              CYA-NUM-ICAN IN CYA NOT = ZEROES
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    CYANX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   OPC-KEY-IOPC IN OPC
               GO TO NXT-OPC-MCC.
       LUP-OPC-CYA.
           PERFORM SEL-REG-CYA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-CIC-IOPC IN CYA NOT = OPC-CIC-IOPC IN OPC OR
              CYA-NUM-ICAN IN CYA NOT = ZEROES
               GO TO NXT-OPC-MCC.
           GO TO LUP-OPC-CYA.
       NXT-OPC-MCC.
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF OPC-FEC-FCOL IN OPC > WSS-FEC-FFET IN WSS-VARI
               GO TO INI-LUP-CAN-MCC.
           GO TO LUP-OPC-MCC.
       INI-LUP-CAN-MCC.
      * Programacion de Ciclo para Registro CAN
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CAN-FEC-FCAN IN CAN.
           MOVE 'CAN-FEC-FCAN' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS OR
              CAN-FEC-FCAN IN CAN > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS CAN EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI ' A '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-LUP-REG-MCC.
           MOVE 2 TO WSS-IND-PROC IN WSS-VARI.
       LUP-CAN-MCC.
      * Proceso de Seleccion y Clasificacion a Partir del Registro CAN
           IF NOT FIO-STAT-OKS
               GO TO FIN-LUP-REG-MCC.
      * Chequea Estado de la Cancelacion
           IF CAN-MSC-STAT IN CAN NOT = 'A'
               GO TO NXT-CAN-MCC.
      * Chequea Oficina del Parametro con Oficina de la Cancelacion
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFIC IN WSS-VARI NOT = CAN-COD-OFCA IN CAN
                   GO TO NXT-CAN-MCC.
           ADD  1  TO WSS-IND-RCAN IN WSS-VARI.
      * Busca los Cargos para Cancelaciones de Operacion de Credito Dete
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE CAN-KEY-ICAN IN CAN TO CYA-KEY-ICAN IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-KEY-ICAN IN CYA NOT = CAN-KEY-ICAN IN CAN
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CAN    CYANX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CAN-KEY-ICAN IN CAN
               GO TO NXT-CAN-MCC.
       LUP-CAN-CYA.
           PERFORM SEL-REG-CYA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-KEY-ICAN IN CYA NOT = CAN-KEY-ICAN IN CAN
               GO TO NXT-CAN-MCC.
           GO TO LUP-CAN-CYA.
       NXT-CAN-MCC.
           MOVE 'CAN-FEC-FCAN' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF CAN-FEC-FCAN IN CAN > WSS-FEC-FFET IN WSS-VARI
               GO TO FIN-LUP-REG-MCC.
           GO TO LUP-CAN-MCC.
       FIN-LUP-REG-MCC.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro MCC al Procesar Varios Regis
       SEL-REG-CYA SECTION.
       INI-SEL-REG-CYA.
           ADD 1 TO WSS-IND-RCYA IN WSS-VARI.
      * Cargo Automatico Cuenta Corriente
           IF CYA-IND-MAUT IN CYA = 'S'
               GO TO FIN-SEL-REG-CYA.
      * Busca Indicador Cuenta Corriente en Tabla
           MOVE 1 TO WSS-I.
       BUS-IND-TCYA.
           IF WSS-IND-TCYA IN WSS-VARI(WSS-I) NOT = CYA-IND-TCYA IN CYA
               ADD 1 TO WSS-I
               IF WSS-I > 10
                   ADD 1 TO WSS-IND-RBAD IN WSS-VARI
                   GO TO FIN-SEL-REG-CYA
               ELSE
                   GO TO BUS-IND-TCYA.
           PERFORM GEN-REG-MCC.
       FIN-SEL-REG-CYA.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro MCC al Procesar Varios Regis
       GEN-REG-MCC SECTION.
       INI-GEN-REG-MCC.
      * Formacion del Registro MCC
           IF WSS-IND-PROC IN WSS-VARI = 1
               MOVE '5' TO MCC-IND-SIST IN MCC
           ELSE
               MOVE '7' TO MCC-IND-SIST IN MCC.
           MOVE CYA-IIC-ICYA IN CYA TO MCC-GLS-ICTA IN MCC.
           MOVE '9'                 TO MCC-IND-IMPU IN MCC.
           MOVE SPACES              TO MCC-GLS-FIL1 IN MCC
           MOVE CYA-SGV-LIQU IN CYA TO MCC-VAL-MOVI IN MCC.
           MOVE '2'                 TO MCC-COD-MOVI IN MCC.
           MOVE '00'                TO MCC-COD-CAJE IN MCC.
           MOVE SPACES              TO MCC-GLS-FIL2 IN MCC.
           IF WSS-IND-PROC IN WSS-VARI = 1
               MOVE OPC-NUM-DCOL IN OPC TO MCC-NUM-DMOV IN MCC
               MOVE OPC-NUM-MCOL IN OPC TO MCC-NUM-MMOV IN MCC
               MOVE OPC-NUM-ACOL IN OPC TO MCC-NUM-AMOV IN MCC
           ELSE
               MOVE CAN-NUM-DCAN IN CAN TO MCC-NUM-DMOV IN MCC
               MOVE CAN-NUM-MCAN IN CAN TO MCC-NUM-MMOV IN MCC
               MOVE CAN-NUM-ACAN IN CAN TO MCC-NUM-AMOV IN MCC.
           MOVE SPACES  TO MCC-IND-CAUT IN MCC
                           MCC-GLS-FIL3 IN MCC
                           MCC-COD-SUCO IN MCC
                           MCC-GLS-FIL4 IN MCC.
           MOVE '000'   TO MCC-COD-SUCM IN MCC.
           MOVE SPACES  TO MCC-GLS-FIL5 IN MCC
                           MCC-IND-MCAR IN MCC
                           MCC-GLS-FIL6 IN MCC.
      *     IF WSS-IND-PROC IN WSS-VARI = 1
      *         IF CYA-SGV-LIQU IN CYA > 0
      *             MOVE  '2'  TO MCC-COD-MOVI IN MCC
      *         ELSE
      *             MOVE  '5'  TO MCC-COD-MOVI IN MCC
      *     ELSE
      *         IF CYA-SGV-LIQU IN CYA > 0
      *             MOVE  '5'  TO MCC-COD-MOVI IN MCC
      *         ELSE
      *             MOVE  '2'  TO MCC-COD-MOVI IN MCC.
      * Genera el Registro MCC
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-MCC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'MCC    NOPUT' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC
               DISPLAY 'REGISTRO MCC : ' MCC
               ADD 1 TO WSS-IND-RERR IN WSS-VARI
           ELSE
               ADD 1 TO WSS-IND-ROKS IN WSS-VARI.
       FIN-GEN-REG-MCC.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OPC LEIDOS   : ' WSS-IND-ROPC IN WSS-VARI.
           DISPLAY 'REGISTROS CAN LEIDOS   : ' WSS-IND-RCAN IN WSS-VARI.
           DISPLAY 'REGISTROS CYA LEIDOS   : ' WSS-IND-RCYA IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-IND-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS MCC GRABADOS : ' WSS-IND-ROKS IN WSS-VARI.
           DISPLAY 'REGISTROS NO GRABADOS  : ' WSS-IND-RERR IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-MCC.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
