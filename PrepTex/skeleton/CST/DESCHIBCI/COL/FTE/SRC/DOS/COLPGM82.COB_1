       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGM82.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    29-MAY-1992   (Ultima Modificacion).

      * Programa Generador de Archivo M82.

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-M82-REQA==.
      *<<<< WSS
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY COLWGVEN.
       COPY GNSBRMSC.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY TABBRCOM.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRVEN.
       COPY COLBREVC.
       COPY COLBRICG.
       COPY COLBRTOC.
       COPY COLBRRCV.
       COPY COLBRM82.
       01  WSS-VARI.
           03 WSS-FEC-FINF.
              05 WSS-NUM-DINF                     PIC 9(02).
              05 WSS-NUM-MINF                     PIC 9(02).
              05 WSS-NUM-SINF                     PIC 9(02).
              05 WSS-NUM-AINF                     PIC 9(02).
           03 WSS-FEC-FCAM.
              05 WSS-SAM-FCAM.
                 07 WSS-NUM-SCAM                  PIC 9(02).
                 07 WSS-NUM-ACAM                  PIC 9(02).
                 07 WSS-NUM-MCAM                  PIC 9(02).
              05 WSS-NUM-DCAM                     PIC 9(02).
           03 WSS-FEC-FVEN.
              05 WSS-SAM-FVEN.
                 07 WSS-NUM-SVEN                  PIC 9(02).
                 07 WSS-NUM-AVEN                  PIC 9(02).
                 07 WSS-NUM-MVEN                  PIC 9(02).
              05 WSS-NUM-DVEN                     PIC 9(02).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-IND-IDLC                        PIC 9(03).
           03 WSS-IND-ROPC           VALUE 0      PIC 9(05).
           03 WSS-IND-RVEN           VALUE 0      PIC 9(05).
           03 WSS-IND-RDLC           VALUE 0      PIC 9(05).
           03 WSS-IND-ROKS           VALUE 0      PIC 9(05).
           03 WSS-IND-RBAD           VALUE 0      PIC 9(05).
           03 WSS-TAB-IDLC.
              05 WSS-NUM-IDLC   OCCURS 999        PIC 9(01).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGM82' TO FIO-PROG.
           PERFORM BUS-FSIS.
           PERFORM OPEN-FILE.
           PERFORM BUS-PAR-M82.
           PERFORM LUP-PUT-M82.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
           COPY GNSBFMSC.
           COPY GNSBFMSG.
           COPY GNSBFTAB.
           COPY TABBFOFI.
           COPY TABBFCAM.
           COPY COLBFOPC.
           COPY COLBFDLC.
           COPY COLBFVEN.
           COPY COLBFEVC.
           COPY COLBFICG.
           COPY COLBFTOC.
           COPY COLBFRCV.
           COPY COLBFM82.
           COPY GNSBBMSG.
           COPY GNSBBTAB.
           COPY TABBBOFI.
           COPY GNSBGFEC.
           COPY GNSBPFEC.
           COPY GNSBGHOY.
           COPY GNSBGPES.
      *SMC-INI 09.09.1993
           COPY COLBGINT.
      *SMC-FIN 09.09.1993
           COPY COLBGVEN.
           COPY GNSBGEND.
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
           COPY GNSBGABT.
      *DAD fin  29-MAY-1992
           COPY GNSBGDTC.
           COPY GNSBGIFD.
           COPY GNSBTABT.
           COPY GNSBBSYS.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-TOC.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-M82.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       BUS-PAR-M82 SECTION.
      *-----------------
       INI-BUS-PAR-M82.
      * Rutina para Capturar y Validar Parametros de Rango de Fechas
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      * Captura Fecha de Generacion
           DISPLAY 'FECHA GENERACION ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINF IN WSS-VARI.
           DISPLAY WSS-FEC-FINF IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Generacion
           DISPLAY WSS-FEC-FINF IN WSS-VARI.
           MOVE WSS-FEC-FINF IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-LE              TO FEC-CHOY.
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992

           MOVE WSS-NUM-DINF IN WSS-VARI TO WSS-NUM-DCAM IN WSS-VARI.
           MOVE WSS-NUM-MINF IN WSS-VARI TO WSS-NUM-MCAM IN WSS-VARI.
           MOVE WSS-NUM-SINF IN WSS-VARI TO WSS-NUM-SCAM IN WSS-VARI.
           MOVE WSS-NUM-AINF IN WSS-VARI TO WSS-NUM-ACAM IN WSS-VARI.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
       FIN-BUS-PAR-M82.
           EXIT.
      ******************************************************************
       LUP-PUT-M82 SECTION.
      *-------------------
       INI-LUP-PUT-M82.
      * Programacion de Ciclo para Registro OPC
           MOVE SPACES         TO OPC-FEC-FCOL IN OPC.
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FCAM IN WSS-VARI
               DISPLAY 'ERROR : NO HAY REGISTROS OPC HASTA FECHA : '
                        WSS-FEC-FINF IN WSS-VARI
               GO TO FIN-LUP-PUT-M82.
       OPC-LUP-PUT-M82.
      * Proceso de Seleccion y Clasificacion a Partir del Registro OPC
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-LUP-PUT-M82.
           ADD  1  TO WSS-IND-ROPC IN WSS-VARI.

      * Busca los Vencimientos para Operacion de Credito Determinada
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE 1                   TO VEN-NUM-IVEN IN VEN
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
      *         MOVE OPC-NUM-IVEN IN OPC TO VEN-NUM-IVEN IN VEN
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'OPC    VENNX' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC ' /KEY : '  VEN-KEY-IVEN
                   GO TO NXT-LUP-PUT-M82
               ELSE
                   NEXT SENTENCE
           ELSE
      *         MOVE  1  TO VEN-NUM-IVEN IN VEN
               PERFORM COL-OBT-VEN
               IF NOT VEN-STAT-OKS
                   MOVE 'COL'          TO MSG-COD-SIST
                   MOVE 'OPC    VENNX' TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC ' /KEY : '  VEN-KEY-IVEN
                   GO TO NXT-LUP-PUT-M82.

           MOVE ZEROES TO WSS-TAB-IDLC IN WSS-VARI.
       VEN-LUP-PUT-M82.
           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FVEN IN WSS-VARI.
           IF WSS-SAM-FVEN IN WSS-VARI > WSS-SAM-FCAM IN WSS-VARI
               GO TO NXT-LUP-PUT-M82.

           IF WSS-SAM-FVEN IN WSS-VARI = WSS-SAM-FCAM IN WSS-VARI
               ADD  1  TO WSS-IND-RVEN IN WSS-VARI
               PERFORM DIS-DLC-VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC NOT = 'S'
               PERFORM COL-FIO-VEN
               IF NOT FIO-STAT-OKS OR
                  OPC-CIC-IOPC IN OPC NOT = VEN-CIC-IOPC IN VEN
                   GO TO NXT-LUP-PUT-M82
               ELSE
                   NEXT SENTENCE
           ELSE
               PERFORM COL-OBT-VEN
               IF VEN-STAT-EOF
                   GO TO NXT-LUP-PUT-M82
               ELSE
               IF NOT VEN-STAT-OKS
                   MOVE 'COL'    TO MSG-COD-SIST
                   MOVE VEN-STAT TO MSG-COD-MENS
                   PERFORM GET-MSG
                   DISPLAY MSG-GLS-DESC ' /KEY : '  VEN-KEY-IVEN
                   GO TO NXT-LUP-PUT-M82.
           GO TO VEN-LUP-PUT-M82.
       NXT-LUP-PUT-M82.
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FCAM IN WSS-VARI
               GO TO FIN-LUP-PUT-M82.
           GO TO OPC-LUP-PUT-M82.
       FIN-LUP-PUT-M82.
           EXIT.
      ******************************************************************
      * Seleccion del Registro para Proceso Mediante Fecha del Listado
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO FIN-SEL-REG-PRO.

      * Lee en Registro TOC Tipo Operaciones Creditos 'Documentos Descon
           MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM COL-FIO-TOC.
           IF NOT FIO-STAT-OKS
               MOVE 'GNS'        TO MSG-COD-SIST
               MOVE 'COD    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' : ' TOC-COD-TABL ' /KEY : '
                       OPC-CIC-IOPC IN OPC
               GO TO FIN-SEL-REG-PRO.
           IF TOC-MSC-OPEC IN TOC NOT = 'D'
               GO TO FIN-SEL-REG-PRO.

      * Mediante BUS-OFI Busca Oficina Obteniendo Codigos de Tabla OFI
           MOVE OPC-COD-OFOC IN OPC TO OFI-COD-OFIC IN OFI.
           PERFORM BUS-OFI.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY MSG-GLS-DESC ' : ' OFI-COD-OFIC ' /KEY : '
                       OPC-CIC-IOPC IN OPC
               GO TO FIN-SEL-REG-PRO.

      * Mediante BUS-TAB Busca Comuna Obteniendo Codigos de Tabla COM
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'COM'               TO TAB-COD-TTAB IN TAB.
           MOVE OFI-COD-COMU IN OFI TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY MSG-GLS-DESC ' : ' TAB-COD-TABL ' /KEY : '
                       OPC-CIC-IOPC IN OPC
               GO TO FIN-SEL-REG-PRO.

      * Obtiene Region de Oficina de Operacion del Credito
           MOVE TAB TO COM.
           MOVE COM-COD-REGI IN COM TO M82-COD-REGI IN M82.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      ******************************************************************
      * Clasifica Documento Legal Operaciones de Creditos segun Vencimie
       DIS-DLC-VEN SECTION.
       INI-DIS-DLC-VEN.
           IF VEN-NUM-IDLC IN VEN > 0
               MOVE VEN-NUM-IDLC IN VEN TO WSS-IND-IDLC IN WSS-VARI
           ELSE
               MOVE VEN-NUM-IVCT IN VEN TO WSS-IND-IDLC IN WSS-VARI.
           IF WSS-NUM-IDLC IN WSS-VARI(WSS-IND-IDLC) = 0
               PERFORM BUS-DLC-M82.
       FIN-DIS-DLC-VEN.
           EXIT.
      ******************************************************************
      * Busca Documentos de Credito y Clasifica segun Situacion Cobranza
      * Formando y Generando Registro M82
       BUS-DLC-M82 SECTION.
       INI-BUS-DLC-M82.
           MOVE OPC-CIC-IOPC IN OPC      TO DLC-CIC-IOPC IN DLC.
           MOVE WSS-IND-IDLC IN WSS-VARI TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    DLCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' /KEY : '  DLC-KEY-IDLC
               GO TO FIN-BUS-DLC-M82.
       MED-BUS-DLC-M82.
           ADD 1 TO WSS-IND-RDLC IN WSS-VARI.
           IF VEN-FEC-FIMV IN VEN > WSS-FEC-FCAM IN WSS-VARI OR
              DLC-VAL-SDLC IN DLC NOT > ZEROES
               MOVE  1  TO WSS-NUM-IDLC IN WSS-VARI(WSS-IND-IDLC)
               MOVE 'V' TO M82-IND-SCBZ IN M82
           ELSE
           IF VEN-FEC-FIMV IN VEN NOT > WSS-FEC-FCAM IN WSS-VARI
               IF DLC-IND-SCBZ IN DLC = 'PROT'
                   MOVE  1  TO WSS-NUM-IDLC IN WSS-VARI(WSS-IND-IDLC)
                   MOVE 'P' TO M82-IND-SCBZ IN M82
               ELSE
                   MOVE  1  TO WSS-NUM-IDLC IN WSS-VARI(WSS-IND-IDLC)
                   MOVE 'I' TO M82-IND-SCBZ IN M82.

      * Complementa Formacion del Registro M82
           MOVE OPC-FEC-FCOL IN OPC TO M82-FEC-FM82 IN M82.
           MOVE OPC-COD-VCOC IN OPC TO M82-COD-VCOC IN M82.
           MOVE SPACES              TO M82-GLS-DISP IN M82.
           IF DLC-VAL-SDLC IN DLC NOT > ZEROES
               MOVE DLC-VAL-NDLC IN DLC TO M82-VAL-SM82 IN M82
           ELSE
               MOVE DLC-VAL-SDLC IN DLC TO M82-VAL-SM82 IN M82.

      * Genera el Registro M82
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-M82.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'M82    NOPUT' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC
               DISPLAY 'REGISTRO M82 : ' M82
               ADD 1 TO WSS-IND-RBAD IN WSS-VARI
           ELSE
               ADD 1 TO WSS-IND-ROKS IN WSS-VARI.
       FIN-BUS-DLC-M82.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OPC LEIDOS   : ' WSS-IND-ROPC IN WSS-VARI.
           DISPLAY 'REGISTROS VEN LEIDOS   : ' WSS-IND-RVEN IN WSS-VARI.
           DISPLAY 'REGISTROS DLC LEIDOS   : ' WSS-IND-RDLC IN WSS-VARI.
           DISPLAY 'REGISTROS M82 GRABADOS : ' WSS-IND-ROKS IN WSS-VARI.
           DISPLAY 'REGISTROS NO GRABADOS  : ' WSS-IND-RBAD IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-TOC.
           PERFORM COL-FIO-M82.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
