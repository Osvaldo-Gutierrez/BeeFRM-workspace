       IDENTIFICATION DIVISION.
      *=======================*
       PROGRAM-ID.      COLPGSEG.
       AUTHOR.          BEE(JGM).
       DATE-WRITTEN.     01-SEPTIEMBRE-1999.
      *
      *
       ENVIRONMENT DIVISION.
      *====================*
       CONFIGURATION SECTION.
      *---------------------*
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------*
       FILE-CONTROL.
      *     SELECT COLBCI ASSIGN TO COLBCI.

       DATA DIVISION.
      *=============*
       WORKING-STORAGE SECTION.
      *-----------------------*
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.

       COPY COLBROPC.
       COPY COLBROPS.
       COPY COLBRSEG.

       COPY COLBRRDC.
       COPY COLBRDLC.
       COPY SGCBRDBC.
       COPY SGCBRDCP.

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGFEC.
       COPY GNSWGCPT.
       COPY GNSWGHOY.
       COPY GNSWGSYS.

       01  WSS-VARI.
           03  WSS-NUM-PLCO                       PIC 9(03).
           03  WSS-GLS-DAT3-MINMAX-I.
               05 FILLER                          PIC X(10).
               05 WSS-AGNO-NUM-MINMAX-I.
                  07 WSS-MIN-AGNO-I               PIC X(02).
                  07 WSS-MAX-AGNO-I               PIC X(02).

           03  WSS-NUM-LOPC           PIC 9(08).
           03  WSS-NUM-GSEG           PIC 9(08).
      *
           03  WSS-NUM-IDLC           PIC 9(03).
           03  WSS-COD-MENS           PIC X(20).
           03  WSS-GLS-MES1           PIC X(03).
           03  WSS-NUM-MES1  REDEFINES WSS-GLS-MES1 PIC 9(03).
           03  WSS-GLS-MES2           PIC X(03).
           03  WSS-NUM-MES2  REDEFINES WSS-GLS-MES2 PIC 9(03).
           03  WSS-FEC-FPRM.
               05  WSS-NUM-MPRM       PIC 9(02).
               05  WSS-NUM-SPRM       PIC 9(02).
               05  WSS-NUM-APRM       PIC 9(02).
           03  WSS-FEC-FPRO.
               05  WSS-NUM-DPRO       PIC 9(02).
               05  WSS-NUM-MPRO       PIC 9(02).
               05  WSS-NUM-SPRO       PIC 9(02).
               05  WSS-NUM-APRO       PIC 9(02).
           03  WSS-FEC-FAUX.
               05  WSS-NUM-SAUX       PIC 9(02).
               05  WSS-NUM-AAUX       PIC 9(02).
               05  WSS-NUM-MAUX       PIC 9(02).
               05  WSS-NUM-DAUX       PIC 9(02).
           03  WSS-FEC-FEC2.
               05  WSS-NUM-SEC2       PIC 9(02).
               05  WSS-NUM-AEC2       PIC 9(02).
               05  WSS-NUM-MEC2       PIC 9(02).
               05  WSS-NUM-DEC2       PIC 9(02).
           03  WSS-VEC-TRMO.
               05  WSS-GLS-TRMO  OCCURS 10  PIC X(03).
           03  WSS-VEC-TRM02 REDEFINES WSS-VEC-TRMO.
               05  WSS-NUM-TRMO  OCCURS 10  PIC 9(03).
           03  WSS-CNTD          VALUE 0    PIC 9(02).
           03  WSS-CMAX          VALUE 0    PIC 9(02).


       PROCEDURE DIVISION.
      *==================*
       MAIN SECTION.
      *------------*
       INI-MAIN.
      *--------*
           COPY GNSBGEDB.
           MOVE 'COLPGSEG' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
      *
           PERFORM ABRE-ARCHIVOS.
           PERFORM ACEPTA-PARAM.
           PERFORM PROCESA-OPC.
      *
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-SEG.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPS.
      *
           PERFORM ESTADADISTICAS.
      *
       FIN-MAIN.
           PERFORM GNS-PRO-END.
      *
       ABRE-ARCHIVOS SECTION.
      *---------------------*
       INI-ABRE-ARCHIVOS.
      *Archivo secuencial SEG
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-SEG.
      *
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPS.
       FIN-ABRE-ARCHIVOS.
           EXIT.

       ACEPTA-PARAM SECTION.
       INI-ACEPTA-PARAM.

      * Limpiar vector de tramos
           PERFORM LIMPIA-VECTOR.
      * Guardar tramos del Seguro en vector para posterior uso
           MOVE 'COL' TO TAB-COD-SIST FIO-SIST.
           MOVE 'SDV' TO TAB-COD-TTAB IN TAB.
           MOVE SPACES TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT (FIO-STAT-OKS AND
                   TAB-COD-TTAB IN TAB = 'SDV')
               DISPLAY 'ERROR EN LECTURA DE TRAMOS DE TABLA SDV'
               GO TO FIN-ACEPTA-PARAM.

       LUP-TRMO.
           IF TAB-IND-VIGE IN TAB NOT = 'S' OR
              TAB-COD-CTAB IN TAB = SPACES
               GO TO SIG-TRMO.
           ADD 1                    TO WSS-CNTD IN WSS-VARI.
           MOVE TAB-CIC-CTAB IN TAB TO WSS-GLS-TRMO(WSS-CNTD).
       SIG-TRMO.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'SDV'
               GO TO LUP-TRMO.

       NXT-TRMO.
      * BUSCA VALORES DE CNAVAL.
           PERFORM BUS-MINMAX-SEGURO.
           IF  WSS-COD-MENS IN WSS-VARI > SPACES
               DISPLAY WSS-COD-MENS IN WSS-VARI
               GO TO FIN-ACEPTA-PARAM.
      *
      * Captura fecha de Proceso
           ACCEPT WSS-FEC-FPRM IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO: ' WSS-FEC-FPRM IN WSS-VARI.
           MOVE 01                       TO WSS-NUM-DPRO IN WSS-VARI.
           MOVE WSS-NUM-MPRM IN WSS-VARI TO WSS-NUM-MPRO IN WSS-VARI.
           MOVE WSS-NUM-SPRM IN WSS-VARI TO WSS-NUM-SPRO IN WSS-VARI.
           MOVE WSS-NUM-APRM IN WSS-VARI TO WSS-NUM-APRO IN WSS-VARI.
      * Validacion de parametro
      *VAL-FEC Valida fecha proceso
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-N               TO FEC-CHOY.
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN VALIDACION F. DE PROCESO : '
                        WSS-FEC-FPRO IN WSS-VARI '  ' FEC-MENS
              GO TO FIN-ACEPTA-PARAM.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO FEC-ITM1.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO FEC-ITM2.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO FEC-ITM3.
           MOVE WSS-NUM-APRO IN WSS-VARI TO FEC-ITM4.
           MOVE 01                       TO FEC-NMES.
           MOVE FEC-RST-MES              TO FEC-CMND.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           PERFORM CAL-FEC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL RESTAR 1 A FECHA PROCESO'
               GO TO FIN-ACEPTA-PARAM.

           MOVE 01                       TO WSS-NUM-DAUX IN WSS-VARI.
           MOVE FEC-ITM2                 TO WSS-NUM-MAUX IN WSS-VARI.
           MOVE FEC-ITM3                 TO WSS-NUM-SAUX IN WSS-VARI.
           MOVE FEC-ITM4                 TO WSS-NUM-AAUX IN WSS-VARI.
       FIN-ACEPTA-PARAM.
           EXIT.

       PROCESA-OPC SECTION.
       INI-PROCESA-OPC.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM COL-FIO-OPS.

       LUP-INP-OPC.
           MOVE OPS  TO OPC.
           IF NOT FIO-STAT-OKS
              GO TO FIN-PROCESA-OPC.

      *
           IF NOT( OPC-NUM-MINP IN OPC = WSS-NUM-MAUX IN WSS-VARI AND
                   OPC-NUM-SINP IN OPC = WSS-NUM-SAUX IN WSS-VARI AND
                   OPC-NUM-AINP IN OPC = WSS-NUM-AAUX IN WSS-VARI AND
                   OPC-MSC-STAT IN OPC = 'A' )
                    GO TO NXT-INP-OPC.
      *
           IF OPC-COD-SEGD IN OPC NOT = 'SINSEG' AND
              OPC-COD-SEGD IN OPC NOT = 'CNAVAL' AND
              OPC-COD-SEGD IN OPC NOT = 'SEDECE'
              GO TO NXT-INP-OPC.
      *
           ADD 1 TO WSS-NUM-LOPC IN WSS-VARI.
      *Informa DD.(REGSEG)
           MOVE ZEROES              TO WSS-NUM-IDLC IN WSS-VARI.
           MOVE OPC-CIC-IOPC IN OPC TO SEG-CIC-IOPC IN SEG.
           MOVE OPC-VAL-CRED IN OPC TO SEG-VAL-CRED IN SEG.
           MOVE ZEROES              TO SEG-VAL-SEGD IN SEG.
           MOVE 'D'                 TO SEG-IND-IDLC IN SEG.
      *
      * Obtiene glosa de moneda.
           PERFORM OBT-GLS-MONE.
      *
      * Obtiene Nombre y Rut
           PERFORM BUSCA-RUT-NOMBRE.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO DIS-COD-MENS.
           MOVE ZEROES TO WSS-NUM-MES2 IN WSS-VARI.
      *
           PERFORM CALC-FECHA-NAC-DD.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO DIS-COD-MENS.
      *
      *
           PERFORM CALC-FECHA-CREDITO.
           PERFORM BUS-FECHA-TERMINO.
           PERFORM CALC-TRAMO.
           PERFORM GRABA-REG-SEG.

      * Deudor Indirecto.
           IF  OPC-VAL-SEGD IN OPC = ZEROES
               GO TO NXT-INP-OPC.
      *
      *INFORMA DI.(REGSEG)
           MOVE 1                   TO WSS-NUM-IDLC IN WSS-VARI.
           MOVE OPC-CIC-IOPC IN OPC TO SEG-CIC-IOPC IN SEG.
           MOVE OPC-VAL-CRED IN OPC TO SEG-VAL-CRED IN SEG.
           MOVE OPC-VAL-SEGD IN OPC TO SEG-VAL-SEGD IN SEG.
           MOVE 'I'                 TO SEG-IND-IDLC IN SEG.

      * Obtiene glosa de moneda.
           PERFORM OBT-GLS-MONE.
      *
      * Obtiene Nombre y Rut
           PERFORM BUSCA-RUT-NOMBRE.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO DIS-COD-MENS.
      *
           PERFORM CALC-FECHA-NAC.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO DIS-COD-MENS.
      *
           PERFORM CALC-FECHA-CREDITO.
           IF WSS-COD-MENS IN WSS-VARI > SPACES
              GO TO DIS-COD-MENS.
      *
           PERFORM BUS-FECHA-TERMINO.

           PERFORM CALC-TRAMO.
           PERFORM GRABA-REG-SEG.
           GO TO NXT-INP-OPC.

       DIS-COD-MENS.
            DISPLAY WSS-COD-MENS IN WSS-VARI  ' '
                    OPC-CIC-IOPC IN OPC.
       NXT-INP-OPC.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           GO TO LUP-INP-OPC.
       FIN-PROCESA-OPC.
           EXIT.


       OBT-GLS-MONE SECTION.
      *--------------------*
       INI-OBT-GLS-MONE.
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'VLR'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS NOT = SPACES
              MOVE MSG-GLS-DCOR TO SEG-GLS-MONE IN SEG
           ELSE
              MOVE TAB-GLS-ABRV TO SEG-GLS-MONE IN SEG.
       FIN-OBT-GLS-MONE.
           EXIT.

       BUSCA-RUT-NOMBRE SECTION.
      *------------------------*
       INI-BUSCA-RUT-NOMBRE.

           MOVE SPACES                   TO WSS-COD-MENS.
           MOVE OPC-CIC-IOPC IN OPC      TO RDC-CIC-IOPC IN RDC.
           MOVE WSS-NUM-IDLC IN WSS-VARI TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC'           TO FIO-AKEY.
           MOVE FIO-GET-NLS              TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
              RDC-CIC-IOPC IN RDC = OPC-CIC-IOPC IN OPC AND
              RDC-NUM-IDLC IN RDC = WSS-NUM-IDLC IN WSS-VARI)
              MOVE 'NO EXISTE RDC ' TO WSS-COD-MENS
              GO TO FIN-BUSCA-RUT-NOMBRE.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               MOVE 'NO EXISTE DBC ' TO WSS-COD-MENS
               GO TO FIN-BUSCA-RUT-NOMBRE.

           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN            TO DBC-GLS-NOMC IN DBC.
       FIN-BUSCA-RUT-NOMBRE.
           EXIT.
      *

       CALC-FECHA-NAC SECTION.
      *-------------------------*
       INI-CALC-FECHA-NAC.
      * Llena fecha de nacimiento en archivo SEG
           MOVE SPACES                   TO WSS-COD-MENS.
           MOVE DBC-CIC-ICLI IN DBC      TO DCP-CIC-ICLI IN DCP.
           MOVE FIO-GET-KEY              TO FIO-CMND.
           PERFORM SGC-FIO-DCP.
           IF NOT FIO-STAT-OKS
              MOVE  'ERROR EN LECTURA DE DCP' TO WSS-COD-MENS
              GO TO FIN-CALC-FECHA-NAC.

           IF DCP-FEC-FNAC IN DCP NOT > ZEROES
              GO TO FIN-CALC-FECHA-NAC.
      *
           MOVE DCP-NUM-DNAC IN DCP TO FEC-ITM1.
           MOVE DCP-NUM-MNAC IN DCP TO FEC-ITM2.
           MOVE DCP-NUM-SNAC IN DCP TO FEC-ITM3.
           MOVE DCP-NUM-ANAC IN DCP TO FEC-ITM4.
           MOVE FEC-SUM-ANO         TO FEC-CMND.
           MOVE WSS-MAX-AGNO-I      TO FEC-NANO.
           PERFORM CAL-FEC
           IF NOT FEC-STAT-OKS
              GO TO FIN-CALC-FECHA-NAC.
      *
           MOVE FEC-ITM1 TO WSS-NUM-DEC2 IN WSS-VARI.
           MOVE FEC-ITM2 TO WSS-NUM-MEC2 IN WSS-VARI.
           MOVE FEC-ITM3 TO WSS-NUM-SEC2 IN WSS-VARI.
           MOVE FEC-ITM4 TO WSS-NUM-AEC2 IN WSS-VARI.

           IF WSS-FEC-FEC2 IN WSS-VARI > OPC-FEC-FCOL IN OPC
              MOVE OPC-NUM-DCOL IN OPC   TO FEC-DEC1
              MOVE OPC-NUM-MCOL IN OPC   TO FEC-MEC1
              MOVE OPC-NUM-SCOL IN OPC   TO FEC-SEC1
              MOVE OPC-NUM-ACOL IN OPC   TO FEC-AEC1

              MOVE WSS-NUM-DEC2 IN WSS-VARI TO FEC-DEC2
              MOVE WSS-NUM-MEC2 IN WSS-VARI TO FEC-MEC2
              MOVE WSS-NUM-SEC2 IN WSS-VARI TO FEC-SEC2
              MOVE WSS-NUM-AEC2 IN WSS-VARI TO FEC-AEC2

              MOVE FEC-FORM-FEC           TO FEC-FORM
              MOVE FEC-DIF-MES            TO FEC-CMND
              PERFORM CAL-FEC
              IF FEC-NDIA > 0
                 ADD 1 TO FEC-NMES
                 MOVE FEC-NMES TO WSS-NUM-MES1 IN WSS-VARI
              ELSE
                 MOVE FEC-NMES TO WSS-NUM-MES1 IN WSS-VARI.

       FIN-CALC-FECHA-NAC.
           EXIT.

       CALC-FECHA-NAC-DD SECTION.
      *-------------------------*
       INI-CALC-FECHA-NAC-DD.
      * Llena fecha de nacimiento en archivo SEG
           MOVE SPACES                   TO WSS-COD-MENS.
           MOVE DBC-CIC-ICLI IN DBC      TO DCP-CIC-ICLI IN DCP.
           MOVE FIO-GET-KEY              TO FIO-CMND.
           PERFORM SGC-FIO-DCP.
           IF NOT FIO-STAT-OKS
              MOVE  'ERROR EN LECTURA DE DCP' TO WSS-COD-MENS
              GO TO FIN-CALC-FECHA-NAC-DD.

           IF DCP-FEC-FNAC IN DCP NOT > ZEROES
              GO TO FIN-CALC-FECHA-NAC-DD.
      *
           MOVE ZEROES   TO WSS-NUM-MES1 IN WSS-VARI.
       FIN-CALC-FECHA-NAC-DD.
           EXIT.
       CALC-FECHA-CREDITO SECTION.
      *------------------------------*
       INI-CALC-FECHA-CREDITO.
      *Calculo de meses del credito
           MOVE SPACES                   TO WSS-COD-MENS.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE ZEROES              TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT (FIO-STAT-OKS AND
              OPC-CIC-IOPC IN OPC = DLC-CIC-IOPC IN DLC)
               MOVE 'NO EXISTE DLC '    TO WSS-COD-MENS
               GO TO FIN-CALC-FECHA-CREDITO.

      * Llena fecha de curse real en archivo SEG

           MOVE OPC-NUM-DCOL IN OPC      TO FEC-DEC1.
           MOVE OPC-NUM-MCOL IN OPC      TO FEC-MEC1.
           MOVE OPC-NUM-SCOL IN OPC      TO FEC-SEC1.
           MOVE OPC-NUM-ACOL IN OPC      TO FEC-AEC1.

           MOVE DLC-NUM-DMAV IN DLC TO FEC-DEC2.
           MOVE DLC-NUM-MMAV IN DLC TO FEC-MEC2.
           MOVE DLC-NUM-SMAV IN DLC TO FEC-SEC2.
           MOVE DLC-NUM-AMAV IN DLC TO FEC-AEC2.

           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-DIF-MES              TO FEC-CMND.
           PERFORM CAL-FEC.
           IF FEC-NDIA > 0
               ADD 1 TO FEC-NMES.

           MOVE FEC-NMES TO WSS-NUM-MES2 IN WSS-VARI.
       FIN-CALC-FECHA-CREDITO.
           EXIT.
      *
       BUS-FECHA-TERMINO SECTION.
      *-------------------------*
       INI-BUS-FECHA-TERMINO.
      * Llena fecha de termino de cobertura en archivo SEG
           IF WSS-NUM-MES1 IN WSS-VARI = ZEROES
           MOVE WSS-NUM-MES2 IN WSS-VARI TO WSS-NUM-PLCO IN WSS-VARI
             MOVE DLC-NUM-DMAV IN DLC TO SEG-NUM-DTER IN SEG
             MOVE DLC-NUM-MMAV IN DLC TO SEG-NUM-MTER IN SEG
             MOVE DLC-NUM-SMAV IN DLC TO SEG-NUM-STER IN SEG
             MOVE DLC-NUM-AMAV IN DLC TO SEG-NUM-ATER IN SEG
             GO TO FIN-BUS-FECHA-TERMINO.

      * Llena fecha de termino de cobertura en archivo SEG
           IF WSS-NUM-MES2 IN WSS-VARI = ZEROES
           MOVE WSS-NUM-MES1 IN WSS-VARI TO WSS-NUM-PLCO IN WSS-VARI
             MOVE WSS-NUM-DEC2 IN WSS-VARI TO SEG-NUM-DTER IN SEG
             MOVE WSS-NUM-MEC2 IN WSS-VARI TO SEG-NUM-MTER IN SEG
             MOVE WSS-NUM-SEC2 IN WSS-VARI TO SEG-NUM-STER IN SEG
             MOVE WSS-NUM-AEC2 IN WSS-VARI TO SEG-NUM-ATER IN SEG
             GO TO FIN-BUS-FECHA-TERMINO.

      * Llena fecha de termino de cobertura en archivo SEG
           IF WSS-NUM-MES1 IN WSS-VARI < WSS-NUM-MES2 IN WSS-VARI
           MOVE WSS-NUM-MES1 IN WSS-VARI TO WSS-NUM-PLCO IN WSS-VARI
               MOVE WSS-NUM-DEC2 IN WSS-VARI TO SEG-NUM-DTER IN SEG
               MOVE WSS-NUM-MEC2 IN WSS-VARI TO SEG-NUM-MTER IN SEG
               MOVE WSS-NUM-SEC2 IN WSS-VARI TO SEG-NUM-STER IN SEG
               MOVE WSS-NUM-AEC2 IN WSS-VARI TO SEG-NUM-ATER IN SEG
               GO TO FIN-BUS-FECHA-TERMINO.

      * Llena fecha de termino de cobertura en archivo SEG
           MOVE WSS-NUM-MES2 IN WSS-VARI TO WSS-NUM-PLCO IN WSS-VARI.
           MOVE DLC-NUM-DMAV IN DLC      TO SEG-NUM-DTER IN SEG.
           MOVE DLC-NUM-MMAV IN DLC      TO SEG-NUM-MTER IN SEG.
           MOVE DLC-NUM-SMAV IN DLC      TO SEG-NUM-STER IN SEG.
           MOVE DLC-NUM-AMAV IN DLC      TO SEG-NUM-ATER IN SEG.
       FIN-BUS-FECHA-TERMINO.
           EXIT.
       CALC-TRAMO SECTION.
      *------------------*
       INI-CALC-TRAMO.
           MOVE 1 TO WSS-CNTD IN WSS-VARI.
       LUP-CALC-TRMO.
           IF WSS-CNTD IN WSS-VARI NOT > 10 AND
              WSS-NUM-PLCO IN WSS-VARI NOT > WSS-NUM-TRMO(WSS-CNTD)
              MOVE WSS-NUM-TRMO(WSS-CNTD) TO SEG-NUM-TRMO IN SEG
           ELSE
           IF WSS-CNTD IN WSS-VARI NOT > 10
               ADD 1 TO WSS-CNTD IN WSS-VARI
               GO TO LUP-CALC-TRMO.
           IF WSS-CNTD IN WSS-VARI > 10
               DISPLAY 'NO EXISTE TRAMO QUE CONTENGA A DIF. '
                       'DE MESES ' WSS-NUM-PLCO IN WSS-VARI.
       FIN-CALC-TRAMO.
           EXIT.


       GRABA-REG-SEG SECTION.
      *---------------------*
       INI-GRABA-REG-SEG.
           MOVE OPC-NUM-DCOL IN OPC TO SEG-NUM-DCOL IN SEG.
           MOVE OPC-NUM-MCOL IN OPC TO SEG-NUM-MCOL IN SEG.
           MOVE OPC-NUM-SCOL IN OPC TO SEG-NUM-SCOL IN SEG.
           MOVE OPC-NUM-ACOL IN OPC TO SEG-NUM-ACOL IN SEG.
      *
           MOVE DCP-NUM-DNAC IN DCP TO SEG-NUM-DNAC IN SEG.
           MOVE DCP-NUM-MNAC IN DCP TO SEG-NUM-MNAC IN SEG.
           MOVE DCP-NUM-SNAC IN DCP TO SEG-NUM-SNAC IN SEG.
           MOVE DCP-NUM-ANAC IN DCP TO SEG-NUM-ANAC IN SEG.
      *
           MOVE DBC-NUM-ICLI IN DBC TO SEG-NUM-ICLI IN SEG.
           MOVE DBC-VRF-ICLI IN DBC TO SEG-VRF-ICLI IN SEG.
           MOVE DBC-GLS-NOMC IN DBC TO SEG-GLS-NOMC IN SEG.
      *ALD-BCI-INI/07032000
           MOVE ZEROES              TO SEG-FIL-ICLI IN SEG.
      *ALD-BCI-FIN/07032000
      *
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-SEG.
           IF NOT FIO-STAT-OKS
              MOVE 'ERROR GRABAR SEG ' TO WSS-COD-MENS IN WSS-VARI.
      *
           ADD 1 TO WSS-NUM-GSEG IN WSS-VARI.
       FIN-GRABA-REG-SEG.
           EXIT.

       BUS-MINMAX-SEGURO SECTION.
      *-------------------------*
       INI-BUS-MINMAX-SEGURO.
           MOVE SPACES                   TO WSS-COD-MENS.
           MOVE 'COL'                    TO TAB-COD-SIST FIO-SIST.
           MOVE 'SDG'                    TO TAB-COD-TTAB IN TAB.
           MOVE 'CNAVAL'                 TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'CNAVAL '            TO WSS-COD-MENS
               GO TO FIN-BUS-MINMAX-SEGURO.

           MOVE TAB-GLS-DAT3 IN TAB TO
                                 WSS-GLS-DAT3-MINMAX-I IN WSS-VARI.
      *
            IF WSS-AGNO-NUM-MINMAX-I IN WSS-VARI NOT NUMERIC
               MOVE 'SEGDESNONUDI' TO WSS-COD-MENS.
       FIN-BUS-MINMAX-SEGURO.
           EXIT.

       LIMPIA-VECTOR SECTION.
      *--------------------*
       INI-LIMPIA-VECTOR.
           MOVE 1 TO WSS-CNTD IN WSS-VARI.
       LUP-NXT.
           IF WSS-CNTD IN WSS-VARI NOT > 10
               MOVE ZEROES TO WSS-NUM-TRMO(WSS-CNTD)
               ADD 1 TO WSS-CNTD IN WSS-VARI
               GO TO LUP-NXT.
           MOVE ZEROES TO WSS-CNTD IN WSS-VARI.
       FIN-LIMPIA-VECTOR.
           EXIT.
       ESTADADISTICAS SECTION.
      *--------------------*
       INI-ESTADISTICAS.
           DISPLAY 'PROCESO COLPGEG '.
           DISPLAY 'NUMERO REG LEIDOS  OPC  : '
                    WSS-NUM-LOPC IN WSS-VARI.
           DISPLAY 'NUMERO REG GRABADOS SEG : '
                    WSS-NUM-GSEG IN WSS-VARI.
       FIN-ESTADISTICAS.
           EXIT.

       COPY COLBFOPS.
       COPY COLBFRDC.
       COPY COLBFDLC.
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY COLBFSEG.


       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBGHOY.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBGCPT.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
