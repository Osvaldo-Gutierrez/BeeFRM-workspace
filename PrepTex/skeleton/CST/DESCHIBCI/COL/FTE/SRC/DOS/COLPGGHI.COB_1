       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGGHI.
       AUTHOR.          BEE TEAM (JGM).
       DATE-WRITTEN.    27-JUL-1999
      *
      *agrega registro al  archivo historico de envio a Boletin.
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      *<<<< WSS
      ***************
       COPY COLBRHDI.
       COPY COLBRADI.
      ***************
      *
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWCVRF.
       COPY GNSWVVRF.
      ***************
       COPY GNSWGSYS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGCPT.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
      *
       01  WSS-VARI.
          03 WSS-CONTRO  VALUE  'REGISTRO-CONTROL    ' PIC X(20).
      *----------------------------------------------------------------*
      *        REGISTRO CABECERA        LARGO REG 201                  *
      *----------------------------------------------------------------*
          03 REG-CABEZA.
             05 RCAB-FILL                   PIC X(50).
             05 RCAB-FECHA-GENER.
                07 RCAB-FEC-GENER-S         PIC 9(02).
                07 RCAB-FEC-GENER-A         PIC 9(02).
                07 RCAB-FEC-GENER-M         PIC 9(02).
                07 RCAB-FEC-GENER-D         PIC 9(02).
      *
           03 WSS-PRM-FPR1.
              05 WSS-PRM-DPR1                     PIC 9(02).
              05 WSS-PRM-MPR1                     PIC 9(02).
              05 WSS-PRM-SPR1                     PIC 9(02).
              05 WSS-PRM-APR1                     PIC 9(02).
      *
           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                     PIC 9(02).
              05 WSS-NUM-APRO                     PIC 9(02).
              05 WSS-NUM-MPRO                     PIC 9(02).
              05 WSS-NUM-DPRO                     PIC 9(02).
      *
           03 WSS-FEC-FVEN.
              05 WSS-NUM-SVEN                     PIC X(02).
              05 WSS-NUM-AVEN                     PIC X(02).
              05 WSS-NUM-MVEN                     PIC X(02).
              05 WSS-NUM-DVEN                     PIC X(02).
      *
           03 WSS-FEC-FENV.
              05 WSS-NUM-SENV                     PIC X(02).
              05 WSS-NUM-AENV                     PIC X(02).
              05 WSS-NUM-MENV                     PIC X(02).
              05 WSS-NUM-DENV                     PIC X(02).
      *
           03 WSS-NUM-LCAB       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-EHDI       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-AHDI       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-LADI       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-PADI       VALUE ZEROES     PIC 9(05).

           03 WSS-IND-ACTU       VALUE SPACE      PIC X(01).
      *
           03 WSS-COD-ERRO                        PIC X(01).
              88 WSS-OK                           VALUE  '0'.
              88 WSS-NOINFORMADA-BOLETIN          VALUE  '1'.


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGGHI' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           PERFORM OPEN-FILE.
           PERFORM LEE-PARAM.
           PERFORM PRO-REG-ADI.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
      *
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-HDI.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLHDI ' FIO-STAT
              PERFORM GNS-PRO-ABT.
      *
      *
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-ADI.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLADI ' FIO-STAT
              PERFORM GNS-PRO-ABT.
       FIN-OPEN-FILE.
           EXIT.

       CLOSE-FILE SECTION.
      *-----------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-HDI.
      *
      * Archivo de aclaracion   de documentos Protestados.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-ADI.
           DISPLAY 'CERRAR ARCHIVOS: HDI, ADI'.
      *
       FIN-CLOSE-FILE.
           EXIT.


      ******************************************************************
       PRO-REG-ADI SECTION.
      *------------------*
       INI-PRO-REG-ADI.
      * fecha computacional
      *
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-ADI.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN REGISTROS ADI: ENVIO A BOLETIN '
               GO TO FIN-PRO-REG-ADI.
      *
            MOVE 'N'    TO WSS-IND-ACTU  IN WSS-VARI.
      **
       LUP-BUS-ADI.
           ADD 1 TO WSS-NUM-LADI IN WSS-VARI.
      *
           IF  ADI-COD-TREG IN ADI = 'RC' AND
               ADI-COD-TDOC IN ADI = 'DI'
               ADD 1 TO WSS-NUM-LCAB IN WSS-VARI
               MOVE ADI              TO REG-CABEZA
               DISPLAY ' FECHA CAB : ' RCAB-FECHA-GENER
               IF RCAB-FECHA-GENER = WSS-FEC-FPRO IN WSS-VARI
                  MOVE 'S'            TO WSS-IND-ACTU IN WSS-VARI
                  MOVE RCAB-FECHA-GENER TO WSS-FEC-FENV IN WSS-VARI
      *
               ELSE
                  MOVE 'N' TO WSS-IND-ACTU IN WSS-VARI.
      *
           IF  WSS-IND-ACTU IN WSS-VARI = 'N'
               GO TO NXT-PRO-ADI.
      *
           IF  ADI-COD-TREG IN ADI NOT = 'RP'
               GO TO NXT-PRO-ADI.
      *
           ADD   1                  TO WSS-NUM-PADI IN WSS-VARI.
      *
           MOVE ADI-FEC-FVEN IN ADI TO HDI-FEC-FVEN IN HDI.
           MOVE ADI-CIC-IOPC IN ADI TO HDI-CIC-IOPC IN HDI.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-HDI.
           IF FIO-STAT-OKS
              ADD   1  TO WSS-NUM-EHDI   IN WSS-VARI
              GO TO NXT-PRO-ADI.
      *
      *     Enviado al Boletin.
            MOVE 'E'                      TO HDI-IND-ESTA IN HDI.
      *
            MOVE ADI-FEC-FVEN IN ADI      TO HDI-FEC-FVEN IN HDI.
            MOVE ADI-CIC-IOPC IN ADI      TO HDI-CIC-IOPC IN HDI.
      *
            MOVE ADI-NUM-CORR IN ADI      TO HDI-NUM-CORR IN HDI.
      *     Tipo de registro
            MOVE ADI-COD-TREG IN ADI      TO HDI-COD-TREG IN HDI.
      *
      *     Tipo Documento
            MOVE ADI-COD-TDOC IN ADI      TO HDI-COD-TDOC IN HDI.
      *
      *     Rut deudor.
            MOVE ADI-NUM-RUTD IN ADI      TO HDI-NUM-RUTD IN HDI.
            MOVE ADI-VRF-RUTD IN ADI      TO HDI-VRF-RUTD IN HDI.
      *
      *     Calidad Juridica.
            MOVE ADI-IND-CJUR IN ADI      TO HDI-COD-CJUR IN HDI.
      *
      *     codigo de oficina.
            MOVE ADI-COD-OFOC IN ADI      TO HDI-COD-OFIC IN HDI.
      *
      *     Codigo de Localidad
            MOVE ADI-COD-LOCA IN ADI      TO HDI-COD-LOCA IN HDI.
      *
      *     Codigo de Moneda
            MOVE ADI-GLS-MONE IN ADI      TO HDI-COD-MONE IN HDI.
      *
      *     Monto
            MOVE ADI-VAL-MONT IN ADI      TO HDI-VAL-MONT IN HDI.
      *
      *     Ind. Causal
            MOVE ADI-COD-CAUS IN ADI      TO HDI-COD-CAUS IN HDI.
      *
      *     Fecha de envio al Boletin.
            MOVE WSS-FEC-FENV IN WSS-VARI TO HDI-FEC-FENV IN HDI.
      *
      *     Fecha de Aclaracion, Cancelacion
            MOVE ZEROES                   TO HDI-FEC-FACL IN HDI.
            MOVE ZEROES                   TO HDI-FEC-FCAN IN HDI.
            MOVE SPACES                   TO HDI-GLS-DISP IN HDI.
      *
            MOVE FIO-PUT         TO FIO-CMND.
            PERFORM COL-FIO-HDI.
            IF FIO-STAT-OKS
               ADD   1 TO WSS-NUM-AHDI IN WSS-VARI.
      *
       NXT-PRO-ADI.
      *
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-ADI.
           IF FIO-STAT-OKS
               GO TO LUP-BUS-ADI.
       FIN-PRO-REG-ADI.
           EXIT.
      *
      *
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------*
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS PROCESO: COLPGGHI '.
           DISPLAY '==============================='.
           DISPLAY ' TOTAL DE REG. LEIDOS ADI : ' WSS-NUM-LADI.
           DISPLAY ' CABECERAS LEIDAS     ADI : ' WSS-NUM-LCAB.
           DISPLAY '----------------------------'.
           DISPLAY ' REG.PROCESADOS            ADI : ' WSS-NUM-PADI.
           DISPLAY ' REG.AGREGADO ( ENVIADOS ) HDI : ' WSS-NUM-AHDI.
           DISPLAY ' REG.EXISTENTES            HDI : ' WSS-NUM-EHDI.
           DISPLAY '==============================='.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.

      *****************************************************
       LEE-PARAM SECTION.
      *-----------------
       INI-LEE-PARAM.
           DISPLAY 'CAPTURA PARAMETROS'.
           DISPLAY '------------------- '.

      * Captura Fecha de Proceso
           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '.
           ACCEPT WSS-PRM-FPR1 IN WSS-VARI.


      * Validacion de parametros
           MOVE WSS-PRM-FPR1 IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ERROR VALIDACION FECHA DE PROCESO: '
                                                    FEC-MENS
               PERFORM GNS-PRO-ABT.
      *
           MOVE WSS-PRM-DPR1 IN WSS-VARI TO WSS-NUM-DPRO IN WSS-VARI.
           MOVE WSS-PRM-MPR1 IN WSS-VARI TO WSS-NUM-MPRO IN WSS-VARI.
           MOVE WSS-PRM-SPR1 IN WSS-VARI TO WSS-NUM-SPRO IN WSS-VARI.
           MOVE WSS-PRM-APR1 IN WSS-VARI TO WSS-NUM-APRO IN WSS-VARI.
      *

           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '
                    WSS-PRM-FPR1 IN WSS-VARI.

       FIN-LEE-PARAM.
           EXIT.
      ***************
       COPY COLBFADI.
       COPY COLBFHDI.
      ***************
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGABT.

       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
      *
       COPY GNSBFMSC.
       COPY GNSBBMSC.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBBMSG.
       COPY GNSBGCPT.
       COPY GNSBGEND.
