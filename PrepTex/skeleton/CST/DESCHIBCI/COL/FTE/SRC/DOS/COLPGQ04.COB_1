       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGQ04.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    29-MAY-1992   (Ultima Modificacion).

      * Programa Generador y/o Actualizador BATCH de Archivo COLSCC.

       ENVIRONMENT DIVISION.
      *====================
      * CONFIGURATION SECTION.
      *---------------------
      * SPECIAL-NAMES.
      *      DECIMAL-POINT IS COMMA.
      * INPUT-OUTPUT SECTION.
      *--------------------
      * FILE-CONTROL.

       DATA DIVISION.
      *=============
      * FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLBROPC.
       COPY COLBRCAN.
       COPY COLBRRCV.
       COPY COLBRTOC.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY COLBRSCC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       01  WSS-VARI.
           03 WSS-FEC-FTER.
              05 WSS-NUM-DTER                     PIC 9(02).
              05 WSS-NUM-MTER                     PIC 9(02).
              05 WSS-NUM-STER                     PIC 9(02).
              05 WSS-NUM-ATER                     PIC 9(02).
           03 WSS-FEC-FFET.
              05 WSS-NUM-SFET                     PIC 9(02).
              05 WSS-NUM-AFET                     PIC 9(02).
              05 WSS-NUM-MFET                     PIC 9(02).
              05 WSS-NUM-DFET                     PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                     PIC 9(02).
              05 WSS-NUM-AFEC                     PIC 9(02).
              05 WSS-NUM-MFEC                     PIC 9(02).
              05 WSS-NUM-DFEC                     PIC 9(02).
           03 WSS-FEC-FHOY.
              05 WSS-NUM-DHOY                     PIC 9(02).
              05 WSS-NUM-MHOY                     PIC 9(02).
              05 WSS-NUM-SHOY                     PIC 9(02).
              05 WSS-NUM-AHOY                     PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                     PIC 9(02).
              05 WSS-NUM-APRO                     PIC 9(02).
              05 WSS-NUM-MPRO                     PIC 9(02).
              05 WSS-NUM-DPRO                     PIC 9(02).
           03 WSS-TOCO                            PIC 9(04).
           03 WSS-NUM-ITOC                        PIC 9(02).
           03 WSS-OPEC                            PIC X(01).
           03 WSS-IND-ODSE                        PIC 9(01).
           03 WSS-COD-ODSE                        PIC X(03).
           03 WSS-IND-PCAN                        PIC 9(01).
           03 WSS-IND-RCV                         PIC 9(01).
           03 WSS-VAL-SCCI                        PIC 9(11)V9(04).
           03 WSS-GLS-MSCC                        PIC X(28).
           03 WSS-NUM-OPCL                        PIC 9(06).
           03 WSS-NUM-OPCP                        PIC 9(06).
           03 WSS-NUM-CANL                        PIC 9(06).
           03 WSS-NUM-CANP                        PIC 9(06).
           03 WSS-SCC-OPCP                        PIC 9(06).
           03 WSS-SCC-OPCM                        PIC 9(06).
           03 WSS-SCC-CANP                        PIC 9(06).
           03 WSS-SCC-CANM                        PIC 9(06).
           03 WSS-VEC-TOCO.
              05 WSS-ELE-TOCO  OCCURS 99  INDEXED BY  WSS-NUM-TOCO.
                 07 WSS-COD-TOCR                  PIC X(03).
                 07 WSS-MSC-OPEC                  PIC X(01).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGGQ4' TO FIO-PROG.
           PERFORM BUS-FSIS.

           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      * Captura Fecha de Generacion
           DISPLAY 'FECHA GENERACION ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY 'FECHA GENERACION : ' WSS-FEC-FTER IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Generacion
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992

           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DFET IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MFET IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-SFET IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-AFET IN WSS-VARI.

      * Forma Fecha de Inicio del Mes
           MOVE WSS-FEC-FFET IN WSS-VARI TO WSS-FEC-FFEC IN WSS-VARI.
           MOVE  01                      TO WSS-NUM-DFEC IN WSS-VARI.

      * Obtiene Fecha Hoy para Proceso de CAN
           PERFORM GET-FHOY.
           MOVE HOY-DHOY IN HOY-VARI TO WSS-NUM-DHOY IN WSS-VARI.
           MOVE HOY-MHOY IN HOY-VARI TO WSS-NUM-MHOY IN WSS-VARI.
           MOVE HOY-SHOY IN HOY-VARI TO WSS-NUM-SHOY IN WSS-VARI.
           MOVE HOY-AHOY IN HOY-VARI TO WSS-NUM-AHOY IN WSS-VARI.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Apertura de archivos Principales
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-SCC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO SCC DE I-O' FIO-STAT
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO OPC'
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO CAN'
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
               PERFORM GNS-PRO-ABT.
      *DAD fin  29-MAY-1992

      * Inicializacion Contadores de Proceso
           MOVE ZEROES TO WSS-NUM-OPCL IN WSS-VARI
                          WSS-NUM-OPCP IN WSS-VARI
                          WSS-NUM-CANL IN WSS-VARI
                          WSS-NUM-CANP IN WSS-VARI
                          WSS-SCC-OPCP IN WSS-VARI
                          WSS-SCC-OPCM IN WSS-VARI
                          WSS-SCC-CANP IN WSS-VARI
                          WSS-SCC-CANM IN WSS-VARI
                          WSS-TOCO.

      * Almacena los Codigos Tipo Operaciones Credito y Como Opera de ta
           MOVE FIO-GET-FST TO FIO-CMND.
       LUP-TOC.
           PERFORM COL-FIO-TOC.
           IF NOT FIO-STAT-OKS
               GO TO LUP-MAIN.
           ADD 1 TO WSS-TOCO.
           MOVE TOC-COD-TOCR TO WSS-COD-TOCR(WSS-TOCO).
           MOVE TOC-MSC-OPEC TO WSS-MSC-OPEC(WSS-TOCO).
       NXT-GET-TOC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-TOC.
      ******************************************************************
       LUP-MAIN.
       INI-PRO-OPC.
      * Inicio Ciclo de Proceso para OPC
           MOVE ZEROES TO OPC-FEC-FCOL IN OPC.
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY OPERACIONES HASTA FECHA GENERACION : '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-PRO-OPC.
       LUP-PRO-OPC.
      * Seleccion de Operaciones con Estado 'ACA'
           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO NXT-PRO-OPC.
           ADD 1 TO WSS-NUM-OPCL IN WSS-VARI.

      * Obtener Origen y Destino Sectorial ( DBC-COD-ODSE ) con OPC-->RD
           PERFORM GET-ODS-OPC.
           IF WSS-IND-ODSE IN WSS-VARI = 1
               GO TO NXT-PRO-OPC.
           ADD 1 TO WSS-NUM-OPCP IN WSS-VARI.

      * Actualiza registros de SCC con VAL-SCRE desde FINI hasta FTER o
      * con VAL-CRED desde FCOL hasta FTER y (FPRO,ODSE,VCOC) de OPC
           IF OPC-FEC-FCOL IN OPC < WSS-FEC-FFEC IN WSS-VARI
               MOVE WSS-FEC-FFEC IN WSS-VARI TO WSS-FEC-FPRO IN WSS-VARI
               MOVE OPC-VAL-SCRE IN OPC      TO WSS-VAL-SCCI IN WSS-VARI
           ELSE
           IF OPC-FEC-FCOL IN OPC NOT < WSS-FEC-FFEC IN WSS-VARI
               MOVE OPC-FEC-FCOL IN OPC TO WSS-FEC-FPRO IN WSS-VARI
               MOVE OPC-VAL-CRED IN OPC TO WSS-VAL-SCCI IN WSS-VARI.
           PERFORM ACT-SCC-OPC.
       NXT-PRO-OPC.
      * Selecciona Operaciones con FCOL < = FTER
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FFET IN WSS-VARI
               GO TO FIN-PRO-OPC.
           GO TO LUP-PRO-OPC.
       FIN-PRO-OPC.
      ******************************************************************
       INI-PRO-CAN.
      * Inicio Ciclo de Proceso para CAN
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CAN-FEC-FCAN IN CAN.
           MOVE 'CAN-FEC-FCAN' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS OR
              CAN-FEC-FCAN IN CAN > HOY-FHOY IN HOY-VARI
               DISPLAY 'NO HAY CANCELACIONES HASTA FECHA PROCESO : '
                       WSS-FEC-FHOY IN WSS-VARI
               GO TO FIN-PRO-CAN.
       LUP-PRO-CAN.
      * Seleccion de Cancelaciones con Estado 'ACA'
           IF CAN-MSC-STAT IN CAN NOT = 'A'
               GO TO NXT-PRO-CAN.
           ADD 1 TO WSS-NUM-CANL IN WSS-VARI.

      * Busca la Operacion de Credito asociada a la Cancelacion
           MOVE CAN-CIC-IOPC IN CAN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-CIC-IOPC IN OPC NOT = CAN-CIC-IOPC IN CAN
               DISPLAY 'SIN OPC, LA CANCELACION : ' CAN-CIC-IOPC IN CAN
               GO TO NXT-PRO-CAN.

      * Busca en Vector el Como Opera con el Codigo Tipo Operacion
           PERFORM BUS-TOC.

      * Obtener Origen y Destino Sectorial ( DBC-COD-ODSE ) con OPC-->RD
           PERFORM GET-ODS-OPC.
           IF WSS-IND-ODSE IN WSS-VARI = 1
               GO TO NXT-PRO-CAN.

      * Procesar RCV acumulando SGV-FINA o SGV-CAPI segun Como Opera
           PERFORM PRO-CAN-RCV.
           IF WSS-IND-RCV IN WSS-VARI = 1
               GO TO NXT-PRO-CAN.
           ADD 1 TO WSS-NUM-CANP IN WSS-VARI.

      * Actualiza registros de SCC desde FINI hasta FTER o FCAN hasta FT
      * segun Indicador IND-PCAN con Monto del Saldo acumulado y
      * (FPRO,ODSE,VCOC) con CAN
           MOVE  0  TO WSS-IND-PCAN IN WSS-VARI.
           IF CAN-FEC-FCAN IN CAN > WSS-FEC-FFET IN WSS-VARI
               IF OPC-FEC-FCOL IN OPC < WSS-FEC-FFEC IN WSS-VARI
                   MOVE  1  TO WSS-IND-PCAN IN WSS-VARI
                   MOVE WSS-FEC-FFEC IN WSS-VARI
                                            TO WSS-FEC-FPRO IN WSS-VARI
               ELSE
                   GO TO NXT-PRO-CAN
           ELSE
           IF CAN-FEC-FCAN IN CAN NOT < WSS-FEC-FFEC IN WSS-VARI
               IF OPC-FEC-FCOL IN OPC NOT < WSS-FEC-FFEC IN WSS-VARI
                   MOVE  2  TO WSS-IND-PCAN IN WSS-VARI
                   MOVE CAN-FEC-FCAN IN CAN TO WSS-FEC-FPRO IN WSS-VARI
               ELSE
               IF OPC-FEC-FCOL IN OPC < WSS-FEC-FFEC IN WSS-VARI
                   MOVE  3  TO WSS-IND-PCAN IN WSS-VARI
                   MOVE WSS-FEC-FFEC IN WSS-VARI
                                            TO WSS-FEC-FPRO IN WSS-VARI.
           PERFORM ACT-SCC-CAN.
       NXT-PRO-CAN.
      * Selecciona Cancelaciones con FCAN < = FHOY
           MOVE 'CAN-FEC-FCAN' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS OR
              CAN-FEC-FCAN IN CAN > HOY-FHOY IN HOY-VARI
               GO TO FIN-PRO-CAN.
           GO TO LUP-PRO-CAN.
       FIN-PRO-CAN.
       FIN-MAIN.
      ******************************************************************
      * Display Totales de Control
           DISPLAY 'REG. OPC SELECCIONADOS : ' WSS-NUM-OPCL IN WSS-VARI.
           DISPLAY 'REG. OPC PARA PROCESO  : ' WSS-NUM-OPCP IN WSS-VARI.
           DISPLAY 'REG. CAN SELECCIONADOS : ' WSS-NUM-CANL IN WSS-VARI.
           DISPLAY 'REG. CAN PARA PROCESO  : ' WSS-NUM-CANP IN WSS-VARI.
           DISPLAY 'REG. SCC CREADO X OPC  : ' WSS-SCC-OPCP IN WSS-VARI.
           DISPLAY 'REG. SCC MODIF. X OPC  : ' WSS-SCC-OPCM IN WSS-VARI.
           DISPLAY 'REG. SCC CREADO X CAN  : ' WSS-SCC-CANP IN WSS-VARI.
           DISPLAY 'REG. SCC MODIF. X CAN  : ' WSS-SCC-CANM IN WSS-VARI.

      * Cierre de archivos Principales y termino proceso
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-SCC.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-RCV.
           PERFORM GNS-PRO-END.

       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY COLBFOPC.
       COPY COLBFCAN.
       COPY COLBFRCV.
       COPY COLBFTOC.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY COLIOSCC.
       COPY GNSBGEND.
      *DAD ini  29-MAY-1992   (Memo.27-04-92)
       COPY GNSBGABT.
      *DAD fin  29-MAY-1992
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY SGCBGCLI.

      ******************************************************************
      *-----------------------------------------------------------------
      * Obtener Origen y Destino Sectorial ( DBC-COD-ODSE ) con OPC-->RD
       GET-ODS-OPC SECTION.
       INI-GET-ODC-OPC.
           MOVE  0  TO WSS-IND-ODSE IN WSS-VARI.
           MOVE OPC-CIC-IOPC IN OPC TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE SPACES              TO RDC-CIC-ICLI IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS OR
              RDC-CIC-IOPC IN RDC NOT = OPC-CIC-IOPC IN OPC
               DISPLAY 'NO TIENE RDC, OPERACION : ' OPC-CIC-IOPC IN OPC
               MOVE  1  TO WSS-IND-ODSE IN WSS-VARI
               GO TO FIN-GET-ODS-OPC.

      *CIC-CLI Busca Cliente por medio de CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE 'SGC'        TO MSG-COD-SIST
               MOVE 'CLI    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC RDC-CIC-ICLI IN RDC
               MOVE  1  TO WSS-IND-ODSE IN WSS-VARI
           ELSE
               MOVE DBC-COD-ODSE IN DBC TO WSS-COD-ODSE IN WSS-VARI.
       FIN-GET-ODS-OPC.
           EXIT.
      *-----------------------------------------------------------------
      * Actualiza registros de SCC con VAL-SCRE desde FINI hasta FTER o
      * con VAL-CRED desde FCOL hasta FTER y (FPRO,ODSE,VCOC) de OPC
       ACT-SCC-OPC SECTION.
       INI-ACT-SCC-OPC.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO SCC-FEC-FSCC IN SCC.
           MOVE WSS-COD-ODSE IN WSS-VARI TO SCC-COD-ODSE IN SCC.
           MOVE OPC-COD-VCOC IN OPC      TO SCC-COD-VCSC IN SCC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-SCC.
           IF FIO-STAT-OKS
      * Acumula
               ADD WSS-VAL-SCCI IN WSS-VARI TO SCC-VAL-SCCI IN SCC
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-SCC
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR AL REGRABAR SCC-OPC : ' SCC-KEY-ISCC
                   DISPLAY 'FIO-STAT : ' FIO-STAT
                   GO TO FIN-ACT-SCC-OPC
               ELSE
                   ADD 1 TO WSS-SCC-OPCM IN WSS-VARI
           ELSE
      * Crea
               MOVE WSS-VAL-SCCI IN WSS-VARI TO SCC-VAL-SCCI IN SCC
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-SCC
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR AL GRABAR SCC-OPC : ' SCC-KEY-ISCC
                   DISPLAY 'FIO-STAT : ' FIO-STAT
                   GO TO FIN-ACT-SCC-OPC
               ELSE
                   ADD 1 TO WSS-SCC-OPCP IN WSS-VARI.

      * Incrementa Dia para Fecha Saldo Colocacion en SCC hasta Fecha Te
           ADD 1 TO WSS-NUM-DPRO IN WSS-VARI.
           IF WSS-NUM-DPRO IN WSS-VARI NOT > WSS-NUM-DFET IN WSS-VARI
               GO TO INI-ACT-SCC-OPC.
       FIN-ACT-SCC-OPC.
           EXIT.
      *-----------------------------------------------------------------
      * Busca en Vector el Como Opera con el Codigo Tipo Operacion
       BUS-TOC SECTION.
       INI-BUS-TOC.
           MOVE SPACES TO WSS-OPEC     IN WSS-VARI.
           MOVE   1    TO WSS-NUM-ITOC IN WSS-VARI.
           SET WSS-NUM-TOCO TO 1.
           SEARCH WSS-ELE-TOCO VARYING WSS-NUM-TOCO
                  WHEN WSS-COD-TOCR(WSS-NUM-TOCO) = OPC-COD-TOOC IN OPC
                   GO TO FND-OKS.
           GO TO FIN-BUS-TOC.
       FND-OKS.
           MOVE WSS-MSC-OPEC(WSS-NUM-TOCO) TO WSS-OPEC IN WSS-VARI.
       FIN-BUS-TOC.
           EXIT.
      *-----------------------------------------------------------------
      * Procesar RCV acumulando SGV-FINA o SGV-CAPI segun Como Opera
       PRO-CAN-RCV SECTION.
       INI-PRO-CAN-RCV.
           MOVE  0  TO WSS-IND-RCV  IN WSS-VARI
                       WSS-VAL-SCCI IN WSS-VARI.
           MOVE CAN-KEY-ICAN IN CAN TO RCV-KEY-ICAN IN RCV.
           MOVE SPACES              TO RCV-CIC-IOPV IN RCV.
           MOVE ZEROES              TO RCV-NUM-IVCT IN RCV.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS OR
              RCV-KEY-ICAN IN RCV NOT = CAN-KEY-ICAN IN CAN
               DISPLAY 'SIN RCV, LA CANCELACION : ' CAN-KEY-ICAN IN CAN
               MOVE  1  TO WSS-IND-RCV IN WSS-VARI
               GO TO FIN-PRO-CAN-RCV.
       LUP-PRO-CAN-RCV.
      * Acumula SGV-FINA o SGV-CAPI segun Como Opera para el Monto del S
           IF WSS-OPEC = 'D'
               ADD RCV-SGV-FINA IN RCV TO WSS-VAL-SCCI IN WSS-VARI
           ELSE
               ADD RCV-SGV-CAPI IN RCV TO WSS-VAL-SCCI IN WSS-VARI.
       NXT-PRO-CAN-RCV.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS OR
              RCV-KEY-ICAN IN RCV NOT = CAN-KEY-ICAN IN CAN
               GO TO FIN-PRO-CAN-RCV.
           GO TO LUP-PRO-CAN-RCV.
       FIN-PRO-CAN-RCV.
           EXIT.
      *-----------------------------------------------------------------
      * Actualiza registros de SCC desde FINI hasta FTER o FCAN hasta FT
      * segun Indicador IND-PCAN con Monto del Saldo acumulado y
      * (FPRO,ODSE,VCOC) con CAN
       ACT-SCC-CAN SECTION.
       INI-ACT-SCC-CAN.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO SCC-FEC-FSCC IN SCC.
           MOVE WSS-COD-ODSE IN WSS-VARI TO SCC-COD-ODSE IN SCC.
           MOVE OPC-COD-VCOC IN OPC      TO SCC-COD-VCSC IN SCC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-SCC.
           IF NOT FIO-STAT-OKS
      * Graba SCC actualizado
               MOVE FIO-PUT TO FIO-CMND
               MOVE 'ERROR AL GRABAR CAN-SCC : '
                            TO WSS-GLS-MSCC IN WSS-VARI
           ELSE
      * Regraba SCC actualizado
               MOVE FIO-MOD TO FIO-CMND
               MOVE 'ERROR AL REGRABAR CAN-SCC : '
                            TO WSS-GLS-MSCC IN WSS-VARI.

      * Segun IND-PCAN Suma o Resta Monto Acumulado al Monto del Saldo d
           IF WSS-IND-PCAN IN WSS-VARI = 1 OR
              WSS-IND-PCAN IN WSS-VARI = 3
               ADD WSS-VAL-SCCI IN WSS-VARI TO SCC-VAL-SCCI IN SCC
           ELSE
           IF WSS-IND-PCAN IN WSS-VARI = 2
               SUBTRACT WSS-VAL-SCCI IN WSS-VARI FROM
                                                 SCC-VAL-SCCI IN SCC.

      * Graba o Regraba SCC actualizado segun existencia previa del regi
           PERFORM COL-FIO-SCC.
           IF NOT FIO-STAT-OKS
               DISPLAY WSS-GLS-MSCC IN WSS-VARI SCC-KEY-ISCC IN SCC
               DISPLAY 'FIO-STAT : ' FIO-STAT
               GO TO FIN-ACT-SCC-CAN
           ELSE
               IF FIO-CMND = FIO-PUT
                   ADD 1 TO WSS-SCC-CANP IN WSS-VARI
               ELSE
                   ADD 1 TO WSS-SCC-CANM IN WSS-VARI.

      * Incrementa Dia para Fecha Saldo Colocacion en SCC hasta Fecha Te
           ADD 1 TO WSS-NUM-DPRO IN WSS-VARI.
           IF WSS-IND-PCAN IN WSS-VARI = 3 AND
              WSS-NUM-DPRO IN WSS-VARI < CAN-NUM-DCAN IN CAN
               GO TO INI-ACT-SCC-CAN
           ELSE
           IF WSS-IND-PCAN IN WSS-VARI < 3 AND
              WSS-NUM-DPRO IN WSS-VARI NOT > WSS-NUM-DFET IN WSS-VARI
               GO TO INI-ACT-SCC-CAN.
       FIN-ACT-SCC-CAN.
           EXIT.
      *-----------------------------------------------------------------
      ******************************************************************
