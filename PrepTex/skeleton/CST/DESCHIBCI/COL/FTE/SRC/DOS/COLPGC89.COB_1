       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGC89.
       AUTHOR.          BEE TEAM (JGM).
       DATE-WRITTEN.    27-JUL-1999
      *
      *Generar informacion para cartera vencida
      *creditos comerciales.
      *
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
      *<<<< WSS
      ***************
       COPY COLBRC89.
       COPY COLBRFVD.

       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWCVRF.
       COPY GNSWVVRF.
       COPY GNSWGFRM.
      *
       01  WSS-VARI.
           03 WSS-IND-PROC   VALUE SPACES        PIC X(01).
           03 WSS-LEE-ROP    VALUE SPACES        PIC X(01).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                     PIC 9(02).
              05 WSS-NUM-APRO                     PIC 9(02).
              05 WSS-NUM-MPRO                     PIC 9(02).
              05 WSS-NUM-DPRO                     PIC 9(02).
      *
           03 WSS-FEC-FVEN.
              05 WSS-NUM-SVEN                     PIC 9(02).
              05 WSS-NUM-AVEN                     PIC 9(02).
              05 WSS-NUM-MVEN                     PIC 9(02).
              05 WSS-NUM-DVEN                     PIC 9(02).
      *
           03 WSS-FEC-FPRM.
              05 WSS-NUM-DPRM                     PIC 9(02).
              05 WSS-NUM-MPRM                     PIC 9(02).
              05 WSS-NUM-SPRM                     PIC 9(02).
              05 WSS-NUM-APRM                     PIC 9(02).
      *
      *
           03 WSS-I              VALUE ZEROES     PIC 9(02).
           03 WSS-IND-TRAMO      VALUE ZEROES     PIC 9(02).
           03 WSS-NUM-LFVD       VALUE ZEROES     PIC 9(08).
           03 WSS-NUM-GC89       VALUE ZEROES     PIC 9(08).
           03 WSS-IND-DISPLAY    VALUE SPACES     PIC X(01).
      *
      *Variables para agrupar.
           03 WSS-GLS-CCSB       VALUE SPACES     PIC X(09).
           03 WSS-COD-CVOC       VALUE ZEROES     PIC 9(03).
           03 WSS-IND-TASA       VALUE SPACES     PIC X(01).
      *
           03 WSS-AUX-CAPI       VALUE ZEROES     PIC S9(14)V9(04).
           03 WSS-SGV-CAPI       VALUE ZEROES     PIC S9(14)V9(04).
      *
           03 WSS-AUX-INTE       VALUE ZEROES     PIC S9(14)V9(04).
           03 WSS-SGV-INTE       VALUE ZEROES     PIC S9(14)V9(04).
      *
      *
           03 WSS-VECT OCCURS 15.
              05 WSS-VAL-CAPI   VALUE ZEROES     PIC S9(14)V9(04).
              05 WSS-VAL-INTE   VALUE ZEROES     PIC S9(14)V9(04).
      *
           03 WSS-COD-MENS       VALUE SPACES     PIC X(15).
           03 WSS-IND-EXISTE     VALUE SPACE      PIC X(01).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGCVM' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           PERFORM OPEN-FILE.
           PERFORM LEE-PARAM.
           PERFORM PRO-REG-FVD.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-FVD.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLFVD ' FIO-STAT.
      *
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-C89.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR ABRIENDO COLC89 ' FIO-STAT
              PERFORM GNS-PRO-ABT.
      *
       FIN-OPEN-FILE.
           EXIT.

       CLOSE-FILE SECTION.
      *-----------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-FVD.
      *
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-C89.
           DISPLAY 'CERRAR ARCHIVOS: FVD, C89'.
      *
       FIN-CLOSE-FILE.
           EXIT.


      ******************************************************************
       PRO-REG-FVD SECTION.
       INI-PRO-REG-FVD.
           MOVE LOW-VALUES  TO FVD.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-FVD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN REGISTROS EN FVD'
               DISPLAY ' FIO-STAT '  FIO-STAT
               GO TO FIN-PRO-REG-FVD.
      *
      * Se salta encabezado FVD.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-FVD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN REGISTROS EN FVD'
               DISPLAY ' FIO-STAT '  FIO-STAT
               GO TO FIN-PRO-REG-FVD.
      *
           MOVE FVD-GLS-CCSB IN FVD TO WSS-GLS-CCSB IN WSS-VARI.
           MOVE FVD-COD-CVOC IN FVD TO WSS-COD-CVOC IN WSS-VARI.
           MOVE FVD-IND-TASA IN FVD TO WSS-IND-TASA IN WSS-VARI.
      *
           PERFORM LIMPIA-VECT VARYING WSS-I FROM 1 BY 1
                               UNTIL WSS-I > 15.
       LUP-PRO-REG-FVD.
           MOVE SPACES   TO WSS-COD-MENS.
           IF NOT FIO-STAT-OKS
               PERFORM GRABA-REG-C89
               GO TO FIN-PRO-REG-FVD.
      **
           ADD  1        TO WSS-NUM-LFVD IN WSS-VARI.
      **
           IF FVD-GLS-CCSB IN FVD NOT = WSS-GLS-CCSB IN WSS-VARI OR
              FVD-COD-CVOC IN FVD NOT = WSS-COD-CVOC IN WSS-VARI OR
              FVD-IND-TASA IN FVD NOT = WSS-IND-TASA IN WSS-VARI
      *
              PERFORM GRABA-REG-C89
              MOVE FVD-GLS-CCSB IN FVD TO WSS-GLS-CCSB IN WSS-VARI
              MOVE FVD-COD-CVOC IN FVD TO WSS-COD-CVOC IN WSS-VARI
              MOVE FVD-IND-TASA IN FVD TO WSS-IND-TASA IN WSS-VARI
      *
              PERFORM LIMPIA-VECT VARYING WSS-I FROM 1 BY 1
                                     UNTIL WSS-I > 15.
      *
           PERFORM BUS-TRAMO.
      *
           IF WSS-IND-DISPLAY IN WSS-VARI = 'S'
              DISPLAY 'CONTADOR : ' WSS-NUM-LFVD IN WSS-VARI
              DISPLAY 'FVD-CCSB : ' FVD-GLS-CCSB IN FVD
              DISPLAY 'FVD-CVOC : ' FVD-COD-CVOC IN FVD
              DISPLAY 'FVD-TASA : ' FVD-IND-TASA IN FVD
              DISPLAY 'FVD-FVEN : ' FVD-FEC-FVEN IN FVD
              DISPLAY 'NDIA     : ' FEC-NDIA
              DISPLAY 'TRAMO    : ' WSS-IND-TRAMO IN WSS-VARI
              DISPLAY '---------------------------------------'.
      *
           PERFORM ACUMULA-VALOR.

       NXT-GRABA-FVD.
           IF WSS-COD-MENS > SPACES
               DISPLAY 'MENSAJE ' WSS-COD-MENS IN WSS-VARI
               ' ' FVD-GLS-CCSB IN FVD.
      *
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-FVD.
           GO TO LUP-PRO-REG-FVD.
       FIN-PRO-REG-FVD.
           EXIT.
      ******************************************************
       ACUMULA-VALOR SECTION.
      *--------------------------*
       INI-ACUMULA-VALOR.
           MOVE FVD-SGV-CAPI IN FVD TO WSS-SGV-CAPI  IN WSS-VARI.
           MOVE FVD-SGV-INTE IN FVD TO WSS-SGV-INTE  IN WSS-VARI.
      *
           IF WSS-IND-TRAMO IN WSS-VARI NOT > 0
               GO TO FIN-ACUMULA-VALOR.
      *
           MOVE WSS-VAL-CAPI IN WSS-VECT ( WSS-IND-TRAMO ) TO
                WSS-AUX-CAPI IN WSS-VARI.
      *Capital
           COMPUTE
           WSS-AUX-CAPI IN WSS-VARI = WSS-SGV-CAPI IN WSS-VARI +
                                      WSS-AUX-CAPI IN WSS-VARI.
      *
           MOVE WSS-AUX-CAPI IN WSS-VARI    TO
                WSS-VAL-CAPI IN WSS-VECT ( WSS-IND-TRAMO ).
      *
      *Interes.
           MOVE WSS-VAL-INTE IN WSS-VECT ( WSS-IND-TRAMO ) TO
                WSS-AUX-INTE IN WSS-VARI.
      *
           COMPUTE
           WSS-AUX-INTE IN WSS-VARI = WSS-SGV-INTE IN WSS-VARI +
                                      WSS-AUX-INTE IN WSS-VARI.
      *
           MOVE WSS-AUX-INTE IN WSS-VARI   TO
                WSS-VAL-INTE IN WSS-VECT ( WSS-IND-TRAMO ).
       FIN-ACUMULA-VALOR.
           EXIT.
      *
       BUS-TRAMO SECTION.
      *--------------------------*
       INI-BUS-TRAMO.
           MOVE SPACES              TO MSG-COD-MENS.
           MOVE ZEROES              TO WSS-IND-TRAMO IN WSS-VARI.
      *
           MOVE FVD-FEC-FVEN IN FVD TO WSS-FEC-FVEN IN WSS-VARI.
      *
           IF WSS-FEC-FVEN IN WSS-VARI < WSS-FEC-FPRO IN WSS-VARI
              MOVE 1 TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
           MOVE WSS-NUM-DPRO IN WSS-VARI TO FEC-DEC1.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO FEC-MEC1.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO FEC-SEC1.
           MOVE WSS-NUM-APRO IN WSS-VARI TO FEC-AEC1.
      *
           MOVE WSS-NUM-DVEN IN WSS-VARI TO FEC-DEC2.
           MOVE WSS-NUM-MVEN IN WSS-VARI TO FEC-MEC2.
           MOVE WSS-NUM-SVEN IN WSS-VARI TO FEC-SEC2.
           MOVE WSS-NUM-AVEN IN WSS-VARI TO FEC-AEC2.
      *
      *
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
              GO TO FIN-BUS-TRAMO.
      *
      * 1) tramos -999999999   a     15
           IF FEC-NDIA < 16
              MOVE 1     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 2) Tramo         16     -->        29
           IF FEC-NDIA > 15 AND FEC-NDIA < 30
              MOVE 2     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 3) tramos        30     -->        59
           IF FEC-NDIA > 29 AND FEC-NDIA < 60
              MOVE 3     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 4) tramos        60     -->        89
           IF FEC-NDIA > 59 AND FEC-NDIA < 90
              MOVE 4     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 5) tramos        90     -->        181
           IF FEC-NDIA > 89 AND FEC-NDIA < 182
              MOVE 5     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 6) tramos        182      -->       365
           IF FEC-NDIA > 181 AND FEC-NDIA < 366
              MOVE 6     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 7) tramos        366     -->        730
           IF FEC-NDIA > 365 AND FEC-NDIA < 731
              MOVE 7     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 8) TRAMOS        331     -->        1095
           IF FEC-NDIA > 330 AND FEC-NDIA < 1096
              MOVE 8     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 9) tramos        1096     -->        1460
           IF FEC-NDIA > 1095 AND FEC-NDIA < 1461
              MOVE 9     TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 10) tramos       1461     -->        1826
           IF FEC-NDIA > 1460 AND FEC-NDIA < 1827
              MOVE 10    TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 11) tramos       1827     -->        2556
           IF FEC-NDIA > 1826 AND FEC-NDIA < 2557
              MOVE 11    TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 12) tramos       2557     -->        3652
           IF FEC-NDIA > 2556 AND FEC-NDIA < 3653
              MOVE 12    TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 13) tramos       3653     -->        5478
           IF FEC-NDIA > 3652 AND FEC-NDIA < 5479
              MOVE 13    TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 14) tramos       5479     -->        7304
           IF FEC-NDIA > 5478 AND FEC-NDIA < 7305
              MOVE 14    TO WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
      *
      * 15) tramos       7305     -->        999999999
           IF FEC-NDIA > 7304 AND FEC-NDIA < 999999999
              MOVE  15   TO   WSS-IND-TRAMO IN WSS-VARI
              GO TO FIN-BUS-TRAMO.
       FIN-BUS-TRAMO.
           EXIT.
       GRABA-REG-C89 SECTION.
      *---------------------*
       INI-GRABA-REG-C89.
           MOVE WSS-GLS-CCSB IN WSS-VARI TO C89-GLS-CCSB IN C89.
           MOVE WSS-COD-CVOC IN WSS-VARI TO C89-COD-CVOC IN C89.
           MOVE WSS-IND-TASA IN WSS-VARI TO C89-IND-TASA IN C89.
      *
           MOVE 1 TO WSS-I IN WSS-VARI.
           PERFORM MOVER-VECT VARYING WSS-I FROM 1 BY 1
                               UNTIL WSS-I > 15.
      *
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-C89.
           IF  NOT FIO-STAT-OKS
               DISPLAY ' ERROR AL GRABAR REG:C89  -> '
               C89-GLS-CCSB IN C89 ' MONEDA : '
               C89-COD-CVOC IN C89 ' TASA : '
               C89-IND-TASA IN C89
               GO TO FIN-GRABA-REG-C89.
            ADD 1 TO WSS-NUM-GC89 IN WSS-VARI.
       FIN-GRABA-REG-C89.
           EXIT.
      *
       LIMPIA-VECT SECTION.
      *--------------------*
       INI-LIMPIA-VECT.
           MOVE ZERO TO  WSS-VAL-CAPI IN WSS-VECT ( WSS-I ).
           MOVE ZERO TO  WSS-VAL-INTE IN WSS-VECT ( WSS-I ).
       FIN-LIMPIA-VECT.
           EXIT.
      *
       MOVER-VECT SECTION.
      *--------------------*
       INI-MOVER-VECT.
      *CAPITAL
      *-------*
           MOVE    '+'  TO C89-IND-SIGC IN C89 (WSS-I).
           IF WSS-VAL-CAPI IN WSS-VARI ( WSS-I )  < ZERO
              MOVE '-'  TO C89-IND-SIGC IN C89 (WSS-I).
      *
           MOVE  WSS-VAL-CAPI IN WSS-VARI ( WSS-I )  TO
                 C89-VAL-CAPI IN C89      ( WSS-I ).
      *
      *INTERES.
      *-------*
           MOVE    '+'  TO C89-IND-SIGI IN C89 (WSS-I).
           IF WSS-VAL-INTE IN WSS-VARI ( WSS-I )  < ZERO
              MOVE '-'  TO C89-IND-SIGI IN C89 (WSS-I).
      *
           MOVE  WSS-VAL-INTE IN WSS-VARI ( WSS-I )  TO
                 C89-VAL-INTE IN C89      ( WSS-I ).
      *
       FIN-MOVER-VECT.
           EXIT.
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------*
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS PROCESO: COLPGC89 '.
           DISPLAY '==============================='.
           DISPLAY ' REGISTROS LEIDOS   FVD   : ' WSS-NUM-LFVD.
           DISPLAY ' REGISTROS GRABADOS C89   : ' WSS-NUM-GC89.
           DISPLAY '==============================='.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.
      *******************************************
       LEE-PARAM SECTION.
      *-----------------
       INI-LEE-PARAM.
           DISPLAY 'CAPTURA PARAMETROS'.
           DISPLAY ' '.

      * Captura Fecha de Proceso
           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '.
           ACCEPT WSS-FEC-FPRM IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO (DDMMSSAA): '
                    WSS-FEC-FPRM IN WSS-VARI.

      * Validacion de parametros
           MOVE WSS-FEC-FPRM IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'ERROR VALIDACION FECHA DE PROCESO: '
                                                    FEC-MENS
               PERFORM GNS-PRO-ABT.
      *
           MOVE WSS-NUM-DPRM IN WSS-VARI TO WSS-NUM-DPRO IN WSS-VARI.
           MOVE WSS-NUM-MPRM IN WSS-VARI TO WSS-NUM-MPRO IN WSS-VARI.
           MOVE WSS-NUM-SPRM IN WSS-VARI TO WSS-NUM-SPRO IN WSS-VARI.
           MOVE WSS-NUM-APRM IN WSS-VARI TO WSS-NUM-APRO IN WSS-VARI.
      *
      *DISPLAY
           ACCEPT WSS-IND-DISPLAY IN WSS-VARI.
           DISPLAY 'DISPLAY S/N  : '
                    WSS-IND-DISPLAY IN WSS-VARI.
           IF  WSS-IND-DISPLAY IN WSS-VARI NOT = 'S' AND
               WSS-IND-DISPLAY IN WSS-VARI NOT = 'N'
               DISPLAY 'ERROR INDICADOR DISPLAY S/N  : '
               PERFORM GNS-PRO-ABT.
       FIN-LEE-PARAM.
           EXIT.
      ******************
       COPY COLBFFVD.
       COPY COLBFC89.
      ******************
       COPY GNSBFMSG.
       COPY GNSBFMSC.

       COPY GNSBFTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGABT.

       COPY GNSBBMSG.
