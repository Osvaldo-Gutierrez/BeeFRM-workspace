       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGCRC.
       AUTHOR.          BEE (DPM).
       DATE-WRITTEN.    09-SEP-1998.

      * PROGRAMA GENERADOR DE ARCHIVO CRC(CLIENTE RELA OPC).
      *OPTIMIZAR (COLPGDCO)
       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-REC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-FIA-REQA==.
      *
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-REC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-FIA-REQA==.
      *>>>>
      *<<<< WSS
       COPY COLBROPS.
       COPY COLBRRDS.
       COPY COLBRCRC.
      *
       COPY SGCBRREC.
       COPY DEUBRFIA.
      *
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.

       01  WSS-VARI.
      * Variables para estadisticas
           03  WSS-NUM-RLEI       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RGRA       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RRCH       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RORC       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-REXI       VALUE ZEROES      PIC 9(06).

      * Monto parametro
           03  WSS-VAL-MPAR       VALUE ZEROES      PIC 9(14).
           03  WSS-VAL-DEUT       VALUE ZEROES      PIC 9(14).
           03  WSS-VAL-DEDI       VALUE ZEROES      PIC 9(14)V9(02).
           03  WSS-VAL-DEIN       VALUE ZEROES      PIC 9(14)V9(02).
           03  WSS-VAL-DECO       VALUE ZEROES      PIC 9(14)V9(02).
           03  WSS-VAL-DEFI       VALUE ZEROES      PIC 9(14)V9(02).
           03  WSS-FEC-FINI.
               05  WSS-NUM-SINI                     PIC 9(02).
               05  WSS-NUM-AINI                     PIC 9(02).
               05  WSS-NUM-MINI                     PIC 9(02).
               05  WSS-NUM-DINI                     PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                     PIC 9(02).
               05  WSS-NUM-ATER                     PIC 9(02).
               05  WSS-NUM-MTER                     PIC 9(02).
               05  WSS-NUM-DTER                     PIC 9(02).
      * DPM fin  30-JUN-1998
           03  WSS-VAL-TPES       VALUE ZEROES      PIC 9(13)V9(02).
           03  WSS-IND-FLAG       VALUE ZEROES      PIC 9(01).
           03  WSS-IND-DISP       VALUE SPACES      PIC X(01).
           03  WSS-CIC-IOPC       VALUE SPACES      PIC X(12).
           03  WSS-CIC-DEUD       VALUE SPACES      PIC X(12).
           03  WSS-CIC-ICLI       VALUE SPACES      PIC X(12).
           03  WSS-NUM-IDLC       VALUE ZEROES      PIC 9(03).
           03  WSS-NUM-TREC       VALUE ZEROES      PIC 9(02).
           03  WSS-IND-PROC       VALUE SPACES      PIC X(01).
           03  WSS-IND-BUSR       VALUE SPACES      PIC X(01).
      *
           03 WSS-NUM-CONT.
              05 WSS-NUM-LOPS     VALUE ZEROES      PIC 9(09).
              05 WSS-NUM-LRDS     VALUE ZEROES      PIC 9(09).
              05 WSS-NUM-LREC     VALUE ZEROES      PIC 9(09).
              05 WSS-NUM-LFIA     VALUE ZEROES      PIC 9(09).
              05 WSS-NUM-NRDS     VALUE ZEROES      PIC 9(09).
              05 WSS-NUM-ERDS     VALUE ZEROES      PIC 9(09).
              05 WSS-NUM-GCRC     VALUE ZEROES      PIC 9(09).
              05 WSS-NUM-ECRC     VALUE ZEROES      PIC 9(09).
      *
           03  SWITCHES.
              05 SW-RDS           PIC X(01) VALUE  '0'.
                 88  RDS-FST                VALUE  '1'.
                 88  RDS-NXT                VALUE  '2'.
                 88  RDS-FIN                VALUE  '3'.


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGDCO' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM OBT-PARAM.
           PERFORM PROCESA-OPS.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           PERFORM COL-FIO-RDS.
      *
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-CRC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR ABRIENDO COLCRC: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-OPEN-FILE.
           EXIT.


      ******************************************************************
       OBT-PARAM SECTION.
      *-----------------
       INI-OBT-PARAM.
           DISPLAY 'ACEPTA PARAMETROS.'
           DISPLAY '------------------'
      *
           ACCEPT   WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FECHA INICIO : ' WSS-FEC-FINI IN WSS-VARI.
           DISPLAY ' '.
      *
           ACCEPT   WSS-FEC-FTER IN WSS-VARI.
           DISPLAY 'FECHA TERMINO: ' WSS-FEC-FTER IN WSS-VARI.
           DISPLAY ' '.
      *
       FIN-OBT-PARAM.
           EXIT.


      ******************************************************************
       PROCESA-OPS SECTION.
      *-------------------
       INI-PROCESA-OPS.
           MOVE ZEROES TO  WSS-NUM-LOPS IN WSS-VARI.
           MOVE ZEROES TO  WSS-NUM-LRDS IN WSS-VARI.
           MOVE ZEROES TO  WSS-NUM-NRDS IN WSS-VARI.
           MOVE ZEROES TO  WSS-NUM-ERDS IN WSS-VARI.
           MOVE ZEROES TO  WSS-NUM-GCRC IN WSS-VARI.
           MOVE ZEROES TO  WSS-NUM-ECRC IN WSS-VARI.
      *
           MOVE '1'    TO  SW-RDS  IN WSS-VARI.
      *
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY OPC INGRESADAS HOY'
               GO TO FIN-PROCESA-OPS.

           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-RDS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY RDC PARA OPS '
               GO TO FIN-PROCESA-OPS.
      *
           ADD 1 TO  WSS-NUM-LRDS IN WSS-VARI.

       LUP-PROCESA-OPS.
           ADD 1 TO   WSS-NUM-LOPS IN WSS-VARI.
           PERFORM BUS-REG-RDS.
           IF WSS-IND-BUSR IN WSS-VARI = 'S'
              PERFORM LEE-RDC-OPC.

       NXT-PROCESA-OPS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           IF NOT  FIO-STAT-OKS
              GO TO FIN-PROCESA-OPS.
      *
           GO TO LUP-PROCESA-OPS.
      *
       FIN-PROCESA-OPS.
           EXIT.
      *
      ******************************************************************
       BUS-REG-RDS SECTION.
       INI-BUS-REG-RDS.
           MOVE 'N' TO WSS-IND-BUSR IN WSS-VARI.
      *
           IF RDS-FIN
              GO TO FIN-BUS-REG-RDS.
      *
       LUP-BUS-REG-RDS.
           IF RDC-CIC-IOPC IN RDS > OPC-CIC-IOPC IN OPS
              ADD 1 TO WSS-NUM-ERDS IN WSS-VARI
              DISPLAY 'ERROR-> OPC NO RDC ' OPC-CIC-IOPC IN OPS
              GO TO FIN-BUS-REG-RDS.

           IF RDC-CIC-IOPC IN RDS = OPC-CIC-IOPC IN OPS
              ADD 1 TO WSS-NUM-NRDS IN WSS-VARI
               MOVE 'S' TO WSS-IND-BUSR IN WSS-VARI
              GO TO FIN-BUS-REG-RDS.
      *
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-RDS.
           IF NOT FIO-STAT-OKS
              MOVE '3'    TO   SW-RDS  IN WSS-VARI
              GO TO FIN-BUS-REG-RDS.
      *
           ADD 1 TO  WSS-NUM-LRDS IN WSS-VARI.
           GO TO LUP-BUS-REG-RDS.
       FIN-BUS-REG-RDS.
           EXIT.
      *
       LEE-RDC-OPC SECTION.
       INI-LEE-RDC-OPC.
           IF RDC-CIC-IOPC IN RDS  NOT = OPC-CIC-IOPC IN OPS
              GO TO NXT-RDC.
      *
           PERFORM BUS-REC-RDC.
           PERFORM BUS-FIA-RDC.

           MOVE RDC-CIC-ICLI IN RDS TO WSS-CIC-DEUD IN WSS-VARI.
           PERFORM GRABA-ARCH-CRC.
       NXT-RDC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-RDS.
           IF NOT FIO-STAT-OKS
              DISPLAY ' FIN RDC '
              GO TO FIN-LEE-RDC-OPC.
      *
           IF  RDC-CIC-IOPC IN RDS NOT = OPC-CIC-IOPC IN OPS
               ADD 1 TO  WSS-NUM-LRDS IN WSS-VARI
               GO TO FIN-LEE-RDC-OPC.
      *
           ADD 1 TO  WSS-NUM-LRDS IN WSS-VARI.
           ADD 1 TO  WSS-NUM-NRDS IN WSS-VARI.
           GO TO INI-LEE-RDC-OPC.
       FIN-LEE-RDC-OPC.
           EXIT.
      ******************************************************************
       GRABA-ARCH-CRC SECTION.
      *---------------------
       INI-GRABA-ARCH-CRC.
           MOVE WSS-CIC-DEUD IN WSS-VARI TO CRC-CIC-ICLI IN CRC.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CRC.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-ECRC IN WSS-VARI
               DISPLAY 'ERROR GRABANDO COLCRC'
                       ' FIO-STAT: ' FIO-STAT
           ELSE
               ADD 1 TO WSS-NUM-GCRC IN WSS-VARI.
      *        DISPLAY 'CIC-ICLI: ' WSS-CIC-DEUD IN WSS-VARI
      *                '  CIC-IOPC ' OPC-CIC-IOPC IN OPS.
       FIN-GRABA-ARCH-CRC.
           EXIT.


      ******************************************************************
       BUS-REC-RDC SECTION.
      *-----------------------
       INI-BUS-REC-RDC.
      *Obtiene primero deudor Complementario
           MOVE RDC-CIC-ICLI IN  RDS TO REC-CIC-ICLI IN REC.
           MOVE 'CD'                 TO REC-COD-TREC IN REC.
           MOVE SPACES               TO REC-CIC-IREL IN REC.
           MOVE ZEROES               TO REC-NUM-TREC IN REC.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-BUS-REC-RDC.
           PERFORM SGC-FIO-REC.
           IF NOT (FIO-STAT-OKS AND
              REC-CIC-ICLI IN REC = RDC-CIC-ICLI IN RDS AND
              REC-COD-TREC IN REC = 'CD')
               GO TO FIN-BUS-REC-RDC.
      *
           IF REC-IND-VIGE IN REC NOT = 'S'
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-BUS-REC-RDC.
      *
           ADD 1 TO  WSS-NUM-LREC IN WSS-VARI.

           MOVE REC-CIC-IREL IN REC TO WSS-CIC-DEUD IN WSS-VARI.
           PERFORM GRABA-ARCH-CRC.
      *
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-BUS-REC-RDC.
       FIN-BUS-REC-RDC.
           EXIT.



       BUS-FIA-RDC SECTION.
       INI-BUS-FIA-RDC.
      *Obtiene Deudor de Fianza
           MOVE RDC-CIC-ICLI IN RDS TO FIA-CIC-ICLI IN FIA.
           MOVE SPACES              TO FIA-CIC-ICFS IN FIA.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-BUS-FIA-RDC.
           MOVE 'FIA-GLS-ICYF' TO FIO-AKEY.
           PERFORM DEU-FIO-FIA.
           IF NOT (FIO-STAT-OKS AND
                   RDC-CIC-ICLI IN RDS = FIA-CIC-ICLI IN FIA)
                   GO TO FIN-BUS-FIA-RDC.

           IF FIA-MSC-STAT IN FIA NOT = 'S'
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-BUS-FIA-RDC.
      *
           ADD 1 TO  WSS-NUM-LFIA IN WSS-VARI.
           MOVE FIA-CIC-ICFS IN FIA TO WSS-CIC-DEUD IN WSS-VARI.
           PERFORM GRABA-ARCH-CRC.
      *
           MOVE FIO-GET-NXT TO FIO-CMND
           GO TO LUP-BUS-FIA-RDC.
       FIN-BUS-FIA-RDC.
           EXIT.



      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           PERFORM COL-FIO-RDS.
           PERFORM COL-FIO-CRC.
       FIN-CLOSE-FILE.
           EXIT.


      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS DEL PROCESO.'.
           DISPLAY '-------------------------'.
           DISPLAY ' '.
           DISPLAY 'REG.OPS LEIDOS    : ' WSS-NUM-LOPS IN WSS-VARI.
           DISPLAY 'REG.REC LEIDOS    : ' WSS-NUM-LREC IN WSS-VARI.
           DISPLAY 'REG.FIA LEIDOS    : ' WSS-NUM-LFIA IN WSS-VARI.
           DISPLAY 'REG.RDS LEIDOS    : ' WSS-NUM-LRDS IN WSS-VARI.
           DISPLAY 'REG.RDC PROCESADA : '
                                  WSS-NUM-NRDS IN WSS-VARI.
           DISPLAY 'REG.RDC ERROR     : '
                                  WSS-NUM-ERDS IN WSS-VARI.
      *
           DISPLAY 'CLI.CRC GRABADOS: ' WSS-NUM-GCRC IN WSS-VARI.
           DISPLAY 'CLI.NO GRABADOS CRC: '
                                  WSS-NUM-ECRC IN WSS-VARI.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.


       COPY COLBFOPS.
       COPY COLBFRDS.
       COPY COLBFCRC.

       COPY SGCBFREC.
       COPY DEUBFFIA.
      *
       COPY GNSBGDTC.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBPFEC.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGABT.
      *
