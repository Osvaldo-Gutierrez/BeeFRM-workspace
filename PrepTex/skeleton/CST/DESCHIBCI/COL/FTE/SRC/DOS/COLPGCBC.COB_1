       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID.       COLPGCBC.
       AUTHOR.           CONSIST(RVM).
       DATE-WRITTEN.     12-JUN-1996 09:52:24.

      * CBC = CLAVES PARA BORRADO DE COL
      * PROGRAMA BATCH QUE CREA ARCHIVO SECUENCIAL CON LAS
      * CLAVES DE OPERACIONES QUE SERAN ELIMINAS DE LA BASE DE
      * DATOS DE COL.

       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
      *====================
       FILE-CONTROL.
      *-------------

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------

       WORKING-STORAGE SECTION.
      *-----------------------
      *INI-JGM 10-01-2000.
       COPY COLBRCBC.
      *FIN-JGM 10-01-2000.
       COPY COLBRCLV.
       COPY COLBROPC.
       COPY COLBRCAN.
       COPY COLBRROV.
       COPY COLBRRDC.
200599 COPY COLBRDLC.
200599 COPY COLBRCYA.
       COPY SGCBRDBC.

       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSBRMSC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSBRMSG.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       COPY TABBRTIO.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
200599 COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
200599 COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.

       01  WSS-VARI.
           03  WSS-KEY-IOPC.
               05  WSS-CIC-IOPC.
                   07  WSS-CAI-IOPC                   PIC X(04).
                   07  WSS-IIC-IOPC                   PIC X(08).
           03  WSS-FEC-FPA1.
               05  WSS-NUM-DPA1      VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-MPA1      VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-SPA1      VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-APA1      VALUE ZEROES     PIC 9(02).
           03  WSS-FEC-FFIN.
               05  WSS-NUM-SFIN      VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-AFIN      VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-MFIN      VALUE ZEROES     PIC 9(02).
               05  WSS-NUM-DFIN      VALUE ZEROES     PIC 9(02).
           03  WSS-NUM-OPCL          VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-OPCS          VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-OPCD          VALUE ZEROES     PIC 9(06).
      *     03  WSS-NUM-OPCL-R        VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-CANL          VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-CAND          VALUE ZEROES     PIC 9(06).
200599     03  WSS-NUM-DLCD          VALUE ZEROES     PIC 9(06).
200599     03  WSS-NUM-CYAD          VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-ROVL          VALUE ZEROES     PIC 9(06).
           03  WSS-NUM-CLVG          VALUE ZEROES     PIC 9(06).
     ***INI PRP 20.06.96
           03  WSS-NUM-NPAR          VALUE ZEROES     PIC 9(06).
     ***FIN PRP 20.06.96
           03  WSS-IND-RENO.
               05  WSS-IND-RENO-1                     PIC X(01).
               05  WSS-IND-RENO-2                     PIC X(02).
      *
           03  WSS-NUM-RBAD          VALUE ZEROES     PIC 9(01).
      *
      *INI-JGM 10-01-2000.
           03  WSS-NUM-RCBC          VALUE ZEROES     PIC 9(09).
           03  WSS-GRABA-REG         VALUE SPACES     PIC X(01).
      *FIN-JGM 10-01-2000.

       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
      *------------
       INI-MAIN.
      *
       COPY GNSBGEDB.
      *EFECTUA APERTURA DE ARCHIVOS
           PERFORM INICIO-CBC.

      *PROCESO PRINCIPAL
           PERFORM PROCESO-CBC.

      *CIERRA ARCHIVOS
           PERFORM FINAL-CBC.
      *
       FIN-MAIN.
           DISPLAY ' '.
           DISPLAY 'ESTADISTICAS  COLPGCBC'.
           DISPLAY '----------------------'.
           DISPLAY 'REG. COLOPC LEIDOS               : '
                   WSS-NUM-OPCL IN WSS-VARI.
           DISPLAY 'REG. COLOPC SELECCIONADOS        : '
                   WSS-NUM-OPCS IN WSS-VARI.
200599     DISPLAY 'REG. COLOPC DESCARTADOS POR S.CTB: '
200599             WSS-NUM-DLCD IN WSS-VARI.
           DISPLAY 'REG. COLOPC DESCARTADOS POR ROV  : '
                   WSS-NUM-OPCD IN WSS-VARI.
           DISPLAY 'REG. COLCAN LEIDOS               : '
                   WSS-NUM-CANL IN WSS-VARI.
           DISPLAY 'REG. COLOPC DESCARTADOS POR FOUT : '
                   WSS-NUM-CAND IN WSS-VARI.
200599     DISPLAY 'REG. COLOPC DESCARTADOS POR T.CAN: '
200599             WSS-NUM-CYAD IN WSS-VARI.
      *     DISPLAY 'REG. COLROV LEIDOS               : '
      *             WSS-NUM-ROVL IN WSS-VARI.
      *     DISPLAY 'REG. COLOPC ASOCIADOS A ROV      : '
      *             WSS-NUM-OPCL-R IN WSS-VARI.
           DISPLAY 'REG. COLCLV GRABADOS             : '
                   WSS-NUM-CLVG IN WSS-VARI.
      *INI-JGM 10-01-2000.
           DISPLAY 'REG. NO GRABADOS(EXISTE ARCHIVO CBC)'
                   WSS-NUM-RCBC IN WSS-VARI.
      *FIN-JGM 10-01-2000.
           DISPLAY ' '.
      *
           IF WSS-NUM-RBAD IN WSS-VARI > ZEROES
      *        PERFORM GNS-PRG-ABT
               MOVE 99 TO RETURN-CODE
               PERFORM PRG-ABT
           ELSE
               PERFORM GNS-PRO-END.

      ***********************************************************
      * PROCESO DE APERTURA DE ARCHIVOS Y LECTURA DE PARAMETROS *
      ***********************************************************
       INICIO-CBC SECTION.
      *---------------
       INI-INICIO-CBC.
           MOVE 'COLPGCBC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

      *APERTURA DE ARCHIVOS

      *LDCSEL (SECUENCIAL)
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-CLV.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO COLCLV '
               DISPLAY 'DE OUTPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

      *COLOPC
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO COLOPC '
               DISPLAY 'DE INPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

      *COLCAN
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO COLCAN '
               DISPLAY 'DE INPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

      *COLROV
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO COLROV '
               DISPLAY 'DE INPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

      *COLRDC
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO COLRDC '
               DISPLAY 'DE INPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

200599*COLDLC
200599     MOVE FIO-INP TO FIO-CMND.
200599     PERFORM COL-FIO-DLC.
200599     IF NOT FIO-STAT-OKS
200599         DISPLAY 'ERROR AL ABRIR ARCHIVO COLDLC '
200599         DISPLAY 'DE INPUT   '    FIO-STAT
200599         MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
200599         GO TO FIN-INICIO-CBC.

200599*COLCYA
200599     MOVE FIO-INP TO FIO-CMND.
200599     PERFORM COL-FIO-CYA.
200599     IF NOT FIO-STAT-OKS
200599         DISPLAY 'ERROR AL ABRIR ARCHIVO COLCYA '
200599         DISPLAY 'DE INPUT   '    FIO-STAT
200599         MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
200599         GO TO FIN-INICIO-CBC.

      *SGCDBC
           MOVE 'SGC'   TO FIO-SIST.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO SGCDBC '
               DISPLAY 'DE INPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

      *TABTAB
           MOVE 'TAB'   TO FIO-SIST.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO TABTAB '
               DISPLAY 'DE INPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

      *GNSTAB
           MOVE 'GNS'   TO FIO-SIST.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO GNSTAB '
               DISPLAY 'DE INPUT   '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.
      *
      *INI-JGM 10-01-2000.
      *CBC
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CBC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO COLCBC '
               DISPLAY 'DE INP      '    FIO-STAT
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.
      *FIN-JGM 10-01-2000.

      *RECIBE FECHA DE PARAMETRO
           DISPLAY ' '.
           DISPLAY 'INGRESE FECHA DE PROCESO  (DDMMSSAA) : '.
           ACCEPT  WSS-FEC-FPA1 IN WSS-VARI.
           DISPLAY WSS-FEC-FPA1 IN WSS-VARI.
     ***INI PRP 20.06.96
           DISPLAY ' '.
           DISPLAY 'INGRESE MAXIMO DE OPERACIONES A GRABAR: '.
           ACCEPT  WSS-NUM-NPAR IN WSS-VARI.
           DISPLAY WSS-NUM-NPAR IN WSS-VARI.
     ***FIN PRP 20.06.96

      *CONVIERTE (DDMMSSAA) A (SSAAMMDD) PARAMETRO 1
           MOVE WSS-NUM-DPA1 IN WSS-VARI TO WSS-NUM-DFIN IN WSS-VARI.
           MOVE WSS-NUM-MPA1 IN WSS-VARI TO WSS-NUM-MFIN IN WSS-VARI.
           MOVE WSS-NUM-SPA1 IN WSS-VARI TO WSS-NUM-SFIN IN WSS-VARI.
           MOVE WSS-NUM-APA1 IN WSS-VARI TO WSS-NUM-AFIN IN WSS-VARI.

      *VALIDA FECHA DE PARAMETRO 1
           MOVE WSS-FEC-FPA1 IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
      *    MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
      *    MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-INICIO-CBC.

       FIN-INICIO-CBC.
           EXIT.

       PROCESO-CBC SECTION.
      *--------------------
       INI-PROCESO-CBC.

           IF WSS-NUM-RBAD IN WSS-VARI > ZEROES
               GO TO FIN-PROCESO-CBC.

      *LEE A PARTIR DESDE LA PRIMERA OPC
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: NO EXISTEN DATOS EN COLOPC '
               DISPLAY 'PROCESO CANCELADO...'
               MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI
               GO TO FIN-PROCESO-CBC.

           MOVE SPACES TO WSS-CIC-IOPC IN WSS-VARI.

       LUP-PROCESO-CBC.

           IF NOT FIO-STAT-OKS
     ***INI PRP 20.06.96
              OR WSS-NUM-CLVG IN WSS-VARI = WSS-NUM-NPAR IN WSS-VARI
     ***FIN PRP 20.06.96
               GO TO FIN-PROCESO-CBC.

           ADD 1 TO WSS-NUM-OPCL IN WSS-VARI.

           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO NXT-PROCESO-CBC.

           IF OPC-VAL-SCRE IN OPC NOT = ZEROES
               GO TO NXT-PROCESO-CBC.

           MOVE OPC-IND-RENO IN OPC TO WSS-IND-RENO IN WSS-VARI.
           IF WSS-IND-RENO-1 IN WSS-VARI = 'P' OR 'N' OR 'R'
               GO TO NXT-PROCESO-CBC.


      *VALIDA QUE OPERACION DE CREDITO SEA ADMINISTRADA POR 'COL'
           PERFORM VALIDA-COL.
           IF NOT FIO-STAT-OKS
               GO TO NXT-PROCESO-CBC.

      *

200599*VALIDA QUE OPERACION TENGA SITUACION CONTABLE = SPACES
200599     PERFORM VALIDA-DLC.
200599     IF NOT FIO-STAT-OKS
200599         GO TO NXT-PROCESO-CBC.

      *
           ADD 1 TO WSS-NUM-OPCS IN WSS-VARI.
SACAR *    DISPLAY 'OPC PARA PROCESO .......: ' OPC-CIC-IOPC IN OPC.
      *

      *LEE ULTIMA CAN DE OPC Y VALIDA CAN-FEC-FOUT QUE NO SEA MAYOR
      *A FPRO
           PERFORM LEE-ULT-CAN.
           IF NOT FIO-STAT-OKS
               GO TO NXT-PROCESO-CBC.


200599*LEE LA CYA DE LA ULTIMA CAN Y VERIFICA QUE NO SEA CTBL
200599     PERFORM VALIDA-CYA.
200599     IF NOT FIO-STAT-OKS
200599         GO TO NXT-PROCESO-CBC.

      *VALIDO QUE NO EXISTAN ROV'S ASOCIADAS A ESTA CAN Y OPC
      *LEO ROV
           PERFORM VALIDA-ROV.
           IF FIO-STAT-OKS
SACAR *        DISPLAY 'OPC DESCARTADO POR ROV ASOCIADO : '
SACAR *                ROV-KEY-IROV IN ROV
               ADD 1 TO WSS-NUM-OPCD IN WSS-VARI
               GO TO NXT-PROCESO-CBC.

      *OBTENGO EL RUT Y EL CIC DEL CLIENTE DESDE DBC
           MOVE OPC-CAI-IOPC IN OPC TO RDC-CAI-IOPC IN RDC.
           MOVE OPC-IIC-IOPC IN OPC TO RDC-IIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
SACAR *        DISPLAY 'NO EXISTE COLRDC PARA OPC : '
SACAR *          OPC-CIC-IOPC IN OPC
               GO TO NXT-PROCESO-CBC.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
SACAR *        DISPLAY 'NO EXISTE SGCDBC PARA OPC : '
SACAR *          OPC-CIC-IOPC IN OPC
               GO TO NXT-PROCESO-CBC.

      *GRABAR REGISTROS
      *INI-JGM 10-01-2000.
           PERFORM BUSCA-CLIENTE-CBC.
           IF WSS-GRABA-REG IN WSS-VARI = 'S'
      *FIN-JGM 10-01-2000.
              PERFORM GRABAR-CLV.

       NXT-PROCESO-CBC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           GO TO LUP-PROCESO-CBC.

      * ERR-PROCESO-CBC.
      *     DISPLAY '.....: AL PROCESAR OPC : ' OPC-CIC-IOPC IN OPC.
      *     MOVE 1 TO WSS-NUM-RBAD IN WSS-VARI.
      *     GO TO FIN-PROCESO-CBC.

       FIN-PROCESO-CBC.
           EXIT.
      *INI-JGM 10-01-2000.
       BUSCA-CLIENTE-CBC SECTION.
       INI-BUSCA-CLIENTE-CBC.
           MOVE 'N'                 TO WSS-GRABA-REG IN WSS-VARI.
           MOVE RDC-CIC-ICLI IN RDC TO CBC-CIC-ICLI IN CBC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-CBC.
           IF  FIO-STAT-OKS
               ADD 1           TO WSS-NUM-RCBC  IN WSS-VARI
               MOVE 'S'        TO WSS-GRABA-REG IN WSS-VARI.
       FIN-BUSCA-CLIENTE-CBC.
           EXIT.
      *FIN-JGM 10-01-2000.

       LEE-ULT-CAN SECTION.
       INI-LEE-ULT-CAN.

      *LEE ULTIMA CAN ASOCIADA A OPC
           MOVE OPC-CIC-IOPC IN OPC TO CAN-CIC-IOPC IN CAN.
           MOVE 999                 TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT (FIO-STAT-OKS AND
                   OPC-CIC-IOPC IN OPC = CAN-CIC-IOPC IN CAN)
                    DISPLAY 'ERROR: NO EXISTE CANCELACION '
                            'PARA OPERACION : ' CAN-CIC-IOPC IN CAN
                    MOVE '23' TO FIO-STAT
                    GO TO FIN-LEE-ULT-CAN.

           ADD 1 TO WSS-NUM-CANL IN WSS-VARI.

           IF CAN-FEC-FOUT IN CAN > WSS-FEC-FFIN IN WSS-VARI
               ADD 1 TO WSS-NUM-CAND IN WSS-VARI
SACAR *        DISPLAY 'OPC DESCARTADO POR FECHA DE CAN : '
SACAR *                OPC-CIC-IOPC IN OPC ' FECHA : '
SACAR *                CAN-FEC-FOUT IN CAN
               MOVE '23' TO FIO-STAT
               GO TO FIN-LEE-ULT-CAN.

       FIN-LEE-ULT-CAN.
           EXIT.

200599 VALIDA-DLC SECTION.
200599 INI-VALIDA-DLC.
200599*ACCESA A LA DLC POR CLAVE OPC PARA SABER SU SITUACION CONTABLE
200599     MOVE OPC-KEY-IOPC        TO DLC-CIC-IOPC IN DLC.
200599     MOVE 1                   TO DLC-NUM-IDLC IN DLC.
200599     MOVE 'DLC-KEY-IDLC' TO FIO-AKEY.
200599     MOVE FIO-GET-KEY TO FIO-CMND.
200599     PERFORM COL-FIO-DLC.
200599     IF FIO-STAT-OKS AND
200599        (DLC-IND-SCTB IN DLC = SPACES)
200599          GO TO FIN-VALIDA-DLC.
200599     MOVE '23' TO FIO-STAT.
200599     ADD 1 TO WSS-NUM-DLCD IN WSS-VARI.
SACAR9*    DISPLAY 'OPC DESCARTADO POR SITUAC. CTBL : '
SACAR9*      OPC-CIC-IOPC IN OPC ' SITUAC : '
SACAR9*      DLC-IND-SCTB IN DLC.
200599 FIN-VALIDA-DLC.
200599     EXIT.

200599 VALIDA-CYA SECTION.
200599 INI-VALIDA-CYA.
200599*ACCESA A LA CYA POR CLAVE CAN PARA SABER SI NO ES CANC. CTBL.
200599     MOVE CAN-KEY-ICAN IN CAN TO CYA-KEY-ICAN IN CYA.
200599     MOVE 1                   TO CYA-NUM-ICYA IN CYA.
200599     MOVE 'CYA-KEY-ICYA' TO FIO-AKEY.
200599     MOVE FIO-GET-KEY TO FIO-CMND.
200599     PERFORM COL-FIO-CYA.
200599     IF FIO-STAT-OKS AND
200599        (CYA-IND-TCYA IN CYA NOT = 'CTBL ')
200599          GO TO FIN-VALIDA-CYA.
200599     MOVE '23' TO FIO-STAT.
200599     ADD 1 TO WSS-NUM-CYAD IN WSS-VARI.
SACAR9*    DISPLAY 'CAN DESCARTADA POR TIPO CANCELAC: '
SACAR9*      CYA-KEY-ICYA IN CYA ' T.CANC.: '
SACAR9*      CYA-IND-TCYA IN CYA.
200599 FIN-VALIDA-CYA.
200599     EXIT.

       VALIDA-ROV SECTION.
       INI-VALIDA-ROV.
      *ACCESA A LA ROV POR LAS DOS CLAVES, PARA SABER SI ESTA OPERACION
      *NO TIENE RELACIONES
           MOVE CAN-CIC-IOPC IN CAN TO ROV-CIC-IOPV IN ROV.
           MOVE CAN-NUM-ICAN IN CAN TO ROV-NUM-IVCT IN ROV.
           MOVE 'ROV-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ROV.
           IF FIO-STAT-OKS AND
              (CAN-CIC-IOPC IN CAN = ROV-CIC-IOPV IN ROV)
                GO TO FIN-VALIDA-ROV
           ELSE
                MOVE CAN-CIC-IOPC IN CAN TO ROV-CIC-IOPC IN ROV
                MOVE SPACES      TO FIO-AKEY
                MOVE FIO-GET-NLS TO FIO-CMND
                PERFORM COL-FIO-ROV
                IF FIO-STAT-OKS AND
                   (CAN-CIC-IOPC IN CAN = ROV-CIC-IOPC IN ROV)
                    GO TO FIN-VALIDA-ROV.

           MOVE '23' TO FIO-STAT.
       FIN-VALIDA-ROV.
           EXIT.

       VALIDA-COL SECTION.
       INI-VALIDA-COL.
      * BUSCA SISTEMA EN DONDE OPERA
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'TIO'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-TOOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'TIO    NEX'   TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' ' OPC-KEY-IOPC IN OPC
                       ' ' TAB-COD-CTAB IN TAB
               MOVE '23' TO FIO-STAT
               GO TO FIN-VALIDA-COL.

           MOVE TAB TO TIO.
           IF TIO-GLS-SIST IN TIO NOT = 'COL'
               MOVE '23' TO FIO-STAT
               GO TO FIN-VALIDA-COL.

           MOVE '00' TO FIO-STAT.
       FIN-VALIDA-COL.
           EXIT.

       GRABAR-CLV SECTION.
       INI-GRABAR-CLV.
           MOVE SPACES              TO CLV-GLS-FLAG IN CLV.
           MOVE OPC-CIC-IOPC IN OPC TO CLV-CIC-IOPC IN CLV.
           MOVE DBC-CIC-ICLI IN DBC TO CLV-CIC-ICLI IN CLV.
           MOVE DBC-NUM-ICLI IN DBC TO CLV-NUM-ICLI IN CLV.
           MOVE DBC-VRF-ICLI IN DBC TO CLV-VRF-ICLI IN CLV.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CLV.
           IF FIO-STAT-OKS
               ADD 1 TO WSS-NUM-CLVG IN WSS-VARI
           ELSE
               DISPLAY 'ERROR: AL GRABAR REGISTRO COLCLV : '
                    CLV-CIC-IOPC IN CLV.
       FIN-GRABAR-CLV.
           EXIT.

       FINAL-CBC SECTION.
       INI-FINAL-CBC.
      *-------------------------------------
           MOVE FIO-CLO TO FIO-CMND.
           MOVE 'COL' TO FIO-SIST.
           PERFORM COL-FIO-CLV.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-ROV.
           PERFORM COL-FIO-RDC.
      *INI-JGM 10-01-2000.
           PERFORM COL-FIO-CBC.
      *FIN-JGM 10-01-2000.
           MOVE 'SGC' TO FIO-SIST.
           PERFORM SGC-FIO-DBC.
           MOVE 'TAB' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE 'GNS' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *-------------------------------------
       FIN-FINAL-CBC.
           EXIT.
      *

      *INI-JGM 10-01-2000
       COPY COLBFCBC.
      *FIN-JGM 10-01-2000

       COPY COLBFCLV.
       COPY COLBFOPC.
200599 COPY COLBFDLC.
200599 COPY COLBFCYA.
       COPY COLBFCAN.
       COPY COLBFROV.
       COPY COLBFRDC.
       COPY SGCBFDBC.

       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGABT.
       COPY GNSBBTAB.
       COPY GNSBFMSG.
       COPY GNSBBMSG.
       COPY GNSBFTAB.
       COPY GNSBFMSC.
       COPY GNSBGEND.
       COPY GNSBPFEC.
       COPY GNSBGFEC.
       COPY GNSBGHOY.

