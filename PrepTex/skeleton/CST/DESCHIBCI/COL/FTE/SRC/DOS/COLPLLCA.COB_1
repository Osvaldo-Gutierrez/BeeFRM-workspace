       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLLCA.
       AUTHOR.          BEE-TEAM ( DPM ).
       DATE-WRITTEN.    14-Mar-97 01:50 PM.

      * Programa Listador de Reporte LCA con Sort.

      *<<<< CMT_ID
      *>>>>

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTLCA         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTLCA         ASSIGN TO        UT-S-RPTLCA.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTLCA         ASSIGN TO        UT-S-RPTLCA.
      *<<<< IOS
      *>>>>

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRLCA.
      *<<<< RR
      *>>>>
       FD  RPTLCA LABEL RECORDS OMITTED
           REPORT IS RPT-LCA
      *<<<< FD_RPT
      *>>>>
           .
      *<<<< FLS
      *>>>>

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LCA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRLCA.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWLCA.
      *<<<< RW
      *>>>>
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       01 WSS-VARI.
           03 WSS-NUM-TOPE        VALUE  69      PIC 9(03).
           03 WSS-NUM-DIFE        VALUE ZEROES   PIC 9(03).
           03 WSS-COD-PESO        VALUE SPACES   PIC X(06).
           03 WSS-COD-UFS         VALUE SPACES   PIC X(06).
           03 WSS-AUX-IOPC        VALUE SPACES   PIC X(12).
           03 WSS-FEC-FCOL.
              05 WSS-NUM-SCOL                    PIC 9(02).
              05 WSS-NUM-ACOL                    PIC 9(02).
              05 WSS-NUM-MCOL                    PIC 9(02).
              05 WSS-NUM-DCOL                    PIC 9(02).
           03 WSS-IND-STAT        VALUE 1        PIC 9(01).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY COLBRDCA.
       COPY COLBROPC.
       COPY COLBRRDC.
       COPY COLBRDLC.
       COPY SGCBRDBC.
       COPY SGCBRDCP.
       COPY GNSWGFEC.
       COPY GNSWGCPT.
       COPY GNSWGHOY.
       COPY TABBROFI.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY COLRFLCA.
      *<<<< RF
      *>>>>
           .
       COPY COLRTLCA.
      *<<<< RT
      *>>>>

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-LCA-OPC-COD-OFOC SECTION.
           USE BEFORE REPORTING CF-LCA-OPC-COD-OFOC.
       INI-CF-LCA-OPC-COD-OFOC.
      *<<<< CF_LCA_OPC_COD_OFOC
      *>>>>
       FIN-CF-LCA-OPC-COD-OFOC.
           EXIT.
       DEC-PH-LCA SECTION.
           USE BEFORE REPORTING PH-LCA.
       INI-PH-LCA.
      *<<<< PH_LCA
      *>>>>
       FIN-PH-LCA.
           EXIT.
       DEC-FF-LCA SECTION.
           USE BEFORE REPORTING FF-LCA.
       INI-FF-LCA.
      *<<<< FF_LCA
           MOVE SPACES TO WSS-GLS-TITU IN WSS-LCA
                          WSS-GLS-OFOC IN WSS-LCA
                          OPC-COD-OFOC IN SRT.
           MOVE 99 TO LINE-COUNTER IN RPT-LCA.
      *>>>>
       FIN-FF-LCA.
           EXIT.
      *<<<< FIN_DEC
      *>>>>
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLLCA' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
      *>>>>
           SORT GNSSRT
       COPY COLRSLCA.
      *<<<< RS
      *>>>>
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
      *<<<< FIN
      *>>>>
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-DCA.

           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-RDC.
           PERFORM SGC-FIO-DBC.
           PERFORM SGC-FIO-DCP.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-LCA.
       FST-INP-SRT.
      *<<<< INI_FST_INP
      * Se obtiene por unica vez COD_PESO, para chequear si la
      * Operacion es en peso u otra, de la siguiente forma :
           MOVE 'TAB'                     TO FIO-SIST.
           MOVE 'VLR'                     TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '                  TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL'            TO FIO-AKEY.
           MOVE  FIO-GET-KEY              TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR TAB-IND-VIGE IN TAB = 'N'
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE 'CICPESOINEX'        TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG '  0999  '
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

      * Se obtiene por unica vez COD_UF, para chequear si la
      * Operacion es en peso u otra, de la siguiente forma :
           MOVE 'TAB'                     TO FIO-SIST.
           MOVE 'VLR'                     TO TAB-CIC-TTAB IN TAB.
           MOVE '0998  '                  TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL'            TO FIO-AKEY.
           MOVE  FIO-GET-KEY              TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR TAB-IND-VIGE IN TAB = 'N'
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE 'CICUF  INEX'        TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG '  0998  '
               MOVE PGM-SMAL TO PGM-STAT-SRT
               GO TO FIN-INP-SRT.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-UFS IN WSS-VARI.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-LCA.
      *<<<< FIN_FST_INP
      *>>>>
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      *Lee DLC para obtener Fecha Max Vcto
           MOVE LCA-CIC-IOPC IN LCA TO DLC-CIC-IOPC IN DLC.
           MOVE ZEROES TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT (FIO-STAT-OKS AND
                   LCA-CIC-IOPC IN LCA = DLC-CIC-IOPC IN DLC)
               DISPLAY 'NO EXISTE REG DLC PARA OPC ' 
                                          LCA-CIC-IOPC IN LCA
               GO TO NXT-INP-SRT.

           IF LCA-VAL-SCRE IN LCA > ZEROES
               PERFORM MOVE-LCA-OPC
           ELSE
           IF LCA-CIC-IOPC IN LCA = DLC-CIC-IOCO IN DLC
               GO TO NXT-INP-SRT
           ELSE
               MOVE DLC-CIC-IOCO IN DLC TO OPC-CIC-IOPC IN OPC
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM COL-FIO-OPC
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR AL LEER OPC ORIGINAL '
                                            OPC-CIC-IOPC IN OPC
                   GO TO NXT-INP-SRT
               ELSE
               IF OPC-VAL-SCRE IN OPC > ZEROES
                   MOVE LCA-FEC-FCOL IN LCA TO OPC-FEC-FCOL IN OPC
                   MOVE LCA-VAL-SEGD IN LCA TO OPC-VAL-SEGD IN OPC
                   MOVE LCA-COD-RMOC IN LCA TO OPC-COD-RMOC IN OPC
                   MOVE LCA-COD-OFOC IN LCA TO OPC-COD-OFOC IN OPC
               ELSE
                   GO TO NXT-INP-SRT.

       NXT-INP.
           PERFORM OBT-DATOS-CLI.
           IF WSS-IND-STAT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.

           PERFORM CAL-DIF-FECHAS.
           IF WSS-NUM-DIFE IN WSS-VARI NOT > WSS-NUM-TOPE IN WSS-VARI
               GO TO NXT-INP-SRT.
           IF OPC-COD-VCOC IN OPC = WSS-COD-PESO IN WSS-VARI
               MOVE OPC-VAL-SCRE IN OPC TO SRT-VAL-SCRP IN SRT
               MOVE ZEROES              TO SRT-VAL-SCRU IN SRT
               MOVE 1                   TO SRT-VAL-OPCP IN SRT
               MOVE ZEROES              TO SRT-VAL-OPCU IN SRT
           ELSE
               MOVE ZEROES              TO SRT-VAL-SCRP IN SRT
               MOVE OPC-VAL-SCRE IN OPC TO SRT-VAL-SCRU IN SRT
               MOVE ZEROES              TO SRT-VAL-OPCP IN SRT
               MOVE 1                   TO SRT-VAL-OPCU IN SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMLCA.
      *<<<< RM
           MOVE OPC-CIC-IOPC IN SRT TO DCA-CIC-IOPC IN DCA.
           MOVE DBC-GLS-NOMC IN SRT TO DCA-GLS-NOMC IN DCA.
           MOVE OPC-COD-OFOC IN SRT TO DCA-COD-OFOC IN DCA.
           MOVE DBC-NUM-ICLI IN SRT TO DCA-NUM-ICLI IN DCA.
           MOVE DBC-VRF-ICLI IN SRT TO DCA-VRF-ICLI IN DCA.
           MOVE DCP-NUM-DNAC IN SRT TO DCA-NUM-DNAC IN DCA.
           MOVE DCP-NUM-MNAC IN SRT TO DCA-NUM-MNAC IN DCA.
           MOVE DCP-NUM-SNAC IN SRT TO DCA-NUM-SNAC IN DCA.
           MOVE DCP-NUM-ANAC IN SRT TO DCA-NUM-ANAC IN DCA.
           MOVE OPC-NUM-DCOL IN SRT TO DCA-NUM-DCOL IN DCA.
           MOVE OPC-NUM-MCOL IN SRT TO DCA-NUM-MCOL IN DCA.
           MOVE OPC-NUM-SCOL IN SRT TO DCA-NUM-SCOL IN DCA.
           MOVE OPC-NUM-ACOL IN SRT TO DCA-NUM-ACOL IN DCA.
           MOVE DLC-NUM-DMAV IN SRT TO DCA-NUM-DMAV IN DCA.
           MOVE DLC-NUM-MMAV IN SRT TO DCA-NUM-MMAV IN DCA.
           MOVE DLC-NUM-SMAV IN SRT TO DCA-NUM-SMAV IN DCA.
           MOVE DLC-NUM-AMAV IN SRT TO DCA-NUM-AMAV IN DCA.
           MOVE SRT-VAL-OPCP IN SRT TO DCA-VAL-OPCP IN DCA.
           MOVE SRT-VAL-SCRP IN SRT TO DCA-VAL-SCRP IN DCA.
           MOVE SRT-VAL-SCRU IN SRT TO DCA-VAL-SCRU IN DCA.
           MOVE SRT-VAL-OPCU IN SRT TO DCA-VAL-OPCU IN DCA.
           MOVE OPC-VAL-SEGD IN SRT TO DCA-VAL-SEGD IN DCA.
           MOVE OPC-COD-RMOC IN SRT TO DCA-COD-RMOC IN DCA.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-DCA.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR GRABACION ARCHIVO SALIDA DCA PARA OPC: '
                            DCA-CIC-IOPC IN DCA.
      *>>>>
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-LCA.
      *<<<< FIN_NXT_INP
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
      *<<<< FIN_INP
      *>>>>
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-LCA.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
      *<<<< INI_OUT
      *>>>>
           OPEN OUTPUT RPTLCA.
      *     MOVE 'COLPLLCA' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-LCA.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
           MOVE 'OFICINA :' TO WSS-GLS-TITU IN WSS-LCA.
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *Obtiene glosa de oficina
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFOC IN WSS-LCA.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-LCA.
      *<<<< FIN_GEN_OUT
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      *<<<< FIN_OUT
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-DCA.
           PERFORM COL-FIO-DLC.
           PERFORM COL-FIO-RDC.
           PERFORM SGC-FIO-DBC.
           PERFORM SGC-FIO-DCP.
      *>>>>
           TERMINATE RPT-LCA.
      *<<<< FIN_TMT
      *>>>>
           CLOSE RPTLCA.
       EXT-OUT-SRT.
      *<<<< EXT_OUT
      *>>>>
           EXIT.
       COPY COLBFLCA.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
       COPY GNSBGHOY.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBGCPT.
       COPY GNSBGFEC.
       COPY GNSBPFEC.

       COPY COLBFOPC.
       COPY COLBFDCA.
       COPY COLBFRDC.
       COPY COLBFDLC.
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY TABBFOFI.
       COPY TABBBOFI.


       MOVE-LCA-OPC SECTION.
      *--------------------
       INI-MOVE-LCA-OPC.
           MOVE LCA-CIC-IOPC IN LCA TO OPC-CIC-IOPC IN OPC.
           MOVE LCA-FEC-FCOL IN LCA TO OPC-FEC-FCOL IN OPC.
           MOVE LCA-VAL-SCRE IN LCA TO OPC-VAL-SCRE IN OPC.
           MOVE LCA-VAL-SEGD IN LCA TO OPC-VAL-SEGD IN OPC.
           MOVE LCA-COD-RMOC IN LCA TO OPC-COD-RMOC IN OPC.
           MOVE LCA-COD-VCOC IN LCA TO OPC-COD-VCOC IN OPC.
           MOVE LCA-COD-OFOC IN LCA TO OPC-COD-OFOC IN OPC.
       FIN-MOVE-LCA-OPC.
           EXIT.


       OBT-DATOS-CLI SECTION.
      *---------------------
       INI-OBT-DATOS-CLI.
           MOVE 1 TO WSS-IND-STAT IN WSS-VARI.
           MOVE OPC-CIC-IOPC IN OPC TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-RDC.

           IF NOT ( FIO-STAT-OKS AND
                    RDC-CIC-IOPC IN RDC = OPC-CIC-IOPC IN OPC AND
                    RDC-NUM-IDLC IN RDC = ZEROES )
               DISPLAY 'ERROR LECTURA RDC ' OPC-CIC-IOPC IN OPC
               GO TO FIN-OBT-DATOS-CLI.

           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR EN LECTURA DE DBC'
               GO TO FIN-OBT-DATOS-CLI.

           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.

           MOVE DBC-CIC-ICLI IN DBC TO DCP-CIC-ICLI IN DCP.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DCP.

           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR EN LECTURA DE DCP'
               GO TO FIN-OBT-DATOS-CLI.
           MOVE 0 TO WSS-IND-STAT IN WSS-VARI.
       FIN-OBT-DATOS-CLI.
           EXIT.


       CAL-DIF-FECHAS SECTION.
      *----------------------
       INI-CAL-DIF-FECHAS.
           IF DCP-FEC-FNAC IN DCP IS NOT NUMERIC
              MOVE ZEROES TO DCP-FEC-FNAC IN DCP.

           IF DCP-FEC-FNAC IN DCP > ZEROES
               MOVE DCP-NUM-DNAC IN DCP TO FEC-DEC1
               MOVE DCP-NUM-MNAC IN DCP TO FEC-MEC1
               MOVE DCP-NUM-SNAC IN DCP TO FEC-SEC1
               MOVE DCP-NUM-ANAC IN DCP TO FEC-AEC1

               MOVE OPC-NUM-DCOL IN OPC TO FEC-DEC2
               MOVE OPC-NUM-MCOL IN OPC TO FEC-MEC2
               MOVE OPC-NUM-SCOL IN OPC TO FEC-SEC2
               MOVE OPC-NUM-ACOL IN OPC TO FEC-AEC2

               MOVE FEC-FORM-FEC        TO FEC-FORM
               MOVE FEC-DIF-ANO         TO FEC-CMND
               PERFORM CAL-FEC
               MOVE FEC-NANO TO WSS-NUM-DIFE IN WSS-VARI
           ELSE
               MOVE WSS-NUM-TOPE IN WSS-VARI TO
                                        WSS-NUM-DIFE IN WSS-VARI
               ADD 1 TO WSS-NUM-DIFE IN WSS-VARI.
       FIN-CAL-DIF-FECHAS.
           EXIT.
      *>>>>
