       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     COLPGAAC.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  17/07/92.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Colocaciones                                       *
      *  Subsist. : Lineas de Avances                                  *
      *  Analista : Retamal Pablo.                                     *
      *  Objetivo : Elimina, crea reg en meatro cartolas y resetea     *
      *             indice a listar                                    *
      *  Archivos :                                                    *
      *   Entrada : COLVPAR, Registro de parametros                    *
      *             LDA 390, Tabla de lineas de avances                *
      *                                                                *
      *   Salida  :                                                    *
      *                                                                *
      *   Ent/Sal : COLMCAR, Maestro cartolas LDA                      *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : .....................................  *
      *                         .....................................  *
      *                         .....................................  *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *=====================
       SPECIAL-NAMES.
                      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT COLVPAR ASSIGN       TO COLVPAR
                  FILE STATUS          IS WR-FS-COLVPAR.
           SELECT COLMCAR ASSIGN       TO COLMCAR
                  ORGANIZATION         IS INDEXED
                  ACCESS               IS DYNAMIC
                  RECORD KEY           IS CAR-CLAVE-LDA
                  FILE STATUS          IS WR-FS-COLMCAR.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
       FD  COLVPAR
           RECORD CONTAINS 080 CHARACTERS
           LABEL RECORD IS STANDARD.
       01  COLRPAR.
           02 FILLER                   PIC X(20).
           02 PAR-FEC-PRO              PIC X(08).
           02 FILLER                   PIC X(20).
           02 PAR-IND-BOR              PIC X(02).
           02 FILLER                   PIC X(30).
           SKIP1
       FD  COLMCAR.
       COPY COLMCAR.
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       01  W-SWITCHES.
           05 WS-PROCESO             PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-NO-OK    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
           05 WS-LDA-COD-EST         PIC X(03) VALUE SPACES.
              88 WS-EST-NON-ING      VALUE 'ACA' 'NVG' 'NCU' 'VCD'.
              88 WS-EST-NON-ING-VCD  VALUE 'ACA' 'NVG' 'NCU'.
              88 WS-EST-VCD          VALUE 'VCD'.
           SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-LDA               PIC 9(06) VALUE 0.
           05 WA-L-CAR               PIC 9(06) VALUE 0.
           05 WA-N-LDA               PIC 9(06) VALUE 0.
           05 WA-N-CAR               PIC 9(06) VALUE 0.
           05 WA-G-CAR               PIC 9(06) VALUE 0.
           05 WA-R-CAR               PIC 9(06) VALUE 0.
           05 WA-E-CAR               PIC 9(06) VALUE 0.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA           PIC X(02).
              10 WR-HOY-MM           PIC X(02).
              10 WR-HOY-DD           PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION          PIC X(15).
              10 WR-PARRAFO          PIC X(15).
              10 WR-ARCHIVO          PIC X(10).
              10 WR-OPERACI          PIC X(10).
              10 WR-FILE-ST          PIC X(02).
              10 WR-MENSAJE          PIC X(35).
           05 WR-FS-COLMCAR          PIC 9(02) VALUE 0.
           05 WR-FS-COLVPAR          PIC 9(02) VALUE 0.
           05 WR-CAR-CLAVE           PIC X(13) VALUE LOW-VALUE.
           05 WR-PARAM-RUTUFECH.
              10 WR-RUTU-FECH.
                 15 WR-RUTU-ANO      PIC 9999.
                 15 WR-RUTU-MES      PIC 99.
                 15 WR-RUTU-DIA      PIC 99.
              10 WR-RUTU-SN          PIC X.
              10 WR-RUTU-HABANT      PIC 9(08) VALUE 0.
              10 WR-RUTU-HABPOS      PIC 9(08) VALUE 0.
              10 WR-RUTU-CODRET      PIC 9     VALUE 0.
      ******************************************************************
      * Areas de uso para Datacom                                      *
      ******************************************************************
      *
       01  WB-UIB.
           05 FILLER                 PIC X(32) VALUE 'COLPGAAC'.
      *
       01  WB-REQ-LDA-AREA.
           02 WB-REQ-LDA-CMD          PIC X(5) VALUE 'REDKG'.
           02 FILLER                  PIC X(3) VALUE 'LDA'.
           02 FILLER                  PIC X(5) VALUE 'LDA01'.
           02 WB-REQ-LDA-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +390.
           02 FILLER                  PIC X(58).
           02 WB-REQ-LDA-KEY-VALUE    PIC X(30) VALUE SPACES.
      *
      *                             LINEA DE AVANCES
       01  LDA.
      *                   CLAVE
                   04  LDA-CLV-LDA-1.
      *                             CIC CLIENTE
                       05  CLI-CIC      PICTURE X(12).
      *                             CODIGO OPERACION (CLAVE PRIMARIA)
                       05  LDA-COP.
      *                             NUMERO SISTEMA
                           06  LDA-NUM-SIS
                                    PICTURE X(4).
      *                             CODIGO CORRELATIVO
                           06  LDA-COD-CRR
                                    PICTURE X(8).
      *                             CODIGO OFICINA
                   04  LDA-COD-OFI  PICTURE X(3).
      *                             ESTADO OPERACION
                   04  LDA-COD-EST  PICTURE X(3).
      *                             VALOR DE CAMBIO (MONEDA)
                   04  LDA-VCB      PICTURE X(6).
      *                             CODIGO EJECUTIVO
                   04  LDA-COD-EJE  PICTURE X(12).
      *                             FECHA INGRESO COMPUTACIONAL
                   04  LDA-FEC-ING-CMP.
      *                             FECHA (ANO) INGRESO COMPUT.
                       05  LDA-FA-ING-CMP
                                    PICTURE 9(4).
      *                             FECHA (MES) INGRESO COMPUT.
                       05  LDA-FM-ING-CMP
                                    PICTURE 9(2).
      *                             FECHA (MES) INGRESO COMPUT.
                       05  LDA-FD-ING-CMP
                                    PICTURE 9(2).
      *                             FECHA CURSE REAL
                   04  LDA-FEC-CUR-REA.
      *                             FECHA (ANO) CURSE REAL
                       05  LDA-FA-CUR-REA
                                    PICTURE 9(4).
      *                             FECHA (MES) CURSE REAL
                       05  LDA-FM-CUR-REA
                                    PICTURE 9(2).
      *                             FECHA (MES) CURSE REAL
                       05  LDA-FD-CUR-REA
                                    PICTURE 9(2).
      *                             FECHA RENOVACION
                   04  LDA-FEC-REN-LIN.
      *                             FECHA (ANO) RENOVACION
                       05  LDA-FA-REN-LIN
                                    PICTURE 9(4).
      *                             FECHA (MES) RENOVACION
                       05  LDA-FM-REN-LIN
                                    PICTURE 9(2).
      *                             FECHA (DIA) RENOVACION
                       05  LDA-FD-REN-LIN
                                    PICTURE 9(2).
      *                             FECHA VENCIMIENTO LINEA
                   04  LDA-FEC-VTO-LIN.
      *                             FECHA (ANO) VENCIMIENTO LINEA
                       05  LDA-FA-VTO-LIN
                                    PICTURE 9(4).
      *                             FECHA (MES) VENCIMIENTO LINEA
                       05  LDA-FM-VTO-LIN
                                    PICTURE 9(2).
      *                             FECHA (DIA) VENCIMIENTO LINEA
                       05  LDA-FD-VTO-LIN
                                    PICTURE 9(2).
      *                             MONTO AUTORIZADO LINEA
                   04  LDA-VAL-AUT  PICTURE S9(11)V9(4).
      *                             FECHA ULTIMA ACTUALIZACION
                   04  LDA-FEC-ULT-ACT.
      *                             FECHA (ANO) ULTIMA ACTUALIZ.
                       05  LDA-FA-ULT-ACT
                                    PICTURE 9(4).
      *                             FECHA (MES) ULTIMA ACTUALIZ.
                       05  LDA-FM-ULT-ACT
                                    PICTURE 9(2).
      *                             FECHA (DIA) ULTIMA ACTUALIZ.
                       05  LDA-FD-ULT-ACT
                                    PICTURE 9(2).
      *                             HORA ULTIMA ACTUALIZACION
                   04  LDA-HOR-ACT  PICTURE 9(6).
      *                             TERMINAL ULTIMA ACTUALIZACION
                   04  LDA-TML-ACT  PICTURE X(4).
      *                             ID. USUARIO ULT ACTUALIZACION
                   04  LDA-IDN-USU-ACT
                                    PICTURE X(12).
      *                             CUENTA CORRIENTE
                   04  CCT-COP.
      *                             NUMERO DE SISTEMA
                       05  CCT-NUM-SIS
                                    PICTURE X(4).
      *                             CODIGO CORRELATIVO
                       05  CCT-COD-CRR
                                    PICTURE X(8).
                   04  FILLER1      PICTURE X(20).

      *
       01  WB-ELEM-LIS                PIC X(11) VALUE 'LDA01'.
      *
           EJECT
       PROCEDURE DIVISION.
      *===================
       A-CONTROL SECTION.
      *=================
           ENTRY 'DBMSCBL'.
      *
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-PROCESO.
           GOBACK.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
       BA-PROC-PPAL.
      *-------------
           PERFORM BB-ABRIR-ARCHIVOS.
           PERFORM BC-VALIDA-PARAMETRO.
           GO BZ-FIN-DE-SECCION.
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN INPUT  COLVPAR
                I-O    COLMCAR.

           IF (WR-FS-COLMCAR NOT = 0 AND NOT = 97)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'COLMCAR    '           TO WR-ARCHIVO
               MOVE 'OPEN       '           TO WR-OPERACI
               MOVE WR-FS-COLMCAR           TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           IF (WR-FS-COLVPAR NOT = 0 AND NOT = 97)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'COLVPAR    '           TO WR-ARCHIVO
               MOVE 'OPEN       '           TO WR-OPERACI
               MOVE WR-FS-COLVPAR           TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       BC-VALIDA-PARAMETRO.
      *--------------------
           PERFORM BF-LEER-COLVPAR.
           MOVE PAR-FEC-PRO                 TO WR-RUTU-FECH.
           MOVE ZEROS                       TO WR-RUTU-HABANT
           MOVE ZEROS                       TO WR-RUTU-HABPOS
           MOVE ZEROS                       TO WR-RUTU-CODRET
           MOVE ' '                         TO WR-RUTU-SN
           CALL 'RUTUFECH' USING  WR-RUTU-FECH   WR-RUTU-SN
                                  WR-RUTU-HABANT WR-RUTU-HABPOS
                                  WR-RUTU-CODRET
           IF (WR-RUTU-CODRET NOT = 0)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BC-VALIDA-PARAMET'     TO WR-PARRAFO
               MOVE 'COLVPAR'               TO WR-ARCHIVO
               MOVE '* FECHA INVALIDA *'    TO WR-MENSAJE
               MOVE ' '                     TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           IF (PAR-IND-BOR NOT = 'SI' AND NOT = 'NO')
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BC-VALIDA-PARAMET'     TO WR-PARRAFO
               MOVE 'COLVPAR'               TO WR-ARCHIVO
               MOVE '* INDICE INVALIDO*'    TO WR-MENSAJE
               MOVE ' '                     TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           ACCEPT WR-FECHA-HOY FROM DATE.
           SKIP1
       BD-DISPLAY-ERROR.
      *-----------------
           SET  WS-PROCESO-NO-OK            TO TRUE
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION  : ' WR-SECCION
           DISPLAY ' PARRAFO          : ' WR-PARRAFO
           DISPLAY ' ARCHIVO          : ' WR-ARCHIVO
           DISPLAY ' OPERACION        : ' WR-OPERACI
           DISPLAY ' FILE STATUS      : ' WR-FILE-ST
           DISPLAY ' MENSAJE DE ERROR : ' WR-MENSAJE
           DISPLAY ' '.
           SKIP1
       BF-LEER-COLVPAR.
      *------------------
           READ COLVPAR AT END MOVE 10      TO WR-FS-COLVPAR END-READ
           IF WR-FS-COLVPAR NOT = 0
              MOVE 'B-INICIALIZAR'          TO WR-SECCION
              MOVE 'BF-LEER-COLVPAR'        TO WR-PARRAFO
              MOVE 'COLVPAR    '            TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-COLVPAR            TO WR-FILE-ST
              PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROC-PPAL.
      *-------------
           PERFORM CB-LEER-LDA.
           PERFORM CC-LEER-COLMCAR
           PERFORM CD-PAREA-ARCHIVOS
            UNTIL (WB-REQ-LDA-RC NOT = '  '        AND
                   WR-CAR-CLAVE      = HIGH-VALUE) OR
                  (WS-PROCESO-NO-OK)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-LDA.
      *------------
           MOVE SPACES                      TO WB-REQ-LDA-RC
           CALL 'DBNTRY' USING WB-UIB WB-REQ-LDA-AREA LDA WB-ELEM-LIS
           IF (WB-REQ-LDA-RC = '  ')
              ADD  1                        TO WA-L-LDA
           ELSE
           IF (WB-REQ-LDA-RC = '14')
               MOVE HIGH-VALUE              TO LDA-COP
           ELSE
               MOVE 'C-PROCESO'             TO WR-SECCION
               MOVE 'CB-LEER-LDA'           TO WR-PARRAFO
               MOVE 'LDA'                   TO WR-ARCHIVO
               MOVE WB-REQ-LDA-CMD          TO WR-OPERACI
               MOVE WB-REQ-LDA-RC           TO WR-FILE-ST
               MOVE WB-REQ-LDA-KEY-VALUE    TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF
           END-IF.
           SKIP1
       CC-LEER-COLMCAR.
      *----------------
           MOVE WR-CAR-CLAVE              TO CAR-CLAVE-LDA
           START COLMCAR KEY IS > CAR-CLAVE-LDA
              INVALID KEY MOVE HIGH-VALUE TO WR-CAR-CLAVE.
           IF WR-FS-COLMCAR = 0
              READ
              COLMCAR NEXT AT END MOVE HIGH-VALUE TO WR-CAR-CLAVE
              END-READ
           END-IF.
           IF WR-FS-COLMCAR = 0
              ADD  1                        TO WA-L-CAR
              MOVE CAR-CLAVE-LDA            TO WR-CAR-CLAVE
           ELSE
           IF WR-FS-COLMCAR NOT = 10 AND NOT = 23
              MOVE 'C-PROCESAR'             TO WR-SECCION
              MOVE 'CC-LEER-COLMCAR'        TO WR-PARRAFO
              MOVE 'COLMCAR'                TO WR-ARCHIVO
              MOVE 'READ       '            TO WR-OPERACI
              MOVE WR-FS-COLMCAR            TO WR-FILE-ST
              PERFORM BD-DISPLAY-ERROR
           END-IF
           END-IF.
           SKIP1
       CD-PAREA-ARCHIVOS.
      *------------------
           MOVE 'REDNX'                     TO WB-REQ-LDA-CMD
           MOVE LDA-COD-EST                 TO WS-LDA-COD-EST
           IF (WS-EST-NON-ING)
               IF (LDA-COP = WR-CAR-CLAVE)
                   PERFORM CE-REG-IGUALES
                   PERFORM CC-LEER-COLMCAR
                   PERFORM CB-LEER-LDA
               ELSE
               IF (LDA-COP < WR-CAR-CLAVE)
                   ADD 1                    TO WA-N-CAR
                   IF (WS-EST-NON-ING-VCD)
                       PERFORM CF-NO-EXISTE-MAESTRO
                   END-IF
                   PERFORM CB-LEER-LDA
               ELSE
                   ADD 1                    TO WA-N-LDA
                   DISPLAY '** NO EXISTE ' WR-CAR-CLAVE ' EN LDA **'
                   SET WS-PROCESO-WARNING   TO TRUE
                   PERFORM CC-LEER-COLMCAR
               END-IF
               END-IF
           ELSE
               PERFORM CB-LEER-LDA
           END-IF.
           SKIP1
       CE-REG-IGUALES.
      *----------------
           IF (WS-EST-VCD AND PAR-IND-BOR = 'SI')
               PERFORM CH-BORRAR-COLMCAR
           ELSE
               IF (WS-EST-NON-ING)
                   MOVE 'N'                 TO CAR-IND-LISTAR
                   PERFORM CI-UPDATE-COLMCAR
               END-IF
           END-IF.
           SKIP1
       CF-NO-EXISTE-MAESTRO.
      *---------------------
           MOVE LDA-COP                     TO CAR-CLAVE-LDA
           MOVE CLI-CIC                     TO CAR-CIC-CLTE
           MOVE ZERO                        TO CAR-FOLIO
           MOVE ZERO                        TO CAR-SALD-ANT
           MOVE ZEROES                      TO CAR-FEC-SANT
           MOVE ZERO                        TO CAR-SALD-ACT
           MOVE ZEROES                      TO CAR-FEC-SACT
           MOVE ZERO                        TO CAR-INTERES
           MOVE CCT-COD-CRR                 TO CAR-CTA-CTE
           MOVE ZERO                        TO CAR-VAL-IMPTO
           MOVE ZEROES                      TO CAR-FEC-IMPTO
           MOVE 'N'                         TO CAR-IND-LISTAR
           MOVE ZERO                        TO CAR-NRO-COLAS
           MOVE SPACES                      TO CAR-FILLER.
           PERFORM CG-GRABAR-COLMCAR.
       CG-GRABAR-COLMCAR.
      *------------------
           WRITE CAR-REGISTRO END-WRITE.
           IF (WR-FS-COLMCAR = 0)
               ADD  1                       TO WA-G-CAR
           ELSE
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'CG-GRABAR-COLMCAR'     TO WR-PARRAFO
               MOVE 'COLMCAR    '           TO WR-ARCHIVO
               MOVE 'WRITE    '             TO WR-OPERACI
               MOVE WR-FS-COLMCAR           TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CH-BORRAR-COLMCAR.
      *------------------
           DELETE COLMCAR END-DELETE.
           IF (WR-FS-COLMCAR = 0)
               ADD  1                       TO WA-E-CAR
           ELSE
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'CH-BORRAR-COLMCAR'     TO WR-PARRAFO
               MOVE 'COLMCAR    '           TO WR-ARCHIVO
               MOVE 'DELETE   '             TO WR-OPERACI
               MOVE WR-FS-COLMCAR           TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CI-UPDATE-COLMCAR.
      *------------------
           REWRITE CAR-REGISTRO END-REWRITE.
           IF (WR-FS-COLMCAR = 0)
               ADD  1                       TO WA-R-CAR
           ELSE
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'CI-UPDATE-COLMCAR'     TO WR-PARRAFO
               MOVE 'COLMCAR    '           TO WR-ARCHIVO
               MOVE 'REWRITE  '             TO WR-OPERACI
               MOVE WR-FS-COLMCAR           TO WR-FILE-ST
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DB-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'PROGRAMA NO EMITE INFORME'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA COLPGAAC FECHA '
                    WR-FECHA-HOY
           DISPLAY '-----------------------------------------------'
           DISPLAY '  ELIMINA,CREA REG EN MAESTRO CARTOLAS'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'REG LEIDOS DE LDA     : ' WA-L-LDA
           DISPLAY 'REG LEIDOS EN COLMCAR : ' WA-L-CAR
           DISPLAY ' '
           DISPLAY 'REG NO EXISTE LDA     : ' WA-N-LDA
           DISPLAY 'REG NO EXISTE COLMCAR : ' WA-N-CAR
           DISPLAY ' '
           DISPLAY 'REG GRABAD EN COLMCAR : ' WA-G-CAR
           DISPLAY 'REG REGRAB EN COLMCAR : ' WA-R-CAR
           DISPLAY 'REG ELIMIN EN COLMCAR : ' WA-E-CAR
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY COLRPAR.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DC-TERMINO.
      *-----------
           CLOSE COLVPAR COLMCAR
           IF WS-PROCESO-NO-OK
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
