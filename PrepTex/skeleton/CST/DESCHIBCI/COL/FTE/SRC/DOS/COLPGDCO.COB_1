       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGDCO.
       AUTHOR.          BEE (DPM).
       DATE-WRITTEN.    09-SEP-1998.

      * Programa Generador de Archivo DSQ con OPC del dia.

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-REC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-REC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ESC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-FIA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==AUX-FIA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AMF-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
      *>>>>
      *<<<< WSS
       COPY COLBRDSQ.
       COPY COLBROPS.
       COPY COLBROPC.
       COPY COLBRRDC.
       COPY COLBRDLC.
       COPY COLBRRDT.
      * COPY DEUBRRDE.
       COPY COLBRRDE.
       COPY DEUWGDEU.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
       COPY TABBRCAM.
       COPY SGCBRREC.
       COPY DEUBRESC.
       COPY DEUBRFIA.
       COPY DEUBRAMF.

       01  WSS-VARI.
      * Variables para estadisticas
           03  WSS-NUM-RLEI       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RGRA       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RRCH       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RORC       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-REXI       VALUE ZEROES      PIC 9(06).

      * Monto parametro
           03  WSS-VAL-MPAR       VALUE ZEROES      PIC 9(14).
           03  WSS-VAL-DEUT       VALUE ZEROES      PIC 9(14).
           03  WSS-VAL-DEDI       VALUE ZEROES      PIC 9(14)V9(02).
           03  WSS-VAL-DEIN       VALUE ZEROES      PIC 9(14)V9(02).
           03  WSS-VAL-DECO       VALUE ZEROES      PIC 9(14)V9(02).
           03  WSS-VAL-DEFI       VALUE ZEROES      PIC 9(14)V9(02).
      * DPM ini  30-JUN-1998 Se cambio fecha de proceso por tramo
      * de fechas a procesar
      *     03  WSS-FEC-FPRO.
      *         05  WSS-NUM-SPRO                     PIC 9(02).
      *         05  WSS-NUM-APRO                     PIC 9(02).
      *         05  WSS-NUM-MPRO                     PIC 9(02).
      *         05  WSS-NUM-DPRO                     PIC 9(02).
           03  WSS-FEC-FINI.
               05  WSS-NUM-SINI                     PIC 9(02).
               05  WSS-NUM-AINI                     PIC 9(02).
               05  WSS-NUM-MINI                     PIC 9(02).
               05  WSS-NUM-DINI                     PIC 9(02).
           03  WSS-FEC-FTER.
               05  WSS-NUM-STER                     PIC 9(02).
               05  WSS-NUM-ATER                     PIC 9(02).
               05  WSS-NUM-MTER                     PIC 9(02).
               05  WSS-NUM-DTER                     PIC 9(02).
      * DPM fin  30-JUN-1998
           03  WSS-VAL-TPES       VALUE ZEROES      PIC 9(13)V9(02).
           03  WSS-IND-FLAG       VALUE ZEROES      PIC 9(01).
           03  WSS-IND-DISP       VALUE SPACES      PIC X(01).
           03  WSS-CIC-IOPC       VALUE SPACES      PIC X(12).
           03  WSS-CIC-DEUD       VALUE SPACES      PIC X(12).
           03  WSS-CIC-ICLI       VALUE SPACES      PIC X(12).
           03  WSS-NUM-IDLC       VALUE ZEROES      PIC 9(03).
           03  WSS-NUM-TREC       VALUE ZEROES      PIC 9(02).
      * DPM ini  8-APR-1999
           03  WSS-IND-PROC       VALUE SPACES      PIC X(01).
      * DPM fin  8-APR-1999


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGDCO' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM OBT-PARAM.
           PERFORM PROCESA-OPC.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPS.

           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-RDC.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-DSQ.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR ABRIENDO COLDSQ: ' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-OPEN-FILE.
           EXIT.


      ******************************************************************
       OBT-PARAM SECTION.
      *-----------------
       INI-OBT-PARAM.
           DISPLAY 'ACEPTA PARAMETROS.'
           DISPLAY '------------------'
           DISPLAY ' '.
           ACCEPT WSS-VAL-MPAR IN WSS-VARI.
           DISPLAY 'MONTO PARAMETRO: ' WSS-VAL-MPAR IN WSS-VARI.
           DISPLAY ' '.
      * DPM ini  30-JUN-1998 Se modifico para poder procesar dentro de
      * un periodo y no un solo dia, para poder reprocesar si ocurre un
      * problema un dia. Si las dos fechas de parametro son iguales,
      * se procesan las OPC del dia
            PERFORM GET-FHOY.
      *     MOVE HOY-DHOY TO WSS-NUM-DPRO IN WSS-VARI.
      *     MOVE HOY-MHOY TO WSS-NUM-MPRO IN WSS-VARI.
      *     MOVE HOY-SHOY TO WSS-NUM-SPRO IN WSS-VARI.
      *     MOVE HOY-AHOY TO WSS-NUM-APRO IN WSS-VARI.
      *     DISPLAY 'FECHA PROCESO: ' WSS-FEC-FPRO IN WSS-VARI.
      *     DISPLAY ' '.
           ACCEPT WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FECHA DE INICIO: ' WSS-FEC-FINI IN WSS-VARI.
           DISPLAY ' '.
           ACCEPT WSS-FEC-FTER IN WSS-VARI.
           DISPLAY 'FECHA DE TERMINO: ' WSS-FEC-FTER IN WSS-VARI.
           DISPLAY ' '.
      * DPM fin  30-JUN-1998
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY 'INDICADOR DE DISPLAY (S/N): '
                                    WSS-IND-DISP IN WSS-VARI.
           DISPLAY ' '.
           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
                                       NOT = 'N'
               DISPLAY 'ASUME IND. DE DISPLAY = N'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI.
      * DPM ini  8-APR-1999
           ACCEPT WSS-IND-PROC IN WSS-VARI.
           DISPLAY 'IND. PROCESO/REPROCESO: ' WSS-IND-PROC IN WSS-VARI.
           IF WSS-IND-PROC IN WSS-VARI NOT = 'P' OR 'R'
               MOVE 'P' TO WSS-IND-PROC IN WSS-VARI
               DISPLAY 'ASUME INDICADOR DE PROCESO: '
                                       WSS-IND-PROC IN WSS-VARI.
      * DPM fin  8-APR-1999
       FIN-OBT-PARAM.
           EXIT.


      ******************************************************************
       PROCESA-OPC SECTION.
      *-------------------
       INI-PROCESA-OPC.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OPS.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY OPC INGRESADAS HOY'
               MOVE 0 TO RETURN-CODE
               GOBACK.

       LUP-PROCESA-OPC.
           MOVE OPC-CIC-IOPC IN OPS TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.

           MOVE ZEROES TO WSS-IND-FLAG IN WSS-VARI.
           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'REG. OPC LEIDO: ' OPC-CIC-IOPC IN OPC.

      * Busca CIC de cliente para pasarlo a rutina de choque
      * Debe buscar todos los clientes de la OPC, Directos e
      * Indirectos
           MOVE OPC-CIC-IOPC IN OPC TO RDC-CIC-IOPC IN RDC
                                       WSS-CIC-IOPC IN WSS-VARI.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE SPACES              TO RDC-CIC-ICLI IN RDC.

           MOVE FIO-GET-NLS TO FIO-CMND.

       LUP-PROCESA-OPC-RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           PERFORM COL-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
              WSS-CIC-IOPC IN WSS-VARI = RDC-CIC-IOPC IN RDC )
               GO TO NXT-PROCESA-OPC.

           MOVE ADR-RDC-REQA TO AUX-RDC-REQA.

           MOVE RDC-CIC-ICLI IN RDC TO WSS-CIC-ICLI IN WSS-VARI.
           MOVE RDC-NUM-IDLC IN RDC TO WSS-NUM-IDLC IN WSS-VARI.

           MOVE ZEROES TO WSS-VAL-DEUT IN WSS-VARI
                          WSS-VAL-DEDI IN WSS-VARI
                          WSS-VAL-DEIN IN WSS-VARI
                          WSS-VAL-DECO IN WSS-VARI
                          WSS-VAL-DEFI IN WSS-VARI.

      *Obtiene deudores Complementario y Fiador para Deudor Directo
           PERFORM OBT-DEU-COM-FIA.

      * Si registro existe en DSQ, no calcula deuda y lee siguiente reg
           MOVE WSS-CIC-ICLI IN WSS-VARI TO DSQ-CIC-DEUD IN DSQ.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DSQ.
           IF FIO-STAT-OKS AND
              WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'REG. EXISTE EN DSQ (NO SE GRABA): '
                                       DSQ-CIC-DEUD IN DSQ
               ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               GO TO NXT-PROCESA-OPC-RDC
           ELSE
           IF FIO-STAT-OKS
               ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               GO TO NXT-PROCESA-OPC-RDC.

           MOVE WSS-CIC-ICLI IN WSS-VARI TO WSS-CIC-DEUD IN WSS-VARI.

      * Calcula Deuda total del cliente con rutina de choque de deudas
           PERFORM CAL-DEUDA-TOTAL.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
      *         ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               GO TO NXT-PROCESA-OPC-RDC.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'CLIENTE: ' WSS-CIC-ICLI IN WSS-VARI
                       ' DEUDA TOTAL: ' WSS-VAL-DEUT IN WSS-VARI
                       ' DEUDA DIRECTA: ' WSS-VAL-DEDI IN WSS-VARI.

           IF WSS-VAL-DEUT IN WSS-VARI < WSS-VAL-MPAR OR
              WSS-VAL-DEDI IN WSS-VARI NOT > ZEROES
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
               GO TO NXT-PROCESA-OPC-RDC.

      * Graba datos de archivo D01 en archivo DSQ, mas la fecha
      * de proceso
           PERFORM GRABA-OPC-DSQ.

       NXT-PROCESA-OPC-RDC.
      *     ADD 1 TO WSS-NUM-IDLC IN WSS-VARI.
           MOVE WSS-CIC-ICLI IN WSS-VARI TO RDC-CIC-ICLI IN RDC.
           MOVE WSS-CIC-IOPC IN WSS-VARI TO RDC-CIC-IOPC IN RDC.
           MOVE WSS-NUM-IDLC IN WSS-VARI TO RDC-NUM-IDLC IN RDC.

           MOVE FIO-KDEF-S TO FIO-KDEF.
           MOVE AUX-RDC-REQA TO ADR-RDC-REQA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PROCESA-OPC-RDC.

       NXT-PROCESA-OPC.
           MOVE FIO-GET-NXT TO FIO-CMND.

           PERFORM COL-FIO-OPS.
           IF FIO-STAT-OKS
               GO TO LUP-PROCESA-OPC.
       FIN-PROCESA-OPC.
           EXIT.


      ******************************************************************
       CAL-DEUDA-TOTAL SECTION.
      *------------------------
       INI-CAL-DEUDA-TOTAL.
      * Llama a rutina de choque de deudas
      * DPM ini  23-OCT-1998 Inicializa variables de rutina de choque
      * de deudas por problemas en programas batch
      *JGM
           PERFORM PRO-INI-WGDEU.
      * DPM fin  23-OCT-1998

           MOVE HOY-FHOY TO DEU-FEC-FPRO.
           PERFORM INI-CAM.
           MOVE 'R' TO DEU-FPRO IN DEU.
           MOVE WSS-CIC-IOPC IN WSS-VARI TO OPC-CIC-IOPC IN OPC.
           MOVE WSS-CIC-DEUD IN WSS-VARI TO DEU-CIC-ICLI IN DEU.
      *

           PERFORM CAL-DEU.
           IF DEU-STAT NOT = DEU-STAT-OKS
               IF WSS-IND-DISP IN WSS-VARI = 'S'
                   DISPLAY 'ERROR RUTINA CHOQUE DEUDA: '
                                             DEU-STAT
                   MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
                   GO TO FIN-CAL-DEUDA-TOTAL
               ELSE
                   MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
                   GO TO FIN-CAL-DEUDA-TOTAL.

      * Obtiene valores de rutina de choque mediante registro RDE
           MOVE 0 TO DEU-R.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM DEU-FIO-RDE.
           IF NOT ( FIO-STAT-OKS AND
              RDE-CIC-ICLI IN RDE = WSS-CIC-DEUD IN WSS-VARI )
               MOVE ZEROES TO WSS-VAL-DEUT IN WSS-VARI
               MOVE 1 TO WSS-IND-FLAG IN WSS-VARI
               IF WSS-IND-DISP IN WSS-VARI = 'S'
                   DISPLAY 'ERROR LECTURA DEURDE: '
                               FIO-STAT
                           ' ' WSS-CIC-DEUD IN WSS-VARI
                           ' ' RDE-CIC-ICLI IN RDE
                   GO TO FIN-CAL-DEUDA-TOTAL
               ELSE
                   GO TO FIN-CAL-DEUDA-TOTAL.

       LUP-CAL-DEUDA-TOTAL.
           IF RDE-COD-VCCR IN RDE NOT > SPACES
               MOVE RDE-VAL-DEUD IN RDE TO WSS-VAL-TPES IN WSS-VARI
           ELSE
           IF RDE-COD-VCCR IN RDE = CAM-COD-MNAC
               MOVE RDE-VAL-DEUD IN RDE TO WSS-VAL-TPES IN WSS-VARI
           ELSE
               MOVE RDE-COD-VCCR IN RDE TO CAM-COD-VCAM IN CAM(1)
               PERFORM GET-CAM
               IF NOT FIO-STAT-OKS
                   MOVE ZEROES TO WSS-VAL-TPES IN WSS-VARI
               ELSE
                   IF CAM-IND-VIGE IN CAM = 'N'
                       MOVE ZEROES TO WSS-VAL-TPES IN WSS-VARI
                   ELSE
                       MULTIPLY RDE-VAL-DEUD IN RDE BY
                                CAM-SGV-VCAM IN CAM
                       GIVING   WSS-VAL-TPES IN WSS-VARI.

           IF RDE-IND-TIPD IN RDE = 'DIR'
               ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DEDI IN WSS-VARI
           ELSE
           IF RDE-IND-TIPD IN RDE = 'IND'
               ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DEIN IN WSS-VARI
           ELSE
           IF RDE-IND-TIPD IN RDE = 'COM'
               ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DECO IN WSS-VARI
           ELSE
           IF RDE-IND-TIPD IN RDE = 'FIA'
              ADD WSS-VAL-TPES IN WSS-VARI TO WSS-VAL-DEFI IN WSS-VARI.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM DEU-FIO-RDE.
           IF FIO-STAT-OKS AND
              RDE-CIC-ICLI IN RDE = WSS-CIC-DEUD IN WSS-VARI
               GO TO LUP-CAL-DEUDA-TOTAL.

      *        Calcula Deuda Total = D. Dir + D. Ind + (D. Com + D. Fia)
      * La deuda se maneja en pesos para estandarizarla
           COMPUTE WSS-VAL-DEUT IN WSS-VARI ROUNDED =
                              ( WSS-VAL-DEDI IN WSS-VARI +
                                WSS-VAL-DEIN IN WSS-VARI +
                                WSS-VAL-DECO IN WSS-VARI +
                                WSS-VAL-DEFI IN WSS-VARI ).
      *INI-JGM
           DISPLAY 'CIC-ICLI: '   WSS-CIC-DEUD IN WSS-VARI
                   ' DEU-TOTAL: ' WSS-VAL-DEUT IN WSS-VARI.
       FIN-CAL-DEUDA-TOTAL.
           EXIT.


      ******************************************************************
       GRABA-OPC-DSQ SECTION.
      *---------------------
       INI-GRABA-OPC-DSQ.
           DISPLAY 'CIC-DEU G: ' WSS-CIC-DEUD IN WSS-VARI.
           MOVE WSS-CIC-DEUD IN WSS-VARI TO DSQ-CIC-DEUD IN DSQ.
           MOVE SPACES                   TO DSQ-GLS-DISP IN DSQ.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-DSQ.
           IF  FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RGRA IN WSS-VARI
               DISPLAY 'REG.GRABADO OK.'
               GO TO FIN-GRABA-OPC-DSQ.

           IF  WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'ERROR DSQ :' FIO-STAT
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI.

           IF FIO-STAT = 48  AND WSS-NUM-RGRA IN WSS-VARI = ZERO
              MOVE FIO-CLO TO FIO-CMND
              PERFORM COL-FIO-DSQ
      *
              MOVE FIO-OUT TO FIO-CMND
              PERFORM COL-FIO-DSQ
              MOVE WSS-CIC-DEUD IN WSS-VARI TO DSQ-CIC-DEUD IN DSQ
              MOVE SPACES                   TO DSQ-GLS-DISP IN DSQ
              MOVE FIO-PUT TO FIO-CMND
              PERFORM COL-FIO-DSQ
              IF NOT FIO-STAT-OKS
                 DISPLAY 'ERROR GRABAR COLDSQ: ' FIO-STAT
                 PERFORM GNS-PRO-ABT
              ELSE
                 MOVE FIO-CLO TO FIO-CMND
                 PERFORM COL-FIO-DSQ
      *
                 MOVE FIO-UPD TO FIO-CMND
                 PERFORM COL-FIO-DSQ
                 IF NOT FIO-STAT-OKS
                    DISPLAY 'ERROR ABRIENDO COLDSQ: ' FIO-STAT
                    PERFORM GNS-PRO-ABT.
       FIN-GRABA-OPC-DSQ.
           EXIT.


      ******************************************************************
       OBT-DEU-COM-FIA SECTION.
      *-----------------------
       INI-OBT-DEU-COM-FIA.
      *Obtiene primero deudor Complementario
           MOVE WSS-CIC-ICLI IN WSS-VARI TO REC-CIC-ICLI IN REC.
      *     MOVE 'DC'                    TO REC-COD-TREC IN REC.
           MOVE 'CD'                     TO REC-COD-TREC IN REC.
      *
           MOVE SPACES                   TO REC-CIC-IREL IN REC.
           MOVE ZEROES                   TO REC-NUM-TREC IN REC.

           MOVE FIO-GET-NLS TO FIO-CMND.

       LUP-OBT-DEU-COM-FIA-1.
           MOVE ZEROES TO WSS-VAL-DEUT IN WSS-VARI
                          WSS-VAL-DEDI IN WSS-VARI
                          WSS-VAL-DEIN IN WSS-VARI
                          WSS-VAL-DECO IN WSS-VARI
                          WSS-VAL-DEFI IN WSS-VARI.

           PERFORM SGC-FIO-REC.
           IF NOT (FIO-STAT-OKS AND
              REC-CIC-ICLI IN REC = WSS-CIC-ICLI IN WSS-VARI AND
              REC-COD-TREC IN REC = 'CD')
               GO TO NXT-OBT-DEU-COM-FIA.

           MOVE ADR-REC-REQA TO AUX-REC-REQA.

           IF REC-IND-VIGE IN REC NOT = 'S'
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-OBT-DEU-COM-FIA-1.

           MOVE REC-NUM-TREC IN REC TO WSS-NUM-TREC IN WSS-VARI.
           MOVE REC-CIC-IREL IN REC TO WSS-CIC-DEUD IN WSS-VARI.

      * Si registro existe en DSQ, no calcula deuda y lee siguiente reg
           MOVE WSS-CIC-DEUD IN WSS-VARI TO DSQ-CIC-DEUD IN DSQ.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DSQ.
           IF FIO-STAT-OKS AND
              WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'CLIENTE COMPL. EXISTE EN DSQ (NO SE GRABA): '
                                       DSQ-CIC-DEUD IN DSQ
               ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-OBT-DEU-COM-FIA-1
           ELSE
           IF FIO-STAT-OKS
               ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-OBT-DEU-COM-FIA-1.

           PERFORM CAL-DEUDA-TOTAL.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
      *         ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE WSS-CIC-ICLI IN WSS-VARI TO REC-CIC-ICLI IN REC
               MOVE WSS-CIC-DEUD IN WSS-VARI TO REC-CIC-IREL IN REC
               MOVE WSS-NUM-TREC IN WSS-VARI TO REC-NUM-TREC IN REC
               MOVE 'CD'                     TO REC-COD-TREC IN REC
               MOVE FIO-KDEF-S TO FIO-KDEF
               MOVE AUX-REC-REQA TO ADR-REC-REQA
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-OBT-DEU-COM-FIA-1.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'CLIENTE COMPL: ' WSS-CIC-DEUD IN WSS-VARI
                       ' DEUDA TOTAL (M$): ' WSS-VAL-DEUT IN WSS-VARI.

           IF WSS-VAL-DEUT IN WSS-VARI NOT > WSS-VAL-MPAR IN WSS-VARI
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
      *         ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE WSS-CIC-ICLI IN WSS-VARI TO REC-CIC-ICLI IN REC
               MOVE WSS-CIC-DEUD IN WSS-VARI TO REC-CIC-IREL IN REC
               MOVE WSS-NUM-TREC IN WSS-VARI TO REC-NUM-TREC IN REC
               MOVE 'CD'                     TO REC-COD-TREC IN REC
               MOVE FIO-KDEF-S TO FIO-KDEF
               MOVE AUX-REC-REQA TO ADR-REC-REQA
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-OBT-DEU-COM-FIA-1.

      * Si tiene Deudor Complementario, y su deuda es mayor que parametr
      * lo graba en archivo DSQ
           PERFORM GRABA-OPC-DSQ.

           MOVE WSS-CIC-ICLI IN WSS-VARI TO REC-CIC-ICLI IN REC.
           MOVE WSS-CIC-DEUD IN WSS-VARI TO REC-CIC-IREL IN REC.

           MOVE WSS-NUM-TREC IN WSS-VARI TO REC-NUM-TREC IN REC.
           MOVE 'CD'                     TO REC-COD-TREC IN REC.

           MOVE FIO-KDEF-S TO FIO-KDEF.
           MOVE AUX-REC-REQA TO ADR-REC-REQA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-OBT-DEU-COM-FIA-1.

       NXT-OBT-DEU-COM-FIA.
      *Obtiene Deudor de Fianza
           MOVE WSS-CIC-ICLI IN WSS-VARI TO FIA-CIC-ICLI IN FIA.
           MOVE SPACES                   TO FIA-CIC-ICFS IN FIA.

           MOVE FIO-GET-NLS TO FIO-CMND.

       LUP-OBT-DEU-COM-FIA-2.
           MOVE 'FIA-GLS-ICYF' TO FIO-AKEY.
           PERFORM DEU-FIO-FIA.
           IF NOT (FIO-STAT-OKS AND
              WSS-CIC-ICLI IN WSS-VARI = FIA-CIC-ICLI IN FIA)
               GO TO FIN-OBT-DEU-COM-FIA.

           MOVE ADR-FIA-REQA TO AUX-FIA-REQA.

           MOVE ZEROES TO WSS-VAL-DEUT IN WSS-VARI
                          WSS-VAL-DEDI IN WSS-VARI
                          WSS-VAL-DEIN IN WSS-VARI
                          WSS-VAL-DECO IN WSS-VARI
                          WSS-VAL-DEFI IN WSS-VARI.

           IF FIA-MSC-STAT IN FIA NOT = 'S'
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
      *         ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               IF WSS-IND-DISP IN WSS-VARI = 'S'
                   DISPLAY 'CLIENTE FIA: ' FIA-CIC-ICFS IN FIA
                           ' ESTADO FIANZA: ' FIA-MSC-STAT IN FIA
                   GO TO LUP-OBT-DEU-COM-FIA-2
               ELSE
                   GO TO LUP-OBT-DEU-COM-FIA-2.

           MOVE FIA-CIC-ICFS IN FIA TO WSS-CIC-DEUD IN WSS-VARI.

      * Si registro existe en DSQ, no calcula deuda y lee siguiente reg
           MOVE WSS-CIC-DEUD IN WSS-VARI TO DSQ-CIC-DEUD IN DSQ.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DSQ.
           IF FIO-STAT-OKS AND
              WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'CLIENTE FIADOR EXISTE EN DSQ (NO SE GRABA): '
                                       DSQ-CIC-DEUD IN DSQ
               ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-OBT-DEU-COM-FIA-2
           ELSE
           IF FIO-STAT-OKS
               ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE FIO-GET-NXT TO FIO-CMND
               GO TO LUP-OBT-DEU-COM-FIA-2.

           PERFORM CAL-DEUDA-TOTAL.
           IF WSS-IND-FLAG IN WSS-VARI = 1
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
      *         ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE WSS-CIC-ICLI IN WSS-VARI TO FIA-CIC-ICLI IN FIA
               MOVE WSS-CIC-DEUD IN WSS-VARI TO FIA-CIC-ICFS IN FIA
               MOVE FIO-GET-NXT TO FIO-CMND
               MOVE FIO-KDEF-S TO FIO-KDEF
               MOVE AUX-FIA-REQA TO ADR-FIA-REQA
               GO TO LUP-OBT-DEU-COM-FIA-2.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY 'CLIENTE FIADOR: ' WSS-CIC-ICLI IN WSS-VARI
                       ' DEUDA TOTAL (M$): ' WSS-VAL-DEUT IN WSS-VARI.

           IF WSS-VAL-DEUT IN WSS-VARI NOT > WSS-VAL-MPAR IN WSS-VARI
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
      *         ADD 1 TO WSS-NUM-REXI IN WSS-VARI
               MOVE WSS-CIC-ICLI IN WSS-VARI TO FIA-CIC-ICLI IN FIA
               MOVE WSS-CIC-DEUD IN WSS-VARI TO FIA-CIC-ICFS IN FIA
               MOVE FIO-GET-NXT TO FIO-CMND
               MOVE FIO-KDEF-S TO FIO-KDEF
               MOVE AUX-FIA-REQA TO ADR-FIA-REQA
               GO TO LUP-OBT-DEU-COM-FIA-2.

      * Si tiene Deudor Fiador, y su deuda es mayor que parametro,
      * lo graba en archivo DSQ
           PERFORM GRABA-OPC-DSQ.

           MOVE WSS-CIC-ICLI IN WSS-VARI TO FIA-CIC-ICLI IN FIA.
           MOVE WSS-CIC-DEUD IN WSS-VARI TO FIA-CIC-ICFS IN FIA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           MOVE FIO-KDEF-S TO FIO-KDEF.
           MOVE AUX-FIA-REQA TO ADR-FIA-REQA.
           GO TO LUP-OBT-DEU-COM-FIA-2.
       FIN-OBT-DEU-COM-FIA.
           EXIT.


      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPS.

           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-RDC.
           PERFORM COL-FIO-DSQ.
       FIN-CLOSE-FILE.
           EXIT.


      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS DEL PROCESO.'.
           DISPLAY '-------------------------'.
           DISPLAY ' '.
           DISPLAY 'REGISTROS OPC LEIDOS                   : '
                                  WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'REGISTROS (CLIENTES) GRABADOS DSQ      : '
                                  WSS-NUM-RGRA IN WSS-VARI.
           DISPLAY 'REGISTROS (CLIENTES) EXISTENTES EN DSQ : '
                                  WSS-NUM-REXI IN WSS-VARI.
           DISPLAY 'OPERACIONES RECHAZADOS                 : '
                                  WSS-NUM-RORC IN WSS-VARI.
           DISPLAY 'CLIENTES RECHAZADOS                    : '
                                  WSS-NUM-RRCH IN WSS-VARI.
      *                            WSS-NUM-REXI IN WSS-VARI.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.


      * COPY DEUBFRDE.
       DEU-FIO-RDE SECTION.
       INI-DEU-FIO-RDE.
           PERFORM GET-RDE.
       FIN-DEU-FIO-RDE.
           EXIT.

       GET-RDE SECTION.
       INI-GET-RDE.
           MOVE '00' TO FIO-STAT.
           IF FIO-CMND = FIO-GET-PRV  OR
              FIO-CMND = FIO-GET-LEQ
               SUBTRACT 1 FROM DEU-R
               IF DEU-R NOT > ZEROES
                   MOVE '10' TO FIO-STAT
                   GO TO FIN-GET-RDE
               ELSE
                   NEXT SENTENCE
           ELSE
               ADD 1 TO DEU-R
               IF DEU-R > DEU-RMAX
                   MOVE '10' TO FIO-STAT
                   GO TO FIN-GET-RDE.
           IF DEU-R > 50
               MOVE '10' TO FIO-STAT
               GO TO FIN-GET-RDE.
           MOVE DEU-CIC-ICLI IN DEU        TO RDE-CIC-ICLI IN RDE.
           MOVE RDE-IND-TIPD IN DEU(DEU-R) TO RDE-IND-TIPD IN RDE.
           MOVE RDE-IND-SCRT IN DEU(DEU-R) TO RDE-IND-SCRT IN RDE.
           MOVE RDE-IND-SCTB IN DEU(DEU-R) TO RDE-IND-SCTB IN RDE.
           MOVE RDE-IND-SCBZ IN DEU(DEU-R) TO RDE-IND-SCBZ IN RDE.
           MOVE RDE-COD-COCR IN DEU(DEU-R) TO RDE-COD-COCR IN RDE.
           MOVE RDE-COD-VCCR IN DEU(DEU-R) TO RDE-COD-VCCR IN RDE.
           MOVE RDE-COD-OFCR IN DEU(DEU-R) TO RDE-COD-OFCR IN RDE.
           MOVE RDE-VAL-DEUD IN DEU(DEU-R) TO RDE-VAL-DEUD IN RDE.
      *    MOVE RDE-VAL-DPES IN DEU(DEU-R) TO RDE-VAL-DPES IN RDE.
       FIN-GET-RDE.
           EXIT.

       COPY COLBFDSQ.
       COPY COLBFOPS.
      *COPY COLBFOPC.
      * Modulo FIO para registro OPC en DATACOM, 25-MAR-1993 10:00:37.98
       COL-FIO-OPC SECTION.
       INI-COL-FIO-OPC.
           MOVE ADR-OPC-REQA TO ADR-REQA.
           MOVE 'OPC' TO ADR-TABL IN ADR-REQA.
      *
      * DPM ini  1-JUL-1998 10:01:57
           IF WSS-IND-PROC IN WSS-VARI = 'R'
               MOVE +162 TO ADR-DBID IN ADR-REQA
               GO TO FSR-COL-FIO-OPC.
      * DPM fin  1-JUL-1998

           MOVE 'COL'  TO IDD-SAUX.
           MOVE 'M'    TO IDD-GAUX.
           MOVE SPACES TO IDD-RAUX.
           MOVE 1 TO IDD-IAUX.
           SET IDD-ITBL TO 1.
           SEARCH IDD-VSIS VARYING IDD-IAUX
             AT END
               GO TO GRP-COL-FIO-OPC
             WHEN IDD-DENT( IDD-ITBL ) = IDD-DAUX
               MOVE IDD-IENT( IDD-ITBL) TO ADR-DBID IN ADR-REQA
               GO TO FSR-COL-FIO-OPC.
       GRP-COL-FIO-OPC.
           MOVE SPACES TO IDD-GAUX.
           MOVE 1      TO IDD-IAUX.
           SET IDD-ITBL TO 1.
           SEARCH IDD-VSIS VARYING IDD-IAUX
             AT END
               MOVE 'En COL-FIO-OPC, NO existe en IDD:' TO FIO-MEN1
               MOVE IDD-DAUX                            TO FIO-MEN2
               PERFORM PRG-ABT
             WHEN IDD-DENT( IDD-ITBL ) = IDD-DAUX
               MOVE IDD-IENT( IDD-ITBL) TO ADR-DBID IN ADR-REQA.
       FSR-COL-FIO-OPC.
      *
           MOVE 'OPC00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-OPC.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OPC-KEY-IOPC' OR FIO-AKEY NOT > SPACES )
               MOVE OPC-KEY-IOPC IN OPC TO ADR-VKEY IN ADR-REQA
               MOVE 'OPC00' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'OPC-FEC-FCOL'
               MOVE OPC-FEC-FCOL IN OPC TO ADR-VKEY IN ADR-REQA
               MOVE 'OPC01' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'OPC-FEC-FING'
               MOVE OPC-FEC-FING IN OPC TO ADR-VKEY IN ADR-REQA
               MOVE 'OPC02' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'OPC-FEC-FINP'
               MOVE OPC-FEC-FINP IN OPC TO ADR-VKEY IN ADR-REQA
               MOVE 'OPC03' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-OPC, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               PERFORM PRG-ABT.
       CON-COL-FIO-OPC.
           MOVE OPC TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OPC
           ELSE
               MOVE 'COLOPC' TO FIO-MEN2
               IF FIO-STAT-FTL
                   PERFORM PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-OPC-REQA.
       FIN-COL-FIO-OPC.
           EXIT.


      *COPY COLBFDLC.
      * Modulo FIO para registro DLC en DATACOM, 25-MAR-1993 09:59:30.49
       COL-FIO-DLC SECTION.
       INI-COL-FIO-DLC.
           MOVE ADR-DLC-REQA TO ADR-REQA.
           MOVE 'DLC' TO ADR-TABL IN ADR-REQA.
      *
      * DPM ini  1-JUL-1998 10:01:57
           IF WSS-IND-PROC IN WSS-VARI = 'R'
               MOVE +162 TO ADR-DBID IN ADR-REQA
               GO TO FSR-COL-FIO-DLC.
      * DPM fin  1-JUL-1998

           MOVE 'COL'  TO IDD-SAUX.
           MOVE 'M'    TO IDD-GAUX.
           MOVE SPACES TO IDD-RAUX.
           MOVE 1 TO IDD-IAUX.
           SET IDD-ITBL TO 1.
           SEARCH IDD-VSIS VARYING IDD-IAUX
             AT END
               GO TO GRP-COL-FIO-DLC
             WHEN IDD-DENT( IDD-ITBL ) = IDD-DAUX
               MOVE IDD-IENT( IDD-ITBL) TO ADR-DBID IN ADR-REQA
               GO TO FSR-COL-FIO-DLC.
       GRP-COL-FIO-DLC.
           MOVE SPACES TO IDD-GAUX.
           MOVE 1      TO IDD-IAUX.
           SET IDD-ITBL TO 1.
           SEARCH IDD-VSIS VARYING IDD-IAUX
             AT END
               MOVE 'En COL-FIO-DLC, NO existe en IDD:' TO FIO-MEN1
               MOVE IDD-DAUX                            TO FIO-MEN2
               PERFORM PRG-ABT
             WHEN IDD-DENT( IDD-ITBL ) = IDD-DAUX
               MOVE IDD-IENT( IDD-ITBL) TO ADR-DBID IN ADR-REQA.
       FSR-COL-FIO-DLC.
      *
           MOVE 'DLC00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-DLC.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'DLC-KEY-IDLC' OR FIO-AKEY NOT > SPACES )
               MOVE DLC-KEY-IDLC IN DLC TO ADR-VKEY IN ADR-REQA
               MOVE 'DLC00' TO ADR-DKEY IN ADR-REQA
           ELSE
           IF FIO-AKEY = 'DLC-IND-SCTB'
               MOVE DLC-IND-SCTB IN DLC TO ADR-VKEY IN ADR-REQA
               MOVE 'DLC01' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-DLC, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               PERFORM PRG-ABT.
       CON-COL-FIO-DLC.
           MOVE DLC TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO DLC
           ELSE
               MOVE 'COLDLC' TO FIO-MEN2
               IF FIO-STAT-FTL
                   PERFORM PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-DLC-REQA.
       FIN-COL-FIO-DLC.
           EXIT.

       COPY COLBFRDC.
       COPY COLBFRDT.

       COPY DEUWGIDE.
       COPY DEUBCDEU.

       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBPFEC.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGPES.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY TABBFCAM.
       COPY TABBGCAM.
       COPY SGCBFREC.
       COPY DEUBFESC.
       COPY DEUBFFIA.
       COPY DEUBFAMF.




