       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     COLPB735.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  20/07/91.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Creditos hipotecarios                              *
      *  Subsist. :                                                    *
      *  Analista : Venegas Jose                                       *
      *  Objetivo : Hacer chequeo a creditos complementarios           *
      *                                                                *
      *  Archivos :                                                    *
      *   Entrada : OPC 160: Tabla de curses                           *
      *             VEN 160: Vencimientos                              *
      *                                                                *
      *   Salida  : COLL735: Listado de observaciones y errores        *
      *                                                                *
      *   Ent/Sal :                                                    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: Osvaldo Donoso J...........            *
      *            Fecha      : 17/03/92                               *
      *            Cambio     : Se usa logica de validacion para       *
      *                         creditos antiguos y nuevos             *
      *                                                                *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *======================
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
           EJECT
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT COLL735 ASSIGN TO UT-S-COLL735.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
           SKIP1
       FD  COLL735
           LABEL RECORDS OMITTED.
       01  COLR735                     PIC X(132).
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       SKIP1
       01  W-SWITCHES.
           05 WS-PROCESO             PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-NO-OK    VALUE 1.
       SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-OPC               PIC 9(06) VALUE 0.
           05 WA-S-OPC               PIC 9(06) VALUE 0.
           05 WA-L-VEN               PIC 9(06) VALUE 0.
           05 WA-CUO-IMP             PIC 9(03) VALUE 0.
           05 WA-CUO-CAN             PIC 9(03) VALUE 0.
           05 WA-CUO-TOT             PIC 9(03) VALUE 0.
           05 WA-SAL-FIN             PIC S9(11)V9(04) VALUE 0.
           05 WA-PAGINA              PIC 9(03) VALUE 0.
           05 WA-LINEA               PIC 9(03) VALUE 99.
           SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA           PIC X(02).
              10 WR-HOY-MM           PIC X(02).
              10 WR-HOY-DD           PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION          PIC X(15).
              10 WR-PARRAFO          PIC X(15).
              10 WR-ARCHIVO          PIC X(10).
              10 WR-OPERACI          PIC X(10).
              10 WR-FILE-ST          PIC X(02).
              10 WR-MENSAJE          PIC X(35).
           05 WR-LINEA01.
              10 FILLER           PIC X(018) VALUE 'BANCO DE CREDITO E'.
              10 FILLER           PIC X(029) VALUE ' INV.'.
              10 FILLER           PIC X(019) VALUE 'CHEQUEO DE OPERAC.'.
              10 FILLER           PIC X(015) VALUE 'COMPLEMENTARIAS'.
              10 FILLER           PIC X(033) VALUE ALL ' '.
              10 FILLER           PIC X(008) VALUE 'FECHA :'.
              10 WR-LA01-DIA      PIC X(002).
              10 FILLER           PIC X(001) VALUE ALL '/'.
              10 WR-LA01-MES      PIC X(002).
              10 FILLER           PIC X(001) VALUE ALL '/'.
              10 WR-LA01-ANO      PIC X(002).
           05 WR-LINEA02.
              10 FILLER           PIC X(012) VALUE 'COLOCACIONES'.
              10 FILLER           PIC X(101) VALUE ALL ' '.
              10 FILLER           PIC X(010) VALUE 'PAGINA :'.
              10 WR-LA02-PAGINA   PIC Z.ZZ9.
           05 WR-LINEA03.
              10 FILLER           PIC X(060) VALUE ALL ' '.
              10 FILLER           PIC X(014) VALUE '(1)        (2)'.
              10 FILLER           PIC X(012) VALUE '         (3)'.
              10 FILLER           PIC X(016) VALUE '             (4)'.
           05 WR-LINEA04.
              10 FILLER           PIC X(018) VALUE '   NUMERO      PRO'.
              10 FILLER           PIC X(015) VALUE 'X  CUOTA  CUOTA'.
              10 FILLER           PIC X(017) VALUE '        SALDO DEL'.
              10 FILLER           PIC X(016) VALUE '        1RA. VEN'.
              10 FILLER           PIC X(018) VALUE '   NRO VEN    NRO'.
              10 FILLER           PIC X(018) VALUE 'CUOTAS      SALDO'.
              10 FILLER           PIC X(003) VALUE 'POR'.
           05 WR-LINEA05.
              10 FILLER           PIC X(018) VALUE ' OPERACION     VEN'.
              10 FILLER           PIC X(015) VALUE '    CAN    ORIG'.
              10 FILLER           PIC X(016) VALUE '         CREDITO'.
              10 FILLER           PIC X(015) VALUE '         IMPAGA'.
              10 FILLER           PIC X(018) VALUE '    CANCELADAS   T'.
              10 FILLER           PIC X(018) VALUE 'OTALES        AMOR'.
              10 FILLER           PIC X(018) VALUE 'TIZAR      ERRORES'.
              10 FILLER           PIC X(011) VALUE ' DETECTADOS'.
           05 WR-LINEA06.
              10 FILLER           PIC X(131) VALUE ALL '-'.
           05 WR-LINEA07.
              10 WR-LA07-NUM-OPC  PIC X(012).
              10 FILLER           PIC X(003) VALUE ALL ' '.
              10 WR-LA07-VEN-OPC  PIC X(003).
              10 FILLER           PIC X(004) VALUE ALL ' '.
              10 WR-LA07-CAN-OPC  PIC X(003).
              10 FILLER           PIC X(004) VALUE ALL ' '.
              10 WR-LA07-ORI-OPC  PIC X(003).
              10 FILLER           PIC X(003) VALUE ALL ' '.
              10 WR-LA07-SAL-OPC  PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
              10 FILLER           PIC X(005) VALUE ALL ' '.
              10 WR-LA07-VEN-VEN  PIC X(003).
              10 FILLER           PIC X(010) VALUE ALL ' '.
              10 WR-LA07-CAN-VEN  PIC X(003).
              10 FILLER           PIC X(007) VALUE ALL ' '.
              10 WR-LA07-VEN-TOT  PIC X(003).
              10 FILLER           PIC X(002) VALUE ALL ' '.
              10 WR-LA07-SAL-VEN  PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
              10 FILLER           PIC X(005) VALUE ALL ' '.
              10 WR-LA07-ERROR    PIC X(020).
           05 WR-LINEA08.
              10 FILLER           PIC X(026) VALUE ALL ' '.
              10 FILLER           PIC X(018) VALUE '** CHEQUEO DE CRED'.
              10 FILLER           PIC X(018) VALUE 'ITOS COMPLEMENTARI'.
              10 FILLER           PIC X(010) VALUE 'OS O.K. **'.
      ******************************************************************
      *      USER INFORMATION BLOCK                                    *
      ******************************************************************
      *
       01  WB-UIB.
           05 FILLER                 PIC X(32) VALUE 'COLPB735'.
      *
      ******************************************************************
      *      REQUEST AREA                                              *
      ******************************************************************
      *
       01  WB-REQ-OPC-AREA.
           02 WB-REQ-OPC-CMD          PIC X(5) VALUE 'REDKG'.
           02 FILLER                  PIC X(3) VALUE 'OPC'.
           02 FILLER                  PIC X(5) VALUE 'OPC00'.
           02 WB-REQ-OPC-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +160.
           02 FILLER                  PIC X(58).
           02 WB-REQ-OPC-KEY-VALUE    PIC X(12) VALUE 'D'.

       01  WB-REQ-VEN-AREA.
           02 WB-REQ-VEN-CMD          PIC X(5) VALUE 'REDKG'.
           02 FILLER                  PIC X(3) VALUE 'VEN'.
           02 FILLER                  PIC X(5) VALUE 'VEN00'.
           02 WB-REQ-VEN-RC           PIC X(2) VALUE SPACES.
           02 FILLER                  PIC X(1).
           02 FILLER                  PIC S9(4) COMP VALUE +160.
           02 FILLER                  PIC X(58).
           02 WB-REQ-VEN-KEY-VALUE.
              03 WB-REQ-VEN-KEY-1     PIC X(12).
              03 WB-REQ-VEN-KEY-2     PIC X(03).

      ******************************************************************
      *                   WORK AREA                                    *
      ******************************************************************
      *
      *    Operacion Credito
      *    -----------------
      *
      *    Nombre Registro : OPC
      *    Clave(s)        : OPC-KEY-IOPC(UU), OPC-FEC-FCOL(DC),
      *                      OPC-FEC-FING(DC), OPC-FEC-FINP(DC)
      *    Largo           : 245
      *    Bloqueo         : 2
      *    Observaciones   :
      *
      *    01) Modificaciones 06/09/88, LJV
      *        - Eliminacion Clave OPC-STP-ITRN
      *    02) Modificaciones 11/10/89, ALD
      *        - Cambio nombre campo OPC-NUM-IEVC
      *          por  OPC-NUM-IVEN
      *        - Ingreso campo OPC-IND-VSTR
      *    03) Modificacion  30/06/93,  DAD
      *        - Modificado Largo/Bloqueo del registro, de 218/4 a 245/2
      *          aumentando la Glosa Disponible.
      *    04) Modificacion  06/09/95,  ALD
      *        - Ingreso Campo OPC-VAL-SEGP
      *        - Modificado Glosa Disponible de 28 a 20
      *
       01  OPC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OPC-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  OPC-KEY-IREG.
      *
      *KEY     Identificacion Operacion Credito
               05  OPC-KEY-IOPC.
      *
      *            Cod. Int. Comp. Operacion Credito
                   07  OPC-CIC-IOPC.
      *
      *                Centro Asignacion Identificacion
                       09  OPC-CAI-IOPC.
                           11  OPC-COD-TOPC             PIC X(01).
                           11  OPC-COD-COPC             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  OPC-IIC-IOPC.
                           11  OPC-COD-OOPC.
                               13  OPC-COD-SOPC         PIC X(02).
                               13  OPC-IND-SOPC         PIC X(01).
                           11  OPC-GLS-NOPC             PIC X(05).
      *
      *        Time Stamp Transaccion
               05  OPC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  OPC-FEC-FTRN.
                       09  OPC-NUM-STRN                 PIC 9(02).
                       09  OPC-NUM-ATRN                 PIC 9(02).
                       09  OPC-NUM-MTRN                 PIC 9(02).
                       09  OPC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  OPC-HRA-HRTR.
                       09  OPC-NUM-HHTR                 PIC 9(02).
                       09  OPC-NUM-MMTR                 PIC 9(02).
                       09  OPC-NUM-SSTR                 PIC 9(02).
      *
      *REQ Indicador Tipo Accion
           03  OPC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  OPC-MSC-STAT                             PIC X(01).
               88  OPC-INGRESADA               VALUE 'I'.
               88  OPC-MODIFICADA              VALUE 'M'.
               88  OPC-APROBADA-CTC            VALUE 'F'.
               88  OPC-LIQUIDADA-CTC           VALUE 'K'.
               88  OPC-LIQUIDADA-LIQ           VALUE 'L'.
               88  OPC-ACTIVADA                VALUE 'A'.
               88  OPC-ESPERA-OPC              VALUE 'C'.
               88  OPC-ESPERA-ENVIO-ABONO-DIF  VALUE 'B'.
               88  OPC-ESPERA-ENVIO-ABONO      VALUE 'G'.
               88  OPC-ESPERA-CONF-ABONO       VALUE 'H'.
               88  OPC-ESPERA-ABONO-X          VALUE 'X'.
               88  OPC-SUSPENDIDA              VALUE 'S'.
               88  OPC-DESACTIVADA             VALUE 'D'.
               88  OPC-ACTUALIZADA             VALUE 'I' 'M'.
               88  OPC-PENDIENTE-CTC           VALUE 'F' 'K'.
               88  OPC-LIQUIDADA               VALUE 'L' 'K'.
               88  OPC-ESPERA-ABONO            VALUE 'B' 'G' 'H' 'X'.
      * RBG INI 10-APR-1996
               88  OPC-APROBADA-APR            VALUE 'J'.
      * RBG FIN 10-APR-1996
      *
      *REQ Codigo Origen Transaccion
           03  OPC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  OPC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  OPC-COD-TTRN                         PIC X(03).
      *
      *REQ Autor Transaccion
           03  OPC-COD-ATRN                             PIC X(12).
      *
      *REQ Valor de Cambio Operacion Credito            TAB VLR
           03  OPC-COD-VCOC.
      *
      *        Miscelaneo Tipo Valor                    TAB TVAL
               05  OPC-MSC-TVOC                         PIC X(01).
      *
      *        Codigo Valor
               05  OPC-COD-CVOC                         PIC X(03).
      *
      *        Codigo Tipo Cambio
               05  OPC-COD-TCOC                         PIC X(02).
      *
      *REQ Codigo Clase Operacion Credito               COL AUX
           03  OPC-COD-COOC.
      *
      *        Codigo Tipo Operacion                    COL TOC
               05  OPC-COD-TOOC                         PIC X(03).
      *
      *        Codigo Auxiliar Operacion
               05  OPC-COD-AOOC                         PIC X(03).
      *
      *REQ Codigo Oficina Operacion Credito             TAB OFI
           03  OPC-COD-OFOC                             PIC X(03).
      *
      *    Responsable Mantencion Operacion Credito     TAB USR
           03  OPC-COD-RMOC                             PIC X(12).
      *
      *    Codigo Destino Operacion Credito             COL DES
           03  OPC-COD-CDOC                             PIC X(03).
      *
      *    Cod. Int. Comp. Linea de Credito
           03  OPC-CIC-ILDC.
      *
      *        Centro Asignacion Identificacion
               05  OPC-CAI-ILDC.
                   07  OPC-COD-TLDC                     PIC X(01).
                   07  OPC-COD-CLDC                     PIC X(03).
      *
      *        Ident. Interna Computacional
               05  OPC-IIC-ILDC.
                   07  OPC-COD-OLDC.
                       09  OPC-COD-SLDC                 PIC X(02).
                       09  OPC-IND-SLDC                 PIC X(01).
                   07  OPC-GLS-NLDC                     PIC X(05).
      *
      *REQ Fecha Curse o Colocacion
           03  OPC-FEC-FCOL.
               05  OPC-NUM-SCOL                         PIC 9(02).
               05  OPC-NUM-ACOL                         PIC 9(02).
               05  OPC-NUM-MCOL                         PIC 9(02).
               05  OPC-NUM-DCOL                         PIC 9(02).
      *
      *REQ Fecha Curse Contable
           03  OPC-FEC-FING.
               05  OPC-NUM-SING                         PIC 9(02).
               05  OPC-NUM-AING                         PIC 9(02).
               05  OPC-NUM-MING                         PIC 9(02).
               05  OPC-NUM-DING                         PIC 9(02).
      *
      *REQ Fecha Curse Computacional
           03  OPC-FEC-FINP.
               05  OPC-NUM-SINP                         PIC 9(02).
               05  OPC-NUM-AINP                         PIC 9(02).
               05  OPC-NUM-MINP                         PIC 9(02).
               05  OPC-NUM-DINP                         PIC 9(02).
      *
      *REQ Monto Credito
           03  OPC-VAL-CRED             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Saldo Valor Credito K Capital Insoluto
           03  OPC-VAL-SCRE             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Analisis Feriados (S/N)                      COL AFER
           03  OPC-IND-AFER                             PIC X(01).
      *
      *REQ Modalidad Calculo Valores Finales            COL MCVF
           03  OPC-MSC-MCVF                             PIC X(01).
      *
      *REQ Tipo Calculo Vencimientos                    COL TCVE
           03  OPC-MSC-TCVE.
      *
      *        Base Calculo Credito                     COL BCCR
               05  OPC-MSC-BCCR                         PIC X(01).
      *
      *        Valores Informados Credito               COL VICR
               05  OPC-MSC-VICR                         PIC X(01).
      *
      *    Tipo Renovacion                              COL RENO
           03  OPC-IND-RENO                             PIC X(03).
      *
      *    Condicion Garantia                           COL GARA
           03  OPC-IND-GARA                             PIC X(03).
      *
      *    Valor Comision
           03  OPC-SGV-VCOM             COMP-3          PIC S9(11)V9(4).
      *
      *    Valor Gastos
           03  OPC-VAL-GAST             COMP-3          PIC S9(11)V9(4).
      *
      *    Codigo de Seguro Desgravamen                 TAB IVB
           03  OPC-COD-SEGD.
               05  OPC-COD-ISEG                         PIC X(03).
               05  OPC-COD-CSEG                         PIC X(03).
               05  OPC-COD-SSEG                         PIC X(03).
      *
      *    Valor Seguro
           03  OPC-VAL-SEGD             COMP-3          PIC 9(11)V9(04).
      *
      *    Valor Liquido
           03  OPC-SGV-LIQU             COMP-3          PIC S9(11)V9(4).
      *
      *MOD-02
      *REQ Numero Proximo Vencimiento
           03  OPC-NUM-IVEN                             PIC 9(03).
      *
      *REQ Numero Vencimientos Originales
           03  OPC-NUM-VCOR                             PIC 9(03).
      *
      *REQ Numero Vencimientos Vigentes
           03  OPC-NUM-VCVG                             PIC 9(03).
      *
      *    Numero Venctos Cancelados en forma Oportuna
           03  OPC-NUM-VCCO                             PIC 9(03).
      *
      *    Numero Vencimientos Cancelados Morosos
           03  OPC-NUM-VCCM                             PIC 9(03).
      *
      *    Numero de Vencimientos Cancelados Vencidos
           03  OPC-NUM-VCCV                             PIC 9(03).
      *
      *REQ Numero Total Vencimientos Cancelados
           03  OPC-NUM-TVCC                             PIC 9(03).
      *
      *ING-02
      *    Indicador Vencimientos Estructurados
           03  OPC-IND-VSTR                             PIC X(01).
      *
      *    Cod. Int. Comp. Credito Enlace
           03  OPC-CIC-ICEN.
      *
      *        Centro Asignacion Identificacion
               05  OPC-CAI-ICEN.
                   07  OPC-COD-TCEN                     PIC X(01).
                   07  OPC-COD-CCEN                     PIC X(03).
      *
      *        Ident. Interna Computacional
               05  OPC-IIC-ICEN.
                   07  OPC-COD-OCEN.
                       09  OPC-COD-SCEN                 PIC X(02).
                       09  OPC-IND-SCEN                 PIC X(01).
                   07  OPC-GLS-NCEN                     PIC X(05).
      *
      *    Codigo Area Gestion                          COL GES
           03  OPC-COD-AGES                             PIC X(02).
      *
      *    Flag para activar sin contabilizar
           03 OPC-IND-CTBL                              PIC X(01).
      *
      *    Indicador Insistencia
           03 OPC-COD-INSI                              PIC X(01).
      *
      *    Canal de Venta
           03 OPC-COD-CVTA                              PIC X(01).
      *
      *MOD-04
      *    Valor Seguro
           03  OPC-VAL-SEGP             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-04
      *    Glosa Disponible
           03  OPC-GLS-DISP                             PIC X(20).
      *    Vencimientos
      *    ------------
      *
      *    Nombre Registro : VEN
      *    Clave(s)        : VEN-KEY-IVEN(UU), VEN-FEC-FVEN(DC),
      *                      VEN-IND-SCTB(DC)
      *    Largo           : 145
      *    Bloqueo         : 3
      *    Observaciones   :
      *
      *    01) Modificaciones 06/09/88, LJV
      *        - Eliminacion Clave VEN-KEY-IREG
      *        - Estandarizacion a Header tipo Log
      *        - Modificacion Largo/Bloqueo registro (117/1)
      *        - Cambio ubicacion campo IND-SCBZ
      *        - Incorporacion campo VAL-MTSC
      *    02) Modificaciones 23/09/88, PGV
      *    03) Modificacion  30/06/93,  DAD
      *        - Modificado Largo/Bloqueo del registro, de 137/2 a 145/3
      *          aumentando la Glosa Disponible.
      *
       01  VEN.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  VEN-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  VEN-KEY-IREG.
      *
      *KEY     Identificacion Vencimientos
               05  VEN-KEY-IVEN.
      *
      *            Cod. Int. Comp. Operac. Credito
                   07  VEN-CIC-IOPC.
      *
      *                Centro Asignac. Identificacion
                       09  VEN-CAI-IOPC.
                           11  VEN-COD-TOPC             PIC X(01).
                           11  VEN-COD-COPC             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  VEN-IIC-IOPC.
                           11  VEN-COD-OOPC.
                               13  VEN-COD-SOPC         PIC X(02).
                               13  VEN-IND-SOPC         PIC X(01).
                           11  VEN-GLS-NOPC             PIC X(05).
      *
      *            Num. Identificacion Vencimiento
                   07  VEN-NUM-IVEN                     PIC 9(03).
      *
      *        Time Stamp Transaccion
               05  VEN-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  VEN-FEC-FTRN.
                       09  VEN-NUM-STRN                 PIC 9(02).
                       09  VEN-NUM-ATRN                 PIC 9(02).
                       09  VEN-NUM-MTRN                 PIC 9(02).
                       09  VEN-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  VEN-HRA-HRTR.
                       09  VEN-NUM-HHTR                 PIC 9(02).
                       09  VEN-NUM-MMTR                 PIC 9(02).
                       09  VEN-NUM-SSTR                 PIC 9(02).
      *
      *REQ Indicador Tipo Accion
           03  VEN-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  VEN-MSC-STAT                             PIC X(01).
      *
      *REQ Codigo Origen Transaccion
           03  VEN-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  VEN-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  VEN-COD-TTRN                         PIC X(03).
      *
      *REQ Autor Transaccion
           03  VEN-COD-ATRN                             PIC X(12).
      *
      *REQ Fecha Vencimiento
           03  VEN-FEC-FVEN.
               05  VEN-NUM-SVEN                         PIC 9(02).
               05  VEN-NUM-AVEN                         PIC 9(02).
               05  VEN-NUM-MVEN                         PIC 9(02).
               05  VEN-NUM-DVEN                         PIC 9(02).
      *
      *REQ Fecha Inicio Mora Vcmto.
           03  VEN-FEC-FIMV.
               05  VEN-NUM-SIMV                         PIC 9(02).
               05  VEN-NUM-AIMV                         PIC 9(02).
               05  VEN-NUM-MIMV                         PIC 9(02).
               05  VEN-NUM-DIMV                         PIC 9(02).
      *
      *REQ Valor Capital Vencimiento
           03  VEN-VAL-CAPI             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Saldo Valor Capital Vcmto.
           03  VEN-VAL-SCAP             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Int Devengados no incluidos
      *    en Valor Final Vcto
           03  VEN-VAL-IDNI             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Valor Final Vencimiento
           03  VEN-VAL-FINA             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Saldo Valor Final Vcmto.
           03  VEN-VAL-SFIN             COMP-3          PIC 9(11)V9(04).
      *
      *    Situacion Contable                           COL SCTB
           03  VEN-IND-SCTB                             PIC X(03).
      *
      *REQ Num. Identificacion Vcmto.
           03  VEN-NUM-IVCT                             PIC 9(03).
      *
      *    Num. Identif. Estructura Vcmto.
           03  VEN-NUM-IEVC                             PIC 9(03).
      *
      *REQ Num Identif Documento Asociado
           03  VEN-NUM-IDLC                             PIC 9(03).
      *
      *    Indicador Cargo Automatico                   GNS VIGE
           03  VEN-IND-CAUT                             PIC X(01).
      *
      *    Situacion Cobranza                           COL SCBZ
           03  VEN-IND-SCBZ                             PIC X(05).
      *
      *ING-01
      *    Monto Trasp Situac Contable
           03  VEN-VAL-MTSC             COMP-3          PIC 9(11)V9(04).
      *
      *ING-02
      *    Ind Trasp Situac Contable
           03  VEN-IND-MTSC                             PIC X(01).
      *
      *MOD-01  PIC X(08)
      *MOD-02  PIC X(07)
      *    Indicador de calculo
           03  VEN-IND-CALC                             PIC X(01).
      *
      *    Situacion Contable Anterior
           03  VEN-IND-CTBA                             PIC X(03).
      *
      *MOD-03
      *    Glosa Disponible
           03  VEN-GLS-DISP                             PIC X(10).
      ******************************************************************
      *                 ELEMENT LIST                                   *
      ******************************************************************
      *
       01  WB-ELEM-LIS        PIC X(11).
           EJECT
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           EJECT
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF WS-PROCESO-OK
              PERFORM C-PROCESAR .
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-PROCESO.
           GOBACK.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
           PERFORM BB-ABRIR-ARCHIVOS.
           PERFORM BC-INICI-LISTADO.
           GO BZ-FIN-DE-SECCION.
           SKIP1
       BB-ABRIR-ARCHIVOS.
      *------------------
           OPEN OUTPUT COLL735.
           SKIP1
       BC-INICI-LISTADO.
      *-----------------
           ACCEPT WR-FECHA-HOY FROM DATE.
           MOVE WR-HOY-MM               TO WR-LA01-MES
           MOVE WR-HOY-DD               TO WR-LA01-DIA
           MOVE WR-HOY-AA               TO WR-LA01-ANO.
           SKIP1
       BD-DISPLAY-ERROR.
      *-----------------
           SET WS-PROCESO-NO-OK        TO TRUE.
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION   : ' WR-SECCION
           DISPLAY '   PARRAFO         : ' WR-PARRAFO
           DISPLAY '   ARCHIVO         : ' WR-ARCHIVO
           DISPLAY '   OPERACION       : ' WR-OPERACI
           DISPLAY '   FILE STATUS     : ' WR-FILE-ST
           DISPLAY '   MENSAJE DE ERROR: ' WR-MENSAJE.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GRAL.
      *----------------
           PERFORM CB-LEER-OPC
           MOVE 'REDNX'                TO WB-REQ-OPC-CMD.
           PERFORM CC-PROCESA-OPERACIONES
              UNTIL (WB-REQ-OPC-RC  NOT = '  ' OR WS-PROCESO-NO-OK OR
                     OPC-COD-TOPC   NOT = 'D').
           IF (WA-PAGINA = 0)
               PERFORM CH-TITULOS
               WRITE COLR735 FROM WR-LINEA08 AFTER 3
           END-IF.
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-OPC.
      *------------
           MOVE SPACES                   TO WB-REQ-OPC-RC
           MOVE 'OPC00'                  TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-OPC-AREA OPC WB-ELEM-LIS
           IF (WB-REQ-OPC-RC NOT = '  ' AND NOT = '14')
               MOVE 'C-PROCESAR'         TO WR-SECCION
               MOVE 'CB-LEER-OPC'        TO WR-PARRAFO
               MOVE 'OPC'                TO WR-ARCHIVO
               MOVE WB-REQ-OPC-CMD       TO WR-OPERACI
               MOVE WB-REQ-OPC-RC        TO WR-FILE-ST
               MOVE WB-REQ-OPC-KEY-VALUE TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CC-PROCESA-OPERACIONES.
      *-----------------------
           ADD 1                         TO WA-L-OPC
           IF (OPC-MSC-STAT = 'A'   AND OPC-VAL-SCRE > 0     AND
               OPC-COD-TOOC = 'CON' AND OPC-COD-AOOC = '222' AND
               OPC-IND-VSTR = 'N')
               ADD 1                     TO WA-S-OPC
               MOVE 0                    TO WA-CUO-TOT
                                            WA-CUO-CAN
                                            WA-CUO-IMP
                                            WA-SAL-FIN
               MOVE OPC-KEY-IOPC         TO WB-REQ-VEN-KEY-VALUE
               MOVE 'REDKG'              TO WB-REQ-VEN-CMD
               PERFORM CE-LEER-VEN
               MOVE 'REDNX'              TO WB-REQ-VEN-CMD
               PERFORM CD-CHEQUEO-CUOTAS
                UNTIL (WB-REQ-VEN-RC NOT = '  ' OR WS-PROCESO-NO-OK OR
                       OPC-CIC-IOPC NOT = VEN-CIC-IOPC)
               IF (WS-PROCESO-OK)
                   PERFORM CF-IMPRIMIR-OBSERVAC
               END-IF
           END-IF.
           PERFORM CB-LEER-OPC.
           SKIP1
       CD-CHEQUEO-CUOTAS.
      *------------------
           ADD 1                         TO WA-L-VEN
           IF (WA-CUO-TOT = 0)
               MOVE VEN-NUM-IVEN         TO WA-CUO-TOT
           ELSE
               ADD 1                     TO WA-CUO-TOT
           END-IF

           IF (VEN-VAL-SFIN NOT > 0)
               IF (WA-CUO-CAN = 0)
                   MOVE VEN-NUM-IVEN     TO WA-CUO-CAN
               ELSE
                   ADD 1                 TO WA-CUO-CAN
               END-IF
           ELSE
               IF (WA-CUO-IMP = 0)
                   MOVE VEN-NUM-IVEN     TO WA-CUO-IMP
               END-IF
               ADD VEN-VAL-SCAP          TO WA-SAL-FIN
           END-IF.
           PERFORM CE-LEER-VEN.
           SKIP1
       CE-LEER-VEN.
      *------------
           MOVE SPACES                   TO WB-REQ-VEN-RC
           MOVE 'VEN00'                  TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-VEN-AREA VEN WB-ELEM-LIS
           IF (WB-REQ-VEN-RC NOT = '  ' AND NOT = '14')
               MOVE 'C-PROCESAR'         TO WR-SECCION
               MOVE 'CF-LEER-VEN'        TO WR-PARRAFO
               MOVE 'VEN'                TO WR-ARCHIVO
               MOVE WB-REQ-VEN-CMD       TO WR-OPERACI
               MOVE WB-REQ-VEN-RC        TO WR-FILE-ST
               MOVE WB-REQ-VEN-KEY-VALUE TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CF-IMPRIMIR-OBSERVAC.
      *---------------------
           IF (OPC-COD-COPC = '041' AND OPC-FEC-FINP NOT < '19910419')
               OR
              (OPC-COD-COPC = '090' AND OPC-FEC-FINP NOT < '19910510')
               OR
              (OPC-COD-COPC NOT = '090' AND
               OPC-COD-COPC NOT = '041' AND
               OPC-FEC-FINP NOT < '19910805')
               COMPUTE OPC-NUM-TVCC = OPC-NUM-VCCO +
                                      OPC-NUM-VCCM + OPC-NUM-VCCV
           END-IF

           MOVE OPC-CIC-IOPC           TO WR-LA07-NUM-OPC
           MOVE OPC-NUM-IVEN           TO WR-LA07-VEN-OPC
           MOVE OPC-NUM-TVCC           TO WR-LA07-CAN-OPC
           MOVE OPC-NUM-VCOR           TO WR-LA07-ORI-OPC
           MOVE OPC-VAL-SCRE           TO WR-LA07-SAL-OPC
           MOVE WA-CUO-IMP             TO WR-LA07-VEN-VEN
           MOVE WA-CUO-CAN             TO WR-LA07-CAN-VEN
           MOVE WA-CUO-TOT             TO WR-LA07-VEN-TOT
           MOVE WA-SAL-FIN             TO WR-LA07-SAL-VEN

           IF (OPC-NUM-IVEN NOT = WA-CUO-IMP)
               MOVE 'CUOTA IMPAGA RESAGAD' TO WR-LA07-ERROR
               IF (WA-LINEA > 50)
                   PERFORM CH-TITULOS
               END-IF
               WRITE COLR735 FROM WR-LINEA07 AFTER 1
               ADD 1                   TO WA-LINEA
               MOVE SPACES             TO WR-LINEA07
           END-IF.
           IF (OPC-NUM-TVCC NOT = WA-CUO-CAN) OR
              (OPC-NUM-TVCC NOT < OPC-NUM-IVEN)
               MOVE 'CANCELACIONES TOTAL ' TO WR-LA07-ERROR
               IF (WA-LINEA > 50)
                   PERFORM CH-TITULOS
               END-IF
               WRITE COLR735 FROM WR-LINEA07 AFTER 1
               ADD 1                   TO WA-LINEA
               MOVE SPACES             TO WR-LINEA07
           END-IF.
           IF (OPC-NUM-VCOR NOT = WA-CUO-TOT)
               MOVE 'VCTOS. ORIGINALES   ' TO WR-LA07-ERROR
               IF (WA-LINEA > 50)
                   PERFORM CH-TITULOS
               END-IF
               WRITE COLR735 FROM WR-LINEA07 AFTER 1
               ADD 1                   TO WA-LINEA
               MOVE SPACES             TO WR-LINEA07
           END-IF.
           IF (OPC-VAL-SCRE NOT = WA-SAL-FIN)
               MOVE 'SALDO DEL CREDITO   ' TO WR-LA07-ERROR
               IF (WA-LINEA > 50)
                   PERFORM CH-TITULOS
               END-IF
               WRITE COLR735 FROM WR-LINEA07 AFTER 1
               ADD 1                   TO WA-LINEA
               MOVE SPACES             TO WR-LINEA07
           END-IF.
           SKIP1
       CH-TITULOS.
      *-----------
           ADD 1                       TO WA-PAGINA
           MOVE WA-PAGINA              TO WR-LA02-PAGINA
           WRITE COLR735 FROM WR-LINEA01 AFTER PAGE
           WRITE COLR735 FROM WR-LINEA02 AFTER 1
           WRITE COLR735 FROM WR-LINEA06 AFTER 2
           WRITE COLR735 FROM WR-LINEA04 AFTER 2
           WRITE COLR735 FROM WR-LINEA05 AFTER 1
           WRITE COLR735 FROM WR-LINEA06 AFTER 1
           MOVE 0                      TO WA-LINEA.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'COLL735 EMITIDO EN FORMA LOCAL'
           ELSE
               DISPLAY 'COLL735 NO EMITIDO O INCOMPLETO'.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA COLPB735 FECHA '
                    WR-FECHA-HOY
           DISPLAY '------------------------------------------------'
           DISPLAY '      CHEQUOE OPERACIONES COMPLEMENTARIAS'
           DISPLAY ' '
           DISPLAY 'LEIDOS OPC       : ' WA-L-OPC
           DISPLAY 'SELECC OPC       : ' WA-S-OPC
           DISPLAY 'LEIDOS VEN       : ' WA-L-VEN
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DC-TERMINO.
      *-----------
           CLOSE COLL735.
           IF WS-PROCESO-NO-OK
              MOVE 100 TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
