       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGGAD.
       AUTHOR.          BEE (DPM).
       DATE-WRITTEN.    9-FEB-1998.

      * Programa Generador de Archivo con movimientos contrarios
      * al archivo DVG.

       ENVIRONMENT DIVISION.
      *====================

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-FZE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GAD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRFZE.
       COPY COLBRGAD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGGRC.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGFEC.
       COPY GNSWGHOY.

       COPY GNSWGSYS.

       01  WSS-VARI.
           03  WSS-FEC-FPAR.
               05  WSS-NUM-DPAR   VALUE ZEROES      PIC 9(02).
               05  WSS-NUM-MPAR   VALUE ZEROES      PIC 9(02).
               05  WSS-NUM-SPAR   VALUE ZEROES      PIC 9(02).
               05  WSS-NUM-APAR   VALUE ZEROES      PIC 9(02).

           03  WSS-DMA-FPRO.
               05  WSS-NUM-DPRO   VALUE ZEROES      PIC 9(02).
               05  WSS-NUM-MPRO   VALUE ZEROES      PIC 9(02).
               05  WSS-NUM-APRO   VALUE ZEROES      PIC 9(02).

      * Variables para estadisticas
           03  WSS-NUM-RLEI       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RGRA       VALUE ZEROES      PIC 9(06).
           03  WSS-NUM-RRCH       VALUE ZEROES      PIC 9(06).
      *
      *****CABECERA*****
      *   CABECERA DE LOTE (BATCH HEADER)
           03 WSS-FZE-CABECERA.
      *      CABECERA-LOTE
              05 WSS-FZE-COD-CABE     VALUE 'BH'     PIC X(02).
      *      COMPANIA
              05 WSS-FZE-COD-CCIA     VALUE 'BCI '   PIC X(04).
      *      COD.IDENTIFICACION
              05 WSS-FZE-COD-IDEN                    PIC X(02).
      *      NUMERO DE LOTE
              05 WSS-FZE-NUM-LOTE                    PIC 9(02).
      *      TIPO DE MOVIMIENTO
              05 WSS-FZE-COD-MVTO     VALUE 2        PIC 9(01).
      *      TOTAL DE LOTE
              05 WSS-FZE-SGV-LOTE     VALUE ZEROES   PIC 9(12)V9(02).
      *      PERIODO
              05 WSS-FZE-COD-PERI     VALUE 0        PIC 9(01).
      *      FECHA
              05 WSS-FZE-FEC-FECH                    PIC X(06).
      *
      *   DETALLE DEL MOVIMIENTO (**PRIMER REGISTRO**)
      *
           03 WSS-FZE-PMERREG.
      *      IDENTIFICACION DEL REGISTRO.
              05 WSS-FZE-COD-IDE1     VALUE 'PT'     PIC X(02).
      *      NUMERO DE MOVIMIENTO
              05 WSS-FZE-NUM-MOV1                    PIC 9(04).
      *      PRIMER REGISTRO
              05 WSS-FZE-NUM-REG1     VALUE 1        PIC 9(01).
      *      DEBE-HABER
              05 WSS-FZE-NUM-DEHA                    PIC 9(02).
      *      COMPANIA
              05 WSS-FZE-COD-CIA1     VALUE 'BCI '   PIC X(04).
      *      CUENTA
              05 WSS-FZE-COD-CCSB                    PIC 9(09).
      *      BLANCOS
              05 WSS-FZE-COD-BLCO     VALUE SPACES   PIC X(09).
      *      SUCURSAL
              05 WSS-FZE-COD-SUCU                    PIC X(12).
      *      SOURCE CODE
              05 WSS-FZE-COD-CODE                    PIC X(10).
      *      FECHA
              05 WSS-FZE-FEC-FCTB                    PIC X(06).
      *      MONTO
              05 WSS-FZE-NUM-MONT     VALUE ZEROES   PIC 9(13)V9(02).
      *
      *
      *    (**SEGUNDO REGISTRO**)
      *
           03 WSS-FZE-SGDOREG.
              05 WSS-FZE-COD-IDE2     VALUE 'PT'     PIC X(02).
              05 WSS-FZE-NUM-MOV2                    PIC 9(04).
              05 WSS-FZE-NUM-REG2     VALUE 2        PIC 9(01).
              05 WSS-FZE-GLS-GLOS                    PIC X(30).
              05 WSS-FZE-COD-VIGE                    PIC X(12).
              05 WSS-FZE-COD-DESC                    PIC X(10).


       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGGAD' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM OBT-PARAM.
           PERFORM GENERA-GAD.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-FZE.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA COLFZE...' FIO-STAT
               PERFORM GNS-PRO-ABT.

           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-GAD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA COLGAD...' FIO-STAT
               PERFORM GNS-PRO-ABT.
       FIN-OPEN-FILE.
           EXIT.

      ******************************************************************
       OBT-PARAM SECTION.
      *-----------------
       INI-OBT-PARAM.
           DISPLAY 'CAPTURA DE PARAMETROS'.
           DISPLAY '---------------------'.
           ACCEPT WSS-FEC-FPAR IN WSS-VARI.
           DISPLAY 'FECHA PROCESO: ' WSS-FEC-FPAR IN WSS-VARI.
           DISPLAY ' '.

      *VAL-FEC VALIDA FECHA
           MOVE WSS-FEC-FPAR IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'FECPROCNEX'   TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC WSS-FEC-FPAR IN WSS-VARI
               PERFORM GNS-PRO-ABT.

           MOVE WSS-NUM-DPAR IN WSS-VARI TO WSS-NUM-DPRO IN WSS-VARI.
           MOVE WSS-NUM-MPAR IN WSS-VARI TO WSS-NUM-MPRO IN WSS-VARI.
           MOVE WSS-NUM-APAR IN WSS-VARI TO WSS-NUM-APRO IN WSS-VARI.
       FIN-OBT-PARAM.
           EXIT.

      ******************************************************************
       GENERA-GAD SECTION.
      *------------------
       INI-GENERA-GAD.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-FZE.

       LUP-GENERA-GAD.
           IF NOT FIO-STAT-OKS
              GO TO FIN-GENERA-GAD.

           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

           MOVE FZE TO WSS-FZE-CABECERA.
           IF WSS-FZE-COD-CABE = 'BH'
               MOVE WSS-DMA-FPRO IN WSS-VARI TO WSS-FZE-FEC-FECH
               MOVE WSS-FZE-CABECERA TO GAD
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-GAD
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR AL GRABAR COLGAD ' FIO-STAT
                   ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
                   GO TO NXT-GENERA-GAD
               ELSE
                   ADD 1 TO WSS-NUM-RGRA IN WSS-VARI
                   GO TO NXT-GENERA-GAD.

           MOVE FZE TO WSS-FZE-PMERREG IN WSS-VARI.
           IF NOT ( WSS-FZE-COD-IDE1 IN WSS-VARI = 'PT' AND
              WSS-FZE-NUM-REG1 IN WSS-VARI = '1' )
               MOVE WSS-FZE-PMERREG IN WSS-VARI TO GAD
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-GAD
               ADD 1 TO WSS-NUM-RGRA IN WSS-VARI
               GO TO NXT-GENERA-GAD
           ELSE
           IF WSS-FZE-NUM-DEHA IN WSS-VARI = '60'
               MOVE '10' TO WSS-FZE-NUM-DEHA IN WSS-VARI
           ELSE
               MOVE '60' TO WSS-FZE-NUM-DEHA IN WSS-VARI.

           MOVE WSS-DMA-FPRO IN WSS-VARI TO WSS-FZE-FEC-FCTB.
           MOVE WSS-FZE-PMERREG IN WSS-VARI TO GAD.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-GAD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL MODIFICAR COLGAD' FIO-STAT
               ADD 1 TO WSS-NUM-RRCH IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-RGRA IN WSS-VARI.

       NXT-GENERA-GAD.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-FZE.
           GO TO LUP-GENERA-GAD.
       FIN-GENERA-GAD.
           EXIT.

      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-FZE.
           PERFORM COL-FIO-GAD.
       FIN-CLOSE-FILE.
           EXIT.

      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *----------------------------
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY 'ESTADISTICAS DEL PROCESO.'.
           DISPLAY '-------------------------'.
           DISPLAY ' '.
           DISPLAY 'REGISTROS FZE LEIDOS      : '
                                  WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'REGISTROS FZE NO GRABADOS : '
                                  WSS-NUM-RRCH IN WSS-VARI.
           DISPLAY 'REGISTROS GAD GRABADOS    : '
                                  WSS-NUM-RGRA IN WSS-VARI.
        FIN-MUESTRA-ESTADISTICAS.
           EXIT.


       COPY COLBFFZE.
       COPY COLBFGAD.

       COPY GNSBFTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBFMSG.
       COPY GNSBBMSG.
