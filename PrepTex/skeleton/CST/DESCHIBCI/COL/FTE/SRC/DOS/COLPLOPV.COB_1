       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLOPV.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    19-Aug-91 06:42 PM.

      * Programa Listador de Reporte OPV con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTOPV         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTOPV         ASSIGN TO        UT-S-RPTOPV.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTOPV         ASSIGN TO        UT-S-RPTOPV.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRROPV.
       FD  RPTOPV LABEL RECORDS OMITTED
           REPORT IS RPT-OPV
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBROPC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWOPV.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY TABBRTIO.
       COPY COLBRDLC.
       COPY COLBRTOC.
       COPY COLBRICG.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       COPY COLWGCTO.
       01  WSS-VARI.
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO                          PIC 9(02).
              05 WSS-NUM-MPRO                          PIC 9(02).
              05 WSS-NUM-SPRO                          PIC 9(02).
              05 WSS-NUM-APRO                          PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                          PIC 9(02).
              05 WSS-NUM-AFEC                          PIC 9(02).
              05 WSS-NUM-MFEC                          PIC 9(02).
              05 WSS-NUM-DFEC                          PIC 9(02).
           03 WSS-COD-OFOC                             PIC X(03).
           03 WSS-I                                    PIC 9(02).
           03 WSS-COD-COOC.
              05 WSS-IND-COOC      OCCURS 10           PIC X(06).
           03 WSS-COD-PESO                             PIC X(06).
           03 WSS-IND-NEXT                             PIC 9(01).
           03 WSS-VAL-MNAC         VALUE ZEROES        PIC 9(11).
           03 WSS-GLS-DATA.
              05 WSS-GLS-TIO                           PIC X(40).
              05 WSS-GLS-AUX                           PIC X(40).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY COLRFOPV.
           .
       COPY COLRTOPV.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-OPV-OPC-COD-OFOC SECTION.
           USE BEFORE REPORTING CF-OPV-OPC-COD-OFOC.
       INI-CF-OPV-OPC-COD-OFOC.
      *<<<< CF_OPV_OPC_COD_OFOC
           MOVE ZEROES TO PAGE-COUNTER.
      *>>>>
       FIN-CF-OPV-OPC-COD-OFOC.
           EXIT.
       DEC-CF-OPV-OPC-COD-COOC SECTION.
           USE BEFORE REPORTING CF-OPV-OPC-COD-COOC.
       INI-CF-OPV-OPC-COD-COOC.
       FIN-CF-OPV-OPC-COD-COOC.
           EXIT.
       DEC-CF-OPV-OPC-COD-VCOC SECTION.
           USE BEFORE REPORTING CF-OPV-OPC-COD-VCOC.
       INI-CF-OPV-OPC-COD-VCOC.
       FIN-CF-OPV-OPC-COD-VCOC.
           EXIT.
       DEC-PH-OPV SECTION.
           USE BEFORE REPORTING PH-OPV.
       INI-PH-OPV.
      *<<<< PH_OPV
      * Mediante BUS-OFI se Obtiene Nombre de la Oficina de Tabla OFI
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.

      * Mediante BUS-TAB Busca Tipo de Operacion Credito de Tabla TIO
           IF OPC-COD-TOOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'TIO'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-TOOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-TIO IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-GLS-TIO IN WSS-VARI.
      * Mediante BUS-TAB Busca Clase de Operacion Credito de Tabla AUX
           IF OPC-COD-COOC IN SRT > SPACES
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-COOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO WSS-GLS-AUX IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-GLS-AUX IN WSS-VARI.
      * Se concatenan las glosas de TIO y de AUX
           MOVE WSS-GLS-DATA IN WSS-VARI TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO WSS-GLS-DATA IN WSS-OPV.

      * Mediante BUS-TAB Busca Tipo de Moneda de Tabla VLR
           IF OPC-COD-VCOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VCOC IN WSS-OPV
           ELSE
               MOVE SPACES TO WSS-GLS-VCOC IN WSS-OPV.
      *>>>>
       FIN-PH-OPV.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLOPV' TO FIO-PROG.
           PERFORM BUS-FSIS.
           SORT GNSSRT
       COPY COLRSOPV.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      * Captura Fecha de Proceso
           DISPLAY 'FECHA DE PROCESO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
      * Captura Codigo de Oficina
           DISPLAY 'OFICINA (*=TODAS) : '.
           ACCEPT  WSS-COD-OFOC IN WSS-VARI.
           DISPLAY WSS-COD-OFOC IN WSS-VARI.
      * Captura Codigo Clase Operacion
           DISPLAY 'CLASE OPERACION (*=TODAS) : '.
           MOVE SPACES TO WSS-COD-COOC IN WSS-VARI.
           ACCEPT  WSS-COD-COOC IN WSS-VARI.
           DISPLAY WSS-COD-COOC IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DPRO IN WSS-OPV
                                            WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MPRO IN WSS-OPV
                                            WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SPRO IN WSS-OPV
                                            WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-APRO IN WSS-OPV
                                            WSS-NUM-AFEC IN WSS-VARI.

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFOC IN WSS-VARI
               MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

      *VAL-TAB Valida Clase de Operacion en TABLAS
           IF WSS-COD-COOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-COOC IN WSS-VARI.
           IF WSS-COD-COOC IN WSS-VARI NOT = '*'
               MOVE 1 TO WSS-I IN WSS-VARI
               PERFORM VAL-PAR-COOC UNTIL WSS-I IN WSS-VARI > 10
               DISPLAY WSS-COD-COOC IN WSS-VARI
               IF WSS-COD-COOC IN WSS-VARI NOT > SPACES
                   DISPLAY 'NO EXISTE NINGUNA CLASE INGRESADA VALIDA'
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Obtiene Codigo Valor de Cambio Mediante CIC de Moneda Nacional
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-END.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

      * Proceso para Cargar las tablas  TAB/TIO  y  COL/TOC  en Memoria
           PERFORM COL-CRG-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
               PERFORM GNS-PRO-END.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       FST-INP-SRT.
      *<<<< INI_FST_INP
           MOVE ZEROES         TO OPC-FEC-FCOL IN OPC.
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FCOL IN OPC > WSS-FEC-FFEC IN WSS-VARI
               DISPLAY 'NO HAY REGISTRO HASTA FECHA DE PROCESO : '
                       WSS-FEC-FPRO IN WSS-VARI
               GO TO FIN-INP-SRT.
           GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro OPC
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.

      * Obtiene Tasa de Interes al Leer ICG por Llave KEY-IICG Formada
           IF TOC-MSC-OPEC IN TOC NOT = 'D'
               PERFORM GET-ICG-OPC
           ELSE
               PERFORM GET-ICG-DLC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'REGISTRO OPC NO TIENE INTERESES ' OPC-KEY-IOPC
               GO TO NXT-INP-SRT.
           IF ICG-COD-CVTI IN ICG > SPACES
               MOVE ICG-SGV-SPRD IN ICG TO SRT-SGV-TINT IN SRT
           ELSE
               MOVE ICG-SGV-TINT IN ICG TO SRT-SGV-TINT IN SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMOPV.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
           MOVE 'OPC-FEC-FCOL' TO FIO-AKEY.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *<<<< FIN_NXT_INP
           IF OPC-FEC-FCOL IN OPC > WSS-FEC-FFEC IN WSS-VARI
               GO TO FIN-INP-SRT.
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTOPV.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-OPV.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
           PERFORM GEN-LIN-OUT.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-OUT-SRT.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-OPV.
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-OPV.
           CLOSE RPTOPV.
       EXT-OUT-SRT.
           EXIT.

       COPY COLBFOPC.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.

      *<<<< EOF
      ******************************************************************
      * Valida que la Clase del parametro Exista
       VAL-PAR-COOC SECTION.
       INI-VAL-PAR-COOC.
           IF WSS-IND-COOC IN WSS-VARI(WSS-I) > SPACES
               MOVE 'COL'           TO TAB-COD-SIST
               MOVE 'AUX'           TO TAB-COD-TTAB IN TAB
               MOVE WSS-IND-COOC IN WSS-VARI(WSS-I)
                                    TO TAB-COD-CTAB IN TAB
               PERFORM VAL-TAB
               IF MSG-COD-MENS IN MSG > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                           ' CLASE : ' WSS-IND-COOC IN WSS-VARI(WSS-I)
                   MOVE SPACES TO WSS-IND-COOC IN WSS-VARI(WSS-I).
           ADD 1 TO WSS-I IN WSS-VARI.
       FIN-VAL-PAR-COOC.
           EXIT.
      ******************************************************************
      * Seleccion del Registro para Proceso Mediante Fecha y Oficina del
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

           IF OPC-MSC-STAT IN OPC NOT = 'A'
               GO TO FIN-SEL-REG-PRO.

           IF OPC-VAL-SCRE IN OPC NOT > ZEROES
               GO TO FIN-SEL-REG-PRO.

           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFOC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   GO TO FIN-SEL-REG-PRO.

      * Valida que pertenezca a Clase del parametro
           IF WSS-COD-COOC IN WSS-VARI NOT = '*'
               MOVE 1 TO WSS-I IN WSS-VARI
               PERFORM VAL-SEL-COOC UNTIL WSS-I IN WSS-VARI > 10 OR
                                   WSS-IND-COOC IN WSS-VARI(WSS-I) =
                                                OPC-COD-COOC IN OPC
               IF WSS-I IN WSS-VARI > 10
                   GO TO FIN-SEL-REG-PRO.

      * Busca con el Codigo Tipo Operacion en la Tabla cargada en Memori
      * 'Sistema que lo Maneja' y 'Opera Como'
           MOVE OPC-COD-TOOC IN OPC TO CTO-TOOC IN CTO-VARI.
           PERFORM COL-BUS-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
                       'CON CODIGO : '     CTO-TOOC IN CTO-VARI
               GO TO FIN-SEL-REG-PRO.

      * Valida el 'Sistema que lo Maneja' de la Operacion de Credito
           IF CTO-SIST IN CTO-VARI NOT = 'COL'
               GO TO FIN-SEL-REG-PRO.

      * Obtiene 'Opera Como'
           MOVE SPACES               TO TOC.
           MOVE CTO-OPEC IN CTO-VARI TO TOC-MSC-OPEC IN TOC.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      ******************************************************************
      * Valida que pertenezca a Clase del parametro
       VAL-SEL-COOC SECTION.
       INI-VAL-SEL-COOC.
           ADD 1 TO WSS-I IN WSS-VARI.
       FIN-VAL-SEL-COOC.
           EXIT.
      ******************************************************************
      * Obtiene Tasa de Interes al Leer ICG por Llave KEY-IICG Formada p
       GET-ICG-OPC SECTION.
       INI-GET-ICG-OPC.
           MOVE ZEROES TO ICG-NUM-ICAN IN ICG.
           PERFORM GET-KEY-IICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-NUM-ICAN IN ICG = ZEROES              AND
                    ICG-CIC-IOPC IN ICG = OPC-CIC-IOPC IN OPC AND
                    ICG-CIC-IOPV IN ICG = SPACES              AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               MOVE '23' TO FIO-STAT.
       FIN-GET-ICG-OPC.
           EXIT.
      ******************************************************************
      * Obtiene Tasa de Interes al Leer ICG por Llave KEY-IICG Formada p
       GET-ICG-DLC SECTION.
       INI-GET-ICG-DLC.
      * Busca Documentos de Credito para Operacion Determinada
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE ZEROES              TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-NLS  TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS OR
              DLC-CIC-IOPC IN DLC NOT = OPC-CIC-IOPC IN OPC
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    DLCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   OPC-KEY-IOPC IN OPC
               MOVE '23' TO FIO-STAT
               GO TO FIN-GET-ICG-DLC.
           MOVE DLC-NUM-IDLC IN DLC TO ICG-NUM-ICAN IN ICG.
           PERFORM GET-KEY-IICG.
           IF NOT ( FIO-STAT-OKS AND
                    ICG-NUM-ICAN IN ICG = DLC-NUM-IDLC IN DLC AND
                    ICG-CIC-IOPC IN ICG = OPC-CIC-IOPC IN OPC AND
                    ICG-CIC-IOPV IN ICG = SPACES              AND
                    ICG-NUM-IVCT IN ICG = ZEROES              AND
                    ICG-IND-TTAS IN ICG = 'INT' )
               PERFORM GET-ICG-OPC.
       FIN-GET-ICG-DLC.
           EXIT.
      ******************************************************************
       GET-KEY-IICG SECTION.
       INI-GET-KEY-IICG.
      * Forma Llave KEY-IICG y Lee ICG para Obtener Tasa de Interes
           MOVE OPC-CIC-IOPC IN OPC TO ICG-CIC-IOPC IN ICG.
           MOVE SPACES              TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS  TO FIO-CMND.
           PERFORM COL-FIO-ICG.
       FIN-GET-KEY-IICG.
           EXIT.
      ******************************************************************
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
       GEN-LIN-OUT SECTION.
       INI-GEN-LIN-OUT.
           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.

      * Expresa en Pesos Saldo del Credito segun Valor de Cambio de Mone
      * la Fecha de Curse Contable
           IF OPC-COD-VCOC IN SRT NOT = WSS-COD-PESO IN WSS-VARI
               MOVE OPC-FEC-FING IN SRT      TO CAM-FEC-FCAM IN CAM(1)
               MOVE OPC-FEC-FING IN SRT      TO CAM-FEC-FCAM IN CAM(2)
               MOVE OPC-COD-VCOC IN SRT      TO CAM-COD-VCAM IN CAM(1)
               MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2)
               MOVE FIO-GET-KEY  TO FIO-CMND
               PERFORM TAB-FIO-CAM
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE VALOR DE CAMBIO PARA MONEDA : '
                            OPC-COD-VCOC IN SRT ' AL '
                            OPC-FEC-FING IN SRT ' DE OPERACION '
                            OPC-CAI-IOPC IN SRT OPC-IIC-IOPC IN SRT
                   MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                   GO TO FIN-GEN-LIN-OUT
               ELSE
                   IF CAM-IND-VIGE NOT = 'S'
                       DISPLAY 'IND. VIGENCIA NO VALIDO PARA MONEDA : '
                                OPC-COD-VCOC IN SRT ' AL '
                                OPC-FEC-FING IN SRT ' DE OPERACION '
                                OPC-CAI-IOPC IN SRT OPC-IIC-IOPC IN SRT
                       MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
                       GO TO FIN-GEN-LIN-OUT
                   ELSE
                     MULTIPLY OPC-VAL-SCRE IN SRT BY CAM-SGV-VCAM IN CAM
                                 GIVING WSS-VAL-MNAC IN WSS-VARI ROUNDED
                   MOVE WSS-VAL-MNAC IN WSS-VARI TO OPC-VAL-SCRE IN SRT.

      * Mediante BUS-TAB Busca Tipo de Tasa de Interes de Tabla INT
           IF ICG-COD-TINT IN SRT > SPACES
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'INT'               TO TAB-COD-TTAB IN TAB
               MOVE ICG-COD-TINT IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-TINT IN WSS-OPV
           ELSE
               MOVE SPACES TO WSS-GLS-TINT IN WSS-OPV.

           PERFORM BUS-REG-CLI.
       FIN-GEN-LIN-OUT.
           EXIT.
      ******************************************************************
      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro
       BUS-REG-CLI SECTION.
       INI-BUS-REG-CLI.
      * Busca CIC del Cliente con el CIC de Operaciones de Creditos en R
      * RDC-NUM-IDLC = 0 ====> DEU.DIR.
           MOVE OPC-CAI-IOPC IN SRT TO RDC-CAI-IOPC IN RDC.
           MOVE OPC-IIC-IOPC IN SRT TO RDC-IIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    RDCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RDC-KEY-IDLC IN RDC
               MOVE  1  TO WSS-IND-NEXT IN WSS-VARI
               GO TO FIN-BUS-REG-CLI.

      *CIC-CLI Busca Datos del Cliente Mediante su CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS > SPACES
               MOVE SPACES TO DBC-NUM-ICLI IN DBC
                              DBC-VRF-ICLI IN DBC
                              DBC-IND-ICLI IN DBC
                              WSS-GLS-AUXI IN WSS-OPV
                              DBC-GLS-ICLI IN DBC
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC
               IF DBC-GLS-ICLI IN DBC > SPACES
                   MOVE '-'    TO WSS-GLS-AUXI IN WSS-OPV
               ELSE
                   MOVE SPACES TO WSS-GLS-AUXI IN WSS-OPV.

           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS OR
              RDC-CAI-IOPC IN RDC NOT = OPC-CAI-IOPC IN SRT OR
              RDC-IIC-IOPC IN RDC NOT = OPC-IIC-IOPC IN SRT OR
              RDC-NUM-IDLC IN RDC NOT = ZEROES
               MOVE SPACES TO WSS-IND-MULD IN WSS-OPV
           ELSE
               MOVE 'M'    TO WSS-IND-MULD IN WSS-OPV.
       FIN-BUS-REG-CLI.
           EXIT.
      ******************************************************************

       COPY TABBFOFI.
       COPY TABBFCAM.
       COPY COLBFDLC.
       COPY COLBFTOC.
       COPY COLBFICG.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY SGCBGCLI.
       COPY COLBGCTO.
      *>>>>
