       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGCCH.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    13-Dec-95 11:25 AM.

      * Programa Listador de Reporte CCH con Sort.
000000*COMPILED SITE 2000          31.08.98

      ******************************************************************
      *  INTERSOFT                                                     *
      *  MODIFICACION: CONVERTIR REPORT-WRITER A LISTADO, MIGRAR A     *
      *                A COBOL FOR MVS Y CONVERSION 2000               *
      *  FECHA: ABRIL 1998                                             *
      ******************************************************************

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTCCH         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTCCH         ASSIGN TO        UT-S-RPTCCH.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.

      ***** CRW-COMIENZO
      *    SELECT RPTCCH         ASSIGN TO        UT-S-RPTCCH.
           SELECT RPTCCH         ASSIGN TO        RPTCCH.
      ***** CRW-FINAL

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRCCH.

      ***** CRW-COMIENZO
      *FD  RPTCCH LABEL RECORDS OMITTED
      *    REPORT IS RPT-CCH
      *    .
       FD  RPTCCH
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD STANDARD.

       01  RPTCCH-REG           PIC X(133).
      ***** CRW-FINAL

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRVEN.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWCCH.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       01  WSS-VARI.
           03 WSS-FEC-FVEN.
              05 WSS-NUM-AVEN       VALUE ZEROS   PIC 9(02).
              05 WSS-NUM-MVEN       VALUE ZEROS   PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-MPRO       VALUE ZEROS   PIC 9(02).
              05 WSS-NUM-APRO       VALUE ZEROS   PIC 9(02).
           03 WSS-FEC-FVAM.
              05 WSS-NUM-AVAM       VALUE ZEROS   PIC 9(02).
              05 WSS-NUM-MVAM       VALUE ZEROS   PIC 9(02).
      *DPM ini  3-OCT-1995
      *     03 WSS-COD-COOC         VALUE SPACES  PIC X(06).
           03 WSS-CNTD              VALUE 0       PIC 9(02).
           03 WSS-CMAX              VALUE 5       PIC 9(02).
           03 WSS-COD-COOC      OCCURS 5          PIC X(06).
      *DPM fin  3-OCT-1995
           03 WSS-COD-OFOC          VALUE SPACES  PIC X(03).
           03 WSS-SUM-VDES          VALUE ZEROS   PIC 9(09)V99.
           03 WSS-NUM-RLEI          VALUE ZEROS   PIC 9(09).
           03 WSS-NUM-RERR          VALUE ZEROS   PIC 9(09).
           03 WSS-NUM-RPRO          VALUE ZEROS   PIC 9(09).

       COPY GNSWGFEC.
       COPY GNSWGCPT.
       COPY GNSWGHOY.
       COPY COLBROPC.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY TABBROFI.
       COPY COLBRCCH.
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *>>>>

      ***** CRW-COMIENZO

      *REPORT SECTION.
      *--------------
      *COPY COLRFCCH.
      *    .
      *COPY COLRTCCH.

       01  RPT-VARIABLES.
           02 RPT-VARIABLES-VARIAS.
               03 RPT-VARI-LINEA              PIC 9(03) VALUE 61.
               03 RPT-VARI-PAGE-COUNTER       PIC 9(03).
           02 RPT-VARIABLES-CONTROL.
               03 RPT-CTRL-COD-OFIC           PIC X(03) VALUE SPACES.
               03 RPT-CTRL-COD-COOC           PIC X(06) VALUE SPACES.
           02 RPT-VARIABLES-TOTALES.
               03 RPT-TOT-VAL-SFIN            PIC 9(12)V9(04) VALUE 0.
               03 RPT-TOT-NUM-REG             PIC 9(05)       VALUE 0.

       01  PH-CCH.

           03  PH-CCH-01.
               05  RPT-HEAD-DUSU              PIC X(40).
               05  FILLER                     PIC X(082) VALUE SPACES.
               05  RPT-HEAD-FDMA              PIC 99B99B9999.

           03  PH-CCH-02.
               05  RPT-HEAD-DSIS              PIC X(40).
               05  FILLER                     PIC X(082) VALUE SPACES.
               05  FILLER                     PIC X(6)
                   VALUE 'PAGINA'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-PAGE-COUNTER      PIC ZZZ.

           03  PH-CCH-03.
               05  FILLER                     PIC X(037) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'LISTADO DE CREDITOS COMPLEMENTARIOS HIP'.
               05  FILLER                     PIC X(13)
                   VALUE 'OTECARIOS DEL'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-NUM-MVEN          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-HEAD-NUM-AVEN          PIC 9(2).

           03  PH-CCH-04.
               05  FILLER                     PIC X(132)
                   VALUE ALL '-'.

           03  PH-CCH-05.
               05  FILLER                     PIC X(39)
                   VALUE 'CLASE                                  '.
               05  FILLER                     PIC X(39)
                   VALUE '                          ---- NUMERO -'.
               05  FILLER                     PIC X(39)
                   VALUE '----   FECHA                           '.
               05  FILLER                     PIC X(10)
                   VALUE '     SALDO'.

           03  PH-CCH-06.
               05  FILLER                     PIC X(39)
                   VALUE 'OPER   SUC           N O M B R E       '.
               05  FILLER                     PIC X(39)
                   VALUE '                R.U.T.    OPERACION   C'.
               05  FILLER                     PIC X(39)
                   VALUE 'UOTA   VCTO             MONTO          '.
               05  FILLER                     PIC X(10)
                   VALUE '   CREDITO'.

           03  PH-CCH-07.
               05  FILLER                     PIC X(132)
                   VALUE ALL '-'.

       01  DL-CCH.

           03  DL-CCH-08.
               05  RPT-DETA-COD-COOC          PIC X(6).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-COD-OFOC          PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-GLS-NOMC          PIC X(40).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-NUM-ICLI          PIC X(8).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-VRF-ICLI          PIC X.
               05  FILLER                     PIC X(003) VALUE SPACES.
               05  RPT-DETA-CAI-IOPC          PIC X(4).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-IIC-IOPC          PIC X(8).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-NUM-NCUO          PIC ZZZ.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-NUM-DVEN          PIC X(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-NUM-MVEN          PIC X(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-NUM-SVEN          PIC X(2).
               05  RPT-DETA-NUM-AVEN          PIC X(2).
               05  RPT-DETA-VAL-SFIN          PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-VAL-SCRE          PIC ZZ.ZZZ.ZZZ.ZZ9,9999.

       01  CF-CCH-OPC-COD-OFOC.

           03  CF-CCH-09.
               05  FILLER                     PIC X(51)
                   VALUE ALL '-'.

           03  CF-CCH-10.
               05  FILLER                     PIC X(21)
                   VALUE 'NUMERO DE REGISTROS :'.
               05  FILLER                     PIC X(025) VALUE SPACES.
               05  RPT-CTRL-NUM-NUNO          PIC ZZZZZ.

           03  CF-CCH-11.
               05  FILLER                     PIC X(21)
                   VALUE 'TOTAL DESCUENTO     :'.
               05  FILLER                     PIC X(012) VALUE SPACES.
               05  RPT-CTRL-VAL-SFIN          PIC ---.---.---.--9,9999.
               05  RPT-CTRL-RESTO.
                   07  RPT-CTRL-FEC-FVEN      PIC 9(8).
                   07  RPT-CTRL-VAL-SCAP      PIC 99999999999,9999.
                   07  RPT-CTRL-VAL-SEGD      PIC 99999999999,9999.
                   07  RPT-CTRL-VAL-SEGP      PIC ZZZZZZZZ9,9999.

      ***** CRW-FINAL

       PROCEDURE DIVISION.
      *==================

      ***** CRW-COMIENZO
      *DECLARATIVES.
      ***** CRW-FINAL

       DEC-CF-CCH-OPC-COD-COOC SECTION.

      ***** CRW-COMIENZO
      *    USE BEFORE REPORTING CF-CCH-OPC-COD-COOC.
      ***** CRW-FINAL

       INI-CF-CCH-OPC-COD-COOC.
       FIN-CF-CCH-OPC-COD-COOC.
           EXIT.
       DEC-CF-CCH-OPC-COD-OFOC SECTION.

      ***** CRW-COMIENZO
      *    USE BEFORE REPORTING CF-CCH-OPC-COD-OFOC.
      ***** CRW-FINAL

       INI-CF-CCH-OPC-COD-OFOC.
       FIN-CF-CCH-OPC-COD-OFOC.
           EXIT.
       DEC-PH-CCH SECTION.

      ***** CRW-COMIENZO
      *    USE BEFORE REPORTING PH-CCH.
      ***** CRW-FINAL

       INI-PH-CCH.
       FIN-PH-CCH.
           EXIT.

      ***** CRW-COMIENZO
      *END DECLARATIVES.
      ***** CRW-FINAL

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPGCCH' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           SORT GNSSRT
       COPY COLRSCCH.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
      * Abrir Archivos input sort
           MOVE FIO-INP TO FIO-CMND.
      DPM ini  17-OCT-1995
           MOVE 'COL' TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      DPM fin  17-OCT-1995
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-RDC.
           PERFORM SGC-FIO-DBC.
      * Rutina para Capturar y Validar Parametros
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      *Captura Fecha de vencimiento
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FECHA INICIO (MMAA) : ' WSS-FEC-FPRO IN WSS-VARI.
      *Captura Clase Operacion
      *DPM ini  3-OCT-1995
      *     ACCEPT WSS-COD-COOC IN WSS-VARI.
      *     DISPLAY 'CLASE OPERACION     : ' WSS-COD-COOC IN WSS-VARI.
           MOVE 1 TO WSS-CNTD IN WSS-VARI.
       LUP-ACEPT-COOC.
           ACCEPT WSS-COD-COOC(WSS-CNTD).
           DISPLAY 'CLASE DE OPERACION  : ' WSS-COD-COOC(WSS-CNTD).
           IF WSS-COD-COOC(WSS-CNTD) = SPACES
               MOVE '*' TO WSS-COD-COOC(WSS-CNTD).
           IF WSS-COD-COOC(WSS-CNTD) NOT = '*' AND
              WSS-COD-COOC(WSS-CNTD) NOT = '999' AND
              WSS-CNTD IN WSS-VARI NOT > WSS-CMAX IN WSS-VARI
               ADD 1 TO WSS-CNTD IN WSS-VARI
               GO TO LUP-ACEPT-COOC.
      *DPM fin  3-OCT-1995
      *Captura Oficina
           ACCEPT WSS-COD-OFOC IN WSS-VARI.
           DISPLAY 'OFICINA             : ' WSS-COD-OFOC IN WSS-VARI.

           DISPLAY 'FIN CAPTURA DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de vencimiento
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FMA             TO FEC-FORM.
           MOVE FEC-FBLK-N               TO FEC-FBLK.
           MOVE FEC-CHOY-GE              TO FEC-CHOY.
           MOVE FEC-VHBL-N               TO FEC-VHBL.
           MOVE FEC-VAL-FEC              TO FEC-CMND.
           PERFORM PRO-FEC.

           IF NOT FEC-STAT-OKS
               DISPLAY 'FECHA DE VENCIMIENTO ' FEC-MENS
                PERFORM GNS-PRO-END.

           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MVEN IN WSS-VARI
                                            WSS-NUM-MVEN IN WSS-CCH.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AVEN IN WSS-VARI
                                            WSS-NUM-AVEN IN WSS-CCH.

      *VAL-TAB Valida Clase de Operacion en TABLAS
      *DPM ini  3-OCT-1995
      *     IF WSS-COD-COOC IN WSS-VARI NOT > SPACES
      *         MOVE '*' TO WSS-COD-COOC IN WSS-VARI.
      *     IF WSS-COD-COOC IN WSS-VARI NOT = '*'

           MOVE 1 TO WSS-CNTD IN WSS-VARI.

           IF WSS-COD-COOC(WSS-CNTD) NOT > SPACES OR
              WSS-COD-COOC(WSS-CNTD) = '999'
               MOVE '*' TO WSS-COD-COOC(WSS-CNTD).

       LUP-COOC.
           IF WSS-COD-COOC(WSS-CNTD) = '*' OR
              WSS-COD-COOC(WSS-CNTD) = '999' OR
              WSS-COD-COOC(WSS-CNTD) NOT > SPACES
               GO TO FIN-VAL-COOC.

           MOVE 'COL'                    TO TAB-COD-SIST.
           MOVE 'AUX'                    TO TAB-COD-TTAB IN TAB.
      *         MOVE WSS-COD-COOC IN WSS-VARI TO TAB-COD-CTAB IN TAB
           MOVE WSS-COD-COOC(WSS-CNTD) TO TAB-COD-CTAB IN TAB.
           PERFORM VAL-TAB.
           IF MSG-COD-MENS > SPACES
      *             DISPLAY 'CLASE DE OPERACION ' WSS-COD-COOC
               DISPLAY 'CLASE DE OPERACION ' WSS-COD-COOC(WSS-CNTD)
                       ' ' MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-END.

           ADD 1 TO WSS-CNTD IN WSS-VARI.
           IF WSS-CNTD IN WSS-VARI NOT > WSS-CMAX IN WSS-VARI AND
             WSS-COD-COOC(WSS-CNTD) NOT = '999' AND
             WSS-COD-COOC(WSS-CNTD) > SPACES
               GO TO LUP-COOC.

       FIN-VAL-COOC.
      *DPM fin  3-OCT-1995


      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY 'CODIGO OFICINA ' WSS-COD-OFOC
                           ' ' MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-VEN.
      *<<<< FIN_FST_INP
           PERFORM GET-FHOY.
           MOVE HOY-SHOY IN HOY-VARI     TO VEN-NUM-SVEN IN VEN
           MOVE WSS-NUM-AVEN IN WSS-VARI TO VEN-NUM-AVEN IN VEN
           MOVE WSS-NUM-MVEN IN WSS-VARI TO VEN-NUM-MVEN IN VEN
           MOVE ZEROS                    TO VEN-NUM-DVEN IN VEN
           MOVE 'VEN-FEC-FVEN'           TO FIO-AKEY
           MOVE FIO-GET-NLS              TO FIO-CMND
           PERFORM COL-FIO-VEN
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTEN VENCIMIENTOS PARA FECHA : '
                                               WSS-FEC-FPRO IN WSS-VARI
               PERFORM GNS-PRO-END.
      *>>>>
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Obtencion y seleccion de registros a procesar
           MOVE VEN-NUM-AVEN IN VEN TO WSS-NUM-AVAM IN WSS-VARI
           MOVE VEN-NUM-MVEN IN VEN TO WSS-NUM-MVAM IN WSS-VARI
      * Valida que VEN este en el mes y anno de parametro
           IF WSS-FEC-FVAM IN WSS-VARI > WSS-FEC-FVEN IN WSS-VARI
              GO TO FIN-INP-SRT.

           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.

      * Busca VEN en OPC
           MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE REGISTRO VEN EN OPC : ' VEN-CIC-IOPC
                                                                IN VEN
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-INP-SRT.

      * Valida que OPC este activada. Valida que el credito no este canc
           IF (OPC-MSC-STAT IN OPC NOT = 'A') OR
              (OPC-VAL-SCRE IN OPC NOT > ZEROS)
               GO TO NXT-INP-SRT.

      * Valida que clase de operacion corresponda a la de parametro
      *DPM ini 3-OCT-1995
            MOVE 1 TO WSS-CNTD IN WSS-VARI.
       LUP-CMP-COOC.
      *     IF WSS-COD-COOC IN WSS-VARI NOT = '*' AND
      *        WSS-COD-COOC IN WSS-VARI NOT = SPACES
      *         IF OPC-COD-COOC IN OPC NOT = WSS-COD-COOC IN WSS-VARI
      *             GO TO NXT-INP-SRT.
           IF WSS-CNTD IN WSS-VARI > WSS-CMAX IN WSS-VARI OR
              WSS-COD-COOC(WSS-CNTD) = '999'
                   GO TO NXT-INP-SRT.

           IF WSS-COD-COOC(WSS-CNTD) NOT = '*' AND
              WSS-COD-COOC(WSS-CNTD) NOT = SPACES
               IF OPC-COD-COOC IN OPC NOT = WSS-COD-COOC(WSS-CNTD)
                   ADD 1 TO WSS-CNTD IN WSS-VARI
                   GO TO LUP-CMP-COOC.

      *DPM fin 3-OCT-1995

      * Valida que oficina corresponda a la de parametro
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*' AND
              WSS-COD-OFOC IN WSS-VARI NOT = SPACES
               IF OPC-COD-OFOC IN OPC NOT = WSS-COD-OFOC IN WSS-VARI
                   GO TO NXT-INP-SRT.
      * RBG INI 12-DEC-1995
           MOVE OPC-VAL-SEGP IN OPC TO OPC-VAL-SEGP IN SRT
      * RBG FIN 12-DEC-1995
      * Busca OPC en RDC para obtener rut cliente
           MOVE OPC-CIC-IOPC IN OPC TO RDC-CIC-IOPC IN RDC
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC
           MOVE 'RDC-KEY-IDLC'      TO FIO-AKEY
           MOVE FIO-GET-KEY         TO FIO-CMND
           PERFORM COL-FIO-RDC
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE REGISTRO OPC EN RDC : ' RDC-KEY-IDLC
                                                                IN RDC
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-INP-SRT
           ELSE
      * Busca con RDC cliente en DBC
               MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC
               MOVE FIO-GET-KEY         TO FIO-CMND
               PERFORM SGC-FIO-DBC
               IF NOT FIO-STAT-OKS
                   DISPLAY 'NO EXISTE CLIENTE EN DBC : '
                                                     DBC-CIC-ICLI IN DBC
                   ADD 1 TO WSS-NUM-RERR IN WSS-VARI
                   GO TO NXT-INP-SRT.

           MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
           PERFORM CPT-BLKS
           MOVE CPT-STRN TO SRT-GLS-NOMC IN SRT

           ADD  VEN-VAL-SFIN IN VEN TO WSS-SUM-VDES IN WSS-VARI.

           IF OPC-IND-VSTR IN OPC = 'S'
               MOVE VEN-NUM-IVCT IN VEN TO SRT-NUM-NCUO IN SRT
           ELSE
               MOVE VEN-NUM-IVEN IN VEN TO SRT-NUM-NCUO IN SRT.

           MOVE 1                   TO SRT-NUM-NUNO IN SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMCCH.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
      *<<<< FIN_INP
      * Cerrar Archivos input sort
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-RDC.
           PERFORM SGC-FIO-DBC.
      *>>>>
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-VEN.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
      *<<<< INI_OUT
      * Abrir Archivos de salida y grabar
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-CCH.
      *>>>>
           OPEN OUTPUT RPTCCH.
      *     MOVE 'COLPGCCH' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.

      ***** CRW-COMIENZO
      *    INITIATE RPT-CCH.
      ***** CRW-FINAL

           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.

      ***** CRW-COMIENZO
           MOVE OPC-COD-OFOC IN SRT TO RPT-CTRL-COD-OFIC.
           MOVE OPC-COD-COOC IN SRT TO RPT-CTRL-COD-COOC.
      ***** CRW-FINAL

       LUP-OUT-SRT.
      *<<<< LUP_OUT
      * Obtencion de registros SORT
           MOVE DBC-NUM-ICLI IN SRT TO CCH-NUM-ICLI IN CCH.
           MOVE DBC-VRF-ICLI IN SRT TO CCH-VRF-ICLI IN CCH.

           IF OPC-COD-OFOC IN SRT NOT NUMERIC
               MOVE ZEROS TO CCH-COD-OFOC IN CCH
           ELSE
               MOVE OPC-COD-OFOC IN SRT TO CCH-COD-OFOC IN CCH.

           MOVE OPC-CAI-IOPC IN SRT TO CCH-CAI-IOPC IN CCH.
           MOVE OPC-IIC-IOPC IN SRT TO CCH-IIC-IOPC IN CCH.
           MOVE SRT-NUM-NCUO IN SRT TO CCH-NUM-IVEN IN CCH.
           MOVE VEN-VAL-SCAP IN SRT TO CCH-VAL-SCAP IN CCH.
           SUBTRACT VEN-VAL-SFIN IN SRT FROM
                    VEN-VAL-SCAP IN SRT GIVING
                    CCH-VAL-INTE IN CCH.
      *DAD ini  20-AUG-1991
           MOVE OPC-VAL-SEGD IN SRT TO CCH-VAL-SEGD IN CCH.
      *DAD fin  20-AUG-1991
           MOVE VEN-NUM-DVEN IN SRT TO CCH-NUM-DVEN IN CCH.
           MOVE VEN-NUM-MVEN IN SRT TO CCH-NUM-MVEN IN CCH.
           MOVE VEN-NUM-AVEN IN SRT TO CCH-NUM-AVEN IN CCH.
      * RBG INI 12-DEC-1995
           MOVE OPC-VAL-SEGP IN SRT TO CCH-VAL-SEGP IN CCH.
      * RBG FIN 12-DEC-1995
      *ALD-BCI-INI/30.01.1996
           MOVE OPC-COD-COOC IN SRT TO CCH-COD-COOC IN CCH.
           MOVE SPACES              TO CCH-GLS-DISP IN CCH.
      *ALD-BCI-FIN/30.01.1996
      *DAD ini  09-JUL-1993   (Memo.08-07-93)
           MOVE OPC-VAL-SCRE IN SRT TO CCH-VAL-SCRE IN CCH.
      *DAD fin  09-JUL-1993

           MOVE FIO-PUT TO FIO-CMND
           PERFORM COL-FIO-CCH
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL GRABAR CCH : ' CCH
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO NXT-OUT-SRT.
           ADD 1 TO WSS-NUM-RPRO IN WSS-VARI.
      *>>>>
       GEN-OUT-SRT.

      ***** CRW-COMIENZO
      *    GENERATE DL-CCH.

           MOVE RPT-DUSU                       TO RPT-HEAD-DUSU
           MOVE RPT-FDMA                       TO RPT-HEAD-FDMA
           MOVE RPT-DSIS                       TO RPT-HEAD-DSIS
           MOVE WSS-NUM-MVEN IN WSS-CCH        TO RPT-HEAD-NUM-MVEN
           MOVE WSS-NUM-AVEN IN WSS-CCH        TO RPT-HEAD-NUM-AVEN
           MOVE OPC-COD-COOC IN SRT            TO RPT-DETA-COD-COOC
           MOVE OPC-COD-OFOC IN SRT            TO RPT-DETA-COD-OFOC
           MOVE SRT-GLS-NOMC IN SRT            TO RPT-DETA-GLS-NOMC
           MOVE DBC-NUM-ICLI IN SRT            TO RPT-DETA-NUM-ICLI
           MOVE DBC-VRF-ICLI IN SRT            TO RPT-DETA-VRF-ICLI
           MOVE OPC-CAI-IOPC IN SRT            TO RPT-DETA-CAI-IOPC
           MOVE OPC-IIC-IOPC IN SRT            TO RPT-DETA-IIC-IOPC
           MOVE SRT-NUM-NCUO IN SRT            TO RPT-DETA-NUM-NCUO
           MOVE VEN-NUM-DVEN IN SRT            TO RPT-DETA-NUM-DVEN
           MOVE VEN-NUM-MVEN IN SRT            TO RPT-DETA-NUM-MVEN
           MOVE VEN-NUM-SVEN IN SRT            TO RPT-DETA-NUM-SVEN
           MOVE VEN-NUM-AVEN IN SRT            TO RPT-DETA-NUM-AVEN
           MOVE VEN-VAL-SFIN IN SRT            TO RPT-DETA-VAL-SFIN
           MOVE OPC-VAL-SCRE IN SRT            TO RPT-DETA-VAL-SCRE
           MOVE VEN-FEC-FVEN IN SRT            TO RPT-CTRL-FEC-FVEN
           MOVE VEN-VAL-SCAP IN SRT            TO RPT-CTRL-VAL-SCAP
           MOVE OPC-VAL-SEGD IN SRT            TO RPT-CTRL-VAL-SEGD
           MOVE OPC-VAL-SEGP IN SRT            TO RPT-CTRL-VAL-SEGP

           IF RPT-VARI-LINEA > 60 OR RPT-VARI-PAGE-COUNTER = 0 OR
              OPC-COD-OFOC IN SRT NOT = RPT-CTRL-COD-OFIC      OR
              OPC-COD-COOC IN SRT NOT = RPT-CTRL-COD-COOC
              IF OPC-COD-OFOC IN SRT NOT = RPT-CTRL-COD-OFIC   OR
                 OPC-COD-COOC IN SRT NOT = RPT-CTRL-COD-COOC
                 MOVE OPC-COD-OFOC IN SRT TO RPT-CTRL-COD-OFIC
                 MOVE OPC-COD-COOC IN SRT TO RPT-CTRL-COD-COOC
                 MOVE RPT-TOT-NUM-REG TO RPT-CTRL-NUM-NUNO
                 MOVE RPT-TOT-VAL-SFIN TO RPT-CTRL-VAL-SFIN
                 MOVE SPACES TO RPT-CTRL-RESTO
                 WRITE RPTCCH-REG FROM  CF-CCH-09
                 WRITE RPTCCH-REG FROM  CF-CCH-10
                 WRITE RPTCCH-REG FROM  CF-CCH-11
                 ADD 3 TO RPT-VARI-LINEA
                 MOVE 0 TO RPT-TOT-NUM-REG
                           RPT-TOT-VAL-SFIN
              END-IF
              ADD 1 TO RPT-VARI-PAGE-COUNTER
              MOVE RPT-VARI-PAGE-COUNTER TO RPT-HEAD-PAGE-COUNTER
              MOVE SPACES TO RPTCCH-REG
              WRITE RPTCCH-REG AFTER PAGE
              WRITE RPTCCH-REG FROM  PH-CCH-01
              WRITE RPTCCH-REG FROM  PH-CCH-02
              WRITE RPTCCH-REG FROM  PH-CCH-03 AFTER 2
              WRITE RPTCCH-REG FROM  PH-CCH-04
              WRITE RPTCCH-REG FROM  PH-CCH-05
              WRITE RPTCCH-REG FROM  PH-CCH-06
              WRITE RPTCCH-REG FROM  PH-CCH-07
              MOVE 10 TO RPT-VARI-LINEA
           END-IF.
           ADD  SRT-NUM-NUNO IN SRT            TO RPT-TOT-NUM-REG
           ADD  VEN-VAL-SFIN IN SRT            TO RPT-TOT-VAL-SFIN
           WRITE RPTCCH-REG FROM  DL-CCH-08.
           ADD 1 TO RPT-VARI-LINEA.

      ***** CRW-FINAL

       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      *<<<< FIN_OUT
      * Cerrar Archivos de salida
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-CCH.
           DISPLAY 'ESTADISTICAS DE PROCESO'.
           DISPLAY '============ == ======='.
           DISPLAY 'REGISTROS LEIDOS     : ' WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'REGISTROS PROCESADOS : ' WSS-NUM-RPRO IN WSS-VARI.
           DISPLAY 'REGISTROS ERRONEOS   : ' WSS-NUM-RERR IN WSS-VARI.
      *>>>>

      ***** CRW-COMIENZO
      *    TERMINATE RPT-CCH.
           MOVE RPT-TOT-NUM-REG TO RPT-CTRL-NUM-NUNO
           MOVE RPT-TOT-VAL-SFIN TO RPT-CTRL-VAL-SFIN
           MOVE SPACES TO RPT-CTRL-RESTO
           WRITE RPTCCH-REG FROM  CF-CCH-09
           WRITE RPTCCH-REG FROM  CF-CCH-10
           WRITE RPTCCH-REG FROM  CF-CCH-11
      ***** CRW-FINAL

           CLOSE RPTCCH.
       EXT-OUT-SRT.
           EXIT.
       COPY COLBFVEN.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
       COPY GNSBGFEC.

      ***** CRW-COMIENZO
       COPY GNSBPFEC.
      ***** CRW-FINAL

       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY GNSBBMSG.
      *DPM ini  9-OCT-1995
       COPY GNSBBTAB.
      *DPM fin  9-OCT-1995
       COPY GNSBVTAB.
       COPY TABBVOFI.
       COPY COLBFOPC.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY TABBFOFI.
       COPY COLBFCCH.
      *>>>>

