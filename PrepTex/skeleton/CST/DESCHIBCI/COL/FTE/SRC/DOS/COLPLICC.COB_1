       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLICC.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    30-Apr-93 09:06 PM.

      * Programa Listador de Reporte ICC con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTICC         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTICC         ASSIGN TO        UT-S-RPTICC.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTICC         ASSIGN TO        UT-S-RPTICC.
      *<<<< IOS
      *DOS SELECT RPTICR         ASSIGN TO SYS012-UR-1403-S.
      *MVS SELECT RPTICR         ASSIGN TO        UT-S-RPTICR.
           SELECT RPTICR         ASSIGN TO        UT-S-RPTICR.
      *>>>>

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRRICC.
       FD  RPTICC LABEL RECORDS OMITTED
           REPORT IS RPT-ICC
           .
      *<<<< FLS
       FD  RPTICR LABEL RECORDS OMITTED
           REPORT IS RPT-ICR.
       01  PASO.
           03 FILLER                              PIC X(01).
      *>>>>

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBROPC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWICC.
      *<<<< RW
       COPY COLRWICR.
      *>>>>
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY GNSBRMSC.
       COPY TABBRUSR.
       COPY TABBROFI.
       COPY TABBRTIO.
       COPY COLBRTOC.
       COPY COLBRCYA.
       COPY COLBRCAN.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       COPY COLWGCTO.

       01  WSS-VARI.
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-DTER                     PIC 9(02).
              05 WSS-NUM-MTER                     PIC 9(02).
              05 WSS-NUM-STER                     PIC 9(02).
              05 WSS-NUM-ATER                     PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                     PIC 9(02).
              05 WSS-NUM-AFEC                     PIC 9(02).
              05 WSS-NUM-MFEC                     PIC 9(02).
              05 WSS-NUM-DFEC                     PIC 9(02).
           03 WSS-FEC-FFET.
              05 WSS-NUM-SFET                     PIC 9(02).
              05 WSS-NUM-AFET                     PIC 9(02).
              05 WSS-NUM-MFET                     PIC 9(02).
              05 WSS-NUM-DFET                     PIC 9(02).
           03 WSS-FEC-FREG.
              05 WSS-NUM-SREG                     PIC 9(02).
              05 WSS-NUM-AREG                     PIC 9(02).
              05 WSS-NUM-MREG                     PIC 9(02).
              05 WSS-NUM-DREG                     PIC 9(02).
           03 WSS-COD-OFIC                        PIC X(03).
           03 WSS-IND-DMOV                        PIC X(05).
           03 WSS-IND-PROC                        PIC 9(01).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-GLS-DAT2.
              05 WSS-CIC-CCTB                     PIC X(08).
              05 FILLER                           PIC X(12).
      *DAD ini  25-FEB-1992   (Memo.18-07-91 pto.1.1)
           03 WSS-GLS-CCTB.
              05 WSS-GLS-DESC                     PIC X(40).
              05 WSS-GLS-DATA.
                 07 FILLER                        PIC X(59).
                 07 WSS-GLS-DVRF                  PIC X(01).
      *DAD fin  25-FEB-1992
           03 WSS-NUM-TDEB        VALUE ZEROES    PIC 9(03).
           03 WSS-NUM-THAB        VALUE ZEROES    PIC 9(03).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY COLRFICC.
           .
       COPY COLRTICC.
      *<<<< RT
       COPY COLRFICR.
           .
       COPY COLRTICR.
      *>>>>

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CF-ICC-SRT-COD-OFIC SECTION.
           USE BEFORE REPORTING CF-ICC-SRT-COD-OFIC.
       INI-CF-ICC-SRT-COD-OFIC.
      *<<<< CF_ICC_SRT_COD_OFIC
           MOVE ZEROES TO PAGE-COUNTER IN RPT-ICC.
      *>>>>
       FIN-CF-ICC-SRT-COD-OFIC.
           EXIT.
       DEC-CF-ICC-SRT-CIC-CCTB SECTION.
           USE BEFORE REPORTING CF-ICC-SRT-CIC-CCTB.
       INI-CF-ICC-SRT-CIC-CCTB.
      *<<<< CF_ICC_SRT_CIC_CCTB
           MOVE WSS-NUM-TDEB IN WSS-VARI TO WSS-NUM-TDEB IN WSS-ICC.
           MOVE WSS-NUM-THAB IN WSS-VARI TO WSS-NUM-THAB IN WSS-ICC.
           MOVE ZEROES TO WSS-NUM-TDEB IN WSS-VARI
                          WSS-NUM-THAB IN WSS-VARI.
      *>>>>
       FIN-CF-ICC-SRT-CIC-CCTB.
           EXIT.
       DEC-CF-ICC-OPC-COD-VCOC SECTION.
           USE BEFORE REPORTING CF-ICC-OPC-COD-VCOC.
       INI-CF-ICC-OPC-COD-VCOC.
       FIN-CF-ICC-OPC-COD-VCOC.
           EXIT.
       DEC-CF-ICC-SRT-FEC-FMOV SECTION.
           USE BEFORE REPORTING CF-ICC-SRT-FEC-FMOV.
       INI-CF-ICC-SRT-FEC-FMOV.
       FIN-CF-ICC-SRT-FEC-FMOV.
           EXIT.
       DEC-PH-ICC SECTION.
           USE BEFORE REPORTING PH-ICC.
       INI-PH-ICC.
      *<<<< PH_ICC
      * Mediante BUS-OFI se Obtine Nombre de la Oficina de Tabla OFI
           IF SRT-COD-OFIC IN SRT > SPACES
               MOVE SRT-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.

      * Mediante BUS-TAB Busca Cuenta Contable de Tabla PCC
           IF SRT-CIC-CCTB IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'PCC'               TO TAB-COD-TTAB IN TAB
               MOVE SRT-CIC-CCTB IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
      *DAD ini  25-FEB-1992   (Memo.18-07-91 pto.1.1)
               MOVE TAB-GLS-DESC IN TAB      TO WSS-GLS-CCTB IN WSS-ICC
      *         MOVE TAB-GLS-DESC IN TAB      TO WSS-GLS-DESC IN WSS-VAR
               MOVE TAB-GLS-DATA IN TAB      TO WSS-GLS-DATA IN WSS-VARI
      *         MOVE WSS-GLS-CCTB IN WSS-VARI TO WSS-GLS-CCTB IN WSS-ICC
               MOVE WSS-GLS-DVRF IN WSS-VARI TO WSS-GLS-DVRF IN WSS-ICC
           ELSE
               MOVE SPACES TO WSS-GLS-CCTB IN WSS-ICC
               MOVE SPACES TO WSS-GLS-DVRF IN WSS-ICC.
      *DAD fin  25-FEB-1992

      * Mediante BUS-TAB Busca Tipo de Moneda de Tabla VLR
           IF OPC-COD-VCOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO WSS-GLS-VCOC IN WSS-ICC
           ELSE
               MOVE SPACES TO WSS-GLS-VCOC IN WSS-ICC.

           IF SRT-IND-TITU IN SRT NOT = SPACES
               MOVE 'CUENTA' TO WSS-GLS-TITU IN WSS-ICC
           ELSE
               MOVE SPACES   TO WSS-GLS-TITU IN WSS-ICC.

           MOVE SRT-FEC-FMOV IN SRT      TO WSS-FEC-FREG IN WSS-VARI.
           MOVE WSS-NUM-DREG IN WSS-VARI TO WSS-NUM-DMOV IN WSS-ICC.
           MOVE WSS-NUM-MREG IN WSS-VARI TO WSS-NUM-MMOV IN WSS-ICC.
           MOVE WSS-NUM-SREG IN WSS-VARI TO WSS-NUM-SMOV IN WSS-ICC.
           MOVE WSS-NUM-AREG IN WSS-VARI TO WSS-NUM-AMOV IN WSS-ICC.
      *>>>>
       FIN-PH-ICC.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLICC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
      *<<<< INI
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMAAAA ) : '
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.
      *Captura Indicador Destino Movimientos
           DISPLAY 'DESTINO MOVIMIENTOS : '.
           ACCEPT  WSS-IND-DMOV IN WSS-VARI.
           DISPLAY WSS-IND-DMOV IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB'         TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-END.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-ICC
                                            WSS-NUM-DINI IN WSS-ICR
                                            WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-ICC
                                            WSS-NUM-MINI IN WSS-ICR
                                            WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-ICC
                                            WSS-NUM-SINI IN WSS-ICR
                                            WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-ICC
                                            WSS-NUM-AINI IN WSS-ICR
                                            WSS-NUM-AFEC IN WSS-VARI.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DTER IN WSS-ICC
                                            WSS-NUM-DTER IN WSS-ICR
                                            WSS-NUM-DFET IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MTER IN WSS-ICC
                                            WSS-NUM-MTER IN WSS-ICR
                                            WSS-NUM-MFET IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-STER IN WSS-ICC
                                            WSS-NUM-STER IN WSS-ICR
                                            WSS-NUM-SFET IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-ATER IN WSS-ICC
                                            WSS-NUM-ATER IN WSS-ICR
                                            WSS-NUM-AFET IN WSS-VARI.

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFIC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFIC IN WSS-VARI.
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFIC IN WSS-VARI
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

      *VAL-MSC Valida indicador ( miscelaneo ) en TABLAS
           IF WSS-IND-DMOV IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-IND-DMOV IN WSS-VARI.
           IF WSS-IND-DMOV IN WSS-VARI NOT = '*'
               DISPLAY WSS-IND-DMOV IN WSS-VARI
               MOVE 'TAB'                    TO MSC-COD-SIST
               MOVE 'TCYA'                   TO MSC-COD-TMSC IN MSC
               MOVE WSS-IND-DMOV IN WSS-VARI TO MSC-COD-CMSC IN MSC
               PERFORM VAL-MSC
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Proceso para Cargar las tablas  TAB/TIO  y  COL/TOC  en Memoria
           PERFORM COL-CRG-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
               PERFORM GNS-PRO-END.
      *>>>>
           SORT GNSSRT
       COPY COLRSICC.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       FST-INP-SRT.
      *<<<< INI_FST_INP
           MOVE WSS-FEC-FFEC IN WSS-VARI TO OPC-FEC-FINP IN OPC.
           MOVE 'OPC-FEC-FINP'           TO FIO-AKEY.
           MOVE FIO-GET-NLS              TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS OR
              OPC-FEC-FINP IN OPC > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS OPC EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI ' A '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-INP-SRT.
           MOVE 1 TO WSS-IND-PROC IN WSS-VARI.
           GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro OPC
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI > 0
               GO TO NXT-INP-SRT.

      * Busca los Abonos para Operacion de Credito Determinada
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE OPC-CIC-IOPC IN OPC TO CYA-CIC-IOPC IN CYA.
           MOVE ZEROES              TO CYA-NUM-ICAN IN CYA.
           MOVE FIO-GET-NLS  TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-CIC-IOPC IN CYA NOT = OPC-CIC-IOPC IN OPC OR
              CYA-NUM-ICAN IN CYA NOT = ZEROES
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    CYANX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   OPC-KEY-IOPC IN OPC
               GO TO NXT-INP-SRT.
       LUP-BUS-OPC-CYA.
           PERFORM BUS-SRT-REG.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-CIC-IOPC IN CYA NOT = OPC-CIC-IOPC IN OPC OR
              CYA-NUM-ICAN IN CYA NOT = ZEROES
               GO TO NXT-INP-SRT.
           GO TO LUP-BUS-OPC-CYA.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMICC.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
           MOVE 'OPC-FEC-FINP' TO FIO-AKEY.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OPC.
      *<<<< FIN_NXT_INP
           IF OPC-FEC-FINP IN OPC > WSS-FEC-FFET IN WSS-VARI
               GO TO FIN-INP-SRT.
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
      *<<<< FIN_INP
      * Programacion de Ciclo para Registro CAN
       INI-INP-SRT-CAN.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CAN-FEC-FOUT IN CAN.
           MOVE 'CAN-FEC-FOUT'           TO FIO-AKEY.
           MOVE FIO-GET-NLS              TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS OR
              CAN-FEC-FOUT IN CAN > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS CAN EN RANGO DE FECHAS : '
                       'DESDE ' WSS-FEC-FINI IN WSS-VARI ' A '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-INP-SRT-CAN.
           MOVE 2 TO WSS-IND-PROC IN WSS-VARI.
       LUP-INP-SRT-CAN.
      * Proceso de Seleccion y Clasificacion a Partir del Registro CAN
           IF NOT FIO-STAT-OKS
               GO TO FIN-INP-SRT-CAN.

      * Busca Valor de Cambio de Operacion de Credito Mediante Cancelaci
           MOVE CAN-CIC-IOPC IN CAN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CAN    OPCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   OPC-CIC-IOPC IN OPC
               GO TO NXT-INP-SRT-CAN.

      * Proceso de Seleccion y Clasificacion a Partir del Registro CAN
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI > 0
               GO TO NXT-INP-SRT-CAN.

      * Busca los Cargos para Cancelaciones de Operacion de Credito Dete
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE CAN-KEY-ICAN IN CAN TO CYA-KEY-ICAN IN CYA.
           MOVE FIO-GET-NLS  TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-KEY-ICAN IN CYA NOT = CAN-KEY-ICAN IN CAN
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CAN    CYANX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CAN-KEY-ICAN IN CAN
               GO TO NXT-INP-SRT-CAN.

      * Para Diferencia Valor Pagado con Informacion se forma y genera u
      * registro SRT
           IF CAN-SGV-DIFE IN CAN NOT = 0
               PERFORM GEN-DIF-VPAG.

      * Para reflejar la Cuenta Sucursales se forman y generan dos regis
           IF CAN-COD-OFCA IN CAN NOT = OPC-COD-OFOC IN OPC
               PERFORM GEN-CTA-SUCU.
       LUP-BUS-CAN-CYA.
           PERFORM BUS-SRT-REG.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-KEY-ICAN IN CYA NOT = CAN-KEY-ICAN IN CAN
               GO TO NXT-INP-SRT-CAN.
           GO TO LUP-BUS-CAN-CYA.
       NXT-INP-SRT-CAN.
           MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF CAN-FEC-FOUT IN CAN > WSS-FEC-FFET IN WSS-VARI
               GO TO FIN-INP-SRT-CAN.
           GO TO LUP-INP-SRT-CAN.
       FIN-INP-SRT-CAN.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-CAN.
      *>>>>
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTICC.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-ICC.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
           OPEN OUTPUT RPTICR.
           INITIATE RPT-ICR.
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
           PERFORM BUS-REG-CLI.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-ICC.
      *<<<< FIN_GEN_OUT
           IF SRT-VAL-DEBE IN SRT NOT = 0
               ADD 1 TO WSS-NUM-TDEB IN WSS-VARI.
           IF SRT-VAL-HABE IN SRT NOT = 0
               ADD 1 TO WSS-NUM-THAB IN WSS-VARI.

           GENERATE DL-ICR.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-ICC.
      *<<<< FIN_TMT
           TERMINATE RPT-ICR.
           CLOSE RPTICR.
      *>>>>
           CLOSE RPTICC.
       EXT-OUT-SRT.
           EXIT.

       COPY COLBFOPC.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      ******************************************************************
      * Seleccion del Registro para Proceso Mediante Oficina de Listado
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

      * Chequea el Estado de la Operacion de Credito o de la Cancelacion
           IF WSS-IND-PROC IN WSS-VARI = 1
               IF OPC-MSC-STAT IN OPC = 'A'
                   MOVE OPC-FEC-FINP IN OPC TO SRT-FEC-FMOV IN SRT
                   MOVE OPC-COD-OFOC IN OPC TO SRT-COD-OFIC IN SRT
               ELSE
                   GO TO FIN-SEL-REG-PRO
           ELSE
           IF WSS-IND-PROC IN WSS-VARI = 2
               IF CAN-MSC-STAT IN CAN = 'A' OR
                  CAN-MSC-STAT IN CAN = 'R'
                   MOVE CAN-FEC-FOUT IN CAN TO SRT-FEC-FMOV IN SRT
                   MOVE CAN-COD-OFCA IN CAN TO SRT-COD-OFIC IN SRT
               ELSE
                   GO TO FIN-SEL-REG-PRO.

      * Chequea Oficina del Parametro con Oficina de Credito o de Cancel
           IF WSS-COD-OFIC IN WSS-VARI NOT = '*'
               IF WSS-IND-PROC IN WSS-VARI = 1
                   IF WSS-COD-OFIC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                       GO TO FIN-SEL-REG-PRO
                   ELSE
                       NEXT SENTENCE
               ELSE
               IF WSS-IND-PROC IN WSS-VARI = 2
                   IF WSS-COD-OFIC IN WSS-VARI NOT = CAN-COD-OFCA IN CAN
                       GO TO FIN-SEL-REG-PRO.

      * Busca con el Codigo Tipo Operacion en la Tabla cargada en Memori
      * 'Sistema que lo Maneja'
           MOVE OPC-COD-TOOC IN OPC TO CTO-TOOC IN CTO-VARI.
           PERFORM COL-BUS-CTO.
           IF CTO-CMSG IN CTO-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE CTO-CMSG IN CTO-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG CTO-MEN2 IN CTO-VARI
                       'CON CODIGO : '     CTO-TOOC IN CTO-VARI
               GO TO FIN-SEL-REG-PRO.

      * Valida el 'Sistema que lo Maneja' de la Operacion de Credito
           IF CTO-SIST IN CTO-VARI NOT = 'COL'
               GO TO FIN-SEL-REG-PRO.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro SRT al Procesar Varios Regis
       BUS-SRT-REG SECTION.
       INI-BUS-SRT-REG.
           IF WSS-IND-DMOV IN WSS-VARI NOT = '*' AND
              WSS-IND-DMOV IN WSS-VARI NOT = CYA-IND-TCYA IN CYA
               GO TO FIN-BUS-SRT-REG.

           IF CYA-SGV-LIQU IN CYA = 0
               GO TO FIN-BUS-SRT-REG.

      *BUS-MSC busca Cuenta Contable e Ind Tiene Cuenta en Miscelaneo de
           MOVE 'TAB'               TO MSC-COD-SIST.
           MOVE 'TCYA'              TO MSC-COD-TMSC IN MSC.
           MOVE CYA-IND-TCYA IN CYA TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC  MSC-COD-TABL IN MSC
               GO TO FIN-BUS-SRT-REG.
           MOVE MSC-GLS-DAT2 IN MSC      TO WSS-GLS-DAT2 IN WSS-VARI.
           MOVE WSS-CIC-CCTB IN WSS-VARI TO SRT-CIC-CCTB IN SRT.
           MOVE MSC-COD-DAT1 IN MSC(1)   TO SRT-IND-TITU IN SRT.

      * Complementa Formacion del Registro SRT
           MOVE WSS-IND-PROC IN WSS-VARI TO SRT-IND-TMOV IN SRT.
           IF WSS-IND-PROC IN WSS-VARI = 1
               IF CYA-SGV-LIQU IN CYA > 0
                   MOVE CYA-SGV-LIQU IN CYA TO SRT-VAL-HABE IN SRT
                   MOVE ZEROES              TO SRT-VAL-DEBE IN SRT
               ELSE
                   MOVE CYA-SGV-LIQU IN CYA TO SRT-VAL-DEBE IN SRT
                   MOVE ZEROES              TO SRT-VAL-HABE IN SRT
           ELSE
           IF WSS-IND-PROC IN WSS-VARI = 2
               IF CYA-SGV-LIQU IN CYA > 0
                   MOVE CYA-SGV-LIQU IN CYA TO SRT-VAL-DEBE IN SRT
                   MOVE ZEROES              TO SRT-VAL-HABE IN SRT
               ELSE
                   MOVE CYA-SGV-LIQU IN CYA TO SRT-VAL-HABE IN SRT
                   MOVE ZEROES              TO SRT-VAL-DEBE IN SRT.

      * Completa Llenado de Campos y Genera el Registro SRT
           COPY COLRMICC.
           RELEASE SRT.
       FIN-BUS-SRT-REG.
           EXIT.
      ******************************************************************
      * Para Diferencia Valor Pagado de CAN con Informacion se forma y g
      * registro SRT
       GEN-DIF-VPAG SECTION.
       INI-GEN-DIF-VPAG.
      *BUS-MSC busca Cuenta Contable en Miscelaneo de TABLAS
           MOVE 'TAB'  TO MSC-COD-SIST.
           MOVE 'TCYA' TO MSC-COD-TMSC IN MSC.
           IF CAN-SGV-DIFE IN CAN > 0
               MOVE 'DSVP' TO MSC-COD-CMSC IN MSC
           ELSE
           IF CAN-SGV-DIFE IN CAN < 0
               MOVE 'DFVP' TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC  MSC-COD-TABL IN MSC
               GO TO FIN-GEN-DIF-VPAG.
           MOVE MSC-GLS-DAT2 IN MSC      TO WSS-GLS-DAT2 IN WSS-VARI.
           MOVE WSS-CIC-CCTB IN WSS-VARI TO SRT-CIC-CCTB IN SRT.

      * Complementa Formacion del Registro SRT
           MOVE WSS-IND-PROC IN WSS-VARI TO SRT-IND-TMOV IN SRT.
           IF CAN-SGV-DIFE IN CAN > 0
               MOVE CAN-SGV-DIFE IN CAN TO SRT-VAL-HABE IN SRT
               MOVE ZEROES              TO SRT-VAL-DEBE IN SRT
           ELSE
           IF CAN-SGV-DIFE IN CAN < 0
               MOVE CAN-SGV-DIFE IN CAN TO SRT-VAL-DEBE IN SRT
               MOVE ZEROES              TO SRT-VAL-HABE IN SRT.

      * Completa Llenado de Campos y Genera el Registro SRT
           MOVE CAN-CAI-IOPC IN CAN TO CYA-CAI-IOPC IN SRT.
           MOVE CAN-IIC-IOPC IN CAN TO CYA-IIC-IOPC IN SRT.
           MOVE CAN-NUM-ICAN IN CAN TO CYA-NUM-ICAN IN SRT.
           MOVE SPACES TO CYA-CAI-ICYA IN SRT
                          CYA-IIC-ICYA IN SRT
                          CYA-IND-TCYA IN SRT
                          CYA-IND-MAUT IN SRT
                          SRT-IND-TITU IN SRT.
           RELEASE SRT.
       FIN-GEN-DIF-VPAG.
           EXIT.
      ******************************************************************
      * Para reflejar la Cuenta Sucursales se forman y generan dos regis
       GEN-CTA-SUCU SECTION.
       INI-GEN-CTA-SUCU.
      *BUS-MSC busca Cuenta Contable en Miscelaneo de TABLAS
           MOVE 'TAB'  TO MSC-COD-SIST.
           MOVE 'TCYA' TO MSC-COD-TMSC IN MSC.
           MOVE 'MSUC' TO MSC-COD-CMSC IN MSC.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC  MSC-COD-TABL IN MSC
               GO TO FIN-GEN-CTA-SUCU.
           MOVE MSC-GLS-DAT2 IN MSC      TO WSS-GLS-DAT2 IN WSS-VARI.
           MOVE WSS-CIC-CCTB IN WSS-VARI TO SRT-CIC-CCTB IN SRT.

      * Complementa Formacion del Registro SRT
           MOVE WSS-IND-PROC IN WSS-VARI TO SRT-IND-TMOV IN SRT.
           MOVE CAN-CAI-IOPC IN CAN      TO CYA-CAI-IOPC IN SRT.
           MOVE CAN-IIC-IOPC IN CAN      TO CYA-IIC-IOPC IN SRT.
           MOVE CAN-NUM-ICAN IN CAN      TO CYA-NUM-ICAN IN SRT.
           MOVE SPACES                   TO CYA-CAI-ICYA IN SRT
                                            CYA-IIC-ICYA IN SRT
                                            CYA-IND-TCYA IN SRT
                                            CYA-IND-MAUT IN SRT
                                            SRT-IND-TITU IN SRT.

      * Completa Llenado de Campos y Genera el Registro SRT del OPC
           MOVE OPC-COD-OFOC IN OPC TO SRT-COD-OFIC IN SRT.
           MOVE CYA-SGV-LIQU IN CYA TO SRT-VAL-DEBE IN SRT.
           MOVE ZEROES              TO SRT-VAL-HABE IN SRT.
           RELEASE SRT.

      * Completa Llenado de Campos y Genera el Registro SRT del CAN
           MOVE CAN-COD-OFCA IN CAN TO SRT-COD-OFIC IN SRT.
           MOVE CYA-SGV-LIQU IN CYA TO SRT-VAL-HABE IN SRT.
           MOVE ZEROES              TO SRT-VAL-DEBE IN SRT.
           RELEASE SRT.
       FIN-GEN-CTA-SUCU.
           EXIT.
      ******************************************************************
      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro
       BUS-REG-CLI SECTION.
       INI-BUS-REG-CLI.
      * Busca CIC del Cliente con el CIC de Operaciones de Creditos en R
      * RDC-NUM-IDLC = 0 ====> DEU.DIR.
           MOVE CYA-CAI-IOPC IN SRT TO RDC-CAI-IOPC IN RDC.
           MOVE CYA-IIC-IOPC IN SRT TO RDC-IIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    RDCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RDC-KEY-IDLC IN RDC.
      *        GO TO FIN-BUS-REG-CLI.

      *CIC-CLI Busca Datos del Cliente Mediante su CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS > SPACES
               MOVE SPACES TO DBC-NUM-ICLI IN DBC
                              DBC-VRF-ICLI IN DBC
                              DBC-IND-ICLI IN DBC
                              WSS-GLS-AUXI IN WSS-ICC
                              DBC-GLS-ICLI IN DBC
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC
               IF DBC-GLS-ICLI IN DBC > SPACES
                   MOVE '-'    TO WSS-GLS-AUXI IN WSS-ICC
               ELSE
                   MOVE SPACES TO WSS-GLS-AUXI IN WSS-ICC.
       FIN-BUS-REG-CLI.
           EXIT.
      ******************************************************************

       COPY GNSBFMSC.
       COPY TABBFUSR.
       COPY TABBFOFI.
       COPY COLBFTOC.
       COPY COLBFCYA.
       COPY COLBFCAN.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY TABBBUSR.
       COPY TABBBOFI.
       COPY GNSBVMSC.
       COPY TABBVOFI.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY COLBGCTO.
       COPY SGCBGCLI.
      *>>>>
