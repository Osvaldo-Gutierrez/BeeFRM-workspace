       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPLOCR.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    30-Apr-93 09:17 PM.

      * Programa Listador de Reporte OCR con Sort.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTOCR         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTOCR         ASSIGN TO        UT-S-RPTOCR.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
           SELECT RPTOCR         ASSIGN TO        UT-S-RPTOCR.

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY COLRROCR.
       FD  RPTOCR LABEL RECORDS OMITTED
           REPORT IS RPT-OCR
           .

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CCR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBRCCR.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY COLRWOCR.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY TABBROFI.
       COPY TABBRCAM.
       COPY COLBROPC.
       COPY COLBRDLC.
       COPY COLBRRDC.
       COPY SGCBRDBC.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
       COPY GNSWVPPH.
       01  WSS-VARI.
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI                     PIC 9(02).
              05 WSS-NUM-MINI                     PIC 9(02).
              05 WSS-NUM-SINI                     PIC 9(02).
              05 WSS-NUM-AINI                     PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-DTER                     PIC 9(02).
              05 WSS-NUM-MTER                     PIC 9(02).
              05 WSS-NUM-STER                     PIC 9(02).
              05 WSS-NUM-ATER                     PIC 9(02).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC                     PIC 9(02).
              05 WSS-NUM-AFEC                     PIC 9(02).
              05 WSS-NUM-MFEC                     PIC 9(02).
              05 WSS-NUM-DFEC                     PIC 9(02).
           03 WSS-FEC-FFET.
              05 WSS-NUM-SFET                     PIC 9(02).
              05 WSS-NUM-AFET                     PIC 9(02).
              05 WSS-NUM-MFET                     PIC 9(02).
              05 WSS-NUM-DFET                     PIC 9(02).
           03 WSS-FEC-FREG.
              05 WSS-NUM-SREG                     PIC 9(02).
              05 WSS-NUM-AREG                     PIC 9(02).
              05 WSS-NUM-MREG                     PIC 9(02).
              05 WSS-NUM-DREG                     PIC 9(02).
           03 WSS-COD-OFOC                        PIC X(03).
           03 WSS-COD-PESO                        PIC X(06).
           03 WSS-IND-NEXT                        PIC 9(01).
           03 WSS-IND-MNAC                        PIC 9(01).
           03 WSS-VAL-MNAC      VALUE ZEROES      PIC 9(11).
           03 WSS-VAL-SDLC      VALUE ZEROES      PIC 9(11)V9(04).
           03 WSS-COD-COOC.
              05 WSS-COD-TOOC                     PIC X(03).
              05 WSS-COD-AOOC                     PIC X(03).
           03 WSS-GLS-TIAU.
              05 WSS-GLS-TIO                      PIC X(05).
              05 WSS-GLS-AUX                      PIC X(05).
      *>>>>
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *>>>>

       REPORT SECTION.
      *--------------
       COPY COLRFOCR.
           .
       COPY COLRTOCR.

       PROCEDURE DIVISION.
      *==================
       DECLARATIVES.
       DEC-CH-OCR-OPC-COD-OFOC SECTION.
           USE BEFORE REPORTING CH-OCR-OPC-COD-OFOC.
       INI-CH-OCR-OPC-COD-OFOC.
      *<<<< CH_OCR_OPC_COD_OFOC
           MOVE ZEROES TO PAGE-COUNTER.
      *>>>>
       FIN-CH-OCR-OPC-COD-OFOC.
           EXIT.
       DEC-CH-OCR-CCR-FEC-FCCR SECTION.
           USE BEFORE REPORTING CH-OCR-CCR-FEC-FCCR.
       INI-CH-OCR-CCR-FEC-FCCR.
      *<<<< CH_OCR_CCR_FEC_FCCR
      *>>>>
       FIN-CH-OCR-CCR-FEC-FCCR.
           EXIT.
       DEC-CH-OCR-OPC-MSC-TVOC SECTION.
           USE BEFORE REPORTING CH-OCR-OPC-MSC-TVOC.
       INI-CH-OCR-OPC-MSC-TVOC.
      *<<<< CH_OCR_OPC_MSC_TVOC
      *>>>>
       FIN-CH-OCR-OPC-MSC-TVOC.
           EXIT.
       DEC-CF-OCR-OPC-MSC-TVOC SECTION.
           USE BEFORE REPORTING CF-OCR-OPC-MSC-TVOC.
       INI-CF-OCR-OPC-MSC-TVOC.
      *<<<< CF_OCR_OPC_MSC_TVOC
      *>>>>
       FIN-CF-OCR-OPC-MSC-TVOC.
           EXIT.
       DEC-PH-OCR SECTION.
           USE BEFORE REPORTING PH-OCR.
       INI-PH-OCR.
      *<<<< PH_OCR
      * Omite impresion de la primera pagina al controlar cortes con CH
       COPY GNSBGPPH.

      * Mediante BUS-OFI se Obtiene Nombre de la Oficina de Tabla OFI
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.
      *>>>>
       FIN-PH-OCR.
           EXIT.
       END DECLARATIVES.

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'COLPLOCR' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           SORT GNSSRT
       COPY COLRSOCR.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
      *<<<< INI_INP
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.

      *Captura Fecha de Inicio
           DISPLAY 'FECHA DE INICIO ( DDMMAAAA ) : '.
           ACCEPT  WSS-FEC-FINI IN WSS-VARI.
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
      *Captura Fecha de Termino
           DISPLAY 'FECHA DE TERMINO ( DDMMAAAA ) : '
           ACCEPT  WSS-FEC-FTER IN WSS-VARI.
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
      *Captura Codigo de Oficina
           DISPLAY 'OFICINA : '.
           ACCEPT  WSS-COD-OFOC IN WSS-VARI.
           DISPLAY WSS-COD-OFOC IN WSS-VARI.

           DISPLAY 'FIN    CAPTURA    DE PARAMETROS'.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      *VAL-FEC Valida Fecha de Inicio
           DISPLAY WSS-FEC-FINI IN WSS-VARI.
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *VAL-FEC Valida Fecha de Termino
           DISPLAY WSS-FEC-FTER IN WSS-VARI.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

      *CMP-FEC Compara Fecha de Inicio con Fecha de Termino
           MOVE WSS-FEC-FINI IN WSS-VARI TO FEC-FEC1.
           MOVE WSS-FEC-FTER IN WSS-VARI TO FEC-FEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-CMP-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-GRT
               MOVE 'TAB'         TO MSG-COD-SIST
               MOVE 'FECFINIFTER' TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG
               PERFORM GNS-PRO-END.

           MOVE WSS-NUM-DINI IN WSS-VARI TO WSS-NUM-DINI IN WSS-OCR
                                            WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MINI IN WSS-VARI TO WSS-NUM-MINI IN WSS-OCR
                                            WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SINI IN WSS-VARI TO WSS-NUM-SINI IN WSS-OCR
                                            WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-AINI IN WSS-VARI TO WSS-NUM-AINI IN WSS-OCR
                                            WSS-NUM-AFEC IN WSS-VARI.
           MOVE WSS-NUM-DTER IN WSS-VARI TO WSS-NUM-DTER IN WSS-OCR
                                            WSS-NUM-DFET IN WSS-VARI.
           MOVE WSS-NUM-MTER IN WSS-VARI TO WSS-NUM-MTER IN WSS-OCR
                                            WSS-NUM-MFET IN WSS-VARI.
           MOVE WSS-NUM-STER IN WSS-VARI TO WSS-NUM-STER IN WSS-OCR
                                            WSS-NUM-SFET IN WSS-VARI.
           MOVE WSS-NUM-ATER IN WSS-VARI TO WSS-NUM-ATER IN WSS-OCR
                                            WSS-NUM-AFET IN WSS-VARI.

      *VAL-OFI Valida codigo de Oficina en TABLAS
           IF WSS-COD-OFOC IN WSS-VARI NOT > SPACES
               MOVE '*' TO WSS-COD-OFOC IN WSS-VARI.
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               DISPLAY WSS-COD-OFOC IN WSS-VARI
               MOVE WSS-COD-OFOC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-GLS-DESC IN MSG
                   PERFORM GNS-PRO-END.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.

      * Obtiene el Codigo Valor de Cambio de Pesos mediante el CIC de la
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'          TO MSG-COD-SIST
               MOVE 'CIC0999  NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   TAB-CIC-CTAB
               PERFORM GNS-PRO-END.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
      *>>>>
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CCR.
       FST-INP-SRT.
      *<<<< INI_FST_INP
      * Inicio de Ciclo Proceso para Registro CCR
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CCR-FEC-FCCR IN CCR.
           MOVE 'CCR-FEC-FCCR' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CCR.
           IF NOT FIO-STAT-OKS OR
              CCR-FEC-FCCR IN CCR > WSS-FEC-FFET IN WSS-VARI
               DISPLAY 'NO HAY REGISTROS EN RANGO DE FECHAS DESDE : '
                       WSS-FEC-FINI IN WSS-VARI ' A '
                       WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-INP-SRT.
           GO TO LUP-INP-SRT.
      *>>>>
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-CCR.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
      * Proceso de Seleccion y Clasificacion a Partir del Registro CCR
           PERFORM SEL-REG-PRO.
           IF WSS-IND-NEXT IN WSS-VARI = 1
               GO TO NXT-INP-SRT.

      * Busca el Documento Legal Credito del Cambio Situacion Cartera
           MOVE CCR-KEY-IDLC IN CCR TO DLC-KEY-IDLC IN DLC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CCR    DLCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CCR-KEY-IDLC IN CCR
               GO TO NXT-INP-SRT.

      * Completa formacion y generacion del registro SRT
           PERFORM BUS-SRT-REG.

           GO TO NXT-INP-SRT.
      *>>>>
       MOV-INP-SRT.
       COPY COLRMOCR.
           RELEASE SRT.
       NXT-INP-SRT.
      *<<<< INI_NXT_INP
           MOVE 'CCR-FEC-FCCR' TO FIO-AKEY.
      *>>>>
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CCR.
      *<<<< FIN_NXT_INP
           IF CCR-FEC-FCCR IN CCR > WSS-FEC-FFET IN WSS-VARI
               GO TO FIN-INP-SRT.
      *>>>>
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-CCR.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTOCR.
           CALL 'GNSPLHDR' USING RPT-VARI.
           INITIATE RPT-OCR.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
      *<<<< FST_OUT
      * Busca glosas de campos en Lineas de Titulos pues con CH no estan
      * la primera pagina
      * Mediante BUS-OFI se Obtiene Nombre de la Oficina de Tabla OFI
           IF OPC-COD-OFOC IN SRT > SPACES
               MOVE OPC-COD-OFOC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI.
      *>>>>
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
           PERFORM GEN-LIN-OUT.
      *>>>>
       GEN-OUT-SRT.
           GENERATE DL-OCR.
      *<<<< FIN_GEN_OUT
      * Fecha Cambio Situacion Cartera
           MOVE CCR-FEC-FCCR IN SRT      TO WSS-FEC-FREG IN WSS-VARI.
           MOVE WSS-NUM-DREG IN WSS-VARI TO WSS-NUM-DCCR IN WSS-OCR.
           MOVE WSS-NUM-MREG IN WSS-VARI TO WSS-NUM-MCCR IN WSS-OCR.
           MOVE WSS-NUM-SREG IN WSS-VARI TO WSS-NUM-SCCR IN WSS-OCR.
           MOVE WSS-NUM-AREG IN WSS-VARI TO WSS-NUM-ACCR IN WSS-OCR.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
           TERMINATE RPT-OCR.
           CLOSE RPTOCR.
       EXT-OUT-SRT.
           EXIT.

       COPY COLBFCCR.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      ******************************************************************
      * Seleccion del registro para Proceso mediante Oficina, Ejecutivo
      * Situacion Cartera
       SEL-REG-PRO SECTION.
       INI-SEL-REG-PRO.
           MOVE  1  TO WSS-IND-NEXT IN WSS-VARI.

      * Busca la Operacion de Credito del Cambio Situacion Cartera
           MOVE CCR-CIC-IOPC IN CCR TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CCR    OPCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CCR-CIC-IOPC IN CCR
               GO TO FIN-SEL-REG-PRO.

      * Proceso de Seleccion a partir del registro OPC
           IF WSS-COD-OFOC IN WSS-VARI NOT = '*'
               IF WSS-COD-OFOC IN WSS-VARI NOT = OPC-COD-OFOC IN OPC
                   GO TO FIN-SEL-REG-PRO.

           MOVE  0  TO WSS-IND-NEXT IN WSS-VARI.
       FIN-SEL-REG-PRO.
           EXIT.
      ******************************************************************
      * Completa formacion y generacion del registro SRT
       BUS-SRT-REG SECTION.
       INI-BUS-SRT-REG.
      * Obtiene Valor de Cambio en Pesos para Moneda Nacional
           MOVE  0  TO WSS-IND-MNAC IN WSS-VARI.
           IF OPC-MSC-TVOC IN OPC = '0' AND
              OPC-COD-VCOC IN OPC NOT = WSS-COD-PESO IN WSS-VARI
               PERFORM DET-VAL-PESO
               IF WSS-IND-MNAC IN WSS-VARI = 0
                   GO TO FIN-BUS-SRT-REG.

      * Para Moneda Nacional distinta de Pesos se transforma valor a pes
           IF WSS-IND-MNAC IN WSS-VARI = 1
               MULTIPLY DLC-VAL-SDLC IN DLC BY CAM-SGV-VCAM IN CAM
                              GIVING WSS-VAL-MNAC IN WSS-VARI ROUNDED
               MOVE WSS-VAL-MNAC IN WSS-VARI TO WSS-VAL-SDLC IN WSS-VARI
           ELSE
               MOVE DLC-VAL-SDLC IN DLC TO WSS-VAL-SDLC IN WSS-VARI.

      * Llena monto del campo Valor Traspasado del reporte
           MOVE WSS-VAL-SDLC IN WSS-VARI TO SRT-VAL-VTRA IN SRT.

      * Completa Llenado de Campos y Genera el Registro SRT
           COPY COLRMOCR.
           RELEASE SRT.
       FIN-BUS-SRT-REG.
           EXIT.
      ******************************************************************
      * Obtiene Valor de Cambio en Pesos para Moneda Nacional a Fecha Tr
        DET-VAL-PESO SECTION.
        INI-DET-VAL-PESO.
           MOVE CCR-FEC-FCCR IN CCR      TO CAM-FEC-FCAM IN CAM(1).
           MOVE CCR-FEC-FCCR IN CCR      TO CAM-FEC-FCAM IN CAM(2).
           MOVE OPC-COD-VCOC IN OPC      TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE VALOR DE CAMBIO A FECHA TRASPASO : '
                       CCR-FEC-FCCR IN CCR ' PARA ' OPC-COD-VCOC IN OPC
                       ' EN OPERACION ' OPC-CIC-IOPC IN OPC
               GO TO FIN-DET-VAL-PESO
           ELSE
               IF CAM-IND-VIGE NOT = 'S'
                  DISPLAY 'IND. VIGENCIA NO VALIDO A FECHA TRASPASO : '
                       CCR-FEC-FCCR IN CCR ' PARA ' OPC-COD-VCOC IN OPC
                          ' EN OPERACION ' OPC-CIC-IOPC IN OPC
                  GO TO FIN-DET-VAL-PESO
               ELSE
                   MOVE  1  TO WSS-IND-MNAC IN WSS-VARI.
        FIN-DET-VAL-PESO.
           EXIT.
      ******************************************************************
      * Genera Lineas de Detalle a Partir de Registro SRT Ordenado
       GEN-LIN-OUT SECTION.
       INI-GEN-LIN-OUT.
      * Se redefine campo COOC e inicializa glosa de trabajo
           MOVE OPC-COD-COOC IN SRT TO WSS-COD-COOC IN WSS-VARI.
           MOVE SPACES              TO WSS-GLS-TIAU IN WSS-VARI.

      * Mediante BUS-TAB busca la glosa del Tipo de Operacion Credito en
      * tabla TIO
           IF WSS-COD-TOOC IN WSS-VARI > SPACES
               MOVE 'TAB'                    TO TAB-COD-SIST
               MOVE 'TIO'                    TO TAB-COD-TTAB IN TAB
               MOVE WSS-COD-TOOC IN WSS-VARI TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-TIO IN WSS-VARI.

      * Mediante BUS-TAB busca la glosa de la Clase de Operacion Credito
      * tabla AUX
           IF OPC-COD-COOC IN SRT > SPACES
               MOVE 'COL'               TO TAB-COD-SIST
               MOVE 'AUX'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-COOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-AUX IN WSS-VARI.

      * Se concatenan las glosas de tablas TIO y AUX para desplegarlas j
           MOVE WSS-GLS-TIAU IN WSS-VARI TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO WSS-GLS-TIAU IN WSS-OCR.

      * Mediante BUS-TAB Busca Tipo de Moneda de Tabla VLR
           IF OPC-COD-VCOC IN SRT > SPACES
               MOVE 'TAB'               TO TAB-COD-SIST
               MOVE 'VLR'               TO TAB-COD-TTAB IN TAB
               MOVE OPC-COD-VCOC IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-ABRV IN TAB TO WSS-GLS-VCOC IN WSS-OCR
           ELSE
               MOVE SPACES TO WSS-GLS-VCOC IN WSS-OCR.

      * Para Situacion Cartera en blanco mover VIGente
           IF CCR-IND-SCRT IN SRT = ' '
               MOVE 'VIG' TO CCR-IND-SCRT IN SRT.
           IF CCR-IND-SCRA IN SRT = ' '
               MOVE 'VIG' TO CCR-IND-SCRA IN SRT.

      * Para Situacion Contable en blanco mover VIGente
           IF DLC-IND-SCTB IN SRT = ' '
               MOVE 'VIG' TO DLC-IND-SCTB IN SRT.

      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro
           PERFORM BUS-REG-CLI.
       FIN-GEN-LIN-OUT.
           EXIT.
      ******************************************************************
      * Busca Datos Cliente por Medio del CIC de Operaciones en Registro
       BUS-REG-CLI SECTION.
       INI-BUS-REG-CLI.
      * Busca CIC del Cliente con el CIC de Operaciones de Creditos en R
      * RDC-NUM-IDLC > 0 ====> DEU.IND.
      * RDC-NUM-IDLC = 0 ====> DEU.DIR.
           MOVE CCR-CAI-IOPC IN SRT TO RDC-CAI-IOPC IN RDC.
           MOVE CCR-IIC-IOPC IN SRT TO RDC-IIC-IOPC IN RDC.
      *    MOVE                     TO RDC-NUM-IDLC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE 'RDC-KEY-IDLC'      TO FIO-AKEY.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'OPC    RDCNX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   RDC-KEY-IDLC IN RDC.
      *        GO TO FIN-BUS-REG-CLI.

      *CIC-CLI Busca Datos del Cliente Mediante su CIC
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY  TO FIO-CMND.
           PERFORM GET-CLI.
           IF MSG-COD-MENS > SPACES
               MOVE SPACES TO DBC-NUM-ICLI IN DBC
                              DBC-VRF-ICLI IN DBC
                              DBC-IND-ICLI IN DBC
                              WSS-GLS-AUXI IN WSS-OCR
                              DBC-GLS-ICLI IN DBC
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC
               IF DBC-GLS-ICLI IN DBC > SPACES
                   MOVE '-'    TO WSS-GLS-AUXI IN WSS-OCR
               ELSE
                   MOVE SPACES TO WSS-GLS-AUXI IN WSS-OCR.
       FIN-BUS-REG-CLI.
           EXIT.
      ******************************************************************

       COPY TABBFOFI.
       COPY TABBFCAM.
       COPY COLBFOPC.
       COPY COLBFDLC.
       COPY COLBFRDC.
       COPY SGCBFDBC.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY TABBBOFI.
       COPY TABBVOFI.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
       COPY SGCBGCLI.
      *>>>>
