       IDENTIFICATION DIVISION.
      *************************
       PROGRAM-ID.     COLPB840.
      *AUTHOR.         DONOSO JARA OSVALDO.
      *                DATE-WRITTEN,  16/03/94.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 30 DE MARZO DE 1998                                    *
      ******************************************************************
      ******************************************************************
      *  Sistema  : Colocaciones                                       *
      *  Subsist. :                                                    *
      *  Analista : Nunez C. Sergio                                    *
      *  Objetivo : Actualizar maestro de certificados modificando la  *
      *             fecha de vcto. de los certificados vigentes cuyos  *
      *             creditos sean cursados en el dia de proceso. Esto  *
      *             posibilitara que se reversen contablemente las ctas*
      *             de orden asociadas al tipo de certificado corresp. *
      *  Archivos :                                                    *
      *   Entrada : OPC 160: Tabla de operaciones (curses)             *
      *                                                                *
      *   Salida  :                                                    *
      *                                                                *
      *   Ent/Sal : SECMCER: Maestro de Certificados de sistema SEC    *
      *                                                                *
      *   Trabajo :                                                    *
      *                                                                *
      *  Mantenciones                                                  *
      *  ============                                                  *
      *            Programador: ...........................            *
      *            Fecha      : dd/mm/aa                               *
      *            Cambio     : ....................................   *
      *                         ....................................   *
      *                         ....................................   *
      *                                                                *
      ******************************************************************
           EJECT
       ENVIRONMENT DIVISION.
      **********************
       CONFIGURATION SECTION.
      *======================
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
           EJECT
       INPUT-OUTPUT SECTION.
      *=====================
       FILE-CONTROL.
           SELECT SECMCER ASSIGN TO SECMCER
                  ORGANIZATION         IS INDEXED
                  ACCESS MODE          IS DYNAMIC
                  RECORD KEY           IS CER-CLV-FORM
                  FILE STATUS          IS WR-FS-SECMCER WR-FS-SECMCER2.
           EJECT
       DATA DIVISION.
      *==============
           SKIP3
       FILE SECTION.
      *=============
           SKIP1
       FD  SECMCER
           RECORD  CONTAINS 200 CHARACTERS.
       01  CER.
           05 CER-CLV-FORM.
      *    Numero de RUT
              10 CER-NUM-RUTC              PIC 9(08).
      *    Digito Verificador de RUT
              10 CER-VRF-RUTC              PIC X(01).
      *    numero del certificado
              10 CER-NUM-CERTIF            PIC 9(06).
      *
      *    Numero Unico de Formulario
           05 CER-COD-CLV.
              10 CER-FILLER             PIC X(02).
              10 CER-COD-CSTO           PIC 9(04).
           05 CER-NUM-FORM              PIC 9(06).
      *
      *    Numero de Evento Contable
           05 CER-NUM-EVEN              PIC 9(05).
              88 CER-NUM-EVEN-OK        VALUE 07201 07202 07203 07204.
      *
      *    Codigo de Ingreso/Egreso
           05 CER-COD-INEG              PIC X(01).
      *
      *    Codigo de Sistema Destino
           05 CER-COD-SISD              PIC X(03).
      *
      *    Codigo de titulo de Campo 1
           05 CER-COD-TCP1              PIC 9(03).
      *
      *    Codigo de titulo de Campo 2
           05 CER-COD-TCP2              PIC 9(03).
      *
      *    TIPO CERTIFICADO
           05 CER-COD-CCP1              PIC X(04).
      *
      *    Valor de Campo 2
           05 CER-COD-CCP2              PIC X(04).
      *
      *    Codigo de titulo de Monto 1
           05 CER-COD-TMT1              PIC 9(03).
      *
      *    Codigo de titulo de Monto 2
           05 CER-COD-TMT2              PIC 9(03).
      *
      *    Codigo de titulo de Monto 3
           05 CER-COD-TMT3              PIC 9(03).
      *
      *    VALOR DE MONTO 1
           05 CER-NUM-VMT1              PIC 9(11)V9(02) COMP-3.
      *
      *    Valor de Monto 2
           05 CER-NUM-VMT2              PIC 9(11)V9(02) COMP-3.
      *
      *    Valor de Monto 3
           05 CER-NUM-VMT3              PIC 9(11)V9(02) COMP-3.
      *
      *
      *    Numero de Cuenta Corriente
           05 CER-NUM-CCTE              PIC 9(08).
      *
      *    Glosa de Formulario
           05 CER-GLS-GLSA              PIC X(40).
      *
      *    Fecha de Creacion (DDMMSSAA)
           05 CER-FEC-CREA.
              10 CER-FEC-CRDD           PIC 9(02).
              10 CER-FEC-CRMM           PIC 9(02).
              10 CER-FEC-CRSS           PIC 9(02).
              10 CER-FEC-CRAA           PIC 9(02).
      *
      *    Hora de Creacion
           05 CER-HOR-CREA              PIC 9(06).
      *
      *    Numero de Terminalista
           05 CER-COD-USUA              PIC X(12).
      *
      *    Codigo de Cancelacion
           05 CER-COD-CANC              PIC X(01).
      *
      *    Codigo de Oficina Origen
           05 CER-OFI-ORIG              PIC 9(03).
      *
      *    Codigo de Oficina Destino
           05 CER-OFI-DEST              PIC 9(03).
      *
      *    Codigo de Reserva
           05 CER-COD-REVE              PIC X(01).
      *
      *    Centro de Costo
           05 CER-NUM-CCOS              PIC X(04).
      *
      *    Codigo de Moneda
           05 CER-COD-MONE              PIC 9(03).
      *
      *    Codigo de Compania DBS
           05 CER-COD-CPIA              PIC X(04).
      *
      *    Codigo de Sistema Origen
           05 CER-COD-SISO              PIC X(03).
      *
      *    Codigo de Generacion de Contabilidad
           05 CER-COD-GCON              PIC X(01).
      *
      *    Codigo de Uso de Cuenta Corriente
           05 CER-COD-UCCT              PIC X(01).
      *
      *    Codigo de Funcion de Cuenta Corriente
           05 CER-COD-FCCT              PIC X(05).
      *
      *    Codigo de Cuenta Corriente
           05 CER-COD-CCTE              PIC X(03).
      *
      *    Codigo de Sumarizacion
           05 CER-COD-SUMA              PIC X(01).
      *
      *    Funcion de Cuenta de Cuadre
           05 CER-COD-CCDR.
              10 CER-FECHA-VEN.
                 15 CER-FECHA-DD        PIC 99.
                 15 CER-FECHA-MM        PIC 99.
                 15 CER-FECHA-AA        PIC 99.
              10 FILLER                 PIC X(6).
      *
      *    Estado del registro V o F.
           05 CER-ESTADO                PIC X(01).
      *
      *
           SKIP1
       WORKING-STORAGE SECTION.
      *========================
       SKIP1
       01  W-SWITCHES.
           05 WS-PROCESO                    PIC 9(01) VALUE 0.
              88 WS-PROCESO-OK       VALUE 0.
              88 WS-PROCESO-ERROR    VALUE 1.
              88 WS-PROCESO-WARNING  VALUE 2.
       SKIP1
       01  W-PARAMETROS.
           05 FILLER                        PIC X(20).
           05 WP-FECHAS.
              10 WP-FECHA-PRO.
                 15 WP-DIA-PRO              PIC 9(02).
                 15 FILLER                  PIC X(01).
                 15 WP-MES-PRO              PIC 9(02).
                 15 FILLER                  PIC X(01).
                 15 WP-ANO-PRO              PIC 9(04).
           05 FILLER                        PIC X(50).
       SKIP1
       01  W-CONTADORES-Y-ACUMULADORES.
           05 WA-L-OPC                      PIC 9(06) VALUE 0.
           05 WA-L-OPC-1                    PIC 9(06) VALUE 0.
           05 WA-G-CER                      PIC 9(06) VALUE 0.
           05 WA-N-CER                      PIC 9(06) VALUE 0.
       SKIP1
       01  W-REGISTROS-DE-TRABAJO.
           05 WR-FS-SECMCER2.
              10 WR-FS-SECMCER2-RC          PIC 9(04) COMP-4.
              10 WR-FS-SECMCER2-FC          PIC 9(04) COMP-4.
              10 WR-FS-SECMCER2-KC          PIC 9(04) COMP-4.
           05 WR-FS-SECMCER                 PIC 9(02) VALUE 0.
           05 WR-FECHA-HOY.
              10 WR-HOY-AA                  PIC X(02).
              10 WR-HOY-MM                  PIC X(02).
              10 WR-HOY-DD                  PIC X(02).
           05 WR-FECHA-PRO.
              10 WP-ANO-PRO                 PIC X(04).
              10 WP-MES-PRO                 PIC X(02).
              10 WP-DIA-PRO                 PIC X(02).
           05 WR-MENSAJES.
              10 WR-SECCION                 PIC X(35).
              10 WR-PARRAFO                 PIC X(35).
              10 WR-ARCHIVO                 PIC X(30).
              10 WR-OPERACI                 PIC X(30).
              10 WR-FILE-ST                 PIC X(02).
              10 WR-FILE-RC                 PIC 9(03).
              10 WR-FILE-FC                 PIC 9(03).
              10 WR-FILE-KC                 PIC 9(03).
              10 WR-MENSAJE                 PIC X(50).
           05 WR-PARAM-RUTUFECH.
              10 WR-RUTU-FECHA.
                 15 WR-RUTU-ANO             PIC X(04).
                 15 WR-RUTU-MES             PIC X(02).
                 15 WR-RUTU-DIA             PIC X(02).
              10 WR-RUTU-SN                 PIC X.
              10 WR-RUTU-HABANT             PIC 9(08) VALUE 0.
              10 WR-RUTU-HABPOS             PIC 9(08) VALUE 0.
              10 WR-RUTU-CODRET             PIC 9     VALUE 0.
      ******************************************************************
      *      AREAS DE USO PARA DATACOM                                 *
      ******************************************************************
      *
       01  WB-UIB.
           05 FILLER                        PIC X(32) VALUE 'COLPB840'.
      *
       01  WB-REQ-OPC-AREA.
           02 WB-REQ-OPC-CMD                PIC X(5) VALUE 'REDKY'.
           02 FILLER                        PIC X(3) VALUE 'OPC'.
           02 WB-REQ-OPC-NKY                PIC X(5) VALUE 'OPC03'.
           02 WB-REQ-OPC-RC                 PIC X(2) VALUE SPACES.
           02 FILLER                        PIC X(1).
           02 FILLER                        PIC S9(4) COMP VALUE +160.
           02 FILLER                        PIC X(58).
           02 WB-REQ-OPC-KEY-VALUE          PIC X(12).
      *
       01  WB-REQ-RDC-AREA.
           02 WB-REQ-RDC-CMD                PIC X(5) VALUE 'REDKY'.
           02 FILLER                        PIC X(3) VALUE 'RDC'.
           02 WB-REQ-RDC-NKY                PIC X(5) VALUE 'RDC01'.
           02 WB-REQ-RDC-RC                 PIC X(2) VALUE SPACES.
           02 FILLER                        PIC X(1).
           02 FILLER                        PIC S9(4) COMP VALUE +160.
           02 FILLER                        PIC X(58).
           02 WB-REQ-RDC-KEY-VALUE.
              03 WB-REQ-RDC-KEY-OPC         PIC X(12).
              03 WB-REQ-RDC-KEY-NUM         PIC 9(03).
      *
       01  WB-REQ-DBC-AREA.
           02 WB-REQ-DBC-CMD                PIC X(5) VALUE 'REDKY'.
           02 FILLER                        PIC X(3) VALUE 'DBC'.
           02 WB-REQ-DBC-NKY                PIC X(5) VALUE 'DBC00'.
           02 WB-REQ-DBC-RC                 PIC X(2) VALUE SPACES.
           02 FILLER                        PIC X(1).
           02 FILLER                        PIC S9(4) COMP VALUE +100.
           02 FILLER                        PIC X(58).
           02 WB-REQ-DBC-KEY-VALUE          PIC X(12).
      *
      *    Operacion Credito
      *    -----------------
      *
      *    Nombre Registro : OPC
      *    Clave(s)        : OPC-KEY-IOPC(UU), OPC-FEC-FCOL(DC),
      *                      OPC-FEC-FING(DC), OPC-FEC-FINP(DC)
      *    Largo           : 245
      *    Bloqueo         : 2
      *    Observaciones   :
      *
      *    01) Modificaciones 06/09/88, LJV
      *        - Eliminacion Clave OPC-STP-ITRN
      *    02) Modificaciones 11/10/89, ALD
      *        - Cambio nombre campo OPC-NUM-IEVC
      *          por  OPC-NUM-IVEN
      *        - Ingreso campo OPC-IND-VSTR
      *    03) Modificacion  30/06/93,  DAD
      *        - Modificado Largo/Bloqueo del registro, de 218/4 a 245/2
      *          aumentando la Glosa Disponible.
      *    04) Modificacion  06/09/95,  ALD
      *        - Ingreso Campo OPC-VAL-SEGP
      *        - Modificado Glosa Disponible de 28 a 20
      *
       01  OPC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OPC-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  OPC-KEY-IREG.
      *
      *KEY     Identificacion Operacion Credito
               05  OPC-KEY-IOPC.
      *
      *            Cod. Int. Comp. Operacion Credito
                   07  OPC-CIC-IOPC.
      *
      *                Centro Asignacion Identificacion
                       09  OPC-CAI-IOPC.
                           11  OPC-COD-TOPC             PIC X(01).
                           11  OPC-COD-COPC             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  OPC-IIC-IOPC.
                           11  OPC-COD-OOPC.
                               13  OPC-COD-SOPC         PIC X(02).
                               13  OPC-IND-SOPC         PIC X(01).
                           11  OPC-GLS-NOPC             PIC X(05).
      *
      *        Time Stamp Transaccion
               05  OPC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  OPC-FEC-FTRN.
                       09  OPC-NUM-STRN                 PIC 9(02).
                       09  OPC-NUM-ATRN                 PIC 9(02).
                       09  OPC-NUM-MTRN                 PIC 9(02).
                       09  OPC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  OPC-HRA-HRTR.
                       09  OPC-NUM-HHTR                 PIC 9(02).
                       09  OPC-NUM-MMTR                 PIC 9(02).
                       09  OPC-NUM-SSTR                 PIC 9(02).
      *
      *REQ Indicador Tipo Accion
           03  OPC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  OPC-MSC-STAT                             PIC X(01).
               88  OPC-INGRESADA               VALUE 'I'.
               88  OPC-MODIFICADA              VALUE 'M'.
               88  OPC-APROBADA-CTC            VALUE 'F'.
               88  OPC-LIQUIDADA-CTC           VALUE 'K'.
               88  OPC-LIQUIDADA-LIQ           VALUE 'L'.
               88  OPC-ACTIVADA                VALUE 'A'.
               88  OPC-ESPERA-OPC              VALUE 'C'.
               88  OPC-ESPERA-ENVIO-ABONO-DIF  VALUE 'B'.
               88  OPC-ESPERA-ENVIO-ABONO      VALUE 'G'.
               88  OPC-ESPERA-CONF-ABONO       VALUE 'H'.
               88  OPC-ESPERA-ABONO-X          VALUE 'X'.
               88  OPC-SUSPENDIDA              VALUE 'S'.
               88  OPC-DESACTIVADA             VALUE 'D'.
               88  OPC-ACTUALIZADA             VALUE 'I' 'M'.
               88  OPC-PENDIENTE-CTC           VALUE 'F' 'K'.
               88  OPC-LIQUIDADA               VALUE 'L' 'K'.
               88  OPC-ESPERA-ABONO            VALUE 'B' 'G' 'H' 'X'.
      * RBG INI 10-APR-1996
               88  OPC-APROBADA-APR            VALUE 'J'.
      * RBG FIN 10-APR-1996
      *
      *REQ Codigo Origen Transaccion
           03  OPC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  OPC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  OPC-COD-TTRN                         PIC X(03).
      *
      *REQ Autor Transaccion
           03  OPC-COD-ATRN                             PIC X(12).
      *
      *REQ Valor de Cambio Operacion Credito            TAB VLR
           03  OPC-COD-VCOC.
      *
      *        Miscelaneo Tipo Valor                    TAB TVAL
               05  OPC-MSC-TVOC                         PIC X(01).
      *
      *        Codigo Valor
               05  OPC-COD-CVOC                         PIC X(03).
      *
      *        Codigo Tipo Cambio
               05  OPC-COD-TCOC                         PIC X(02).
      *
      *REQ Codigo Clase Operacion Credito               COL AUX
           03  OPC-COD-COOC.
      *
      *        Codigo Tipo Operacion                    COL TOC
               05  OPC-COD-TOOC                         PIC X(03).
      *
      *        Codigo Auxiliar Operacion
               05  OPC-COD-AOOC                         PIC X(03).
      *
      *REQ Codigo Oficina Operacion Credito             TAB OFI
           03  OPC-COD-OFOC                             PIC X(03).
      *
      *    Responsable Mantencion Operacion Credito     TAB USR
           03  OPC-COD-RMOC                             PIC X(12).
      *
      *    Codigo Destino Operacion Credito             COL DES
           03  OPC-COD-CDOC                             PIC X(03).
      *
      *    Cod. Int. Comp. Linea de Credito
           03  OPC-CIC-ILDC.
      *
      *        Centro Asignacion Identificacion
               05  OPC-CAI-ILDC.
                   07  OPC-COD-TLDC                     PIC X(01).
                   07  OPC-COD-CLDC                     PIC X(03).
      *
      *        Ident. Interna Computacional
               05  OPC-IIC-ILDC.
                   07  OPC-COD-OLDC.
                       09  OPC-COD-SLDC                 PIC X(02).
                       09  OPC-IND-SLDC                 PIC X(01).
                   07  OPC-GLS-NLDC                     PIC X(05).
      *
      *REQ Fecha Curse o Colocacion
           03  OPC-FEC-FCOL.
               05  OPC-NUM-SCOL                         PIC 9(02).
               05  OPC-NUM-ACOL                         PIC 9(02).
               05  OPC-NUM-MCOL                         PIC 9(02).
               05  OPC-NUM-DCOL                         PIC 9(02).
      *
      *REQ Fecha Curse Contable
           03  OPC-FEC-FING.
               05  OPC-NUM-SING                         PIC 9(02).
               05  OPC-NUM-AING                         PIC 9(02).
               05  OPC-NUM-MING                         PIC 9(02).
               05  OPC-NUM-DING                         PIC 9(02).
      *
      *REQ Fecha Curse Computacional
           03  OPC-FEC-FINP.
               05  OPC-NUM-SINP                         PIC 9(02).
               05  OPC-NUM-AINP                         PIC 9(02).
               05  OPC-NUM-MINP                         PIC 9(02).
               05  OPC-NUM-DINP                         PIC 9(02).
      *
      *REQ Monto Credito
           03  OPC-VAL-CRED             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Saldo Valor Credito K Capital Insoluto
           03  OPC-VAL-SCRE             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Analisis Feriados (S/N)                      COL AFER
           03  OPC-IND-AFER                             PIC X(01).
      *
      *REQ Modalidad Calculo Valores Finales            COL MCVF
           03  OPC-MSC-MCVF                             PIC X(01).
      *
      *REQ Tipo Calculo Vencimientos                    COL TCVE
           03  OPC-MSC-TCVE.
      *
      *        Base Calculo Credito                     COL BCCR
               05  OPC-MSC-BCCR                         PIC X(01).
      *
      *        Valores Informados Credito               COL VICR
               05  OPC-MSC-VICR                         PIC X(01).
      *
      *    Tipo Renovacion                              COL RENO
           03  OPC-IND-RENO                             PIC X(03).
      *
      *    Condicion Garantia                           COL GARA
           03  OPC-IND-GARA                             PIC X(03).
      *
      *    Valor Comision
           03  OPC-SGV-VCOM             COMP-3          PIC S9(11)V9(4).
      *
      *    Valor Gastos
           03  OPC-VAL-GAST             COMP-3          PIC S9(11)V9(4).
      *
      *    Codigo de Seguro Desgravamen                 TAB IVB
           03  OPC-COD-SEGD.
               05  OPC-COD-ISEG                         PIC X(03).
               05  OPC-COD-CSEG                         PIC X(03).
               05  OPC-COD-SSEG                         PIC X(03).
      *
      *    Valor Seguro
           03  OPC-VAL-SEGD             COMP-3          PIC 9(11)V9(04).
      *
      *    Valor Liquido
           03  OPC-SGV-LIQU             COMP-3          PIC S9(11)V9(4).
      *
      *MOD-02
      *REQ Numero Proximo Vencimiento
           03  OPC-NUM-IVEN                             PIC 9(03).
      *
      *REQ Numero Vencimientos Originales
           03  OPC-NUM-VCOR                             PIC 9(03).
      *
      *REQ Numero Vencimientos Vigentes
           03  OPC-NUM-VCVG                             PIC 9(03).
      *
      *    Numero Venctos Cancelados en forma Oportuna
           03  OPC-NUM-VCCO                             PIC 9(03).
      *
      *    Numero Vencimientos Cancelados Morosos
           03  OPC-NUM-VCCM                             PIC 9(03).
      *
      *    Numero de Vencimientos Cancelados Vencidos
           03  OPC-NUM-VCCV                             PIC 9(03).
      *
      *REQ Numero Total Vencimientos Cancelados
           03  OPC-NUM-TVCC                             PIC 9(03).
      *
      *ING-02
      *    Indicador Vencimientos Estructurados
           03  OPC-IND-VSTR                             PIC X(01).
      *
      *    Cod. Int. Comp. Credito Enlace
           03  OPC-CIC-ICEN.
      *
      *        Centro Asignacion Identificacion
               05  OPC-CAI-ICEN.
                   07  OPC-COD-TCEN                     PIC X(01).
                   07  OPC-COD-CCEN                     PIC X(03).
      *
      *        Ident. Interna Computacional
               05  OPC-IIC-ICEN.
                   07  OPC-COD-OCEN.
                       09  OPC-COD-SCEN                 PIC X(02).
                       09  OPC-IND-SCEN                 PIC X(01).
                   07  OPC-GLS-NCEN                     PIC X(05).
      *
      *    Codigo Area Gestion                          COL GES
           03  OPC-COD-AGES                             PIC X(02).
      *
      *    Flag para activar sin contabilizar
           03 OPC-IND-CTBL                              PIC X(01).
      *
      *    Indicador Insistencia
           03 OPC-COD-INSI                              PIC X(01).
      *
      *    Canal de Venta
           03 OPC-COD-CVTA                              PIC X(01).
      *
      *MOD-04
      *    Valor Seguro
           03  OPC-VAL-SEGP             COMP-3          PIC 9(11)V9(04).
      *
      *MOD-04
      *    Glosa Disponible
           03  OPC-GLS-DISP                             PIC X(20).
      *    RELACION DEUDOR CREDITO
      *    -----------------------
      *
      *    NOMBRE REGISTRO : RDC
      *    CLAVE(S)        : RDC-KEY-IRDC(UU), RDC-KEY-IDLC(DU)
      *    LARGO           : 075
      *    BLOQUEO         : 1
      *    OBSERVACIONES   :
      *
      *    01) MODIFICACIONES 06/09/88, LJV
      *        - ELIMINACION CLAVE RDC-KEY-IREG
      *        - ESTANDARIZACION A HEADER TIPO LOG
      *        - MODIFICACION LARGO/BLOQUEO REGISTRO (064/1)
      *        - INCORPORACION CAMPO RDC-COD-TRDC
      *
       01  RDC.
      *
      *    FILLER COMPATIBILIDAD VSAM/MVS
           03  RDC-GLS-FLAG                             PIC X(01).
      *
      *    CLAVE PRIMARIA REGISTRO
           03  RDC-KEY-IREG.
      *
      *KEY     IDENTIFICACION RELACION DEUDOR CREDITO
               05  RDC-KEY-IRDC.
      *
      *            COD. INT. COMP. CLIENTE
                   07  RDC-CIC-ICLI.
      *
      *                CENTRO ASIGNACION IDENTIFICACION
                       09  RDC-CAI-ICLI.
                           11  RDC-COD-TCLI             PIC X(01).
                           11  RDC-COD-CCLI             PIC X(03).
      *
      *                IDENT. INTERNA COMPUTACIONAL
                       09  RDC-IIC-ICLI                 PIC X(08).
      *
      *            IDENTIFICACION DOCUMENTO LEGAL
                   07  RDC-KEY-IDLC.
      *
      *                COD. INT. COMP. OPERACION CURSADA
                       09  RDC-CIC-IOPC.
      *
      *                    CENTRO ASIGNACION IDENTIFICACION
                           11  RDC-CAI-IOPC.
                               13  RDC-COD-TOPC         PIC X(01).
                               13  RDC-COD-COPC         PIC X(03).
      *
      *                    IDENT. INTERNA COMPUTACIONAL
                           11  RDC-IIC-IOPC.
                               13  RDC-COD-OOPC.
                                   15  RDC-COD-SOPC     PIC X(02).
                                   15  RDC-IND-SOPC     PIC X(01).
                               13  RDC-GLS-NOPC         PIC X(05).
      *
      *                NUM IDENTIFICACION DOCUMENTO LEGAL
                       09  RDC-NUM-IDLC                 PIC 9(03).
      *
      *ING-01
      *        CODIGO TIPO RELACION                     COL RDC
               05  RDC-COD-TRDC                         PIC X(03).
      *
      *        TIME STAMP TRANSACCION
               05  RDC-STP-ITRN.
      *
      *            FECHA TRANSACCION
                   07  RDC-FEC-FTRN.
                       09  RDC-NUM-STRN                 PIC 9(02).
                       09  RDC-NUM-ATRN                 PIC 9(02).
                       09  RDC-NUM-MTRN                 PIC 9(02).
                       09  RDC-NUM-DTRN                 PIC 9(02).
      *
      *            HORA TRANSACCION
                   07  RDC-HRA-HRTR.
                       09  RDC-NUM-HHTR                 PIC 9(02).
                       09  RDC-NUM-MMTR                 PIC 9(02).
                       09  RDC-NUM-SSTR                 PIC 9(02).
      *
      *REQ INDICADOR TIPO ACCION
           03  RDC-MSC-TACC                             PIC X(01).
      *
      *    INDICADOR STATUS
           03  RDC-MSC-STAT                             PIC X(01).
      *
      *REQ CODIGO ORIGEN TRANSACCION
           03  RDC-COD-OTRN.
      *
      *        INDICADOR TIPO ORIGEN
               05  RDC-MSC-TTRN                         PIC X(01).
      *
      *        CODIGO TIPO ORIGEN
               05  RDC-COD-TTRN                         PIC X(03).
      *
      *REQ AUTOR TRANSACCION
           03  RDC-COD-ATRN                             PIC X(12).
      *
      *PEN DIRECCION ASOCIADA
           03  RDC-NUM-DIRE                             PIC X(02).
      *
      *REQ MONTO LIMITE DEUDA ( PORCENTAJE )
           03  RDC-VAL-LIMD             COMP            PIC 9(03)V9(06).
      *
      *MOD-01  PIC X(11)
      *    GLOSA DISPONIBLE
           03  RDC-GLS-DISP                             PIC X(06).
      *    Datos Basicos del Cliente
      *    -------------------------
      *
      *    Nombre Registro : DBC
      *    Clave(s)        : DBC-KEY-IDBC(UU), DBC-STP-ITRN(UU),DBC-IDC-
      *                      DBC-GLS-NOMC(DC), DBC-SNX-NOMC(DC)
      *    Largo           : 241
      *    Bloqueo         : 1
      *    Observaciones   :
      *
       01  DBC.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  DBC-GLS-FLAG                             PIC X(01).
      *
      *KEY Clave Primaria Registro
           03  DBC-KEY-IREG.
      *
      *        Identificacion Datos Basicos Clientes
               05  DBC-KEY-IDBC.
      *
      *            Cod. Int. Comp. Cliente
                   07  DBC-CIC-ICLI.
      *
      *                Centro Asignacion Identificacion
                       09  DBC-CAI-ICLI.
                           11  DBC-COD-TCLI             PIC X(01).
                           11  DBC-COD-CCLI             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  DBC-IIC-ICLI                 PIC X(08).
      *
      *        Time Stamp Transaccion
               05  DBC-STP-ITRN.
      *
      *            Fecha Transaccion
                   07  DBC-FEC-FTRN.
                       09  DBC-NUM-STRN                 PIC 9(02).
                       09  DBC-NUM-ATRN                 PIC 9(02).
                       09  DBC-NUM-MTRN                 PIC 9(02).
                       09  DBC-NUM-DTRN                 PIC 9(02).
      *
      *            Hora Transaccion
                   07  DBC-HRA-HRTR.
                       09  DBC-NUM-HHTR                 PIC 9(02).
                       09  DBC-NUM-MMTR                 PIC 9(02).
                       09  DBC-NUM-SSTR                 PIC 9(02).
      *
      *    Indicador Tipo Accion
           03  DBC-MSC-TACC                             PIC X(01).
      *
      *    Indicador Status
           03  DBC-MSC-STAT                             PIC X(01).
      *
      *    Codigo Origen Transaccion
           03  DBC-COD-OTRN.
      *
      *        Indicador Tipo Origen
               05  DBC-MSC-TTRN                         PIC X(01).
      *
      *        Codigo Tipo Origen
               05  DBC-COD-TTRN                         PIC X(03).
      *
      *    Autor Transaccion
           03  DBC-COD-ATRN                             PIC X(12).
      *
      *KEY Rut Cliente + Indicador o Calificador + Extension
           03  DBC-IDC-ICLI.
      *
      *        RUT Cliente
               05  DBC-NUM-ICLI                         PIC X(08).
      *
      *        Indicador Tipo RUT
               05  DBC-IND-ICLI                         PIC X(01).
      *
      *        Extension del RUT
               05  DBC-GLS-ICLI                         PIC X(03).
      *
      *KEY Digito Verificador del Rut
           03  DBC-VRF-ICLI                             PIC X(01).
      *
      *KEY Nombre Cliente Generico (Persona o Empresa)
           03  DBC-GLS-NOMC.
               05  DBC-GLS-APAT                         PIC X(25).
               05  DBC-GLS-AMAT                         PIC X(25).
               05  DBC-GLS-NOMB                         PIC X(25).
      *
      *    Soundex del Nombre de Cliente
           03  DBC-SNX-NOMC.
               05  DBC-SNX-APAT                         PIC X(03).
               05  DBC-SNX-AMAT                         PIC X(03).
               05  DBC-SNX-NOMB                         PIC X(03).
      *
      *    Numero de Direccion de Envio Correspondencia
           03  DBC-NUM-DCOR                             PIC 9(02).
      *
      *OPR Ind Tipo Cliente ( Persona o Empresa )       SGC TICL
           03  DBC-IND-TICL                             PIC X(01).
      *
      *    Tipo Cliente (Potencial,No Cliente,Grupo)    SGC TCL
           03  DBC-COD-TICL                             PIC X(02).
      *
      *    Codigo Clasificacion Comercial               SGC CLC
           03  DBC-COD-CLCM                             PIC X(02).
      *
      *RCM Codigo del Ejecutivo Encargado del Cliente   TAB USR
           03  DBC-COD-EJEC                             PIC X(12).
      *
      *OPR Codigo de Oficina donde opera el Cliente     TAB OFI
           03  DBC-COD-OFIC                             PIC X(03).
      *
      *RCM Indicador si opera en otra Oficina           SGC OOFI
           03  DBC-IND-OOFI                             PIC X(01).
      *
      *EXT Codigo Actividad Economica                   TAB ACT
           03  DBC-COD-ACTI                             PIC X(09).
      *
      *EXT Origen y Destino Sect (Comp.Inst)            TAB ODS
           03  DBC-COD-ODSE                             PIC X(03).
      *
      *INF Fecha Ultima Declaracion Articulo 85
           03  DBC-FEC-FA85.
               05  DBC-NUM-SA85                         PIC 9(02).
               05  DBC-NUM-AA85                         PIC 9(02).
               05  DBC-NUM-MA85                         PIC 9(02).
               05  DBC-NUM-DA85                         PIC 9(02).
      *
      *    Tipo Ultimo Pago Impuesto a la Renta         SGC UPIR
           03  DBC-IND-UPIR                             PIC X(03).
      *
      *    Fecha Ultimo Pago Impuesto a la Renta
           03  DBC-FEC-FUPI.
               05  DBC-NUM-SUPI                         PIC 9(02).
               05  DBC-NUM-AUPI                         PIC 9(02).
               05  DBC-NUM-MUPI                         PIC 9(02).
               05  DBC-NUM-DUPI                         PIC 9(02).
      *
      *INF Indicador de Multas Banco Central            SGC MUBC
           03  DBC-IND-MUBC                             PIC X(01).
      *
      *INF Indicador de Juicios Banco Central           SGC JUBC
           03  DBC-IND-JUBC                             PIC X(01).
      *
      *EXT Codigo Estado Cliente ( como Deudor )        SGC SCLI
           03  DBC-MSC-SCLI                             PIC X(01).
      *
      *    Fecha Estado Cliente
           03  DBC-FEC-FSCL.
               05  DBC-NUM-SSCL                         PIC 9(02).
               05  DBC-NUM-ASCL                         PIC 9(02).
               05  DBC-NUM-MSCL                         PIC 9(02).
               05  DBC-NUM-DSCL                         PIC 9(02).
      *
      *    Codigo de Bloqueo del Cliente                SGC BLO
           03  DBC-COD-BLOC                             PIC X(02).
      *
      *    Numero Total de Direcciones
           03  DBC-NUM-TDIR                             PIC 9(02).
      *
      *    Numero Total de Comentarios
           03  DBC-NUM-TCMT                             PIC 9(03).
      *
      *    Fecha Inicio Operacion
           03  DBC-FEC-FIOP.
               05  DBC-NUM-SIOP                         PIC 9(02).
               05  DBC-NUM-AIOP                         PIC 9(02).
               05  DBC-NUM-MIOP                         PIC 9(02).
               05  DBC-NUM-DIOP                         PIC 9(02).
      *
      *    Fecha Termino Operacion
           03  DBC-FEC-FTOP.
               05  DBC-NUM-STOP                         PIC 9(02).
               05  DBC-NUM-ATOP                         PIC 9(02).
               05  DBC-NUM-MTOP                         PIC 9(02).
               05  DBC-NUM-DTOP                         PIC 9(02).
      *
      *    Numero Operaciones Fecha Termino Nula
           03  DBC-NUM-OFTN                             PIC 9(03).
      *
      *    Fecha Ingreso de Datos a la Base de Datos
           03  DBC-FEC-FINP.
               05  DBC-NUM-SINP                         PIC 9(02).
               05  DBC-NUM-AINP                         PIC 9(02).
               05  DBC-NUM-MINP                         PIC 9(02).
               05  DBC-NUM-DINP                         PIC 9(02).
      *
      *    Disponible
      *    03  DBC-GLS-DISP                             PIC X(00).
      *
       01  WB-ELEM-LIS                      PIC X(11).
           EJECT
       PROCEDURE DIVISION.
      *===================
           ENTRY 'DBMSCBL'.
           EJECT
       A-CONTROL SECTION.
      *=================
       AA-INICIALIZACION.
           PERFORM B-INICIALIZAR.
           SKIP1
       AB-PROCESAMIENTO.
           IF (NOT WS-PROCESO-ERROR)
               PERFORM C-PROCESAR
           END-IF.
           SKIP1
       AC-TERMINACION.
           PERFORM D-TERMINAR.
           SKIP1
       AZ-FIN-DE-SECCION.
           GOBACK.
           EJECT
       B-INICIALIZAR SECTION.
      *======================
           PERFORM BA-ABRIR-ARCHIVOS.
           PERFORM BB-VALIDA-PARAME.
           GO BZ-FIN-DE-SECCION.
           SKIP1
       BA-ABRIR-ARCHIVOS.
      *------------------
           OPEN I-O SECMCER.
           IF (WR-FS-SECMCER NOT = 0)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BA-ABRIR-ARCHIVOS'     TO WR-PARRAFO
               MOVE 'SECMCER'               TO WR-ARCHIVO
               MOVE 'OPEN'                  TO WR-OPERACI
               MOVE WR-FS-SECMCER           TO WR-FILE-ST
               MOVE WR-FS-SECMCER2-RC       TO WR-FILE-RC
               MOVE WR-FS-SECMCER2-FC       TO WR-FILE-FC
               MOVE WR-FS-SECMCER2-KC       TO WR-FILE-KC
               MOVE '**ERROR EN OPEN**'     TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       BB-VALIDA-PARAME.
      *-----------------
           ACCEPT W-PARAMETROS
           MOVE CORR WP-FECHA-PRO           TO WR-FECHA-PRO
           MOVE WR-FECHA-PRO                TO WR-RUTU-FECHA
           MOVE ZEROS                       TO WR-RUTU-HABANT
                                               WR-RUTU-HABPOS
                                               WR-RUTU-CODRET
           MOVE ' '                         TO WR-RUTU-SN
           CALL 'RUTUFECH' USING  WR-RUTU-FECHA  WR-RUTU-SN
                                  WR-RUTU-HABANT WR-RUTU-HABPOS
                                  WR-RUTU-CODRET
           IF (WR-RUTU-CODRET NOT = 0)
               MOVE 'B-INICIALIZAR'         TO WR-SECCION
               MOVE 'BB-VALIDA-PARAME'      TO WR-PARRAFO
               MOVE 'SYSIN      '           TO WR-ARCHIVO
               MOVE WP-FECHA-PRO            TO WR-OPERACI
               MOVE '*FECHA PRO INVALIDA*'  TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       BD-DISPLAY-ERROR.
      *-----------------
           SET WS-PROCESO-ERROR             TO TRUE.
           DISPLAY ' '
           DISPLAY 'ERROR EN SECCION   : ' WR-SECCION
           DISPLAY '   PARRAFO         : ' WR-PARRAFO
           DISPLAY '   ARCHIVO         : ' WR-ARCHIVO
           DISPLAY '   OPERACION       : ' WR-OPERACI
           DISPLAY '   FILE STATUS     : ' WR-FILE-ST
           DISPLAY '   RETURN CODE     : ' WR-FILE-RC
           DISPLAY '   FUNTION CODE    : ' WR-FILE-FC
           DISPLAY '   FEEDBACK CODE   : ' WR-FILE-KC
           DISPLAY '   MENSAJE DE ERROR: ' WR-MENSAJE.
           SKIP1
       BZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       C-PROCESAR SECTION.
      *===================
       CA-PROCESO-GRAL.
      *----------------
           MOVE WR-FECHA-PRO                TO WB-REQ-OPC-KEY-VALUE
           PERFORM CB-LEER-OPC.
      * Se procesan curses del dia
           MOVE 'REDNE'                     TO WB-REQ-OPC-CMD
           PERFORM CC-PROCESA-CURSES
              UNTIL (WB-REQ-OPC-RC  NOT = '  ' OR WS-PROCESO-ERROR)
           GO CZ-FIN-DE-SECCION.
           SKIP1
       CB-LEER-OPC.
      *------------
           MOVE SPACES                      TO WB-REQ-OPC-RC
           MOVE 'OPC00'                     TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-OPC-AREA OPC WB-ELEM-LIS
           IF (WB-REQ-OPC-RC NOT = '  ' AND NOT = '14')
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'CB-LEER-OPC'           TO WR-PARRAFO
               MOVE 'OPC'                   TO WR-ARCHIVO
               MOVE WB-REQ-OPC-CMD          TO WR-OPERACI
               MOVE WB-REQ-OPC-RC           TO WR-FILE-ST
               MOVE WB-REQ-OPC-KEY-VALUE    TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CC-PROCESA-CURSES.
      *------------------
      * Chequeo estado opc
           IF (OPC-COD-TOPC     = 'D'   AND OPC-MSC-STAT = 'A'   AND
               OPC-VAL-SCRE     > 0)
               ADD 1                        TO WA-L-OPC
               IF (OPC-COD-CDOC NOT < '100' AND OPC-COD-CDOC < '200')
      * Busca cic del cliente
                   ADD 1                        TO WA-L-OPC-1
                   MOVE OPC-CIC-IOPC            TO WB-REQ-RDC-KEY-OPC
                   MOVE 000                     TO WB-REQ-RDC-KEY-NUM
                   PERFORM CF-LEER-RDC
                   IF (WB-REQ-RDC-RC = SPACES)
      * Busca Rut del cliente
                       MOVE RDC-CIC-ICLI        TO WB-REQ-DBC-KEY-VALUE
                       PERFORM CE-LEER-DBC
                       IF (WB-REQ-DBC-RC = SPACES)
      * Busca certificado del cliente
                           MOVE DBC-NUM-ICLI    TO CER-CLV-FORM
                           PERFORM XX-START-SECMCER
                           PERFORM XX-LEER-SECMCER
                           UNTIL (CER-NUM-RUTC NOT = DBC-NUM-ICLI OR
                                  CER-NUM-EVEN-OK                 OR
                                  WR-FS-SECMCER NOT = 0 OR
                                  WS-PROCESO-ERROR)
                           IF (CER-NUM-RUTC = DBC-NUM-ICLI AND
                               CER-NUM-EVEN-OK             AND
                               WR-FS-SECMCER = 0)
                               PERFORM XX-ACTUALIZAR-SECMCER
                           ELSE
                               ADD 1            TO WA-N-CER
                           END-IF
                       END-IF
                   END-IF
               END-IF
           END-IF.

           IF (NOT WS-PROCESO-ERROR)
               PERFORM CB-LEER-OPC
           END-IF.
           SKIP1
       XX-START-SECMCER.
      *-----------------
           START SECMCER KEY IS > CER-CLV-FORM END-START.
           IF (WR-FS-SECMCER = 0)
               PERFORM XX-LEER-SECMCER
           ELSE
           IF (WR-FS-SECMCER NOT = 23)
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'XX-START-SECMCER'      TO WR-PARRAFO
               MOVE 'SECMCER'               TO WR-ARCHIVO
               MOVE 'START'                 TO WR-OPERACI
               MOVE WR-FS-SECMCER           TO WR-FILE-ST
               MOVE WR-FS-SECMCER2-RC       TO WR-FILE-RC
               MOVE WR-FS-SECMCER2-FC       TO WR-FILE-FC
               MOVE WR-FS-SECMCER2-KC       TO WR-FILE-KC
               MOVE '**ERROR EN START*'     TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       XX-LEER-SECMCER.
      *----------------
           READ SECMCER NEXT END-READ
           IF (WR-FS-SECMCER NOT = 0 AND NOT = 10)
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'XX-LEER-SECMCER'       TO WR-PARRAFO
               MOVE 'SECMCER'               TO WR-ARCHIVO
               MOVE 'READ'                  TO WR-OPERACI
               MOVE WR-FS-SECMCER           TO WR-FILE-ST
               MOVE WR-FS-SECMCER2-RC       TO WR-FILE-RC
               MOVE WR-FS-SECMCER2-FC       TO WR-FILE-FC
               MOVE WR-FS-SECMCER2-KC       TO WR-FILE-KC
               MOVE '**ERROR EN READ *'     TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       XX-ACTUALIZAR-SECMCER.
      *----------------------
           MOVE WP-DIA-PRO IN WP-FECHA-PRO  TO CER-FECHA-DD
           MOVE WP-MES-PRO IN WP-FECHA-PRO  TO CER-FECHA-MM
           MOVE WP-ANO-PRO IN WP-FECHA-PRO  TO CER-FECHA-AA
           REWRITE CER END-REWRITE
           IF (WR-FS-SECMCER  = 0)
               ADD 1                        TO WA-G-CER
           ELSE
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'XX-ACTUALIZAR-SECMCER' TO WR-PARRAFO
               MOVE 'SECMCER'               TO WR-ARCHIVO
               MOVE 'WRITE'                 TO WR-OPERACI
               MOVE WR-FS-SECMCER           TO WR-FILE-ST
               MOVE WR-FS-SECMCER2-RC       TO WR-FILE-RC
               MOVE WR-FS-SECMCER2-FC       TO WR-FILE-FC
               MOVE WR-FS-SECMCER2-KC       TO WR-FILE-KC
               MOVE 'ERROR EN REWRITE'      TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CE-LEER-DBC.
      *------------
           MOVE SPACES                      TO WB-REQ-DBC-RC
           MOVE 'DBC00'                     TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-DBC-AREA DBC WB-ELEM-LIS
           IF (WB-REQ-DBC-RC NOT = '  ' AND NOT = '14')
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'CE-LEER-DBC'           TO WR-PARRAFO
               MOVE 'DBC'                   TO WR-ARCHIVO
               MOVE WB-REQ-DBC-CMD          TO WR-OPERACI
               MOVE WB-REQ-DBC-RC           TO WR-FILE-ST
               MOVE WB-REQ-DBC-KEY-VALUE    TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CF-LEER-RDC.
      *------------
           MOVE SPACES                      TO WB-REQ-RDC-RC
           MOVE 'RDC00'                     TO WB-ELEM-LIS
           CALL 'DBNTRY' USING WB-UIB WB-REQ-RDC-AREA RDC WB-ELEM-LIS
           IF (WB-REQ-RDC-RC NOT = '  ' AND NOT = '14')
               MOVE 'C-PROCESAR'            TO WR-SECCION
               MOVE 'CF-LEER-RDC'           TO WR-PARRAFO
               MOVE 'RDC'                   TO WR-ARCHIVO
               MOVE WB-REQ-RDC-CMD          TO WR-OPERACI
               MOVE WB-REQ-RDC-RC           TO WR-FILE-ST
               MOVE WB-REQ-RDC-KEY-VALUE    TO WR-MENSAJE
               PERFORM BD-DISPLAY-ERROR
           END-IF.
           SKIP1
       CZ-FIN-DE-SECCION.
      *------------------
           EXIT.
           EJECT
       D-TERMINAR SECTION.
      *===================
       DA-ESTADISTICAS.
      *----------------
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'INFORMES'
           DISPLAY '--------'
           DISPLAY ' '
           DISPLAY 'COLPB840 NO EMITE INFORME'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           ACCEPT WR-FECHA-HOY FROM DATE.
           DISPLAY 'ESTADISTICAS DE PROCESO PROGRAMA COLPB840 FECHA '
                    WR-FECHA-HOY
           DISPLAY '------------------------------------------------'
           DISPLAY '     Actualizacion fecha maestro certificados'
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'Estadisticas de proceso : '
           DISPLAY ' '
           DISPLAY '  Total de creditos cursados en el dia ' WA-L-OPC
           DISPLAY '  Total de creditos cursados por plan  ' WA-L-OPC-1
           DISPLAY '  Total de certificados regrabados     ' WA-G-CER
           DISPLAY '  Total de certificados no encontrados ' WA-N-CER
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'TARJETAS DE PARAMETROS'
           DISPLAY '----------------------'
           DISPLAY ' '
           DISPLAY W-PARAMETROS.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY 'MENSAJES PARA EL EXPLOTADOR'
           DISPLAY '---------------------------'
           DISPLAY ' '
           IF WS-PROCESO-OK
               DISPLAY 'NO HAY MENSAJES PARA EL EXPLOTADOR'
           ELSE
               DISPLAY 'HAY MENSAJES PARA EL EXPLOTADOR'
           END-IF.
           DISPLAY ' '
           DISPLAY ' '
           DISPLAY ' '.
           SKIP1
       DB-TERMINO.
      *-----------
           CLOSE SECMCER.
           IF WS-PROCESO-ERROR
              MOVE 100                      TO RETURN-CODE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
              DISPLAY '********                              ********'
              DISPLAY '******** TERMINO ERRONEO DEL PROCESO  ********'
              DISPLAY '********                              ********'
              DISPLAY '**********************************************'
              DISPLAY '**********************************************'
           ELSE
              DISPLAY ' '
              DISPLAY ' '
              DISPLAY '*****PROCESO TERMINO EN FORMA NORMAL*****'
           END-IF.
           SKIP1
       DZ-FIN-DE-SECCION.
      *------------------
           EXIT.
