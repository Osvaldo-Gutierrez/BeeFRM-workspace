       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGMVC.
       AUTHOR.          CONSIST.
      * Genera archivo MCC con OPC que tienen cargo automatico en cuenta
      * corriente desde VEN.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-AUT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-EVC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ICG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *<<<< WSS
       COPY COLBROPC.
       COPY COLBRCYA.
       COPY COLBRAUT.
       COPY COLBRVEN.
       COPY COLBRRCV.
       COPY COLBRCAN.
       COPY COLBRTOC.
       COPY COLBRRDT.
       COPY COLBRDLC.
       COPY COLBREVC.
       COPY COLBRICG.
       COPY COLWGVEN.
       COPY SGCBROPE.
       COPY TABBRCAM.
       COPY TABBROFI.
       COPY TABBRTIO.
       COPY COLBRMCC.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGPES.
      *SMC-INI 09.09.1993
       COPY COLWGINT.
      *SMC-FIN 09.09.1993
       COPY GNSWGCVI.
       01  WSS-VARI.
           03 WSS-COD-OFIC.
              05 WSS-IND-OFIC        OCCURS 10    PIC X(03).
           03 WSS-VAL-IDNI COMP-3    VALUE +0     PIC S9(11)V9(4).
           03 WSS-OFI-NFND           VALUE ZEROS  PIC 9(02).
           03 WSS-NUM-IVCT           VALUE ZEROS  PIC 9(04).
           03 WSS-FEC-FAMT.
              05 WSS-NUM-SAMT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AAMT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MAMT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DAMT        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FANT.
              05 WSS-NUM-SANT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AANT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MANT        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DANT        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FMOV.
              05 WSS-NUM-DMOV        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MMOV        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AMOV        VALUE ZEROS  PIC 9(01).
           03 WSS-NUM-RLEI           VALUE ZEROS  PIC 9(04).
           03 WSS-NUM-RPRO           VALUE ZEROS  PIC 9(04).
           03 WSS-NUM-RERR           VALUE ZEROS  PIC 9(04).
           03 WSS-REG-VEN            VALUE SPACES PIC X(137).
           03 WSS-NUM-IVEN           VALUE ZEROS  PIC 9(03).
           03 WSS-NUM-DIAS           VALUE ZEROS  PIC 9(04).
           03 WSS-FEC-FMIN.
              05 WSS-NUM-DMIN        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MMIN        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SMIN        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AMIN        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FINI.
              05 WSS-NUM-DINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SINI        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AINI        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FTER.
              05 WSS-NUM-STER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-ATER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MTER        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DTER        VALUE ZEROS  PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROS  PIC 9(02).
           03 WSS-COD-CMSC           VALUE SPACES PIC X(01).
           03 WSS-SGV-PROC COMP-3    VALUE +0     PIC S9(11)V9(04).
           03 WSS-SGV-COLO COMP-3    VALUE +0     PIC S9(11)V9(04).
           03 WSS-IND-MORO           VALUE SPACES PIC X(01).
           03 WSS-I                               PIC 9(03).
           03 WSS-GLS-MEN2           VALUE SPACES PIC X(40).
           03 WSS-VAL-SCRE COMP-3    VALUE ZEROS  PIC 9(11)V9(04).
           03 WSS-VAL-SCR1 COMP-3    VALUE ZEROS  PIC 9(11)V9(04).
           03 WSS-VAL-SMON COMP-3    VALUE +0     PIC S9(11)V9(04).
           03 WSS-IND-FICA           VALUE SPACES PIC X(01).
           03 WSS-IND-ULTC           VALUE SPACES PIC X(01).
           03 WSS-IND-ACC            VALUE SPACES PIC X(01).
           03 WSS-TAB-TCYA.
              05 WSS-IND-TCYA        OCCURS 10    PIC X(05).
           03 WSS-IND-MCYA           VALUE ZEROS  PIC 9(01).
           03 WSS-KEY-ICYA.
               05 WSS-KEY-GLS1       VALUE SPACES PIC X(12).
               05 WSS-KEY-GLS2       VALUE SPACES PIC X(04).
           03 WSS-FEC-FHOY.
              05 WSS-FEC-SHOY        VALUE ZEROS  PIC 9(02).
              05 WSS-FEC-AHOY        VALUE ZEROS  PIC 9(02).
              05 WSS-FEC-MHOY        VALUE ZEROS  PIC 9(02).
              05 WSS-FEC-DHOY        VALUE ZEROS  PIC 9(02).
           03 WSS-VEN-KEY-IVEN                    PIC X(15).
           03 WSS-IND-OTRO                        PIC X(01).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGMVC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM PRM-CAN.
           PERFORM INI-CAN.
           PERFORM LUP-PUT-VEN.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-CAN.
           PERFORM SGC-FIO-OPE.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-MCC.
       FIN-OPEN-FILE.
           EXIT.
       PRM-CAN SECTION.
      *-----------------
       INI-PRM-CAN.
           MOVE 1 TO WSS-I.
           PERFORM LIM-OFI-PRMT UNTIL WSS-I > 10.
           DISPLAY 'Inicio de Captura de Parametros'.
      *Captura Fecha de Proceso
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FEC DE PROC (DDMMAAAA) : ' WSS-FEC-FPRO IN WSS-VARI.
      *Captura Oficinas del credito
           ACCEPT  WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'OFICINA                : ' WSS-COD-OFIC IN WSS-VARI.
           DISPLAY 'Fin    de Captura    de Parametros'.
           DISPLAY 'Inicio de Validacion de Parametros'.
      *VAL-FEC Valida Fecha de Proceso
           DISPLAY WSS-FEC-FPRO IN WSS-VARI.
           MOVE WSS-FEC-FPRO    IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DTER IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MTER IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-STER IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-ATER IN WSS-VARI.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO WSS-FEC-FMIN IN WSS-VARI.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DMOV IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MMOV IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AMOV IN WSS-VARI.
       BUS-DIA-HBL.
           MOVE WSS-FEC-FMIN TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-RST-DIA  TO FEC-CMND.
           MOVE 1            TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-FECH TO WSS-FEC-FINI.
      *VAL-FEC Validar Fecha
           MOVE WSS-FEC-FINI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-FER
                MOVE WSS-FEC-FINI IN WSS-VARI TO WSS-FEC-FMIN
                     IN WSS-VARI
                GO TO BUS-DIA-HBL.
           MOVE WSS-FEC-FMIN IN WSS-VARI TO WSS-FEC-FINI IN WSS-VARI.
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-IND-OFIC IN WSS-VARI(1) NOT = '*'
               MOVE 1 TO WSS-I
               PERFORM VAL-OFI-PRMT UNTIL WSS-I > 10.
               IF WSS-OFI-NFND IN WSS-VARI = 10
                   PERFORM GNS-PRO-END.
           PERFORM GET-FHOY.
           MOVE HOY-DHOY TO WSS-FEC-DHOY IN WSS-VARI.
           MOVE HOY-MHOY TO WSS-FEC-MHOY IN WSS-VARI.
           MOVE HOY-SHOY TO WSS-FEC-SHOY IN WSS-VARI.
           MOVE HOY-AHOY TO WSS-FEC-AHOY IN WSS-VARI.
           DISPLAY 'Fin de Validacion de Parametros'.
           DISPLAY 'FECHA INICIO  : ' WSS-FEC-FINI IN WSS-VARI.
           DISPLAY 'FECHA TERMINO : ' WSS-FEC-FPRO IN WSS-VARI.
       FIN-PRM-CAN.
           EXIT.
       LIM-OFI-PRMT SECTION.
      *--------------------
       INI-LIM-OFI-PRMT.
           MOVE SPACES TO WSS-IND-OFIC IN WSS-VARI(WSS-I)
           ADD 1       TO WSS-I IN WSS-VARI.
       FIN-LIM-OFI-PRMT.
           EXIT.
       VAL-OFI-PRMT SECTION.
      *--------------------
       INI-VAL-OFI-PRMT.
           IF WSS-IND-OFIC IN WSS-VARI(WSS-I) NOT = SPACES
               MOVE WSS-IND-OFIC IN WSS-VARI(WSS-I) TO
                                    OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF (MSG-COD-MENS = 'COD    NEX') OR
                  (MSG-COD-MENS = 'COD    NVG')
                    MOVE 'COL' TO MSG-COD-SIST
                    PERFORM GET-MSG
                    DISPLAY 'OFICINA ' WSS-IND-OFIC IN WSS-VARI(WSS-I)
                    ' ' MSG-GLS-DESC
                    ADD 1 TO WSS-OFI-NFND IN WSS-VARI.
           ADD 1 TO WSS-I IN WSS-VARI.
       FIN-VAL-OFI-PRMT.
           EXIT.
       INI-CAN SECTION.
      *---------------
       INI-INI-CAN.
      * Inicializacion de Variables
           MOVE 'TVAL'         TO MSC-CIC-TMSC IN MSC.
           MOVE '0'            TO MSC-CIC-CMSC IN MSC.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'EN MISCELANEO TVAL NO EXISTE CODIGO 0'
               PERFORM GNS-PRO-END.
           MOVE MSC-COD-CMSC IN MSC TO WSS-COD-CMSC IN WSS-VARI.
           MOVE SPACES         TO WSS-TAB-TCYA IN WSS-VARI.
      *BUS-MSC Busca Codigos con CIC Cuenta Corriente en Miscelaneo de T
           MOVE 'TAB'          TO MSC-COD-SIST.
           MOVE 'TCYA'         TO MSC-CIC-TMSC IN MSC.
           MOVE 'CC'           TO MSC-CIC-CMSC IN MSC.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           PERFORM BUS-MSC.
           IF MSG-COD-MENS > SPACES
               DISPLAY MSG-GLS-DESC  MSC-CIC-TABL IN MSC
               GO TO FIN-INI-CAN.
           MOVE 1 TO WSS-I.
           MOVE MSC-COD-CMSC IN MSC TO WSS-IND-TCYA IN WSS-VARI(WSS-I).
       LUP-MSC-TCYA.
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'MSC-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS OR
              MSC-CIC-TMSC IN MSC NOT = 'TCYA'
               GO TO FIN-INI-CAN.
           IF MSC-CIC-MSCL IN MSC = 'TCYACC'
               ADD 1 TO WSS-I
               MOVE MSC-COD-CMSC IN MSC TO WSS-IND-TCYA
                                                   IN WSS-VARI(WSS-I).
           GO TO LUP-MSC-TCYA.
       FIN-INI-CAN.
           EXIT.
       LUP-PUT-VEN SECTION.
      *-------------------
       INI-LUP-VEN.
      * Programacion de Ciclo para Registro VEN
           MOVE WSS-NUM-DINI IN WSS-VARI TO VEN-NUM-DVEN IN VEN.
           MOVE WSS-NUM-MINI IN WSS-VARI TO VEN-NUM-MVEN IN VEN.
           MOVE WSS-NUM-SINI IN WSS-VARI TO VEN-NUM-SVEN IN VEN.
           MOVE WSS-NUM-AINI IN WSS-VARI TO VEN-NUM-AVEN IN VEN.
           MOVE FIO-GET-NLS              TO FIO-CMND.
       LUP-BUS-VEN.
           MOVE SPACES         TO WSS-IND-MORO IN WSS-VARI.
           MOVE 'VEN-FEC-FVEN' TO FIO-AKEY.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-LUP-PUT-VEN.
           ADD 1 TO WSS-NUM-RLEI IN WSS-VARI.
           IF VEN-FEC-FVEN IN VEN > WSS-FEC-FTER IN WSS-VARI
               GO TO FIN-LUP-PUT-VEN.

      * Si el vencimiento no esta completo
           IF VEN-IND-CALC IN VEN = 'N'
               GO TO OTRO-VEN.

      * Si el vencimiento esta cancelado
           IF VEN-VAL-SCAP IN VEN = ZEROS AND
              VEN-VAL-SFIN IN VEN = ZEROS
               GO TO OTRO-VEN.

           MOVE VEN-CIC-IOPC IN VEN TO OPC-CIC-IOPC IN OPC.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           IF NOT FIO-STAT-OKS
               MOVE 'COL'        TO MSG-COD-SIST
               MOVE 'OPC    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               PERFORM BCK-OUT
               GO TO OTRO-VEN
           ELSE
               IF ( OPC-MSC-STAT IN OPC NOT = 'A' ) OR
                  ( OPC-VAL-SCRE IN OPC NOT > ZEROS )
                   GO TO OTRO-VEN.
      *Valida oficina
           IF WSS-COD-OFIC IN WSS-VARI > SPACES AND
              WSS-IND-OFIC IN WSS-VARI(1) NOT = '*'
               MOVE 1 TO WSS-I
               PERFORM SEL-OFI-PRMT UNTIL WSS-I > 10 OR
               OPC-COD-OFOC IN OPC = WSS-IND-OFIC IN WSS-VARI(WSS-I)
               IF WSS-I > 10
                   GO TO OTRO-VEN.
      * Mediante BUS-TAB Busca Sistema Valido en Tabla TIO para Operacio
           MOVE 'TAB'               TO TAB-COD-SIST.
           MOVE 'TIO'               TO TAB-COD-TTAB IN TAB.
           MOVE OPC-COD-TOOC IN OPC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS = 'COD    NEX'
               DISPLAY MSG-GLS-DESC ' : ' TAB-COD-TABL
               GO TO OTRO-VEN.
      * Obtiene Sistema Valido de la Operacion de Credito
           MOVE TAB TO TIO.
           IF TIO-GLS-SIST IN TIO NOT = 'COL'
               GO TO OTRO-VEN.
           IF VEN-NUM-IVCT IN VEN = 1
               GO TO SIG-LUP-PUT-VEN.
      * Busca VEN anterior.
      * Si VEN anterior no esta cancelada, leer otro VEN
      * Si VEN anterior esta cancelada procesar
           MOVE 'N' TO WSS-IND-OTRO.
           MOVE VEN-KEY-IVEN IN VEN TO WSS-VEN-KEY-IVEN IN WSS-VARI
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           SUBTRACT 1 FROM VEN-NUM-IVCT IN VEN GIVING
                               VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           IF OPC-IND-VSTR IN OPC = 'S'
               PERFORM COL-OBT-VEN
               IF VEN-STAT-OKS
                   IF VEN-VAL-SFIN NOT = ZEROS
                       MOVE 'S' TO WSS-IND-OTRO
                   ELSE
                       NEXT SENTENCE
               ELSE
                   MOVE 'S' TO WSS-IND-OTRO
           ELSE
               PERFORM COL-FIO-VEN
               IF FIO-STAT-OKS
                   IF VEN-VAL-SFIN NOT = ZEROS
                        MOVE 'S' TO WSS-IND-OTRO
                   ELSE
                       NEXT SENTENCE
               ELSE
                   MOVE 'S' TO WSS-IND-OTRO.
           MOVE WSS-VEN-KEY-IVEN IN WSS-VARI TO VEN-KEY-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL RECUPERAR VEN ORIGINAL : '
               VEN-KEY-IVEN IN VEN
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               GO TO FIN-LUP-PUT-VEN.
           IF WSS-IND-OTRO = 'S'
               GO TO OTRO-VEN.
       SIG-LUP-PUT-VEN.
           PERFORM BUS-CYA-AUT.
           MOVE SPACES TO MSG-COD-MENS.
           IF NOT (FIO-STAT-OKS AND VEN-IND-CAUT IN VEN NOT = 'N')
               GO TO OTRO-VEN.
           PERFORM INI-REG-MCC.
           PERFORM GEN-REG-MCC.
      * Codigo que debe ser restaurado para cargo automatico
      *     PERFORM BUS-REG-RCV.
      *     IF MSG-COD-MENS > SPACES
      *         GO TO DIS-MEN-ERR.
      *     PERFORM BUS-CYA-AUT.
      *     IF NOT FIO-STAT-OKS
      *         GO TO DIS-MEN-ERR.
      *     PERFORM GEN-REG-CAN.
      *     IF NOT FIO-STAT-OKS
      *         GO TO DIS-MEN-ERR.
      *     PERFORM GEN-REG-RCV.
      *     IF NOT FIO-STAT-OKS
      *         GO TO DIS-MEN-ERR.
      *     PERFORM GEN-REG-CYA.
      *     IF NOT FIO-STAT-OKS
      *         GO TO DIS-MEN-ERR.
      * Hasta aqui se debe restaurar
           MOVE '1' TO WSS-IND-MORO IN WSS-VARI.
      * AAA 9-JAN-1991 12:31:53 Elimina calculo con tasa variable por
      * peticion de BCI hasta Nuevo aviso.
      *     PERFORM PRO-TFPR.
      * Fin
           IF MSG-COD-MENS > SPACES
      *AAA Ojo ver despliege de mensaje cuando PRO-TFPR erroneo
               MOVE 'COL'        TO MSG-COD-SIST
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC ' PARA OPC:' OPC-CIC-IOPC IN OPC
               ' ' WSS-GLS-MEN2
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
               PERFORM BCK-OUT.
           ADD 1 TO WSS-NUM-RPRO IN WSS-VARI.
       OTRO-VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-BUS-VEN.
       FIN-LUP-PUT-VEN.
           EXIT.
        SEL-OFI-PRMT SECTION.
      *--------------------
        INI-SEL-OFI-PRMT.
           ADD 1 TO WSS-I.
        FIN-SEL-OFI-PRMT.
           EXIT.
      * Inicializa registro MCC
       INI-REG-MCC SECTION.
       INI-INI-REG-MCC.
           MOVE SPACES TO MCC.
           MOVE ZEROS  TO MCC-COD-CAJE IN MCC.
           MOVE ZEROS  TO MCC-COD-SUCM IN MCC.
       FIN-INI-REG-MCC.
           EXIT.
      * Formacion y Generacion del Registro MCC al Procesar Varios Regis
       GEN-REG-MCC SECTION.
       INI-GEN-REG-MCC.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE 7                        TO MCC-IND-SIST IN MCC.
           MOVE CYA-IIC-ICYA IN CYA      TO MCC-GLS-ICTA IN MCC.
           MOVE '9'                      TO MCC-IND-IMPU IN MCC.
           PERFORM CAL-VAL-PESO
           IF MSG-COD-MENS > SPACES
               GO TO FIN-GEN-REG-MCC.
           MOVE '2'                      TO MCC-COD-MOVI IN MCC.
           MOVE WSS-FEC-FMOV IN WSS-VARI TO MCC-FEC-FMOV IN MCC.
           MOVE OPC-COD-COPC IN OPC      TO MCC-CAI-IOP1 IN MCC.
           MOVE OPC-IIC-IOPC IN OPC      TO MCC-IIC-IOP1 IN MCC.
      *Busca oficina de la cuenta
           MOVE CYA-CIC-ICYA IN CYA      TO OPE-CIC-IOPE IN OPE.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-OPE.
           IF FIO-STAT-OKS
               MOVE OPE-COD-OFOP IN OPE  TO MCC-COD-SUCO IN MCC
           ELSE
               MOVE 'OPEIOPENEX'        TO MSG-COD-MENS
               GO TO FIN-GEN-REG-MCC.
      *
           MOVE OPC-COD-OFOC IN OPC      TO MCC-COD-SUCM IN MCC.
           MOVE OPC-COD-COPC IN OPC      TO MCC-CAI-IOP2 IN MCC.
           MOVE OPC-IIC-IOPC IN OPC      TO MCC-IIC-IOP2 IN MCC.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-MCC.
           IF NOT FIO-STAT-OKS
               MOVE 'MCC    NOPUT' TO MSG-COD-MENS
               DISPLAY 'FIO-STAT     : ' FIO-STAT
               DISPLAY 'REGISTRO MCC : ' MCC.
       FIN-GEN-REG-MCC.
           EXIT.
       BUS-CYA-AUT SECTION.
       INI-BUS-CYA-AUT.
           MOVE OPC-CAI-IOPC IN OPC TO AUT-CAI-IOPC IN AUT.
           MOVE OPC-IIC-IOPC IN OPC TO AUT-IIC-IOPC IN AUT.
           MOVE 'AUT'               TO AUT-NUM-ICAN IN AUT.
           MOVE 1                   TO AUT-NUM-ICYA IN AUT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-AUT.
           IF NOT FIO-STAT-OKS
               MOVE 'CYAAUTNEX' TO MSG-COD-MENS
           ELSE
               MOVE AUT TO CYA.
       FIN-BUS-CYA-AUT.
           EXIT.
       GEN-REG-CAN SECTION.
       INI-GEN-REG-CAN.
      * Programacion de Generacion Cancelacion
           PERFORM BUS-ULT-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 1 TO CAN-NUM-ICAN IN CAN.
           MOVE '00' TO FIO-STAT.
           PERFORM INI-REG-CAN.
           IF NOT FIO-STAT-OKS
               GO TO FIN-GEN-REG-CAN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-CAN.
           EXIT.
       BUS-ULT-CAN SECTION.
       INI-BUS-ULT-CAN.
      *Busca Correlativo Ultima Cancelacion.
           MOVE VEN-CIC-IOPC IN VEN TO CAN-CIC-IOPC IN CAN.
           MOVE 999                 TO CAN-NUM-ICAN IN CAN.
           MOVE FIO-GET-LEQ TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = CAN-CIC-IOPC IN CAN
               ADD 1 TO CAN-NUM-ICAN IN CAN
               GO TO FIN-BUS-ULT-CAN.
           MOVE FIO-STAT-NEX TO FIO-STAT.
       FIN-BUS-ULT-CAN.
           EXIT.
       INI-REG-CAN SECTION.
       INI-INI-REG-CAN.
           MOVE VEN-CIC-IOPC IN VEN      TO CAN-CIC-IOPC IN CAN.
           MOVE ZEROES                   TO CAN-STP-ITRN IN CAN.
           MOVE SPACES                   TO CAN-MSC-TACC IN CAN.
           MOVE 'G'                      TO CAN-MSC-STAT IN CAN.
           MOVE SPACES                   TO CAN-COD-OTRN IN CAN.
           MOVE SPACES                   TO CAN-COD-ATRN IN CAN.
           MOVE CYA-COD-OFIC IN CYA      TO CAN-COD-OFCA IN CAN.
           MOVE OPC-COD-RMOC IN OPC      TO CAN-COD-RCAN IN CAN.
           MOVE 'CTE'                    TO CAN-MSC-CANC IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DCAN IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MCAN IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SCAN IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-ACAN IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DEGR IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MEGR IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SEGR IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-AEGR IN CAN.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO CAN-NUM-DOUT IN CAN.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO CAN-NUM-MOUT IN CAN.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO CAN-NUM-SOUT IN CAN.
           MOVE WSS-NUM-APRO IN WSS-VARI TO CAN-NUM-AOUT IN CAN.
           MOVE ZEROES                   TO CAN-SGV-GAST IN CAN.
      *Calcular VEN-VAL-SFIN * VCAM_FPRO si es reajustable
           PERFORM CAL-SGV-TPAG.
           MOVE ZEROES                   TO CAN-SGV-TREN IN CAN.
           MOVE ZEROES                   TO CAN-SGV-DIFE IN CAN.
           MOVE SPACES                   TO CAN-GLS-DISP IN CAN.
       FIN-INI-REG-CAN.
           EXIT.
       CAL-VAL-PESO SECTION.
       INI-CAL-VAL-PESO.
           MOVE SPACES TO MSG-COD-MENS.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB
               MOVE '0999  '            TO TAB-CIC-CTAB IN TAB
               MOVE VEN-NUM-DVEN IN VEN TO TAB-NUM-DTRN IN TAB
               MOVE VEN-NUM-MVEN IN VEN TO TAB-NUM-MTRN IN TAB
               MOVE VEN-NUM-SVEN IN VEN TO TAB-NUM-STRN IN TAB
               MOVE VEN-NUM-AVEN IN VEN TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SFIN IN
                          VEN GIVING PES-SGV-DECI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE TO MCC-VAL-MOVI IN MCC
               ELSE
               IF MSG-COD-MENS IN MSG = 'CODVCAMEQL'
                   MOVE SPACES              TO MSG-COD-MENS
                   MOVE VEN-VAL-SFIN IN VEN TO MCC-VAL-MOVI IN MCC
               ELSE
                   MOVE CAM-KEY-CAMB IN CAM TO WSS-GLS-MEN2
           ELSE
               MOVE VEN-VAL-SFIN IN VEN TO MCC-VAL-MOVI IN MCC.
       FIN-CAL-VAL-PESO.
           EXIT.
       CAL-SGV-TPAG SECTION.
       INI-CAL-SGV-TPAG.
      *Calcular VEN-VAL-SFIN * VCAM_FPRO si es reajustable
           MOVE VEN-VAL-SFIN IN VEN TO CAN-SGV-TPAG IN CAN.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC      TO TAB-COD-CTAB IN TAB
               MOVE '0999  '                 TO TAB-CIC-CTAB IN TAB
               MOVE WSS-NUM-DPRO IN WSS-VARI TO TAB-NUM-DTRN IN TAB
               MOVE WSS-NUM-MPRO IN WSS-VARI TO TAB-NUM-MTRN IN TAB
               MOVE WSS-NUM-SPRO IN WSS-VARI TO TAB-NUM-STRN IN TAB
               MOVE WSS-NUM-APRO IN WSS-VARI TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SFIN IN
                          VEN GIVING CAN-SGV-TPAG IN CAN
               ELSE
                   IF MSG-COD-MENS IN MSG NOT = 'CODVCAMEQL'
                       MOVE FIO-STAT-NEX TO FIO-STAT.
       FIN-CAL-SGV-TPAG.
           EXIT.
       GEN-REG-RCV SECTION.
       INI-GEN-REG-RCV.
      * Programacion de Generacion Relacion Cancelacion Vencimiento
           MOVE '00' TO FIO-STAT.
           PERFORM INI-REG-RCV.
           IF NOT FIO-STAT-OKS
               GO TO FIN-GEN-REG-RCV.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF NOT FIO-STAT-OKS
               MOVE 'RCV    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-RCV.
           EXIT.
       INI-REG-RCV SECTION.
       INI-INI-REG-RCV.
           MOVE SPACES                   TO RCV-GLS-FLAG IN RCV.
           MOVE CAN-CIC-IOPC IN CAN      TO RCV-CIC-IOPC IN RCV.
           MOVE CAN-NUM-ICAN IN CAN      TO RCV-NUM-ICAN IN RCV.
           MOVE VEN-CIC-IOPC IN VEN      TO RCV-CIC-IOPV IN RCV.
           MOVE VEN-NUM-IVEN IN VEN      TO RCV-NUM-IVCT IN RCV.
           MOVE ZEROES                   TO RCV-STP-ITRN IN RCV.
           MOVE SPACES                   TO RCV-MSC-TACC IN RCV.
           MOVE SPACES                   TO RCV-MSC-STAT IN RCV.
           MOVE SPACES                   TO RCV-COD-OTRN IN RCV.
           MOVE SPACES                   TO RCV-COD-ATRN IN RCV.
           MOVE OPC-CIC-IOPC IN OPC      TO RCV-CIC-IOPD IN RCV.
           MOVE VEN-NUM-IDLC IN VEN      TO RCV-NUM-IDLC IN RCV.
           MOVE VEN-NUM-IEVC IN VEN      TO RCV-NUM-IEVC IN RCV.
           MOVE 'F'                      TO RCV-MSC-MCAN IN RCV.
           MOVE VEN-VAL-SCAP IN VEN      TO RCV-SGV-CAPI IN RCV.
           MOVE VEN-VAL-IDNI IN VEN      TO RCV-SGV-INTE IN RCV.
           MOVE VEN-VAL-SFIN IN VEN      TO RCV-SGV-FINA IN RCV.
           MOVE VEN-IND-SCTB IN VEN      TO RCV-IND-SCTB IN RCV.
           MOVE VEN-IND-SCBZ IN VEN      TO RCV-IND-SCBZ IN RCV.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO RCV-NUM-DCAN IN RCV.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO RCV-NUM-MCAN IN RCV.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO RCV-NUM-SCAN IN RCV.
           MOVE WSS-NUM-APRO IN WSS-VARI TO RCV-NUM-ACAN IN RCV.
           MOVE WSS-NUM-DPRO IN WSS-VARI TO RCV-NUM-DOUT IN RCV.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO RCV-NUM-MOUT IN RCV.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO RCV-NUM-SOUT IN RCV.
           MOVE WSS-NUM-APRO IN WSS-VARI TO RCV-NUM-AOUT IN RCV.
      *Calculo de reajustes SCAP (VCAM_FPRO - VCAM_FCOL)
           PERFORM CAL-RCV-REAJ.
           MOVE SPACES                   TO RCV-GLS-DISP IN RCV.
       FIN-INI-REG-RCV.
           EXIT.
       CAL-RCV-REAJ SECTION.
       INI-CAL-RCV-REAJ.
      *Calculo de reajustes SCAP (VCAM_FPRO - VCAM_FCOL)
           MOVE ZEROES                       TO RCV-SGV-REAJ IN RCV.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC      TO TAB-COD-CTAB IN TAB
               MOVE '0999  '                 TO TAB-CIC-CTAB IN TAB
               MOVE WSS-NUM-DPRO IN WSS-VARI TO TAB-NUM-DTRN IN TAB
               MOVE WSS-NUM-MPRO IN WSS-VARI TO TAB-NUM-MTRN IN TAB
               MOVE WSS-NUM-SPRO IN WSS-VARI TO TAB-NUM-STRN IN TAB
               MOVE WSS-NUM-APRO IN WSS-VARI TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN
                          VEN GIVING WSS-SGV-PROC IN WSS-VARI
               ELSE
                   IF MSG-COD-MENS IN MSG NOT = 'CODVCAMEQL'
                       MOVE FIO-STAT-NEX TO FIO-STAT
                       GO TO FIN-CAL-RCV-REAJ
                   ELSE
                       GO TO FIN-CAL-RCV-REAJ.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               MOVE OPC-COD-VCOC IN OPC TO TAB-COD-CTAB IN TAB
               MOVE '0999  '            TO TAB-CIC-CTAB IN TAB
               MOVE OPC-NUM-DCOL IN OPC TO TAB-NUM-DTRN IN TAB
               MOVE OPC-NUM-MCOL IN OPC TO TAB-NUM-MTRN IN TAB
               MOVE OPC-NUM-SCOL IN OPC TO TAB-NUM-STRN IN TAB
               MOVE OPC-NUM-ACOL IN OPC TO TAB-NUM-ATRN IN TAB
               PERFORM GET-VCAM
               IF MSG-COD-MENS IN MSG NOT > SPACES
                   MULTIPLY CAM-SGV-VCAM IN CAM BY VEN-VAL-SCAP IN
                          VEN GIVING WSS-SGV-COLO IN WSS-VARI
               ELSE
                   IF MSG-COD-MENS IN MSG NOT = 'CODVCAMEQL'
                       MOVE FIO-STAT-NEX TO FIO-STAT
                       GO TO FIN-CAL-RCV-REAJ.
           IF OPC-MSC-TVOC IN OPC = WSS-COD-CMSC IN WSS-VARI
               SUBTRACT WSS-SGV-COLO IN WSS-VARI FROM
                        WSS-SGV-PROC IN WSS-VARI GIVING
                        RCV-SGV-REAJ IN RCV.
       FIN-CAL-RCV-REAJ.
           EXIT.
       GEN-REG-CYA SECTION.
       INI-GEN-REG-CYA.
      * Programacion de Generacion Cargos y Abonos
           PERFORM INI-REG-CYA.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS
               MOVE 'CYA    NACT' TO MSG-COD-MENS.
       FIN-GEN-REG-CYA.
           EXIT.
       INI-REG-CYA SECTION.
       INI-INI-REG-CYA.
           MOVE CAN-NUM-ICAN IN CAN TO CYA-NUM-ICAN IN CYA.
           MOVE 1                   TO CYA-NUM-ICYA IN CYA.
           MOVE CAN-SGV-TPAG IN CAN TO CYA-SGV-LIQU IN CYA.
           MOVE 'S'                 TO CYA-IND-MAUT IN CYA.
           MOVE SPACES              TO CYA-IND-FREE IN CYA.
           MOVE SPACES              TO CYA-GLS-DISP IN CYA.
       FIN-INI-REG-CYA.
           EXIT.
       BCK-OUT SECTION.
       INI-BCK-OUT.
           MOVE FIO-BCK-OUT TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'BCKOUTN' TO MSG-COD-MENS.
       FIN-BCK-OUT.
           EXIT.
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTRO VEN LEIDOS     : ' WSS-NUM-RLEI IN WSS-VARI
           DISPLAY 'REGISTRO VEN PROCESADOS : ' WSS-NUM-RPRO IN WSS-VARI
           DISPLAY 'REGISTRO VEN ERRONEOS   : ' WSS-NUM-RERR IN WSS-VARI
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-RCV.
           PERFORM COL-FIO-CAN.
           PERFORM SGC-FIO-OPE.
           PERFORM COL-FIO-MCC.
       FIN-CLOSE-FILE.
           EXIT.
       PRO-TFPR SECTION.
       INI-PRO-TFPR.
           IF OPC-IND-VSTR IN OPC = 'S'
               GO TO FIN-PRO-TFPR.
           MOVE VEN-NUM-IVEN IN VEN TO WSS-NUM-IVCT IN WSS-VARI.
           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FAMT IN WSS-VARI.
           MOVE ZEROES TO WSS-VAL-SCR1 IN WSS-VARI.
           PERFORM OBT-VAL-SCRE VARYING WSS-I FROM OPC-NUM-IVEN IN OPC
                   BY 1 UNTIL WSS-I > WSS-NUM-IVCT IN WSS-VARI.
           MOVE WSS-NUM-IVCT IN WSS-VARI TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT (FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = OPC-CIC-IOPC IN OPC )
               GO TO LEE-CUR-VEN.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           PERFORM CAL-TFPR.
           IF WSS-IND-MORO IN WSS-VARI = '1'
               IF MSG-COD-MENS > SPACES
                   DISPLAY MSG-COD-MENS '/OPERACION : ' OPC-CIC-IOPC
                       '/VENCIMIENTO : ' VEN-NUM-IVEN IN VEN
               ELSE
                   MOVE FIO-MOD TO FIO-CMND
                   PERFORM COL-FIO-VEN
           ELSE
           IF MSG-COD-MENS NOT > SPACES
              MOVE 'PGS' TO VEN-IND-SCTB IN VEN
              MOVE FIO-MOD TO FIO-CMND
              PERFORM COL-FIO-VEN
           ELSE
               DISPLAY MSG-COD-MENS '/OPERACION : ' OPC-CIC-IOPC
                       '/VENCIMIENTO : ' VEN-NUM-IVEN IN VEN.
           IF WSS-IND-ACC IN WSS-VARI NOT = 'E'
               GO TO LEE-CUR-VEN.
       NXT-DEL-VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT (FIO-STAT-OKS AND
              VEN-CIC-IOPC IN VEN = OPC-CIC-IOPC IN OPC)
               GO TO LEE-CUR-VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN
           IF NOT FIO-STAT-OKS
               GO TO LEE-CUR-VEN.
           MOVE FIO-DEL TO FIO-CMND.
           PERFORM COL-FIO-VEN
           IF NOT FIO-STAT-OKS
               GO TO LEE-CUR-VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC
           SUBTRACT 1 FROM OPC-NUM-VCVG IN OPC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           GO TO NXT-DEL-VEN.
       LEE-CUR-VEN.
           MOVE OPC-CIC-IOPC IN OPC      TO VEN-CIC-IOPC IN VEN.
           MOVE WSS-NUM-IVCT IN WSS-VARI TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       FIN-PRO-TFPR.
           EXIT.
       OBT-VAL-SCRE SECTION.
       INI-OBT-VAL-SCRE.
           MOVE WSS-I IN WSS-VARI TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF FIO-STAT-OKS
               ADD VEN-VAL-SCAP IN VEN TO WSS-VAL-SCR1 IN WSS-VARI.
       FIN-OBT-VAL-SCRE.
           EXIT.
       BUS-REG-RCV SECTION.
       INI-BUS-REG-RCV.
           MOVE SPACES TO MSG-COD-MENS.
           MOVE VEN-CIC-IOPC IN VEN TO RCV-CIC-IOPV IN RCV.
           MOVE VEN-NUM-IVEN IN VEN TO RCV-NUM-IVCT IN RCV.
           MOVE 'RCV-KEY-IVCT' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-RCV.
           IF FIO-STAT-OKS
               MOVE 'CAN    YAEXI' TO MSG-COD-MENS.
       FIN-BUS-REG-RCV.
           EXIT.
       CAL-TFPR SECTION.
       INI-CAL-TFPR.
           MOVE OPC-CIC-IOPC IN OPC TO ICG-CIC-IOPC IN ICG.
           MOVE ZEROES              TO ICG-NUM-ICAN IN ICG.
           MOVE SPACES              TO ICG-CIC-IOPV IN ICG.
           MOVE ZEROES              TO ICG-NUM-IVCT IN ICG.
           MOVE 'INT'               TO ICG-IND-TTAS IN ICG.
           MOVE ZEROES              TO ICG-FEC-FINI IN ICG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-ICG.
           IF NOT (FIO-STAT-OKS AND
              ICG-CIC-IOPC IN ICG = OPC-CIC-IOPC IN OPC AND
              ICG-NUM-ICAN IN ICG = ZEROES              AND
              ICG-CIC-IOPV IN ICG = SPACES              AND
              ICG-NUM-IVCT IN ICG = ZEROES              AND
              ICG-IND-TTAS IN ICG = 'INT' )
               MOVE '00' TO FIO-STAT
               GO TO FIN-CAL-TFPR.
           IF ICG-COD-CVTI IN ICG NOT > SPACES
               GO TO FIN-CAL-TFPR.
       VEN-CAL-TFPR.
           MOVE VEN-CIC-IOPC IN VEN      TO DLC-CIC-IOPC IN DLC.
           MOVE VEN-NUM-IDLC IN VEN      TO DLC-NUM-IDLC IN DLC.
           MOVE FIO-GET-KEY              TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           MOVE WSS-NUM-DAMT IN WSS-VARI TO FEC-DEC1.
           MOVE WSS-NUM-MAMT IN WSS-VARI TO FEC-MEC1.
           MOVE WSS-NUM-SAMT IN WSS-VARI TO FEC-SEC1.
           MOVE WSS-NUM-AAMT IN WSS-VARI TO FEC-AEC1.
           MOVE VEN-NUM-DVEN IN VEN      TO FEC-DEC2.
           MOVE VEN-NUM-MVEN IN VEN      TO FEC-MEC2.
           MOVE VEN-NUM-SVEN IN VEN      TO FEC-SEC2.
           MOVE VEN-NUM-AVEN IN VEN      TO FEC-AEC2.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-DIF-DIA              TO FEC-CMND.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-NDIA                 TO INT-SEPA INT-SEPD.
           MOVE ICG-COD-TINT IN ICG      TO INT-TIPT.
           MOVE ICG-SGV-SPRD IN ICG      TO INT-TINT.
           MOVE WSS-NUM-DAMT IN WSS-VARI TO INT-DDMA.
           MOVE WSS-NUM-MAMT IN WSS-VARI TO INT-MDMA.
           MOVE WSS-NUM-SAMT IN WSS-VARI TO INT-SDMA.
           MOVE WSS-NUM-AAMT IN WSS-VARI TO INT-ADMA.
           MOVE OPC-COD-VCOC IN OPC      TO INT-VCAM.
           PERFORM OBT-VTIN.
           IF INT-CMSG > SPACES
               MOVE INT-CMSG TO MSG-COD-MENS
               GO TO FIN-CAL-TFPR.
           SUBTRACT WSS-VAL-SCR1 IN WSS-VARI FROM OPC-VAL-SCRE IN OPC
                    GIVING WSS-VAL-SCRE IN WSS-VARI.
           MULTIPLY WSS-VAL-SCRE IN WSS-VARI BY INT-TIPE GIVING
                    WSS-VAL-IDNI IN WSS-VARI.
           MOVE SPACES TO WSS-IND-ACC  IN WSS-VARI.
           MOVE SPACES TO WSS-IND-FICA IN WSS-VARI.
           IF VEN-FEC-FVEN IN VEN = DLC-FEC-FMAV IN DLC
               MOVE 'U' TO WSS-IND-ULTC IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-IND-ULTC IN WSS-VARI.
       CAL-INT-FICA.
           IF VEN-VAL-FINA IN VEN =  ZEROES
               MOVE 'F' TO WSS-IND-FICA IN WSS-VARI
               ADD WSS-VAL-IDNI IN WSS-VARI VEN-VAL-CAPI IN VEN
                                     GIVING VEN-VAL-FINA IN VEN
                   MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN.
           IF VEN-VAL-CAPI IN VEN = ZEROES
               MOVE 'C' TO WSS-IND-FICA IN WSS-VARI
               SUBTRACT WSS-VAL-IDNI IN WSS-VARI FROM
                 VEN-VAL-FINA IN VEN GIVING VEN-VAL-CAPI IN VEN
               MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN.
           IF WSS-IND-FICA IN WSS-VARI = 'C'
               SUBTRACT VEN-VAL-CAPI IN VEN FROM
                        WSS-VAL-SCRE IN WSS-VARI GIVING WSS-VAL-SMON
                        IN WSS-VARI
               IF WSS-VAL-SMON IN WSS-VARI < ZEROES
                   MOVE WSS-VAL-SCRE IN WSS-VARI TO VEN-VAL-CAPI
                        IN VEN
                   MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN
                   MOVE ZEROES TO VEN-VAL-FINA IN VEN
                   MOVE 'E' TO WSS-IND-ACC IN WSS-VARI
                   GO TO CAL-INT-FICA
               ELSE
                   IF WSS-IND-ULTC IN WSS-VARI = 'U'
                       PERFORM OBT-MON-SCTV
                       MOVE FIO-MOD TO FIO-CMND
                       PERFORM COL-FIO-VEN
                       ADD VEN-VAL-SCAP IN VEN TO
                         WSS-VAL-SCR1 IN WSS-VARI
                       PERFORM UPD-VEN-SIGU.
      *AAA 29-NOV-1990 12:01:43
      * ELIMINA CALCULO DE MULTIPLES VENCIMIENTOS PARA ULTIMA CUOTA
      *                 GO TO VEN-CAL-TFPR.
           PERFORM OBT-MON-SCTV.
       FIN-CAL-TFPR.
           EXIT.
       PRO-TFPR-VSTR SECTION.
       INI-PRO-TFPR-VSTR.
           MOVE 1 TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF FIO-STAT-OKS AND
              OPC-CIC-IOPC IN OPC = VEN-CIC-IOPC IN VEN
               MOVE VEN TO WSS-REG-VEN IN WSS-VARI
               MOVE FIO-GET-KEY-UPD TO FIO-CMND
               PERFORM COL-FIO-VEN
               MOVE FIO-DEL TO FIO-CMND
               PERFORM COL-FIO-VEN
               MOVE WSS-REG-VEN IN WSS-VARI TO VEN
               IF VEN-VAL-CAPI IN VEN > ZEROES AND
                  VEN-VAL-FINA IN VEN > ZEROES
                   GO TO GEN-SIG-VEN.
           MOVE OPC-CIC-IOPC IN OPC TO VEN-CIC-IOPC IN VEN.
           MOVE 1 TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE VEN-NUM-IVCT IN VEN TO VEN-NUM-IVEN IN VEN.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-OBT-VEN.
           IF VEN-STAT-EOF
               GO TO FIN-PRO-TFPR-VSTR.
           IF NOT ( VEN-STAT-OKS OR VEN-STAT-TVX )
               MOVE VEN-STAT TO MSG-COD-MENS
               GO TO FIN-PRO-TFPR-VSTR.
           IF VEN-STAT-TVX
               MOVE INT-DDMA     TO FEC-DEC1
               MOVE INT-MDMA     TO FEC-MEC1
               MOVE INT-SDMA     TO FEC-SEC1
               MOVE INT-ADMA     TO FEC-AEC1
               MOVE WSS-NUM-DPRO TO FEC-DEC2
               MOVE WSS-NUM-MPRO TO FEC-MEC2
               MOVE WSS-NUM-SPRO TO FEC-SEC2
               MOVE WSS-NUM-APRO TO FEC-AEC2
               MOVE FEC-FORM-FEC TO FEC-FORM
               MOVE FEC-CMP-FEC  TO FEC-CMND
               PERFORM PRO-FEC
               IF FEC-STAT-LES
                   MOVE 'CANFECTV<=PR' TO MSG-COD-MENS
                   GO TO FIN-PRO-TFPR-VSTR.
           MOVE VEN-NUM-IVEN IN VEN TO VEN-NUM-IVCT IN VEN.
       GEN-SIG-VEN.
           MOVE 1 TO VEN-NUM-IVEN IN VEN.
           MOVE VEN TO WSS-REG-VEN IN WSS-VARI
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE 'VEN    EVCNX' TO VEN-STAT
               MOVE VEN-STAT TO MSG-COD-MENS
               GO TO FIN-PRO-TFPR-VSTR.
           MOVE WSS-REG-VEN IN WSS-VARI TO VEN
           MOVE 'PGS' TO VEN-IND-SCTB IN VEN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
       FIN-PRO-TFPR-VSTR.
           EXIT.
       UPD-VEN-SIGU SECTION.
       INI-UPD-VEN-SIGU.
           ADD 1 TO VEN-NUM-IVEN IN VEN.
      *         05  VEN-STP-ITRN.
           MOVE VEN-FEC-FVEN IN VEN TO WSS-FEC-FAMT IN WSS-VARI.
           MOVE SPACES              TO VEN-MSC-TACC IN VEN.
           MOVE SPACES              TO VEN-MSC-STAT IN VEN.
           MOVE SPACES              TO VEN-COD-OTRN IN VEN.
           MOVE SPACES              TO VEN-COD-ATRN IN VEN.
           PERFORM OBT-FEC-VCIM.
           MOVE ZEROES              TO VEN-VAL-CAPI IN VEN.
           MOVE ZEROES              TO VEN-VAL-SCAP IN VEN.
           MOVE ZEROES              TO VEN-VAL-IDNI IN VEN.
           MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN.
           MOVE SPACES TO VEN-IND-SCTB IN VEN.
           MOVE VEN-NUM-IVEN IN VEN TO VEN-NUM-IVCT IN VEN.
           MOVE SPACES              TO VEN-IND-SCBZ IN VEN.
           MOVE ZEROES              TO VEN-VAL-MTSC IN VEN.
           MOVE SPACES              TO VEN-IND-MTSC IN VEN.
           MOVE SPACES              TO VEN-GLS-DISP IN VEN.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-OPC
           ADD 1 TO OPC-NUM-VCVG IN OPC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-OPC.
           MOVE VEN-NUM-IDLC IN VEN TO DLC-CIC-IOPC IN DLC.
           MOVE OPC-CIC-IOPC IN OPC TO DLC-CIC-IOPC IN DLC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
           MOVE VEN-FEC-FVEN IN VEN TO DLC-FEC-FMAV IN DLC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-DLC.
       FIN-UPD-VEN-SIGU.
           EXIT.
       OBT-MON-SCTV SECTION.
       INI-OBT-MON-SCTV.
           IF OPC-COD-VCOC IN OPC = CAM-COD-MNAC IN CAM-VARI
               MOVE VEN-VAL-CAPI IN VEN TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO VEN-VAL-CAPI IN VEN
               MOVE VEN-VAL-CAPI IN VEN TO VEN-VAL-SCAP IN VEN
               MOVE VEN-VAL-FINA IN VEN TO PES-SGV-DECI IN PES-VARI
               PERFORM PES-SCTV
               MOVE PES-SGV-ENTE IN PES-VARI TO VEN-VAL-FINA IN VEN
               MOVE VEN-VAL-FINA IN VEN TO VEN-VAL-SFIN IN VEN.
       FIN-OBT-MON-SCTV.
           EXIT.
       OBT-FEC-VCIM SECTION.
       INI-OBT-FEC-VCIM.
           MOVE VEN-NUM-DVEN IN VEN      TO FEC-ITM1.
           MOVE VEN-NUM-MVEN IN VEN      TO FEC-ITM2.
           MOVE VEN-NUM-SVEN IN VEN      TO FEC-ITM3.
           MOVE VEN-NUM-AVEN IN VEN      TO FEC-ITM4.
           MOVE FEC-FORM-FEC             TO FEC-FORM.
           MOVE FEC-SUM-DIA              TO FEC-CMND.
           MOVE WSS-NUM-DIAS IN WSS-VARI TO FEC-NDIA.
      *    PERFORM PRO-FEC.
           PERFORM CAL-FEC.
           MOVE FEC-ITM1                 TO VEN-NUM-DVEN IN VEN.
           MOVE FEC-ITM2                 TO VEN-NUM-MVEN IN VEN.
           MOVE FEC-ITM3                 TO VEN-NUM-SVEN IN VEN.
           MOVE FEC-ITM4                 TO VEN-NUM-AVEN IN VEN.
           MOVE VEN-FEC-FVEN IN VEN      TO VEN-FEC-FIMV IN VEN.
       FIN-OBT-FEC-VCIM.
           EXIT.
       COPY COLBFOPC.
       COPY COLBFCYA.
       COPY COLBFAUT.
       COPY COLBFVEN.
       COPY COLBFRCV.
       COPY COLBFCAN.
       COPY COLBFTOC.
       COPY COLBFRDT.
       COPY COLBFDLC.
       COPY COLBFEVC.
       COPY COLBFICG.
       COPY COLBGVEN.
       COPY SGCBFOPE.
       COPY TABBFCAM.
       COPY TABBVOFI.
       COPY COLBFMCC.
       COPY TABBFOFI.
       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBGCAM.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGPES.
      *SMC-INI 09.09.1993
       COPY COLBGINT.
      *SMC-FIN 09.09.1993
       COPY GNSBGCVI.
