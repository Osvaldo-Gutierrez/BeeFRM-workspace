       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPKVEN.
       AUTHOR.          CONSIST.
      * Programa de Carga de Archivo VEN Nuevo.
       ENVIRONMENT DIVISION.
      *====================
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
      *COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-VEN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OLD-REQA==.
      *<<<< WSS
      *COPY GNSBRMSG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGIFD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY COLBRVEN.
      *COPY COLBROLD.         COPIAR DENTRO DEL PROGRAMA.
      *    Vencimientos
      *    ------------
      *
      *    Nombre Registro : OLD
      *    Clave(s)        : OLD-KEY-IOLD(UU), OLD-KEY-IREG(UU)
      *    Largo           : 117
      *    Bloqueo         : 1
      *    Observaciones   :
      *
      *    02) Modificaciones 20/04/88, LJV
      *
       01  OLD.
      *
      *    Filler Compatibilidad VSAM/MVS
           03  OLD-GLS-FLAG                             PIC X(01).
      *
      *    Clave Primaria Registro
           03  OLD-KEY-IREG.
      *
      *        Identificacion Invocacion
               05  OLD-KEY-IINV.
      *
      *            Indicador Acceso Invocacion ( I/O )
                   07  OLD-IND-AINV                     PIC X(01).
      *
      *            Time Stamp Invocacion
                   07  OLD-STP-IINV.
      *                Fecha Invocacion
                       09  OLD-FEC-FINV.
                           11  OLD-NUM-SINV             PIC 9(02).
                           11  OLD-NUM-AINV             PIC 9(02).
                           11  OLD-NUM-MINV             PIC 9(02).
                           11  OLD-NUM-DINV             PIC 9(02).
      *
      *                Hora Invocacion
                       09  OLD-HRA-HRIN.
                           11  OLD-NUM-HHIN             PIC 9(02).
                           11  OLD-NUM-MMIN             PIC 9(02).
                           11  OLD-NUM-SSIN             PIC 9(02).
      *
      *            Codigo Origen Invocacion
                   07  OLD-COD-OINV.
      *
      *                Miscelaneo Tipo Origen
                       09  OLD-MSC-TINV                 PIC X(01).
      *
      *                Codigo Tipo Origen
                       09  OLD-COD-TINV                 PIC X(03).
      *
      *KEY     Identificacion OLDcimientos
               05  OLD-KEY-IOLD.
      *
      *            Cod. Int. Comp. Operacion Credito
                   07  OLD-CIC-IOPC.
      *
      *                Centro Asignacion Identificacion
                       09  OLD-CAI-IOPC.
                           11  OLD-COD-TOPC             PIC X(01).
                           11  OLD-COD-COPC             PIC X(03).
      *
      *                Ident. Interna Computacional
                       09  OLD-IIC-IOPC.
                           11  OLD-COD-OOPC.
                               13  OLD-COD-SOPC         PIC X(02).
                               13  OLD-IND-SOPC         PIC X(01).
                           11  OLD-GLS-NOPC             PIC X(05).
      *
      *            Numero Identificacion OLDcimiento
                   07  OLD-NUM-IOLD                     PIC 9(03).
      *
      *REQ Fecha OLDcimiento
           03  OLD-FEC-FOLD.
               05  OLD-NUM-SOLD                         PIC 9(02).
               05  OLD-NUM-AOLD                         PIC 9(02).
               05  OLD-NUM-MOLD                         PIC 9(02).
               05  OLD-NUM-DOLD                         PIC 9(02).
      *
      *REQ Fecha Inicio Mora OLDcimiento
           03  OLD-FEC-FIMV.
               05  OLD-NUM-SIMV                         PIC 9(02).
               05  OLD-NUM-AIMV                         PIC 9(02).
               05  OLD-NUM-MIMV                         PIC 9(02).
               05  OLD-NUM-DIMV                         PIC 9(02).
      *
      *REQ Valor Capital OLDcimiento
           03  OLD-VAL-CAPI             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Saldo Valor Capital OLDcimiento
           03  OLD-VAL-SCAP             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Int DeOLDgados no incluidos en Valor Final
           03  OLD-VAL-IDNI             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Valor Final OLDcimiento
           03  OLD-VAL-FINA             COMP-3          PIC 9(11)V9(04).
      *
      *REQ Saldo Valor Final OLDcimiento
           03  OLD-VAL-SFIN             COMP-3          PIC 9(11)V9(04).
      *
      *    Situacion Contable                           COL SCTB
           03  OLD-IND-SCTB                             PIC X(03).
      *
      *REQ Numero Identificacion OLDcimiento
           03  OLD-NUM-IVCT                             PIC 9(03).
      *
      *    Numero Identificacion Estructura OLDcimientos
           03  OLD-NUM-IEVC                             PIC 9(03).
      *
      *REQ Numero Identificacion Documento Asociado
           03  OLD-NUM-IDLC                             PIC 9(03).
      *
      *    Situacion Cobranza                           COL SCBZ
           03  OLD-IND-SCBZ                             PIC X(05).
      *
      *ING-02
      *    Indicador Cargo Automatico                   GNS VIGE
           03  OLD-IND-CAUT                             PIC X(01).
      *
      *MOD-02  PIC X(09)
      *    Glosa Disponible
           03  OLD-GLS-DISP                             PIC X(08).
       01  WSS-VARI.
           03 WSS-NUM-ROLD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RNEW           VALUE 0      PIC 9(05).
           03 WSS-NUM-RMOD           VALUE 0      PIC 9(05).
           03 WSS-NUM-RBAD           VALUE 0      PIC 9(05).
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPKVEN' TO FIO-PROG.
           PERFORM BUS-FSIS.
      *    PERFORM OPEN-FILE.
           PERFORM LUP-PUT-REG.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           COPY GNSBGGBK.
      ******************************************************************
       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL ABRIR ARCHIVO ANTIGUAS TABLAS'
               DISPLAY 'FIO-STAT = ' FIO-STAT
               STOP RUN.
       FIN-OPEN-FILE.
           EXIT.
      ******************************************************************
       LUP-PUT-REG SECTION.
      *-------------------
       INI-LUP-REG.
      * Programacion de Ciclo para Registro OLD
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO HAY REGISTROS EN ARCHIVO ANTIGUO '
               GO TO FIN-LUP-REG.
      *         MOVE 'COL'          TO MSG-COD-SIST
      *         MOVE 'VEN    VENNX' TO MSG-COD-MENS
      *         PERFORM GET-MSG
      *         DISPLAY MSG-GLS-DESC   VEN-KEY-IVEN IN VEN
       LUP-OLD-NEW.
      * Proceso de Seleccion y Clasificacion a Partir del Registro ANTIG
           ADD 1 TO WSS-NUM-ROLD IN WSS-VARI.
           PERFORM BUS-REG-NEW.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-OLD.
           IF FIO-STAT-OKS
               GO TO LUP-OLD-NEW.
       FIN-LUP-REG.
           EXIT.
      ******************************************************************
      * Formacion y Generacion del Registro NEW al Procesar Registro ANT
       BUS-REG-NEW SECTION.
       INI-BUS-REG-NEW.
      * Formacion del Registro NEW
           MOVE SPACES       TO VEN.
           MOVE OLD-KEY-IOLD TO VEN-KEY-IVEN.
           MOVE OLD-FEC-FOLD TO VEN-FEC-FVEN.
           MOVE OLD-FEC-FIMV TO VEN-FEC-FIMV.
           MOVE OLD-VAL-CAPI TO VEN-VAL-CAPI.
           MOVE OLD-VAL-SCAP TO VEN-VAL-SCAP.
           MOVE OLD-VAL-IDNI TO VEN-VAL-IDNI.
           MOVE OLD-VAL-FINA TO VEN-VAL-FINA.
           MOVE OLD-VAL-SFIN TO VEN-VAL-SFIN.
           MOVE OLD-IND-SCTB TO VEN-IND-SCTB.
           MOVE OLD-NUM-IVCT TO VEN-NUM-IVCT.
           MOVE OLD-NUM-IEVC TO VEN-NUM-IEVC.
           MOVE OLD-NUM-IDLC TO VEN-NUM-IDLC.
           MOVE OLD-IND-CAUT TO VEN-IND-CAUT.
           MOVE OLD-IND-SCBZ TO VEN-IND-SCBZ.
           MOVE ZEROES       TO VEN-VAL-MTSC.
      * Genera el Registro NEW
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           IF NOT FIO-STAT-OKS
               MOVE FIO-PUT TO FIO-CMND
               PERFORM COL-FIO-VEN
               ADD 1 TO WSS-NUM-RNEW IN WSS-VARI
           ELSE
               MOVE FIO-MOD TO FIO-CMND
               PERFORM COL-FIO-VEN
               ADD 1 TO WSS-NUM-RMOD IN WSS-VARI.
           IF NOT FIO-STAT-OKS
               ADD 1 TO WSS-NUM-RBAD IN WSS-VARI
               DISPLAY 'REGISTRO ERRONEO : '  VEN.
       FIN-BUS-REG-NEW.
           EXIT.
      ******************************************************************
       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           DISPLAY 'REGISTROS OLD LEIDOS   : ' WSS-NUM-ROLD IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS   : ' WSS-NUM-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS NEW GRABADOS : ' WSS-NUM-RNEW IN WSS-VARI.
           DISPLAY 'REGISTROS MODIFICADOS  : ' WSS-NUM-RMOD IN WSS-VARI.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-VEN.
           PERFORM COL-FIO-OLD.
       FIN-CLOSE-FILE.
           EXIT.
      ******************************************************************
      *COPY GNSBFMSG.
      *COPY GNSBBMSG.
       COPY GNSBGDTC.
       COPY GNSBGIFD.
       COPY GNSBTABT.
       COPY COLBFVEN.
      *COPY COLBFOLD.            COPIAR DENTRO DEL PROGRAMA.
      * Modulo FIO para registro OLD en DATACOM, 16-SEP-1988 17:56:16.88
       COL-FIO-OLD SECTION.
       INI-COL-FIO-OLD.
           MOVE ADR-OLD-REQA TO ADR-REQA.
           MOVE 'VEN' TO ADR-TABL IN ADR-REQA.
           MOVE IFD-GSIS( 14 ) TO ADR-DBID IN ADR-REQA.
           MOVE 'VEN00' TO ADR-ELM1.
           MOVE FIO-PROG TO ADR-PROG.
           IF FIO-KDEF = FIO-KDEF-S
               GO TO CON-COL-FIO-OLD.
           MOVE FIO-CMND TO FIO-IAKY.
           IF NOT FIO-IAKY-CON AND
              ( FIO-AKEY = 'OLD-KEY-IOLD' OR FIO-AKEY NOT > SPACES )
               MOVE OLD-KEY-IOLD IN OLD TO ADR-VKEY IN ADR-REQA
               MOVE 'VEN00' TO ADR-DKEY IN ADR-REQA
           ELSE
               MOVE 'En COL-FIO-OLD, key' TO FIO-MENS
               MOVE FIO-AKEY   TO FIO-MENS-KEY
               MOVE 'Invalida' TO FIO-MENS-FIL
               GO TO PRG-ABT.
       CON-COL-FIO-OLD.
           MOVE OLD TO FIO-DFLD.
           PERFORM GNS-FIO-DTC.
           IF FIO-STAT-OKS
               MOVE FIO-DFLD TO OLD
           ELSE
               MOVE 'COLOLD' TO FIO-MEN2
               IF FIO-STAT-FTL
                   GO TO PRG-ABT.
           MOVE SPACES   TO FIO-AKEY.
           MOVE FIO-KDEF-N TO FIO-KDEF.
           MOVE ADR-REQA TO ADR-OLD-REQA.
       FIN-COL-FIO-OLD.
           EXIT.
