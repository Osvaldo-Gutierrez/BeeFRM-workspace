       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGINY.
       AUTHOR.          BEE TEAM (EX-CONSIST) (RBG).
       DATE-WRITTEN.    20-ENE-1997.
      *
      * Genera archivo INY con cargos/abonos a cuentas corrientes
      * via inyector.
      ***************************************************************
      * MODIFICADO : 28.05.1998.  JUAN ANTONIO DIAZ REYES.          *
      *              SE CAMBIA LA SENTENCIA 'ON 10 AND EVERY 10'    *
      *              POR UN CONTADOR.                               *
      ***************************************************************
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION  SECTION.
      *---------------------
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAN-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CYA-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-ROV-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TOC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *RFU INI  21-ABR-1998
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
      *RFU FIN  21-ABR-1998
      *<<<< WSS
       COPY GNSBRMSG.
       COPY GNSBRMSC.
       COPY GNSBRTAB.
       COPY COLBRCAN.
      *RFU INI  21-ABR-1998
       COPY COLBROPC.
      *RFU FIN  21-ABR-1998
       COPY COLBRCYA.
       COPY COLBRTOC.
       COPY SGCBROPE.
       COPY COLBRROV.
       COPY COLBRINY.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY TABBROFI.
       01  WR-TOPE                                PIC 9(02) VALUE 10.
       01  WR-CICLO-LUP-CAN-CYA                   PIC 9(02) VALUE 0.
       01  W-VARI-RIN.
           03 W-MVO-OPEC                          PIC X(08).
       01  WSS-VARI.
           03 WSS-COD-OFIC.
              05 WSS-IND-OFIC        OCCURS 10    PIC X(03).
           03 WSS-I                  VALUE 0      PIC 9(02).
           03 WSS-FEC-FPRO.
              05 WSS-NUM-DPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-SPRO        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-APRO        VALUE ZEROS  PIC 9(02).
           03 WSS-IND-TPRO           VALUE SPACES PIC X(01).
           03 WSS-IND-DISP           VALUE 'S'    PIC X(01).
           03 WSS-FEC-FFEC.
              05 WSS-NUM-SFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-AFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-MFEC        VALUE ZEROS  PIC 9(02).
              05 WSS-NUM-DFEC        VALUE ZEROS  PIC 9(02).
           03 WSS-IND-MCYA           VALUE ZEROS  PIC 9(01).
           03 WSS-NUM-RLEI-CAN       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RMOD-CAN       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RERR-CAN       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RLEI-CYA       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RERR-CYA       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RGRA-INY       VALUE ZEROS  PIC 9(09).
           03 WSS-NUM-RERR-INY       VALUE ZEROS  PIC 9(09).
           03 WSS-EDT-RLEI-CAN                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RMOD-CAN                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RERR-CAN                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RLEI-CYA                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RERR-CYA                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RGRA-INY                    PIC ZZZ.ZZZ.ZZZ.
           03 WSS-EDT-RERR-INY                    PIC ZZZ.ZZZ.ZZZ.
           03  WSS-GLS-ANO.
               05  WSS-GLS-ANO1                   PIC X(01).
               05  WSS-GLS-ANO2                   PIC X(01).
           03 WSS-COD-MSG1          VALUE SPACES  PIC X(12).
           03 WSS-COD-MSG2          VALUE SPACES  PIC X(12).

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *************
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGINY' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM PARAM-INY.
           PERFORM LUP-PUT-INY.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

      ******************************************************************
       OPEN-FILE SECTION.
      ******************
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-TOC.
           PERFORM SGC-FIO-OPE.
           PERFORM COL-FIO-ROV.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-INY.
       FIN-OPEN-FILE.
           EXIT.

      ******************************************************************
       PARAM-INY SECTION.
      ******************
       INI-PARAM-INY.
      * Rutina para Capturar y Validar Parametros.
           DISPLAY 'INICIO CAPTURA DE PARAMETROS'.
      * Captura Fecha de Proceso.
           ACCEPT WSS-FEC-FPRO IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO            :'WSS-FEC-FPRO
                                                            IN WSS-VARI.
      * Captura Tipo de Proceso.
           ACCEPT WSS-IND-TPRO IN WSS-VARI.
           DISPLAY 'TIPO DE PROCESO             :'WSS-IND-TPRO
                                                            IN WSS-VARI.
      * Captura Indicador Display.
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY 'INDICADOR DE DISPLAY (S/N)  :'WSS-IND-DISP
                                                            IN WSS-VARI.
           DISPLAY 'FIN CAPTURA DE PARAMETROS'.
           DISPLAY ' '.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.

      * VAL-FEC Valida Fecha de Proceso.
           MOVE WSS-FEC-FPRO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-ABT
           ELSE
               DISPLAY 'VALIDACION FECHA OK.'.

      * Valida tipo de proceso.
           IF WSS-IND-TPRO IN WSS-VARI NOT > SPACES
               MOVE 'C' TO WSS-IND-TPRO IN WSS-VARI
           ELSE
               IF WSS-IND-TPRO IN WSS-VARI NOT = 'R' AND
                  WSS-IND-TPRO IN WSS-VARI NOT = 'C'
                   DISPLAY 'TIPO DE PROCESO INVALIDO' WSS-IND-TPRO
                                                       IN WSS-VARI
                   PERFORM GNS-PRO-ABT
               ELSE
                   DISPLAY 'VALIDACION TIPO PROCESO OK.'.

      * Valida codigo de display (S/N).
           IF WSS-IND-DISP IN WSS-VARI NOT = ('S' AND 'N')
               DISPLAY 'CODIGO DE DISPLAY NO ES VALIDO, '
               DISPLAY 'DEBE INGRESAR S/N '
               DISPLAY 'PROCESO CANCELADO...'
               PERFORM GNS-PRO-ABT
           ELSE
               DISPLAY 'VALIDACION INDICADOR DE DISPLAY OK.'.

           MOVE WSS-NUM-DPRO IN WSS-VARI TO WSS-NUM-DFEC IN WSS-VARI.
           MOVE WSS-NUM-MPRO IN WSS-VARI TO WSS-NUM-MFEC IN WSS-VARI.
           MOVE WSS-NUM-SPRO IN WSS-VARI TO WSS-NUM-SFEC IN WSS-VARI.
           MOVE WSS-NUM-APRO IN WSS-VARI TO WSS-NUM-AFEC IN WSS-VARI.
           DISPLAY 'FIN    VALIDACION DE PARAMETROS'.
           DISPLAY ' '.

      * INICIALIZO INY.
           MOVE SPACES TO INY.
           MOVE ZEROES TO INY-SGV-MNTO IN INY.
           MOVE ZEROES TO INY-FEC-FMVO IN INY.
       FIN-PARAM-INY.
           EXIT.

      ******************************************************************
       LUP-PUT-INY SECTION.
      ********************
       INI-LUP-CAN-INY.
      * Programacion de Ciclo para Registro CAN.
           MOVE WSS-FEC-FFEC IN WSS-VARI TO CAN-FEC-FOUT IN CAN.
           MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
           MOVE FIO-GET-NLS    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT (FIO-STAT-OKS AND
              CAN-FEC-FOUT IN CAN = WSS-FEC-FFEC IN WSS-VARI)
               DISPLAY 'NO HAY REGISTROS CAN PARA FECHA DE PROCESO :'
                                              WSS-FEC-FPRO IN WSS-VARI
      *                                       WSS-FEC-FFEC IN WSS-VARI
               GO TO FIN-LUP-PUT-INY.
       LUP-CAN-INY.
      * Proceso de seleccion y clasificacion a partir del registro CAN
           IF WSS-IND-TPRO IN WSS-VARI = 'R'
               IF CAN-MSC-STAT IN CAN = 'G' OR 'H'
                   NEXT SENTENCE
               ELSE
                   GO TO NXT-CAN-INY
           ELSE
               IF CAN-MSC-STAT IN CAN = 'G'
                   NEXT SENTENCE
               ELSE
                   GO TO NXT-CAN-INY.

           ADD  1  TO WSS-NUM-RLEI-CAN IN WSS-VARI.

      * Busca los Cargos para Cancelaciones de Operacion de Credito Dete
           MOVE SPACES              TO CYA-KEY-ICYA IN CYA.
           MOVE CAN-KEY-ICAN IN CAN TO CYA-KEY-ICAN IN CYA.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF NOT FIO-STAT-OKS OR
              CYA-KEY-ICAN IN CYA NOT = CAN-KEY-ICAN IN CAN
               MOVE 'COL'          TO MSG-COD-SIST
               MOVE 'CAN    CYANX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC   CAN-KEY-ICAN IN CAN
               ADD 1               TO WSS-NUM-RERR-CAN IN WSS-VARI
               GO TO NXT-CAN-INY.
       LUP-CAN-CYA.
           ADD 1    TO WSS-NUM-RLEI-CYA IN WSS-VARI.
           PERFORM GEN-REG-INY.
           IF WSS-COD-MSG2 IN WSS-VARI > SPACES
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE WSS-COD-MSG2 IN WSS-VARI TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC WSS-COD-MSG2
               MOVE FIO-BCK-OUT          TO FIO-CMND
               PERFORM COL-FIO-CAN
               DISPLAY 'BCK-OUT-CAN : ' CAN-KEY-ICAN IN CAN
               GO TO NXT-CAN-INY.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-CYA.
           IF FIO-STAT-OKS AND
              CYA-KEY-ICAN IN CYA = CAN-KEY-ICAN IN CAN
               GO TO LUP-CAN-CYA.

      *    ON 10 AND EVERY 10
           ADD 1 TO WR-CICLO-LUP-CAN-CYA.
           IF WR-CICLO-LUP-CAN-CYA = WR-TOPE
              MOVE ZEROES      TO WR-CICLO-LUP-CAN-CYA
              MOVE FIO-CHK-PNT TO FIO-CMND
              PERFORM COL-FIO-CAN.

       NXT-CAN-INY.
           MOVE 'CAN-FEC-FOUT' TO FIO-AKEY.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF FIO-STAT-OKS AND
              CAN-FEC-FOUT IN CAN = WSS-FEC-FFEC IN WSS-VARI
              GO TO LUP-CAN-INY.
       FIN-LUP-PUT-INY.
           EXIT.

      ******************************************************************
       GEN-REG-INY SECTION.
      ********************
       INI-GEN-REG-INY.
           MOVE SPACES TO WSS-COD-MSG1 IN WSS-VARI.
           MOVE SPACES TO WSS-COD-MSG2 IN WSS-VARI.
           MOVE CAN-NUM-AOUT IN CAN TO WSS-GLS-ANO IN WSS-VARI.
           PERFORM GRA-REG-INY.
           IF WSS-COD-MSG1 IN WSS-VARI > SPACES
               ADD 1 TO WSS-NUM-RERR-INY IN WSS-VARI
               GO TO FIN-GEN-REG-INY
           ELSE
               ADD 1 TO WSS-NUM-RGRA-INY IN WSS-VARI.

       SKP-GEN-REG-INY.
      * Marca como procesada la CAN para que no la considere si se repro
           IF CAN-MSC-STAT IN CAN = 'H'
               GO TO FIN-GEN-REG-INY.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NEX' TO WSS-COD-MSG2 IN WSS-VARI
               ADD 1 TO WSS-NUM-RERR-CAN IN WSS-VARI
               GO TO FIN-GEN-REG-INY.

           MOVE 'H'     TO CAN-MSC-STAT IN CAN.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           IF NOT FIO-STAT-OKS
               MOVE 'CAN    NOMOD' TO WSS-COD-MSG2 IN WSS-VARI
               ADD 1 TO WSS-NUM-RERR-CAN IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-RMOD-CAN IN WSS-VARI.
       FIN-GEN-REG-INY.
           EXIT.

      ******************************************************************
      * Graba archivo INY.
       GRA-REG-INY SECTION.
      ********************
       INI-GRA-INY.
      *    MOVE 8                        TO INY-NUM-TREG IN INY.
           MOVE 7                        TO INY-NUM-TREG IN INY.
           MOVE CYA-IIC-ICYA IN CYA      TO INY-NUM-NCTA IN INY.
      *RRM INI 04/03/1998
      *    MOVE 1                        TO INY-NUM-IMPU IN INY.
           MOVE 9                        TO INY-NUM-IMPU IN INY.
      *RRM FIN 04/03/1998
           MOVE SPACES                   TO INY-COD-SCHE IN INY.
           MOVE CYA-SGV-LIQU IN CYA      TO INY-SGV-MNTO IN INY.
      *    MOVE 9                        TO INY-COD-TMVO IN INY.
           MOVE 'I'                      TO INY-COD-TMVO IN INY.
           MOVE ZEROES                   TO INY-NUM-CBAN IN INY.
           MOVE SPACES                   TO INY-COD-DIS1 IN INY.
           MOVE CAN-NUM-DOUT IN CAN      TO INY-NUM-DMVO IN INY.
           MOVE CAN-NUM-MOUT IN CAN      TO INY-NUM-MMVO IN INY.
           MOVE WSS-GLS-ANO2 IN WSS-VARI TO INY-NUM-AMVO IN INY.
           MOVE ZEROES                   TO INY-IND-TATM IN INY.
           MOVE CAN-CIC-IOPC IN CAN      TO INY-CIC-IOPC IN INY.
           MOVE SPACES                   TO INY-GLS-DIS2 IN INY.
           MOVE CAN-COD-OFCA IN CAN      TO INY-COD-SORI IN INY.
           MOVE CAN-NUM-ICAN IN CAN      TO INY-NUM-ICAN IN INY.
           MOVE SPACES                   TO INY-GLS-DIS3 IN INY.
           MOVE CAN-COD-OFCA IN CAN      TO INY-COD-SREC IN INY.
           MOVE SPACES                   TO INY-GLS-DIS4 IN INY.
           PERFORM VERIFICA-TIPO
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-INY.
           IF NOT FIO-STAT-OKS
               MOVE 'ERR AL GRAB' TO WSS-COD-MSG1 IN WSS-VARI
           ELSE
               MOVE SPACES TO WSS-COD-MSG1 IN WSS-VARI.
       FIN-GRA-REG-INY.
           EXIT.

      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *****************************
       INI-MUESTRA-ESTADISTICAS.
           MOVE WSS-NUM-RLEI-CAN TO WSS-EDT-RLEI-CAN.
           MOVE WSS-NUM-RMOD-CAN TO WSS-EDT-RMOD-CAN.
           MOVE WSS-NUM-RERR-CAN TO WSS-EDT-RERR-CAN.
           MOVE WSS-NUM-RLEI-CYA TO WSS-EDT-RLEI-CYA.
           MOVE WSS-NUM-RERR-CYA TO WSS-EDT-RERR-CYA.
           MOVE WSS-NUM-RGRA-INY TO WSS-EDT-RGRA-INY.
           MOVE WSS-NUM-RERR-INY TO WSS-EDT-RERR-INY.
           DISPLAY ' '.
           DISPLAY ' ESTADISTICAS DEL PROCESO'.
           DISPLAY ' ========================'.
           DISPLAY ' '.
           DISPLAY ' REG. CAN LEIDOS          : 'WSS-EDT-RLEI-CAN.
           DISPLAY ' REG. CAN MODIFICADOS     : 'WSS-EDT-RMOD-CAN.
           DISPLAY ' REG. CAN ERRONEOS        : 'WSS-EDT-RERR-CAN.
           DISPLAY ' REG. CYA LEIDOS          : 'WSS-EDT-RLEI-CYA.
           DISPLAY ' REG. CYA ERRONEOS        : 'WSS-EDT-RERR-CYA.
           DISPLAY ' REG. INY GRABADOS        : 'WSS-EDT-RGRA-INY.
           DISPLAY ' REG. INY NO GRAB.C/ERROR : 'WSS-EDT-RERR-INY.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.

      ******************************************************************
       CLOSE-FILE SECTION.
      *******************
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-CAN.
           PERFORM COL-FIO-CYA.
           PERFORM COL-FIO-TOC.
           PERFORM SGC-FIO-OPE.
           PERFORM COL-FIO-ROV.
           PERFORM COL-FIO-INY.
       FIN-CLOSE-FILE.
           EXIT.

      ******************************************************************
      * Copys necsarios para un funcionamiento del programa.

       COPY GNSBFMSG.
       COPY GNSBFMSC.
       COPY GNSBFTAB.
       COPY COLBFINY.
      *RFU INI  21-ABR-1998
       COPY COLBFOPC.
      *RFU FIN  21-ABR-1998
       COPY COLBFCAN.
       COPY COLBFCYA.
       COPY COLBFTOC.
       COPY SGCBFOPE.
       COPY COLBFROV.
       COPY TABBVOFI.
       COPY TABBFOFI.
       COPY GNSBBMSG.
       COPY GNSBBMSC.
       COPY GNSBBTAB.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY COLBGTCT.

      *RFU INI  21-ABR-1998
       VERIFICA-TIPO.
      *--------------
           MOVE 'TAB'               TO FIO-SIST.
           MOVE 'TCYA'              TO MSC-COD-TMSC IN MSC.
           MOVE CYA-IND-TCYA IN CYA TO MSC-COD-CMSC IN MSC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-MSC.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO MSC-COD-DAT1 IN MSC(3)
           ELSE
      * SE PROCESAN CARGOS EN CTA. CTE. SEGUN INDICADOR MISMO U
      * Y ORIGEN DE BOLETAS DE GARANTIA
           IF MSC-COD-DAT1 IN MSC(3) = 'O'
                   MOVE 'U'                TO INY-COD-TMVO IN INY
           ELSE
                   PERFORM BUSCA-OPEC
                   IF W-MVO-OPEC = 'B'
      *RRM-INI 19-MAY-1999
      *                    MOVE 'Z'            TO INY-COD-TMVO IN INY.
                           MOVE 'Z'            TO INY-COD-TMVO IN INY
      * PAGO DE INTERESES CON CARGO EN CUENTA CORRIENTE
                   ELSE IF CYA-IND-INTE IN CYA = 'I'
                           MOVE 8               TO INY-NUM-TREG IN INY
                           MOVE 'T'             TO INY-COD-TMVO IN INY.
      *RRM-FIN 19-MAY-1999


      * BUSCA CON CLASE EL COMO OPERA DE LA OPC
       BUSCA-OPEC.
      *-----------
           MOVE SPACES TO W-MVO-OPEC
           MOVE CAN-CIC-IOPC IN CAN TO OPC-CIC-IOPC IN OPC
           MOVE FIO-GET-KEY TO FIO-CMND
           PERFORM COL-FIO-OPC
           IF FIO-STAT-OKS
              MOVE OPC-COD-TOOC IN OPC TO TOC-COD-TOCR IN TOC
              MOVE FIO-GET-KEY TO FIO-CMND
              PERFORM COL-FIO-TOC
               IF FIO-STAT-OKS
                  MOVE 'COL'               TO MSC-COD-SIST FIO-SIST
                  MOVE 'OPEC'              TO MSC-COD-TMSC IN MSC
                  MOVE TOC-MSC-OPEC IN TOC TO MSC-COD-CMSC IN MSC
                  MOVE FIO-GET-KEY TO FIO-CMND
                  PERFORM GNS-FIO-MSC
                   IF FIO-STAT-OKS
                      MOVE MSC-CIC-CMSC IN MSC TO W-MVO-OPEC.
      *RFU FIN  21-ABR-1998

