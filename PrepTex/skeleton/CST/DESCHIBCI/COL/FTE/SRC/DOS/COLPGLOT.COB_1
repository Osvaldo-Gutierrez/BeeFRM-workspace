       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      COLPGLOT.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.
      * Programa Separador de Interfaz Contable en Lotes
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY COLBRLT1.
       COPY COLBRLT2.
       COPY COLBRLT3.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       01  WSS-VARI.
      *
      * DEFINICION DE CAMPOS PARA ARCHIVO DE INTERFAZ PARA CONTABILIDAD
      *
      *****CABECERA*****
      *   CABECERA DE LOTE (BATCH HEADER)
           03 WSS-FZE-CABECERA.
      *      CABECERA-LOTE
              05 WSS-FZE-COD-CABE     VALUE 'BH'     PIC X(02).
      *      COMPANIA
              05 WSS-FZE-COD-CCIA     VALUE 'BCI '   PIC X(04).
      *      COD.IDENTIFICACION
              05 WSS-FZE-COD-IDEN                    PIC X(02).
      *      NUMERO DE LOTE
              05 WSS-FZE-NUM-LOTE                    PIC 9(02).
      *      TIPO DE MOVIMIENTO
              05 WSS-FZE-COD-MVTO     VALUE 2        PIC 9(01).
      *      TOTAL DE LOTE
              05 WSS-FZE-SGV-LOTE     VALUE ZEROES   PIC 9(14)V9(02).
      *      PERIODO
              05 WSS-FZE-COD-PERI     VALUE 0        PIC 9(01).
      *      FECHA
              05 WSS-FZE-FEC-FECH                    PIC X(06).
      *
      *   DETALLE DEL MOVIMIENTO (**PRIMER REGISTRO**)
      *
           03 WSS-FZE-PMERREG.
      *      IDENTIFICACION DEL REGISTRO.
              05 WSS-FZE-COD-IDE1     VALUE 'PT'     PIC X(02).
      *      NUMERO DE MOVIMIENTO
              05 WSS-FZE-NUM-MOV1                    PIC 9(04).
      *      PRIMER REGISTRO
              05 WSS-FZE-NUM-REG1     VALUE 1        PIC 9(01).
      *      DEBE-HABER
              05 WSS-FZE-NUM-DEHA                    PIC 9(02).
      *      COMPANIA
              05 WSS-FZE-COD-CIA1     VALUE 'BCI '   PIC X(04).
      *      CUENTA
              05 WSS-FZE-COD-CCSB                    PIC 9(09).
      *      BLANCOS
              05 WSS-FZE-COD-BLCO     VALUE SPACES   PIC X(09).
      *      SUCURSAL
              05 WSS-FZE-COD-SUCU                    PIC X(12).
      *      SOURCE CODE
              05 WSS-FZE-COD-CODE                    PIC X(10).
      *      FECHA
              05 WSS-FZE-FEC-FCTB                    PIC X(06).
      *      MONTO
              05 WSS-FZE-NUM-MONT     VALUE ZEROES   PIC 9(13)V9(02).
      *
      *
      *    (**SEGUNDO REGISTRO**)
      *
           03 WSS-FZE-SGDOREG.
              05 WSS-FZE-COD-IDE2     VALUE 'PT'     PIC X(02).
              05 WSS-FZE-NUM-MOV2                    PIC 9(04).
              05 WSS-FZE-NUM-REG2     VALUE 2        PIC 9(01).
              05 WSS-FZE-GLS-GLOS                    PIC X(30).
              05 WSS-FZE-COD-VIGE                    PIC X(12).
              05 WSS-FZE-COD-DESC                    PIC X(10).
      *
      *>>>>
           03 WSS-FZE-PMERREG-LT2.
      *      FILLER
              05 FILLER-LT2                       PIC X(59).
      *      MONTO
              05 WSS-FZE-NUM-MONT-LT2  VALUE ZEROES  PIC 9(11)V9(02).
              05 WSS-GLS-RESTANTE-1-LT2           PIC X(08).
      *
      *    AUX JUS
           03 WSS-MONTO                     PIC 9(13)V9(02).
           03 WSS-MONTO-GRABA               PIC 9(13)V9(02).
           03 WSS-TOPE VALUE 99999999999    PIC 9(11).
           03 WSS-AUXI                      PIC 9(11)V9(02).
           03 WSS-RESTO                     PIC 9(11)V9(02).
           03 WSS-INDI-I                    PIC 9(02).
           03 WSS-INDI-K                    PIC 9(02).
           03 WSS-CONT-LOTES                PIC 9(02).
           03 WSS-NUM-CORR                  PIC 9(02).
      * Contadores para las Estadisticas.
           03 WSS-NUM-RLEI      VALUE ZEROES PIC 9(06).
           03 WSS-NUM-RLCB      VALUE ZEROES PIC 9(06).
           03 WSS-NUM-CON2     VALUE ZEROES PIC 9(06).
           03 WSS-NUM-CON3     VALUE ZEROES PIC 9(06).
           03 WSS-NUM-CAB2     VALUE ZEROES PIC 9(06).
           03 WSS-NUM-CAB3     VALUE ZEROES PIC 9(06).


      *
       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'COLPGLOT' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM INICIO.
           PERFORM INP-SRT.
           PERFORM CLOSE-FILE.
           PERFORM MUESTRA-ESTADISTICAS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-LT1.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-LT2.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM COL-FIO-LT3.
       FIN-OPEN-FILE.
           EXIT.
      *
       INICIO SECTION.
       INI-INICIO.
           DISPLAY 'INICIO GENERACION DE INTERFAZ EN LOTES'.
       FIN-INICIO.
           EXIT.
      *
       INP-SRT SECTION.
      *---------------*
       INI-INP-SRT.
      *-----------*
      *INI-JGM 09-04-1998
           MOVE ZEROES TO WSS-NUM-RLEI IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-RLCB IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-CON2 IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-CON3 IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-CAB2 IN WSS-VARI.
           MOVE ZEROES TO WSS-NUM-CAB3 IN WSS-VARI.
      *FIN-JGM 09-04-1998

           MOVE ZEROES TO WSS-NUM-CORR.
           MOVE 1      TO WSS-CONT-LOTES.
      * Lectura Cabecera del Interfaz, no se utilizara.
           PERFORM LEE-REG-LT1.
           IF NOT (FIO-STAT-OKS AND LT1-IND-TREG IN LT1 = 'BH')
              DISPLAY 'NO EXISTEN REGISTROS A PROCESAR'
      *       PERFORM GNS-PRO-ABT
              GO TO FIN-INP-SRT.
           MOVE LT1 TO WSS-FZE-CABECERA.
           PERFORM GRABA-CABECERA.

       LUP-INP-SRT.
      *-----------*
      * Loop de lectura para registros de tipo 1
           PERFORM LEE-REG-LT1.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.


      * Redefino Reg.Interzaf a formato de Primer Registro
           MOVE LT1 TO WSS-FZE-PMERREG.

      * Verifica que tipo de registro sea correcto
           IF WSS-FZE-NUM-REG1 NOT = 1
              DISPLAY 'ERROR AL PROCESAR EL REG. '
                       WSS-NUM-RLEI  IN WSS-VARI
                       ' ARCHIVO : LT1'
              GO TO   GNS-PRO-ABT.


      * Loop de lectura para registros de tipo 2
           PERFORM LEE-REG-LT1.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.



           MOVE LT1 TO WSS-FZE-SGDOREG.

      * Redefino Reg.Interzaf a formato de Segundo Registro
           IF WSS-FZE-NUM-REG2 NOT = 2
              DISPLAY 'ERROR AL PROCESAR REG. '
                       WSS-NUM-RLEI IN WSS-VARI
                       ' ARCHIVO : LT1 '
              GO TO GNS-PRO-ABT.

      * Asigno Monto a variable de trabajo
           MOVE WSS-FZE-NUM-MONT TO WSS-MONTO.

      * Si monto sobrepasa el tope permitido hay que partirlo
           IF WSS-MONTO > WSS-TOPE
              PERFORM REPARTE-MONTO
              MOVE WSS-RESTO TO WSS-MONTO.

           MOVE WSS-MONTO TO WSS-MONTO-GRABA.
           PERFORM GRABA-DETALLE.
           GO TO LUP-INP-SRT.

       REPARTE-MONTO.
      *-------------*
           MOVE 1 TO WSS-INDI-I WSS-INDI-K.
           DIVIDE WSS-MONTO BY WSS-TOPE GIVING WSS-INDI-I.
           PERFORM REPARTE-MONTO-B UNTIL (WSS-INDI-K > WSS-INDI-I).
           MULTIPLY WSS-TOPE BY WSS-INDI-I GIVING WSS-AUXI.
           SUBTRACT WSS-AUXI FROM WSS-MONTO GIVING WSS-RESTO.

       REPARTE-MONTO-B.
      *---------------*
           MOVE WSS-TOPE TO WSS-MONTO-GRABA.
           PERFORM GRABA-DETALLE.
           ADD 1 TO WSS-INDI-K.

       GRABA-CABECERA.
      *--------------*
           MOVE WSS-CONT-LOTES   TO WSS-FZE-NUM-LOTE.
           MOVE ZEROES           TO WSS-NUM-CORR.
           MOVE WSS-FZE-CABECERA TO LT2.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM COL-FIO-LT2.
           ADD 1 TO WSS-CONT-LOTES.
           ADD 1 TO WSS-NUM-CAB2 IN WSS-VARI.

       GRABA-DETALLE.
      *-------------*
           IF WSS-MONTO-GRABA > ZEROES
               ADD 1                    TO WSS-NUM-CORR
               MOVE WSS-NUM-CORR        TO WSS-FZE-NUM-MOV1
               MOVE WSS-MONTO-GRABA     TO WSS-FZE-NUM-MONT
               MOVE WSS-FZE-PMERREG     TO WSS-FZE-PMERREG-LT2
               MOVE WSS-MONTO-GRABA     TO WSS-FZE-NUM-MONT-LT2
               MOVE SPACES              TO WSS-GLS-RESTANTE-1-LT2
               ADD 1                    TO WSS-NUM-CORR
               MOVE WSS-NUM-CORR        TO WSS-FZE-NUM-MOV2
               IF WSS-FZE-NUM-DEHA = 10
                   MOVE WSS-FZE-PMERREG-LT2 TO LT2
                   MOVE FIO-PUT TO FIO-CMND
                   PERFORM COL-FIO-LT2
                   ADD 1 TO WSS-NUM-CON2 IN WSS-VARI
                   MOVE WSS-FZE-SGDOREG TO LT2
                   MOVE FIO-PUT TO FIO-CMND
                   PERFORM COL-FIO-LT2
                   ADD 1 TO WSS-NUM-CON2 IN WSS-VARI
               ELSE
               IF WSS-FZE-NUM-DEHA = 60
                   MOVE WSS-FZE-PMERREG-LT2 TO LT3
                   MOVE FIO-PUT TO FIO-CMND
                   PERFORM COL-FIO-LT3
                   ADD 1 TO WSS-NUM-CON3 IN WSS-VARI
                   MOVE WSS-FZE-SGDOREG TO LT3
                   MOVE FIO-PUT TO FIO-CMND
                   PERFORM COL-FIO-LT3
                   ADD 1 TO WSS-NUM-CON3 IN WSS-VARI.

       LEE-REG-LT1.
      *-----------*
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM COL-FIO-LT1.
           IF FIO-STAT-OKS
              IF  LT1-IND-TREG IN LT1 = 'BH'
                  ADD 1  TO WSS-NUM-RLCB IN WSS-VARI
              ELSE
      * CONTADOR DE REG. LEIDOS LT1
              ADD 1  TO WSS-NUM-RLEI IN WSS-VARI.

       FIN-INP-SRT.
      *-----------*
           EXIT.


       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-LT1.
           PERFORM COL-FIO-LT2.
           PERFORM COL-FIO-LT3.
       FIN-CLOSE-FILE.
           EXIT.

      *INI-JGM 09-04-1998.
      ******************************************************************
       MUESTRA-ESTADISTICAS SECTION.
      *****************************
       INI-MUESTRA-ESTADISTICAS.
           DISPLAY ' '.
           DISPLAY ' ESTADISTICAS DEL PROCESO COLPGLOT.'.
           DISPLAY ' ---------------------------------'.
           DISPLAY ' '.
           DISPLAY 'REGISTROS LEIDOS DESDE ARCHIVO LT1 : '
                    WSS-NUM-RLEI IN WSS-VARI.
           DISPLAY 'CABECERAS LEIDOS DESDE ARCHIVO LT1 : '
                    WSS-NUM-RLCB IN WSS-VARI.
      *
           DISPLAY 'MOVIMIENTOS GRABADOS EN LT2        : '
                    WSS-NUM-CON2 IN WSS-VARI.
           DISPLAY 'CABECERA GRABADAS EN LT2           : '
                    WSS-NUM-CAB2 IN WSS-VARI.
      *
           DISPLAY 'MOVIMIENTOS GRABADOS EN LT3        : '
                    WSS-NUM-CON3 IN WSS-VARI.
           DISPLAY 'CABECERA GRABADAS EN LT3           : '
                    WSS-NUM-CAB3 IN WSS-VARI.
       FIN-MUESTRA-ESTADISTICAS.
           EXIT.
      *FIN-JGM 09-04-1998.
       COPY COLBFLT1.
       COPY COLBFLT2.
       COPY COLBFLT3.
       COPY GNSBGIDD.
       COPY GNSBBSYS.
       COPY GNSBGEND.
       COPY GNSBGABT.


