       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      LDCPGVPV.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    22-FEB-1999.


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            C01 IS SALTO
            DECIMAL-POINT IS COMMA.


       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CLF-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY LDCBRCLF.
       COPY LDCBRCLS.
       COPY LDCBRVPV.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSWGGRC.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGSYS.
      *Entidades de GNS
       COPY GNSBRMSC.
       COPY GNSWGHOY.
       COPY GNSWGFEC.
       COPY GNSWGCPT.
       COPY GNSWGIDC.
      *Entidades de TAB
       COPY TABBROFI.
      *Entidades de LDC
       COPY LDCBRLDC.
       COPY LDCBRRDC REPLACING RDC BY RDL.
       COPY SGCBRDBC.
       COPY SGCBRDCP.
       COPY SGCBRDCE.
      *
       COPY COLBRRDC.
       01  WSS-VARI.

           03 VEC-GLS-TBAN.
             05  VEC-COD-TBAN       OCCURS 20       PIC X(03).
           03 WSS-CNTD              VALUE ZEROES    PIC 9(02).
           03 WSS-CMAX              VALUE 20        PIC 9(02).

           03 WSS-COD-TBAN          VALUE ZEROES    PIC 9(03).

           03 WSS-REG-LEID          VALUE ZEROES    PIC 9(09).
           03 WSS-REG-SELE          VALUE ZEROES    PIC 9(09).
           03 WSS-REG-GRAB          VALUE ZEROES    PIC 9(09).
           03 WSS-REG-SBAN          VALUE ZEROES    PIC 9(09).
           03 WSS-REG-DSRD          VALUE ZEROES    PIC 9(09).
           03 WSS-REG-RSCL          VALUE ZEROES    PIC 9(09).
           03 WSS-REG-PROC          VALUE ZEROES    PIC 9(09).
      *<<<< WSS_DTC
      * Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      * Entidades de GNS
        COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSC-REQA==.
      * Entidades de COL
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
      * Entidades de LDC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDL-REQA==.
      * Entidades de SGC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DCP-REQA==.

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
      *------------
       INI-MAIN.
           COPY GNSBGEDB.
           MOVE 'LDCPGVPV' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM LEE-PARAMETROS.
           PERFORM PROCESA-CLS.
           PERFORM CLOSE-FILE.
           PERFORM MUEST-ESTADIS.
       FIN-MAIN.
           PERFORM GNS-PRO-END.

       OPEN-FILE SECTION.
      *-----------------
       INI-OPEN-FILE.

           MOVE FIO-INP      TO FIO-CMND.
           PERFORM LDC-FIO-CLS.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-OUT      TO FIO-CMND.
           PERFORM LDC-FIO-VPV.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'LDC'        TO FIO-SIST.
           PERFORM LDC-FIO-LDC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM LDC-FIO-LDC.

           MOVE 'LDC'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM LDC-FIO-RDC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM LDC-FIO-RDC.

           MOVE 'TAB'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE 'DEU'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE 'TAB'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM TAB-FIO-OFI.

           MOVE 'SGC'        TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

           MOVE 'COL'   TO FIO-SIST.
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM COL-FIO-RDC.

       FIN-OPEN-FILE.
           EXIT.


       LEE-PARAMETROS SECTION.
      *-----------------------
       INI-LEE-PARAMETROS.
           DISPLAY ' '.
           DISPLAY 'INICIO CAPTURA PARAMETROS'.
           DISPLAY ' '.

      * Captura codigo tipo de banca
           ACCEPT VEC-GLS-TBAN IN WSS-VARI.
           DISPLAY 'TIPO DE BANCA: ' VEC-GLS-TBAN IN WSS-VARI.

      * Se debe validar parametro de tipo de Banca.
           MOVE 1  TO WSS-CNTD IN WSS-VARI.
       LUP-VAL-TBAN.
           IF VEC-COD-TBAN(WSS-CNTD) NOT = '*' AND
              WSS-CNTD IN WSS-VARI NOT > WSS-CMAX IN WSS-VARI AND
              VEC-COD-TBAN(WSS-CNTD) NOT = '999'
                 MOVE 'SGC' TO FIO-SIST
                 MOVE 'BAN' TO TAB-COD-TTAB IN TAB
                 MOVE VEC-COD-TBAN(WSS-CNTD) TO TAB-COD-CTAB IN TAB
                 MOVE FIO-GET-KEY TO FIO-CMND
                 PERFORM GNS-FIO-TAB
                 IF FIO-STAT-OKS
                     ADD 1 TO WSS-CNTD IN WSS-VARI
                     GO TO LUP-VAL-TBAN
                 ELSE
                     DISPLAY 'CODIGO TIPO BANCA NO ENCONTRADO '
                             VEC-COD-TBAN(WSS-CNTD)
                     PERFORM GNS-PRO-ABT.

           DISPLAY ' '.
           DISPLAY 'FIN VALIDACION PARAMETROS'.
           DISPLAY ' '.

       FIN-LEE-PARAMETROS.
           EXIT.


       PROCESA-CLS SECTION.
      *-------------------
       INI-PROCESA-CLS.
           MOVE FIO-GET-FST TO FIO-CMND.

       LUP-PROCESA-CLS.
      *Se lee el registro CLS:
           PERFORM LDC-FIO-CLS.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY 'FIN DE PROCESO PARA CLS'
              GO TO FIN-PROCESA-CLS.
           ADD 1 TO WSS-REG-LEID IN WSS-VARI.
           MOVE CLS TO CLF.

      * Aqui se hace el filtrado de CLS (CLF).
           IF CLF-MSC-STAT IN CLF = 'C' OR 'R'
               GO TO NXT-REG-CLS.

            ADD 1 TO WSS-REG-SELE.
      * Obtiene datos del cliente y de la banca: (los deja en VPV)
           PERFORM OBT-DAT-CLI-BANC.

      * Selecciona registros:
           MOVE 1 TO WSS-CNTD IN WSS-VARI.
       LUP-TBAN.
           IF VEC-COD-TBAN(WSS-CNTD) = '*'
                 GO TO FIN-LUP-TBAN.
           IF WSS-CNTD IN WSS-VARI > WSS-CMAX IN WSS-VARI OR
              VEC-COD-TBAN(WSS-CNTD) = '999'
                 GO TO NXT-REG-CLS.
           IF VEC-COD-TBAN(WSS-CNTD) NOT = VPV-COD-TBAN IN VPV
                 ADD 1 TO WSS-CNTD IN WSS-VARI
                 GO TO LUP-TBAN.
       FIN-LUP-TBAN.

      * Obtiene el resto de los datos y los graba:
           ADD 1 TO WSS-REG-PROC IN WSS-VARI.
           PERFORM LLEN-GRAB-VPV.

       NXT-REG-CLS.
      * Lee el siguiente registro CLS:
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PROCESA-CLS.

       FIN-PROCESA-CLS.
           EXIT.


       LLEN-GRAB-VPV SECTION.
      *------------------
       INI-LLEN-GRAB-VPV.
      * Mueve el Regional:
           PERFORM OBT-COD-REG.
      * Mueve la oficina:
           MOVE CLF-COD-OFIC IN CLF TO VPV-COD-OFIC IN VPV.
      * Mueve el ejecutivo:
           MOVE CLF-COD-EJEC IN CLF TO VPV-COD-EJEC IN VPV.
      * Mueve el cupo maximo:
           MOVE CLF-VAL-CMAX IN CLF TO VPV-VAL-CMAX IN VPV.
      * Mueve fecha de vencimiento:
           MOVE CLF-NUM-SVEN IN CLF TO VPV-NUM-SVEN IN VPV.
           MOVE CLF-NUM-AVEN IN CLF TO VPV-NUM-AVEN IN VPV.
           MOVE CLF-NUM-MVEN IN CLF TO VPV-NUM-MVEN IN VPV.
           MOVE CLF-NUM-DVEN IN CLF TO VPV-NUM-DVEN IN VPV.
      * Mueve fecha de proxima revision:
           MOVE CLF-NUM-SPRV IN CLF TO VPV-NUM-SREV IN VPV.
           MOVE CLF-NUM-APRV IN CLF TO VPV-NUM-AREV IN VPV.
           MOVE CLF-NUM-MPRV IN CLF TO VPV-NUM-MREV IN VPV.
           MOVE CLF-NUM-DPRV IN CLF TO VPV-NUM-DREV IN VPV.
      * Mueve separadores:
           MOVE '*' TO VPV-IND-SEP0
                       VPV-IND-SEP1
                       VPV-IND-SEP2
                       VPV-IND-SEP3
                       VPV-IND-SEP4
                       VPV-IND-SEP5
                       VPV-IND-SEP7
                       VPV-IND-SEP8
                       VPV-IND-SEP9
                       VPV-IND-SE10.
           MOVE '-' TO VPV-IND-SEP6.
      * Graba VPV:
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM LDC-FIO-VPV.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: AL GRABAR DATOS EN LDCVPV ' FIO-STAT.
           ADD 1 TO WSS-REG-GRAB IN WSS-VARI.
       FIN-LLEN-GRAB-VPV.
           EXIT.


       OBT-DAT-CLI-BANC SECTION.
      *--------------------
       INI-OBT-DAT-CLI-BANC.
      *En esta seccion se obtiene nombre, rut y tipo de banca del clien
      *
      * Primero obtengo Datos desde RDC para datos clientes y
      * posteriormente puedo leer el Tipo de Banca.
           MOVE CLF-CAI-ICLF IN CLF TO RDC-CAI-ICLF IN RDL.
           MOVE CLF-IIC-ICLF IN CLF TO RDC-IIC-ICLF IN RDL.
           MOVE 'RDC-KEY-ICLF'      TO FIO-AKEY.
           MOVE FIO-GET-NLS         TO FIO-CMND.
           PERFORM LDC-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
                    CLF-CAI-ICLF IN CLF = RDC-CAI-ICLF IN RDL AND
                    CLF-IIC-ICLF IN CLF = RDC-IIC-ICLF IN RDL )
               DISPLAY 'ERROR: NO EXISTE RDC ASOCIADA A CLF : '
                        CLF-CIC-ICLF IN CLF
               ADD 1 TO WSS-REG-RSCL IN WSS-VARI
               GO TO FIN-OBT-DAT-CLI-BANC.

      * Lee con CIC CLIENTE archivo DBC
           MOVE RDC-CIC-ICLI IN RDL TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR: NO EXISTE DBC ASOCIADA A RDC : '
                      RDC-CIC-ICLI IN RDL
               MOVE SPACES  TO VPV-GLS-NOMC IN VPV
               MOVE ZEROES  TO VPV-NUM-ICLI IN VPV
               MOVE SPACES  TO VPV-VRF-ICLI IN VPV
               ADD 1 TO WSS-REG-DSRD IN WSS-VARI
               GO TO FIN-OBT-DAT-CLI-BANC.

           MOVE DBC-GLS-NOMC IN DBC  TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.
      * Muevo nombre e idc a VPV:
           MOVE DBC-GLS-NOMC IN DBC TO VPV-GLS-NOMC IN VPV.
           MOVE DBC-NUM-ICLI IN DBC TO VPV-NUM-ICLI IN VPV.
           MOVE DBC-VRF-ICLI IN DBC TO VPV-VRF-ICLI IN VPV.

           IF DBC-IND-TICL IN DBC = 'P'
               MOVE RDC-CIC-ICLI IN RDL TO DCP-CIC-ICLI IN DCP
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM SGC-FIO-DCP
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR: CLIENTE NO TIENE BANCA ASOCIADA, '
                          'IDC DEL CLIENTE : ' DBC-IDC-ICLI IN DBC
                   MOVE SPACES TO VPV-COD-TBAN IN VPV
                   ADD 1 TO WSS-REG-SBAN IN WSS-VARI
                   GO TO FIN-OBT-DAT-CLI-BANC
               ELSE
                   IF DCP-COD-TBAN IN DCP > SPACES
                       MOVE DCP-COD-TBAN IN DCP TO VPV-COD-TBAN IN VPV
                   ELSE
                       DISPLAY 'ERROR: CLIENTE NO TIENE BANCA '
                              'ASOCIADA, IDC : ' DBC-CIC-ICLI IN DBC
                       MOVE SPACES TO VPV-COD-TBAN IN VPV
                       ADD 1 TO WSS-REG-SBAN IN WSS-VARI
                       GO TO FIN-OBT-DAT-CLI-BANC
           ELSE
              IF DBC-IND-TICL IN DBC = 'E'
                  MOVE DBC-CIC-ICLI IN DBC TO DCE-CIC-ICLI IN DCE
                  MOVE FIO-GET-KEY TO FIO-CMND
                  PERFORM SGC-FIO-DCE
                  IF NOT FIO-STAT-OKS
                      DISPLAY 'ERROR: CLIENTE NO TIENE BANCA ASOCIADA, '
                              'IDC DEL CLIENTE : ' DBC-IDC-ICLI IN DBC
                      MOVE SPACES TO VPV-COD-TBAN IN VPV
                      ADD 1 TO WSS-REG-SBAN IN WSS-VARI
                      GO TO FIN-OBT-DAT-CLI-BANC
                  ELSE
                      IF DCE-COD-TBAN IN DCE > SPACES
                          MOVE DCE-COD-TBAN IN DCE TO VPV-COD-TBAN
                                                                IN VPV
                      ELSE
                          DISPLAY 'ERROR: CLIENTE NO TIENE BANCA '
                                 'ASOCIDA, IDC : ' DBC-IDC-ICLI IN DBC
                          MOVE SPACES TO VPV-COD-TBAN IN VPV
                          ADD 1 TO WSS-REG-SBAN IN WSS-VARI
                          GO TO FIN-OBT-DAT-CLI-BANC.

       FIN-OBT-DAT-CLI-BANC.
           EXIT.


       OBT-COD-REG SECTION.
      *---------------------------
       INI-OBT-COD-REG.
      * Busca Codigo Regional en OFI:
           MOVE CLF-COD-OFIC IN CLF TO OFI-COD-OFIC IN OFI.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM TAB-FIO-OFI.
           IF NOT FIO-STAT-OKS
              MOVE SPACES TO VPV-COD-REGI IN VPV
           ELSE
              IF DBC-IND-TICL IN DBC = 'P'
                 IF OFI-COD-REGP IN OFI = SPACES
                    MOVE OFI-COD-REGE IN OFI TO VPV-COD-REGI IN VPV
                 ELSE
                    MOVE OFI-COD-REGP IN OFI TO VPV-COD-REGI IN VPV
              ELSE
                 IF OFI-COD-REGE IN OFI = SPACES
                    MOVE OFI-COD-REGP IN OFI TO VPV-COD-REGI IN VPV
                 ELSE
                    MOVE OFI-COD-REGE IN OFI TO VPV-COD-REGI IN VPV.
       FIN-OBT-COD-REG.
           EXIT.


       MUEST-ESTADIS SECTION.
      *---------------------
       INI-MUEST-ESTADIS.
           DISPLAY '  '.
           DISPLAY '  '.
           DISPLAY '     RESULTADOS DEL PROCESO'.
           DISPLAY '     ----------------------'.
           DISPLAY '  '.
           DISPLAY 'REGISTROS CLF LEIDOS        : ' WSS-REG-LEID.
           DISPLAY 'REGISTROS CLF SELECCIONADOS : ' WSS-REG-SELE.
           DISPLAY 'CLIENTES EN CLF SIN RDC     : ' WSS-REG-RSCL.
           DISPLAY 'CLIENTES EN RDC SIN DBC     : ' WSS-REG-DSRD.
           DISPLAY 'CLIENTES EN CLF SIN BANCA   : ' WSS-REG-SBAN.
           DISPLAY 'REGISTROS CLF PROCESADOS    : ' WSS-REG-PROC.
           DISPLAY 'REGISTROS GRABADOS (SALIDA) : ' WSS-REG-GRAB.
       FIN-MUEST-ESTADIS.
           EXIT.

       CLOSE-FILE SECTION.
      *------------------
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-CLS.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-VPV.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-CLF.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-LDC.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SGC-FIO-DBC.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-RDC.

           MOVE 'TAB' TO FIO-SIST.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM TAB-FIO-OFI.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM COL-FIO-RDC.

       FIN-CLOSE-FILE.
           EXIT.

       COPY LDCBFCLF.
       COPY LDCBFCLS.
       COPY LDCBFVPV.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGIRH.
       COPY GNSBGABT.

      *Entidades de GNS
       COPY GNSBFMSC.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBVTAB.
       COPY GNSBBMSC.
       COPY GNSBVMSC.
       COPY GNSBGCPT.
      *Entidades de TAB
       COPY TABBFOFI.
       COPY TABBVOFI.
       COPY TABBBOFI.
      *Entidades de LDC
       COPY LDCBFLDC.
       COPY LDCBFRDC REPLACING RDC BY RDL
                  ==ADR-RDC-REQA== BY ==ADR-RDL-REQA==.
       COPY SGCBFDBC.
       COPY SGCBFDCP.
       COPY SGCBFDCE.
      *
       COPY COLBFRDC.
      *>>>>
