       IDENTIFICATION DIVISION.
      *========================
       PROGRAM-ID. LDCPPAML.
       AUTHOR. ADA.
       DATE-WRITTEN. 26-NOV-1999 15:26:36
      *
      * PROGRAMA RECIBE MENSAJES DE UN PC CON REQUERIMIENTOS
      * RELACIONADOS CON LA ACTIVACION DE MULTILINEAS
      *
       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *==============

       WORKING-STORAGE SECTION.
      *------------------------
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGSYS.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGFRM.
       COPY GNSWGCPT.
       COPY GNSWVSCR.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY LDCWGAML.
       COPY LDCBRCLF.
       COPY LDCBRRDC.
       COPY LDCBRLDC.
       COPY SGCBRDBC.
       COPY TABBRUSR.
       COPY TABBROFI.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CLF-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-USR-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.

       01  SND-SEND VALUE SPACES.
           03 SND-HEAD-SEND.
      *
      *       Status Respuesta 0=OK ; 1=ERR
              05 SND-RES-STAT                         PIC X(0001).
      *
      *       Fecha Transaccion
              05 SND-FEC-FTRN                         PIC X(0008).
      *
      *       Hora Transaccion
              05 SND-HRS-HTRN                         PIC X(0006).
      *
      *       Indicador Mensajes Pendientes 0=NO ; 1=SI
              05 SND-IND-MSGP                         PIC X(0001).
      *
      *       Largo de la parte Variable del Mensaje
              05 SND-NUM-LENM                         PIC 9(0005).
      *
      *       Codigo Transaccion
              05 SND-COD-TRAN                         PIC X(0008).
      *
      *       Codigo Sistema
              05 SND-COD-SIST                         PIC X(0003).
      *
      *       Codigo Usuario
              05 SND-COD-USER                         PIC X(0012).
      *
      *       Password
              05 SND-COD-PASS                         PIC X(0008).
      *
      *       Filler Vacio y Fijo de 111 Caracteres
           03 SND-GLS-FILL                            PIC X(0111).
      *
      *    DATA a ser transmitida de OUTPUT
           03 SND-GLS-DOUT                            PIC X(1089).
      *
      *
       01  RCV-RECEIVE VALUE SPACES.
      *
      *    Header utilizado por el Driver de Comunicacion
           03 RCV-HEAD-DRIVE.
               05 RCV-HEAD-DRIVE-TRAN                 PIC X(008).
               05 RCV-HEAD-DRIVE-SIST                 PIC X(003).
               05 RCV-HEAD-DRIVE-USER                 PIC X(012).
               05 RCV-HEAD-DRIVE-PSSW                 PIC X(008).
      *
      *    Identificacion del TCP-IP
           03 RCV-IDEN-TCPIP                          PIC X(0006).
      *
      *    Indicador de Mensajes: I=Inicio, C=Continua,
      *                           F=Fin,    X=Msg.Unico
           03 RCV-IND-MENS                            PIC X(0001).
      *
      *    DATA a ser transmitida de INPUT
           03 RCV-GLS-DINP                            PIC X(1024).

      * Fin variables area de comunicacion BCI

       01  TSIDENT.
           03 TSKNO                    PIC S9(07) COMP-3.
           03 TERM                     PIC X(04).

       01  WSS-VARI.
      *LARGO RECEIVE = 31 + 6 + 1 + 1024 = 1062
           03 WSS-LEN-RCV    VALUE +1062     COMP    PIC S9(04).
      *LARGO SEND = 52 + 111 + 1089 = 1252
           03 WSS-LEN-SND    VALUE +1252     COMP    PIC S9(04).
           03 WSS-NUM-OCUR   VALUE 8                 PIC 9(02).
           03 WSS-I                                  PIC 9(02).
           03 WSS-IACT                               PIC X(01).
           03 WSS-NOMC.
              05 WSS-NOMB                            PIC X(25).
              05 WSS-APAT                            PIC X(25).
              05 WSS-AMAT                            PIC X(25).
           03 WSS-FVEN.
              05 WSS-SVEN                            PIC 9(02).
              05 WSS-AVEN                            PIC 9(02).
              05 WSS-MVEN                            PIC 9(02).
              05 WSS-DVEN                            PIC 9(02).
           03 WSS-CIC-ICLF                           PIC X(12).
           03 WSS-FEC-FCIE                           PIC X(08).

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02 FILLER                     PIC X(01).
       COPY GNSLGFIO.

       PROCEDURE DIVISION.
      *===================

       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.

           MOVE 'LDCPPMEN'  TO IDD-PROG FIO-PROG.
           MOVE IDD-VARI    TO SYS-CMMA.
           MOVE +507        TO SYS-TCMA.
           MOVE 'GNSPPIDD'  TO SYS-PROG.
           MOVE SYS-LINK    TO SYS-CMND.
           PERFORM GNS-PRO-SYS.
           MOVE SYS-CMMA TO IDD-VARI.

           PERFORM PROCESA-MSG.
       FIN-MAIN.
           EXEC CICS RETURN END-EXEC.
       EXT-MAIN.
           EXIT.

       PROCESA-MSG SECTION.
       INI-PROCESA-MSG.
      *
      *Inicializa variables
           MOVE SPACES TO AML-OUTP.
           MOVE '0'    TO AML-CRET.
      *
      *Recibe Mensaje desde Cliente
           PERFORM RCV-MSG-INP-HST.
      *
      *Procesa el requerimiento solicitado
           MOVE RCV-GLS-DINP TO AML-INPU.
           IF AML-CMND = 'CON'
               PERFORM PROCESA-CON
               MOVE AML-LCON TO SND-NUM-LENM
           ELSE
           IF AML-CMND = 'ACT'
               PERFORM PROCESA-ACT
               MOVE AML-LACT TO SND-NUM-LENM
           ELSE
               MOVE '1' TO AML-CRET
               MOVE 'COMANDO INEXISTENTE, REVISAR APLICACION'
                        TO AML-MENS
               MOVE AML-LERR TO SND-NUM-LENM.
      *
      *Envia Mensaje al Cliente
      *
           PERFORM ENV-MSG-OUT-HST.
       FIN-PROCESA-MSG.
           EXIT.

      *-------------------------------------------------------------

       RCV-MSG-INP-HST SECTION.
       INI-RCV-MSG-INP-HST.
           MOVE SPACES TO RCV-RECEIVE.
           EXEC CICS RECEIVE
                     INTO(RCV-RECEIVE)
                     LENGTH(WSS-LEN-RCV)
                     CONVID(EIBTRMID)
                     END-EXEC.
           MOVE EIBTRMID    TO TERM.
           MOVE EIBTASKN    TO TSKNO.
           IF EIBCONF = HIGH-VALUES
               EXEC CICS ISSUE CONFIRMATION
                         CONVID(TERM)
               END-EXEC.
       FIN-RCV-MSG-INP-HST.
           EXIT.

      *-------------------------------------------------------------

       PROCESA-CON SECTION.
       INI-PROCESA-CON.
           MOVE SPACES TO AML-OCMS AML-OMSJ.
           IF AML-IDCC > SPACES
               MOVE AML-IDCC TO AML-OIDC
               PERFORM BUSCA-CLIENTE-CON-IDC
               IF AML-OCMS NOT > SPACES
                   PERFORM BUSCA-MARGEN-CON-DBC
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE AML-ICLF TO AML-OCLF
               PERFORM BUSCA-MARGEN-CON-CLF
               IF AML-OCMS NOT > SPACES
                   PERFORM BUSCA-CLIENTE-CON-CLF.
           IF AML-OCMS > SPACES
               GO TO FIN-PROCESA-CON.
      *Verifica condiciones para activar
      *  1) Que el usuario sea de la misma oficina de la CLF
           IF AML-OFIA NOT = CLF-COD-OFIC IN CLF
               MOVE 'CLFOFUSOFIC' TO AML-OCMS
               GO TO FIN-PROCESA-CON.
      *  2) Que la CLF este activada
           IF CLF-MSC-STAT IN CLF NOT = 'A'
               MOVE 'CLF    NOACA' TO AML-OCMS
               GO TO FIN-PROCESA-CON.
      *Busca lineas del tipo multilineas
           PERFORM BUSCA-MLTS-PARA-CLF.
       FIN-PROCESA-CON.
           IF AML-OCMS > SPACES
               MOVE 'LDC'    TO MSG-COD-SIST
               MOVE AML-OCMS TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO AML-OMS1.
       EXT-PROCESA-CON.
           EXIT.

       BUSCA-CLIENTE-CON-IDC SECTION.
       INI-BUSCA-CLIENTE-CON-IDC.
           MOVE AML-IDCC TO DBC-IDC-ICLI IN DBC.
           MOVE 'DBC-IDC-ICLI' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               MOVE 'CLI    NEX' TO AML-OCMS
           ELSE
               PERFORM MOVER-DATOS-CLIENTE.
       FIN-BUSCA-CLIENTE-CON-IDC.
           EXIT.

       BUSCA-MARGEN-CON-DBC SECTION.
       INI-BUSCA-MARGEN-CON-DBC.
           MOVE DBC-CIC-ICLI IN DBC TO RDC-CIC-ICLI IN RDC.
           MOVE SPACES              TO RDC-CIC-ICLF IN RDC.

           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM LDC-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
                    DBC-CIC-ICLI IN DBC = RDC-CIC-ICLI IN RDC )
               MOVE 'CLFCLIENOCLF' TO AML-OCMS
               GO TO FIN-BUSCA-MARGEN-CON-DBC.

           MOVE ZEROES              TO WSS-FEC-FCIE.
           MOVE RDC-CIC-ICLF IN RDC TO WSS-CIC-ICLF.
       LUP-BUSCA-MARGEN-CON-DBC.
           MOVE RDC-CIC-ICLF IN RDC TO CLF-CIC-ICLF IN CLF.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
           IF NOT FIO-STAT-OKS
               MOVE 'CLFNOEXIS' TO AML-OCMS
               GO TO FIN-BUSCA-MARGEN-CON-DBC.

           IF NOT ( CLF-MSC-STAT IN CLF = 'C' OR 'R' )
               PERFORM MOVER-DATOS-MARGEN
               GO TO FIN-BUSCA-MARGEN-CON-DBC.

           IF CLF-FEC-FCIE IN CLF     > WSS-FEC-FCIE AND
              CLF-MSC-STAT IN CLF NOT = 'R'
               MOVE CLF-CIC-ICLF IN CLF TO WSS-CIC-ICLF
               MOVE CLF-FEC-FCIE IN CLF TO WSS-FEC-FCIE.

       NXT-BUSCA-MARGEN-CON-DBC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM LDC-FIO-RDC.
           IF FIO-STAT-OKS AND
              DBC-CIC-ICLI IN DBC = RDC-CIC-ICLI IN RDC
               GO TO LUP-BUSCA-MARGEN-CON-DBC.

           MOVE WSS-CIC-ICLF TO CLF-CIC-ICLF IN CLF.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
           IF NOT FIO-STAT-OKS
               MOVE 'CLFNOEXIS' TO AML-OCMS
           ELSE
               PERFORM MOVER-DATOS-MARGEN.
       FIN-BUSCA-MARGEN-CON-DBC.
           EXIT.

       BUSCA-MARGEN-CON-CLF SECTION.
       INI-BUSCA-MARGEN-CON-CLF.
           MOVE AML-ICLF TO CLF-KEY-ICLF IN CLF.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
           IF NOT FIO-STAT-OKS
               MOVE 'CLFNOEXIS' TO AML-OCMS
           ELSE
               PERFORM MOVER-DATOS-MARGEN.
       FIN-BUSCA-MARGEN-CON-CLF.
           EXIT.

       BUSCA-CLIENTE-CON-CLF SECTION.
       INI-BUSCA-CLIENTE-CON-CLF.
           MOVE CLF-KEY-ICLF IN CLF TO RDC-KEY-ICLF IN RDC.
           MOVE 'RDC-KEY-ICLF' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM LDC-FIO-RDC.
           IF NOT FIO-STAT-OKS
               MOVE 'CLI    NEX' TO AML-OCMS
               GO TO FIN-BUSCA-CLIENTE-CON-CLF.
           MOVE RDC-CIC-ICLI IN RDC TO DBC-CIC-ICLI IN DBC.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
               MOVE 'CLI    NEX' TO AML-OCMS
           ELSE
               PERFORM MOVER-DATOS-CLIENTE.
       FIN-BUSCA-CLIENTE-CON-CLF.
           EXIT.

       MOVER-DATOS-CLIENTE SECTION.
       INI-MOVER-DATOS-CLIENTE.
      *Llena los datos de cliente en mensaje de salida
           MOVE DBC-IDC-ICLI IN DBC TO AML-OIDC.
           MOVE DBC-VRF-ICLI IN DBC TO AML-OVRC.
           MOVE DBC-CIC-ICLI IN DBC TO AML-OCIC.
           IF DBC-IND-TICL IN DBC = 'P'
               MOVE DBC-GLS-NOMB IN DBC TO WSS-NOMB
               MOVE DBC-GLS-APAT IN DBC TO WSS-APAT
               MOVE DBC-GLS-AMAT IN DBC TO WSS-AMAT
           ELSE
               MOVE DBC-GLS-NOMC IN DBC TO WSS-NOMC.
           MOVE WSS-NOMC TO CPT-STRN.
           PERFORM CPT-BLKS.
           MOVE CPT-STRN            TO AML-NOMC.
           MOVE DBC-COD-OFIC IN DBC TO AML-OFIC.
           MOVE DBC-COD-EJEC IN DBC TO AML-EJEC.
       FIN-MOVER-DATOS-CLIENTE.
           EXIT.

       MOVER-DATOS-MARGEN SECTION.
       INI-MOVER-DATOS-MARGEN.
      *Llena los datos de margen en mensaje de salida
           MOVE CLF-CIC-ICLF IN CLF TO AML-OCLF.
           MOVE CLF-COD-VCCF IN CLF TO AML-VCCF.
           MOVE CLF-COD-OFIC IN CLF TO AML-OFIM.
           MOVE CLF-COD-EJEC IN CLF TO AML-EJEM.
           MOVE CLF-VAL-CMAX IN CLF TO AML-CMAX.
           MOVE CLF-NUM-DVEN IN CLF TO AML-DVEM.
           MOVE CLF-NUM-MVEN IN CLF TO AML-MVEM.
           MOVE CLF-NUM-SVEN IN CLF TO AML-SVEM.
           MOVE CLF-NUM-AVEN IN CLF TO AML-AVEM.
       FIN-MOVER-DATOS-MARGEN.
           EXIT.

       BUSCA-MLTS-PARA-CLF SECTION.
       INI-BUSCA-MLTS-PARA-CLF.
           PERFORM GET-FHOY.
           MOVE 0 TO WSS-I.
           MOVE 'N' TO WSS-IACT.
           MOVE CLF-CIC-ICLF TO LDC-CIC-ICLF IN LDC.
           MOVE 0            TO LDC-NUM-ILDC IN LDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-BUSCA-MLTS-PARA-CLF.
           PERFORM LDC-FIO-LDC.
           IF NOT ( FIO-STAT-OKS AND
                LDC-CIC-ICLF IN LDC = CLF-CIC-ICLF IN CLF )
               GO TO FIN-BUSCA-MLTS-PARA-CLF.
      *Revisa si la linea encontrada es MLT
           IF LDC-COD-TIPO IN LDC NOT = 'MLT'
               GO TO NXT-BUSCA-MLTS-PARA-CLF.
      *Revisa si el CIC del tipo de linea encontrada es del tipo
      *multilinea (MLT)
           MOVE 'LDC' TO FIO-SIST.
           MOVE 'TLC'               TO TAB-COD-TTAB IN TAB.
           MOVE LDC-COD-TIPO IN LDC TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               GO TO NXT-BUSCA-MLTS-PARA-CLF.
           IF TAB-CIC-CTAB IN TAB NOT = 'MLT'
               GO TO NXT-BUSCA-MLTS-PARA-CLF.
      *Carga datos de multilinea en vector de salida
           ADD 1 TO WSS-I.
           IF WSS-I > WSS-NUM-OCUR
               GO TO FIN-BUSCA-MLTS-PARA-CLF.
           MOVE LDC-FEC-FVEN IN LDC TO WSS-FVEN.
           MOVE LDC-NUM-ILDC IN LDC TO AML-ILDC(WSS-I).
           MOVE LDC-COD-TIPO IN LDC TO AML-TIPO(WSS-I).
           MOVE LDC-VAL-CMAX IN LDC TO AML-LMAX(WSS-I).
           MOVE LDC-COD-VCLC IN LDC TO AML-VCLC(WSS-I).
           MOVE LDC-NUM-DINI IN LDC TO AML-DINI(WSS-I).
           MOVE LDC-NUM-MINI IN LDC TO AML-MINI(WSS-I).
           MOVE LDC-NUM-SINI IN LDC TO AML-SINI(WSS-I).
           MOVE LDC-NUM-AINI IN LDC TO AML-AINI(WSS-I).
           MOVE LDC-NUM-PVIG IN LDC TO AML-NVIG(WSS-I).
           MOVE LDC-IND-PVIG IN LDC TO AML-UVIG(WSS-I).
           MOVE LDC-NUM-DVEN IN LDC TO AML-DVEN(WSS-I).
           MOVE LDC-NUM-MVEN IN LDC TO AML-MVEN(WSS-I).
           MOVE LDC-NUM-SVEN IN LDC TO AML-SVEN(WSS-I).
           MOVE LDC-NUM-AVEN IN LDC TO AML-AVEN(WSS-I).
           MOVE LDC-IND-VIGE IN LDC TO AML-VIGE(WSS-I).
           MOVE LDC-MSC-STAT IN LDC TO AML-STAT(WSS-I).
           IF LDC-MSC-STAT IN LDC NOT = 'A'
               MOVE 'S' TO WSS-IACT.
       NXT-BUSCA-MLTS-PARA-CLF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-BUSCA-MLTS-PARA-CLF.
       FIN-BUSCA-MLTS-PARA-CLF.
           IF WSS-I = 0
               MOVE 'CLFMLTSNEX' TO AML-OCMS
           ELSE
           IF ( WSS-I = 1 ) AND
              ( WSS-FVEN < HOY-FHOY )
               MOVE 'CLFMLTSVNCDA' TO AML-OCMS
           ELSE
           IF WSS-IACT = 'N'
               MOVE 'CLFMLTSYAACA' TO AML-OCMS.
       EXT-BUSCA-MLTS-PARA-CLF.
           EXIT.

       PROCESA-ACT SECTION.
       INI-PROCESA-ACT.
           MOVE SPACES TO AML-CMSG AML-MSJE.
      *Verifica condiciones de CLF para activar
           MOVE AML-KCLF TO CLF-CIC-ICLF IN CLF.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
           IF NOT FIO-STAT-OKS
               MOVE 'CLFNOEXIS' TO AML-CMSG
               MOVE AML-KCLF    TO AML-MSJ2
               GO TO FIN-PROCESA-ACT.
      *  1) Que el usuario sea de la misma oficina de la CLF
           IF AML-KOFI NOT = CLF-COD-OFIC IN CLF
               MOVE 'CLFOFUSOFIC' TO AML-CMSG
               GO TO FIN-PROCESA-ACT.
      *  2) Que la CLF este activada
           IF CLF-MSC-STAT IN CLF NOT = 'A'
               MOVE 'CLF    NOACA' TO AML-CMSG
               GO TO FIN-PROCESA-ACT.
           MOVE 0 TO WSS-I.
       LUP-PROCESA-ACT.
           ADD 1 TO WSS-I.
           IF WSS-I > WSS-NUM-OCUR
               GO TO FIN-PROCESA-ACT.
           IF AML-KLDC(WSS-I) NOT > 0
               GO TO LUP-PROCESA-ACT.
           PERFORM GET-FHOY.
           MOVE AML-KCLF        TO LDC-CIC-ICLF IN LDC.
           MOVE AML-KLDC(WSS-I) TO LDC-NUM-ILDC IN LDC.
           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM LDC-FIO-LDC.
           IF NOT FIO-STAT-OKS
               MOVE 'AMLERROGKUPD' TO AML-CMSG
               MOVE LDC-KEY-ILDC IN LDC TO AML-MSJ2
               MOVE FIO-BCK-OUT TO FIO-CMND
               PERFORM LDC-FIO-CLF
               GO TO FIN-PROCESA-ACT.
      *Verfica que la linea no este activada
           IF LDC-MSC-STAT IN LDC = 'A'
               MOVE 'CLFMLTSYAACA'      TO AML-CMSG
               MOVE LDC-KEY-ILDC IN LDC TO AML-MSJ2
               MOVE FIO-BCK-OUT TO FIO-CMND
               PERFORM LDC-FIO-CLF
               GO TO FIN-PROCESA-ACT.
      *Graba el registro LDC para indicar que esta activada la linea
           MOVE HOY-STP-ITRN TO LDC-STP-ITRN IN LDC.
           MOVE AML-KAUT     TO LDC-COD-ATRN IN LDC.
           MOVE TERM         TO LDC-COD-OTRN IN LDC.
           MOVE HOY-DHOY     TO LDC-NUM-DINI IN LDC.
           MOVE HOY-MHOY     TO LDC-NUM-MINI IN LDC.
           MOVE HOY-SHOY     TO LDC-NUM-SINI IN LDC.
           MOVE HOY-AHOY     TO LDC-NUM-AINI IN LDC.
           MOVE LDC-NUM-DINI IN LDC TO FEC-DEC1.
           MOVE LDC-NUM-MINI IN LDC TO FEC-MEC1.
           MOVE LDC-NUM-SINI IN LDC TO FEC-SEC1.
           MOVE LDC-NUM-AINI IN LDC TO FEC-AEC1.
           MOVE LDC-NUM-DVEN IN LDC TO FEC-DEC2.
           MOVE LDC-NUM-MVEN IN LDC TO FEC-MEC2.
           MOVE LDC-NUM-SVEN IN LDC TO FEC-SEC2.
           MOVE LDC-NUM-AVEN IN LDC TO FEC-AEC2.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-DIF-DIA  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'AMLDIFDIAERR'      TO AML-CMSG
               MOVE LDC-KEY-ILDC IN LDC TO AML-MSJ2
               MOVE FIO-BCK-OUT TO FIO-CMND
               PERFORM LDC-FIO-CLF
               GO TO FIN-PROCESA-ACT.
           MOVE FEC-NDIA     TO LDC-NUM-PVIG IN LDC.
           MOVE 'D'          TO LDC-IND-PVIG IN LDC.
           MOVE 'A'          TO LDC-MSC-STAT IN LDC.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM LDC-FIO-LDC.
           IF NOT FIO-STAT-OKS
               MOVE 'AMLERROMOD'        TO AML-CMSG
               MOVE LDC-KEY-ILDC IN LDC TO AML-MSJ2
               MOVE FIO-BCK-OUT TO FIO-CMND
               PERFORM LDC-FIO-CLF
               GO TO FIN-PROCESA-ACT.
           GO TO LUP-PROCESA-ACT.
       FIN-PROCESA-ACT.
           IF AML-CMSG > SPACES
               MOVE 'LDC'    TO MSG-COD-SIST
               MOVE AML-CMSG TO MSG-COD-MENS
               PERFORM GET-MSG
               MOVE MSG-GLS-DESC TO AML-MSJ1.
       EXT-PROCESA-ACT.
           EXIT.

      *-------------------------------------------------------------

       ENV-MSG-OUT-HST SECTION.
       INI-ENV-MSG-OUT-HST.
           MOVE '0'      TO SND-RES-STAT.
           PERFORM GET-FHOY.
           MOVE HOY-FHOY TO SND-FEC-FTRN.
           MOVE HOY-HHOY TO SND-HRS-HTRN.
           MOVE '0'      TO SND-IND-MSGP.
      *    MOVE +1024    TO SND-NUM-LENM.
           MOVE RCV-HEAD-DRIVE-TRAN TO SND-COD-TRAN.
           MOVE RCV-HEAD-DRIVE-SIST TO SND-COD-SIST.
           MOVE RCV-HEAD-DRIVE-USER TO SND-COD-USER.
           MOVE RCV-HEAD-DRIVE-PSSW TO SND-COD-PASS.
           MOVE SPACES   TO SND-GLS-FILL.
           MOVE SPACES   TO SND-GLS-DOUT.
           MOVE AML-OUTP TO SND-GLS-DOUT.
           EXEC CICS SEND
                     LENGTH(WSS-LEN-SND)
                     FROM(SND-SEND)
                     CONVID(TERM)
                     LAST
           END-EXEC.
       FIN-ENV-MSG-OUT-HST.
           EXIT.

      *-------------------------------------------------------------

       COPY GNSBGEND.
       COPY GNSBPFEC.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBHSYS.
       COPY GNSBGSYS.
       COPY GNSBIABT.
       COPY GNSBGDTC.
       COPY GNSBGCPT.
       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY SGCBFDBC.
       COPY LDCBFCLF.
       COPY LDCBFRDC.
       COPY LDCBFLDC.
       COPY TABBBUSR.
       COPY TABBFUSR.
       COPY TABBBOFI.
       COPY TABBFOFI.
