       IDENTIFICATION DIVISION.
      *=======================*
       PROGRAM-ID.      LDCPGLPC.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    22-ENE-1996
      *
       ENVIRONMENT DIVISION.
      *====================*
       CONFIGURATION SECTION.
      *---------------------*
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------*
       FILE-CONTROL.

       DATA DIVISION.
      *=============*
       WORKING-STORAGE SECTION.
      *-----------------------*
      *<<<< WSS_DTC

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-LDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CLF-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.

      *<<<< WSS
       COPY LDCBRLPC.
       COPY SGCBRBDE.

       COPY LDCBRLDC.
       COPY LDCBRRDC.
       COPY LDCBRCLF.
       COPY LDCWGCCU.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSBRIDD.
       COPY GNSWVSCR.

       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.

       01  WSS-VARI.
           03 WSS-NUM-RPRO      VALUE ZEROES     PIC 9(06).
           03 WSS-VAL-EDIT         PIC +99999999999,9999.


       PROCEDURE DIVISION.
      *==================*
       MAIN SECTION.
      *------------*
       INI-MAIN.
      *--------*
           COPY GNSBGEDB.
           MOVE 'LDCPGLPC' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           PERFORM  ABRE-ARCHIVOS.
           MOVE SPACES TO CCU-CMMA.

      * OBTIENE CODIGO VALOR DE CAMBIO DE PESOS MEDIANTE CIC DE LA MONED
           MOVE 'TAB'          TO FIO-SIST.
           MOVE 'VLR'          TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '       TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN OBTENCION CIC MONEDA 0999 : '
                        TAB-COD-CTAB IN TAB ' FS: ' FIO-STAT
              PERFORM CIERRA-ARCHIVOS
              PERFORM GNS-PRO-ABT.
           MOVE TAB-COD-CTAB IN TAB TO CCU-COD-PESO IN CCU-VARI.

      *RECIBE FECHA DE PARAMETRO
           DISPLAY ' '.
           DISPLAY 'INGRESE FECHA DE PROCESO  (DDMMSSAA) : '.
           ACCEPT  LPC-FEC-FPRO IN LPC.
           DISPLAY LPC-FEC-FPRO IN LPC.
           MOVE 1        TO LPC-COD-TREG IN LPC.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM LDC-FIO-LPC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL GRABAR SEQ-LPC: ' FIO-STAT
              PERFORM CIERRA-ARCHIVOS
              PERFORM GNS-PRO-END.

           MOVE LOW-VALUES         TO BDE-CIC-ICLI.
           MOVE FIO-GET-FST        TO FIO-CMND.

           PERFORM SGC-FIO-BDE.
           IF NOT FIO-STAT-OKS
              DISPLAY ' ARCHIVO VACIO '
              PERFORM CIERRA-ARCHIVOS
              STOP RUN.

       LUP-BDE.
           MOVE BDE-NUM-ICLI IN BDE   TO LPC-NUM-ICLI IN LPC.
           MOVE BDE-VRF-ICLI IN BDE   TO LPC-VRF-ICLI IN LPC.
           MOVE BDE-CIC-ICLI IN BDE   TO LPC-CIC-ICLI IN LPC.

           MOVE BDE-CIC-ICLI IN BDE   TO RDC-CIC-ICLI IN RDC.
           MOVE LOW-VALUES            TO RDC-KEY-ICLF IN RDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM LDC-FIO-RDC.
           IF NOT ( FIO-STAT-OKS AND
              RDC-CIC-ICLI IN RDC = BDE-CIC-ICLI IN BDE )
                PERFORM REG-SIN-CLF
                GO TO CONT-LPC.
           MOVE RDC-KEY-ICLF IN RDC TO CLF-KEY-ICLF IN CLF.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM LDC-FIO-CLF.
           IF NOT FIO-STAT-OKS
               DISPLAY ' CLF INEXISTENTE '
               PERFORM CIERRA-ARCHIVOS
               PERFORM GNS-PRO-END.

           MOVE 1                   TO LPC-IND-TCLF IN LPC.
           MOVE CLF-COD-VCCF        TO LPC-COD-VCOC IN LPC.
           MOVE CLF-CAI-ICLF IN CLF TO CCU-CAI-ICLF.
           MOVE CLF-IIC-ICLF IN CLF TO CCU-IIC-ICLF.
           MOVE SPACES TO CCU-CAI-IOPC(1).
           MOVE ZEROES TO CCU-IIC-IOPC(1).
           MOVE SPACES TO CCU-CAI-DCAN(1).
           MOVE ZEROES TO CCU-IIC-DCAN(1).
           MOVE ZEROES TO CCU-NUM-DCAN(1).
           MOVE SPACES TO CCU-OPE-INDS.
           MOVE SPACES TO CCU-CAI-ICLI.
           MOVE ZEROES TO CCU-IIC-ICLI.
           MOVE ZEROES TO CCU-VAL-CUTI.
           CALL 'LDCSBCCU' USING CCU-CMMA .
           IF CCU-NUM-MENS < CCU-NUM-CALC AND
              CCU-NUM-MENS > ZEROES
              MOVE CCU-COD-SIST IN CCU-VARI TO MSG-COD-SIST
              MOVE CCU-COD-MENS IN CCU-VARI TO MSG-COD-MENS
              PERFORM GET-MSG
              IF CCU-CAMB > SPACES
                 MOVE CCU-CAMB TO CCU-MEN2
                 MOVE MSG-GLS-DESC TO CCU-MEN1
                 DISPLAY 'CALIFICACION ' CCU-CAI-ICLF CCU-IIC-ICLF
                 DISPLAY 'ERROR CALCULO CUPOS ' CCU-MENS
                 PERFORM REG-SIN-CLF
                 GO TO CONT-LPC
              ELSE
                 MOVE MSG-GLS-DESC TO CCU-MENS
                 DISPLAY 'CALIFICACION ' CCU-CAI-ICLF CCU-IIC-ICLF
                 DISPLAY 'ERROR CALCULO CUPOS ' CCU-MENS
                 PERFORM REG-SIN-CLF
                 GO TO CONT-LPC.

           PERFORM LEE-LDC.

       CONT-LPC.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM SGC-FIO-BDE.
           IF FIO-STAT-OKS
              GO TO LUP-BDE.

           DISPLAY ' FIN DE ARCHIVO '.
           MOVE 3                TO LPC-COD-TREG IN LPC.
           MOVE LOW-VALUES       TO LPC-GLS-DATA IN LPC.
           MOVE WSS-NUM-RPRO     TO LPC-NUM-RPRO IN LPC.
           DISPLAY 'NUMERO DE REGISTROS PROCESADOS ' WSS-NUM-RPRO.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM LDC-FIO-LPC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR AL GRABAR SEQ-LPC: ' FIO-STAT.
           PERFORM CIERRA-ARCHIVOS.
           STOP RUN.

       FIN-MAIN.
      *--------*

       ABRE-ARCHIVOS SECTION.
       INI-ABRE-ARCHIVOS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-BDE.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR EN APERTURA SEQ: ' FIO-STAT
              PERFORM GNS-PRO-END.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM LDC-FIO-LPC.
           IF NOT FIO-STAT-OKS
              DISPLAY 'ERROR EN APERTURA SEQ: ' FIO-STAT
              PERFORM GNS-PRO-END.
       FIN-ABRE-ARCHIVOS.
           EXIT.


       CIERRA-ARCHIVOS SECTION.
       INI-CIERRA-ARCHIVOS.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM SGC-FIO-BDE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM LDC-FIO-LPC.
       FIN-CIERRA-ARCHIVOS.
           EXIT.


       REG-SIN-CLF SECTION.
       INI-REG-SIN-CLF.
           MOVE 2                   TO LPC-COD-TREG IN LPC.
           MOVE SPACES              TO LPC-KEY-ILDC IN LPC.
           MOVE ZEROES              TO WSS-VAL-EDIT IN WSS-VARI.
           MOVE WSS-VAL-EDIT IN WSS-VARI TO LPC-VAL-CAUT IN LPC.
           MOVE WSS-VAL-EDIT IN WSS-VARI TO LPC-VAL-CUTI IN LPC.
           MOVE WSS-VAL-EDIT IN WSS-VARI TO LPC-VAL-CDIS IN LPC.
           MOVE 0                   TO LPC-IND-TCLF IN LPC.
           MOVE SPACES              TO LPC-GLS-NLDC IN LPC.
           MOVE ZEROES              TO LPC-COD-VCOC IN LPC.
           PERFORM GUARDAR-REG2.
       FIN-REG-SIN-CLF.
           EXIT.


       GUARDAR-REG2 SECTION.
       INI-GUARDAR-REG2.
              MOVE FIO-PUT TO FIO-CMND.
              PERFORM LDC-FIO-LPC.
              IF NOT FIO-STAT-OKS
                 DISPLAY 'ERROR AL GRABAR SEQ-LPC: ' FIO-STAT
                 PERFORM CIERRA-ARCHIVOS
                 PERFORM GNS-PRO-END.
              ADD 1 TO WSS-NUM-RPRO IN WSS-VARI.
       FIN-GUARDAR-REG2.
           EXIT.


       LEE-LDC SECTION.
       INI-LEE-LDC.
           MOVE CLF-CIC-ICLF IN CLF TO LDC-CIC-ICLF IN LDC.
           MOVE ZEROES              TO LDC-NUM-ILDC IN LDC.
           MOVE FIO-GET-NLS TO FIO-CMND.
       LUP-LEE-LDC.
           PERFORM LDC-FIO-LDC.
           IF NOT ( FIO-STAT-OKS AND
                    CLF-CIC-ICLF IN CLF = LDC-CIC-ICLF IN LDC )
               GO TO FIN-LEE-LDC.

           MOVE 2                   TO LPC-COD-TREG IN LPC.
           MOVE LDC-KEY-ILDC        TO LPC-KEY-ILDC IN LPC.
           MOVE 'LDC'               TO TAB-COD-SIST.
           MOVE 'TLC'               TO TAB-COD-TTAB IN TAB.
           MOVE LDC-COD-TIPO IN LDC TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS NOT > SPACES
               MOVE TAB-GLS-DESC IN TAB TO LPC-GLS-NLDC IN LPC
           ELSE
               DISPLAY ' NO EXISTE TLC PARA LINEA :' LDC-KEY-ILDC.

           MOVE CTOT-LCR IN CCU-VARI(LDC-NUM-ILDC IN LDC) TO
                                 WSS-VAL-EDIT IN WSS-VARI.
           MOVE WSS-VAL-EDIT IN WSS-VARI TO LPC-VAL-CAUT IN LPC.
           MOVE CUTI-LCR IN CCU-VARI(LDC-NUM-ILDC IN LDC) TO
                                 WSS-VAL-EDIT IN WSS-VARI.
           MOVE WSS-VAL-EDIT IN WSS-VARI TO LPC-VAL-CUTI IN LPC.
           IF CDIS-LCR IN CCU-VARI(LDC-NUM-ILDC IN LDC) < ZEROES
              MOVE ZEROES TO WSS-VAL-EDIT IN WSS-VARI
              MOVE WSS-VAL-EDIT IN WSS-VARI TO LPC-VAL-CDIS IN LPC
           ELSE
              MOVE CDIS-LCR IN CCU-VARI(LDC-NUM-ILDC IN LDC) TO
                                 WSS-VAL-EDIT IN WSS-VARI
              MOVE WSS-VAL-EDIT IN WSS-VARI TO LPC-VAL-CDIS IN LPC.
           PERFORM GUARDAR-REG2.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-LEE-LDC.
       FIN-LEE-LDC.
           EXIT.


       COPY LDCBFLPC.
       COPY SGCBFBDE.
       COPY LDCBFRDC.
       COPY LDCBFCLF.
       COPY LDCBFLDC.

       COPY GNSBFTAB.
       COPY GNSBFMSG.

       COPY GNSBGDTC.
       COPY GNSBBMSG.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBBTAB.
       COPY GNSBGEND.
       COPY GNSBGABT.



