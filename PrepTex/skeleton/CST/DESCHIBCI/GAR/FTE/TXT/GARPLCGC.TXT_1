*%! Codigo adicional para archivo
*% IF CMT_ID
      *<<<< CMT_ID
      *>>>>
*% END
*% IF IOS
      *<<<< IOS
      *>>>>
*% END
*% IF RR
      *<<<< RR
      *>>>>
*% END
*% IF FD_RPT
      *<<<< FD_RPT
      *>>>>
*% END
*% IF FLS
      *<<<< FLS
      *>>>>
*% END
*% IF RW
      *<<<< RW
      *>>>>
*% END
*% IF WSS
      *<<<< WSS
       COPY GARBRCG2.
       COPY GARBRSCD.
       COPY GARBRGVT.
       COPY GARBRTAG.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY TABBRCAM.
       COPY TABBROFI.

       01  WSS-VARI.
           03  WSS-IND-LARC        VALUE SPACES    PIC X(01).
           03  WSS-IND-DISP        VALUE SPACES    PIC X(01).
           03  WSS-IND-RLEI        VALUE ZEROES    PIC 9(03).
           03  WSS-IND-RGRA        VALUE ZEROES    PIC 9(03).
           03  WSS-IND-RPRO        VALUE ZEROES    PIC 9(03).
           03  WSS-IND-RBAD        VALUE ZEROES    PIC 9(03).
           03  WSS-FEC-FPR2.
               05  WSS-NUM-DPR2    VALUE ZEROES    PIC 9(02).
               05  WSS-NUM-MPR2    VALUE ZEROES    PIC 9(02).
               05  WSS-NUM-SPR2    VALUE ZEROES    PIC 9(02).
               05  WSS-NUM-APR2    VALUE ZEROES    PIC 9(02).
           03  WSS-FEC-FING.
               05  WSS-NUM-SING    VALUE ZEROES    PIC 9(02).
               05  WSS-NUM-AING    VALUE ZEROES    PIC 9(02).
               05  WSS-NUM-MING    VALUE ZEROES    PIC 9(02).
               05  WSS-NUM-DING    VALUE ZEROES    PIC 9(02).
           03  WSS-FEC-CNT                         PIC X(08).
           03  WSS-COD-PESO.
               05  WSS-TIP-VCB-PESO                PIC X(01).
               05  FILLER                          PIC X(05).
           03  WSS-GLS-DATA.
               05  WSS-COD-CTA-DEB                 PIC X(10).
               05  WSS-COD-CTA-HAB                 PIC X(10).
           03  WSS-COD-CTAT.
               05  WSS-COD-CTAM                    PIC X(04).
               05  WSS-COD-CTAR                    PIC X(06).
           03  WSS-VAL-CNT         VALUE ZEROES    PIC S9(11)V9(04).
           03  WSS-COD-CTA.
               05  WSS-CLA-OPE-SUP                 PIC X(03).
               05  WSS-VCB                         PIC X(06).
           03  WSS-AUX-OFIO                        PIC X(03).
           03  WSS-GLS-MSGE.
               05  WSS-GLS-MSG1    VALUE SPACES    PIC X(20).
               05  WSS-GLS-MSG2    VALUE SPACES    PIC X(22).
               05  WSS-GLS-MSG3    VALUE SPACES    PIC X(08).
               05  WSS-GLS-MSG4    VALUE SPACES    PIC X(08).
               05  WSS-GLS-MSG5    VALUE SPACES    PIC X(08).
               05  WSS-GLS-MSG6    VALUE SPACES    PIC X(13).
               05  WSS-GLS-MSG7    VALUE SPACES    PIC X(12).
           03  WSS-BUSCA-MSG-ERROR    VALUE 0      PIC 9(01).
           03  WSS-NO-BUSCA-MSG-ERROR VALUE 1      PIC 9(01).
           03  WSS-IND-GET-MSG                     PIC 9(01).
           03  WSS-VAL-PESO        VALUE ZEROES    PIC S9(11).
           03  WSS-COD-VCAM.
               05  WSS-IND-MNAC    VALUE SPACES    PIC X(01).
               05  WSS-COD-MONE    VALUE SPACES    PIC X(05).
           03  WSS-COD-OFIC-ANT    VALUE SPACES    PIC X(03).
           03  WSS-COD-CTAM-ANT    VALUE SPACES    PIC X(04).
           03  WSS-COD-CTAR-ANT    VALUE SPACES    PIC X(06).
           03  WSS-IND-MNAC-ANT    VALUE SPACES    PIC X(01).
           03  WSS-COD-VCAM-ANT    VALUE SPACES    PIC X(05).
           03  WSS-GLS-CCBL-ANT    VALUE SPACES    PIC X(25).
           03  WSS-COD-OFIC-CUR    VALUE SPACES    PIC X(03).
           03  WSS-COD-CTAM-CUR    VALUE SPACES    PIC X(04).
           03  WSS-COD-CTAR-CUR    VALUE SPACES    PIC X(06).
           03  WSS-IND-MNAC-CUR    VALUE SPACES    PIC X(01).
           03  WSS-COD-VCAM-CUR    VALUE SPACES    PIC X(05).
           03  WSS-GLS-CCBL-CUR    VALUE SPACES    PIC X(25).
           03  WSS-VAL-ACNT        VALUE ZEROES    PIC S9(11)V9(04).
           03  WSS-VAL-ASIS        VALUE ZEROES    PIC S9(11)V9(04).
           03  WSS-VAL-ACNT-CUR    VALUE ZEROES    PIC S9(11)V9(04).
           03  WSS-VAL-ASIS-CUR    VALUE ZEROES    PIC S9(11)V9(04).
           03  WSS-SGV-SALD-ANT    VALUE ZEROES    PIC S9(13).
           03  WSS-SGV-SALD-CUR    VALUE ZEROES    PIC S9(13).
      *>>>>
*% END
*% IF WSS_DTC
      *<<<< WSS_DTC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
      *>>>>
*% END
*% IF RF
      *<<<< RF
      *>>>>
*% END
*% IF RT
      *<<<< RT
      *>>>>
*% END
*% IF CF_CGC_SRT_COD_VCAM
      *<<<< CF_CGC_SRT_COD_VCAM
      *>>>>
*% END
*% IF CF_CGC_SRT_COD_OFIC
      *<<<< CF_CGC_SRT_COD_OFIC
      *>>>>
*% END
*% IF CF_CGC_SRT_COD_CTAM
      *<<<< CF_CGC_SRT_COD_CTAM
      *>>>>
*% END
*% IF CF_CGC_SRT_IND_MNAC
      *<<<< CF_CGC_SRT_IND_MNAC
      *>>>>
*% END
*% IF PH_CGC
      *<<<< PH_CGC
      *>>>>
*% END
*% IF FIN_DEC
      *<<<< FIN_DEC
      *>>>>
*% END
*% IF INI
      *<<<< INI
      *>>>>
*% END
*% IF RS
      *<<<< RS
      *>>>>
*% END
*% IF FIN
      *<<<< FIN
      *>>>>
*% END
*% IF INI_INP
      *<<<< INI_INP
           DISPLAY ' '.
           DISPLAY 'CAPTURA DE PARAMETROS.'.
           DISPLAY '----------------------'.
           DISPLAY ' '.
           ACCEPT WSS-IND-DISP IN WSS-VARI.
           DISPLAY 'INDICADOR DE DISPLAY (S/N) ? '
                                 WSS-IND-DISP IN WSS-VARI.
           DISPLAY ' '.

      *RVM-CST-INI 20-NOV-1996
           DISPLAY 'FECHA DE PROCESO  (DDMMSSAA)  : '.
           ACCEPT WSS-FEC-FPR2 IN WSS-VARI.
           DISPLAY 'FECHA DE PROCESO ' WSS-FEC-FPR2 IN WSS-VARI.
      *RVM-CST-FIN 20-NOV-1996
           DISPLAY ' '.

      * Validacion de parametros.
           DISPLAY 'INICIO VALIDACION DE PARAMETROS'.
           DISPLAY 'VALIDACION FECHA PROCESO'.
      *VAL-FEC VALIDA FECHA DE TERMINO
           MOVE WSS-FEC-FPR2 IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-LE  TO FEC-CHOY.
           MOVE FEC-VHBL-S   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY FEC-MENS
               PERFORM GNS-PRO-END.

           DISPLAY 'VALIDACION INDICADOR DE DISPLAY (S/N)'.
           IF WSS-IND-DISP IN WSS-VARI NOT = 'S' AND
                                       NOT = 'N'
               MOVE 'N' TO WSS-IND-DISP IN WSS-VARI
               DISPLAY 'ERROR FORMATO INDICADOR DISPLAY, ASUME N'.

           DISPLAY 'FIN VALIDACION DE PARAMETROS'.
           DISPLAY ' '.

           MOVE WSS-NUM-DPR2 IN WSS-VARI TO SRT-NUM-DPRO IN SRT.
           MOVE WSS-NUM-MPR2 IN WSS-VARI TO SRT-NUM-MPRO IN SRT.

           MOVE WSS-NUM-DPR2 IN WSS-VARI TO WSS-NUM-DING IN WSS-VARI.
           MOVE WSS-NUM-MPR2 IN WSS-VARI TO WSS-NUM-MING IN WSS-VARI.
           MOVE WSS-NUM-SPR2 IN WSS-VARI TO WSS-NUM-SING IN WSS-VARI.
           MOVE WSS-NUM-APR2 IN WSS-VARI TO WSS-NUM-AING IN WSS-VARI.

      * Apertura de archivos
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GVT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-TAG.

 
      * Obtiene codigo valor de cambio de pesos mediante CIC de la moneda
           MOVE 'TAB'               TO FIO-SIST.
           MOVE 'VLR'               TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '            TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL'      TO FIO-AKEY.
           MOVE FIO-GET-KEY         TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               MOVE 'TAB'           TO MSG-COD-SIST FIO-SIST
               MOVE 'CIC0999  NEX'  TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC  TAB-CIC-CTAB
               PERFORM GNS-PRO-END.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.

           MOVE 'GAR'          TO FIO-SIST.
           MOVE 'VSN'          TO TAB-COD-TTAB IN TAB.
           MOVE 'GARLECARCCTB' TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
                    TAB-MSC-STAT IN TAB = 'S' )
               MOVE 'N' TO WSS-IND-LARC IN WSS-VARI
           ELSE
               MOVE 'S' TO WSS-IND-LARC IN WSS-VARI.

           DISPLAY ' '.
           DISPLAY 'LEE ARCHIVO CON SALDO CONTABLE ? : '
                                WSS-IND-LARC IN WSS-VARI.
           DISPLAY ' '.

           IF WSS-IND-LARC IN WSS-VARI = 'S'
      *         MOVE FIO-ACCS-UPD TO FIO-ACCS
      *         MOVE FIO-ACC      TO FIO-CMND
      *         PERFORM GAR-FIO-SCD
               MOVE FIO-INP TO FIO-CMND
               PERFORM GAR-FIO-SCD
               IF NOT FIO-STAT-OKS
                   DISPLAY 'ERROR APERTURA ARCHIVO SCD'
                   PERFORM GNS-PRO-END.
      *>>>>
*% END
*% IF INI_FST_INP
      *<<<< INI_FST_INP
      *>>>>
*% END
*% IF FIN_FST_INP
      *<<<< FIN_FST_INP
      *>>>>
*% END
*% IF LUP_INP
      *<<<< LUP_INP
           ADD 1 TO WSS-IND-RLEI IN WSS-VARI.

           MOVE SPACES TO MSG-COD-SIST.
           MOVE SPACES TO MSG-COD-MENS.

           PERFORM BUS-GDG.

           PERFORM BUS-TAG.

           PERFORM BUS-GVT.

           PERFORM BUS-OFI-CNT.

           MOVE GAR-VAL-AJT-SUP IN GVT TO WSS-VAL-CNT
                                          SRT-VAL-CNT IN SRT.
           PERFORM DET-VAL-PESO.
           IF MSG-COD-MENS > SPACES
               PERFORM ERR-INP
               GO TO NXT-INP-SRT.

           ADD 1 TO WSS-IND-RPRO IN WSS-VARI.

           MOVE WSS-VAL-CNT TO SRT-VAL-SIST IN SRT.

           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY ' '
               DISPLAY 'IDENT. DE GARANTIA    : ' GAR-CLV-GDG-1 IN CGC
               DISPLAY 'OFICINA GARANTIA      : ' GAR-COD-OFI IN CGC
               DISPLAY 'TIPO/SUBTIPO GARANTIA : ' GAR-CLA-OPE IN CGC
               DISPLAY 'MONEDA DE GARANTIA    : ' GAR-VCB IN CGC
               DISPLAY 'MONTO GVT MON. ORIGEN : ' GAR-VAL-AJT-SUP IN GVT
               DISPLAY 'FECHA REVALORIZACION  : ' GAR-FEC-RVL IN GVT
               DISPLAY 'CUENTA DE DCC         : ' WSS-COD-CTA-DEB
               DISPLAY 'MONTO GVT EN PESOS    : ' WSS-VAL-CNT
               DISPLAY ' '.

           IF WSS-IND-LARC IN WSS-VARI = 'S'
               PERFORM OBT-SALDO-CTBL
           ELSE
               MOVE ZEROES TO SRT-SGV-SALD IN SRT.

           RELEASE SRT.
           GO TO NXT-INP-SRT.
      *>>>>
*% END
*% IF RM
      *<<<< RM
      *>>>>
*% END
*% IF INI_NXT_INP
      *<<<< INI_NXT_INP
      *>>>>
*% END
*% IF FIN_NXT_INP
      *<<<< FIN_NXT_INP
      *>>>>
*% END
*% IF FIN_INP
      *<<<< FIN_INP
           IF WSS-IND-LARC IN WSS-VARI = 'S'
               MOVE FIO-CLO TO FIO-CMND
               PERFORM GAR-FIO-SCD.
      *>>>>
*% END
*% IF INI_OUT
      *<<<< INI_OUT
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-CG2.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR APERTURA ARCHIVO GAR CG2'
               PERFORM GNS-PRO-ABT.
      *>>>>
*% END
*% IF FST_OUT
      *<<<< FST_OUT
      *Obtiene nombre de sucursal mediante BUS-OFI
           IF SRT-COD-OFIC IN SRT > SPACES
               MOVE SRT-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               IF MSG-COD-MENS NOT > SPACES
                   MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIC IN WSS-CGC
               ELSE
                   MOVE SPACES TO WSS-GLS-OFIC IN WSS-CGC
           ELSE
               MOVE SPACES TO WSS-GLS-OFIC IN WSS-CGC.

           MOVE SRT-COD-OFIC IN SRT TO WSS-COD-OFIC-ANT.
           MOVE SRT-IND-MNAC IN SRT TO WSS-IND-MNAC-ANT.
           MOVE SRT-COD-VCAM IN SRT TO WSS-COD-VCAM-ANT.
           MOVE SRT-COD-CTAM IN SRT TO WSS-COD-CTAM-ANT.
           MOVE SRT-COD-CTAR IN SRT TO WSS-COD-CTAR-ANT.
           MOVE SRT-GLS-CCBL IN SRT TO WSS-GLS-CCBL-ANT.
           MOVE SRT-SGV-SALD IN SRT TO WSS-SGV-SALD-ANT.
      *>>>>
*% END
*% IF LUP_OUT
      *<<<< LUP_OUT
           IF WSS-IND-DISP IN WSS-VARI = 'S'
               DISPLAY '------------LUP OUT---------------'
               DISPLAY 'SRT COD OFIC ' SRT-COD-OFIC IN SRT
               DISPLAY 'SRT IND MNAC ' SRT-IND-MNAC IN SRT
               DISPLAY 'SRT COD VCAM ' SRT-COD-VCAM IN SRT
               DISPLAY 'MONTO M ORIGEN ' SRT-VAL-CNT IN SRT
               DISPLAY 'MONTO EN PESOS ' SRT-VAL-CNT IN SRT
               DISPLAY 'SRT COD CTAM ' SRT-COD-CTAM IN SRT
               DISPLAY 'SRT COD CTAR ' SRT-COD-CTAR IN SRT
               DISPLAY 'SRT GLS CCBL ' SRT-GLS-CCBL IN SRT
               DISPLAY 'SRT SGV SALD ' SRT-SGV-SALD IN SRT.

           MOVE SRT TO CG2.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-CG2.
           ADD 1 TO WSS-IND-RGRA IN WSS-VARI.

           IF SRT-COD-OFIC IN SRT = WSS-COD-OFIC-ANT AND
              SRT-IND-MNAC IN SRT = WSS-IND-MNAC-ANT AND
              SRT-COD-CTAM IN SRT = WSS-COD-CTAM-ANT AND
              SRT-COD-CTAR IN SRT = WSS-COD-CTAR-ANT
               ADD SRT-VAL-CNT IN SRT TO WSS-VAL-ACNT
               ADD SRT-VAL-SIST IN SRT TO WSS-VAL-ASIS
               GO TO NXT-OUT-SRT.

           IF WSS-IND-LARC IN WSS-VARI = 'S'
               SUBTRACT WSS-SGV-SALD-ANT FROM WSS-VAL-ASIS
                                   GIVING WSS-SGV-DIFE IN WSS-CGC
           ELSE
               MOVE ZEROES TO WSS-SGV-DIFE IN WSS-CGC.

           PERFORM RESTAURA-SRT.
      *Obtiene nombre de sucursal mediante BUS-OFI
           IF SRT-COD-OFIC IN SRT > SPACES
               MOVE SRT-COD-OFIC IN SRT TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               IF MSG-COD-MENS NOT > SPACES
                   MOVE OFI-GLS-DESC IN OFI TO WSS-GLS-OFIC IN WSS-CGC
               ELSE
                   MOVE SPACES TO WSS-GLS-OFIC IN WSS-CGC
           ELSE
               MOVE SPACES TO WSS-GLS-OFIC IN WSS-CGC.

           GO TO NXT-OUT-SRT.
      *>>>>
*% END
*% IF FIN_GEN_OUT
      *<<<< FIN_GEN_OUT
      *>>>>
*% END
*% IF FIN_OUT
      *<<<< FIN_OUT
           MOVE WSS-COD-OFIC-ANT TO SRT-COD-OFIC IN SRT.
           MOVE WSS-IND-MNAC-ANT TO SRT-IND-MNAC IN SRT.
           MOVE WSS-COD-CTAM-ANT TO SRT-COD-CTAM IN SRT.
           MOVE WSS-COD-CTAR-ANT TO SRT-COD-CTAR IN SRT.
           MOVE WSS-GLS-CCBL-ANT TO SRT-GLS-CCBL IN SRT.
           MOVE WSS-VAL-ACNT     TO SRT-VAL-CNT IN SRT.
           MOVE WSS-VAL-ASIS     TO SRT-VAL-SIST IN SRT.
           MOVE WSS-SGV-SALD-ANT TO SRT-SGV-SALD IN SRT.

           IF WSS-IND-LARC IN WSS-VARI = 'S'
               SUBTRACT WSS-SGV-SALD-ANT FROM WSS-VAL-ASIS
                                   GIVING WSS-SGV-DIFE IN WSS-CGC
           ELSE
               MOVE ZEROES TO WSS-SGV-DIFE IN WSS-CGC.

           GENERATE DL-CGC.

           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-CG2.

           DISPLAY 'ESTADISTICAS DE PROCESO : '.
           DISPLAY '-------------------------'.
           DISPLAY ' '.
           DISPLAY 'REGISTROS LEIDOS     : ' WSS-IND-RLEI IN WSS-VARI.
           DISPLAY 'REGISTROS PROCESADOS : ' WSS-IND-RPRO IN WSS-VARI.
           DISPLAY 'REGISTROS RECHAZADOS : ' WSS-IND-RBAD IN WSS-VARI.
           DISPLAY 'REGISTROS GRABADOS EN ARCHIVO CG2 : ' 
                                             WSS-IND-RGRA IN WSS-VARI.
      *>>>>
*% END
*% IF FIN_TMT
      *<<<< FIN_TMT
      *>>>>
*% END
*% IF EXT_OUT
      *<<<< EXT_OUT
      *>>>>
*% END
*% IF EOF
      *<<<< EOF
       COPY GARBFCG2.
       COPY GARBFSCD.
       COPY GARBFGVT.
       COPY GARBFTAG.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY TABBFCAM.
       COPY TABBFOFI.
       COPY TABBBOFI.
       COPY GNSBGFEC.
       COPY GNSBGHOY.

      ******************************************************************
      * Proceso de seleccion a partir de registro CGC
       BUS-GDG SECTION.
       INI-BUS-GDG.
      * BUSCA LA GARANTIA
           IF GDG-FEC-ING-SIS IN CGC > WSS-FEC-FING IN WSS-VARI
               MOVE 'GAR'                TO MSG-COD-SIST
               MOVE 'GDG    FECNO'       TO MSG-COD-MENS
               GO TO FIN-BUS-GDG.

      *Moneda de la Garantia
           MOVE GAR-VCB IN CGC           TO WSS-COD-VCAM IN WSS-VARI.
           MOVE WSS-IND-MNAC IN WSS-VARI TO SRT-IND-MNAC IN SRT.
           MOVE WSS-COD-MONE IN WSS-VARI TO SRT-COD-VCAM IN SRT.

      *Oficina de la Garantia
           MOVE GAR-COD-OFI IN CGC       TO SRT-COD-OFIC IN SRT.
       FIN-BUS-GDG.
           EXIT.


      ******************************************************************
       BUS-TAG SECTION.
       INI-BUS-TAG.
           IF MSG-COD-MENS > SPACES
               MOVE WSS-BUSCA-MSG-ERROR TO WSS-IND-GET-MSG
               GO TO FIN-BUS-TAG.

      * Busca Tabla TAG
           MOVE GAR-TIP-OPE IN CGC TO GAR-TIP-OPE IN TAG.
           MOVE GAR-STP-OPE IN CGC TO GAR-STP-OPE IN TAG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
               MOVE 'GAR'        TO MSG-COD-SIST
               MOVE 'TAG    NEX' TO MSG-COD-MENS
               GO TO FIN-BUS-TAG.

      * Busca Codigo Cuenta Contable en Tabla DCC de GAR
           MOVE 'GAR'                  TO TAB-COD-SIST.
           MOVE 'DCC'                  TO TAB-COD-TTAB IN TAB.
           MOVE GAR-CLA-OPE-SUP IN TAG TO WSS-CLA-OPE-SUP.
           MOVE GAR-VCB         IN CGC TO WSS-VCB.
           MOVE WSS-COD-CTA            TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               MOVE 'GAR'        TO MSG-COD-SIST
               MOVE 'DCC    NEX' TO MSG-COD-MENS
               DISPLAY 'DCC : ' TAB-COD-CTAB IN TAB
               GO TO FIN-BUS-TAG.

           MOVE TAB-GLS-DATA IN TAB      TO WSS-GLS-DATA IN WSS-VARI.

           MOVE WSS-COD-CTA-DEB TO WSS-COD-CTAT IN WSS-VARI.
           MOVE WSS-COD-CTAM IN WSS-VARI TO SRT-COD-CTAM IN SRT.
           MOVE WSS-COD-CTAR IN WSS-VARI TO SRT-COD-CTAR IN SRT.

      * Busca Glosa Cuenta Contable en Tabla PCC de TAB
           MOVE 'TAB'           TO TAB-COD-SIST.
           MOVE 'PCC'           TO TAB-COD-TTAB IN TAB.
           MOVE WSS-COD-CTA-DEB TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               MOVE 'TAB'        TO MSG-COD-SIST
               MOVE 'PCC    NEX' TO MSG-COD-MENS
               DISPLAY 'CUENTA : ' TAB-COD-CTAB IN TAB
               GO TO FIN-BUS-TAG.

           MOVE TAB-GLS-DESC IN TAB TO SRT-GLS-CCBL IN SRT.
       FIN-BUS-TAG.
           EXIT.


      ******************************************************************
      * BUSCA EL VALOR CONTABILIZADO EN LA TASACION VIGENTE
       BUS-GVT SECTION.
       INI-BUS-GVT.
           IF MSG-COD-MENS > SPACES
               MOVE WSS-BUSCA-MSG-ERROR TO WSS-IND-GET-MSG
               GO TO FIN-BUS-GVT.

           MOVE GAR-NUM-SIS IN CGC TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN CGC TO GAR-COD-CRR IN GVT.
           MOVE GAR-COD-TSN IN CGC TO GAR-COD-TSN IN GVT.
           MOVE 'GAR-CLV-GVT-1'    TO FIO-AKEY.
           MOVE FIO-GET-KEY        TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               MOVE 'GAR'        TO MSG-COD-SIST
               MOVE 'GVT    NEX' TO MSG-COD-MENS.
       FIN-BUS-GVT.
           EXIT.


      ******************************************************************
       OBT-SALDO-CTBL SECTION.
       INI-OBT-SALDO-CTBL.
           MOVE WSS-COD-CTA-DEB IN WSS-VARI TO SCD-CIC-CCSB IN SCD.
           MOVE 0                           TO SCD-NUM-CTTE IN SCD.
           MOVE GAR-COD-OFI IN CGC          TO SCD-COD-OFIC IN SCD.
           MOVE FIO-GET-KEY                 TO FIO-CMND.
           PERFORM GAR-FIO-SCD.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE REGISTRO '
                       SCD-KEY-IREG IN SCD
                       ' EN TABLA SCD'
               MOVE ZEROES TO SRT-SGV-SALD IN SRT
               GO TO FIN-OBT-SALDO-CTBL.
           MOVE SCD-SGV-SALD IN SCD TO SRT-SGV-SALD IN SRT.
       FIN-OBT-SALDO-CTBL.
           EXIT.


      ******************************************************************
       BUS-OFI-CNT SECTION.
       INI-BUS-OFI-CNT.
      * Carga oficina contable en SRT
           IF MSG-COD-MENS > SPACES
               MOVE WSS-BUSCA-MSG-ERROR TO WSS-IND-GET-MSG
               GO TO FIN-BUS-OFI-CNT.

      * OJO: en BCI, en M/N se contabiliza siempre en la oficina de orig
      *              en M/X se contabiliza en oficinas centralizadoras
           IF GAR-TIP-VCB IN CGC = WSS-TIP-VCB-PESO
               MOVE GAR-COD-OFI IN CGC TO SRT-COD-OFIC IN SRT
           ELSE
               MOVE GAR-COD-OFI IN CGC TO OFI-COD-OFIC IN OFI
               PERFORM BUS-OFI
               MOVE OFI-COD-OFCO IN OFI TO SRT-COD-OFIC IN SRT.
       FIN-BUS-OFI-CNT.
           EXIT.


      ******************************************************************
      * OBTIENE VALOR DE CAMBIO EN PESOS PARA MONEDA NACIONAL
        DET-VAL-PESO SECTION.
        INI-DET-VAL-PESO.
      * OBTIENE VALOR DE CAMBIO EN PESOS A FECHA CONTABILIZACION
           IF MSG-COD-MENS > SPACES
               MOVE WSS-BUSCA-MSG-ERROR TO WSS-IND-GET-MSG
               GO TO FIN-DET-VAL-PESO.

           IF NOT (
              GAR-TIP-VCB IN CGC = '0' AND
              GAR-VCB IN CGC NOT = WSS-COD-PESO IN WSS-VARI
              )
               GO TO FIN-DET-VAL-PESO.

           MOVE GAR-FEC-RVL IN GVT       TO CAM-FEC-FCAM IN CAM(1).
           MOVE GAR-FEC-RVL IN GVT       TO CAM-FEC-FCAM IN CAM(2).
           MOVE GAR-VCB IN CGC           TO CAM-COD-VCAM IN CAM(1).
           MOVE WSS-COD-PESO IN WSS-VARI TO CAM-COD-VCAM IN CAM(2).
           MOVE FIO-GET-KEY              TO FIO-CMND.
           PERFORM TAB-FIO-CAM.
           IF NOT (
              FIO-STAT-OKS AND CAM-IND-VIGE IN CAM = 'S'
              )
               MOVE 'CAM    NEX' TO MSG-COD-MENS
      *RVM:
               MOVE 'VCAM NO EXISTE O NO '   TO WSS-GLS-MSG1
               MOVE 'ESTA VIGTE. A FECHA : ' TO WSS-GLS-MSG2
               MOVE WSS-FEC-FPR2 IN WSS-VARI TO WSS-GLS-MSG3
               MOVE ' PARA : '               TO WSS-GLS-MSG4
               MOVE GAR-VCB IN CGC           TO WSS-GLS-MSG5
               MOVE ' EN OPERC. : '          TO WSS-GLS-MSG6
               MOVE GAR-CLV-GDG-1 IN CGC     TO WSS-GLS-MSG7
               MOVE WSS-GLS-MSGE IN WSS-VARI TO MSG-GLS-DESC IN MSG
      *
               MOVE WSS-NO-BUSCA-MSG-ERROR TO WSS-IND-GET-MSG
               GO TO FIN-DET-VAL-PESO
           ELSE
               MULTIPLY WSS-VAL-CNT BY CAM-SGV-VCAM IN CAM GIVING
                        WSS-VAL-PESO ROUNDED
               MOVE WSS-VAL-PESO TO WSS-VAL-CNT.
       FIN-DET-VAL-PESO.
           EXIT.


      ******************************************************************
      * Despliega mensaje error en la entrada
       ERR-INP SECTION.
       INI-ERR-INP.
           IF WSS-IND-GET-MSG = WSS-BUSCA-MSG-ERROR
               PERFORM GET-MSG.
           DISPLAY MSG-COD-MENS ' ' MSG-GLS-DESC
           DISPLAY ' CON ID GARANTIA : ' GAR-CLV-GDG-1 IN CGC
                   ' Y TSN : ' GAR-COD-TSN IN GVT.
           ADD 1 TO WSS-IND-RBAD IN WSS-VARI.
           MOVE WSS-NO-BUSCA-MSG-ERROR TO WSS-IND-GET-MSG.
       FIN-ERR-INP.
           EXIT.


      ******************************************************************
       RESTAURA-SRT SECTION.
       INI-RESTAURA-SRT.
           MOVE SRT-COD-OFIC IN SRT TO WSS-COD-OFIC-CUR.
           MOVE SRT-IND-MNAC IN SRT TO WSS-IND-MNAC-CUR.
           MOVE SRT-COD-VCAM IN SRT TO WSS-COD-VCAM-CUR.
           MOVE SRT-COD-CTAM IN SRT TO WSS-COD-CTAM-CUR.
           MOVE SRT-COD-CTAR IN SRT TO WSS-COD-CTAR-CUR.
           MOVE SRT-GLS-CCBL IN SRT TO WSS-GLS-CCBL-CUR.
           MOVE SRT-VAL-CNT IN SRT TO WSS-VAL-ACNT-CUR.
           MOVE SRT-VAL-SIST IN SRT TO WSS-VAL-ASIS-CUR.
           MOVE SRT-SGV-SALD IN SRT TO WSS-SGV-SALD-CUR.

           MOVE WSS-COD-OFIC-ANT TO SRT-COD-OFIC IN SRT.
           MOVE WSS-IND-MNAC-ANT TO SRT-IND-MNAC IN SRT.
           MOVE WSS-COD-VCAM-ANT TO SRT-COD-VCAM IN SRT.
           MOVE WSS-COD-CTAM-ANT TO SRT-COD-CTAM IN SRT.
           MOVE WSS-COD-CTAR-ANT TO SRT-COD-CTAR IN SRT.
           MOVE WSS-GLS-CCBL-ANT TO SRT-GLS-CCBL IN SRT.
           MOVE WSS-VAL-ACNT     TO SRT-VAL-CNT IN SRT.
           MOVE WSS-VAL-ASIS     TO SRT-VAL-SIST IN SRT.
           MOVE WSS-SGV-SALD-ANT TO SRT-SGV-SALD IN SRT.
           GENERATE DL-CGC.
           MOVE ZEROES           TO WSS-VAL-ACNT.
           MOVE ZEROES           TO WSS-VAL-ASIS.
           ADD WSS-VAL-ACNT-CUR  TO WSS-VAL-ACNT IN WSS-VARI.
           ADD WSS-VAL-ASIS-CUR  TO WSS-VAL-ASIS IN WSS-VARI.
           MOVE WSS-COD-OFIC-CUR TO SRT-COD-OFIC IN SRT
                                    WSS-COD-OFIC-ANT.
           MOVE WSS-IND-MNAC-CUR TO SRT-IND-MNAC IN SRT
                                    WSS-IND-MNAC-ANT.
           MOVE WSS-COD-VCAM-CUR TO SRT-COD-VCAM IN SRT
                                    WSS-COD-VCAM-ANT.
           MOVE WSS-COD-CTAM-CUR TO SRT-COD-CTAM IN SRT
                                    WSS-COD-CTAM-ANT.
           MOVE WSS-COD-CTAR-CUR TO SRT-COD-CTAR IN SRT
                                    WSS-COD-CTAR-ANT.
           MOVE WSS-GLS-CCBL-CUR TO SRT-GLS-CCBL IN SRT
                                    WSS-GLS-CCBL-ANT.
           MOVE WSS-VAL-ACNT-CUR TO SRT-VAL-CNT IN SRT.
           MOVE WSS-VAL-ASIS-CUR TO SRT-VAL-SIST IN SRT.
           MOVE WSS-SGV-SALD-CUR TO SRT-SGV-SALD IN SRT
                                    WSS-SGV-SALD-ANT.
       FIN-RESTAURA-SRT.
           EXIT.
      *>>>>
*% END
