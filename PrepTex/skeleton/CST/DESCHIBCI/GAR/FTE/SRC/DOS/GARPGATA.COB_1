       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGATA.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    26-MAY-2003.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      * Archivo Gargis, contiene: Garantia, Cic-Cliente.
       COPY GARBRGIS.
      *
      * Archivo Garvts,contiene Ultima Tasacion, igual Registro GVT.
       COPY GARBRVTS.
      *
      * Registros de Archivos secuenciales aplanados de las Bases
      * (GARBRGDS = GDG, GARBRDDS = GDD, GARBRGVS = GVT)
       COPY GARBRGDS.
       COPY GARBRDDS.
       COPY GARBRGVS.
      *
      * Registros de las Bases Originales.
       COPY GARBRGDG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GARBRGDD.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GARBRGVT.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
      *MHM-INI 20-JUN-2003
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *MHM-FIN 20-JUN-2003
      *
       01  WS-VARIABLES.
           02 WS-LEE-DDS-FST       PIC 9(01) VALUE ZEROS.
           02 WS-LEE-GVS-FST       PIC 9(01) VALUE ZEROS.
           02 AUX-GAR-TIP-OPE      PIC X(03) VALUE SPACES.
           02 FIN-CICLO            PIC 9(01) VALUE ZEROS.
           02 WS-CON-GDS           PIC 9(09) VALUE ZEROS.
           02 WS-CON-GRA           PIC 9(09) VALUE ZEROS.
           02 WS-CON-NGR           PIC 9(09) VALUE ZEROS.
           02 WS-CON-PRO           PIC 9(09) VALUE ZEROS.
           02 CON-GRT-PRO          PIC 9(09) VALUE ZEROS.
           02 CON-GRT-NPR          PIC 9(09) VALUE ZEROS.
           02 CON-GRT-BUENA        PIC 9(09) VALUE ZEROS.

      *MHM-INI 20-JUN-2003
       01  WSS-VARI.
           03 WSS-GLS-DESC.
              05 WSS-GLS-DES1     VALUE SPACES          PIC X(40).
              05 WSS-GLS-DES2     VALUE SPACES          PIC X(40).

           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                           PIC 9(02).
              05 WSS-NUM-APRO                           PIC 9(02).
              05 WSS-NUM-MPRO                           PIC 9(02).
              05 WSS-NUM-DPRO                           PIC 9(02).

           03 WSS-DMA-FPRO.
              05 WSS-DMA-DPRO                           PIC 9(02).
              05 WSS-DMA-MPRO                           PIC 9(02).
              05 WSS-DMA-SPRO                           PIC 9(02).
              05 WSS-DMA-APRO                           PIC 9(02).

           03 WSS-IND-GDGP        VALUE SPACES          PIC X(01).
           03 WSS-GLS-TIPO.
              05 WSS-TIP-GTIA     VALUE SPACES          PIC X(03).
              05 WSS-STP-GTIA     VALUE SPACES          PIC X(03).

      *MHM-FIN 20-JUN-2003

       01  AUX-GDG-KEY.
           02 AUX-GAR-NUM-SIS      PIC X(04).
           02 AUX-GAR-COD-CRR      PIC X(08).
           02 AUX-COD-TSN          PIC 9(03).

       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGATA" TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
      *MHM-INI 20-JUN-2003
           PERFORM CAPTURA-PARAMETROS.
           PERFORM VALIDA-PARAMETROS.
           PERFORM PROCESA-FECHAS.
      *MHM-FIN 20-JUN-2003
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-GAR-GDS.
           MOVE ZEROS TO WS-CON-GDS.
           MOVE ZEROS TO WS-CON-GRA.
           MOVE ZEROS TO WS-CON-NGR.
           MOVE ZEROS TO WS-CON-PRO.
           MOVE ZEROS TO CON-GRT-PRO.
           MOVE ZEROS TO CON-GRT-NPR.
           MOVE ZEROS TO CON-GRT-BUENA.
           MOVE 1  TO WS-LEE-DDS-FST.
           MOVE 1  TO WS-LEE-GVS-FST.
           PERFORM PROCESA-GAR UNTIL FIO-STAT-EOF.
           DISPLAY 'REGISTROS LEIDOS DE GDS   ' WS-CON-GDS.
           DISPLAY 'REGISTROS PROCESS.  GDS   ' WS-CON-PRO.
           DISPLAY 'REGISTROS GRABADOS EN GIS ' WS-CON-GRA.
           DISPLAY 'REGISTROS NOGRABADOS GIS  ' WS-CON-NGR.
           DISPLAY 'VTS PROCESADOS      ' CON-GRT-PRO.
           DISPLAY 'VTS GRABADAS BUENAS ' CON-GRT-BUENA.
           DISPLAY 'VTS NO PROCESADAS   ' CON-GRT-NPR.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-DDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GVS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-GIS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-VTS.
       FIN-OPEN-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
      *MHM-INI 20-JUN-2003
       CAPTURA-PARAMETROS SECTION.
       INI-CAPTURA-PARAMETROS.
      *    Captura Fecha de Proceso
           ACCEPT WSS-DMA-FPRO.
           DISPLAY 'FECHA PROCESO = ' WSS-DMA-FPRO.
       FIN-CAPTURA-PARAMETROS.
           EXIT.
      *
       VALIDA-PARAMETROS SECTION.
       INI-VALIDA-PARAMETROS.
           MOVE WSS-DMA-FPRO TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-S   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF NOT FEC-STAT-OKS
               DISPLAY 'PROCESO ERRONEA, ' FEC-MENS
               PERFORM GNS-PRO-ABT.
       FIN-VALIDA-PARAMETROS.
           EXIT.

       PROCESA-FECHAS SECTION.
       INI-PROCESA-FECHAS.
           MOVE WSS-DMA-SPRO  TO WSS-NUM-SPRO.
           MOVE WSS-DMA-APRO  TO WSS-NUM-APRO.
           MOVE WSS-DMA-MPRO  TO WSS-NUM-MPRO.
           MOVE WSS-DMA-DPRO  TO WSS-NUM-DPRO.
       FIN-PROCESA-FECHAS.
           EXIT.
      *MHM-FIN 20-JUN-2003

       LEE-GAR-GDS SECTION.
       LUP-PRO-GDS.
           PERFORM GAR-FIO-GDS.
           ADD 1 TO WS-CON-GDS.
           MOVE GDS TO GDG.
       FIN-LEE-GAR-GDS.
           EXIT.

       PROCESA-GAR SECTION.
       PROCESA-GAR-INI.
           PERFORM LEE-TAB-TIO.
      *MHM-INI 20-JUN-2003
           PERFORM LEE-TAB-AUX.
      *MHM-FIN 20-JUN-2003
      *RDG 09-JUL-2003
      *RDG    IF AUX-GAR-TIP-OPE = "951" OR = "952" THEN
      *RDG 09-JUL-2003
      *MHM-INI 20-JUN-2003
      *        IF GAR-ES1  IN GDS = "10" THEN
              IF GAR-ES1  IN GDS = "10" AND
                 GAR-IND-CPL IN GDS = 'S' AND
                 WSS-IND-GDGP IN WSS-VARI NOT = 'N'
      *MHM-FIN 20-JUN-2003
                 ADD 1 TO WS-CON-PRO
      *RDG-INI 07-JUL-2003
      *          MOVE 1 TO FIN-CICLO
      *          PERFORM GAR-OBTENGO-DEUDOR UNTIL FIN-CICLO = 2
      *          MOVE 1 TO FIN-CICLO
      *RDG-FIN 07-JUL-2003
                 MOVE ZEROS TO FIN-CICLO
                 PERFORM GAR-OBTENGO-ULTASA UNTIL FIN-CICLO = 2.
           MOVE FIO-GET-NXT  TO FIO-CMND.
           PERFORM LEE-GAR-GDS.
       FIN-PROCESA-GAR.
           EXIT.

       LEE-TAB-TIO SECTION.
       INI-LEE-TAB.
           IF GAR-TIP-OPE IN GDS > SPACES THEN
              MOVE 'TAB'              TO TAB-COD-SIST
              MOVE 'TIO'              TO TAB-COD-TTAB IN TAB
              MOVE SPACES             TO TAB-COD-CTAB IN TAB
              MOVE GAR-TIP-OPE IN GDS TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
              IF MSG-COD-MENS = SPACES THEN
                 MOVE TAB-CIC-CTAB IN TAB TO AUX-GAR-TIP-OPE.
       FIN-LEE-TAB.
           EXIT.

      *MHM-INI 20-JUN-2003
       LEE-TAB-AUX SECTION.
       INI-LEE-TAB-AUX.
           MOVE GAR-TIP-OPE IN GDS TO WSS-TIP-GTIA IN WSS-VARI.
           MOVE GAR-STP-OPE IN GDS TO WSS-STP-GTIA IN WSS-VARI.
           IF WSS-GLS-TIPO IN WSS-VARI > SPACES
              MOVE 'TAB'                    TO TAB-COD-SIST
              MOVE 'AUX'                    TO TAB-COD-TTAB IN TAB
              MOVE SPACES                   TO TAB-COD-CTAB IN TAB
              MOVE WSS-GLS-TIPO IN WSS-VARI TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
              IF MSG-COD-MENS = SPACES
                 MOVE TAB-COD-DAT1 IN TAB-GLS-DAT1(3) TO
                                           WSS-IND-GDGP IN WSS-VARI.
       FIN-LEE-TAB-AUX.
           EXIT.

      *MHM-FIN 20-JUN-2003

       GAR-OBTENGO-DEUDOR SECTION.
       INI-GAR-DEUDOR.
           IF WS-LEE-DDS-FST = 1 THEN
              MOVE FIO-GET-FST    TO FIO-CMND
              PERFORM LEE-GAR-DDS
              MOVE 2   TO WS-LEE-DDS-FST.
           IF GAR-CLV-GDG-1 IN GDS = GAR-CLV-GDD-2 IN DDS THEN
              ADD  1 TO WS-CON-GRA
              PERFORM PROC-DEUDOR UNTIL GAR-CLV-GDG-1 IN GDS
                                  NOT = GAR-CLV-GDD-2 IN DDS
              MOVE 1 TO FIN-CICLO
           ELSE
              IF GAR-CLV-GDG-1 IN GDS > GAR-CLV-GDD-2 IN DDS THEN
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-GAR-DDS
              ELSE
                 DISPLAY 'GARANTIA SIN DEUDOR ' GAR-CLV-GDG-1 IN GDS
                 ADD  1 TO WS-CON-NGR
                 MOVE 1 TO FIN-CICLO.
       FIN-GAR-DEUDOR.
           EXIT.

       LEE-GAR-DDS SECTION.
       INI-LEE-GAR-DDS.
           PERFORM GAR-FIO-DDS.
           MOVE DDS TO GDD.
       FIN-LEE-GAR-DDS.
           EXIT.

       PROC-DEUDOR SECTION.
       INI-PROC-DEUDOR.
           MOVE GAR-NUM-SIS  IN GDS  TO GIS-GDG-NUM-SIS.
           MOVE GAR-COD-CRR  IN GDS  TO GIS-GDG-COD-CRR.
           MOVE GDD-CAI-ICLI IN DDS  TO GIS-GDD-CAI-ICLI.
           MOVE GDD-IIC-ICLI IN DDS  TO GIS-GDD-ICC-ICLI.
           MOVE FIO-PUT   TO FIO-CMND.
           PERFORM GAR-FIO-GIS.
           MOVE FIO-GET-NXT  TO FIO-CMND.
           PERFORM LEE-GAR-DDS.
       FIN-PROC-DEUDOR.
           EXIT.
      *
      *-----------------------------------------------------------------
      *
       GAR-OBTENGO-ULTASA SECTION.
       INI-GAR-OBTENGO-ULTASA.
           PERFORM GDG-PASO-KEY.
           IF WS-LEE-GVS-FST = 1 THEN
              MOVE FIO-GET-FST    TO FIO-CMND
              PERFORM LEE-GAR-GVS
              MOVE 2   TO WS-LEE-GVS-FST.
           IF AUX-GDG-KEY = GAR-CLV-GVT-1 IN GVS THEN
              ADD 1 TO CON-GRT-PRO
              PERFORM PROC-ULTASA
              MOVE 2 TO FIN-CICLO
           ELSE
              IF AUX-GDG-KEY > GAR-CLV-GVT-1 IN GVS THEN
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-GAR-GVS
              ELSE
                 ADD 1 TO CON-GRT-NPR
                 DISPLAY 'GARANT SIN VALOR-TASA: ' AUX-GDG-KEY
                 MOVE 2 TO FIN-CICLO.
       FIN-GAR-OBTENGO-ULTASA.
           EXIT.

       LEE-GAR-GVS SECTION.
       INI-LEE-GAR-GVS.
           PERFORM GAR-FIO-GVS.
           MOVE GVS TO GVT.
       FIN-LEE-GAR-GVS.
           EXIT.

       GDG-PASO-KEY SECTION.
       INI-GDG-PASO-KEY.
           MOVE GAR-NUM-SIS IN GDS TO AUX-GAR-NUM-SIS.
           MOVE GAR-COD-CRR IN GDS TO AUX-GAR-COD-CRR.
           MOVE GAR-COD-TSN IN GDS TO AUX-COD-TSN.
       FIN-GDG-PASO-KEY.
           EXIT.

       PROC-ULTASA SECTION.
       INI-ULTASA-GVS.
      *MHM-INI 20-JUN-2003
           IF GVT-IDN-USU-ACT IN GVT = 'GARPGVGV' OR
              GAR-FEC-TSN IN GVT < WSS-FEC-FPRO IN WSS-VARI
               GO TO NXT-ULTASA-GVS.
      *MHM-FIN 20-JUN-2003
      *RDG-INI 03-JUL-2003
           ADD 1 TO CON-GRT-BUENA.
           PERFORM GAR-OBTENGO-DEUDOR UNTIL FIN-CICLO = 1.
      *RDG-FIN 03-JUL-2003
           MOVE SPACES                 TO GVT-GLS-FLAG    IN VTS.
           MOVE SPACES                 TO GAR-NUM-SIS     IN VTS.
           MOVE SPACES                 TO GAR-COD-CRR     IN VTS.
           MOVE ZEROS                  TO GAR-COD-TSN     IN VTS.
           MOVE ZEROS                  TO GAR-FA-TSN      IN VTS.
           MOVE ZEROS                  TO GAR-FM-TSN      IN VTS.
           MOVE ZEROS                  TO GAR-FD-TSN      IN VTS.
           MOVE ZEROS                  TO GAR-VAL-LIQ-TSN IN VTS.
           MOVE ZEROS                  TO GAR-VAL-CML-TSN IN VTS.
           MOVE ZEROS                  TO GAR-PCT-PON     IN VTS.
           MOVE ZEROS                  TO GAR-VAL-PON     IN VTS.
           MOVE ZEROS                  TO GAR-PCT-AJT-SUP IN VTS.
           MOVE ZEROS                  TO GAR-VAL-AJT-SUP IN VTS.
           MOVE SPACES                 TO GAR-TIV-TSD     IN VTS.
           MOVE SPACES                 TO GAR-CIV-TSD     IN VTS.
           MOVE SPACES                 TO GAR-SIV-TSD     IN VTS.
           MOVE SPACES                 TO GAR-TIV-SVS     IN VTS.
           MOVE SPACES                 TO GAR-CIV-SVS     IN VTS.
           MOVE SPACES                 TO GAR-SIV-SVS     IN VTS.
           MOVE SPACES                 TO GAR-TIP-CTR     IN VTS.
           MOVE ZEROS                  TO GAR-M2-TRR      IN VTS.
           MOVE SPACES                 TO GAR-M2-CTR      IN VTS.
           MOVE SPACES                 TO GAR-TIP-MAQ-EQP IN VTS.
           MOVE ZEROS                  TO GAR-FA-RVL      IN VTS.
           MOVE ZEROS                  TO GAR-FM-RVL      IN VTS.
           MOVE ZEROS                  TO GAR-FD-RVL      IN VTS.
           MOVE SPACES                 TO GAR-COD-UMD     IN VTS.
           MOVE ZEROS                  TO GVT-FA-ING-SIS  IN VTS.
           MOVE ZEROS                  TO GVT-FM-ING-SIS  IN VTS.
           MOVE ZEROS                  TO GVT-FD-ING-SIS  IN VTS.
           MOVE ZEROS                  TO GVT-FA-ULT-ACT  IN VTS.
           MOVE ZEROS                  TO GVT-FM-ULT-ACT  IN VTS.
           MOVE ZEROS                  TO GVT-FD-ULT-ACT  IN VTS.
           MOVE ZEROS                  TO GVT-HOR-ACT     IN VTS.
           MOVE SPACES                 TO GVT-TML-ACT     IN VTS.
           MOVE SPACES                 TO GVT-IDN-USU-ACT IN VTS.
           MOVE SPACES                 TO GVT-IDN-USU-ING IN VTS.
           MOVE SPACES                 TO GVT-MSC-TACC    IN VTS.
           MOVE SPACES                 TO GVT-MSC-STAT    IN VTS.
           MOVE GVT-GLS-FLAG    IN GVT TO GVT-GLS-FLAG    IN VTS.
           MOVE GAR-NUM-SIS     IN GVT TO GAR-NUM-SIS     IN VTS.
           MOVE GAR-COD-CRR     IN GVT TO GAR-COD-CRR     IN VTS.
           MOVE GAR-COD-TSN     IN GVT TO GAR-COD-TSN     IN VTS.
           MOVE GAR-FA-TSN      IN GVT TO GAR-FA-TSN      IN VTS.
           MOVE GAR-FM-TSN      IN GVT TO GAR-FM-TSN      IN VTS.
           MOVE GAR-FD-TSN      IN GVT TO GAR-FD-TSN      IN VTS.
           IF GAR-VAL-LIQ-TSN IN GVT IS NUMERIC
              MOVE GAR-VAL-LIQ-TSN IN GVT TO GAR-VAL-LIQ-TSN IN VTS.
           IF GAR-VAL-CML-TSN IN GVT IS NUMERIC
              MOVE GAR-VAL-CML-TSN IN GVT TO GAR-VAL-CML-TSN IN VTS.
           IF GAR-PCT-PON     IN GVT IS NUMERIC
              MOVE GAR-PCT-PON     IN GVT TO GAR-PCT-PON     IN VTS.
           IF GAR-VAL-PON     IN GVT IS NUMERIC
              MOVE GAR-VAL-PON     IN GVT TO GAR-VAL-PON     IN VTS.
           IF GAR-PCT-AJT-SUP IN GVT IS NUMERIC
              MOVE GAR-PCT-AJT-SUP IN GVT TO GAR-PCT-AJT-SUP IN VTS.
           IF GAR-VAL-AJT-SUP IN GVT IS NUMERIC
              MOVE GAR-VAL-AJT-SUP IN GVT TO GAR-VAL-AJT-SUP IN VTS.
           MOVE GAR-IIV-TSD     IN GVT TO GAR-IIV-TSD     IN VTS.
           MOVE GAR-IIV-SVS     IN GVT TO GAR-IIV-SVS     IN VTS.
           MOVE GAR-TIP-CTR     IN GVT TO GAR-TIP-CTR     IN VTS.
           MOVE GAR-M2-TRR      IN GVT TO GAR-M2-TRR      IN VTS.
           MOVE GAR-M2-CTR      IN GVT TO GAR-M2-CTR      IN VTS.
           MOVE GAR-TIP-MAQ-EQP IN GVT TO GAR-TIP-MAQ-EQP IN VTS.
           MOVE GAR-FA-RVL      IN GVT TO GAR-FA-RVL      IN VTS.
           MOVE GAR-FM-RVL      IN GVT TO GAR-FM-RVL      IN VTS.
           MOVE GAR-FD-RVL      IN GVT TO GAR-FD-RVL      IN VTS.
           MOVE GAR-COD-UMD     IN GVT TO GAR-COD-UMD     IN VTS.
           MOVE GVT-FA-ING-SIS  IN GVT TO GVT-FA-ING-SIS  IN VTS.
           MOVE GVT-FM-ING-SIS  IN GVT TO GVT-FM-ING-SIS  IN VTS.
           MOVE GVT-FD-ING-SIS  IN GVT TO GVT-FD-ING-SIS  IN VTS.
           MOVE GVT-FA-ULT-ACT  IN GVT TO GVT-FA-ULT-ACT  IN VTS.
           MOVE GVT-FM-ULT-ACT  IN GVT TO GVT-FM-ULT-ACT  IN VTS.
           MOVE GVT-FD-ULT-ACT  IN GVT TO GVT-FD-ULT-ACT  IN VTS.
           MOVE GVT-HOR-ACT     IN GVT TO GVT-HOR-ACT     IN VTS.
           MOVE GVT-TML-ACT     IN GVT TO GVT-TML-ACT     IN VTS.
           MOVE GVT-IDN-USU-ACT IN GVT TO GVT-IDN-USU-ACT IN VTS.
           MOVE GVT-IDN-USU-ING IN GVT TO GVT-IDN-USU-ING IN VTS.
           MOVE GVT-MSC-TACC    IN GVT TO GVT-MSC-TACC    IN VTS.
           MOVE GVT-MSC-STAT    IN GVT TO GVT-MSC-STAT    IN VTS.
           MOVE SPACES                 TO GVT-GLS-DISP    IN VTS.
      *MHM-INI 20-JUN-2003
      *BUS-TAB busca glosa de codigo en tabla
           IF GAR-IIV-TSD IN GVT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'TAS ' TO TAB-COD-TTAB IN TAB
               MOVE GAR-IIV-TSD IN GVT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO GAR-GLS-NMTA IN VTS
           ELSE
               MOVE SPACES TO GAR-GLS-NMTA IN VTS.
      *BUS-TAB busca glosa de codigo en tabla
           IF GAR-IIV-SVS IN GVT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'TAS ' TO TAB-COD-TTAB IN TAB
               MOVE GAR-IIV-SVS IN GVT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DESC IN TAB TO GAR-GLS-NMSU IN VTS
           ELSE
               MOVE SPACES TO GAR-GLS-NMSU IN VTS.
      *BUS-TAB busca glosa de codigo en tabla
           IF GAR-COD-UMD IN GVT > SPACES
               MOVE 'GAR' TO TAB-COD-SIST
               MOVE 'UMD ' TO TAB-COD-TTAB IN TAB
               MOVE GAR-COD-UMD IN GVT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               MOVE TAB-GLS-DCOR IN TAB TO GAR-GLS-NMUM IN VTS
           ELSE
               MOVE SPACES TO GAR-GLS-NMUM IN VTS.

           MOVE GAR-DES-1 IN GDS         TO WSS-GLS-DES1 IN WSS-VARI.
           MOVE GAR-DES-2 IN GDS         TO WSS-GLS-DES2 IN WSS-VARI.
           MOVE WSS-GLS-DESC IN WSS-VARI TO GAR-GLS-DESC IN VTS.
           MOVE SPACES                   TO GAR-IND-VERF IN VTS.
      *MHM-FIN 20-JUN-2003
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-VTS.
      *MHM-INI 20-JUN-2003
       NXT-ULTASA-GVS.
      *MHM-FIN 20-JUN-2003
           MOVE FIO-GET-NXT  TO FIO-CMND.
           PERFORM LEE-GAR-GVS.
           PERFORM GDG-PASO-KEY.
       FIN-ULTASA-GVS.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-GIS.
           PERFORM GAR-FIO-VTS.
           PERFORM GAR-FIO-GDS.
           PERFORM GAR-FIO-DDS.
           PERFORM GAR-FIO-GVS.
       FIN-CLOSE-FILE.
           EXIT.
      *
       COPY GARBFGIS.
       COPY GARBFVTS.
       COPY GARBFGDS.
       COPY GARBFDDS.
       COPY GARBFGVS.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY GNSBGEND.

      *MHM-INI 20-JUN-2003
       COPY GNSBGHOY.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBBSYS.
      *MHM-FIN 20-JUN-2003
