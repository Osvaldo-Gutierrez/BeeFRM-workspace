       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.  GARPBALG.
       AUTHOR.      M. ALBORNOZ D.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      **************************************************************
      *  OBJETIVO :                                                *
      *           ACTUALIZAR FECHA DE VALORIZACION, GENERANDO      *
      *           MOVIMIENTO DE MANTENCION EN GVT                  *
      *                                                            *
      *                                                            *
      ********************* ARCHIVOS  USADOS************************
      *         INPUT : GDG                                        *
      *                                                            *
      *         I/O   :  GVT, GMO                                  *
      *                                                            *
      **************************************************************

      * Modificado por CONSIST SEP/91 CONVERSION A GENESIS
      * Se incorpora el uso de GAR-FEC-RVL in GVT
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************

       ENVIRONMENT DIVISION.
      *=======================
       CONFIGURATION SECTION.
      *----------------------
       SPECIAL-NAMES.
             DECIMAL-POINT IS COMMA.

      * INPUT-OUTPUT SECTION.
      *----------------------
      * FILE-CONTROL.
      *-------------
      *      SELECT ARCH-RECH ASSIGN TO DA-3380-S-ARCHREC.

       DATA DIVISION.
      *=============
      * FILE SECTION.
      *-------------

       WORKING-STORAGE SECTION.
      *========================

       77    WK-CONT-GDG-LEIDOS        PIC 9(06)             VALUE 0.
       77    WK-CONT-GVT-LEIDOS        PIC 9(06)             VALUE 0.
       77    WK-CONT-MOV-GDG           PIC 9(05)             VALUE 0.
       77    WK-CONT-MOV-GVT           PIC 9(05)             VALUE 0.
       77    WK-CONT-MOV-GMO           PIC 9(05)             VALUE 0.
       77    WK-FEC-RVL                PIC 9(08)             VALUE 0.

       01    WK-FEC.
             03 SEP-SS                 PIC X(02).
             03 SEP-AA                 PIC X(02).
             03 SEP-MM                 PIC X(02).
             03 SEP-DD                 PIC X(02).

       01   WK-GLOSA-ERROR.
            03 WK-GLOSA-ERR      PIC X(30).
            03 FILLER            PIC X(09)  VALUE 'RET.CODE='.
            03 WK-RET-COD        PIC X(02).
            03 WK-MSG            PIC X(09).

       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWGHOY.
      * COPY GNSWGFEC.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGCIC.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSBRERR.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GMO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RES-REQA==.

       COPY GARBRGDG.
       COPY GARBRGVT.
       COPY GARBRGMO.
      *
       COPY RLOVR001.

      *------------------------------------------------------------*
      *                                                            *
      *                   PROGRAMA PRINCIPAL                       *
      *                                                            *
      *------------------------------------------------------------*

       PROCEDURE DIVISION.
      *==================
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           PERFORM INI.
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN.

           PERFORM B-PROCESAR.

       FIN-MAIN.
           PERFORM C-TERMINAR.
       COPY GNSBGGBK.

       INI SECTION.
       INI-INI.
           MOVE 'GARPBALG' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           MOVE ERR-TERR-LGC IN ERR-VARI TO ERR-COD-TERR IN ERR.
           MOVE 'GARPBALG'               TO ERR-COD-ATRN IN ERR.
           MOVE ERR-TTRN-BAT IN ERR-VARI TO ERR-MSC-TTRN IN ERR.
           MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR.
           MOVE 'M' TO ERR-MSC-TACC IN ERR.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

      * BUS-TAB TABLA DE FECHAS DE PROCESO
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'GAR' TO FIO-SIST.
           MOVE 'TFP' TO TAB-COD-TTAB IN TAB.
           MOVE '09'  TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DAT1 TO WK-FEC
           ELSE
              MOVE 'TABTFPNEX'  TO ERR-COD-MENS IN ERR
              MOVE TAB-COD-CTAB TO ERR-COD-CMPO IN ERR
              MOVE ADR-TAB-REQA TO ADR-REQA
              PERFORM PRO-ERR
              PERFORM PRG-ABT.

           DISPLAY 'GARPBALG : '.
           DISPLAY ' '.
           DISPLAY 'FECHA PROCESO : ' WK-FEC.

      * POSICIONA GDG.

           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR
               MOVE 'GDG    NEX'    TO ERR-COD-MENS IN ERR
               MOVE SPACES          TO ERR-COD-CMPO IN ERR
               MOVE ADR-GDG-REQA    TO ADR-REQA
               PERFORM PRO-ERR
               PERFORM PRG-ABT
           ELSE
               MOVE ERR-SVRT-REC IN ERR-VARI TO ERR-IND-SVRT IN ERR.

           ADD 1 TO WK-CONT-GDG-LEIDOS.
           PERFORM GET-FHOY.

       FIN-INI.
           EXIT.

       B-PROCESAR SECTION.
      *-----------
       INI-B-PROCESAR.

           PERFORM B1-PROCESA-GDG UNTIL NOT FIO-STAT-OKS.
           GO TO FIN-B-PROCESAR.

       B1-PROCESA-GDG.
      *--------------
           IF GAR-IND-CPL = 'S' AND (GAR-ES1 > '09' AND < '20') AND
              GAR-TIP-OPE = '955' AND GAR-STP-OPE = '091' AND
              GAR-VCB  = '0998  '
      *
              MOVE GAR-NUM-SIS IN GDG TO GAR-NUM-SIS IN GVT
              MOVE GAR-COD-CRR IN GDG TO GAR-COD-CRR IN GVT
              MOVE GAR-COD-TSN IN GDG TO GAR-COD-TSN IN GVT
              MOVE FIO-GET-KEY-UPD TO FIO-CMND
              PERFORM GAR-FIO-GVT
              IF FIO-STAT-OKS
                  ADD 1 TO WK-CONT-GVT-LEIDOS
                  MOVE GAR-FEC-RVL IN GVT TO WK-FEC-RVL
                  MOVE WK-FEC             TO GAR-FEC-RVL IN GVT
                  PERFORM B1-2-PROCESA-GVT
                  PERFORM B1-3-ACTUALIZA-GMO
              ELSE
                  MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR
                  MOVE 'GVT    NEX'    TO ERR-COD-MENS IN ERR
                  MOVE SPACES          TO ERR-COD-CMPO IN ERR
                  MOVE ADR-GVT-REQA    TO ADR-REQA
                  PERFORM PRO-ERR
                  PERFORM PRG-ABT.

           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF FIO-STAT-OKS
              ADD 1 TO WK-CONT-GDG-LEIDOS.


       B1-2-PROCESA-GVT.
      *-----------------
           PERFORM GET-FHOY.
           MOVE WK-FEC            TO GVT-FEC-ULT-ACT  IN GVT.
           MOVE HOY-HHOY          TO GVT-HOR-ACT      IN GVT.
           MOVE SPACES            TO GVT-TML-ACT      IN GVT
           MOVE 'PBALG'           TO GVT-IDN-USU-ACT  IN GVT.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
                  MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR
                  MOVE 'GVT    MODNO'  TO ERR-COD-MENS IN ERR
                  MOVE SPACES          TO ERR-COD-CMPO IN ERR
                  MOVE ADR-GVT-REQA    TO ADR-REQA
                  PERFORM PRO-ERR
                  PERFORM PRG-ABT
           ELSE
              ADD 1 TO WK-CONT-MOV-GVT.


       B1-3-ACTUALIZA-GMO.
      *-------------------
           MOVE WK-FEC                   TO GAR-FEC-MOV IN GMO.
           MOVE HOY-HHOY                 TO GAR-HOR-MOV IN GMO.
           MOVE 'PBALG'                  TO GAR-TML-MOV IN GMO.
           MOVE GAR-NUM-SIS IN GDG       TO GAR-NUM-SIS IN GMO.
           MOVE GAR-COD-CRR IN GDG       TO GAR-COD-CRR IN GMO.
           MOVE  1                       TO GAR-CRR-MOV IN GMO.
           MOVE  2                       TO GAR-COD-MOV IN GMO.
      *CONSIST
      *     MOVE 'GVT004'                 TO GAR-COD-CPO IN GMO.
      *     MOVE 0                        TO GAR-VAL-CPO-ANT IN GMO
      *                                      GAR-VAL-CPO-NVO IN GMO.
           MOVE 'GVT005'                 TO GAR-COD-CPO IN GMO.
           MOVE GAR-VAL-AJT-SUP IN GVT   TO GAR-VAL-CPO-ANT IN GMO
                                            GAR-VAL-CPO-NVO IN GMO.
      *FIN CONSIST
           MOVE 'N'                      TO GAR-IND-VAL IN GMO.
           MOVE 'N'                      TO GAR-IND-CLV IN GMO.
           MOVE SPACES                   TO GAR-COD-CLV-ANT IN GMO
                                            GAR-COD-CLV-NVO IN GMO.
           MOVE WK-FEC-RVL               TO GAR-GLS-CPO-ANT IN GMO.
           MOVE GAR-FEC-RVL IN GVT       TO GAR-GLS-CPO-NVO IN GMO.
           MOVE GAR-COD-OFI IN GDG       TO GAR-COD-OFI-MOV IN GMO.
           MOVE 'PBALG'                  TO GMO-IDN-USU-ING IN GMO.
           MOVE WK-FEC                   TO GMO-FEC-ING-SIS IN GMO.

           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           IF NOT FIO-STAT-OKS
              SUBTRACT 1 FROM WK-CONT-MOV-GVT
              DISPLAY 'GARPBALG. NO PUDO GRABAR REG. GMO PARA GTIA. '
                      GAR-CLV-GMO-1 IN GMO ' RET-COD=' FIO-STAT
              MOVE 'NO PUDO GRABAR REG. GMO  '   TO WK-GLOSA-ERR
              DISPLAY 'GARPBALG. PGM CANCELADO '
              MOVE FIO-STAT                      TO WK-RET-COD
              MOVE ADR-GMO-REQA    TO ADR-REQA
              PERFORM PRO-ERR
              PERFORM PRG-ABT.
           ADD 1 TO WK-CONT-MOV-GMO.

       FIN-B-PROCESAR.
           EXIT.

       C-TERMINAR SECTION.
      *-----------
       INI-C-TERMINAR.
           DISPLAY '==========================================='.
           DISPLAY '= REGS. LEIDOS GDG .................'
                      WK-CONT-GDG-LEIDOS ' ='.
           DISPLAY '= REGS. LEIDOS GVT .................'
                      WK-CONT-GVT-LEIDOS ' ='.
           DISPLAY '= MVTOS. GDG GRABADOS...............'
                      WK-CONT-MOV-GDG   ' ='.
           DISPLAY '= MVTOS. GVT GRABADOS...............'
                      WK-CONT-MOV-GVT   ' ='.
           DISPLAY '= MVTOS. GMO GRABADOS...............'
                      WK-CONT-MOV-GMO   ' ='.
           DISPLAY '=                                         ='.
           DISPLAY '==========================================='.
       FIN-C-TERMINAR.
           EXIT.

       COPY GARBFGDG.
       COPY GARBFGVT.
       COPY GARBFGMO.

       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
      *COPY GNSBFERR.
       COPY GNSBGERR REPLACING ==FIO-STAT IN FIO-VARI== BY ==FIO-STAT==.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBBSYS.
       COPY GNSBGHOY.

