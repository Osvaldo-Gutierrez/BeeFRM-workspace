       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.  GARPBLMO.
       AUTHOR.     M. ALBORNOZ D.
      *            Modificado por F. LIZANA B.  JUL 1991
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
      *************************************************************
      *                                                           *
      *      CON ESTE PROGRAMA SE DELETEAN LOS  REGISTROS         *
      *      CON FECHA ANTERIOR A LA DEL MES Y ANO ACTUAL         *
      *      LA TABLA ACTUALIZADA ES LA GMO.                      *
      *                                                           *
      *                                    OCTUBRE - 1987         *
      *************************************************************
      ******************************************************************
      *      MANTENCIONES.
      *      ==============
      *
      *      PROGRAMADOR : FORMAT 2000 INC.
      *      FECHA       : 20.02.1998
      *      CAMBIO      : SE APLICA TECNICA WINDOWING PARA CONVERSION
      *                      2000 COMPLIANT.
      ******************************************************************
       ENVIRONMENT DIVISION.
      *=====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
             C01 IS CANAL-1
             DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
      *---------------------
       FILE-CONTROL.
             SELECT RPTLMO       ASSIGN TO UT-S-RPTLMO.

       DATA DIVISION.
      *==============
       FILE SECTION.
      *-------------
       FD    RPTLMO                   LABEL RECORDS OMITTED.
       01    REG-RPTLMO               PIC X(132).

       WORKING-STORAGE SECTION.
      *---------------------
       01  WK-FECHA-SSAAMMDD.
           03 WK-SSAA.
      ********05 WK-SS               PIC 9(02)  VALUE 19.
              05 WK-SS               PIC 9(02).
              05 WK-AA               PIC 9(02).
           03 WK-MM                  PIC 9(02).
           03 WK-DD                  PIC 9(02).

       01  WSS-NUM-PAGE              PIC 9(01) VALUE 0.

       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWGHOY.
      * COPY GNSWGFEC.
       COPY GNSWGSYS.
       COPY GNSBRMSG.
       COPY GNSBRTAB.
       COPY GNSBRERR.

       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GMO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.

       COPY GARBRGMO.

      *------------------------------------------------------------*
      * LINEAS DE SALIDA                                           *
      *------------------------------------------------------------*
       01   LINEA-TIT1.
            03 FILLER  PIC X(46) VALUE 'BANCO DE CREDITO E INVERSIONES'.
            03 FILLER  PIC X(35) VALUE 'ESTADISTICA DE ACTUALIZACION'.
            03 FILLER  PIC X(33) VALUE 'PGM: GARPBLMO'.
            03 FILLER  PIC X(08) VALUE 'FECHA : '.
            03 LT1-FECHA.
               05 WK-DIA                 PIC 9(02).
               05 FILLER  VALUE SPACE    PIC X(01).
               05 WK-MES                 PIC 9(02).
               05 FILLER  VALUE SPACE    PIC X(01).
               05 WK-ANO                 PIC 9(04).

       01   LINEA-TIT2.
            03 FILLER  PIC X(46) VALUE '   SISTEMA  DE  GARANTIAS'.
            03 FILLER  PIC X(35) VALUE '============================'.
            03 FILLER  PIC X(33) VALUE SPACES.
            03 FILLER  PIC X(10) VALUE 'PAGINA:   '.
            03 LT2-PAG PIC Z9.

       01   LINEA-TIT22.
            03 FILLER  PIC X(46) VALUE SPACES.
            03 FILLER  PIC X(35) VALUE 'MOVIMIENTOS BORRADOS HASTA EL '.
            03 MESBO   PIC 9(02) .
            03 FILLER  PIC X(01) VALUE '/'.
            03 ANOBO   PIC 9(04) .
            03 FILLER  PIC X(15) VALUE ' INCLUSIVE.'.

       01   LINEA-TIT3.
            03 FILLER  PIC X(30) VALUE SPACES.
            03 FILLER  PIC X(37) VALUE 'REGISTROS LEIDOS   : '.
            03 WK-CONT-GMO-LEIDOS        PIC 9(05)  VALUE 0.

       01   LINEA-TIT4.
            03 FILLER  PIC X(30) VALUE SPACES.
            03 FILLER  PIC X(37) VALUE 'REGISTROS BORRADOS : '.
            03 WK-CONT-GMO-ELIM          PIC 9(05)  VALUE 0.

       01   WK-GLOSA-ERROR.
            03 WK-GLOSA-ERR      PIC X(30).
            03 FILLER            PIC X(09)  VALUE 'RET.CODE='.
            03 WK-RET-COD        PIC X(02).
            03 WK-MSG            PIC X(09).

       01    WK-FEC.
              05 SEP-FEC.
                 06 A-FEC        PIC 9(4).
                 06 M-FEC        PIC 9(2).
                 06 D-FEC        PIC 9(2).

       01  FECHA-COMPA.
           02 A-COMPA   PIC X(4).
           02 M-COMPA   PIC X(2).
           02 D-COMPA   PIC X(2).
      *
           COPY RLOVR001.

      *------------------------------------------------------------*
      *                                                            *
      *                   PROGRAMA PRINCIPAL                       *
      *                                                            *
      *------------------------------------------------------------*

       PROCEDURE DIVISION.
      *===================
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           PERFORM INI.
           IF NOT FIO-STAT-OKS
               GO TO FIN-MAIN.

           PERFORM ELIMINA-GMO UNTIL NOT FIO-STAT-OKS.

       FIN-MAIN.
           PERFORM FINAL-CIERRA.
       COPY GNSBGGBK.

       INI SECTION.
       INI-INI.
           MOVE 'GARPBLMO' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           MOVE ERR-TERR-LGC IN ERR-VARI TO ERR-COD-TERR IN ERR.
           MOVE 'GARPBLMO'               TO ERR-COD-ATRN IN ERR.
           MOVE ERR-TTRN-BAT IN ERR-VARI TO ERR-MSC-TTRN IN ERR.
           MOVE ERR-SVRT-FAT IN ERR-VARI TO ERR-IND-SVRT IN ERR.
           MOVE 'M' TO ERR-MSC-TACC IN ERR.

           PERFORM GET-FHOY.
           MOVE HOY-SHOY TO WK-SS.
           MOVE HOY-AHOY TO WK-AA.
           MOVE HOY-MHOY TO WK-MM, WK-MES.
           MOVE HOY-DHOY TO WK-DD, WK-DIA.
           MOVE WK-SSAA  TO WK-ANO.

           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           IF NOT FIO-STAT-OKS
               PERFORM PRO-ERR
               PERFORM PRG-ABT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.

      * BUS-TAB TABLA DE FECHAS DE PROCESO
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'GAR' TO FIO-SIST.
           MOVE 'TFP' TO TAB-COD-TTAB IN TAB.
      * ultimo dia del mes
           MOVE '09'  TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DAT1 TO WK-FEC
               MOVE '01'         TO D-FEC
           ELSE
              MOVE 'TABTFPNEX' TO ERR-COD-MENS IN ERR
              MOVE '09'        TO ERR-COD-CMPO IN ERR
              MOVE TAB-COD-CTAB IN TAB TO ERR-GLS-VKEY IN ERR
              PERFORM PRO-ERR
              PERFORM PRG-ABT.

           DISPLAY 'GARPBLMO : '.
           DISPLAY ' '.
           DISPLAY 'FECHA PROCESO : ' WK-FEC.

           OPEN OUTPUT RPTLMO.

      * POSICIONA GMO.

           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           IF NOT FIO-STAT-OKS
               MOVE ERR-SVRT-ADV IN ERR-VARI TO ERR-IND-SVRT IN ERR
               MOVE 'GMO    NEX'    TO ERR-COD-MENS IN ERR
               MOVE SPACES          TO ERR-COD-CMPO IN ERR
               MOVE ADR-GMO-REQA    TO ADR-REQA
               PERFORM PRO-ERR
               MOVE FIO-STAT-NEX TO FIO-STAT
           ELSE
               MOVE ERR-SVRT-REC IN ERR-VARI TO ERR-IND-SVRT IN ERR.

      * Fecha tope para el listado
           MOVE A-FEC TO ANOBO.
           SUBTRACT 1 FROM M-FEC GIVING MESBO.

       FIN-INI.
           EXIT.


       ELIMINA-GMO SECTION.
       INI-ELIMINA-GMO.
      *------------------
           ADD 1 TO WK-CONT-GMO-LEIDOS.
           IF GAR-FEC-MOV < SEP-FEC
               MOVE FIO-GET-KEY-UPD TO FIO-CMND
               PERFORM GAR-FIO-GMO
               MOVE FIO-DEL TO FIO-CMND
               PERFORM GAR-FIO-GMO
               IF NOT FIO-STAT-OKS
                   MOVE 'GMO    DEL'    TO ERR-COD-MENS IN ERR
                   MOVE 'GAR-CLV-GMO-1' TO ERR-COD-CMPO IN ERR
                   MOVE ADR-GMO-REQA TO ADR-REQA
                   PERFORM PRO-ERR
                   MOVE FIO-GET-NXT TO FIO-CMND
                   PERFORM GAR-FIO-GMO
               ELSE
                   ADD 1 TO WK-CONT-GMO-ELIM
                   MOVE FIO-GET-NXT TO FIO-CMND
                   PERFORM GAR-FIO-GMO
           ELSE
               MOVE FIO-STAT-NEX TO FIO-STAT.
       FIN-ELIMINA-GMO.
           EXIT.

       FINAL-CIERRA SECTION.
       INI-FINAL-CIERRA.
           PERFORM ESTADISTICAS.
           CLOSE RPTLMO.
       FIN-FINAL-CIERRA.
           EXIT.

       ESTADISTICAS SECTION.
       INI-ESTADISTICAS.
           ADD 1 TO WSS-NUM-PAGE.
           MOVE WSS-NUM-PAGE TO LT2-PAG.
           WRITE REG-RPTLMO FROM LINEA-TIT1 AFTER CANAL-1.
           WRITE REG-RPTLMO FROM LINEA-TIT2 AFTER 2.
           WRITE REG-RPTLMO FROM LINEA-TIT22 AFTER 4.
           WRITE REG-RPTLMO FROM LINEA-TIT3 AFTER 4.
           WRITE REG-RPTLMO FROM LINEA-TIT4 AFTER 3.
           DISPLAY '==========================================='.
           DISPLAY '= REGS. LEIDOS GMO .................'
                      WK-CONT-GMO-LEIDOS ' ='.
           DISPLAY '= REG.  ELIMINADOS .................'
                      WK-CONT-GMO-ELIM  ' ='.
           DISPLAY '=                                         ='.
           DISPLAY '==========================================='.
       FIN-ESTADISTICAS.
           EXIT.

       COPY GARBFGMO.

       COPY GNSBBMSG.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
      *COPY GNSBFERR.
       COPY GNSBGERR.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBBSYS.
       COPY GNSBGHOY.

