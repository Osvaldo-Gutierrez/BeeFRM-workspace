       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGDBC.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    27-MAY-2003.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      * Archivo Gargis, contiene: Garantia, Cic-Cliente.
        COPY GARBRGIS.
      *
      * Archivo Garcli, contiene:Garantia,Cic-Cliente,Rut-Cliente,Nom-Cl
        COPY GARBRCLS.
      *
      * Registros de Archivos secuenciales aplanados de las Bases
      * (SGCBRDBS = SGCBRDBC)
       COPY SGCBRDBS.
      *
      * Registros de las Bases Originales.
       COPY SGCBRDBC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
      *
       01  WS-VARIABLES.
           03 AUX-GIS-GD-CLI       PIC X(12) VALUE SPACES.
           03 WS-CON-GIS           PIC 9(09) VALUE ZEROS.
           03 WS-CON-GRA           PIC 9(09) VALUE ZEROS.
           03 WS-CON-NGR           PIC 9(09) VALUE ZEROS.
           03 WS-EOF-GIS           PIC 9(01) VALUE ZEROS.


       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGDBC" TO FIO-PROG.
           MOVE SPACES TO AUX-GIS-GD-CLI.
           MOVE ZEROS TO WS-EOF-GIS.
           MOVE ZEROS TO WS-CON-GIS.
           MOVE ZEROS TO WS-CON-GRA.
           MOVE ZEROS TO WS-CON-NGR.
           PERFORM OPEN-FILE.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-GAR-GIS.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-SGC-DBS.
           PERFORM PROCESA-GIS UNTIL WS-EOF-GIS = 1.
           DISPLAY 'REGISTROS LEE GIS   ' WS-CON-GIS.
           DISPLAY 'REGISTROS GRA CLS   ' WS-CON-GRA.
           DISPLAY 'REGISTROS NO GRABA  ' WS-CON-NGR.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GIS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-CLS.
       FIN-OPEN-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       LEE-GAR-GIS SECTION.
       LUP-PRO-GIS.
           PERFORM GAR-FIO-GIS.
           IF NOT FIO-STAT-EOF THEN
              ADD  1 TO WS-CON-GIS
           ELSE
              MOVE 1 TO WS-EOF-GIS.
       FIN-LEE-GAR-GIS.
           EXIT.

       LEE-SGC-DBS SECTION.
       LUP-PRO-DBS.
           PERFORM SGC-FIO-DBS.
           MOVE DBS TO DBC.
       FIN-LEE-SGC-DBS.
           EXIT.

       PROCESA-GIS SECTION.
       INI-PROCESA-GIS.
           MOVE GIS-GDD-CICLI TO AUX-GIS-GD-CLI.
           IF GIS-GDD-CICLI = DBC-CIC-ICLI IN DBS THEN
              PERFORM GRABA-CLIENTE UNTIL
                      GIS-GDD-CICLI NOT = AUX-GIS-GD-CLI
                      OR FIO-STAT-EOF
           ELSE
              IF GIS-GDD-CICLI < DBC-CIC-ICLI IN DBS THEN
                 ADD 1 TO WS-CON-NGR
                 PERFORM REG-NOEXISTE
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-GAR-GIS
              ELSE
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-SGC-DBS.
       FIN-PROCESA-GIS.
           EXIT.

       GRABA-CLIENTE SECTION.
       INI-GRABA-CLTE.
           ADD 1 TO WS-CON-GRA.
           MOVE GIS-GDG-NUM-SIS      TO CLS-GDG-NUM-SIS.
           MOVE GIS-GDG-COD-CRR      TO CLS-GDG-COD-CRR.
           MOVE GIS-GDD-CAI-ICLI     TO CLS-GDD-CAI-ICLI.
           MOVE GIS-GDD-ICC-ICLI     TO CLS-GDD-ICC-ICLI.
           MOVE DBC-NUM-ICLI IN DBS  TO CLS-DBC-NUM-ICLI.
           MOVE DBC-IND-ICLI IN DBS  TO CLS-DBC-IND-ICLI.
           MOVE DBC-GLS-ICLI IN DBS  TO CLS-DBC-GLS-ICLI.
           MOVE DBC-VRF-ICLI IN DBS  TO CLS-DBC-VRF-ICLI.
           MOVE DBC-GLS-APAT IN DBS  TO CLS-DBC-GLS-APAT.
           MOVE DBC-GLS-AMAT IN DBS  TO CLS-DBC-GLS-AMAT.
           MOVE DBC-GLS-NOMB IN DBS  TO CLS-DBC-GLS-NOMB.
           MOVE FIO-PUT              TO FIO-CMND.
           PERFORM GAR-FIO-CLS.
           MOVE FIO-GET-NXT          TO FIO-CMND.
           PERFORM LEE-GAR-GIS.
       FIN-GRABA-CLTE.
           EXIT.

       REG-NOEXISTE SECTION.
       INI-REG-NOEX.
           DISPLAY 'GARANTIA SIN CLIENTE  ' GIS-GDG-COD-GAR.
       FIN-REG-NOEX.
           EXIT.
       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-GIS.
           PERFORM SGC-FIO-DBS.
           PERFORM GAR-FIO-CLS.
       FIN-CLOSE-FILE.
           EXIT.
      *
       COPY GARBFGIS.
       COPY SGCBFDBS.
       COPY GARBFCLS.
       COPY GNSBGEND.
      *
