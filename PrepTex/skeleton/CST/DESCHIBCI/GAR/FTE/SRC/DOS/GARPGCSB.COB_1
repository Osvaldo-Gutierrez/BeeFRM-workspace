       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGCSB.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    02-JUL-2003.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      * Archivos de Entrada.
       COPY GARBRTCL.
       COPY SGCBRDBS.
      * Archivo de Salida.
       COPY GARBRCSB.
      * Registros de las Bases Originales.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
      *
       01  WS-VARIABLES.
           03 AUX-TCL-GD-CLI       PIC X(12) VALUE SPACES.
           03 WS-CON-TCL           PIC 9(09) VALUE ZEROS.
           03 WS-CON-GRA           PIC 9(09) VALUE ZEROS.
           03 WS-CON-NGR           PIC 9(09) VALUE ZEROS.


       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGCSB" TO FIO-PROG.
           MOVE SPACES TO AUX-TCL-GD-CLI.
           MOVE ZEROS  TO WS-CON-TCL.
           MOVE ZEROS  TO WS-CON-GRA.
           MOVE ZEROS  TO WS-CON-NGR.
           PERFORM OPEN-FILE.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-GAR-TCL.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-SGC-DBS.
           PERFORM PROCESA-TCL UNTIL FIO-STAT-EOF.
           DISPLAY 'LEE REGISTRO  TCL   ' WS-CON-TCL.
           DISPLAY 'GRABA REGIST. CSB   ' WS-CON-GRA.
           DISPLAY 'NO GRABA REG.       ' WS-CON-NGR.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-TCL.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-CSB.
       FIN-OPEN-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       LEE-GAR-TCL SECTION.
       INI-LEE-TCL.
           PERFORM GAR-FIO-TCL.
           IF NOT FIO-STAT-EOF THEN
              ADD 1 TO WS-CON-TCL.
       FIN-LEE-TCL.
           EXIT.

       LEE-SGC-DBS SECTION.
       INI-LEE-DBS.
           PERFORM SGC-FIO-DBS.
       FIN-LEE-DBS.
           EXIT.

       PROCESA-TCL SECTION.
       INI-PROCESA-TCL.
           MOVE TCL-GDD-CICLI TO AUX-TCL-GD-CLI.
           IF TCL-GDD-CICLI = DBC-CIC-ICLI IN DBS THEN
              PERFORM GRABA-CLIENTE UNTIL
                      TCL-GDD-CICLI NOT = AUX-TCL-GD-CLI
                      OR FIO-STAT-EOF
           ELSE
              IF TCL-GDD-CICLI < DBC-CIC-ICLI IN DBS THEN
                 ADD 1 TO WS-CON-NGR
                 DISPLAY 'GARANTIA SIN CLIENTE  ' TCL-GDG-COD-GAR
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-GAR-TCL
              ELSE
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-SGC-DBS.
       FIN-PROCESA-TCL.
           EXIT.

       GRABA-CLIENTE SECTION.
       INI-GRABA-CLTE.
           MOVE SPACES      TO CSB-GDG-NUM-SIS.
           MOVE SPACES      TO CSB-GDG-COD-CRR.
           MOVE SPACES      TO CSB-GDD-CAI-ICLI.
           MOVE SPACES      TO CSB-GDD-ICC-ICLI.
           MOVE SPACES      TO CSB-DBC-NUM-ICLI.
           MOVE SPACES      TO CSB-DBC-IND-ICLI.
           MOVE SPACES      TO CSB-DBC-GLS-ICLI.
           MOVE SPACES      TO CSB-DBC-VRF-ICLI.
           MOVE SPACES      TO CSB-DBC-GLS-APAT.
           MOVE SPACES      TO CSB-DBC-GLS-AMAT.
           MOVE SPACES      TO CSB-DBC-GLS-NOMB.
      *
           ADD 1 TO WS-CON-GRA.
           MOVE TCL-GDG-NUM-SIS      TO CSB-GDG-NUM-SIS.
           MOVE TCL-GDG-COD-CRR      TO CSB-GDG-COD-CRR.
           MOVE TCL-GDD-CAI-ICLI     TO CSB-GDD-CAI-ICLI.
           MOVE TCL-GDD-ICC-ICLI     TO CSB-GDD-ICC-ICLI.
           MOVE DBC-NUM-ICLI IN DBS  TO CSB-DBC-NUM-ICLI.
           MOVE DBC-IND-ICLI IN DBS  TO CSB-DBC-IND-ICLI.
           MOVE DBC-GLS-ICLI IN DBS  TO CSB-DBC-GLS-ICLI.
           MOVE DBC-VRF-ICLI IN DBS  TO CSB-DBC-VRF-ICLI.
           MOVE DBC-GLS-APAT IN DBS  TO CSB-DBC-GLS-APAT.
           MOVE DBC-GLS-AMAT IN DBS  TO CSB-DBC-GLS-AMAT.
           MOVE DBC-GLS-NOMB IN DBS  TO CSB-DBC-GLS-NOMB.
           MOVE FIO-PUT              TO FIO-CMND.
           PERFORM GAR-FIO-CSB.
           MOVE FIO-GET-NXT          TO FIO-CMND.
           PERFORM LEE-GAR-TCL.
       FIN-GRABA-CLTE.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-TCL.
           PERFORM SGC-FIO-DBS.
           PERFORM GAR-FIO-CSB.
       FIN-CLOSE-FILE.
           EXIT.
      *
       COPY GARBFTCL.
       COPY SGCBFDBS.
       COPY GARBFCSB.
       COPY GNSBGEND.
      *
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBGIDD.
       COPY GNSBGDTC.
