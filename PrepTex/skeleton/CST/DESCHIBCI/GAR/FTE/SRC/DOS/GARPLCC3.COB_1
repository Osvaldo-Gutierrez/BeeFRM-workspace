       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPLCC3.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    24-May-94 11:53 AM.

      * Programa Listador de Reporte CC3 con Sort.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MODIFICACION: CONVERTIR REPORT-WRITER A LISTADO, MIGRAR A     *
      *                A COBOL FOR MVS Y CONVERSION 2000               *
      *  FECHA: 24/02/1998                                             *
      ******************************************************************


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTCC3         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTCC3         ASSIGN TO        UT-S-RPTCC3.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      ***** CRW-COMIENZO
      *    SELECT RPTCC3         ASSIGN TO        UT-S-RPTCC3.
           SELECT RPTCC3         ASSIGN TO        RPTCC3.
      ***** CRW-FINAL

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY GARRRCC3.
      ***** CRW-COMIENZO
      *FD  RPTCC3 LABEL RECORDS OMITTED
      *    REPORT IS RPT-CC3
      *    .
       FD  RPTCC3
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD STANDARD.

       01  RPTCC3-REG           PIC X(133).
      ***** CRW-FINAL

0
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GARBRGDG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GARRWCC3.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
       COPY TABBROFI.

       COPY GARBRGVT.
       COPY GARBRTAG.

       01  WSS-VARI.
      * Fecha de Proceso que es leida de tabla TFP
           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                     PIC 9(02).
              05 WSS-NUM-APRO                     PIC 9(02).
              05 WSS-NUM-MPRO                     PIC 9(02).
              05 WSS-NUM-DPRO                     PIC 9(02).
           03 WSS-COD-CTA.
              05 WSS-CLA-OPE-SUP                  PIC X(03).
              05 WSS-VCB                          PIC X(06).
           03 WSS-GLS-MSGE.
              05 WSS-GLS-MSG1        VALUE SPACES PIC X(20).
              05 WSS-GLS-MSG2        VALUE SPACES PIC X(22).
              05 WSS-GLS-MSG3        VALUE SPACES PIC X(08).
              05 WSS-GLS-MSG4        VALUE SPACES PIC X(08).
              05 WSS-GLS-MSG5        VALUE SPACES PIC X(08).
              05 WSS-GLS-MSG6        VALUE SPACES PIC X(13).
              05 WSS-GLS-MSG7        VALUE SPACES PIC X(12).
           03 WSS-VAL-PESO                        PIC S9(11).
           03 VEC-MES OCCURS 12.
              05 WSS-VEC-DMES                     PIC X(10).
           03 WSS-TIP-STP.
              05 WSS-TIP-OPE                      PIC X(03).
              05 WSS-STP-OPE                      PIC X(03).
      *>>>>
      *<<<< WSS_DTC
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *Entidades de GAR
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAG-REQA==.
      *>>>>

      ***** CRW-COMIENZO
      *REPORT SECTION.
      *--------------
      *COPY GARRFCC3.
      *    .
      *COPY GARRTCC3.
       01  RPT-VARIABLES.
           02 RPT-VARIABLES-VARIAS.
               03 RPT-IND-INICIO-LISTADO      PIC X(02) VALUE 'SI'.
               03 RPT-VARI-LINEA              PIC 9(03) VALUE 60.
               03 RPT-VARI-PAGE-COUNTER       PIC 9(03).
           02 RPT-VARIABLES-CONTROL.
               03 RPT-CTRL-VCB                PIC X(06) VALUE SPACES.
               03 RPT-CTRL-CIC-CCSB           PIC X(10) VALUE SPACES.
               03 RPT-CTRL-COD-OFI            PIC X(03) VALUE SPACES.
           02 RPT-VARIABLES-CONTROL.
               03 RPT-SUMA-VAL-COME           PIC 9(12)V9(04).
               03 RPT-SUMA-VAL-AJUS           PIC 9(12)V9(04).
               03 RPT-SUMA-VAL-UCON           PIC 9(12)V9(04).
               03 RPT-SUMA-CTA-COME           PIC 9(12)V9(04).
               03 RPT-SUMA-CTA-AJUS           PIC 9(12)V9(04).
               03 RPT-SUMA-CTA-UCON           PIC 9(12)V9(04).

       01  PH-CC3.

           03  PH-CC3-01.
               05  RPT-HEAD-DUSU              PIC X(30).
               05  FILLER                     PIC X(013) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'GARANTIAS POR CUENTA CONTABLE MONEDA EX'.
               05  FILLER                     PIC X(8)
                   VALUE 'TRANJERA'.
               05  FILLER                     PIC X(032) VALUE SPACES.
               05  RPT-HEAD-FDMA              PIC XXBXXBXXXX.

           03  PH-CC3-02.
               05  RPT-HEAD-DSIS              PIC X(30).
               05  FILLER                     PIC X(092) VALUE SPACES.
               05  FILLER                     PIC X(6)
                   VALUE 'PAGINA'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-PAGE-COUNTER      PIC ZZZ.

           03  PH-CC3-03.
               05  FILLER                     PIC X(9)
                   VALUE 'OFICINA :'.
               05  FILLER                     PIC X(005) VALUE SPACES.
               05  RPT-HEAD-GLS-OFI           PIC X(28).
               05  FILLER                     PIC X(008) VALUE SPACES.
               05  FILLER                     PIC X(5)
                   VALUE 'MES :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-DMES          PIC X(10).
               05  FILLER                     PIC X(008) VALUE SPACES.
               05  FILLER                     PIC X(10)
                   VALUE 'CTA DEBE :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-CIC-CCSB          PIC X(10).
               05  FILLER                     PIC X(008) VALUE SPACES.
               05  FILLER                     PIC X(8)
                   VALUE 'MONEDA :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-VCB               PIC X(6).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-VCB           PIC X(12).

           03  PH-CC3-04.
               05  FILLER                     PIC X(132)
                   VALUE ALL '='.

           03  PH-CC3-05.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'CLAVE        DESCRIPCION               '.
               05  FILLER                     PIC X(39)
                   VALUE '          FECHA                    VALO'.
               05  FILLER                     PIC X(39)
                   VALUE 'R      VALOR AJUSTADO   FECHA          '.
               05  FILLER                     PIC X(14)
                   VALUE '  VALOR ULTIMA'.

           03  PH-CC3-06.
               05  FILLER                     PIC X(39)
                   VALUE 'GARANTIA      GARANTIA                 '.
               05  FILLER                     PIC X(39)
                   VALUE '           TASACION             COMERCI'.
               05  FILLER                     PIC X(39)
                   VALUE 'AL               SUPER   ULT.CON.      '.
               05  FILLER                     PIC X(15)
                   VALUE 'CONTABILIZACION'.

           03  PH-CC3-07.
               05  FILLER                     PIC X(132)
                   VALUE ALL '='.

       01  DL-CC3.

           03  DL-CC3-08.
               05  RPT-DETA-NUM-SIS           PIC X(4).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-COD-CRR           PIC X(8).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-GLS-DESC          PIC X(35).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-FD-TSN            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FM-TSN            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FA-TSN            PIC 9(4).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-VAL-CML-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-VAL-AJT-SUP       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-FD-RVL            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FM-RVL            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FA-RVL            PIC 9(4).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-VAL-UCON          PIC ZZ.ZZZ.ZZZ.ZZ9,9999.

       01  CF-CC3-GDG-VCB.

           03  CF-CC3-09.
               05  FILLER                     PIC X(060) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE '--------------------  -----------------'.
               05  FILLER                     PIC X(33)
                   VALUE '--            -------------------'.

           03  CF-CC3-10.
               05  FILLER                     PIC X(6)
                   VALUE 'MONEDA'.
               05  FILLER                     PIC X(054) VALUE SPACES.
               05  RPT-FOOT-VAL-COME          PIC ZZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-VAL-AJUS          PIC ZZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(011) VALUE SPACES.
               05  RPT-FOOT-VAL-UCON          PIC ZZZ.ZZZ.ZZZ.ZZ9,9999.

       01  CF-CC3-SRT-CIC-CCSB.

           03  CF-CC3-11.
               05  FILLER                     PIC X(060) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE '-------------------- ------------------'.
               05  FILLER                     PIC X(33)
                   VALUE '--           --------------------'.

           03  CF-CC3-12.
               05  FILLER                     PIC X(12)
                   VALUE 'TOTAL CUENTA'.
               05  FILLER                     PIC X(048) VALUE SPACES.
               05  RPT-FOOT-CTA-COME          PIC ZZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-CTA-AJUS          PIC ZZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(011) VALUE SPACES.
               05  RPT-FOOT-CTA-UCON          PIC ZZZ.ZZZ.ZZZ.ZZ9,9999.

      *01  CF-CC3-GDG-COD-OFI.
      ***** CRW-FINAL

       PROCEDURE DIVISION.
      *==================
      ***** CRW-COMIENZO
      *DECLARATIVES.
      *DEC-CF-CC3-GDG-COD-OFI SECTION.
      *    USE BEFORE REPORTING CF-CC3-GDG-COD-OFI.
      *INI-CF-CC3-GDG-COD-OFI.
      *<<<< CF_CC3_GDG_COD_OFI
      *>>>>
      *FIN-CF-CC3-GDG-COD-OFI.
      *    EXIT.
      *DEC-CF-CC3-SRT-CIC-CCSB SECTION.
      *    USE BEFORE REPORTING CF-CC3-SRT-CIC-CCSB.
      *INI-CF-CC3-SRT-CIC-CCSB.
      *FIN-CF-CC3-SRT-CIC-CCSB.
      *    EXIT.
      *DEC-CF-CC3-GDG-VCB SECTION.
      *    USE BEFORE REPORTING CF-CC3-GDG-VCB.
      *INI-CF-CC3-GDG-VCB.
      *<<<< CF_CC3_GDG_VCB
      *>>>>
      *FIN-CF-CC3-GDG-VCB.
      *    EXIT.
      *DEC-PH-CC3 SECTION.
      *    USE BEFORE REPORTING PH-CC3.

       DEC-PH-CC3 SECTION.
      ***** CRW-FINAL
       INI-PH-CC3.
      *<<<< PH_CC3 AND ( PGM_DOS OR PGM_MVS )
      *BUS-TAB Busca Glosa de Valor de Cambio
           IF GDG-VCB IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE SPACES TO TAB-COD-CTAB IN TAB
               MOVE GDG-VCB IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS = SPACES
                  MOVE TAB-GLS-DCOR IN TAB TO
                       WSS-GLS-VCB IN WSS-CC3
               ELSE
                  MOVE SPACES TO WSS-GLS-VCB IN WSS-CC3
           ELSE
               MOVE SPACES TO WSS-GLS-VCB IN WSS-CC3.

      *BUS-OFI Busca Codigo de Tabla
           IF GDG-COD-OFI IN SRT > SPACES
                MOVE 'TAB' TO TAB-COD-SIST
                MOVE 'OFI' TO TAB-COD-TTAB IN TAB
                MOVE SPACES TO OFI-COD-OFIC IN OFI
                MOVE GDG-COD-OFI IN SRT TO OFI-COD-OFIC IN OFI
                IF OFI-COD-OFIC > SPACES
                    PERFORM BUS-OFI
                    IF OFI-GLS-DESC > SPACES
                        MOVE OFI-GLS-DESC IN OFI TO
                             WSS-GLS-OFI IN WSS-CC3
                    ELSE
                        MOVE SPACES TO WSS-GLS-OFI IN WSS-CC3.
      *>>>>
       FIN-PH-CC3.
           EXIT.
      ***** CRW-COMIENZO
      *END DECLARATIVES.
      ***** CRW-FINAL

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'GARPLCC3' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM TAB-FIO-OFI.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'TAB'   TO FIO-SIST.
           PERFORM TAB-FIO-OFI.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'TAB'   TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GVT.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GVT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-TAG.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-TAG.

      *Lee Tabla de Fechas de Proceso
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'GAR' TO FIO-SIST.
           MOVE 'TFP' TO TAB-COD-TTAB IN TAB.
           MOVE '01'  TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DAT1 TO WSS-FEC-FPRO IN WSS-VARI
               DISPLAY 'FECHA DE PROCESO : ' TAB-GLS-DAT1
           ELSE
              DISPLAY 'ERROR EN LECTURA FECHA DE PROCESO COD 01'
               PERFORM CLO-FIL
               PERFORM GNS-PRO-ABT.

           MOVE 'ENERO'      TO WSS-VEC-DMES IN VEC-MES(1).
           MOVE 'FEBRERO'    TO WSS-VEC-DMES IN VEC-MES(2).
           MOVE 'MARZO'      TO WSS-VEC-DMES IN VEC-MES(3).
           MOVE 'ABRIL'      TO WSS-VEC-DMES IN VEC-MES(4).
           MOVE 'MAYO'       TO WSS-VEC-DMES IN VEC-MES(5).
           MOVE 'JUNIO'      TO WSS-VEC-DMES IN VEC-MES(6).
           MOVE 'JULIO'      TO WSS-VEC-DMES IN VEC-MES(7).
           MOVE 'AGOSTO'     TO WSS-VEC-DMES IN VEC-MES(8).
           MOVE 'SEPTIEMBRE' TO WSS-VEC-DMES IN VEC-MES(9).
           MOVE 'OCTUBRE'    TO WSS-VEC-DMES IN VEC-MES(10).
           MOVE 'NOVIEMBRE'  TO WSS-VEC-DMES IN VEC-MES(11).
      *DAD INI  29-NOV-1994
      *    MOVE 'DICIEMBRE'  TO WSS-VEC-DMES IN VEC-MES(11).
           MOVE 'DICIEMBRE'  TO WSS-VEC-DMES IN VEC-MES(12).
      *DAD FIN  29-NOV-1994

           MOVE WSS-VEC-DMES IN VEC-MES(WSS-NUM-MPRO) TO WSS-GLS-DMES.

      *>>>>
           SORT GNSSRT
       COPY GARRSCC3.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
      *<<<< FIN
           PERFORM CLO-FIL.
      *>>>>
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
           IF GAR-IND-CPL IN GDG NOT = 'S' OR
              GAR-FEC-ALZ > ZEROES OR
              GAR-TIP-VCB IN GDG NOT = '1'
               GO TO NXT-INP-SRT.

      *Lee Registro Tag
           MOVE GAR-TIP-OPE IN GDG TO GAR-TIP-OPE IN TAG.
           MOVE GAR-STP-OPE IN GDG TO GAR-STP-OPE IN TAG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
               MOVE 'GAR'        TO MSG-COD-SIST
               MOVE 'TAG    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY 'TAG : ' GAR-CLV-TAG-1 IN TAG ' '
                       MSG-GLS-DESC
               GO TO NXT-INP-SRT.

      * Busca Codigo Cuenta Contable en Tabla DCC de GAR
           MOVE 'GAR'                  TO TAB-COD-SIST.
           MOVE 'DCC'                  TO TAB-COD-TTAB IN TAB.
           MOVE GAR-CLA-OPE-SUP IN TAG TO WSS-CLA-OPE-SUP.
           MOVE GAR-VCB         IN GDG TO WSS-VCB.
           MOVE WSS-COD-CTA            TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               MOVE 'GAR'        TO MSG-COD-SIST
               MOVE 'DCC    NEX' TO MSG-COD-MENS
               PERFORM GET-MSG
               DISPLAY 'DCC : ' TAB-COD-CTAB IN TAB ' '
                       MSG-GLS-DESC
               GO TO NXT-INP-SRT.
           MOVE TAB-GLS-DATA IN TAB TO SRT-CIC-CCSB.

           MOVE 'TAB' TO TAB-COD-SIST.
           MOVE 'AUX' TO TAB-COD-TTAB IN TAB.
           MOVE GAR-TIP-OPE IN GDG TO WSS-TIP-OPE.
           MOVE GAR-STP-OPE IN GDG TO WSS-STP-OPE.
           MOVE WSS-TIP-STP TO TAB-COD-CTAB IN TAB.
           PERFORM BUS-TAB.
           IF MSG-COD-MENS > SPACES
               MOVE SPACES TO MSG-COD-MENS
           ELSE
               MOVE TAB-GLS-DESC IN TAB      TO SRT-GLS-DESC IN SRT.

      *Busca Registro GVT
           MOVE GAR-NUM-SIS IN GDG TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GDG TO GAR-COD-CRR IN GVT.
           MOVE GAR-COD-TSN IN GDG TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO GVT.

           IF NOT FIO-STAT-OKS
               MOVE ZEROES TO GAR-FEC-TSN IN GVT
                              GAR-FEC-RVL IN GVT
                              GAR-VAL-CML-TSN IN GVT
                              GAR-VAL-AJT-SUP IN GVT.

           MOVE GAR-VAL-AJT-SUP IN GVT TO SRT-VAL-UCON IN SRT.

      *>>>>
       MOV-INP-SRT.
       COPY GARRMCC3.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-GDG.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTCC3.
      *     MOVE 'GARPLCC3' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
      ***** CRW-COMIENZO
      *    INITIATE RPT-CC3.
           MOVE 0 TO RPT-SUMA-VAL-COME
           MOVE 0 TO RPT-SUMA-VAL-AJUS
           MOVE 0 TO RPT-SUMA-VAL-UCON
           MOVE 0 TO RPT-SUMA-CTA-COME
           MOVE 0 TO RPT-SUMA-CTA-AJUS
           MOVE 0 TO RPT-SUMA-CTA-UCON
           MOVE 0 TO RPT-VARI-PAGE-COUNTER.
      ***** CRW-FINAL
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
       GEN-OUT-SRT.
      ***** CRW-COMIENZO
      *    GENERATE DL-CC3.
           MOVE GDG-NUM-SIS IN SRT             TO RPT-DETA-NUM-SIS
           MOVE GDG-COD-CRR IN SRT             TO RPT-DETA-COD-CRR
           MOVE SRT-GLS-DESC IN SRT            TO RPT-DETA-GLS-DESC
           MOVE GVT-FD-TSN IN SRT              TO RPT-DETA-FD-TSN
           MOVE GVT-FM-TSN IN SRT              TO RPT-DETA-FM-TSN
           MOVE GVT-FA-TSN IN SRT              TO RPT-DETA-FA-TSN
           MOVE GVT-VAL-CML-TSN IN SRT         TO RPT-DETA-VAL-CML-TSN
           MOVE GVT-VAL-AJT-SUP IN SRT         TO RPT-DETA-VAL-AJT-SUP
           MOVE GVT-FD-RVL IN SRT              TO RPT-DETA-FD-RVL
           MOVE GVT-FM-RVL IN SRT              TO RPT-DETA-FM-RVL
           MOVE GVT-FA-RVL IN SRT              TO RPT-DETA-FA-RVL
           MOVE SRT-VAL-UCON IN SRT            TO RPT-DETA-VAL-UCON
           IF RPT-IND-INICIO-LISTADO = 'SI'
              MOVE GDG-VCB IN SRT         TO RPT-CTRL-VCB
              MOVE SRT-CIC-CCSB IN SRT    TO RPT-CTRL-CIC-CCSB
              MOVE GDG-COD-OFI IN SRT     TO RPT-CTRL-COD-OFI
              MOVE 'NO' TO RPT-IND-INICIO-LISTADO.
           IF GDG-VCB IN SRT      NOT = RPT-CTRL-VCB      OR
              SRT-CIC-CCSB IN SRT NOT = RPT-CTRL-CIC-CCSB OR
              GDG-COD-OFI IN SRT  NOT = RPT-CTRL-COD-OFI
              PERFORM INI-CTRL-REPORT
              PERFORM INI-HEAD-REPORT.
           IF RPT-VARI-LINEA > 59
              PERFORM INI-HEAD-REPORT.
           ADD GVT-VAL-CML-TSN IN SRT TO RPT-SUMA-VAL-COME
           ADD GVT-VAL-AJT-SUP IN SRT TO RPT-SUMA-VAL-AJUS
           ADD SRT-VAL-UCON    IN SRT TO RPT-SUMA-VAL-UCON
           WRITE RPTCC3-REG FROM  DL-CC3-08
           ADD 1 TO RPT-VARI-LINEA.
      ***** CRW-FINAL

       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      ***** CRW-COMIENZO
      *    TERMINATE RPT-CC3.
           MOVE HIGH-VALUE TO RPT-CTRL-VCB
           MOVE HIGH-VALUE TO RPT-CTRL-CIC-CCSB
           MOVE HIGH-VALUE TO RPT-CTRL-COD-OFI
           PERFORM INI-CTRL-REPORT.
      ***** CRW-FINAL
           CLOSE RPTCC3.
       EXT-OUT-SRT.
           EXIT.
      ***** CRW-COMIENZO
       INI-HEAD-REPORT SECTION.
       INI-HEADING.
           PERFORM DEC-PH-CC3
           MOVE RPT-DUSU                       TO RPT-HEAD-DUSU
           MOVE RPT-FDMA                       TO RPT-HEAD-FDMA
           MOVE RPT-DSIS                       TO RPT-HEAD-DSIS
           MOVE WSS-GLS-OFI IN WSS-CC3         TO RPT-HEAD-GLS-OFI
           MOVE WSS-GLS-DMES IN WSS-CC3        TO RPT-HEAD-GLS-DMES
           MOVE SRT-CIC-CCSB IN SRT            TO RPT-HEAD-CIC-CCSB
           MOVE GDG-VCB IN SRT                 TO RPT-HEAD-VCB
           MOVE WSS-GLS-VCB IN WSS-CC3         TO RPT-HEAD-GLS-VCB
           ADD 1 TO RPT-VARI-PAGE-COUNTER
           MOVE RPT-VARI-PAGE-COUNTER TO RPT-HEAD-PAGE-COUNTER
           MOVE SPACES TO RPTCC3-REG
           WRITE RPTCC3-REG AFTER PAGE
           WRITE RPTCC3-REG FROM  PH-CC3-01
           WRITE RPTCC3-REG FROM  PH-CC3-02
           WRITE RPTCC3-REG FROM  PH-CC3-03 AFTER 2

           WRITE RPTCC3-REG FROM  PH-CC3-04
           WRITE RPTCC3-REG FROM  PH-CC3-05
           WRITE RPTCC3-REG FROM  PH-CC3-06
           WRITE RPTCC3-REG FROM  PH-CC3-07
           MOVE 9 TO RPT-VARI-LINEA.
       FIN-HEADING.
       FIN-HEAD-REPORT.
           EXIT.
       INI-CTRL-REPORT SECTION.
       INI-CONTROL.
           IF GDG-VCB IN SRT      NOT = RPT-CTRL-VCB      OR
              SRT-CIC-CCSB IN SRT NOT = RPT-CTRL-CIC-CCSB OR
              GDG-COD-OFI IN SRT  NOT = RPT-CTRL-COD-OFI
              MOVE GDG-VCB IN SRT      TO RPT-CTRL-VCB
              MOVE RPT-SUMA-VAL-COME   TO RPT-FOOT-VAL-COME
              MOVE RPT-SUMA-VAL-AJUS   TO RPT-FOOT-VAL-AJUS
              MOVE RPT-SUMA-VAL-UCON   TO RPT-FOOT-VAL-UCON
              ADD  RPT-SUMA-VAL-COME   TO RPT-SUMA-CTA-COME
              ADD  RPT-SUMA-VAL-AJUS   TO RPT-SUMA-CTA-AJUS
              ADD  RPT-SUMA-VAL-UCON   TO RPT-SUMA-CTA-UCON
              MOVE 0 TO RPT-SUMA-VAL-COME
              MOVE 0 TO RPT-SUMA-VAL-AJUS
              MOVE 0 TO RPT-SUMA-VAL-UCON
              WRITE RPTCC3-REG FROM  CF-CC3-09
              WRITE RPTCC3-REG FROM  CF-CC3-10
              ADD 2 TO RPT-VARI-LINEA.
           IF SRT-CIC-CCSB IN SRT NOT = RPT-CTRL-CIC-CCSB OR
              GDG-COD-OFI IN SRT  NOT = RPT-CTRL-COD-OFI
              MOVE SRT-CIC-CCSB IN SRT TO RPT-CTRL-CIC-CCSB
              MOVE RPT-SUMA-CTA-COME   TO RPT-FOOT-CTA-COME
              MOVE RPT-SUMA-CTA-AJUS   TO RPT-FOOT-CTA-AJUS
              MOVE RPT-SUMA-CTA-UCON   TO RPT-FOOT-CTA-UCON
              MOVE 0 TO RPT-SUMA-CTA-COME
              MOVE 0 TO RPT-SUMA-CTA-AJUS
              MOVE 0 TO RPT-SUMA-CTA-UCON
              WRITE RPTCC3-REG FROM  CF-CC3-11
              WRITE RPTCC3-REG FROM  CF-CC3-12
              ADD 2 TO RPT-VARI-LINEA.
           IF GDG-COD-OFI IN SRT  NOT = RPT-CTRL-COD-OFI
              MOVE GDG-COD-OFI IN SRT  TO RPT-CTRL-COD-OFI.
       FIN-CONTROL.
       FIN-CTRL-REPORT.
           EXIT.
      ***** CRW-FINAL

       COPY GARBFGDG.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
       COPY GARBFGVT.
       COPY GARBFTAG.

      *>>>> NOT PGM_VMS
       COPY TABBFOFI.
       COPY TABBBOFI.

       COPY GNSBBTAB.
       COPY GNSBBMSG.
      *>>>>
       CLO-FIL SECTION.
       INI-CLO-FIL.
           MOVE FIO-CLO      TO FIO-CMND.
           PERFORM TAB-FIO-OFI.

           MOVE FIO-CLO      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-CLO      TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           PERFORM GAR-FIO-TAG.
       FIN-CLO-FIL.
           EXIT.
      *>>>>
