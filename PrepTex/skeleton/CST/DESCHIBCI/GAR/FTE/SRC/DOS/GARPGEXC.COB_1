       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGEXC.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    18-JUN-2003.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GARBRUTS.
       COPY GARBRBCI.
       COPY GARBRCSE.
       COPY GARBRGEN.
       COPY GARBRMNR.
       COPY GARBRGEC.
       COPY GARBRENR.
      *
       COPY GARBRGDS.
       COPY GARBRDDS.
       COPY GARBREES.
       COPY COLBRAUX.
      *
      * Registros de las Bases Originales.
       COPY GARBRGDG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GARBRGDD.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GARBRGSE.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GSE-REQA==.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY COLBROPC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OPC-REQA==.
       COPY COLBRRDC.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RDC-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
      *-------------
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *-------------
      *
       01  WS-VARIABLES.
           02 WSA-GAR-TIP-OPE      PIC X(10) VALUE SPACES.
           02 WSS-FEC-NANO         PIC 9(04) VALUE ZEROS.
           02 EOF-UTS              PIC 9(01) VALUE ZEROS.
           02 FIN-BCI              PIC 9(01) VALUE ZEROS.
           02 EOF-BCI              PIC 9(01) VALUE ZEROS.
           02 FIN-GDS              PIC 9(01) VALUE ZEROS.
           02 FIN-TESP             PIC 9(01) VALUE ZEROS.
           02 FIN-RDS              PIC 9(01) VALUE ZEROS.
           02 FIN-ROPS             PIC 9(01) VALUE ZEROS.
           02 WSA-TIPO-OPERA       PIC X(03) VALUE SPACES.
           02 WSA-LEE-TIP-OPE      PIC X(03) VALUE SPACES.
           02 SW-HAY-RDC           PIC 9(01) VALUE ZEROS.
           02 SW-LEE-BCI           PIC 9(01) VALUE ZEROS.
           02 SW-LEE-GDS           PIC 9(01) VALUE ZEROS.
           02 SW-LEE-EES           PIC 9(01) VALUE ZEROS.
           02 SW-LEE-DDS           PIC 9(01) VALUE ZEROS.
           02 WS-CON-TASCOM        PIC 9(09) VALUE ZEROS.
           02 WS-CON-TASGEN        PIC 9(09) VALUE ZEROS.
           02 WS-CON-TASMNR        PIC 9(09) VALUE ZEROS.
           02 WS-CON-TASCSE        PIC 9(09) VALUE ZEROS.
           02 WS-CON-TASENR        PIC 9(09) VALUE ZEROS.
           02 SW-MAY-REV           PIC 9(01) VALUE ZEROS.
           02 CON-CDR              PIC 9(09) VALUE ZEROS.

       01  WSA-UTS-GAR.
           02 WSA-UTS-NUM-SIS      PIC X(04).
           02 WSA-UTS-COD-CRR      PIC X(08).

       01  WSA-NUM-CREDITO.
           02 WSA-NUM-SIS-CRD      PIC X(04).
           02 WSA-COD-CRR-CRD      PIC X(08).

       01  WS-FEC-TASACION.
           02 WS-GAR-FS-TSN        PIC 9(02).
           02 WS-GAR-FA-TSN        PIC 9(02).
           02 WS-GAR-FM-TSN        PIC 9(02).
           02 WS-GAR-FD-TSN        PIC 9(02).

       01  WS-FECHA-PROCESO.
           02 WSS-NUM-DP02         PIC 9(02).
           02 WSS-NUM-MP02         PIC 9(02).
           02 WSS-NUM-SP02         PIC 9(02).
           02 WSS-NUM-AP02         PIC 9(02).

       01  WS-FEC-PROC-SAMD.
           02 WSS-FPR-SP02         PIC 9(02).
           02 WSS-FPR-AP02         PIC 9(02).
           02 WSS-FPR-MP02         PIC 9(02).
           02 WSS-FPR-DP02         PIC 9(02).

       01  WS-FECHA-TRAB.
           02 WSS-NUM-STRA         PIC 9(02).
           02 WSS-NUM-ATRA         PIC 9(02).
           02 WSS-NUM-MTRA         PIC 9(02).
           02 WSS-NUM-DTRA         PIC 9(02).


       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGEXC" TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           MOVE ZEROS TO CON-CDR.
           MOVE ZEROS TO EOF-UTS.
           MOVE ZEROS TO EOF-BCI.
           MOVE ZEROS TO SW-LEE-BCI.
           MOVE ZEROS TO SW-LEE-GDS.
           MOVE ZEROS TO SW-LEE-EES.
           MOVE ZEROS TO SW-LEE-DDS.
           MOVE ZEROS TO WS-CON-TASCOM.
           MOVE ZEROS TO WS-CON-TASGEN.
           MOVE ZEROS TO WS-CON-TASMNR.
           MOVE ZEROS TO WS-CON-TASCSE.
           MOVE ZEROS TO WS-CON-TASENR.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-GARUTS.
           PERFORM PROCESA-EXC UNTIL EOF-UTS = 1.
           DISPLAY 'GRTS. CON EXCEP.COMERCIAL ' WS-CON-TASCOM.
           DISPLAY 'GRTS. CON EXCEP.NORMATIVA ' WS-CON-TASGEN.
           DISPLAY 'GRTS. MALAS NO RETASADAS  ' WS-CON-TASMNR.
           DISPLAY 'GRTS. CORRECTAS SIN EXCEP ' WS-CON-TASCSE.
           DISPLAY 'GRTS. NO RELACIONADAS     ' WS-CON-TASENR.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE 'GAR'   TO FIO-SIST.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-UTS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-BCI.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-DDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-EES.
      * Archivos de Salida.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-CSE.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-GEN.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-GEC.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-MNR.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-ENR.
       FIN-OPEN-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       LEE-GARUTS SECTION.
       INI-LEE-UTS.
           IF EOF-UTS = ZEROS THEN
              PERFORM GAR-FIO-UTS.
           IF FIO-STAT-EOF THEN
              MOVE 1 TO EOF-UTS.
       FIN-LEE-UTS.
           EXIT.
      *
       LEE-GARBCI SECTION.
       INI-LEE-BCI.
           IF EOF-BCI = ZEROS THEN
              MOVE FIO-GET-NXT    TO FIO-CMND
              PERFORM GAR-FIO-BCI.
           IF FIO-STAT-EOF THEN
              MOVE 1 TO EOF-BCI.
       FIN-LEE-BCI.
           EXIT.
      *
       LEE-GARGDS SECTION.
       INI-LEE-GDS.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM GAR-FIO-GDS.
       FIN-LEE-GDS.
           EXIT.
      *
       LEE-GAREES SECTION.
       INI-LEE-EES.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM GAR-FIO-EES.
       FIN-LEE-EES.
           EXIT.
      *
       LEE-GARDDS SECTION.
       INI-LEE-DDS.
           MOVE FIO-GET-NXT    TO FIO-CMND.
           PERFORM GAR-FIO-DDS.
       FIN-LEE-DDS.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       PROCESA-EXC SECTION.
       INI-PROCESA-EXC.
           MOVE ZEROS TO SW-MAY-REV.
           PERFORM CARGA-WSA-UTS-GAR.
           PERFORM LEE-TABAUX.
           IF WSA-GAR-TIP-OPE(1:1) = 'A' THEN
              MOVE ZEROS TO FIN-BCI
              PERFORM PROCESA-GARBCI UNTIL FIN-BCI = 1
                                      OR   EOF-BCI = 1
              PERFORM CONS-NOEX-BCI
           ELSE
              IF WSA-GAR-TIP-OPE(1:1) NOT = ' ' THEN
                 PERFORM GRABA-GARCSE
                 PERFORM REVISA-BCI
              ELSE
                 IF SW-MAY-REV = ZEROS THEN
                    DISPLAY 'GRT SIN DATO ANEXO TIPO SUBTIPO'
                    DISPLAY 'GARANTIA     ' WSA-UTS-GAR
                    DISPLAY 'TIPO SUBTIPO ' UTS-GAR-CLA-OPE
                    PERFORM REVISA-BCI.
           IF SW-MAY-REV = ZEROS THEN
              MOVE FIO-GET-NXT    TO FIO-CMND
              PERFORM LEE-GARUTS.
       FIN-PROCESA-EXC.
           EXIT.
      *
       CARGA-WSA-UTS-GAR SECTION.
       INI-UTS-GAR.
           MOVE UTS-GAR-NUM-SIS TO WSA-UTS-NUM-SIS.
           MOVE UTS-GAR-COD-CRR TO WSA-UTS-COD-CRR.
       FIN-UTS-GAR.
           EXIT.
      *
       LEE-TABAUX SECTION.
       INI-LEE-TAB.
           IF UTS-GAR-CLA-OPE > SPACES THEN
              MOVE 'TAB'              TO TAB-COD-SIST
              MOVE 'AUX'              TO TAB-COD-TTAB IN TAB
              MOVE SPACES             TO TAB-COD-CTAB IN TAB
              MOVE UTS-GAR-CLA-OPE    TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
              MOVE SPACES TO WSA-GAR-TIP-OPE
              IF MSG-COD-MENS = SPACES THEN
                 MOVE TAB-COD-DAT1 IN TAB-GLS-DAT1(1) TO
                                      WSA-GAR-TIP-OPE.
       FIN-LEE-TAB.
           EXIT.
      *
       PROCESA-GARBCI SECTION.
       INI-PROC-BCI.
           IF SW-LEE-BCI = ZEROS
              PERFORM LEE-GARBCI
              MOVE 1 TO SW-LEE-BCI.
           IF WSA-UTS-GAR = BCI-ID-GAR THEN
              PERFORM GRABA-GARGEC
              PERFORM LEE-GARBCI
              MOVE 1     TO FIN-BCI
           ELSE
              IF WSA-UTS-GAR > BCI-ID-GAR THEN
                 PERFORM GRABA-GARENR
                 PERFORM LEE-GARBCI
              ELSE
                 MOVE ZEROS TO FIN-GDS
                 PERFORM PROCESA-GARGDS UNTIL FIN-GDS = 1
                 MOVE 1 TO FIN-BCI.
           IF EOF-BCI = 1 THEN
              MOVE 1 TO FIN-BCI.
       FIN-PROC-BCI.
           EXIT.
      *
       CONS-NOEX-BCI SECTION.
       INI-NOEX-BCI.
           IF EOF-BCI = 1 AND FIN-BCI = ZEROS THEN
              MOVE ZEROS TO FIN-GDS
              PERFORM PROCESA-GARGDS UNTIL FIN-GDS = 1.
      *
       REVISA-BCI SECTION.
       INI-REV-BCI.
           IF SW-LEE-BCI = ZEROS
              PERFORM LEE-GARBCI
              MOVE 1 TO SW-LEE-BCI.
           IF EOF-BCI = ZEROS THEN
              IF WSA-UTS-GAR = BCI-ID-GAR THEN
                 PERFORM LEE-GARBCI
              ELSE
                 IF WSA-UTS-GAR > BCI-ID-GAR THEN
                    PERFORM GRABA-GARENR
                    MOVE 1 TO SW-MAY-REV
                    PERFORM LEE-GARBCI.
       FIN-REV-BCI.
           EXIT.

       PROCESA-GARGDS SECTION.
       INI-PROC-GDS.
           IF SW-LEE-GDS = ZEROS THEN
              PERFORM LEE-GARGDS
              MOVE 1 TO SW-LEE-GDS.
           IF WSA-UTS-GAR = GAR-CLV-GDG-1 IN GDS THEN
              PERFORM VER-TIPO-ESPEC
           ELSE
              IF WSA-UTS-GAR < GAR-CLV-GDG-1 IN GDS THEN
                 MOVE 1 TO FIN-GDS.
           IF FIN-GDS = ZEROS THEN
              PERFORM LEE-GARGDS.
       FIN-PROC-GDS.
           EXIT.
      *
       VER-TIPO-ESPEC SECTION.
       INI-TIP-ESP.
           MOVE ZEROS  TO FIN-TESP.
           MOVE SPACES TO GEN-GLOSA-TIPO.
           MOVE SPACES TO GEN-GLOSA-TEXT.
           IF GAR-COD-LIM IN GDS = 'E' THEN
              PERFORM GRTS-ESPECIFICAS UNTIL FIN-TESP = 1
           ELSE
              IF GAR-COD-LIM IN GDS = 'G' THEN
                 MOVE ZEROS TO SW-HAY-RDC
                 PERFORM GRTS-GENERALES UNTIL FIN-TESP = 1.
           IF GEN-GLOSA-TIPO NOT = SPACES THEN
              MOVE 'NO ES NECESARIO RETASAR' TO GEN-GLOSA-TEXT
              PERFORM GRABA-GARGEN.
       FIN-TIP-ESP.
           EXIT.
      *
      * Garantias Especificas
      *
       GRTS-ESPECIFICAS SECTION.
       INI-GRTS-ESP.
           IF SW-LEE-EES = ZEROS THEN
              PERFORM LEE-GAREES
              MOVE 1 TO SW-LEE-EES.
           IF WSA-UTS-GAR = GAR-ID IN EES THEN
              PERFORM PROCESO-EESOPS
              PERFORM LEE-GAREES
           ELSE
              IF WSA-UTS-GAR > GAR-ID IN EES THEN
                 PERFORM LEE-GAREES
              ELSE
                 MOVE 1 TO FIN-TESP.
       FIN-GRTS-ESP.
           EXIT.
      *
       PROCESO-EESOPS SECTION.
       INI-PROC-OPS.
      * Se lee base de datos OPC.
           MOVE GAR-NUM-SIS-CDT IN EES TO WSA-NUM-SIS-CRD.
           MOVE GAR-COD-CRR-CDT IN EES TO WSA-COD-CRR-CRD.
           PERFORM LEE-OPC-KEY.
           IF FIO-STAT-OKS
              PERFORM OBTENGO-SALDO
           ELSE
               DISPLAY 'CRD NO EXISTE EN OPC' WSA-NUM-CREDITO.
       FIN-PROC-OPS.
           EXIT.
      *
      * Garantias Generales.
      *
       GRTS-GENERALES SECTION.
       INI-GRTS-GEN.
           IF SW-LEE-DDS = ZEROS THEN
              PERFORM LEE-GARDDS
              MOVE 1 TO SW-LEE-DDS.
           IF WSA-UTS-GAR = GAR-ID IN DDS THEN
              PERFORM PROCESO-COLRDS
              PERFORM LEE-GARDDS
           ELSE
              IF WSA-UTS-GAR > GAR-ID IN DDS THEN
                 PERFORM LEE-GARDDS
              ELSE
                 MOVE 1 TO FIN-TESP
                 IF SW-HAY-RDC = ZEROS THEN
                    MOVE 1  TO GEN-GLOSA-TIPO(1:1).
       FIN-GRTS-GEN.
           EXIT.
      *
       PROCESO-COLRDS SECTION.
       INI-PROC-RDS.
      *    Numero de documento del cliente
           MOVE GDD-CAI-ICLI IN DDS TO RDC-CAI-ICLI IN RDC.
           MOVE GDD-IIC-ICLI IN DDS TO RDC-IIC-ICLI IN RDC.
           MOVE SPACES              TO RDC-CIC-IOPC IN RDC.
           MOVE ZEROES              TO RDC-NUM-IDLC IN RDC.
           MOVE FIO-GET-NLS         TO FIO-CMND.
           PERFORM COL-FIO-RDC.
           IF FIO-STAT-OKS
              MOVE ZEROS TO FIN-RDS
              PERFORM BUSCO-COLRDC UNTIL FIN-RDS = 1.
       FIN-PROC-RDS.
           EXIT.

       BUSCO-COLRDC SECTION.
       INI-BUS-COLRDC.
           IF RDC-CIC-ICLI IN RDC = GDD-CIC-ICLI IN DDS THEN
              MOVE 1 TO SW-HAY-RDC
              PERFORM PROCESO-RDSOPS
              MOVE FIO-GET-NXT TO FIO-CMND
              PERFORM COL-FIO-RDC
           ELSE
              IF RDC-CIC-ICLI IN RDC > GDD-CIC-ICLI IN DDS
                 MOVE 1 TO FIN-RDS.
       FIN-BUS-COLRDC.
           EXIT.
      *
       PROCESO-RDSOPS SECTION.
       INI-PROC-RDSOPS.
           MOVE RDC-CAI-IOPC IN RDC TO WSA-NUM-SIS-CRD.
           MOVE RDC-IIC-IOPC IN RDC TO WSA-COD-CRR-CRD.
           PERFORM LEE-OPC-KEY.
           IF FIO-STAT-OKS
              PERFORM OBTENGO-SALDO
           ELSE
              DISPLAY 'NUM. CRED.NO EXISTE EN OPC'
              DISPLAY 'CREDITO ' WSA-NUM-CREDITO.
       FIN-PROC-OPS.
           EXIT.

       LEE-OPC-KEY SECTION.
       INI-OPC-KEY.
           MOVE WSA-NUM-SIS-CRD TO OPC-CAI-IOPC IN OPC.
           MOVE WSA-COD-CRR-CRD TO OPC-IIC-IOPC IN OPC.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM COL-FIO-OPC.
       FIN-OPC-KEY.
           EXIT.

      *-------------------------------------------------------------
       OBTENGO-SALDO SECTION.
       INI-OBT-SALDO.
           IF OPC-VAL-SCRE IN OPC = ZEROS THEN
              MOVE 1  TO GEN-GLOSA-TIPO(1:1)
           ELSE
              IF OPC-VAL-SCRE IN OPC > ZEROS THEN
                 IF OPC-MSC-STAT IN OPC = 'A'   THEN
                    PERFORM CONS-TIPO-OPE
                 ELSE
                    MOVE 1  TO GEN-GLOSA-TIPO(1:1).
       FIN-OBT-SALDO.
           EXIT.
      *
       LEE-TAB-TIO SECTION.
       INI-TAB-TIO.
           IF WSA-LEE-TIP-OPE > SPACES THEN
              MOVE 'TAB'           TO TAB-COD-SIST
              MOVE 'TIO'           TO TAB-COD-TTAB IN TAB
              MOVE SPACES          TO TAB-COD-CTAB IN TAB
              MOVE WSA-LEE-TIP-OPE TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
              IF MSG-COD-MENS = SPACES THEN
                 MOVE TAB-CIC-CTAB IN TAB TO WSA-TIPO-OPERA.
       FIN-TAB-TIO.
           EXIT.
      *
       CONS-TIPO-OPE SECTION.
       INI-TIP-OPE.
           IF OPC-FEC-FINP IN OPC < UTS-GAR-FEC-TSN THEN
              DISPLAY 'FEC.COMPUT MENOR A FEC. TASACION'
           ELSE
              IF OPC-COD-TOPC NOT = 'F' THEN
                 PERFORM VER-TIPO-GRT
                 PERFORM COMPARA-FECHAS
              ELSE
                 MOVE 2  TO GEN-GLOSA-TIPO(2:1).
       FIN-TIP-OPE.
           EXIT.
      *
       VER-TIPO-GRT SECTION.
       INI-TIP-GRT.
           MOVE UTS-GAR-TIP-OPE TO WSA-LEE-TIP-OPE.
           PERFORM LEE-TAB-TIO.
       FIN-TIP-OPE.
           EXIT.
      *
       COMPARA-FECHAS SECTION.
       INI-COMP-FEC.
           IF WSA-TIPO-OPERA = '951' THEN
              MOVE 2    TO FEC-NANO
           ELSE
              MOVE 1    TO FEC-NANO.
           PERFORM RESTO-FECHA.
           PERFORM VALIDO-VENCTO.
       FIN-COMP-FEC.
           EXIT.
      *
       RESTO-FECHA SECTION.
       INI-RST-FEC.
      * FECHA CURSE COMPUTACIONAL OPC
           MOVE OPC-NUM-DINP IN OPC TO WSS-FPR-DP02.
           MOVE OPC-NUM-MINP IN OPC TO WSS-FPR-MP02.
           MOVE OPC-NUM-SINP IN OPC TO WSS-FPR-SP02.
           MOVE OPC-NUM-AINP IN OPC TO WSS-FPR-AP02.
      * FECHA TASACION UTS
           MOVE UTS-GAR-FEC-TSN TO WS-FEC-TASACION.
           MOVE WS-GAR-FD-TSN TO FEC-ITM1.
           MOVE WS-GAR-FM-TSN TO FEC-ITM2.
           MOVE WS-GAR-FS-TSN TO FEC-ITM3.
           MOVE WS-GAR-FA-TSN TO FEC-ITM4.

           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-SUM-ANO  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'ERRDIFANO'  TO MSG-COD-MENS IN MSG
           ELSE
              MOVE FEC-ITM1 TO WSS-NUM-DTRA
              MOVE FEC-ITM2 TO WSS-NUM-MTRA
              MOVE FEC-ITM3 TO WSS-NUM-STRA
              MOVE FEC-ITM4 TO WSS-NUM-ATRA.
       FIN-RST-FEC.
           EXIT.
      *
       VALIDO-VENCTO SECTION.
       INI-VAL-VCTO.
           IF WS-FECHA-TRAB < WS-FEC-PROC-SAMD THEN
              PERFORM GRABA-GARMNR
              MOVE 1 TO FIN-TESP
              MOVE 1 TO FIN-RDS
              MOVE SPACES TO GEN-GLOSA-TIPO
           ELSE
              MOVE 3  TO GEN-GLOSA-TIPO(3:1).
       FIN-VAL-VCTO.
           EXIT.
      *
      *--------------Grabaciones de Archivos------------------------
      *
       GRABA-GARCSE SECTION.
       INI-GRAB-CSE.
           MOVE SPACES                 TO CSE-GAR-NUM-SIS.
           MOVE SPACES                 TO CSE-GAR-COD-CRR.
           MOVE SPACES                 TO CSE-GAR-TIP-OPE.
           MOVE SPACES                 TO CSE-GAR-STP-OPE.
           MOVE ZEROS                  TO CSE-GAR-MONTO-ACT.
           MOVE ZEROS                  TO CSE-GAR-FEC-TSN.
           MOVE ZEROS                  TO CSE-GAR-FEC-EXC.
           MOVE UTS-GAR-NUM-SIS        TO CSE-GAR-NUM-SIS.
           MOVE UTS-GAR-COD-CRR        TO CSE-GAR-COD-CRR.
           MOVE UTS-GAR-TIP-OPE        TO CSE-GAR-TIP-OPE.
           MOVE UTS-GAR-STP-OPE        TO CSE-GAR-STP-OPE.
           MOVE UTS-GAR-MONTO-TSN      TO CSE-GAR-MONTO-ACT.
           MOVE UTS-GAR-FEC-TSN        TO CSE-GAR-FEC-TSN.
           MOVE BCI-FEC-VENC-EXCEPCION TO CSE-GAR-FEC-EXC.
           MOVE FIO-PUT           TO FIO-CMND.
           PERFORM GAR-FIO-CSE.
           ADD 1 TO WS-CON-TASCSE.
       FIN-GRAB-CSE.
           EXIT.
      *
       GRABA-GARGEC SECTION.
       INI-GRAB-GEC.
           MOVE SPACES                 TO GEC-GAR-NUM-SIS.
           MOVE SPACES                 TO GEC-GAR-COD-CRR.
           MOVE SPACES                 TO GEC-GAR-TIP-OPE.
           MOVE SPACES                 TO GEC-GAR-STP-OPE.
           MOVE ZEROS                  TO GEC-GAR-MONTO-ACT.
           MOVE ZEROS                  TO GEC-GAR-FEC-TSN.
           MOVE ZEROS                  TO GEC-FEC-VCTO-EXCP-COM.
           MOVE UTS-GAR-NUM-SIS        TO GEC-GAR-NUM-SIS.
           MOVE UTS-GAR-COD-CRR        TO GEC-GAR-COD-CRR.
           MOVE UTS-GAR-TIP-OPE        TO GEC-GAR-TIP-OPE.
           MOVE UTS-GAR-STP-OPE        TO GEC-GAR-STP-OPE.
           MOVE UTS-GAR-MONTO-TSN      TO GEC-GAR-MONTO-ACT.
           MOVE UTS-GAR-FEC-TSN        TO GEC-GAR-FEC-TSN.
           MOVE BCI-FEC-VENC-EXCEPCION TO GEC-FEC-VCTO-EXCP-COM.
           MOVE FIO-PUT                TO FIO-CMND.
           PERFORM GAR-FIO-GEC.
           ADD 1 TO WS-CON-TASCOM.
       FIN-GRAB-GEC.
           EXIT.
      *
       GRABA-GARGEN SECTION.
       INI-GRAB-GEN.
           MOVE SPACES                 TO GEN-GAR-NUM-SIS.
           MOVE SPACES                 TO GEN-GAR-COD-CRR.
           MOVE SPACES                 TO GEN-GAR-TIP-OPE.
           MOVE SPACES                 TO GEN-GAR-STP-OPE.
           MOVE ZEROS                  TO GEN-GAR-MONTO-ACT.
           MOVE ZEROS                  TO GEN-GAR-FEC-TSN.
           MOVE UTS-GAR-NUM-SIS        TO GEN-GAR-NUM-SIS.
           MOVE UTS-GAR-COD-CRR        TO GEN-GAR-COD-CRR.
           MOVE UTS-GAR-TIP-OPE        TO GEN-GAR-TIP-OPE.
           MOVE UTS-GAR-STP-OPE        TO GEN-GAR-STP-OPE.
           MOVE UTS-GAR-MONTO-TSN      TO GEN-GAR-MONTO-ACT.
           MOVE UTS-GAR-FEC-TSN        TO GEN-GAR-FEC-TSN.
           MOVE FIO-PUT                TO FIO-CMND.
           PERFORM GAR-FIO-GEN.
           ADD 1 TO WS-CON-TASGEN.
       FIN-GRAB-GEN.
           EXIT.
      *
       GRABA-GARMNR SECTION.
       INI-GRAB-MNR.
           MOVE SPACES                 TO MNR-GAR-NUM-SIS.
           MOVE SPACES                 TO MNR-GAR-COD-CRR.
           MOVE SPACES                 TO MNR-GAR-TIP-OPE.
           MOVE SPACES                 TO MNR-GAR-STP-OPE.
           MOVE ZEROS                  TO MNR-GAR-MONTO-ACT.
           MOVE ZEROS                  TO MNR-GAR-FEC-TSN.
           MOVE UTS-GAR-NUM-SIS        TO MNR-GAR-NUM-SIS.
           MOVE UTS-GAR-COD-CRR        TO MNR-GAR-COD-CRR.
           MOVE UTS-GAR-TIP-OPE        TO MNR-GAR-TIP-OPE.
           MOVE UTS-GAR-STP-OPE        TO MNR-GAR-STP-OPE.
           MOVE UTS-GAR-MONTO-TSN      TO MNR-GAR-MONTO-ACT.
           MOVE UTS-GAR-FEC-TSN        TO MNR-GAR-FEC-TSN.
           MOVE FIO-PUT                TO FIO-CMND.
           PERFORM GAR-FIO-MNR.
           ADD 1 TO WS-CON-TASMNR.
       FIN-GRAB-MNR.
           EXIT.
      *
       GRABA-GARENR SECTION.
       INI-GRAB-ENR.
           MOVE SPACES                 TO ENR-GAR-NUM-SIS.
           MOVE SPACES                 TO ENR-GAR-COD-CRR.
           MOVE ZEROS                  TO ENR-FEC-VCTO-EXCP-COM.
           MOVE BCI-GAR-NUM-SIS        TO ENR-GAR-NUM-SIS.
           MOVE BCI-GAR-COD-CRR        TO ENR-GAR-COD-CRR.
           MOVE BCI-FEC-VENC-EXCEPCION TO ENR-FEC-VCTO-EXCP-COM.
           MOVE FIO-PUT                TO FIO-CMND.
           PERFORM GAR-FIO-ENR.
           ADD 1 TO WS-CON-TASENR.
       FIN-GRAB-ENR.
           EXIT.
      *-------------------------------------------------------------
      *
       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-UTS.
           PERFORM GAR-FIO-BCI.
           PERFORM GAR-FIO-GDS.
           PERFORM GAR-FIO-DDS.
           PERFORM GAR-FIO-EES.
           PERFORM GAR-FIO-CSE.
           PERFORM GAR-FIO-GEN.
           PERFORM GAR-FIO-MNR.
           PERFORM GAR-FIO-MNR.
           PERFORM GAR-FIO-ENR.
       FIN-CLOSE-FILE.
           EXIT.
      *
       COPY GARBFUTS.
       COPY GARBFBCI.
       COPY GARBFGDS.
       COPY GARBFDDS.
       COPY GARBFEES.
       COPY GARBFCSE.
       COPY GARBFGEN.
       COPY GARBFMNR.
       COPY GARBFGEC.
       COPY GARBFENR.
       COPY COLBFOPC.
       COPY COLBFRDC.
      *--------------
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBTABT.
       COPY GNSBGABT.
      *--------------
       COPY GNSBPFEC.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
      *--------------
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY GNSBGEND.
      *-------------------------------------------------------------
