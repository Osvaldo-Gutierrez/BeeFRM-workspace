       IDENTIFICATION DIVISION.
       PROGRAM-ID.  GARPGGDP.

      * Traspasa registros del archivo aplanado GDP a la base de datos
      * nueva

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
       WORKING-STORAGE SECTION.

       COPY GARBRDPS.
       COPY GARBRGDP.

       COPY TABBRCAM.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GSI-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAG-REQA==.
       
       COPY GNSWVSCR.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSLGFIO.
       COPY GNSWGSYS.

       01  WS-VARIABLES.
           02 WSS-FIN-ARCH     VALUE ZEROES PIC 9(01).
           02 WSS-REG-LEIDOS   VALUE ZEROES PIC 9(09).
           02 WSS-REG-GRABADOS VALUE ZEROES PIC 9(09).
           02 WSS-CON-ERROR    VALUE ZEROES PIC 9(09).
          
       PROCEDURE DIVISION.
      *
       MAIN-PROGRAM SECTION.
       INI-MAIN.
           ENTRY 'DBMSCBL'               
           MOVE "GARPGGDP" TO FIO-PROG.  
           PERFORM GNS-BUS-IDD.  
           PERFORM OPEN-FILE.
           MOVE 0 TO WSS-FIN-ARCH.
           MOVE 0 TO WSS-REG-LEIDOS.
           MOVE 0 TO WSS-REG-GRABADOS.
           MOVE 0 TO WSS-CON-ERROR.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-DPS.
           IF NOT FIO-STAT-OKS 
              MOVE 1 TO WSS-FIN-ARCH
           ELSE   
              ADD 1 TO WSS-REG-LEIDOS.
           PERFORM PROCESO UNTIL WSS-FIN-ARCH = 1.
           PERFORM ESTADISTICAS.
           PERFORM CLOSE-FILE
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-DPS.
       FIN-OPEN-FILE.
           EXIT.
      *
       PROCESO SECTION.
       INI-PROCESO.
           PERFORM MUEVE-DATOS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-DPS.
           IF FIO-STAT-EOF
              MOVE 1 TO WSS-FIN-ARCH
           ELSE
              ADD 1 TO WSS-REG-LEIDOS.
       FIN-PROCESO.
           EXIT.
      *
       MUEVE-DATOS SECTION.
       INI-MUEVE-DATOS.
           MOVE GDP-GLS-FLAG    IN DPS TO GDP-GLS-FLAG IN GDP
           MOVE GAR-NUM-SIS     IN DPS TO GDP-CAI-GTIA IN GDP
           MOVE GAR-COD-CRR     IN DPS TO GDP-IIC-GTIA IN GDP
           MOVE GDP-FEC-ULT-ACT IN DPS TO GDP-NUM-STRN IN GDP
           MOVE GDP-HOR-ACT     IN DPS TO GDP-NUM-HHTR IN GDP
           MOVE GDP-MSC-TACC    IN DPS TO GDP-MSC-TACC IN GDP
           MOVE GDP-MSC-STAT    IN DPS TO GDP-MSC-STAT IN GDP
           MOVE GDP-TML-ACT     IN DPS TO GDP-MSC-TTRN IN GDP
           MOVE GDP-IDN-USU-ACT IN DPS TO GDP-COD-ATRN IN GDP
           MOVE GAR-TIP-DPN     IN DPS TO GDP-COD-TDPN IN GDP
           MOVE GAR-TIP-PDO-DPN IN DPS TO GDP-IND-PDPN IN GDP
           MOVE GAR-CTD-PDO-DPN IN DPS TO GDP-NUM-PDPN IN GDP
           MOVE GAR-IND-VIG-DPN IN DPS TO GDP-IND-VDPN IN GDP
           MOVE GAR-FEC-ULT-DPN IN DPS TO GDP-FEC-FUDP IN GDP
           MOVE GAR-FEC-PXM-DPN IN DPS TO GDP-NUM-SPDP IN GDP
           MOVE GAR-VAL-ULT-DPN IN DPS TO GDP-SGV-UDPN IN GDP
           MOVE GAR-VAL-PXM-DPN IN DPS TO GDP-SGV-PDPN IN GDP
           MOVE GAR-PDO-ULT-DPN IN DPS TO GDP-NUM-UDPN IN GDP
           MOVE GAR-VAL-ORI-TSN IN DPS TO GDP-SGV-TSNO IN GDP
           MOVE GAR-FEC-VAL-ORI IN DPS TO GDP-NUM-STSO IN GDP
           MOVE GDP-FEC-ING-SIS IN DPS TO GDP-NUM-SING IN GDP
           MOVE GDP-IDN-USU-ING IN DPS TO GDP-CIC-UING IN GDP
           MOVE GDP-GLS-DISP    IN DPS TO GDP-GLS-DISP IN GDP
           MOVE FIO-PUT                TO FIO-CMND.
           PERFORM GAR-FIO-GDP.
           IF FIO-STAT-OKS 
              ADD 1 TO WSS-REG-GRABADOS
           ELSE   
              ADD 1 TO WSS-CON-ERROR.
       FIN-MUEVE-DATOS.
           EXIT.
      *                                                                 
       ESTADISTICAS SECTION.                                            
       INI-ESTADISTICAS.                                                
           DISPLAY 'REGISTROS LEIDOS DE DPS     : ' WSS-REG-LEIDOS.     
           DISPLAY 'REGISTROS GRABADOS DE GDP   : ' WSS-REG-GRABADOS.   
           DISPLAY 'REGISTROS MAL GRABADOS      : ' WSS-CON-ERROR.
       FIN-ESTADISTICAS.                                                
           EXIT.     
      *     
       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-DPS.
       FIN-CLOSE-FILE.
           EXIT.
      *
       COPY GARBFDPS.
       COPY GARBFGDP.

       COPY GNSBBIDD.

       COPY GNSBTABT.
       COPY GNSBGEND.
