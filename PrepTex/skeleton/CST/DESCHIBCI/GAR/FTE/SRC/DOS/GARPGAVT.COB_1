       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGAVT.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    26-JUN-2003.

      *ACTUALIZA GVT CON VALORES DE WEBTASAC

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GARBRTCF.
       COPY GARBRTSF.
       COPY GARBRTPF.
      *
      * Registros de las Bases Originales.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GARBRGVT.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GARBRGDG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GARBRGTP.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GTP-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGFEC.
       COPY GNSWGPES.
       COPY GNSWGHOY.
      *
       01  WSS-VARI.
           03 WSS-NUM-RLEE-TCF        VALUE ZEROES       PIC 9(05).
           03 WSS-NUM-RLEE-TSF        VALUE ZEROES       PIC 9(05).
           03 WSS-NUM-RERR            VALUE ZEROES       PIC 9(05).
           03 WSS-NUM-GRA-TPF         VALUE ZEROES       PIC 9(05).
           03 WSS-IND-ERRO            VALUE SPACES       PIC X(01).
           03 WSS-NUM-GVTM            VALUE ZEROES       PIC 9(05).
           03 WSS-COD-TASD            VALUE SPACES       PIC X(12).
           03 WSS-COD-SUPV            VALUE SPACES       PIC X(12).
           03 WSS-COD-PESO            VALUE SPACES       PIC X(06).
           03 WSS-COD-UF              VALUE SPACES       PIC X(06).
           03 WSS-I                   VALUE ZEROES       PIC 9(04).
           03 WSS-J                   VALUE ZEROES       PIC 9(04).
           03 WSS-M                   VALUE ZEROES       PIC 9(04).
           03 WSS-UNO                 VALUE 1            PIC 9(01).


      *    Vector de Codigos de Tabla TAS
           03 WSS-TAS.
              05 WSS-VEC-TAS OCCURS 1000.
                 07 WSS-COD-CTAB                      PIC X(12).
                 07 WSS-COD-WEBT                      PIC X(11).

           03 WSS-GLS-AUX1.
              05 WSS-COD-TAWT                         PIC X(11).
              05 WSS-GLS-FILL                         PIC X(19).


       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGAVT" TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM CARGA-TAB-TAS.
           PERFORM PRO-TCF.
           PERFORM PRO-TSF.
           PERFORM DISP-ESTAD.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-TCF.
           PERFORM GAR-FIO-TSF.
           PERFORM GAR-FIO-GTP.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-TPF.
       FIN-OPEN-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-TCF.
           PERFORM GAR-FIO-TSF.
           PERFORM GAR-FIO-TPF.
           PERFORM GAR-FIO-GVT.
           PERFORM GAR-FIO-GTP.
       FIN-CLOSE-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       PRO-TCF SECTION.
       INI-PRO-TCF.
           PERFORM OBT-COD-PESO.
           PERFORM OBT-COD-UF.
           MOVE FIO-GET-FST  TO FIO-CMND.
       LUP-PRO-TCF.
           PERFORM GAR-FIO-TCF.
           IF NOT FIO-STAT-OKS OR FIO-STAT-EOF
               GO TO FIN-PRO-TCF.
           ADD 1 TO WSS-NUM-RLEE-TCF IN WSS-VARI.

           MOVE TCF-CAI-IGDG IN TCF TO GAR-NUM-SIS IN GTP.
           MOVE TCF-IIC-IGDG IN TCF TO GAR-COD-CRR IN GTP.
           MOVE ZEROES              TO GAR-COD-TSN IN GTP.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-GTP.
           IF FIO-STAT-OKS AND
              GAR-NUM-SIS IN GTP = TCF-CAI-IGDG IN TCF AND
              GAR-COD-CRR IN GTP = TCF-IIC-IGDG IN TCF
               PERFORM PRO-CON-GTP
               GO TO NXT-PRO-TCF.

           MOVE TCF-CAI-IGDG IN TCF TO GAR-NUM-SIS IN GVT.
           MOVE TCF-IIC-IGDG IN TCF TO GAR-COD-CRR IN GVT.
           MOVE TCF-NUM-IGVT IN TCF TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE GVT PARA : ' TCF-CIC-IGDG IN TCF
                                               TCF-NUM-IGVT IN TCF
               GO TO NXT-PRO-TCF.

           MOVE GAR-NUM-SIS IN GVT TO GAR-NUM-SIS IN GDG.
           MOVE GAR-COD-CRR IN GVT TO GAR-COD-CRR IN GDG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE GDG PARA GVT : ' GAR-CLV-GVT-1 IN GVT
               GO TO NXT-PRO-TCF.

           IF NOT (GAR-VCB IN GDG = WSS-COD-PESO IN WSS-VARI OR
                   GAR-VCB IN GDG = WSS-COD-UF   IN WSS-VARI)
               DISPLAY 'MONEDA DE GDG DISTINTA DE PESOS Y UF'
               DISPLAY 'GARANTIA : ' GAR-CLV-GDG-1 IN GDG
               DISPLAY 'MONEDA   : ' GAR-VCB IN GDG
               GO TO NXT-PRO-TCF.

           IF TCF-IND-VTAS IN TCF = 'T'
               PERFORM BUS-COD-TAS
               IF WSS-IND-ERRO IN WSS-VARI > SPACES
                  GO TO NXT-PRO-TCF.

           IF TCF-IND-VSUP IN TCF = 'T'
               PERFORM BUS-COD-SUP
               IF WSS-IND-ERRO IN WSS-VARI > SPACES
                  GO TO NXT-PRO-TCF.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL LEER UPD GVT : ' GAR-CLV-GVT-1 IN GVT
               GO TO NXT-PRO-TCF.

           PERFORM MOV-GVT-TCF.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL MODIFICAR GVT : ' GAR-CLV-GVT-1 IN GVT
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-GVTM IN WSS-VARI.

       NXT-PRO-TCF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-TCF.
       FIN-PRO-TCF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       MOV-GVT-TCF SECTION.
       INI-MOV-GVT-TCF.
           IF TCF-IND-VCOM IN TCF = 'T'
               IF GAR-VCB IN GDG = WSS-COD-PESO IN WSS-VARI
                   MOVE TCF-VAL-CMTP IN TCF TO GAR-VAL-CML-TSN IN GVT
                   COMPUTE GAR-VAL-AJT-SUP IN GVT ROUNDED =
                     GAR-VAL-CML-TSN IN GVT -
                   ( GAR-VAL-CML-TSN IN GVT *
                     GAR-PCT-AJT-SUP IN GVT / 100 )
                   MOVE GAR-VAL-AJT-SUP IN GVT TO
                     PES-SGV-DECI IN PES-VARI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE IN PES-VARI TO
                     GAR-VAL-AJT-SUP IN GVT
               ELSE
                   MOVE TCF-VAL-CMTU IN TCF TO GAR-VAL-CML-TSN IN GVT
                   COMPUTE GAR-VAL-AJT-SUP IN GVT ROUNDED =
                     GAR-VAL-CML-TSN IN GVT -
                   ( GAR-VAL-CML-TSN IN GVT *
                     GAR-PCT-AJT-SUP IN GVT / 100 ).

           IF TCF-IND-VLIQ IN TCF = 'T'
               IF GAR-VCB IN GDG = WSS-COD-PESO IN WSS-VARI
                   MOVE TCF-VAL-LQTP IN TCF TO GAR-VAL-LIQ-TSN IN GVT
                   COMPUTE GAR-VAL-PON IN GVT ROUNDED =
                     GAR-VAL-LIQ-TSN IN GVT * GAR-PCT-PON IN GVT / 100
                   MOVE GAR-VAL-PON  IN GVT TO PES-SGV-DECI IN PES-VARI
                   PERFORM PES-SCTV
                   MOVE PES-SGV-ENTE IN PES-VARI TO GAR-VAL-PON IN GVT
               ELSE
                   MOVE TCF-VAL-LQTU IN TCF TO GAR-VAL-LIQ-TSN IN GVT
                   COMPUTE GAR-VAL-PON IN GVT ROUNDED =
                     GAR-VAL-LIQ-TSN IN GVT * GAR-PCT-PON IN GVT / 100.

           IF TCF-IND-VUME IN TCF = 'T'
               MOVE TCF-COD-UMET IN TCF TO GAR-COD-UMD IN GVT.
           IF TCF-IND-VTCR IN TCF = 'T'
               MOVE TCF-GLS-TCRT IN TCF TO GAR-M2-CTR IN GVT.
           IF TCF-IND-VTTR IN TCF = 'T'
               MOVE TCF-GLS-TTRT IN TCF TO GAR-M2-TRR IN GVT.
           IF TCF-IND-VTAS IN TCF = 'T'
               MOVE WSS-COD-TASD IN WSS-VARI TO GAR-IIV-TSD IN GVT.
           IF TCF-IND-VSUP IN TCF = 'T'
               MOVE WSS-COD-SUPV IN WSS-VARI TO GAR-IIV-SVS IN GVT.
           IF TCF-IND-VFTS IN TCF = 'T'
               MOVE TCF-FEC-FTST IN TCF TO GAR-FEC-TSN IN GVT.
           MOVE 'GARPGVGV' TO GVT-IDN-USU-ACT IN GVT.
       FIN-MOV-GVT-TCF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       BUS-COD-TAS SECTION.
       INI-BUS-COD-TAS.
           MOVE SPACES TO WSS-IND-ERRO IN WSS-VARI.
           MOVE SPACES TO WSS-COD-TASD IN WSS-VARI.
      * Busca Codigo de Tasador en el Arreglo
           PERFORM CHK-COD-TAS VARYING WSS-J FROM 1 BY 1 UNTIL
             WSS-J > WSS-I OR
             TCF-COD-TAST IN TCF = WSS-COD-WEBT IN WSS-TAS(WSS-J).

      * Encuentra el Codigo de Tasador
           IF WSS-J NOT > WSS-I
              MOVE WSS-COD-CTAB IN WSS-TAS(WSS-J) TO
                                         WSS-COD-TASD IN WSS-VARI
               GO TO FIN-BUS-COD-TAS.

      * No Encuentra el Codigo de Tasador
           DISPLAY 'NO EXISTE TAS CON D.A. : ' TCF-COD-TAST IN TCF
                   ' GAR : ' TCF-CIC-IGDG IN TCF.
           MOVE 'S'  TO WSS-IND-ERRO IN WSS-VARI.

       FIN-BUS-COD-TAS.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       BUS-COD-SUP SECTION.
       INI-BUS-COD-SUP.
           MOVE SPACES TO WSS-IND-ERRO IN WSS-VARI.
           MOVE SPACES TO WSS-COD-SUPV IN WSS-VARI.
      * Busca Codigo de Supervisor en el Arreglo
           PERFORM CHK-COD-TAS VARYING WSS-J FROM 1 BY 1 UNTIL
             WSS-J > WSS-I OR
             TCF-COD-SUPT IN TCF = WSS-COD-WEBT IN WSS-TAS(WSS-J).

      * Encuentra el Codigo de Supervisor
           IF WSS-J NOT > WSS-I
              MOVE WSS-COD-CTAB IN WSS-TAS(WSS-J) TO
                                         WSS-COD-SUPV IN WSS-VARI
               GO TO FIN-BUS-COD-SUP.

      * No Encuentra el Codigo de Supervisor
           DISPLAY 'NO EXISTE TAS CON D.A. : ' TCF-COD-SUPT IN TCF
                   ' GAR : ' TCF-CIC-IGDG IN TCF.
           MOVE 'S'  TO WSS-IND-ERRO IN WSS-VARI.

       FIN-BUS-COD-SUP.
           EXIT.
      *
      *
      *---------------------------------------------------------------
      *
       PRO-TSF SECTION.
       INI-PRO-TSF.
           MOVE FIO-GET-FST  TO FIO-CMND.
       LUP-PRO-TSF.
           PERFORM GAR-FIO-TSF.
           IF NOT FIO-STAT-OKS OR FIO-STAT-EOF
               GO TO FIN-PRO-TSF.
           ADD 1 TO WSS-NUM-RLEE-TSF IN WSS-VARI.

           MOVE TSF-CAI-IGDG IN TSF TO GAR-NUM-SIS IN GVT.
           MOVE TSF-IIC-IGDG IN TSF TO GAR-COD-CRR IN GVT.
           MOVE TSF-NUM-IGVT IN TSF TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
                DISPLAY 'NO EXISTE GVT PARA : ' TSF-CIC-IGDG IN TSF
                                                TSF-NUM-IGVT IN TSF
                GO TO NXT-PRO-TSF.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL LEER UPD GVT : ' GAR-CLV-GVT-1 IN GVT
               GO TO NXT-PRO-TSF.

           PERFORM MOV-GVT-TSF.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL MODIFICAR GVT : ' GAR-CLV-GVT-1 IN GVT
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-GVTM IN WSS-VARI.

       NXT-PRO-TSF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-TSF.
       FIN-PRO-TSF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       MOV-GVT-TSF SECTION.
       INI-MOV-GVT-TSF.
           MOVE 'GARPGVGV' TO GVT-IDN-USU-ACT IN GVT.
       FIN-MOV-GVT-TSF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       DISP-ESTAD SECTION.
       INI-DISP-ESTAD.
           DISPLAY 'REG. LEIDOS TCF   : ' WSS-NUM-RLEE-TCF IN WSS-VARI.
           DISPLAY 'REG. LEIDOS TSF   : ' WSS-NUM-RLEE-TSF IN WSS-VARI.
           DISPLAY 'REG. GVT MODIF.   : ' WSS-NUM-GVTM IN WSS-VARI.
           DISPLAY 'REG. GTP GRABADOS : ' WSS-NUM-GRA-TPF IN WSS-VARI.
           DISPLAY 'ERRORES AL MODIF. : ' WSS-NUM-RERR IN WSS-VARI.
       FIN-DISP-ESTAD.
           EXIT.
      *
      *---------------------------------------------------------------
      *
      * Se obtiene codigo de pesos
       OBT-COD-PESO SECTION.
       INI-OBT-COD-PESO.
           MOVE SPACES                    TO WSS-COD-PESO IN WSS-VARI.
           MOVE 'TAB'                     TO FIO-SIST.
           MOVE 'VLR'                     TO TAB-CIC-TTAB IN TAB.
           MOVE '0999  '                  TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL'            TO FIO-AKEY.
           MOVE  FIO-GET-KEY              TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR TAB-IND-VIGE IN TAB = 'N'
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE 'CICMNACINEX'        TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG '  0999  '
               PERFORM GNS-PRO-ABT
               GO TO FIN-OBT-COD-PESO.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-PESO IN WSS-VARI.
       FIN-OBT-COD-PESO.
           EXIT.
      *
      *---------------------------------------------------------------
      *
      * Se obtiene codigo de UF
       OBT-COD-UF SECTION.
       INI-OBT-COD-UF.
           MOVE SPACES                    TO WSS-COD-UF IN WSS-VARI.
           MOVE 'TAB'                     TO FIO-SIST.
           MOVE 'VLR'                     TO TAB-CIC-TTAB IN TAB.
           MOVE '0998  '                  TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL'            TO FIO-AKEY.
           MOVE  FIO-GET-KEY              TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS OR TAB-IND-VIGE IN TAB = 'N'
               MOVE 'COL'                TO MSG-COD-SIST
               MOVE 'CICMNACINEX'        TO MSG-COD-MENS IN MSG
               PERFORM GET-MSG
               DISPLAY MSG-GLS-DESC IN MSG '  0999  '
               PERFORM GNS-PRO-ABT
               GO TO FIN-OBT-COD-UF.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-UF IN WSS-VARI.
       FIN-OBT-COD-UF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       PRO-CON-GTP SECTION.
       INI-PRO-CON-GTP.
           MOVE SPACES TO TPF-GLS-DISP IN TPF.
           MOVE TCF-CIC-IGDG IN TCF TO TPF-CIC-IGDG IN TPF.
           MOVE TCF-NUM-IGVT IN TCF TO TPF-NUM-IGVT IN TPF.
           MOVE TCF-IND-VCOM IN TCF TO TPF-IND-VCOM IN TPF.
           MOVE TCF-IND-VLIQ IN TCF TO TPF-IND-VLIQ IN TPF.
           MOVE TCF-IND-VUME IN TCF TO TPF-IND-VUME IN TPF.
           MOVE TCF-IND-VTCR IN TCF TO TPF-IND-VTCR IN TPF.
           MOVE TCF-IND-VTTR IN TCF TO TPF-IND-VTTR IN TPF.
           MOVE TCF-IND-VTAS IN TCF TO TPF-IND-VTAS IN TPF.
           MOVE TCF-IND-VSUP IN TCF TO TPF-IND-VSUP IN TPF.
           MOVE TCF-IND-VFTS IN TCF TO TPF-IND-VFTS IN TPF.
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-TPF.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL GRABAR TPF : ' TCF-CIC-IGDG IN TCF
           ELSE
               ADD 1 TO WSS-NUM-GRA-TPF IN WSS-VARI.

           MOVE TCF-CAI-IGDG IN TCF TO GAR-NUM-SIS IN GVT.
           MOVE TCF-IIC-IGDG IN TCF TO GAR-COD-CRR IN GVT.
           MOVE TCF-NUM-IGVT IN TCF TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE GVT PARA : ' TCF-CIC-IGDG IN TCF
                                               TCF-NUM-IGVT IN TCF
               GO TO FIN-PRO-CON-GTP.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL LEER UPD GVT : ' GAR-CLV-GVT-1 IN GVT
               GO TO FIN-PRO-CON-GTP.
           MOVE 'GARPGVGV' TO GVT-IDN-USU-ACT IN GVT.
           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL MODIFICAR GVT : ' GAR-CLV-GVT-1 IN GVT
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-GVTM IN WSS-VARI.

       FIN-PRO-CON-GTP.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       CARGA-TAB-TAS SECTION.
       INI-CARGA-TAB-TAS.
           MOVE ZEROES       TO WSS-I.
           MOVE 'TAB'        TO FIO-SIST.
           MOVE 'TAS'        TO TAB-COD-TTAB IN TAB.
           MOVE SPACES       TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       LUP-CARGA-TAB-TAS.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'TAS' )
               GO TO FIN-CARGA-TAB-TAS.

           IF TAB-COD-CTAB IN TAB NOT > SPACES
               GO TO NXT-CARGA-TAB-TAS.

           ADD 1 TO WSS-I.
           MOVE TAB-COD-CTAB IN TAB TO WSS-COD-CTAB IN WSS-TAS(WSS-I).
           MOVE TAB-GLS-DAT3 IN TAB TO WSS-GLS-AUX1 IN WSS-VARI.
           MOVE WSS-COD-TAWT IN WSS-VARI
                                    TO WSS-COD-WEBT IN WSS-TAS(WSS-I).

       NXT-CARGA-TAB-TAS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-CARGA-TAB-TAS.

       FIN-CARGA-TAB-TAS.
           EXIT.

       DSP-TAB-TAS SECTION.
       INI-DSP-TAB-TAS.
           MOVE 1 TO WSS-M.
       LUP-DSP-TAB-TAS.
           DISPLAY 'POSICION : ' WSS-M
           DISPLAY 'TAS : ' WSS-COD-CTAB IN WSS-TAS(WSS-M).
           DISPLAY 'WET : ' WSS-COD-WEBT IN WSS-TAS(WSS-M).
           ADD 1 TO WSS-M.
           IF WSS-M < WSS-I
               GO TO LUP-DSP-TAB-TAS.
       FIN-DSP-TAB-TAS.
           EXIT.

       CHK-COD-TAS SECTION.
       INI-CHK-COD-TAS.
       FIN-CHK-COD-TAS.
           EXIT.

       COPY GARBFTCF.
       COPY GARBFTSF.
       COPY GARBFTPF.
       COPY GARBFGVT.
       COPY GARBFGDG.
       COPY GARBFGTP.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY GNSBGEND.
       COPY GNSBGHOY.
       COPY GNSBGFEC.
       COPY GNSBGPES.
       COPY GNSBPFEC.
       COPY GNSBBSYS.
