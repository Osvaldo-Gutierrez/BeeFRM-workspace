       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGASE.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    07-JUL-2003.

      *ACTUALIZA GSE CON VALORES DE SEGUROS

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GARBRSCF.
       COPY GARBRSSF.
      *
      * Registros de las Bases Originales.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GARBRGSE.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GSE-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGFEC.
       COPY GNSWGPES.
       COPY GNSWGHOY.
      *
       01  WSS-VARI.
           03 WSS-IND-PROC            VALUE SPACES       PIC X(01).
           03 WSS-NUM-RLEE-SCF        VALUE ZEROES       PIC 9(05).
           03 WSS-NUM-RLEE-SSF        VALUE ZEROES       PIC 9(05).
           03 WSS-NUM-RERR            VALUE ZEROES       PIC 9(05).
           03 WSS-IND-ERRO            VALUE SPACES       PIC X(01).
           03 WSS-NUM-GSEM            VALUE ZEROES       PIC 9(05).
           03 WSS-COD-CCSE            VALUE SPACES       PIC X(09).
           03 WSS-COD-VCOC            VALUE SPACES       PIC X(06).
           03 WSS-I                   VALUE ZEROES       PIC 9(04).
           03 WSS-J                   VALUE ZEROES       PIC 9(04).
           03 WSS-X                   VALUE ZEROES       PIC 9(04).
           03 WSS-Y                   VALUE ZEROES       PIC 9(04).
           03 WSS-M                   VALUE ZEROES       PIC 9(04).


      *    Vector de Codigos de Tabla CSE
           03 WSS-CSE.
              05 WSS-VEC-CSE OCCURS 1000.
                 07 WSS-COD-CSEG                      PIC X(09).
                 07 WSS-COD-CSES                      PIC X(09).

           03 WSS-GLS-AUXI.
              05 WSS-GLS-CSEG                         PIC X(09).
              05 WSS-GLS-FILL                         PIC X(21).

           03 WSS-GLS-DOCE.
              05 WSS-COD-ASEG                         PIC X(09).
              05 WSS-GLS-FILL                         PIC X(03).

      *    Vector de Codigos de Tabla VLR
           03 WSS-VLR.
              05 WSS-VEC-VLR OCCURS 1000.
                 07 WSS-COD-VLRG                      PIC X(06).
                 07 WSS-COD-VLRS                      PIC X(04).

           03 WSS-GLS-AUX1.
              05 WSS-GLS-FIL1                         PIC X(20).
              05 WSS-COD-MONS                         PIC X(04).
              05 WSS-GLS-FIL2                         PIC X(06).

           03 WSS-GLS-AUX2.
              05 WSS-COD-MONG                         PIC X(06).
              05 WSS-GLS-FILL                         PIC X(06).


       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGASE" TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM CAPTURA-PARAMETROS.
           PERFORM VALIDA-PARAMETROS.
           IF WSS-IND-PROC IN WSS-VARI = 'S'
               GO TO FIN-MAIN.
           PERFORM OPEN-FILE.
           PERFORM CARGA-TAB-CSE.
           PERFORM CARGA-TAB-VLR.
           PERFORM PRO-SCF.
           PERFORM PRO-SSF.
           PERFORM DISP-ESTAD.
           PERFORM CLOSE-FILE.
       FIN-MAIN.
           PERFORM GNS-PRO-END.
      *
      *---------------------------------------------------------------
      *
       CAPTURA-PARAMETROS SECTION.
       INI-CAPTURA-PARAMETROS.
           ACCEPT WSS-IND-PROC IN WSS-VARI.
           DISPLAY 'TIPO DE EJECUCION : ' WSS-IND-PROC IN WSS-VARI.
       FIN-CAPTURA-PARAMETROS.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       VALIDA-PARAMETROS SECTION.
       INI-VALIDA-PARAMETROS.
           IF WSS-IND-PROC IN WSS-VARI NOT = 'S' AND
              WSS-IND-PROC IN WSS-VARI NOT = 'T'
               DISPLAY 'TIPO DE EJECUCION INVALIDO'
               PERFORM GNS-PRO-ABT.
       FIN-VALIDA-PARAMETROS.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-SCF.
           PERFORM GAR-FIO-SSF.
           MOVE FIO-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
       FIN-OPEN-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-SCF.
           PERFORM GAR-FIO-SSF.
           PERFORM GAR-FIO-GSE.
       FIN-CLOSE-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       PRO-SCF SECTION.
       INI-PRO-SCF.
           MOVE FIO-GET-FST  TO FIO-CMND.
       LUP-PRO-SCF.
           PERFORM GAR-FIO-SCF.
           IF NOT FIO-STAT-OKS OR FIO-STAT-EOF
               GO TO FIN-PRO-SCF.
           ADD 1 TO WSS-NUM-RLEE-SCF IN WSS-VARI.

           MOVE SCF-CAI-IGDG IN SCF TO GAR-NUM-SIS IN GSE.
           MOVE SCF-IIC-IGDG IN SCF TO GAR-COD-CRR IN GSE.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
           IF NOT FIO-STAT-OKS
               DISPLAY 'NO EXISTE GSE PARA : ' SCF-CIC-IGDG IN SCF
               GO TO NXT-PRO-SCF.

           IF SCF-IND-VCSE IN SCF = 'S'
               PERFORM BUS-COD-CSE
               IF WSS-IND-ERRO IN WSS-VARI > SPACES
                  GO TO NXT-PRO-SCF.

           IF SCF-IND-VMON IN SCF = 'S'
               PERFORM BUS-COD-VLR
               IF WSS-IND-ERRO IN WSS-VARI > SPACES
                  GO TO NXT-PRO-SCF.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL LEER UPD GSE : ' GAR-CLV-GSE-1 IN GSE
               GO TO NXT-PRO-SCF.

           PERFORM MOV-GSE-SCF.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL MODIFICAR GSE : ' GAR-CLV-GSE-1 IN GSE
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-GSEM IN WSS-VARI.

       NXT-PRO-SCF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-SCF.
       FIN-PRO-SCF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       MOV-GSE-SCF SECTION.
       INI-MOV-GSE-SCF.
           IF SCF-IND-VPLZ IN SCF = 'S'
               MOVE SCF-COD-PLZS IN SCF TO GAR-COD-PLZ IN GSE.
           IF SCF-IND-VSEG IN SCF = 'S'
               MOVE SCF-VAL-SEGS IN SCF TO GAR-VAL-SEG IN GSE.
           IF SCF-IND-VFIV IN SCF = 'S'
               MOVE SCF-FEC-FIVS IN SCF TO GAR-FEC-VIG-SEG IN GSE.
           IF SCF-IND-VFVE IN SCF = 'S'
               MOVE SCF-FEC-FVES IN SCF TO GAR-FEC-VTO-SEG IN GSE.
           IF SCF-IND-VCSE IN SCF = 'S'
               MOVE WSS-COD-CCSE IN WSS-VARI TO GAR-IIV-CIA-SEG IN GSE.
           IF SCF-IND-VSED IN SCF = 'S'
               MOVE SCF-IND-SEDS IN SCF TO GAR-IND-EDS-SEG IN GSE.
           IF SCF-IND-VMON IN SCF = 'S'
               MOVE WSS-COD-VCOC IN WSS-VARI TO GAR-VCB-SEG IN GSE.

           MOVE 'GARPGVGV' TO GSE-IDN-USU-ACT IN GSE.
       FIN-MOV-GSE-SCF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       BUS-COD-CSE SECTION.
       INI-BUS-COD-SCE.
           MOVE SPACES TO WSS-IND-ERRO IN WSS-VARI.
      * Busca Codigo de Compania de Seguro en el Arreglo
           PERFORM CHK-COD-CSE VARYING WSS-J FROM 1 BY 1 UNTIL
             WSS-J > WSS-I OR
             SCF-COD-CSES IN SCF = WSS-COD-CSES IN WSS-CSE(WSS-J).

      * Encuentra el Codigo Compania de Seguros
           IF WSS-J NOT > WSS-I
              MOVE WSS-COD-CSEG IN WSS-CSE(WSS-J) TO
                                         WSS-COD-CCSE IN WSS-VARI
               GO TO FIN-BUS-COD-CSE.

      * No Encuentra el Codigo de Compania de Seguros
           DISPLAY 'NO EXISTE CSE CON D.A. : ' SCF-COD-CSES IN SCF
                   ' GAR : ' SCF-CIC-IGDG IN SCF.
           MOVE 'S'  TO WSS-IND-ERRO IN WSS-VARI.

       FIN-BUS-COD-CSE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       BUS-COD-VLR SECTION.
       INI-BUS-COD-VLR.
           MOVE SPACES TO WSS-IND-ERRO IN WSS-VARI.
      * Busca Codigo de Moneda en el Arreglo
           PERFORM CHK-COD-VLR VARYING WSS-Y FROM 1 BY 1 UNTIL
             WSS-Y > WSS-X OR
             SCF-COD-MONS IN SCF = WSS-COD-VLRS IN WSS-VLR(WSS-Y).

      * Encuentra el Codigo de Moneda
           IF WSS-Y NOT > WSS-X
              MOVE WSS-COD-VLRG IN WSS-VLR(WSS-Y) TO
                                         WSS-COD-VCOC IN WSS-VARI
               GO TO FIN-BUS-COD-VLR.

      * No Encuentra el Codigo de Moneda
           DISPLAY 'NO EXISTE VLR CON D.A. : ' SCF-COD-MONS IN SCF
                   ' GAR : ' SCF-CIC-IGDG IN SCF.
           MOVE 'S'  TO WSS-IND-ERRO IN WSS-VARI.

       FIN-BUS-COD-VLR.
           EXIT.
      *
      *
      *---------------------------------------------------------------
      *
       PRO-SSF SECTION.
       INI-PRO-SSF.
           MOVE FIO-GET-FST  TO FIO-CMND.
       LUP-PRO-SSF.
           PERFORM GAR-FIO-SSF.
           IF NOT FIO-STAT-OKS OR FIO-STAT-EOF
               GO TO FIN-PRO-SSF.
           ADD 1 TO WSS-NUM-RLEE-SSF IN WSS-VARI.

           MOVE SSF-CAI-IGDG IN SSF TO GAR-NUM-SIS IN GSE.
           MOVE SSF-IIC-IGDG IN SSF TO GAR-COD-CRR IN GSE.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
           IF NOT FIO-STAT-OKS
                DISPLAY 'NO EXISTE GSE PARA : ' SSF-CIC-IGDG IN SSF
                GO TO NXT-PRO-SSF.

           MOVE FIO-GET-KEY-UPD TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL LEER UPD GSE : ' GAR-CLV-GSE-1 IN GSE
               GO TO NXT-PRO-SSF.

           PERFORM MOV-GSE-SSF.

           MOVE FIO-MOD TO FIO-CMND.
           PERFORM GAR-FIO-GSE.
           IF NOT FIO-STAT-OKS
               DISPLAY 'ERROR AL MODIFICAR GSE : ' GAR-CLV-GSE-1 IN GSE
               ADD 1 TO WSS-NUM-RERR IN WSS-VARI
           ELSE
               ADD 1 TO WSS-NUM-GSEM IN WSS-VARI.

       NXT-PRO-SSF.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-PRO-SSF.
       FIN-PRO-SSF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       MOV-GSE-SSF SECTION.
       INI-MOV-GSE-SSF.
           MOVE 'GARPGVGV' TO GSE-IDN-USU-ACT IN GSE.
       FIN-MOV-GSE-SSF.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       DISP-ESTAD SECTION.
       INI-DISP-ESTAD.
           DISPLAY 'REG. LEIDOS SCF   : ' WSS-NUM-RLEE-SCF IN WSS-VARI.
           DISPLAY 'REG. LEIDOS SSF   : ' WSS-NUM-RLEE-SSF IN WSS-VARI.
           DISPLAY 'REG. GSE MODIF.   : ' WSS-NUM-GSEM IN WSS-VARI.
           DISPLAY 'ERRORES AL MODIF. : ' WSS-NUM-RERR IN WSS-VARI.
       FIN-DISP-ESTAD.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       CARGA-TAB-VLR SECTION.
       INI-CARGA-TAB-VLR.
           MOVE ZEROES       TO WSS-X.
           MOVE 'TAB'        TO FIO-SIST.
           MOVE 'VLR'        TO TAB-COD-TTAB IN TAB.
           MOVE SPACES       TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       LUP-CARGA-TAB-VLR.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'VLR' )
               GO TO FIN-CARGA-TAB-VLR.

           IF TAB-COD-CTAB IN TAB NOT > SPACES
               GO TO NXT-CARGA-TAB-VLR.

           ADD 1 TO WSS-X.
           MOVE TAB-COD-CTAB IN TAB TO WSS-GLS-AUX2 IN WSS-VARI.
           MOVE WSS-COD-MONG IN WSS-VARI TO
                                       WSS-COD-VLRG IN WSS-VLR(WSS-X).
           MOVE TAB-GLS-DAT3 IN TAB TO WSS-GLS-AUX1 IN WSS-VARI.
           MOVE WSS-COD-MONS IN WSS-VARI TO
                                       WSS-COD-VLRS IN WSS-VLR(WSS-X).

       NXT-CARGA-TAB-VLR.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-CARGA-TAB-VLR.

       FIN-CARGA-TAB-VLR.
           EXIT.

       CHK-COD-VLR SECTION.
       INI-CHK-COD-VLR.
       FIN-CHK-COD-VLR.
           EXIT.

       CARGA-TAB-CSE SECTION.
       INI-CARGA-TAB-CSE.
           MOVE ZEROES       TO WSS-I.
           MOVE 'TAB'        TO FIO-SIST.
           MOVE 'CSE'        TO TAB-COD-TTAB IN TAB.
           MOVE SPACES       TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-NLS  TO FIO-CMND.
       LUP-CARGA-TAB-CSE.
           PERFORM GNS-FIO-TAB.
           IF NOT ( FIO-STAT-OKS AND
              TAB-COD-TTAB IN TAB = 'CSE' )
               GO TO FIN-CARGA-TAB-CSE.

           IF TAB-COD-CTAB IN TAB NOT > SPACES
               GO TO NXT-CARGA-TAB-CSE.

           ADD 1 TO WSS-I.
           MOVE TAB-COD-CTAB IN TAB TO WSS-GLS-DOCE IN WSS-VARI.
           MOVE WSS-COD-ASEG IN WSS-VARI TO
                                       WSS-COD-CSEG IN WSS-CSE(WSS-I).
           MOVE TAB-GLS-DAT3 IN TAB TO WSS-GLS-AUXI IN WSS-VARI.
           MOVE WSS-GLS-CSEG IN WSS-VARI TO
                                       WSS-COD-CSES IN WSS-CSE(WSS-I).

       NXT-CARGA-TAB-CSE.
           MOVE FIO-GET-NXT TO FIO-CMND.
           GO TO LUP-CARGA-TAB-CSE.

       FIN-CARGA-TAB-CSE.
           EXIT.

       CHK-COD-CSE SECTION.
       INI-CHK-COD-CSE.
       FIN-CHK-COD-CSE.
           EXIT.

       DSP-TAB-VLR SECTION.
       INI-DSP-TAB-VLR.
           MOVE 1 TO WSS-M.
       LUP-DSP-TAB-VLR.
           IF WSS-COD-VLRG IN WSS-VLR(WSS-M) NOT > SPACES
               GO TO FIN-DSP-TAB-VLR.
           DISPLAY WSS-COD-VLRG IN WSS-VLR(WSS-M).
           DISPLAY WSS-COD-VLRS IN WSS-VLR(WSS-M).
           ADD 1 TO WSS-M.
           GO TO LUP-DSP-TAB-VLR.
       FIN-DSP-TAB-VLR.
           EXIT.

       COPY GARBFSCF.
       COPY GARBFSSF.
       COPY GARBFGSE.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY GNSBGEND.
       COPY GNSBGHOY.
       COPY GNSBGFEC.
       COPY GNSBGPES.
       COPY GNSBPFEC.
       COPY GNSBBSYS.
