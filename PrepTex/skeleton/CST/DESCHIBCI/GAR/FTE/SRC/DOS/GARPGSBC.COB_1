       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGSBC.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    02-JUL-2003.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      * Archivos de Entrada.
       COPY GARBRGDS.
       COPY GARBRGSS.
       COPY GARBRDDS.
      * Archivos de Salida.
       COPY GARBRSBG.
       COPY GARBRTCL.
      *
      * Registros de las Bases Originales.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *
       01  WS-VARIABLES.
           02 WS-EOF-GDS           PIC 9(01) VALUE ZEROS.
           02 WS-LEE-GSS           PIC 9(01) VALUE ZEROS.
           02 WS-LEE-DDS           PIC 9(01) VALUE ZEROS.
           02 WS-FIN-GSS           PIC 9(01) VALUE ZEROS.
           02 WS-FIN-DDS           PIC 9(01) VALUE ZEROS.
           02 WS-CON-SBG           PIC 9(09) VALUE ZEROS.
           02 WS-CON-TCL           PIC 9(09) VALUE ZEROS.
           02 WSA-CIC-ICLI         PIC X(12) VALUE SPACES.

       01  WSS-VARI.
           03 WSS-GLS-DESC.
              05 WSS-GLS-DES1     VALUE SPACES          PIC X(40).
              05 WSS-GLS-DES2     VALUE SPACES          PIC X(40).

           03 WSS-FEC-FPRO.
              05 WSS-NUM-SPRO                           PIC 9(02).
              05 WSS-NUM-APRO                           PIC 9(02).
              05 WSS-NUM-MPRO                           PIC 9(02).
              05 WSS-NUM-DPRO                           PIC 9(02).

           03 WSS-DMA-FPRO.
              05 WSS-DMA-DPRO                           PIC 9(02).
              05 WSS-DMA-MPRO                           PIC 9(02).
              05 WSS-DMA-SPRO                           PIC 9(02).
              05 WSS-DMA-APRO                           PIC 9(02).

           03 WSS-IND-GDGP        VALUE SPACES          PIC X(01).
           03 WSS-GLS-TIPO.
              05 WSS-TIP-GTIA     VALUE SPACES          PIC X(03).
              05 WSS-STP-GTIA     VALUE SPACES          PIC X(03).



       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGSBC" TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM INICIAL-VARIABLES.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-GAR-GDS.
           PERFORM PROCESA-GAR UNTIL WS-EOF-GDS = 1.
           PERFORM MUESTRA-ESTADISTICA.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE 'GAR'   TO FIO-SIST.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GSS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-DDS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-SBG.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-TCL.
       FIN-OPEN-FILE.
           EXIT.

       INICIAL-VARIABLES.
       INI-INIC-VAR.
           MOVE ZEROS  TO WS-EOF-GDS.
           MOVE ZEROS  TO WS-LEE-GSS.
           MOVE ZEROS  TO WS-LEE-DDS.
           MOVE ZEROS  TO WS-CON-SBG.
           MOVE ZEROS  TO WS-CON-TCL.
           MOVE SPACES TO WSA-CIC-ICLI.
       FIN-INIC-VAR.
           EXIT.

      *
      *---------------------------------------------------------------
      *
       LEE-GAR-GDS SECTION.
       LUP-PRO-GDS.
           IF NOT FIO-STAT-EOF
              PERFORM GAR-FIO-GDS
           ELSE
              MOVE 1 TO WS-EOF-GDS.
       FIN-LEE-GAR-GDS.
           EXIT.

       PROCESA-GAR SECTION.
       INI-PROCESA-GAR.
           IF GAR-ES1     IN GDS = "10" AND
              GAR-IND-CPL IN GDS = 'S'
              MOVE ZEROS TO WS-FIN-GSS
              PERFORM BUSCA-SEGURO UNTIL WS-FIN-GSS = 1.
           MOVE FIO-GET-NXT  TO FIO-CMND.
           PERFORM LEE-GAR-GDS.
       FIN-PROCESA-GAR.
           EXIT.

       BUSCA-SEGURO SECTION.
       INI-BUS-SEGURO.
           IF WS-LEE-GSS = ZEROS THEN
              MOVE FIO-GET-FST    TO FIO-CMND
              PERFORM LEE-GAR-GSS
              MOVE 1   TO WS-LEE-GSS.
           IF GAR-CLV-GDG-1 IN GDS = GAR-CLV-GSE-1 IN GSS THEN
              PERFORM VALIDA-SEGURO
              MOVE 1 TO WS-FIN-GSS
           ELSE
              IF GAR-CLV-GDG-1 IN GDS > GAR-CLV-GSE-1 IN GSS THEN
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-GAR-GSS
              ELSE
                 MOVE 1 TO WS-FIN-GSS.
       FIN-BUS-SEGURO.
           EXIT.

       LEE-GAR-GSS SECTION.
       INI-LEE-GSS.
           PERFORM GAR-FIO-GSS.
       FIN-LEE-GSS.
           EXIT.

       VALIDA-SEGURO SECTION.
       INI-VAL-SEGURO.
           IF GSE-IDN-USU-ACT IN GSS NOT = 'GARPGVGV' AND
              GAR-COD-RPB-SEG IN GSS = '1'
              PERFORM GRABA-GARSBG
              MOVE ZEROS TO WS-FIN-DDS
              PERFORM BUSCA-CLIENTE UNTIL WS-FIN-DDS = 1.
       FIN-VAL-SEGURO.
           EXIT.

       GRABA-GARSBG SECTION.
       INI-GRAB-SBG.
           MOVE SPACES     TO SBG-NUM-SIS.
           MOVE SPACES     TO SBG-COD-CRR.
           MOVE SPACES     TO SBG-COD-PLZ.
           MOVE ZEROS      TO SBG-VAL-SEG.
           MOVE SPACES     TO SBG-VCB-SEG.
           MOVE SPACES     TO SBG-NOM-MON.
           MOVE ZEROS      TO SBG-FEC-INI-VIG.
           MOVE ZEROS      TO SBG-FEC-VTO-SEG.
           MOVE SPACES     TO SBG-CIA-SEG.
           MOVE SPACES     TO SBG-NOM-CIA-SEG.
           MOVE SPACES     TO SBG-IND-SEG-END.
           MOVE SPACES     TO SBG-IND-VEG.
      *
           MOVE GAR-NUM-SIS IN GDS     TO SBG-NUM-SIS.
           MOVE GAR-COD-CRR IN GDS     TO SBG-COD-CRR.
           MOVE GAR-COD-PLZ IN GSS     TO SBG-COD-PLZ.
           MOVE GAR-VAL-SEG IN GSS     TO SBG-VAL-SEG.
           MOVE GAR-VCB-SEG IN GSS     TO SBG-VCB-SEG.
           PERFORM EXTRAE-MONEDA.
           MOVE GAR-FEC-VIG-SEG IN GSS TO SBG-FEC-INI-VIG.
           MOVE GAR-FEC-VTO-SEG IN GSS TO SBG-FEC-VTO-SEG.
           MOVE GAR-IIV-CIA-SEG IN GSS TO SBG-CIA-SEG.
           PERFORM EXTRAE-CIASEG.
           MOVE GAR-IND-EDS-SEG IN GSS TO SBG-IND-SEG-END.
           MOVE 'N'                    TO SBG-IND-VEG.
           MOVE FIO-PUT                TO FIO-CMND.
           PERFORM GAR-FIO-SBG.
           ADD 1 TO WS-CON-SBG.
       FIN-GRAB-SBG.
           EXIT.

       EXTRAE-MONEDA SECTION.
       INI-EXT-MON.
           IF GAR-VCB-SEG IN GSS > SPACES
              MOVE 'TAB'              TO FIO-SIST
              MOVE 'TAB'              TO TAB-COD-SIST
              MOVE 'VLR '             TO TAB-COD-TTAB IN TAB
              MOVE GAR-VCB-SEG IN GSS TO TAB-COD-CTAB IN TAB
              MOVE FIO-GET-KEY        TO FIO-CMND
              PERFORM BUS-TAB
              IF NOT FIO-STAT-OKS OR TAB-IND-VIGE IN TAB = 'N'
                 DISPLAY 'COD. MONEDA NO VALIDO : '
                                         GAR-VCB-SEG IN GSS
                 MOVE SPACES              TO SBG-NOM-MON
              ELSE
                 MOVE TAB-GLS-DESC IN TAB TO SBG-NOM-MON.
       INI-EXT-MON.
           EXIT.
      *
       EXTRAE-CIASEG SECTION.
       INI-EXT-CIA.
           IF GAR-IIV-CIA-SEG IN GSS > SPACES
              MOVE 'TAB'                  TO FIO-SIST
              MOVE 'TAB'                  TO TAB-COD-SIST
              MOVE 'CSE '                 TO TAB-COD-TTAB IN TAB
              MOVE GAR-IIV-CIA-SEG IN GSS TO TAB-COD-CTAB IN TAB
              MOVE FIO-GET-KEY        TO FIO-CMND
              PERFORM BUS-TAB
              IF NOT FIO-STAT-OKS OR TAB-IND-VIGE IN TAB = 'N'
                 DISPLAY 'COD. CIA. SEGURO NO VALIDO : '
                            GAR-IIV-CIA-SEG IN GSS
                 MOVE SPACES              TO SBG-NOM-MON
              ELSE
                 MOVE TAB-GLS-DESC IN TAB TO SBG-NOM-CIA-SEG.
       INI-EXT-CIA.
           EXIT.

       BUSCA-CLIENTE SECTION.
       INI-BUS-CLTE.
           IF WS-LEE-DDS = ZEROS THEN
              MOVE FIO-GET-FST    TO FIO-CMND
              PERFORM LEE-GAR-DDS
              MOVE 1   TO WS-LEE-DDS.
           IF GAR-CLV-GDG-1 IN GDS = GAR-CLV-GDD-2 IN DDS THEN
              PERFORM GRABA-GARTCL UNTIL GAR-CLV-GDG-1 IN GDS
                                   NOT = GAR-CLV-GDD-2 IN DDS
              MOVE 1 TO WS-FIN-DDS
           ELSE
              IF GAR-CLV-GDG-1 IN GDS > GAR-CLV-GDD-2 IN DDS THEN
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-GAR-DDS
              ELSE
                 DISPLAY 'GARANTIA SIN DEUDOR ' GAR-CLV-GDG-1 IN GDS
                 MOVE 1 TO WS-FIN-DDS.
       FIN-BUS-CLTE.
           EXIT.

       LEE-GAR-DDS SECTION.
       INI-LEE-DDS.
           PERFORM GAR-FIO-DDS.
       FIN-LEE-DDS.
           EXIT.

       GRABA-GARTCL SECTION.
       INI-GRAB-TCL.
           MOVE SPACES TO TCL-GDG-NUM-SIS.
           MOVE SPACES TO TCL-GDG-COD-CRR.
           MOVE SPACES TO TCL-GDD-CAI-ICLI.
           MOVE SPACES TO TCL-GDD-ICC-ICLI.
      *
           MOVE GAR-NUM-SIS  IN GDS  TO TCL-GDG-NUM-SIS.
           MOVE GAR-COD-CRR  IN GDS  TO TCL-GDG-COD-CRR.
           MOVE GDD-CAI-ICLI IN DDS  TO TCL-GDD-CAI-ICLI.
           MOVE GDD-IIC-ICLI IN DDS  TO TCL-GDD-ICC-ICLI.
           MOVE FIO-PUT   TO FIO-CMND.
           PERFORM GAR-FIO-TCL.
           ADD 1 TO WS-CON-TCL.
           MOVE FIO-GET-NXT  TO FIO-CMND.
           PERFORM LEE-GAR-DDS.
       FIN-GRAB-TCL.
           EXIT.
      *
       MUESTRA-ESTADISTICA SECTION.
       INI-MUE-EST.
           DISPLAY 'REGISTROS GRABADOS SBG ' WS-CON-SBG.
           DISPLAY 'REGISTROS GRABADOS TCL ' WS-CON-TCL.
       INI-MUE-EST.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-GDS.
           PERFORM GAR-FIO-GSS.
           PERFORM GAR-FIO-DDS.
           PERFORM GAR-FIO-SBG.
           PERFORM GAR-FIO-TCL.
       FIN-CLOSE-FILE.
           EXIT.
      *
       COPY GARBFGDS.
       COPY GARBFGSS.
       COPY GARBFDDS.
       COPY GARBFSBG.
       COPY GARBFTCL.

       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBTABT.
       COPY GNSBGABT.
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY GNSBGEND.
      *
       COPY GNSBGHOY.
       COPY GNSBGFEC.
       COPY GNSBPFEC.
       COPY GNSBBSYS.
