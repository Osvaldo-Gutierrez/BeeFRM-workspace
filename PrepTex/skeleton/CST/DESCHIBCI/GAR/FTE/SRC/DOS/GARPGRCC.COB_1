       IDENTIFICATION DIVISION.
      *........................
       PROGRAM-ID.    GARPGRCC.
       AUTHOR.        GERARDO URETA Q.
       DATE-WRITTEN.  MAY,10 1988.
       DATE-COMPILED.

      ****************************************************************
      * COPIA DE SUBRUTINA GARPGRGA
      * SUBRUTINA QUE OBTIENE UN RESUMEN DE LAS GARANTIAS DE UN DEUDOR
      *
      * ESTAS SON DEVUELTAS EN UN REGISTRO CON 100 OCURRENCIAS
      *
      * MODIFICACIONES:
      * - SE ENTREGAN CONVERTIDOS A PESOS LOS MONTOS :
      *   VALOR COMERCIAL, VALOR LIQUIDACION Y VALOR AJUSTADO SUPER.
      *   ===  SENC 05/05/89  ===
      *
      * - SE CAMBIA CURRENT-DATE POR ACCESO A TABLA TAB
      *   ===  CEHC 17/10/90  ===
      *
      * - SE CAMBIA A PROGRAMA COBOL CICS PARA SER INVOCADO VIA LINK
      *   ===  S LL M  25/04/91  ===
      *
      * - SE CAMBIA INVOCACION A RUTINA GARP052D POR PROGRAMA GARP052P
      *   ===  S LL M  06/05/91  ===

      * - CONVERTIDO POR CONSIST A NUEVO SISTEMA DE GARANTIAS
      *   ===  F. LIZANA  6-DEC-1993  ===
      ****************************************************************

       ENVIRONMENT DIVISION.
      *---------------------

       DATA DIVISION.
      *--------------

       WORKING-STORAGE SECTION.
      *------------------------

       77  I                        PIC 9(03)   VALUE 0.
       77  J                        PIC 9(03)   VALUE 0.

       77  W-COD-MON-CHILE          PIC X(06)   VALUE '0999  '.
      *FLB CST  6-DEC-1993
      * 77  W-CB-1                   PIC S9(11)V9(4) VALUE +0.
      * 77  W-CB-2                   PIC S9(11)V9(4) VALUE +0.

       01  SCR-VARI   VALUE SPACES  PIC X(01).

       COPY GNSWVFIO.
       COPY GNSWCFIO.
       COPY GNSWGFRM.
       COPY GNSWGFEC.
       COPY GNSWGSYS.

       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.

       COPY GNSBRMSG.
       COPY GNSBRTAB.

       COPY TABBRCAM.

       COPY GARBRGDD.
       COPY GARBRGDG.
       COPY GARBRGVT.
       COPY GARBRTAG.

       COPY SGCBRDBC.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *FLB CST  6-DEC-1993 FIN

       01  VAR-CALCULOS.
      *FLB CST  6-DEC-1993
      *     05  L-COMM               PIC S9(4)       COMP.
      *     05  MONTO-ENT            PIC S9(14)      COMP-3  VALUE +0.
      *FLB CST  6-DEC-1993 FIN
           05  VAL-CML-PESOS        PIC S9(14)      COMP-3  VALUE +0.
           05  VAL-LIQ-PESOS        PIC S9(14)      COMP-3  VALUE +0.
           05  VAL-AJT-PESOS        PIC S9(14)      COMP-3  VALUE +0.


       01  FECHA-W.
           03  DIA-W                PIC 9(02).
           03  MES-W                PIC 9(02).
           03  SIGL-W               PIC 9(02).
           03  AGNO-W               PIC 9(02).

       01  FECHA.
           03  SIGLO                PIC 9(02).
           03  AGNO                 PIC 9(02).
           03  MES                  PIC 9(02).
           03  DIA                  PIC 9(02).

       EJECT

       LINKAGE SECTION.
      *----------------
       01  DFHCOMMAREA.
           02  WOR-PARAMETROS.
           03 GRGA-ID-W                  PIC X(12).
           03 GRGA-STATUS-W              PIC X(12).
           03 GRGA-RESUMEN-W  OCCURS 100 TIMES.
              05 GRGA-KEY-IRGA-W.
                 07 GRGA-IND-REAL-W      PIC X(02).
                 07 GRGA-COD-COGA-W.
                    09 GRGA-COD-TOGA-W   PIC X(03).
                    09 GRGA-COD-AOGA-W   PIC X(03).
                 07 GRGA-IND-CGAR-W      PIC X(01).
                 07 GRGA-COD-VCGA-W.
                    09 GRGA-MSC-TVGA-W   PIC X(01).
                    09 GRGA-COD-CVGA-W   PIC X(03).
                    09 GRGA-COD-TCGA-W   PIC X(02).
                 07 GRGA-COD-OFGA-W      PIC X(03).
              05 GRGA-VAL-COME-W         PIC 9(11)V9(4) COMP-3.
              05 GRGA-VAL-LIQU-W         PIC 9(11)V9(4) COMP-3.
              05 GRGA-VAL-POND-W         PIC 9(11)V9(4) COMP-3.

       EJECT
       PROCEDURE DIVISION.
      *------------------
      *FLB CST  6-DEC-1993
       COPY GNSBGEDB.
           MOVE 'GARPGRCC' TO FIO-PROG.
      *Buscar Independencia de Datos
           PERFORM GNS-HDL-SYS.
           PERFORM GNS-HDL-VSM.
           MOVE 'GARPGRCC' TO IDD-PROG.
           EXEC CICS
                 LINK
                 PROGRAM('GNSPPIDD')
                 COMMAREA(IDD-VARI)
                 LENGTH(+507)
           END-EXEC.

           MOVE SPACES         TO GRGA-STATUS-W.

           PERFORM INICIALIZAR VARYING J FROM 1 BY 1 UNTIL J > 100.

      *FLB CST  6-DEC-1993
      *     MOVE 'REDKY'   TO TAB-COMMAND.
      *     MOVE 'MSC'     TO TAB-VALUE1.
      *     MOVE 'FPROCON' TO TAB-VALUE2.
      *     CALL 'DBNTRY' USING UIB REQ-AREA-TAB WA-TAB ELEM-LIST-TAB.
      *     IF TAB-RET-COD NOT = SPACES

           MOVE 'COL'          TO FIO-SIST.
           MOVE 'MSC'          TO TAB-COD-TTAB IN TAB.
           MOVE 'FPROCON'      TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
      *FLB CST  6-DEC-1993 FIN
              MOVE 'NOTFOUND TAB' TO GRGA-STATUS-W
              PERFORM RETORNO.

           MOVE TAB-GLS-DATA  TO FECHA-W.
           MOVE DIA-W         TO DIA.
           MOVE MES-W         TO MES.
           MOVE AGNO-W        TO AGNO.
           MOVE SIGL-W        TO SIGLO.

      *FLB CST  6-DEC-1993
      *     MOVE GRGA-ID-W TO GDD-VALUE.
      *     MOVE 'REDKY'   TO GDD-COMMAND.
      *     CALL 'DBNTRY' USING UIB REQ-AREA-GDD WA-GDD ELEM-LIST-GDD.
      *     IF GDD-RET-COD NOT = SPACES OR
      *        GAR-ID-DDR IN WA-GDD NOT = GRGA-ID-W

           MOVE GRGA-ID-W TO DBC-IDC-ICLI IN DBC.
           MOVE 'DBC-IDC-ICLI' TO FIO-AKEY.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM SGC-FIO-DBC.
           IF NOT FIO-STAT-OKS
              MOVE 'NOTFOUND GDD' TO GRGA-STATUS-W
              PERFORM RETORNO.

           MOVE DBC-KEY-IDBC IN DBC TO GDD-CIC-ICLI  IN GDD.
           MOVE LOW-VALUES          TO GAR-CLV-GDD-2 IN GDD.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-GDD.
           IF NOT ( FIO-STAT-OKS AND
                    GDD-CIC-ICLI IN GDD = DBC-KEY-IDBC IN DBC )
      *FLB CST  6-DEC-1993 FIN
              MOVE 'NOTFOUND GDD' TO GRGA-STATUS-W
              PERFORM RETORNO.

           MOVE 0 TO I.

      *FLB CST  6-DEC-1993
      *    PERFORM GENERO-INFORME UNTIL GDD-RET-COD NOT = SPACES OR
      *                         GAR-ID-DDR IN WA-GDD NOT = GRGA-ID-W OR
      *                          GRGA-STATUS-W NOT = SPACES.
           PERFORM GENERO-INFORME UNTIL NOT FIO-STAT-OKS OR
                      GDD-CIC-ICLI IN GDD NOT = DBC-KEY-IDBC IN DBC OR
                      GRGA-STATUS-W NOT = SPACES.
      *FLB CST  6-DEC-1993 FIN

           PERFORM RETORNO.

       INICIALIZAR SECTION.
      *--------------------

           MOVE SPACES TO GRGA-KEY-IRGA-W(J).
           MOVE ZEROES TO GRGA-VAL-COME-W(J)
                          GRGA-VAL-LIQU-W(J)
                          GRGA-VAL-POND-W(J).

       FIN-INICIALIZAR.
           EXIT.

       COMPARO SECTION.
      *----------------

      *    PARRAFO NULO.

       FIN-COMPARO.
           EXIT.

       GENERO-INFORME SECTION.
      *-----------------------

           MOVE +0  TO    VAL-CML-PESOS
                          VAL-LIQ-PESOS
                          VAL-AJT-PESOS.


      *FLB CST  6-DEC-1993
      *     MOVE GAR-NUM-SIS IN WA-GDD TO GDG-SIS.
      *     MOVE GAR-COD-CRR IN WA-GDD TO GDG-CRR.
      *     MOVE 'REDKY'               TO GDG-COMMAND.
      *     CALL 'DBNTRY' USING UIB REQ-AREA-GDG WA-GDG ELEM-LIST-GDG.
      *     IF GDG-RET-COD NOT = SPACES
           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GDG.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GDG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
      *FLB CST  6-DEC-1993 FIN
              GO TO LEO-SGTE-GDD.
           IF GAR-IND-CPL IN GDG NOT = 'S' OR
              GAR-ES1 IN GDG < '10' OR > '19'
              GO TO LEO-SGTE-GDD.
           IF (GAR-TIP-OPE IN GDG = '952' AND
               (GAR-STP-OPE IN GDG > '079' AND < '084')) OR
              (GAR-TIP-OPE IN GDG = '954' AND
               GAR-STP-OPE IN GDG = '090')
              GO TO SIGO-PROC.
           IF GAR-FEC-VTO IN GDG < FECHA
              GO TO LEO-SGTE-GDD.
       SIGO-PROC.
      *FLB CST  6-DEC-1993
      *     MOVE GAR-NUM-SIS IN WA-GDD TO GVT-SIS.
      *     MOVE GAR-COD-CRR IN WA-GDD TO GVT-CRR.
      *     MOVE GAR-COD-TSN IN WA-GDG TO GVT-TSN.
      *     MOVE 'REDKY'               TO GVT-COMMAND.
      *     CALL 'DBNTRY' USING UIB REQ-AREA-GVT WA-GVT ELEM-LIST-GVT.
      *     IF GVT-RET-COD NOT = SPACES
           MOVE GAR-NUM-SIS IN GDD TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GDD TO GAR-COD-CRR IN GVT.
           MOVE GAR-COD-TSN IN GDG TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
      *FLB CST  6-DEC-1993 FIN
              GO TO LEO-SGTE-GDD.
      *FLB CST  6-DEC-1993
      *     MOVE SPACES TO WA-TAG.
      *     MOVE 'REDKY' TO TAG-COMMAND.
      *     MOVE 'GI05'                TO TAG-VALUE1.
      *     MOVE GAR-TIP-OPE IN WA-GDG TO TAG-VALUE2.
      *     MOVE GAR-STP-OPE IN WA-GDG TO TAG-VALUE3.
      *     CALL 'DBNTRY' USING UIB REQ-AREA-TAG WA-TAG ELEM-LIST-TAG.
      *     IF TAG-RET-COD NOT = SPACES
           MOVE SPACES TO TAG.
           MOVE GAR-TIP-OPE IN GDG TO GAR-TIP-OPE IN TAG.
           MOVE GAR-STP-OPE IN GDG TO GAR-STP-OPE IN TAG.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
              MOVE 'NOTFOUND TAG' TO GRGA-STATUS-W
      *        MOVE '14'           TO GDG-RET-COD
      *FLB CST  6-DEC-1993 FIN
           ELSE
              IF I > 0
                 PERFORM COMPARO VARYING J FROM 1 BY 1
                         UNTIL J > I OR J > 100 OR
                        (GRGA-COD-TOGA-W(J) = GAR-TIP-OPE IN GDG AND
                         GRGA-COD-AOGA-W(J) = GAR-STP-OPE IN GDG AND
                         GRGA-IND-CGAR-W(J) = GAR-COD-LIM IN GDG AND
                         GRGA-COD-VCGA-W(J) = GAR-VCB     IN GDG AND
                         GRGA-COD-OFGA-W(J) = GAR-COD-OFI IN GDG)
                 PERFORM REVISO-1
              ELSE
                 ADD 1 TO I

                 PERFORM  CONVERTIR-A-PESOS

                 MOVE GAR-TIP-OPE IN GDG TO GRGA-COD-TOGA-W(I)
                 MOVE GAR-STP-OPE IN GDG TO GRGA-COD-AOGA-W(I)
                 MOVE GAR-COD-LIM IN GDG TO GRGA-IND-CGAR-W(I)
                 MOVE GAR-VCB     IN GDG TO GRGA-COD-VCGA-W(I)
                 MOVE GAR-COD-OFI IN GDG TO GRGA-COD-OFGA-W(I)

                 MOVE   VAL-CML-PESOS    TO GRGA-VAL-COME-W(I)
                 MOVE   VAL-LIQ-PESOS    TO GRGA-VAL-LIQU-W(I)
                 MOVE   VAL-AJT-PESOS    TO GRGA-VAL-POND-W(I)

                 IF GAR-TIP-REA-NRE IN TAG = 'N'
                 THEN MOVE 'NR'         TO GRGA-IND-REAL-W (I)
                 ELSE MOVE GAR-TIP-REA-NRE IN TAG
                                        TO GRGA-IND-REAL-W(I).
       LEO-SGTE-GDD.
      *FLB CST  6-DEC-1993
      *     MOVE 'REDNX' TO GDD-COMMAND.
      *     CALL 'DBNTRY' USING UIB REQ-AREA-GDD WA-GDD ELEM-LIST-GDD.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GDD.
      *FLB CST  6-DEC-1993 FIN

       FIN-GENERO-INFORME.
           EXIT.

       REVISO-1 SECTION.
           IF J NOT > I

              PERFORM  CONVERTIR-A-PESOS

              COMPUTE GRGA-VAL-COME-W(J) = GRGA-VAL-COME-W(J) +
                                        VAL-CML-PESOS
              COMPUTE GRGA-VAL-LIQU-W(J) = GRGA-VAL-LIQU-W(J) +
                                        VAL-LIQ-PESOS
              COMPUTE GRGA-VAL-POND-W(J) = GRGA-VAL-POND-W(J) +
                                        VAL-AJT-PESOS
           ELSE
              IF J NOT > 100

                 PERFORM  CONVERTIR-A-PESOS

                 ADD 1 TO I
                 MOVE GAR-TIP-OPE IN GDG TO GRGA-COD-TOGA-W(I)
                 MOVE GAR-STP-OPE IN GDG TO GRGA-COD-AOGA-W(I)
                 MOVE GAR-COD-LIM IN GDG TO GRGA-IND-CGAR-W(I)
                 MOVE GAR-VCB     IN GDG TO GRGA-COD-VCGA-W(I)
                 MOVE GAR-COD-OFI IN GDG TO GRGA-COD-OFGA-W(I)

                 MOVE   VAL-CML-PESOS    TO GRGA-VAL-COME-W(I)
                 MOVE   VAL-LIQ-PESOS    TO GRGA-VAL-LIQU-W(I)
                 MOVE   VAL-AJT-PESOS    TO GRGA-VAL-POND-W(I)

                 IF GAR-TIP-REA-NRE IN TAG = 'N'
                 THEN MOVE 'NR'         TO GRGA-IND-REAL-W (I)
                 ELSE MOVE GAR-TIP-REA-NRE IN TAG
                                        TO GRGA-IND-REAL-W(I)
              ELSE
      *FLB CST  6-DEC-1993
      *           MOVE 'SOBRE 100 RG' TO GRGA-STATUS-W
      *           MOVE '14'           TO GDG-RET-COD.
                 MOVE 'SOBRE 100 RG' TO GRGA-STATUS-W.
      *FLB CST  6-DEC-1993 FIN

       FIN-REVISO-1.
           EXIT.

       CONVERTIR-A-PESOS  SECTION.
      *...........................

           IF GAR-VCB  IN GDG  = W-COD-MON-CHILE
           THEN  MOVE GAR-VAL-CML-TSN IN  GVT  TO   VAL-CML-PESOS
                 MOVE GAR-VAL-LIQ-TSN IN  GVT  TO   VAL-LIQ-PESOS
                 MOVE GAR-VAL-AJT-SUP IN  GVT  TO   VAL-AJT-PESOS

      *FLB CST  6-DEC-1993
      *     ELSE  MOVE SPACES                  TO  W-PARAMETROS
      *           MOVE 'CAM'                   TO  W-ID-ARCH
      *           MOVE GAR-FEC-TSN  IN WA-GVT  TO  W-CAM-FECHA1
      *                                            W-CAM-FECHA2
      *           MOVE GAR-VCB IN WA-GDG       TO  W-CAM-COD-VAL1
      *           MOVE W-COD-MON-CHILE         TO  W-CAM-COD-VAL2
      *           MOVE +130                    TO L-COMM

      *           EXEC CICS LINK PROGRAM('GARP052P')
      *                         COMMAREA(W-PARAMETROS)
      *                         LENGTH(L-COMM)
      *                         END-EXEC
      *           MOVE W-CAM-VALOR        TO W-CB-1
            ELSE
                MOVE GAR-FA-RVL   IN GVT      TO FEC-FANO
                MOVE FEC-SVLD                 TO CAM-NUM-SCAM IN CAM(1)
                MOVE FEC-AVLD                 TO CAM-NUM-ACAM IN CAM(1)
                MOVE GAR-FM-RVL   IN GVT      TO CAM-NUM-MCAM IN CAM(1)
                MOVE GAR-FD-RVL   IN GVT      TO CAM-NUM-DCAM IN CAM(1)
                MOVE CAM-FEC-FCAM IN CAM(1)   TO CAM-FEC-FCAM IN CAM(2)
                MOVE GAR-VCB      IN GDG      TO CAM-COD-VCAM IN CAM(1)
                MOVE W-COD-MON-CHILE          TO CAM-COD-VCAM IN CAM(2)
                MOVE FIO-GET-KEY TO FIO-CMND
                PERFORM TAB-FIO-CAM
                IF NOT (
                   FIO-STAT-OKS AND CAM-IND-VIGE IN CAM = 'S'
                       )
                    MOVE 'NOTFOUND CAM' TO GRGA-STATUS-W
                    PERFORM RETORNO
                ELSE
                    COMPUTE VAL-CML-PESOS ROUNDED =
                        GAR-VAL-CML-TSN IN GVT * CAM-SGV-VCAM IN CAM

                    COMPUTE VAL-LIQ-PESOS ROUNDED =
                        GAR-VAL-LIQ-TSN IN GVT * CAM-SGV-VCAM IN CAM

                    COMPUTE VAL-AJT-PESOS ROUNDED =
                        GAR-VAL-AJT-SUP IN GVT * CAM-SGV-VCAM IN CAM.
      *FLB CST  6-DEC-1993 FIN

       FIN-CONVERTIR-A-PESOS.
           EXIT.


       RETORNO SECTION.
      *----------------
           GO TO RET-EXIT-PGM.

       RET-EXIT-PGM.
      *FLB CST  6-DEC-1993
      *    EXEC CICS RETURN END-EXEC.
       COPY GNSBGGBK.

       COPY GNSBFMSG.
       COPY GNSBFTAB.

       COPY GNSBIABT.

       COPY GNSBGDTC.
      *COPY GNSBGIDD.
       COPY GNSBGSYS.
       COPY GNSBHSYS.
       COPY GNSBHVSM.

       COPY TABBFCAM.

       COPY GARBFGDD.
       COPY GARBFGDG.
       COPY GARBFGVT.
       COPY GARBFTAG.

       COPY SGCBFDBC.
      *FLB CST  6-DEC-1993 FIN
