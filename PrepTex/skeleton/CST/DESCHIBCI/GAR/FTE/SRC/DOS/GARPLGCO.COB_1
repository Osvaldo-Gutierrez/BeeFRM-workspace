       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPLGCO.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    29-Nov-94 12:37 PM.

      * Programa Listador de Reporte GCO con Sort.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MODIFICACION: CONVERTIR REPORT-WRITER A LISTADO, MIGRAR A     *
      *                A COBOL FOR MVS Y CONVERSION 2000               *
      *  FECHA: 13/03/1998                                             *
      ******************************************************************


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTGCO         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTGCO         ASSIGN TO        UT-S-RPTGCO.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      ***** CRW-COMIENZO
      *    SELECT RPTGCO         ASSIGN TO        UT-S-RPTGCO.
           SELECT RPTGCO         ASSIGN TO        RPTGCO.
      ***** CRW-FINAL

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY GARRRGCO.
      ***** CRW-COMIENZO
      *FD  RPTGCO LABEL RECORDS OMITTED
      *    REPORT IS RPT-GCO
      *    .
       FD  RPTGCO
           RECORD CONTAINS 132 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD STANDARD.

       01  RPTGCO-REG           PIC X(132).
      ***** CRW-FINAL

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GMO-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GARBRGMO.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GARRWGCO.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
      *Entidades de GNS
       COPY GNSWGCPT.
      *DAD ini  28-NOV-1994
       COPY GNSBRRCE.
      *DAD fin  28-NOV-1994
      *Entidades de TAB
       COPY TABBROFI.
      *Entidades de SGC
       COPY SGCBRDBC.
      *Entidades de GAR
       COPY GARBRGDG.
       COPY GARBRGDD.
       COPY GARBRGLE.
       COPY GARBRGVT.

       01  WSS-VARI.
      *DAD ini  28-NOV-1994
           03  WSS-CLV-GDG-1.
               05 WSS-NUM-SIS                       PIC X(04).
               05 WSS-COD-CRR                       PIC X(08).
      *DAD fin  28-NOV-1994
           03 WSS-COD-TFP             VALUE SPACES PIC X(02).
           03 WSS-FEC-FPRO.
               05 WSS-FEC-APRO        VALUE 0      PIC 9(04).
               05 WSS-FEC-MPRO        VALUE 0      PIC 9(02).
               05 WSS-FEC-DPRO        VALUE 0      PIC 9(02).
           03 WSS-FEC-FAUX.
               05 WSS-FEC-AAUX        VALUE 0      PIC 9(04).
               05 WSS-FEC-MAUX        VALUE 0      PIC 9(02).
               05 WSS-FEC-DAUX        VALUE 0      PIC 9(02).
           03 WSS-LINEA1              VALUE SPACES PIC X(01).
           03 WSS-LINEA2              VALUE SPACES PIC X(01).
           03 WSS-CNT-STP-ANT         VALUE ZEROES PIC 9(05).
           03 WSS-CNT-TIP-ANT         VALUE ZEROES PIC 9(05).
           03 WSS-CNT-VCB-ANT         VALUE ZEROES PIC 9(05).
           03 WSS-CNT-OFI-ANT         VALUE ZEROES PIC 9(05).
      *>>>>
      *<<<< WSS_DTC
      *DAD ini  28-NOV-1994
      *Entidades de GNS
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-RCE-REQA==.
      *DAD fin  28-NOV-1994
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *Entidades de GAR
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GLE-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
      *Entidades de SGC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *>>>>

      ***** CRW-COMIENZO
      *REPORT SECTION.
      *--------------
      *COPY GARRFGCO.
      *    .
      *COPY GARRTGCO.
       01  RPT-VARIABLES.
           02 RPT-VARIABLES-VARIAS.
               03 RPT-IND-INICIO-LISTADO      PIC X(02) VALUE 'SI'.
               03 RPT-VARI-LINEA              PIC 9(03) VALUE 60.
               03 RPT-VARI-PAGE-COUNTER       PIC 9(03).
           02 RPT-VARIABLES-CONTROL.
               03 RPT-CTRL-STP-OPE            PIC X(03) VALUE SPACES.
               03 RPT-CTRL-TIP-OPE            PIC X(03) VALUE SPACES.
               03 RPT-CTRL-VCB                PIC X(06) VALUE SPACES.
               03 RPT-CTRL-COD-OFI-MOV        PIC X(03) VALUE SPACES.
       01  RPT-VARIABLES-SUMAS.
               03 RPT-SUMA-STP-OPE-CML-TSN    PIC 9(11)V9(04).
               03 RPT-SUMA-STP-OPE-LIQ-TSN    PIC 9(11)V9(04).
               03 RPT-SUMA-TIP-OPE-CML-TSN    PIC 9(11)V9(04).
               03 RPT-SUMA-TIP-OPE-LIQ-TSN    PIC 9(11)V9(04).
               03 RPT-SUMA-VCB-CML-TSN        PIC 9(11)V9(04).
               03 RPT-SUMA-VCB-LIQ-TSN        PIC 9(11)V9(04).

       01  PH-GCO.

           03  PH-GCO-01.
               05  RPT-HEAD-DUSU              PIC X(40).
               05  FILLER                     PIC X(015) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'I N F O R M E   D E   G A R A N T I A S'.
               05  FILLER                     PIC X(028) VALUE SPACES.
               05  RPT-HEAD-FDMA              PIC 99B99B9999.

           03  PH-GCO-02.
               05  RPT-HEAD-DSIS              PIC X(40).
               05  FILLER                     PIC X(023) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'C O N S T I T U I D A S                '.
               05  FILLER                     PIC X(24)
                   VALUE '                    PAG.'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-PAGE-COUNTER      PIC ZZZZZ.

           03  PH-GCO-03.
               05  RPT-HEAD-LINEA1            PIC X.

           03  PH-GCO-04.
               05  FILLER                     PIC X(5)
                   VALUE 'OFI :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-COD-OFI-MOV       PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-OFI           PIC X(15).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  FILLER                     PIC X(5)
                   VALUE 'MON :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-VCB               PIC X(6).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-VCB           PIC X(15).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  FILLER                     PIC X(6)
                   VALUE 'TIPO :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-TIP-OPE           PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-TIP-OPE       PIC X(15).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  FILLER                     PIC X(6)
                   VALUE 'SUBT :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-STP-OPE           PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-STP-OPE       PIC X(15).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  FILLER                     PIC X(10)
                   VALUE 'FEC.PROC :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-FEC-DPRO          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '/'.
               05  RPT-HEAD-FEC-MPRO          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '/'.
               05  RPT-HEAD-FEC-APRO          PIC 9(4).

           03  PH-GCO-05.
               05  FILLER                     PIC X(132)
                   VALUE ALL '='.

           03  PH-GCO-06.
               05  FILLER                     PIC X(39)
                   VALUE 'NUM   GARANTIA   IDC DEL DEUDOR      FE'.
               05  FILLER                     PIC X(39)
                   VALUE 'CHA       FECHA            VALOR       '.
               05  FILLER                     PIC X(39)
                   VALUE '       VALOR        COBERTURA   GR  SEG'.
               05  FILLER                     PIC X(14)
                   VALUE ' INS CMT   NUM'.

           03  PH-GCO-07.
               05  FILLER                     PIC X(036) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'PERFECC     CONSTITC        COMERCIAL  '.
               05  FILLER                     PIC X(39)
                   VALUE '       LIQUIDACION                     '.
               05  FILLER                     PIC X(18)
                   VALUE '             GRTIA'.

           03  PH-GCO-08.
               05  FILLER                     PIC X(132)
                   VALUE ALL '='.

       01  DL-GCO.

           03  DL-GCO-09.
               05  RPT-DETA-NUM-SIS           PIC X(4).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-COD-CRR           PIC X(8).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-NUM-ICLI          PIC X(8).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-VRF-ICLI          PIC X.
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-IND-ICLI          PIC X.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-GLS-ICLI          PIC X(3).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-FD-PFN            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FM-PFN            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FA-PFN            PIC 9(4).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-FD-ESC-CNS        PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FM-ESC-CNS        PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FA-ESC-CNS        PIC 9(4).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-VAL-CML-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-VAL-LIQ-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-GLS-LIM           PIC X(10).
               05  FILLER                     PIC X(003) VALUE SPACES.
               05  RPT-DETA-COD-GRA           PIC X.
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-IND-EXI-GSE       PIC X(2).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-IND-OBL-INS       PIC X(2).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-IND-CMT           PIC X(2).
               05  FILLER                     PIC X(003) VALUE SPACES.
               05  RPT-DETA-CNT-GAR           PIC ZZZZZ.

           03  DL-GCO-10.
               05  RPT-DETA-GLS-NOMC          PIC X(60).

           03  DL-GCO-11.
               05  RPT-DETA-LINEA2            PIC X.

       01  CF-GCO-GDG-STP-OPE.

           03  CF-GCO-12.
               05  FILLER                     PIC X(132)
                   VALUE ALL '-'.

           03  CF-GCO-13.
               05  FILLER                     PIC X(12)
                   VALUE 'SUBTIPO    :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-GLS-STP-OPE       PIC X(15).
               05  FILLER                     PIC X(030) VALUE SPACES.
               05  RPT-FOOT-STP-OPE-CML-TSN   PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-STP-OPE-LIQ-TSN   PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(030) VALUE SPACES.
               05  RPT-FOOT-CNT-STP           PIC ZZZZZ.

       01  CF-GCO-GDG-TIP-OPE.

           03  CF-GCO-14.
               05  FILLER                     PIC X(056) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE '---------------------------------------'.
               05  FILLER                     PIC X(37)
                   VALUE '---                            ------'.

           03  CF-GCO-15.
               05  FILLER                     PIC X(12)
                   VALUE 'TIPO       :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-GLS-TIP-OPE       PIC X(15).
               05  FILLER                     PIC X(030) VALUE SPACES.
               05  RPT-FOOT-TIP-OPE-CML-TSN   PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-TIP-OPE-LIQ-TSN   PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(030) VALUE SPACES.
               05  RPT-FOOT-CNT-TIP           PIC ZZZZZ.

       01  CF-GCO-GDG-VCB.

           03  CF-GCO-16.
               05  FILLER                     PIC X(056) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE '---------------------------------------'.
               05  FILLER                     PIC X(37)
                   VALUE '---                            ------'.

           03  CF-GCO-17.
               05  FILLER                     PIC X(12)
                   VALUE 'MONEDA     :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-GLS-VCB           PIC X(15).
               05  FILLER                     PIC X(030) VALUE SPACES.
               05  RPT-FOOT-VCB-CML-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-VCB-LIQ-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(030) VALUE SPACES.
               05  RPT-FOOT-CNT-VCB           PIC ZZZZZ.

       01  CF-GCO-GMO-COD-OFI-MOV.

           03  CF-GCO-18.
               05  FILLER                     PIC X(126) VALUE SPACES.
               05  FILLER                     PIC X(6)
                   VALUE ALL '-'.

           03  CF-GCO-19.
               05  FILLER                     PIC X(12)
                   VALUE 'OFICINA    :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-GLS-OFI           PIC X(15).
               05  FILLER                     PIC X(099) VALUE SPACES.
               05  RPT-FOOT-CNT-OFI           PIC ZZZZZ.
      ***** CRW-FINAL

       PROCEDURE DIVISION.
      *==================
      ***** CRW-COMIENZO
      *DECLARATIVES.
      *DEC-CF-GCO-GMO-COD-OFI-MOV SECTION.
      *    USE BEFORE REPORTING CF-GCO-GMO-COD-OFI-MOV.

       DEC-CF-GCO-GMO-COD-OFI-MOV SECTION.
      ***** CRW-FINAL
       INI-CF-GCO-GMO-COD-OFI-MOV.
      *<<<< CF_GCO_GMO_COD_OFI_MOV
               MOVE 1 TO WSS-CNT-GAR.
               MOVE WSS-CNT-STP-ANT TO WSS-CNT-STP.
               MOVE 0 TO WSS-CNT-STP-ANT.
               MOVE WSS-CNT-TIP-ANT TO WSS-CNT-TIP.
               MOVE 0 TO WSS-CNT-TIP-ANT.
               MOVE WSS-CNT-VCB-ANT TO WSS-CNT-VCB.
               MOVE 0 TO WSS-CNT-VCB-ANT.
               MOVE WSS-CNT-OFI-ANT TO WSS-CNT-OFI.
               MOVE 0 TO WSS-CNT-OFI-ANT.

      *>>>>
       FIN-CF-GCO-GMO-COD-OFI-MOV.
           EXIT.
      ***** CRW-COMIENZO
      *DEC-CF-GCO-GDG-VCB SECTION.
      *    USE BEFORE REPORTING CF-GCO-GDG-VCB.
      ***** CRW-FINAL

       DEC-CF-GCO-GDG-VCB SECTION.
       INI-CF-GCO-GDG-VCB.
      *<<<< CF_GCO_GDG_VCB
               MOVE WSS-CNT-STP-ANT TO WSS-CNT-STP.
               MOVE 0 TO WSS-CNT-STP-ANT.
               MOVE WSS-CNT-TIP-ANT TO WSS-CNT-TIP.
               MOVE 0 TO WSS-CNT-TIP-ANT.
               MOVE WSS-CNT-VCB-ANT TO WSS-CNT-VCB.
               MOVE 0 TO WSS-CNT-VCB-ANT.
      *>>>>
       FIN-CF-GCO-GDG-VCB.
           EXIT.

      ***** CRW-COMIENZO
      *DEC-CF-GCO-GDG-TIP-OPE SECTION.
      *    USE BEFORE REPORTING CF-GCO-GDG-TIP-OPE.
      ***** CRW-FINAL

       DEC-CF-GCO-GDG-TIP-OPE SECTION.
       INI-CF-GCO-GDG-TIP-OPE.
      *<<<< CF_GCO_GDG_TIP_OPE
               MOVE WSS-CNT-STP-ANT TO WSS-CNT-STP.
               MOVE 0 TO WSS-CNT-STP-ANT.
               MOVE WSS-CNT-TIP-ANT TO WSS-CNT-TIP.
               MOVE 0 TO WSS-CNT-TIP-ANT.
      *>>>>
       FIN-CF-GCO-GDG-TIP-OPE.
           EXIT.
      ***** CRW-COMIENZO
      *DEC-CF-GCO-GDG-STP-OPE SECTION.
      *    USE BEFORE REPORTING CF-GCO-GDG-STP-OPE.
      ***** CRW-FINAL

       DEC-CF-GCO-GDG-STP-OPE SECTION.
       INI-CF-GCO-GDG-STP-OPE.
      *<<<< CF_GCO_GDG_STP_OPE
               MOVE WSS-CNT-STP-ANT TO WSS-CNT-STP.
               MOVE 0 TO WSS-CNT-STP-ANT.
      *>>>>
       FIN-CF-GCO-GDG-STP-OPE.
           EXIT.
      ***** CRW-COMIENZO
      *DEC-PH-GCO SECTION.
      *    USE BEFORE REPORTING PH-GCO.
      ***** CRW-FINAL

       DEC-PH-GCO SECTION.
       INI-PH-GCO.
      *<<<< PH_GCO
      *BUS-TAB Busca Glosa de Subtipo Garantia
           IF GDG-STP-OPE IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'AUX' TO TAB-COD-TTAB IN TAB
               MOVE SPACES TO TAB-COD-CTAB IN TAB
               STRING GDG-TIP-OPE IN SRT, GDG-STP-OPE IN SRT
                      DELIMITED BY SIZE INTO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS = SPACES AND
                   GDG-STP-OPE IN SRT > SPACES
                      MOVE TAB-GLS-DESC IN TAB TO
                           WSS-GLS-STP-OPE IN WSS-GCO
               ELSE
                  MOVE SPACES TO WSS-GLS-STP-OPE IN WSS-GCO
           ELSE
               MOVE SPACES TO WSS-GLS-STP-OPE IN WSS-GCO.
      *BUS-TAB Busca Glosa de Tabla Tipo de Operacion
           IF GDG-TIP-OPE IN SRT > SPACES
                MOVE 'TAB' TO TAB-COD-SIST
                MOVE 'TIO' TO TAB-COD-TTAB IN TAB
                MOVE SPACES TO TAB-COD-CTAB IN TAB
                MOVE GDG-TIP-OPE IN SRT TO TAB-COD-CTAB IN TAB
                PERFORM BUS-TAB
                IF MSG-COD-MENS = SPACES
                   MOVE TAB-GLS-DESC IN TAB TO
                        WSS-GLS-TIP-OPE IN WSS-GCO
                ELSE
                   MOVE SPACES TO WSS-GLS-TIP-OPE IN WSS-GCO
            ELSE
                MOVE SPACES TO WSS-GLS-TIP-OPE IN WSS-GCO.

      *BUS-TAB Busca Glosa de Valor de Cambio
           IF GDG-VCB IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE SPACES TO TAB-COD-CTAB IN TAB
               MOVE GDG-VCB IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS = SPACES
                  MOVE TAB-GLS-DESC IN TAB TO
                       WSS-GLS-VCB IN WSS-GCO
               ELSE
                  MOVE SPACES TO WSS-GLS-VCB IN WSS-GCO
           ELSE
               MOVE SPACES TO WSS-GLS-VCB IN WSS-GCO.

      *BUS-OFI Busca Codigo de Tabla
           IF GMO-COD-OFI-MOV IN SRT > SPACES
                MOVE 'TAB' TO TAB-COD-SIST
                MOVE 'OFI' TO TAB-COD-TTAB IN TAB
                MOVE SPACES TO OFI-COD-OFIC IN OFI
                MOVE GMO-COD-OFI-MOV IN SRT TO OFI-COD-OFIC IN OFI
                IF OFI-COD-OFIC > SPACES
                    PERFORM BUS-OFI
                    IF OFI-GLS-DESC > SPACES
                        MOVE OFI-GLS-DESC IN OFI TO
                             WSS-GLS-OFI IN WSS-GCO
                    ELSE
                        MOVE SPACES TO WSS-GLS-OFI IN WSS-GCO.
      *>>>>
       FIN-PH-GCO.
           EXIT.
      ***** CRW-COMIENZO
      *END DECLARATIVES.
      ***** CRW-FINAL

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'GARPLGCO' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI
           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM TAB-FIO-OFI.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'TAB'   TO FIO-SIST.
           PERFORM TAB-FIO-OFI.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'TAB'   TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GDD.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GDD.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GDG.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GDG.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GVT.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GVT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GLE.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GLE.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'SGC'        TO FIO-SIST.
           PERFORM SGC-FIO-DBC.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'SGC'   TO FIO-SIST.
           PERFORM SGC-FIO-DBC.

           DISPLAY 'CODIGO FECHA DE PROCESO  : '.
           ACCEPT WSS-COD-TFP IN WSS-VARI.
           DISPLAY WSS-COD-TFP IN WSS-VARI.

      * Busca Registros Por Codigo de Tabla de Fecha de Proceso
           MOVE ZEROES TO WSS-FEC-FPRO IN WSS-VARI.
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'GAR' TO FIO-SIST.
           MOVE 'TFP' TO TAB-COD-TTAB IN TAB.
           MOVE WSS-COD-TFP IN WSS-VARI TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'NO ENCONTRE OCURRENCIA TABLA TFP : '
                       TAB-COD-CTAB IN TAB
               DISPLAY ' '
               MOVE PGM-SMAL TO PGM-STAT-SRT
               PERFORM GNS-PRO-END
           ELSE
               DISPLAY TAB-GLS-DESC IN TAB
               DISPLAY ' '
               DISPLAY 'FECHA DE PROCESO   : ' TAB-GLS-DAT1 IN TAB.

           MOVE TAB-GLS-DAT1 IN TAB TO WSS-FEC-FPRO IN WSS-VARI.

      *Vuelve Formato Fecha (SSAAMMDD) a Formato (DDMMSSAA)
           MOVE WSS-FEC-DPRO IN WSS-VARI TO WSS-FEC-DPRO IN WSS-GCO.
           MOVE WSS-FEC-MPRO IN WSS-VARI TO WSS-FEC-MPRO IN WSS-GCO.
           MOVE WSS-FEC-APRO IN WSS-VARI TO WSS-FEC-APRO IN WSS-GCO.

      *>>>>
           SORT GNSSRT
       COPY GARRSGCO.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
      *<<<< FIN_FST_INP
           IF WSS-COD-TFP = '01'
              MOVE WSS-FEC-FPRO IN WSS-VARI TO
                   WSS-FEC-FAUX IN WSS-VARI.

           IF WSS-COD-TFP = '10'
              MOVE WSS-FEC-APRO IN WSS-VARI TO
                   WSS-FEC-AAUX IN WSS-VARI
              MOVE WSS-FEC-MPRO IN WSS-VARI TO
                   WSS-FEC-MAUX IN WSS-VARI
              MOVE 0 TO WSS-FEC-DAUX IN WSS-VARI.

           MOVE WSS-FEC-FAUX IN WSS-VARI TO GAR-CLV-GMO-1.
           MOVE 'GAR' TO FIO-SIST.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           IF NOT FIO-STAT-OKS
               DISPLAY ' '
               DISPLAY 'NO ENCONTRE REGISTROS GMO : '
                       WSS-FEC-FPRO IN WSS-VARI
               DISPLAY ' '
               GO TO FIN-INP-SRT.
      *>>>>
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP

           IF WSS-COD-TFP = '01' AND
              GAR-FEC-MOV IN GMO NOT = WSS-FEC-FPRO IN WSS-VARI
               GO TO FIN-INP-SRT.

           IF WSS-COD-TFP = '10' AND
              (GAR-FA-MOV IN GMO NOT = WSS-FEC-APRO IN WSS-VARI OR
               GAR-FM-MOV IN GMO NOT = WSS-FEC-MPRO IN WSS-VARI)
               GO TO FIN-INP-SRT.

           IF NOT (
              GAR-COD-MOV IN GMO = '1' AND
              GAR-COD-CPO IN GMO = 'GDG001'
              )
                 GO TO NXT-INP-SRT.

      *Busca Llaves del GDG
           MOVE GAR-NUM-SIS IN GMO TO GAR-NUM-SIS IN GDG.
           MOVE GAR-COD-CRR IN GMO TO GAR-COD-CRR IN GDG.
           MOVE 'GAR' TO FIO-SIST.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO GDG.

           IF NOT FIO-STAT-OKS
                MOVE ZEROES TO GAR-FEC-ESC-CNS IN GDG.

      *Busca Registro GLE
           MOVE GAR-NUM-SIS IN GMO TO GAR-NUM-SIS IN GLE.
           MOVE GAR-COD-CRR IN GMO TO GAR-COD-CRR IN GLE.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GLE.
           IF NOT FIO-STAT-OKS
                MOVE SPACES TO GLE.

           IF NOT FIO-STAT-OKS
                MOVE ZEROES TO GAR-FEC-PFN IN GLE.

      *Busca Glosa del Deudor
           MOVE GAR-NUM-SIS IN GMO TO GAR-NUM-SIS IN GDD.
           MOVE GAR-COD-CRR IN GMO TO GAR-COD-CRR IN GDD.
           MOVE 'GAR-CLV-GDD-2' TO FIO-AKEY.
      *RVM-BSH-INI 15
      *    MOVE FIO-GET-KEY TO FIO-CMND.
           MOVE FIO-GET-NLS TO FIO-CMND.
      *RVM-BSH-FIN 15
           PERFORM GAR-FIO-GDD.
      *FLB CST  28-FEB-1994
      *     IF FIO-STAT-OKS
            IF FIO-STAT-OKS AND
               GAR-CLV-GDD-2 IN GDD = GAR-CLV-GDG-1 IN GDG
               MOVE GDD-CIC-ICLI IN GDD TO DBC-CIC-ICLI IN DBC
               MOVE 'DBC-KEY-IDBC' TO FIO-AKEY
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM SGC-FIO-DBC
               IF NOT FIO-STAT-OKS
                   MOVE SPACES TO DBC
      *FLB CST  28-FEB-1994
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE SPACES TO DBC.
      *FLB CST  28-FEB-1994 FIN

           IF FIO-STAT-OKS AND DBC-GLS-NOMC IN DBC > SPACES
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.

      *Busca Registro GVT
           MOVE GAR-NUM-SIS IN GMO TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GMO TO GAR-COD-CRR IN GVT.
           MOVE GAR-COD-TSN IN GDG TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO GVT.

           IF NOT FIO-STAT-OKS
                MOVE ZEROES TO GAR-VAL-CML-TSN IN GVT
                               GAR-VAL-LIQ-TSN IN GVT.
      *>>>>
       MOV-INP-SRT.
       COPY GARRMGCO.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GMO.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-GMO.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTGCO.
      *     MOVE 'GARPLGCO' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
      ***** CRW-COMIENZO
      *    INITIATE RPT-GCO.
           MOVE 0 TO RPT-SUMA-STP-OPE-CML-TSN
           MOVE 0 TO RPT-SUMA-STP-OPE-LIQ-TSN
           MOVE 0 TO RPT-SUMA-TIP-OPE-CML-TSN
           MOVE 0 TO RPT-SUMA-TIP-OPE-LIQ-TSN
           MOVE 0 TO RPT-SUMA-VCB-CML-TSN
           MOVE 0 TO RPT-SUMA-VCB-LIQ-TSN
           MOVE 0 TO RPT-VARI-PAGE-COUNTER.
      ***** CRW-FINAL
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT

      *BUS-TAB Busca Glosa de Limite de Garantia
           IF GDG-COD-LIM IN SRT > SPACES
               MOVE 'GAR' TO TAB-COD-SIST
               MOVE 'LIM' TO TAB-COD-TTAB IN TAB
               MOVE GDG-COD-LIM IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS = SPACES
                  MOVE TAB-GLS-DESC IN TAB TO
                       WSS-GLS-LIM IN WSS-GCO
               ELSE
                  MOVE SPACES TO WSS-GLS-LIM IN WSS-GCO
           ELSE
               MOVE SPACES TO WSS-GLS-LIM IN WSS-GCO.


      * Acumular Numero de Garantia por Oficina, que se despliega en DL
           ADD 1 TO WSS-CNT-GAR.

      *DAD ini  28-NOV-1994
      *CMT verificar si existen
           MOVE GMO-NUM-SIS IN SRT TO WSS-NUM-SIS IN WSS-VARI.
           MOVE GMO-COD-CRR IN SRT TO WSS-COD-CRR IN WSS-VARI.
           MOVE 'GAR'                     TO RCE-COD-SIST IN RCE.
           MOVE 'GDG'                     TO RCE-COD-ENTI IN RCE.
           MOVE WSS-CLV-GDG-1 IN WSS-VARI TO RCE-GLS-IENT IN RCE.
           MOVE SPACES                    TO RCE-COD-SENT IN RCE.
           MOVE 'RCE-KEY-IENT' TO FIO-AKEY.
           MOVE FIO-GET-KEY    TO FIO-CMND.
           PERFORM GNS-FIO-RCE.
           IF FIO-STAT-OKS
               MOVE 'SI' TO WSS-IND-CMT IN WSS-GCO
           ELSE
               MOVE 'NO' TO WSS-IND-CMT IN WSS-GCO.
      *DAD fin  28-NOV-1994
      *>>>>
       GEN-OUT-SRT.
      ***** CRW-COMIENZO
      *    GENERATE DL-GCO.
           MOVE GMO-NUM-SIS IN SRT             TO RPT-DETA-NUM-SIS
           MOVE GMO-COD-CRR IN SRT             TO RPT-DETA-COD-CRR
           MOVE DBC-NUM-ICLI IN SRT            TO RPT-DETA-NUM-ICLI
           MOVE DBC-VRF-ICLI IN SRT            TO RPT-DETA-VRF-ICLI
           MOVE DBC-IND-ICLI IN SRT            TO RPT-DETA-IND-ICLI
           MOVE DBC-GLS-ICLI IN SRT            TO RPT-DETA-GLS-ICLI
           MOVE GLE-FD-PFN IN SRT              TO RPT-DETA-FD-PFN
           MOVE GLE-FM-PFN IN SRT              TO RPT-DETA-FM-PFN
           MOVE GLE-FA-PFN IN SRT              TO RPT-DETA-FA-PFN
           MOVE GDG-FD-ESC-CNS IN SRT          TO RPT-DETA-FD-ESC-CNS
           MOVE GDG-FM-ESC-CNS IN SRT          TO RPT-DETA-FM-ESC-CNS
           MOVE GDG-FA-ESC-CNS IN SRT          TO RPT-DETA-FA-ESC-CNS
           MOVE GVT-VAL-CML-TSN IN SRT         TO RPT-DETA-VAL-CML-TSN
           MOVE GVT-VAL-LIQ-TSN IN SRT         TO RPT-DETA-VAL-LIQ-TSN
           MOVE WSS-GLS-LIM IN WSS-GCO         TO RPT-DETA-GLS-LIM
           MOVE GDG-COD-GRA IN SRT             TO RPT-DETA-COD-GRA
           MOVE GDG-IND-EXI-GSE IN SRT         TO RPT-DETA-IND-EXI-GSE
           MOVE GDG-IND-OBL-INS IN SRT         TO RPT-DETA-IND-OBL-INS
           MOVE WSS-IND-CMT IN WSS-GCO         TO RPT-DETA-IND-CMT
           MOVE WSS-CNT-GAR IN WSS-GCO         TO RPT-DETA-CNT-GAR
           MOVE DBC-GLS-NOMC IN SRT            TO RPT-DETA-GLS-NOMC
           MOVE WSS-LINEA2 IN WSS-GCO          TO RPT-DETA-LINEA2
           IF RPT-IND-INICIO-LISTADO = 'SI'
              MOVE GDG-STP-OPE IN SRT     TO RPT-CTRL-STP-OPE
              MOVE GDG-TIP-OPE IN SRT     TO RPT-CTRL-TIP-OPE
              MOVE GDG-VCB IN SRT         TO RPT-CTRL-VCB
              MOVE GMO-COD-OFI-MOV IN SRT TO RPT-CTRL-COD-OFI-MOV
              MOVE 'NO' TO RPT-IND-INICIO-LISTADO.
           IF GDG-STP-OPE IN SRT     NOT = RPT-CTRL-STP-OPE     OR
              GDG-TIP-OPE IN SRT     NOT = RPT-CTRL-TIP-OPE     OR
              GDG-VCB IN SRT         NOT = RPT-CTRL-VCB         OR
              GMO-COD-OFI-MOV IN SRT NOT = RPT-CTRL-COD-OFI-MOV
              PERFORM INI-CTRL-REPORT
              PERFORM INI-HEAD-REPORT.
           IF RPT-VARI-LINEA > 57
              PERFORM INI-HEAD-REPORT.
           ADD GVT-VAL-CML-TSN IN SRT TO RPT-SUMA-STP-OPE-CML-TSN
           ADD GVT-VAL-LIQ-TSN IN SRT TO RPT-SUMA-STP-OPE-LIQ-TSN
           WRITE RPTGCO-REG FROM  DL-GCO-09
           WRITE RPTGCO-REG FROM  DL-GCO-10
           WRITE RPTGCO-REG FROM  DL-GCO-11
           ADD 3 TO RPT-VARI-LINEA.
      ***** CRW-FINAL

      *<<<< FIN_GEN_OUT
      * Acumular Contadores de Garantias
           ADD 1 TO WSS-CNT-STP-ANT.
           ADD 1 TO WSS-CNT-TIP-ANT.
           ADD 1 TO WSS-CNT-VCB-ANT.
           ADD 1 TO WSS-CNT-OFI-ANT.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      ***** CRW-COMIENZO
      *    TERMINATE RPT-GCO.
           MOVE HIGH-VALUE TO GDG-STP-OPE IN SRT
           MOVE HIGH-VALUE TO GDG-TIP-OPE IN SRT
           MOVE HIGH-VALUE TO GDG-VCB IN SRT
           MOVE HIGH-VALUE TO GMO-COD-OFI-MOV IN SRT
           PERFORM INI-CTRL-REPORT.
      ***** CRW-FINAL
           CLOSE RPTGCO.
       EXT-OUT-SRT.
           EXIT.
      ***** CRW-COMIENZO
       INI-HEAD-REPORT SECTION.
       INI-HEADING.
           PERFORM DEC-PH-GCO
           MOVE RPT-DUSU                       TO RPT-HEAD-DUSU
           MOVE RPT-FDMA                       TO RPT-HEAD-FDMA
           MOVE RPT-DSIS                       TO RPT-HEAD-DSIS
           MOVE WSS-LINEA1 IN WSS-GCO          TO RPT-HEAD-LINEA1
           MOVE GMO-COD-OFI-MOV IN SRT         TO RPT-HEAD-COD-OFI-MOV
           MOVE WSS-GLS-OFI IN WSS-GCO         TO RPT-HEAD-GLS-OFI
           MOVE GDG-VCB IN SRT                 TO RPT-HEAD-VCB
           MOVE WSS-GLS-VCB IN WSS-GCO         TO RPT-HEAD-GLS-VCB
           MOVE GDG-TIP-OPE IN SRT             TO RPT-HEAD-TIP-OPE
           MOVE WSS-GLS-TIP-OPE IN WSS-GCO     TO RPT-HEAD-GLS-TIP-OPE
           MOVE GDG-STP-OPE IN SRT             TO RPT-HEAD-STP-OPE
           MOVE WSS-GLS-STP-OPE IN WSS-GCO     TO RPT-HEAD-GLS-STP-OPE
           MOVE WSS-FEC-DPRO IN WSS-GCO        TO RPT-HEAD-FEC-DPRO
           MOVE WSS-FEC-MPRO IN WSS-GCO        TO RPT-HEAD-FEC-MPRO
           MOVE WSS-FEC-APRO IN WSS-GCO        TO RPT-HEAD-FEC-APRO
           ADD 1 TO RPT-VARI-PAGE-COUNTER
           MOVE RPT-VARI-PAGE-COUNTER TO RPT-HEAD-PAGE-COUNTER
           MOVE SPACES TO RPTGCO-REG
           WRITE RPTGCO-REG AFTER PAGE
           WRITE RPTGCO-REG FROM  PH-GCO-01
           WRITE RPTGCO-REG FROM  PH-GCO-02
           WRITE RPTGCO-REG FROM  PH-GCO-03
           WRITE RPTGCO-REG FROM  PH-GCO-04
           WRITE RPTGCO-REG FROM  PH-GCO-05
           WRITE RPTGCO-REG FROM  PH-GCO-06
           WRITE RPTGCO-REG FROM  PH-GCO-07
           WRITE RPTGCO-REG FROM  PH-GCO-08
           MOVE 9 TO RPT-VARI-LINEA.
       FIN-HEADING.
       FIN-HEAD-REPORT.
           EXIT.
       INI-CTRL-REPORT SECTION.
       INI-CONTROL.
           IF GDG-STP-OPE IN SRT     NOT = RPT-CTRL-STP-OPE     OR
              GDG-TIP-OPE IN SRT     NOT = RPT-CTRL-TIP-OPE     OR
              GDG-VCB IN SRT         NOT = RPT-CTRL-VCB         OR
              GMO-COD-OFI-MOV IN SRT NOT = RPT-CTRL-COD-OFI-MOV
              PERFORM DEC-CF-GCO-GDG-STP-OPE
              MOVE GDG-STP-OPE IN SRT         TO RPT-CTRL-STP-OPE
              MOVE WSS-GLS-STP-OPE IN WSS-GCO TO RPT-FOOT-GLS-STP-OPE
              MOVE RPT-SUMA-STP-OPE-CML-TSN TO RPT-FOOT-STP-OPE-CML-TSN

              MOVE RPT-SUMA-STP-OPE-LIQ-TSN TO RPT-FOOT-STP-OPE-LIQ-TSN
              ADD RPT-SUMA-STP-OPE-CML-TSN  TO RPT-SUMA-TIP-OPE-CML-TSN

              ADD RPT-SUMA-STP-OPE-LIQ-TSN  TO RPT-SUMA-TIP-OPE-LIQ-TSN
              MOVE 0 TO RPT-SUMA-STP-OPE-CML-TSN
              MOVE 0 TO RPT-SUMA-STP-OPE-LIQ-TSN
              MOVE WSS-CNT-STP IN WSS-GCO   TO RPT-FOOT-CNT-STP
              WRITE RPTGCO-REG FROM  CF-GCO-12
              WRITE RPTGCO-REG FROM  CF-GCO-13
              ADD 2 TO RPT-VARI-LINEA.
           IF GDG-TIP-OPE IN SRT     NOT = RPT-CTRL-TIP-OPE     OR
              GDG-VCB IN SRT         NOT = RPT-CTRL-VCB         OR
              GMO-COD-OFI-MOV IN SRT NOT = RPT-CTRL-COD-OFI-MOV
              PERFORM DEC-CF-GCO-GDG-TIP-OPE
              MOVE GDG-TIP-OPE IN SRT     TO RPT-CTRL-TIP-OPE
              MOVE WSS-GLS-TIP-OPE IN WSS-GCO TO RPT-FOOT-GLS-TIP-OPE
              MOVE RPT-SUMA-TIP-OPE-CML-TSN TO RPT-FOOT-TIP-OPE-CML-TSN
              MOVE RPT-SUMA-TIP-OPE-LIQ-TSN TO RPT-FOOT-TIP-OPE-LIQ-TSN
              ADD RPT-SUMA-TIP-OPE-CML-TSN  TO RPT-SUMA-VCB-CML-TSN
              ADD RPT-SUMA-TIP-OPE-LIQ-TSN  TO RPT-SUMA-VCB-LIQ-TSN
              MOVE 0 TO RPT-SUMA-TIP-OPE-CML-TSN
              MOVE 0 TO RPT-SUMA-TIP-OPE-LIQ-TSN
              MOVE WSS-CNT-TIP IN WSS-GCO     TO RPT-FOOT-CNT-TIP
              WRITE RPTGCO-REG FROM  CF-GCO-14
              WRITE RPTGCO-REG FROM  CF-GCO-15
              ADD 2 TO RPT-VARI-LINEA.
           IF GDG-VCB IN SRT         NOT = RPT-CTRL-VCB         OR
              GMO-COD-OFI-MOV IN SRT NOT = RPT-CTRL-COD-OFI-MOV
              PERFORM DEC-CF-GCO-GDG-VCB
              MOVE GDG-VCB IN SRT         TO RPT-CTRL-VCB
              MOVE WSS-GLS-VCB IN WSS-GCO TO RPT-FOOT-GLS-VCB
              MOVE RPT-SUMA-VCB-CML-TSN   TO RPT-FOOT-VCB-CML-TSN
              MOVE RPT-SUMA-VCB-LIQ-TSN   TO RPT-FOOT-VCB-LIQ-TSN
              MOVE 0 TO RPT-SUMA-VCB-CML-TSN
              MOVE 0 TO RPT-SUMA-VCB-LIQ-TSN
              MOVE WSS-CNT-VCB IN WSS-GCO TO RPT-FOOT-CNT-VCB
              WRITE RPTGCO-REG FROM  CF-GCO-16
              WRITE RPTGCO-REG FROM  CF-GCO-17
              ADD 2 TO RPT-VARI-LINEA.
           IF GMO-COD-OFI-MOV IN SRT NOT = RPT-CTRL-COD-OFI-MOV
              PERFORM DEC-CF-GCO-GMO-COD-OFI-MOV
              MOVE GMO-COD-OFI-MOV IN SRT TO RPT-CTRL-COD-OFI-MOV
              MOVE WSS-GLS-OFI IN WSS-GCO TO RPT-FOOT-GLS-OFI
              MOVE WSS-CNT-OFI IN WSS-GCO TO RPT-FOOT-CNT-OFI
              WRITE RPTGCO-REG FROM  CF-GCO-18
              WRITE RPTGCO-REG FROM  CF-GCO-19
              ADD 2 TO RPT-VARI-LINEA.
       FIN-CONTROL.
       FIN-CTRL-REPORT.
           EXIT.
      ***** CRW-FINAL

       COPY GARBFGMO.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      *DAD ini  28-NOV-1994
      *Entidades de GNS
       COPY GNSBFRCE.
      *DAD fin  28-NOV-1994

      *Entidades de SGC
       COPY SGCBFDBC.

      *Entidades de GAR
       COPY GARBFGDG.
       COPY GARBFGDD.
       COPY GARBFGLE.
       COPY GARBFGVT.

      *Entidades de TAB
       COPY GNSBGCPT.
       COPY TABBFOFI.
       COPY TABBBOFI.

       COPY GNSBBTAB.
       COPY GNSBBMSG.
      *>>>>
