       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPLVCR.
       AUTHOR.          CONSIST.
       DATE-WRITTEN.    25-May-94 10:19 AM.

      * Programa Listador de Reporte VCR con Sort.
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MODIFICACION: CONVERTIR REPORT-WRITER A LISTADO, MIGRAR A     *
      *                A COBOL FOR MVS Y CONVERSION 2000               *
      *  FECHA: 13/03/1998                                             *
      ******************************************************************


       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
      *---------------------
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      *--------------------
       FILE-CONTROL.
      *DOS SELECT GNSSRT         ASSIGN TO SYS001-DA-3330-S-SORTWK1.
      *MVS SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      *DOS SELECT RPTVCR         ASSIGN TO SYS011-UR-1403-S.
      *MVS SELECT RPTVCR         ASSIGN TO        UT-S-RPTVCR.
           SELECT GNSSRT         ASSIGN TO        DA-S-SORTWK1.
      ***** CRW-COMIENZO
      *    SELECT RPTVCR         ASSIGN TO        UT-S-RPTVCR.
           SELECT RPTVCR         ASSIGN TO        RPTVCR.
      ***** CRW-FINAL

       DATA DIVISION.
      *=============
       FILE SECTION.
      *------------
       SD  GNSSRT.
       COPY GARRRVCR.
      ***** CRW-COMIENZO
      *FD  RPTVCR LABEL RECORDS OMITTED
      *    REPORT IS RPT-VCR
      *    .
       FD  RPTVCR
           RECORD CONTAINS 132 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD STANDARD.

       01  RPTVCR-REG           PIC X(132).
      ***** CRW-FINAL

       WORKING-STORAGE SECTION.
      *-----------------------
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GARBRGDG.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGRPT.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GARRWVCR.
       COPY GNSWGSYS.
       01  PGM-STAT.
           03 PGM-STAT-SRT            VALUE ' ' PIC X(01).
              88 PGM-STAT-SRT-OKS     VALUE ' '.
              88 PGM-STAT-SRT-MAL     VALUE 'M'.
           03 PGM-SOKS                VALUE ' ' PIC X(01).
           03 PGM-SMAL                VALUE 'M' PIC X(01).
      *<<<< WSS
      *Entidades de GNS
       COPY GNSWGFEC.
       COPY GNSWGHOY.
       COPY GNSWGCPT.
      *Entidades de TAB
       COPY TABBROFI.
      *Entidades de SGC
       COPY SGCBRDBC.
      *Entidades de GAR
       COPY GARBRGES.
       COPY GARBRGDD.
       COPY GARBRGVT.
      *Entidades de COL
       COPY COLBRDLC.

       01  WSS-VARI.
      *FORMATO (SSAAMMDD)
           03  WSS-FEC-FPRO.
               05  WSS-FEC-APRO         VALUE 0      PIC 9(04).
               05  WSS-FEC-MPRO         VALUE 0      PIC 9(02).
               05  WSS-FEC-DPRO         VALUE 0      PIC 9(02).
      *FORMATO (DDMMSSAA)
           03  WSS-FEC-VTO.
               05  WSS-FEC-DVTO         VALUE 0      PIC 9(02).
               05  WSS-FEC-MVTO         VALUE 0      PIC 9(02).
               05  WSS-FEC-AVTO         VALUE 0      PIC 9(04).
      *FORMATO (SSAAMMDD)
           03  WSS-FEC-VTO-AUX.
               05  WSS-FEC-AAUX         VALUE 0      PIC 9(04).
               05  WSS-FEC-MAUX         VALUE 0      PIC 9(02).
               05  WSS-FEC-DAUX         VALUE 0      PIC 9(02).
           03  WSS-LINEA1               VALUE SPACES PIC X(01).
           03  WSS-LINEA2               VALUE SPACES PIC X(01).
           03  WSS-COD-OFIC             VALUE SPACES PIC X(03).
           03  WSS-CNT-STP-ANT          VALUE ZEROES PIC 9(05).
           03  WSS-CNT-TIP-ANT          VALUE ZEROES PIC 9(05).
           03  WSS-CNT-VCB-ANT          VALUE ZEROES PIC 9(05).
           03  WSS-CNT-OFI-ANT          VALUE ZEROES PIC 9(05).
      *>>>>
      *<<<< WSS_DTC
      *Entidades de TAB
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-OFI-REQA==.
      *Entidades de COL
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DLC-REQA==.
      *Entidades de GAR
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GES-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDD-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
      *Entidades de SGC
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-DBC-REQA==.
      *>>>>

      ***** CRW-COMIENZO
      *REPORT SECTION.
      *--------------
      *COPY GARRFVCR.
      *    .
      *COPY GARRTVCR.
       01  RPT-VARIABLES.
           02 RPT-VARIABLES-VARIAS.
               03 RPT-IND-INICIO-LISTADO      PIC X(02) VALUE 'SI'.
               03 RPT-VARI-LINEA              PIC 9(03) VALUE 60.
               03 RPT-VARI-PAGE-COUNTER       PIC 9(03).
           02 RPT-VARIABLES-CONTROL.
               03 RPT-CTRL-VCB                PIC X(06) VALUE SPACES.
               03 RPT-CTRL-STP-OPE            PIC X(03) VALUE SPACES.
               03 RPT-CTRL-TIP-OPE            PIC X(03) VALUE SPACES.
               03 RPT-CTRL-COD-OFI            PIC X(03) VALUE SPACES.
       01  RPT-VARIABLES-SUMAS.
               03 RPT-SUMA-VCB-CML-TSN        PIC 9(11)V9(04).
               03 RPT-SUMA-VCB-LIQ-TSN        PIC 9(11)V9(04).

       01  PH-VCR.

           03  PH-VCR-01.
               05  RPT-HEAD-DUSU              PIC X(40).
               05  FILLER                     PIC X(013) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'I N F O R M E   D E   V E N C I M I E N'.
               05  FILLER                     PIC X(6)
                   VALUE ' T O S'.
               05  FILLER                     PIC X(024) VALUE SPACES.
               05  RPT-HEAD-FDMA              PIC 99B99B9999.

           03  PH-VCR-02.
               05  RPT-HEAD-DSIS              PIC X(40).
               05  FILLER                     PIC X(025) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'D E   C R E D I T O S                  '.
               05  FILLER                     PIC X(22)
                   VALUE '                  PAG.'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-PAGE-COUNTER      PIC ZZZZZ.

           03  PH-VCR-03.
               05  RPT-HEAD-LINEA1            PIC X.

           03  PH-VCR-04.
               05  FILLER                     PIC X(072) VALUE SPACES.
               05  FILLER                     PIC X(17)
                   VALUE 'VENCIMIENTOS AL :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-FEC-DCDT          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-HEAD-FEC-MCDT          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-HEAD-FEC-ACDT          PIC 9(4).
               05  FILLER                     PIC X(006) VALUE SPACES.
               05  FILLER                     PIC X(15)
                   VALUE 'FECHA PROCESO :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-FEC-DPRO          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '/'.
               05  RPT-HEAD-FEC-MPRO          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '/'.
               05  RPT-HEAD-FEC-APRO          PIC 9(4).

           03  PH-VCR-05.
               05  FILLER                     PIC X(10)
                   VALUE 'OFICINA  :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-COD-OFI           PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-OFI           PIC X(15).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  FILLER                     PIC X(6)
                   VALUE 'TIPO :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-TIP-OPE           PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-TIP-OPE       PIC X(15).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  FILLER                     PIC X(9)
                   VALUE 'SUBTIPO :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-STP-OPE           PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-STP-OPE       PIC X(25).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  FILLER                     PIC X(8)
                   VALUE 'MONEDA :'.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-VCB               PIC X(6).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-HEAD-GLS-VCB           PIC X(15).

           03  PH-VCR-06.
               05  FILLER                     PIC X(132)
                   VALUE ALL '='.

           03  PH-VCR-07.
               05  FILLER                     PIC X(39)
                   VALUE 'NUM  GARANTIA   IDC DEL DEUDOR     F.VC'.
               05  FILLER                     PIC X(39)
                   VALUE 'TO          VALOR               VALOR  '.
               05  FILLER                     PIC X(39)
                   VALUE '         LIMITE     F.VCTO   SIST   IDE'.
               05  FILLER                     PIC X(13)
                   VALUE 'NTIF      NUM'.

           03  PH-VCR-08.
               05  FILLER                     PIC X(036) VALUE SPACES.
               05  FILLER                     PIC X(39)
                   VALUE 'GTIA         COMERCIAL           LIQUID'.
               05  FILLER                     PIC X(39)
                   VALUE 'ACION                  CREDITO  CDTO   '.
               05  FILLER                     PIC X(17)
                   VALUE 'CREDITO     GRTIA'.

           03  PH-VCR-09.
               05  FILLER                     PIC X(132)
                   VALUE ALL '='.

       01  DL-VCR.

           03  DL-VCR-10.
               05  RPT-DETA-NUM-SIS           PIC X(4).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-COD-CRR           PIC X(8).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-NUM-ICLI          PIC X(8).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-VRF-ICLI          PIC X.
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-IND-ICLI          PIC X.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-GLS-ICLI          PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-FD-VTO            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FM-VTO            PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-FA-VTO            PIC 9(4).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-VAL-CML-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-VAL-LIQ-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-GLS-LIM           PIC X(12).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-NUM-DMAV          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-NUM-MMAV          PIC 9(2).
               05  FILLER                     PIC X(1)
                   VALUE ALL '-'.
               05  RPT-DETA-NUM-SMAV          PIC 9(2).
               05  RPT-DETA-NUM-AMAV          PIC 9(2).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-COD-SIS-CDT       PIC X(3).
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-DETA-CIC-IOPC          PIC X(12).
               05  FILLER                     PIC X(002) VALUE SPACES.
               05  RPT-DETA-CNT-GAR           PIC ZZZZZ.

           03  DL-VCR-11.
               05  RPT-DETA-GLS-NOMC          PIC X(75).

           03  DL-VCR-12.
               05  RPT-DETA-LINEA2            PIC X.

       01  CF-VCR-GDG-VCB.

           03  CF-VCR-13.
               05  FILLER                     PIC X(132)
                   VALUE ALL '-'.

           03  CF-VCR-14.
               05  FILLER                     PIC X(6)
                   VALUE 'MONEDA'.
               05  FILLER                     PIC X(038) VALUE SPACES.
               05  RPT-FOOT-VCB-CML-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(001) VALUE SPACES.
               05  RPT-FOOT-VCB-LIQ-TSN       PIC ZZ.ZZZ.ZZZ.ZZ9,9999.
               05  FILLER                     PIC X(043) VALUE SPACES.
               05  RPT-FOOT-CNT-VCB           PIC ZZZZZ.

       01  CF-VCR-GDG-STP-OPE.

           03  CF-VCR-15.
               05  FILLER                     PIC X(125) VALUE SPACES.
               05  FILLER                     PIC X(7)
                   VALUE ALL '-'.

           03  CF-VCR-16.
               05  FILLER                     PIC X(7)
                   VALUE 'SUBTIPO'.
               05  FILLER                     PIC X(119) VALUE SPACES.
               05  RPT-FOOT-CNT-STP           PIC ZZZZZ.

       01  CF-VCR-GDG-TIP-OPE.

           03  CF-VCR-17.
               05  FILLER                     PIC X(125) VALUE SPACES.
               05  FILLER                     PIC X(7)
                   VALUE ALL '-'.

           03  CF-VCR-18.
               05  FILLER                     PIC X(4)
                   VALUE 'TIPO'.
               05  FILLER                     PIC X(122) VALUE SPACES.
               05  RPT-FOOT-CNT-TIP           PIC ZZZZZ.

       01  CF-VCR-GDG-COD-OFI.

           03  CF-VCR-19.
               05  FILLER                     PIC X(125) VALUE SPACES.
               05  FILLER                     PIC X(7)
                   VALUE ALL '-'.

           03  CF-VCR-20.
               05  FILLER                     PIC X(7)
                   VALUE 'OFICINA'.
               05  FILLER                     PIC X(119) VALUE SPACES.
               05  RPT-FOOT-CNT-OFI           PIC ZZZZZ.
      ***** CRW-FINAL

       PROCEDURE DIVISION.
      *==================
      ***** CRW-COMIENZO
      *DECLARATIVES.
      *DEC-CF-VCR-GDG-COD-OFI SECTION.
      *    USE BEFORE REPORTING CF-VCR-GDG-COD-OFI.
      ***** CRW-FINAL

       DEC-CF-VCR-GDG-COD-OFI SECTION.
       INI-CF-VCR-GDG-COD-OFI.
      *<<<< CF_VCR_GDG_COD_OFI
               MOVE 1 TO WSS-CNT-GAR.
               MOVE WSS-CNT-VCB-ANT TO WSS-CNT-VCB.
               MOVE 0 TO WSS-CNT-VCB-ANT.
               MOVE WSS-CNT-STP-ANT TO WSS-CNT-STP.
               MOVE 0 TO WSS-CNT-STP-ANT.
               MOVE WSS-CNT-TIP-ANT TO WSS-CNT-TIP.
               MOVE 0 TO WSS-CNT-TIP-ANT.
               MOVE WSS-CNT-OFI-ANT TO WSS-CNT-OFI.
               MOVE 0 TO WSS-CNT-OFI-ANT.

      *>>>>
       FIN-CF-VCR-GDG-COD-OFI.
           EXIT.
      ***** CRW-COMIENZO
      *DEC-CF-VCR-GDG-TIP-OPE SECTION.
      *    USE BEFORE REPORTING CF-VCR-GDG-TIP-OPE.
      ***** CRW-FINAL

       DEC-CF-VCR-GDG-TIP-OPE SECTION.
       INI-CF-VCR-GDG-TIP-OPE.
      *<<<< CF_VCR_GDG_TIP_OPE
               MOVE WSS-CNT-VCB-ANT TO WSS-CNT-VCB.
               MOVE 0 TO WSS-CNT-VCB-ANT.
               MOVE WSS-CNT-STP-ANT TO WSS-CNT-STP.
               MOVE 0 TO WSS-CNT-STP-ANT.
               MOVE WSS-CNT-TIP-ANT TO WSS-CNT-TIP.
               MOVE 0 TO WSS-CNT-TIP-ANT.
      *>>>>
       FIN-CF-VCR-GDG-TIP-OPE.
           EXIT.
      ***** CRW-COMIENZO
      *DEC-CF-VCR-GDG-STP-OPE SECTION.
      *    USE BEFORE REPORTING CF-VCR-GDG-STP-OPE.
      ***** CRW-FINAL

       DEC-CF-VCR-GDG-STP-OPE SECTION.
       INI-CF-VCR-GDG-STP-OPE.
      *<<<< CF_VCR_GDG_STP_OPE
               MOVE WSS-CNT-VCB-ANT TO WSS-CNT-VCB.
               MOVE 0 TO WSS-CNT-VCB-ANT.
               MOVE WSS-CNT-STP-ANT TO WSS-CNT-STP.
               MOVE 0 TO WSS-CNT-STP-ANT.
      *>>>>
       FIN-CF-VCR-GDG-STP-OPE.
           EXIT.
      ***** CRW-COMIENZO
      *DEC-CF-VCR-GDG-VCB SECTION.
      *    USE BEFORE REPORTING CF-VCR-GDG-VCB.
      ***** CRW-FINAL

       DEC-CF-VCR-GDG-VCB SECTION.
       INI-CF-VCR-GDG-VCB.
      *<<<< CF_VCR_GDG_VCB
               MOVE WSS-CNT-VCB-ANT TO WSS-CNT-VCB.
               MOVE 0 TO WSS-CNT-VCB-ANT.
      *>>>>
       FIN-CF-VCR-GDG-VCB.
           EXIT.
      ***** CRW-COMIENZO
      *DEC-PH-VCR SECTION.
      *    USE BEFORE REPORTING PH-VCR.
      ***** CRW-FINAL

       DEC-PH-VCR SECTION.
       INI-PH-VCR.
      *<<<< PH_VCR AND ( PGM_DOS OR PGM_MVS )
      *BUS-TAB Busca Glosa de Valor de Cambio
           IF GDG-VCB IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'VLR' TO TAB-COD-TTAB IN TAB
               MOVE SPACES TO TAB-COD-CTAB IN TAB
               MOVE GDG-VCB IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS = SPACES
                  MOVE TAB-GLS-DCOR IN TAB TO
                       WSS-GLS-VCB IN WSS-VCR
               ELSE
                  MOVE SPACES TO WSS-GLS-VCB IN WSS-VCR
           ELSE
               MOVE SPACES TO WSS-GLS-VCB IN WSS-VCR.

      *BUS-TAB Busca Glosa de Subtipo Garantia
           IF GDG-STP-OPE IN SRT > SPACES
               MOVE 'TAB' TO TAB-COD-SIST
               MOVE 'AUX' TO TAB-COD-TTAB IN TAB
               MOVE SPACES TO TAB-COD-CTAB IN TAB
               STRING GDG-TIP-OPE IN SRT, GDG-STP-OPE IN SRT
                      DELIMITED BY SIZE INTO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS = SPACES AND
                   GDG-STP-OPE IN SRT > SPACES
                      MOVE TAB-GLS-DCOR IN TAB TO
                           WSS-GLS-STP-OPE IN WSS-VCR
               ELSE
                  MOVE SPACES TO WSS-GLS-STP-OPE IN WSS-VCR
           ELSE
               MOVE SPACES TO WSS-GLS-STP-OPE IN WSS-VCR.
      *BUS-TAB Busca Glosa de Tabla Tipo de Operacion
           IF GDG-TIP-OPE IN SRT > SPACES
                MOVE 'TAB' TO TAB-COD-SIST
                MOVE 'TIO' TO TAB-COD-TTAB IN TAB
                MOVE SPACES TO TAB-COD-CTAB IN TAB
                MOVE GDG-TIP-OPE IN SRT TO TAB-COD-CTAB IN TAB
                PERFORM BUS-TAB
                IF MSG-COD-MENS = SPACES
                   MOVE TAB-GLS-DCOR IN TAB TO
                        WSS-GLS-TIP-OPE IN WSS-VCR
                ELSE
                   MOVE SPACES TO WSS-GLS-TIP-OPE IN WSS-VCR
            ELSE
                MOVE SPACES TO WSS-GLS-TIP-OPE IN WSS-VCR.

      *BUS-OFI Busca Codigo de Tabla
           IF GDG-COD-OFI IN SRT > SPACES
                MOVE 'TAB' TO TAB-COD-SIST
                MOVE 'OFI' TO TAB-COD-TTAB IN TAB
                MOVE SPACES TO OFI-COD-OFIC IN OFI
                MOVE GDG-COD-OFI IN SRT TO OFI-COD-OFIC IN OFI
                IF OFI-COD-OFIC > SPACES
                    PERFORM BUS-OFI
                    IF OFI-GLS-DESC > SPACES
                        MOVE OFI-GLS-DESC IN OFI TO
                             WSS-GLS-OFI IN WSS-VCR
                    ELSE
                        MOVE SPACES TO WSS-GLS-OFI IN WSS-VCR.
      *>>>>
       FIN-PH-VCR.
           EXIT.
      ***** CRW-COMIENZO
      *END DECLARATIVES.
      ***** CRW-FINAL

       MAIN SECTION.
      *------------
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE 'GARPLVCR' TO FIO-PROG.
           PERFORM GNS-BUS-IDD.

           MOVE FIO-INP      TO FIO-CMND.
           MOVE 'GNS'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
      *<<<< INI

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM TAB-FIO-OFI.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'TAB'   TO FIO-SIST.
           PERFORM TAB-FIO-OFI.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'TAB'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'TAB'   TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GES.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GES.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GDD.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GDD.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'GAR'        TO FIO-SIST.
           PERFORM GAR-FIO-GVT.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GVT.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'SGC'        TO FIO-SIST.
           PERFORM SGC-FIO-DBC.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'SGC'   TO FIO-SIST.
           PERFORM SGC-FIO-DBC.

           MOVE FIO-ACCS-UPD TO FIO-ACCS.
           MOVE FIO-ACC      TO FIO-CMND.
           MOVE 'COL'        TO FIO-SIST.
           PERFORM COL-FIO-DLC.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'COL'   TO FIO-SIST.
           PERFORM COL-FIO-DLC.

           DISPLAY 'INGRESE FECHA DE VENCIMIENTO (DDMMSSAA) : ' .
           ACCEPT WSS-FEC-VTO IN WSS-VARI.
           DISPLAY WSS-FEC-VTO IN WSS-VARI.

      *VAL-FEC Validar Fecha
           MOVE WSS-FEC-VTO IN WSS-VARI TO FEC-FECH.
           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-FBLK-N   TO FEC-FBLK.
           MOVE FEC-CHOY-N   TO FEC-CHOY.
           MOVE FEC-VHBL-N   TO FEC-VHBL.
           MOVE FEC-VAL-FEC  TO FEC-CMND.
           PERFORM PRO-FEC.
           IF FEC-STAT-OKS
      *VUELVE FORMATO FECHA (DDMMSSAA) A (SSAAMMDD)
               MOVE WSS-FEC-DVTO TO WSS-FEC-DAUX IN WSS-VARI
               MOVE WSS-FEC-MVTO TO WSS-FEC-MAUX IN WSS-VARI
               MOVE WSS-FEC-AVTO TO WSS-FEC-AAUX IN WSS-VARI
           ELSE
               DISPLAY 'FECHA INCORRECTA : ' WSS-FEC-VTO IN WSS-VARI
               MOVE PGM-SMAL TO PGM-STAT-SRT
               PERFORM GNS-PRO-END.


           DISPLAY 'INGRESE OFICINA  (*/0XX/999) : '.
           ACCEPT WSS-COD-OFIC IN WSS-VARI.
           DISPLAY WSS-COD-OFIC IN WSS-VARI.

      *<<<< VAL-OFI
      *VAL-OFI Valida Codigo de Oficina
           IF ( WSS-COD-OFIC IN WSS-VARI > SPACES AND
                WSS-COD-OFIC IN WSS-VARI NOT = '*' )
               MOVE WSS-COD-OFIC IN WSS-VARI TO OFI-COD-OFIC IN OFI
               PERFORM VAL-OFI
               IF MSG-COD-MENS = 'COD    NEX'
                    DISPLAY 'ERROR: CODIGO DE OFICINA INEXISTENTE'
                    PERFORM GNS-PRO-END.

      *>>>>

      * Mueve Fecha de Vencimiento al Reporte
           MOVE WSS-FEC-AVTO IN WSS-VARI TO WSS-FEC-ACDT IN WSS-VCR.
           MOVE WSS-FEC-MVTO IN WSS-VARI TO WSS-FEC-MCDT IN WSS-VCR.
           MOVE WSS-FEC-DVTO IN WSS-VARI TO WSS-FEC-DCDT IN WSS-VCR.

      *Lee Tabla de Fechas de Proceso
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'GAR' TO FIO-SIST.
           MOVE 'TFP' TO TAB-COD-TTAB IN TAB.
           MOVE '01'  TO TAB-COD-CTAB IN TAB.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           IF FIO-STAT-OKS
               MOVE TAB-GLS-DAT1 TO WSS-FEC-FPRO IN WSS-VARI.

      *Vuelve Formato Fecha (SSAAMMDD) a Formato (DDMMSSAA)
           MOVE WSS-FEC-DPRO IN WSS-VARI TO WSS-FEC-DPRO IN WSS-VCR.
           MOVE WSS-FEC-MPRO IN WSS-VARI TO WSS-FEC-MPRO IN WSS-VCR.
           MOVE WSS-FEC-APRO IN WSS-VARI TO WSS-FEC-APRO IN WSS-VCR.


      *>>>>
           SORT GNSSRT
       COPY GARRSVCR.
           INPUT  PROCEDURE IS INP-SRT
           OUTPUT PROCEDURE IS OUT-SRT.
       FIN-MAIN.
      *<<<< FIN
           PERFORM CLO-FIL-VSAM.
      *>>>>
           IF PGM-STAT-SRT-MAL OR SORT-RETURN > 0
               PERFORM GNS-PRO-ABT
           ELSE
               PERFORM GNS-PRO-END.

       INP-SRT SECTION.
      *---------------
       INI-INP-SRT.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
       FST-INP-SRT.
           MOVE FIO-GET-FST TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
       LUP-INP-SRT.
           IF NOT FIO-STAT-OKS
              GO TO FIN-INP-SRT.
      *<<<< LUP_INP
           IF NOT (
              GAR-IND-CPL IN GDG = 'S' AND
              GAR-ES1 IN GDG > '09' AND GAR-ES1 IN GDG < '20' AND
              (WSS-COD-OFIC = '*' OR WSS-COD-OFIC = GAR-COD-OFI IN GDG
               OR WSS-COD-OFIC = '0XX' AND GAR-COD-OFI IN GDG < '100')
              )
               GO TO NXT-INP-SRT.

      * Verifica que GDG tenga GES y fecha DLC-FEC-FMAV sea menor que
      * Fecha ingresada
           MOVE GAR-NUM-SIS IN GDG TO GAR-NUM-SIS IN GES.
           MOVE GAR-COD-CRR IN GDG TO GAR-COD-CRR IN GES.
           MOVE ZEROES             TO GAR-COD-ESP IN GES.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-GES.
           IF FIO-STAT-OKS AND
              GAR-NUM-SIS IN GDG = GAR-NUM-SIS IN GES AND
              GAR-COD-CRR IN GDG = GAR-COD-CRR IN GES AND
              GAR-COD-CRR-CDT IN GES > ZEROES
               MOVE GAR-NUM-SIS-CDT IN GES TO DLC-CAI-IOPC IN DLC
               MOVE GAR-COD-CRR-CDT IN GES TO DLC-IIC-IOPC IN DLC
               MOVE GAR-NUM-DOC-CDT IN GES TO DLC-NUM-IDLC IN DLC
               MOVE FIO-GET-KEY            TO FIO-CMND
               PERFORM COL-FIO-DLC
               IF ( FIO-STAT-OKS AND
                  DLC-FEC-FMAV IN DLC NOT >
                  WSS-FEC-VTO-AUX IN WSS-VARI
                  )
                   NEXT SENTENCE
               ELSE
                   GO TO NXT-INP-SRT
           ELSE
               GO TO NXT-INP-SRT.

      *Busca Registro GVT
           MOVE GAR-NUM-SIS IN GDG TO GAR-NUM-SIS IN GVT.
           MOVE GAR-COD-CRR IN GDG TO GAR-COD-CRR IN GVT.
           MOVE GAR-COD-TSN IN GDG TO GAR-COD-TSN IN GVT.
           MOVE FIO-GET-KEY TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
           IF NOT FIO-STAT-OKS
               MOVE SPACES TO GVT.

           IF NOT FIO-STAT-OKS
                MOVE ZEROES TO GAR-VAL-CML-TSN IN GVT
                               GAR-VAL-LIQ-TSN IN GVT.

      *Busca Glosa del Deudor
           MOVE GAR-NUM-SIS IN GDG TO GAR-NUM-SIS IN GDD.
           MOVE GAR-COD-CRR IN GDG TO GAR-COD-CRR IN GDD.
           MOVE 'GAR-CLV-GDD-2' TO FIO-AKEY.
      *RVM-BSH-INI 15
      *    MOVE FIO-GET-KEY TO FIO-CMND.
           MOVE FIO-GET-NLS TO FIO-CMND.
      *RVM-BSH-FIN 15
           PERFORM GAR-FIO-GDD.
      *FLB CST  28-FEB-1994
      *     IF FIO-STAT-OKS
            IF FIO-STAT-OKS AND
               GAR-CLV-GDD-2 IN GDD = GAR-CLV-GDG-1 IN GDG
               MOVE GDD-CIC-ICLI IN GDD TO DBC-CIC-ICLI IN DBC
               MOVE 'DBC-KEY-IDBC' TO FIO-AKEY
               MOVE FIO-GET-KEY TO FIO-CMND
               PERFORM SGC-FIO-DBC
               IF NOT FIO-STAT-OKS
                   MOVE SPACES TO DBC
      *FLB CST  28-FEB-1994
               ELSE
                   NEXT SENTENCE
           ELSE
               MOVE SPACES TO DBC.
      *FLB CST  28-FEB-1994 FIN

           IF FIO-STAT-OKS AND DBC-GLS-NOMC IN DBC > SPACES
               MOVE DBC-GLS-NOMC IN DBC TO CPT-STRN
               PERFORM CPT-BLKS
               MOVE CPT-STRN TO DBC-GLS-NOMC IN DBC.

      *>>>>
       MOV-INP-SRT.
       COPY GARRMVCR.
           RELEASE SRT.
       NXT-INP-SRT.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GDG.
           GO TO LUP-INP-SRT.
       FIN-INP-SRT.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-GDG.

       OUT-SRT SECTION.
      *---------------
       INI-OUT-SRT.
           IF PGM-STAT-SRT-MAL
               GO TO EXT-OUT-SRT.
           OPEN OUTPUT RPTVCR.
      *     MOVE 'GARPLVCR' TO RPT-NPGM.
           CALL 'GNSPLHDR' USING RPT-VARI.
      ***** CRW-COMIENZO
      *    INITIATE RPT-VCR.
           MOVE 0 TO RPT-SUMA-VCB-CML-TSN
           MOVE 0 TO RPT-SUMA-VCB-LIQ-TSN
           MOVE 0 TO RPT-VARI-PAGE-COUNTER.
      ***** CRW-FINAL
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
       LUP-OUT-SRT.
      *<<<< LUP_OUT
      *BUS-TAB Busca Glosa de Limite de Garantia
           IF GDG-COD-LIM IN SRT > SPACES
               MOVE 'GAR' TO TAB-COD-SIST
               MOVE 'LIM' TO TAB-COD-TTAB IN TAB
               MOVE GDG-COD-LIM IN SRT TO TAB-COD-CTAB IN TAB
               PERFORM BUS-TAB
               IF MSG-COD-MENS = SPACES
                  MOVE TAB-GLS-DCOR IN TAB TO
                       WSS-GLS-LIM IN WSS-VCR
               ELSE
                  MOVE SPACES TO WSS-GLS-LIM IN WSS-VCR
           ELSE
               MOVE SPACES TO WSS-GLS-LIM IN WSS-VCR.


      * Acumular Numero de Garantia por Oficina, que se despliega en DL
           ADD 1 TO WSS-CNT-GAR.

      *>>>>
       GEN-OUT-SRT.
      ***** CRW-COMIENZO
      *    GENERATE DL-VCR.
           MOVE GDG-NUM-SIS IN SRT             TO RPT-DETA-NUM-SIS
           MOVE GDG-COD-CRR IN SRT             TO RPT-DETA-COD-CRR
           MOVE DBC-NUM-ICLI IN SRT            TO RPT-DETA-NUM-ICLI
           MOVE DBC-VRF-ICLI IN SRT            TO RPT-DETA-VRF-ICLI
           MOVE DBC-IND-ICLI IN SRT            TO RPT-DETA-IND-ICLI
           MOVE DBC-GLS-ICLI IN SRT            TO RPT-DETA-GLS-ICLI
           MOVE GDG-FD-VTO IN SRT              TO RPT-DETA-FD-VTO
           MOVE GDG-FM-VTO IN SRT              TO RPT-DETA-FM-VTO
           MOVE GDG-FA-VTO IN SRT              TO RPT-DETA-FA-VTO
           MOVE GVT-VAL-CML-TSN IN SRT         TO RPT-DETA-VAL-CML-TSN
           MOVE GVT-VAL-LIQ-TSN IN SRT         TO RPT-DETA-VAL-LIQ-TSN
           MOVE WSS-GLS-LIM IN WSS-VCR         TO RPT-DETA-GLS-LIM
           MOVE DLC-NUM-DMAV IN SRT            TO RPT-DETA-NUM-DMAV
           MOVE DLC-NUM-MMAV IN SRT            TO RPT-DETA-NUM-MMAV
           MOVE DLC-NUM-SMAV IN SRT            TO RPT-DETA-NUM-SMAV
           MOVE DLC-NUM-AMAV IN SRT            TO RPT-DETA-NUM-AMAV
           MOVE GES-COD-SIS-CDT IN SRT         TO RPT-DETA-COD-SIS-CDT
           MOVE GES-CIC-IOPC IN SRT            TO RPT-DETA-CIC-IOPC
           MOVE WSS-CNT-GAR IN WSS-VCR         TO RPT-DETA-CNT-GAR
           MOVE DBC-GLS-NOMC IN SRT            TO RPT-DETA-GLS-NOMC
           MOVE WSS-LINEA2 IN WSS-VCR          TO RPT-DETA-LINEA2
           IF RPT-IND-INICIO-LISTADO = 'SI'
              MOVE GDG-VCB IN SRT         TO RPT-CTRL-VCB
              MOVE GDG-STP-OPE IN SRT     TO RPT-CTRL-STP-OPE
              MOVE GDG-TIP-OPE IN SRT     TO RPT-CTRL-TIP-OPE
              MOVE GDG-COD-OFI IN SRT     TO RPT-CTRL-COD-OFI
              MOVE 'NO' TO RPT-IND-INICIO-LISTADO.
           IF GDG-VCB IN SRT         NOT = RPT-CTRL-VCB         OR
              GDG-STP-OPE IN SRT     NOT = RPT-CTRL-STP-OPE     OR
              GDG-TIP-OPE IN SRT     NOT = RPT-CTRL-TIP-OPE     OR
              GDG-COD-OFI IN SRT     NOT = RPT-CTRL-COD-OFI
              PERFORM INI-CTRL-REPORT
              PERFORM INI-HEAD-REPORT.
           IF RPT-VARI-LINEA > 57
              PERFORM INI-HEAD-REPORT.
           ADD GVT-VAL-CML-TSN IN SRT TO RPT-SUMA-VCB-CML-TSN
           ADD GVT-VAL-LIQ-TSN IN SRT TO RPT-SUMA-VCB-LIQ-TSN
           WRITE RPTVCR-REG FROM  DL-VCR-10
           WRITE RPTVCR-REG FROM  DL-VCR-11
           WRITE RPTVCR-REG FROM  DL-VCR-12
           ADD 3 TO RPT-VARI-LINEA.
      ***** CRW-FINAL

      *<<<< FIN_GEN_OUT
      * Acumular Contadores de Garantias
           ADD 1 TO WSS-CNT-VCB-ANT.
           ADD 1 TO WSS-CNT-STP-ANT.
           ADD 1 TO WSS-CNT-TIP-ANT.
           ADD 1 TO WSS-CNT-OFI-ANT.
      *>>>>
       NXT-OUT-SRT.
           RETURN GNSSRT AT END
                GO TO FIN-OUT-SRT.
           GO TO LUP-OUT-SRT.
       FIN-OUT-SRT.
      ***** CRW-COMIENZO
      *    TERMINATE RPT-VCR.
           MOVE HIGH-VALUE TO GDG-VCB IN SRT
           MOVE HIGH-VALUE TO GDG-STP-OPE IN SRT
           MOVE HIGH-VALUE TO GDG-TIP-OPE IN SRT
           MOVE HIGH-VALUE TO GDG-COD-OFI IN SRT
           PERFORM INI-CTRL-REPORT.
      ***** CRW-FINAL
           CLOSE RPTVCR.
       EXT-OUT-SRT.
           EXIT.
      ***** CRW-COMIENZO
       INI-HEAD-REPORT SECTION.
       INI-HEADING.
           PERFORM DEC-PH-VCR
           MOVE RPT-DUSU                       TO RPT-HEAD-DUSU
           MOVE RPT-FDMA                       TO RPT-HEAD-FDMA
           MOVE RPT-DSIS                       TO RPT-HEAD-DSIS
           MOVE WSS-LINEA1 IN WSS-VCR          TO RPT-HEAD-LINEA1
           MOVE WSS-FEC-DCDT IN WSS-VCR        TO RPT-HEAD-FEC-DCDT
           MOVE WSS-FEC-MCDT IN WSS-VCR        TO RPT-HEAD-FEC-MCDT
           MOVE WSS-FEC-ACDT IN WSS-VCR        TO RPT-HEAD-FEC-ACDT
           MOVE WSS-FEC-DPRO IN WSS-VCR        TO RPT-HEAD-FEC-DPRO
           MOVE WSS-FEC-MPRO IN WSS-VCR        TO RPT-HEAD-FEC-MPRO
           MOVE WSS-FEC-APRO IN WSS-VCR        TO RPT-HEAD-FEC-APRO
           MOVE GDG-COD-OFI IN SRT             TO RPT-HEAD-COD-OFI
           MOVE WSS-GLS-OFI IN WSS-VCR         TO RPT-HEAD-GLS-OFI
           MOVE GDG-TIP-OPE IN SRT             TO RPT-HEAD-TIP-OPE
           MOVE WSS-GLS-TIP-OPE IN WSS-VCR     TO RPT-HEAD-GLS-TIP-OPE
           MOVE GDG-STP-OPE IN SRT             TO RPT-HEAD-STP-OPE
           MOVE WSS-GLS-STP-OPE IN WSS-VCR     TO RPT-HEAD-GLS-STP-OPE
           MOVE GDG-VCB IN SRT                 TO RPT-HEAD-VCB
           MOVE WSS-GLS-VCB IN WSS-VCR         TO RPT-HEAD-GLS-VCB
           ADD 1 TO RPT-VARI-PAGE-COUNTER
           MOVE RPT-VARI-PAGE-COUNTER TO RPT-HEAD-PAGE-COUNTER
           MOVE SPACES TO RPTVCR-REG
           WRITE RPTVCR-REG AFTER PAGE
           WRITE RPTVCR-REG FROM  PH-VCR-01
           WRITE RPTVCR-REG FROM  PH-VCR-02
           WRITE RPTVCR-REG FROM  PH-VCR-03
           WRITE RPTVCR-REG FROM  PH-VCR-04
           WRITE RPTVCR-REG FROM  PH-VCR-05
           WRITE RPTVCR-REG FROM  PH-VCR-06
           WRITE RPTVCR-REG FROM  PH-VCR-07
           WRITE RPTVCR-REG FROM  PH-VCR-08
           WRITE RPTVCR-REG FROM  PH-VCR-09
           MOVE 10 TO RPT-VARI-LINEA.
       FIN-HEADING.
       FIN-HEAD-REPORT.
           EXIT.
       INI-CTRL-REPORT SECTION.
       INI-CONTROL.
           IF GDG-VCB IN SRT         NOT = RPT-CTRL-VCB         OR
              GDG-STP-OPE IN SRT     NOT = RPT-CTRL-STP-OPE     OR
              GDG-TIP-OPE IN SRT     NOT = RPT-CTRL-TIP-OPE     OR
              GDG-COD-OFI IN SRT     NOT = RPT-CTRL-COD-OFI
              PERFORM DEC-CF-VCR-GDG-VCB
              MOVE GDG-VCB IN SRT         TO RPT-CTRL-VCB
              MOVE RPT-SUMA-VCB-CML-TSN   TO RPT-FOOT-VCB-CML-TSN
              MOVE RPT-SUMA-VCB-LIQ-TSN   TO RPT-FOOT-VCB-LIQ-TSN
              MOVE 0 TO RPT-SUMA-VCB-CML-TSN
              MOVE 0 TO RPT-SUMA-VCB-LIQ-TSN
              MOVE WSS-CNT-VCB IN WSS-VCR TO RPT-FOOT-CNT-VCB
              WRITE RPTVCR-REG FROM  CF-VCR-13
              WRITE RPTVCR-REG FROM  CF-VCR-14
              ADD 2 TO RPT-VARI-LINEA.
           IF GDG-STP-OPE IN SRT     NOT = RPT-CTRL-STP-OPE     OR
              GDG-TIP-OPE IN SRT     NOT = RPT-CTRL-TIP-OPE     OR
              GDG-COD-OFI IN SRT     NOT = RPT-CTRL-COD-OFI
              PERFORM DEC-CF-VCR-GDG-STP-OPE
              MOVE GDG-STP-OPE IN SRT     TO RPT-CTRL-STP-OPE
              MOVE WSS-CNT-STP IN WSS-VCR TO RPT-FOOT-CNT-STP
              WRITE RPTVCR-REG FROM  CF-VCR-15
              WRITE RPTVCR-REG FROM  CF-VCR-16
              ADD 2 TO RPT-VARI-LINEA.
           IF GDG-TIP-OPE IN SRT     NOT = RPT-CTRL-TIP-OPE     OR
              GDG-COD-OFI IN SRT     NOT = RPT-CTRL-COD-OFI
              PERFORM DEC-CF-VCR-GDG-TIP-OPE
              MOVE GDG-TIP-OPE IN SRT     TO RPT-CTRL-TIP-OPE
              MOVE WSS-CNT-TIP IN WSS-VCR TO RPT-FOOT-CNT-TIP
              WRITE RPTVCR-REG FROM  CF-VCR-17
              WRITE RPTVCR-REG FROM  CF-VCR-18
              ADD 2 TO RPT-VARI-LINEA.
           IF GDG-COD-OFI IN SRT NOT = RPT-CTRL-COD-OFI
              PERFORM DEC-CF-VCR-GDG-COD-OFI
              MOVE GDG-COD-OFI IN SRT     TO RPT-CTRL-COD-OFI
              MOVE WSS-CNT-OFI IN WSS-VCR TO RPT-FOOT-CNT-OFI
              WRITE RPTVCR-REG FROM  CF-VCR-19
              WRITE RPTVCR-REG FROM  CF-VCR-20
              ADD 2 TO RPT-VARI-LINEA.
       FIN-CONTROL.
       FIN-CTRL-REPORT.
           EXIT.
      ***** CRW-FINAL

       COPY GARBFGDG.
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBTABT.
       COPY GNSBBSYS.
       COPY GNSBFTAB.
       COPY GNSBFMSG.
       COPY GNSBGEND.
       COPY GNSBGABT.

      *<<<< EOF
      *Entidades de SGC
       COPY SGCBFDBC.

      *Entidades de GAR
       COPY GARBFGES.
       COPY GARBFGDD.
       COPY GARBFGVT.

      *Entidades de COL
       COPY COLBFDLC.

      *Entidades de GNS
       COPY GNSBGFEC.
       COPY GNSBGHOY.
       COPY GNSBGCPT.
      *<<<< NOT PGM_VMS
       COPY GNSBBTAB.
       COPY GNSBBMSG.

      *Entidades de TAB
       COPY TABBFOFI.
       COPY TABBBOFI.
      *>>>>
       COPY TABBVOFI.

       CLO-FIL-VSAM SECTION.
       INI-CLO-FIL-VSAM.
           MOVE FIO-CLO TO FIO-CMND.
           MOVE 'TAB'   TO FIO-SIST.
           PERFORM TAB-FIO-OFI.

           PERFORM GNS-FIO-TAB.

           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GNS-FIO-TAB.

           PERFORM GAR-FIO-GES.

           PERFORM GAR-FIO-GDD.

           PERFORM GAR-FIO-GVT.

           MOVE 'SGC'   TO FIO-SIST.
           PERFORM SGC-FIO-DBC.

           PERFORM COL-FIO-DLC.
       FIN-CLO-FIL-VSAM.
           EXIT.
      *>>>>
