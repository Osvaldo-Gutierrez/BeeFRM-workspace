       IDENTIFICATION DIVISION.
      *-----------------------
       PROGRAM-ID.  GARPGGVT.

      * Carga de Base Garantias, entidades GVT, GIM, GVE
      * a partir de archivo aplanado GARGVS.

       ENVIRONMENT DIVISION.
      *--------------------
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
               DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *-------------
       WORKING-STORAGE SECTION.
       COPY GARBRGVS.
       COPY GARBRGVT.
       COPY GARBRGIM.
       COPY GARBRGVE.

       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GIM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVE-REQA==.

       COPY GNSWGHOY.                                                   
       COPY GNSWGRQA.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSLGFIO.



       01  WS-VARIABLES.
           02 WS-EOF-GVS        PIC 9(01).
           02 WS-GRAB-GIM       PIC 9(01).

           02 WSA-GAR-ID.
              03 WSA-NUM-SIS    PIC X(04).
              03 WSA-COD-CRR    PIC X(08). 

       01  WS-DIA-HOY.
           02 WS-FEC-SHOY    PIC 9(02).
           02 WS-FEC-AHOY    PIC 9(02).
           02 WS-FEC-MHOY    PIC 9(02).
           02 WS-FEC-DHOY    PIC 9(02).


       PROCEDURE DIVISION.
      *------------------
       MAIN-PROGRAM SECTION.
       INI-MAIN.
           PERFORM GET-FHOY.                                            
           MOVE HOY-SHOY  TO WS-FEC-SHOY.
           MOVE HOY-AHOY  TO WS-FEC-AHOY.
           MOVE HOY-MHOY  TO WS-FEC-MHOY.
           MOVE HOY-DHOY  TO WS-FEC-DHOY.
           MOVE SPACES TO WSA-NUM-SIS.
           MOVE SPACES TO WSA-COD-CRR.
           MOVE FIO-INP     TO FIO-CMND.
           PERFORM GAR-FIO-GVS.
           MOVE ZEROS       TO WS-EOF-GVS.
           PERFORM LEE-GARGVS.
           PERFORM CARGA-AUX-GARID.
           PERFORM PROCESA-GARGVS UNTIL WS-EOF-GVS = 1.
           MOVE FIO-CLO     TO FIO-CMND.
           PERFORM GAR-FIO-GVS.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
           
       LEE-GARGVS SECTION.
       INI-LEE-GVS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-GVS.
           IF NOT FIO-STAT-OKS
              MOVE 1 TO WS-EOF-GVS.
           IF FIO-STAT-EOF
              MOVE 1 TO WS-EOF-GVS.
       FIN-LEE-GVS.
           EXIT.

       CARGA-AUX-GARID SECTION.
       INI-AUX-GID.
      * Clave GAR-ID GVS a Auxiliar
           MOVE GAR-NUM-SIS IN GVS TO WSA-NUM-SIS.
           MOVE GAR-COD-CRR IN GVS TO WSA-COD-CRR. 
       FIN-AUX-GID.
           EXIT.

       PROCESA-GARGVS SECTION.
       INI-PROC-GVS.
           PERFORM GRABA-GARGVT.
           PERFORM LEE-GARGVS.
           IF GAR-ID IN GVS NOT = WSA-GAR-ID
              PERFORM GRABA-GARGIM
              PERFORM GRABA-GARGVE
              PERFORM CARGA-AUX-GARID.
       FIN-PROC-GVS.
           EXIT.

       GRABA-GARGVT SECTION.
       INI-GRAB-GVT.
           MOVE GVT-GLS-FLAG    IN GVS  TO GVT-GLS-FLAG IN GVT.
           MOVE GAR-CLV-GVT-1   IN GVS  TO GVT-KEY-IGVT IN GVT.
           MOVE GVT-FEC-ULT-ACT IN GVS  TO GVT-FEC-FTRN IN GVT.
           MOVE GVT-HOR-ACT     IN GVS  TO GVT-HRA-HRTR IN GVT.
           MOVE GVT-MSC-TACC    IN GVS  TO GVT-MSC-TACC IN GVT.
           MOVE GVT-MSC-STAT    IN GVS  TO GVT-MSC-STAT IN GVT.
           MOVE GVT-TML-ACT     IN GVS  TO GVT-COD-OTRN IN GVT.
           MOVE GVT-IDN-USU-ACT IN GVS  TO GVT-COD-ATRN IN GVT.
           MOVE GAR-FEC-TSN     IN GVS  TO GVT-FEC-FTSN IN GVT.
           MOVE GAR-VAL-LIQ-TSN IN GVS  TO GVT-SGV-LTSN IN GVT.
           MOVE GAR-VAL-CML-TSN IN GVS  TO GVT-SGV-VCTS IN GVT.
           MOVE GAR-PCT-PON     IN GVS  TO GVT-SGV-PPCT IN GVT.
           MOVE GAR-VAL-PON     IN GVS  TO GVT-SGV-VPND IN GVT.
           MOVE GAR-PCT-AJT-SUP IN GVS  TO GVT-SGV-PAJU IN GVT.
           MOVE GAR-VAL-AJT-SUP IN GVS  TO GVT-SGV-VGAS IN GVT.
           MOVE GAR-IIV-TSD     IN GVS  TO GVT-COD-ITSD IN GVT.
           MOVE GAR-IIV-SVS     IN GVS  TO GVT-COD-ISVS IN GVT.
           MOVE GAR-TIP-CTR     IN GVS  TO GVT-COD-TCNS IN GVT.
           MOVE GAR-M2-TRR      IN GVS  TO GVT-VAL-TTM2 IN GVT.
           MOVE GAR-M2-CTR      IN GVS  TO GVT-COD-M2CT IN GVT.
           MOVE GAR-TIP-MAQ-EQP IN GVS  TO GVT-COD-MQEQ IN GVT.
           MOVE GAR-FEC-RVL     IN GVS  TO GVT-FEC-FRVL IN GVT.
           MOVE GAR-COD-UMD     IN GVS  TO GVT-COD-UMDI IN GVT.
           MOVE SPACES                  TO GVT-COD-CRIT IN GVT.
           MOVE SPACES                  TO GVT-COD-OAVA IN GVT.
           MOVE ZEROS                   TO GVT-SGV-FISI IN GVT.
           MOVE SPACES                  TO GVT-COD-ADAG IN GVT.
           MOVE SPACES                  TO GVT-COD-SDAG IN GVT.
           MOVE GVT-FEC-ING-SIS IN GVS  TO GVT-FEC-FING IN GVT.
           MOVE GVT-IDN-USU-ING IN GVS  TO GVT-CIC-UING IN GVT.
           MOVE SPACES                  TO GVT-GLS-DISP IN GVT.
           MOVE FIO-PUT  TO FIO-CMND.
           PERFORM GAR-FIO-GVT.
       FIN-GRAB-GVT.
           EXIT.       

       GRABA-GARGIM SECTION.
       INI-GRAB-GIM.
           MOVE ZEROS TO WS-GRAB-GIM.
           IF GVT-COD-TCNS IN GVT NOT = SPACES THEN
              MOVE 1 TO WS-GRAB-GIM.
            
           IF GVT-VAL-TTM2 IN GVT > ZEROS THEN
              MOVE 1 TO WS-GRAB-GIM.

           IF GVT-COD-M2CT IN GVT NOT = SPACES THEN
              MOVE 1 TO WS-GRAB-GIM.
           
           IF GVT-COD-UMDI IN GVT NOT = SPACES THEN
              MOVE 1 TO WS-GRAB-GIM.
      *
           IF WS-GRAB-GIM = 1 THEN
              MOVE GVT-GLS-FLAG IN GVT TO GIM-GLS-FLAG IN GIM
              MOVE WSA-GAR-ID          TO GIM-KEY-IGIM IN GIM
              MOVE WS-DIA-HOY          TO GIM-FEC-FTRN IN GIM
              MOVE ZEROS               TO GIM-HRA-HRTR IN GIM
              MOVE 'I'                 TO GIM-MSC-TACC IN GIM
              MOVE 'I'                 TO GIM-MSC-STAT IN GIM
              MOVE SPACES              TO GIM-COD-OTRN IN GIM
              MOVE 'MIGRACION'         TO GIM-COD-ATRN IN GIM
              MOVE SPACES              TO GIM-COD-STIM IN GIM
              MOVE ZEROS               TO GIM-NUM-EDIM IN GIM
              MOVE GVT-COD-TCNS IN GVT TO GIM-COD-TCNS IN GIM
              MOVE GVT-COD-UMDI IN GVT TO GIM-COD-UMDI IN GIM
              MOVE GVT-VAL-TTM2 IN GVT TO GIM-VAL-TEIM IN GIM
              MOVE GVT-COD-M2CT IN GVT TO GIM-VAL-COIM IN GIM
              MOVE SPACES              TO GIM-GLS-NOAR IN GIM
              MOVE SPACES              TO GIM-COD-RFAR IN GIM
              MOVE ZEROS               TO GIM-VAL-MOAR IN GIM
              MOVE ZEROS               TO GIM-FEC-FVAR IN GIM
              MOVE WS-DIA-HOY          TO GIM-FEC-FING IN GIM
              MOVE 'MIGRACION'         TO GIM-CIC-UING IN GIM
              MOVE SPACES              TO GIM-GLS-DISP IN GIM
              MOVE FIO-PUT  TO FIO-CMND
              PERFORM GAR-FIO-GIM.
       FIN-GRAB-GIM.
           EXIT.       

       GRABA-GARGVE SECTION.
       INI-GRAB-GVE.
           IF GVT-COD-MQEQ IN GVT NOT = SPACES THEN
              MOVE GVT-GLS-FLAG IN GVT TO GVE-GLS-FLAG IN GVE
              MOVE WSA-GAR-ID          TO GVE-KEY-IGVE IN GVE
              MOVE WS-DIA-HOY          TO GVE-FEC-FTRN IN GVE
              MOVE ZEROS               TO GVE-HRA-HRTR IN GVE
              MOVE 'I'                 TO GVE-MSC-TACC IN GVE
              MOVE 'I'                 TO GVE-MSC-STAT IN GVE
              MOVE SPACES              TO GVE-COD-OTRN IN GVE
              MOVE 'MIGRACION'         TO GVE-COD-ATRN IN GVE
              MOVE SPACES              TO GVE-COD-MAVE IN GVE
              MOVE SPACES              TO GVE-GLS-MAVE IN GVE
              MOVE SPACES              TO GVE-GLS-SUVE IN GVE
              MOVE ZEROS               TO GVE-COD-ORVE IN GVE
              MOVE ZEROS               TO GVE-COD-EQVE IN GVE
              MOVE ZEROS               TO GVE-COD-CIVE IN GVE
              MOVE ZEROS               TO GVE-COD-PUVE IN GVE
              MOVE ZEROS               TO GVE-COD-TRVE IN GVE
              MOVE ZEROS               TO GVE-COD-ADVE IN GVE
              MOVE SPACES              TO GVE-GLS-ADVE IN GVE
              MOVE SPACES              TO GVE-GLS-PTTE IN GVE
              MOVE SPACES              TO GVE-IND-PTTE IN GVE
              MOVE SPACES              TO GVE-GLS-MOVE IN GVE
              MOVE SPACES              TO GVE-GLS-SEVE IN GVE
              MOVE SPACES              TO GVE-GLS-COVE IN GVE
              MOVE ZEROS               TO GVE-COD-AGVE IN GVE
              MOVE ZEROS               TO GVE-FEC-FVEN IN GVE
              MOVE SPACES              TO GVE-COD-CFCD IN GVE
              MOVE ZEROS               TO GVE-NUM-AMOD IN GVE
              MOVE ZEROS               TO GVE-FEC-FING IN GVE
              MOVE SPACES              TO GVE-CIC-UING IN GVE
              MOVE SPACES              TO GVE-COD-TVEH IN GVE
              MOVE SPACES              TO GVE-NUM-NPIM IN GVE
              MOVE SPACES              TO GVE-COD-VEND IN GVE
              MOVE ZEROS               TO GVE-SGV-ENGA IN GVE
              MOVE GVT-COD-MQEQ IN GVT TO GVE-COD-MQEQ IN GVE
              MOVE SPACES              TO GVE-GLS-DISP IN GVE
              MOVE FIO-PUT  TO FIO-CMND
              PERFORM GAR-FIO-GVE.
       FIN-GRAB-GVE.
           EXIT.       

       COPY GARBFGVS.
       COPY GARBFGVT.
       COPY GARBFGIM.
       COPY GARBFGVE.
       COPY GNSBGHOY.                                                   
       COPY GNSBBSYS.                                                   
      *
       COPY GNSBTABT.
       COPY GNSBGEND.
