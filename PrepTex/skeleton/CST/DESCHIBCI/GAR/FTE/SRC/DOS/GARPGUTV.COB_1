       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGUTV.
       AUTHOR.          BEE-TEAM.
       DATE-WRITTEN.    06-MAY-2003.

       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.

       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      * Archivo Garuts,contiene:Garantia,Tipo y Subtipo Tasacion
      * Fecha vencimiento tasacion,Monto Tasacion.
       COPY GARBRUTS.
      *
      * Registros de Archivos secuenciales aplanados de las Bases
      * (GARBRGDS = GDG, GARBRGVS = GVT)
       COPY GARBRGDS.
       COPY GARBRGVS.
      *
      * Registros de las Bases Originales.
       COPY GARBRGDG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GDG-REQA==.
       COPY GARBRGVT.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-GVT-REQA==.
       COPY GNSBRTAB.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
      *
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
       COPY GNSWVIDD.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
      *********
       COPY GNSWGFEC.
       COPY GNSWGHOY.
      *********
      *
       01  WS-VARIABLES.
           02 WS-LEE-DDS-FST       PIC 9(01) VALUE ZEROS.
           02 WS-LEE-GVS-FST       PIC 9(01) VALUE ZEROS.
           02 AUX-GAR-TIP-OPE      PIC X(03) VALUE SPACES.
           02 FIN-CICLO            PIC 9(01) VALUE ZEROS.
           02 WSS-FEC-NANO         PIC 9(04) VALUE ZEROS.
           02 WS-CGRT-PRO          PIC 9(09) VALUE ZEROS.
           02 WS-CGRT-GRA          PIC 9(09) VALUE ZEROS.
           02 WS-CGRT-NGR          PIC 9(09) VALUE ZEROS.

       01  AUX-GDG-KEY.
           02 AUX-GAR-NUM-SIS      PIC X(04).
           02 AUX-GAR-COD-CRR      PIC X(08).
           02 AUX-COD-TSN          PIC 9(03).

       01  WS-FEC-TASACION.
           02 WS-GAR-FS-TSN        PIC 9(02).
           02 WS-GAR-FA-TSN        PIC 9(02).
           02 WS-GAR-FM-TSN        PIC 9(02).
           02 WS-GAR-FD-TSN        PIC 9(02).

       01  WS-FECHA-PROCESO.
           02 WSS-NUM-DP02         PIC 9(02).
           02 WSS-NUM-MP02         PIC 9(02).
           02 WSS-NUM-SP02         PIC 9(02).
           02 WSS-NUM-AP02         PIC 9(02).

       01  WS-FEC-PROC-SAMD.
           02 WSS-FPR-SP02         PIC 9(02).
           02 WSS-FPR-AP02         PIC 9(02).
           02 WSS-FPR-MP02         PIC 9(02).
           02 WSS-FPR-DP02         PIC 9(02).

       01  WS-FECHA-TRAB.
           02 WSS-NUM-STRA         PIC 9(02).
           02 WSS-NUM-ATRA         PIC 9(02).
           02 WSS-NUM-MTRA         PIC 9(02).
           02 WSS-NUM-DTRA         PIC 9(02).

       PROCEDURE DIVISION.
      *------------------
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           MOVE "GARPGUTV" TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           MOVE ZEROS TO WS-CGRT-PRO.
           MOVE ZEROS TO WS-CGRT-GRA.
           MOVE ZEROS TO WS-CGRT-NGR.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-GAR-GDS.
           MOVE FIO-GET-FST    TO FIO-CMND.
           PERFORM LEE-GAR-GVS.
           PERFORM CAPTURAR-FEC-PROC.
           PERFORM PROCESA-GAR UNTIL FIO-STAT-EOF.
           DISPLAY 'GARANTIAS PROCESADAS  ' WS-CGRT-PRO.
           DISPLAY 'GARANTIAS GRABADAS    ' WS-CGRT-GRA.
           DISPLAY 'GARANTIAS NO GRABADAS ' WS-CGRT-NGR.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
           MOVE FIO-INP TO FIO-CMND.
           MOVE 'GAR'   TO FIO-SIST.
           PERFORM GAR-FIO-GDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GVS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-UTS.
       FIN-OPEN-FILE.
           EXIT.
      *
      *---------------------------------------------------------------
      *
       LEE-GAR-GDS SECTION.
       LUP-PRO-GDS.
           PERFORM GAR-FIO-GDS.
           MOVE GDS TO GDG.
       FIN-LEE-GAR-GDS.
           EXIT.

       LEE-GAR-GVS SECTION.
       INI-LEE-GAR-GVS.
           PERFORM GAR-FIO-GVS.
           MOVE GVS TO GVT.
       FIN-LEE-GAR-GVS.
           EXIT.

       CAPTURAR-FEC-PROC SECTION.
       INI-FEC-PROC.
           ACCEPT WS-FECHA-PROCESO.
       FIN-FEC-PROC.
           EXIT.

       PROCESA-GAR SECTION.
       PROCESA-GAR-INI.
           IF GAR-ES1     IN GDS = "10" AND
              GAR-IND-CPL IN GDS = "S"  THEN
              ADD 1 TO WS-CGRT-PRO
              PERFORM LEE-TAB-TIO
              MOVE ZEROS TO FIN-CICLO
              PERFORM OBTENGO-ULTASA UNTIL FIN-CICLO = 1.
           MOVE FIO-GET-NXT  TO FIO-CMND.
           PERFORM LEE-GAR-GDS.
       FIN-PROCESA-GAR.
           EXIT.

       LEE-TAB-TIO SECTION.
       INI-LEE-TAB.
           IF GAR-TIP-OPE IN GDS > SPACES THEN
              MOVE 'TAB'              TO TAB-COD-SIST
              MOVE 'TIO'              TO TAB-COD-TTAB IN TAB
              MOVE SPACES             TO TAB-COD-CTAB IN TAB
              MOVE GAR-TIP-OPE IN GDS TO TAB-COD-CTAB IN TAB
              PERFORM BUS-TAB
              IF MSG-COD-MENS = SPACES THEN
                 MOVE TAB-CIC-CTAB IN TAB TO AUX-GAR-TIP-OPE.
       FIN-LEE-TAB.
           EXIT.

       OBTENGO-ULTASA SECTION.
       INI-OBTENGO-ULTASA.
           PERFORM GDG-PASO-KEY.
           IF AUX-GDG-KEY = GAR-CLV-GVT-1 IN GVS THEN
              PERFORM VER-VENCMTO
              MOVE 1 TO FIN-CICLO
           ELSE
              IF AUX-GDG-KEY > GAR-CLV-GVT-1 IN GVS THEN
                 MOVE FIO-GET-NXT  TO FIO-CMND
                 PERFORM LEE-GAR-GVS
              ELSE
                 DISPLAY 'GRT. SIN VALOR-TASACION ' AUX-GDG-KEY
                 MOVE 1 TO FIN-CICLO.
       FIN-OBTENGO-ULTASA.
           EXIT.

       GDG-PASO-KEY SECTION.
       INI-GDG-PASO-KEY.
           MOVE GAR-NUM-SIS IN GDS TO AUX-GAR-NUM-SIS.
           MOVE GAR-COD-CRR IN GDS TO AUX-GAR-COD-CRR.
           MOVE GAR-COD-TSN IN GDS TO AUX-COD-TSN.
       FIN-GDG-PASO-KEY.
           EXIT.

       VER-VENCMTO SECTION.
       INI-VER-VENCMTO.
           IF AUX-GAR-TIP-OPE = "951" THEN
              MOVE 2    TO FEC-NANO
              PERFORM RESTO-FECHA
              PERFORM VALIDO-VENCTO
           ELSE
              MOVE 1    TO FEC-NANO
              PERFORM RESTO-FECHA
              PERFORM VALIDO-VENCTO.
       FIN-VER-VENCMTO.
           EXIT.

       RESTO-FECHA SECTION.
       INI-RST-FEC.
      * TRASPASO FECHA DE PROCESO.
           MOVE WSS-NUM-DP02 TO WSS-FPR-DP02.
           MOVE WSS-NUM-MP02 TO WSS-FPR-MP02.
           MOVE WSS-NUM-SP02 TO WSS-FPR-SP02.
           MOVE WSS-NUM-AP02 TO WSS-FPR-AP02.
      * Fecha de Tasacion GVS.
           MOVE GAR-FEC-TSN IN GVS  TO WS-FEC-TASACION.
           MOVE WS-GAR-FD-TSN TO FEC-ITM1.
           MOVE WS-GAR-FM-TSN TO FEC-ITM2.
           MOVE WS-GAR-FS-TSN TO FEC-ITM3.
           MOVE WS-GAR-FA-TSN TO FEC-ITM4.

           MOVE FEC-FORM-FEC TO FEC-FORM.
           MOVE FEC-SUM-ANO  TO FEC-CMND.
           PERFORM CAL-FEC.
           IF NOT FEC-STAT-OKS
               MOVE 'ERRDIFANO'  TO MSG-COD-MENS IN MSG
           ELSE
              MOVE FEC-ITM1 TO WSS-NUM-DTRA
              MOVE FEC-ITM2 TO WSS-NUM-MTRA
              MOVE FEC-ITM3 TO WSS-NUM-STRA
              MOVE FEC-ITM4 TO WSS-NUM-ATRA.
       FIN-RST-FEC.
           EXIT.

       VALIDO-VENCTO SECTION.
       INI-VAL-VCTO.
              IF WS-FECHA-TRAB < WS-FEC-PROC-SAMD THEN
                 PERFORM GRABA-TASA-VENC
                 ADD 1 TO WS-CGRT-GRA
              ELSE
                 ADD 1 TO WS-CGRT-NGR.
       FIN-VAL-VCTO.
           EXIT.

       GRABA-TASA-VENC SECTION.
       INI-TASA-VENC.
      * Inicializo Registro.
           MOVE SPACES                 TO UTS-GAR-NUM-SIS.
           MOVE SPACES                 TO UTS-GAR-COD-CRR.
           MOVE ZEROS                  TO UTS-GAR-COD-TSN.
           MOVE SPACES                 TO UTS-GAR-TIP-OPE.
           MOVE SPACES                 TO UTS-GAR-STP-OPE.
           MOVE ZEROS                  TO UTS-GAR-FA-TSN.
           MOVE ZEROS                  TO UTS-GAR-FM-TSN.
           MOVE ZEROS                  TO UTS-GAR-FD-TSN.
           MOVE ZEROS                  TO UTS-GAR-MONTO-TSN.
      * Muevo registro a Archivo GARUTS.
           MOVE GAR-NUM-SIS     IN GDS TO UTS-GAR-NUM-SIS.
           MOVE GAR-COD-CRR     IN GDS TO UTS-GAR-COD-CRR.
           MOVE GAR-COD-TSN     IN GDS TO UTS-GAR-COD-TSN.
           MOVE GAR-TIP-OPE     IN GDS TO UTS-GAR-TIP-OPE.
           MOVE GAR-STP-OPE     IN GDS TO UTS-GAR-STP-OPE.
           MOVE GAR-FA-TSN      IN GVS TO UTS-GAR-FA-TSN.
           MOVE GAR-FM-TSN      IN GVS TO UTS-GAR-FM-TSN.
           MOVE GAR-FD-TSN      IN GVS TO UTS-GAR-FD-TSN.
           MOVE GAR-VAL-LIQ-TSN IN GVS TO UTS-GAR-MONTO-TSN.
      *
           MOVE FIO-PUT TO FIO-CMND.
           PERFORM GAR-FIO-UTS.
           MOVE FIO-GET-NXT  TO FIO-CMND.
           PERFORM LEE-GAR-GVS.
       FIN-TASA-VENC.
           EXIT.

       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GAR-FIO-UTS.
           PERFORM GAR-FIO-GDS.
           PERFORM GAR-FIO-GVS.
       FIN-CLOSE-FILE.
           EXIT.
      *
       COPY GARBFUTS.
       COPY GARBFGDS.
       COPY GARBFGVS.
       COPY GNSBFMSG.
       COPY GNSBFTAB.
       COPY GNSBBTAB.
       COPY GNSBBMSG.
       COPY GNSBTABT.
       COPY GNSBGABT.
      **********
       COPY GNSBPFEC.
       COPY GNSBBSYS.
       COPY GNSBGFEC.
       COPY GNSBGHOY.
      **********
       COPY GNSBGIDD.
       COPY GNSBGDTC.
       COPY GNSBGEND.
