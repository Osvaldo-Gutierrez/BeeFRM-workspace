       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.     GARP303.
       AUTHOR.         G. URETA Q.
       DATE-WRITTEN.   27-AUG-1987.
       DATE-COMPILED.
      *         PROGRAMA COBOL + DATACOM/DB
      *         QUE TIENE POR OBJETIVO CALCULAR
      *         LOS MONTOS REALES Y NO REALES
      *         DE LAS GARANTIAS.
      *
      *         SE USAN LAS TABLAS :
      *             + INPUT
      *               - D01 (VSAM)
      *
      *             + INPUT-OUTPUT
      *               - D03 (DATACOM)
      *
      ******************************************************************
      *  INTERSOFT                                                     *
      *  MIGRACION A COBOL OS/390                                      *
      *  FECHA: 15 DE ABRIL DE 1998                                    *
      ******************************************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.

       FILE-CONTROL.

           SELECT D01                  ASSIGN       TO CYDV001
                                       ORGANIZATION IS INDEXED
                                       ACCESS       IS DYNAMIC
                                       RECORD KEY   IS D01-CLAVE
                                       FILE STATUS  IS ST-D01.

      *
       DATA DIVISION.
       FILE SECTION.

       FD  D01
           LABEL RECORD IS STANDARD.

           COPY CYDV001C.

      *
       WORKING-STORAGE SECTION.

       77  CLAVE-ANT                       PIC X(10).
       77  CLAVE-SIG                       PIC X(10).
       77  SUMA-TOTAL-REA                  PIC S9(11)V9(2) VALUE +0.
       77  SUMA-TOTAL-NRE                  PIC S9(11)V9(2) VALUE +0.
       77  CONT-D03-LEIDOS                 PIC 9(05) VALUE 0.
       77  CONT-D03-GRABADOS               PIC 9(05) VALUE 0.
       77  ST-D01                          PIC 9(02) VALUE 0.
       77  PRIM-LECT                       PIC 9(01) VALUE 0.
       77  EOF-D01                         PIC 9(01) VALUE 0.

      *------------------------------------------------------------*
      *                                                            *
      *               USER INFORMATION BLOCK.                      *
      *                                                            *
      *------------------------------------------------------------*

       01  UIB.
           03  FILLER                      PIC X(32) VALUE 'GARP303'.

      *------------------------------------------------------------*
      *                                                            *
      *                   REQUEST AREA                             *
      *                                                            *
      *------------------------------------------------------------*

       01  REQ-AREA-D03.
           03  D03-COMMAND                 PIC X(5).
           03  D03-TABLE                   PIC X(3)  VALUE 'D03'.
           03  D03-KEY                     PIC X(5)  VALUE 'D0301'.
           03  D03-RET-COD                 PIC X(2)  VALUE SPACES.
           03  D03-INT-RC                  PIC X(1).
           03  D03-DB-ID                   PIC S9(4) COMP VALUE +220.
           03  D03-REC-ID                  PIC X(7).
           03  FILLER                      PIC X(25).
           03  D03-CONT-MAX                PIC S9(4) COMP.
           03  D03-CONT-IO                 PIC S9(4) COMP.
           03  FILLER                      PIC X(22).
           03  D03-VALUE                   PIC X(10).

      *------------------------------------------------------------*
      *                                                            *
      *                     WORK AREA                              *
      *                                                            *
      *------------------------------------------------------------*

       01  WA-D03.
      *    COPYDD IGA-R-D03.IGA-E-D0301(PROD),02,D
      *                             EQUIVALENTE AL REGISTRO D03.
           02  IGA-E-D0301.
      *                             CLAVE MAESTRA D03
               04  GAR-CLV-D03-1.
      *                             ID. DEUDOR SEGUN SUPER (GDD)
                   06  GAR-ID-SUP-DDR.
      *                             RUT DEUDOR
                       08  GAR-RUT-SUP-DDR
                                    PICTURE X(9).
      *                             DIGITO VERIFICADOR RUT DEUDOR
                       08  GAR-VRT-SUP-DDR
                                    PICTURE X(1).
      *                             RAZON SOCIAL DEL DEUDOR (DGC)
               04  GAR-RSO-DDR      PICTURE X(40).
      *                             MONTO TOTAL GTIAS. REALES DEL DDR
               04  GAR-VAL-GAR-REA  PICTURE S9(11)V9(2).
      *                             IDENTIFICACION  DE LA GARANTIA (GDG)
               04  GAR-ID           PICTURE X(12).
      *                             ID DUENO GTIA. GDG O DDR GDD (SUPER)
               04  GAR-ID-SUP-GNT.
      *                             RUT DUENO O DDR
                   06  GAR-RUT-SUP-GNT
                                    PICTURE X(9).
      *                             D. VER. RUT DUENO O DDR
                   06  GAR-VRT-SUP-GNT
                                    PICTURE X(1).
      *                             RAZON SOCIAL DEL GARANTE (DGC)
               04  GAR-RSO-GNT      PICTURE X(40).
      *                             LIMITE (GDG)
               04  GAR-COD-LIM      PICTURE X(1).
      *                             IDENTIFICACION DEL CREDITO (GES)
               04  GAR-COD-CLV-ESP  PICTURE X(12).
      *                             VALOR GARANTIA (MONTO)
               04  GAR-VAL          PICTURE S9(11)V9(2).
      *                             TIPO OPERACION (GDG)
               04  GAR-TIP-OPE      PICTURE 9(1).
      *                             SUBTIPO DE OPERACION (GDG)
               04  GAR-STP-OPE      PICTURE 9(2).
      *                             TIPO DE CONSTRUCCION (GVT)
               04  GAR-TIP-CTR      PICTURE 9(2).
      *                             METROS CUADRADOS TERRENO (GVT)
               04  GAR-M2-TRR       PICTURE 9(5).
      *                             METROS CUADRADOS CONSTRUIDOS (GVT)
               04  GAR-M2-CTR       PICTURE 9(5).
      *                             TIPO DE MAQ. Y EQUIP. (GVT)
               04  GAR-TIP-MAQ-EQP  PICTURE 9(2).
      *                             VAL. LIQ. TASAC. (MILES $) (GVT)
               04  GAR-VAL-LIQ-TSN  PICTURE S9(11)V9(2).
      *                             FECHA DE TASACION (GVT)
               04  GAR-FEC-TSN.
      *                             FECHA (ANO) DE TASACION (GVT)
                   06  GAR-FA-TSN   PICTURE 9(2).
      *                             FECHA (MES) DE TASACION (GVT)
                   06  GAR-FM-TSN   PICTURE 9(2).
      *                             FECHA (DIA) DE TASACION (GVT)
                   06  GAR-FD-TSN   PICTURE 9(2).
      *                             CODIGO DE MONEDA SUPER
               04  GAR-COD-MON-SUP  PICTURE 9(3).
      *                             VAL. CTABLE GTIA (GDG) (MILES $)
               04  GAR-VAL-ULT-CON  PICTURE S9(11)V9(2).
      *                             FECHA DE VENCIMIENTO (GDG)
               04  GAR-FEC-VTO.
      *                             FECHA (ANO) DE VENCIMIENTO (GDG)
                   06  GAR-FA-VTO   PICTURE 9(2).
      *                             FECHA (MES) DE VENCIMIENTO (GDG)
                   06  GAR-FM-VTO   PICTURE 9(2).
      *                             FECHA (DIA) DE VENCIMIENTO (GDG)
                   06  GAR-FD-VTO   PICTURE 9(2).
      *                             GRADO DE GARANTIA (GDG)
               04  GAR-COD-GRA      PICTURE 9(1).
      *                             ID. SUPER. AC. PREF. 1 (GAP)
               04  GAR-ID-SUP-AP1.
      *                             RUT SUPER. AC. PREF. 1
                   06  GAR-RUT-SUP-AP1
                                    PICTURE X(9).
      *                             D. VER. SUPER. AC. PREF. 1
                   06  GAR-VRT-SUP-AP1
                                    PICTURE X(1).
      *                             ID. SUPER. AC. PREF. 2 (GAP)
               04  GAR-ID-SUP-AP2.
      *                             RUT SUPER. AC. PREF. 2
                   06  GAR-RUT-SUP-AP2
                                    PICTURE X(9).
      *                             D. VER. SUPER. AC. PREF. 2
                   06  GAR-VRT-SUP-AP2
                                    PICTURE X(1).
      *                             VAL. RESID. GTIA. (MILES $)
               04  GAR-VAL-RSD      PICTURE S9(11)V9(2).
      *                             DIRECCION UBICACION (GUB)
               04  GAR-DIR-1        PICTURE X(40).
      *                             CODIGO COMUNA UBIC. (GUB)
               04  GAR-COD-CMN      PICTURE X(15).
      *                             LOCALIDAD DE LA GARANTIA (GUB)
               04  GAR-LOC          PICTURE X(6).
      *                             CODIGO DE REGISTRO (GLE)
               04  GAR-COD-REG      PICTURE 9(2).
      *                             DESC. LOCALIDAD REGISTRO (GLE)
               04  GAR-DES-LOC-REG  PICTURE X(40).
      *                             LOCALIDAD DE REGISTRO (GLE)
               04  GAR-LOC-REG      PICTURE X(6).
      *                             ANO INSCRIPCION GARANTIA (GLE)
               04  GAR-ANO-ICR      PICTURE 9(2).
      *                             FOJA INSCRIPCION GARANTIA (GLE)
               04  GAR-FOJ-ICR      PICTURE X(6).
      *                             NUMERO INSCRIPCION GTIA. (GLE)
               04  GAR-NUM-ICR      PICTURE 9(10).
      *                             UNIDAD DE LA SUPERFICIE DE TERRENO
               04  GAR-UMD-SUP-TER  PICTURE 9(1).
      *                             MON.TOT.GTIAS.NO REALES DDR(MIL.$)
               04  GAR-VAL-GAR-NRE  PICTURE S9(11)V9(2).
      *                             TIPO DE GARANTIA REAL O NO REAL
               04  GAR-TIP-REA-NRE  PICTURE X(1).
      *                             IND. MULTIPLES DEUDORES (GDG)
               04  GAR-IND-MUL-DDR  PICTURE X(2).
      *                             CORRELAT. DE NUM. ESPECIFIC.
               04  GAR-CRR-NUM-ESP  PICTURE 9(3).
      *                             FECHA CREACION REGISTRO
               04  D03-FEC-ING-SIS.
      *                             FECHA (ANO) CREACION REGISTRO
                   06  D03-FA-ING-SIS
                                    PICTURE 9(2).
      *                             FECHA (MES) CREACION REGISTRO
                   06  D03-FM-ING-SIS
                                    PICTURE 9(2).
      *                             FECHA (DIA) CREACION REGISTRO
                   06  D03-FD-ING-SIS
                                    PICTURE 9(2).
      *                             AUTOR INGRESO REGISTRO
               04  D03-ATR-ING-SIS  PICTURE X(12).
      *                             AUTOR ULTIMA ACTUALIZACION
               04  D03-ATR-ULT-ACT  PICTURE X(12).
      *                             FECHA ULTIMA ACTUALIZACION REG.
               04  D03-FEC-ULT-ACT.
      *                             FECHA (ANO) ULTIMA ACT. REG.
                   06  D03-FA-ULT-ACT
                                    PICTURE 9(2).
      *                             FECHA (MES) ULTIMA ACT. REG.
                   06  D03-FM-ULT-ACT
                                    PICTURE 9(2).
      *                             FECHA (DIA) ULTIMA ACT. REG.
                   06  D03-FD-ULT-ACT
                                    PICTURE 9(2).
      *                             INDICADOR RUT DEUDOR
               04  GAR-IRT-DDR      PICTURE X(1).
      *                             EXTENSION RUT DEUDOR
               04  GAR-GRT-DDR      PICTURE X(3).
      *                             INDICADOR RUT GARANTE
               04  GAR-IRT-GNT      PICTURE X(1).
      *                             EXTENSION RUT GARANTE
               04  GAR-GRT-GNT      PICTURE X(3).
      *                             IND. RUT ACREED. PREF. 1 GRADO
               04  GAR-IRT-AP1      PICTURE X(1).
      *                             EXT. RUT ACRED. PREF. 1 GRADO
               04  GAR-GRT-AP1      PICTURE X(3).
      *                             IND. RUT ACREED. PREF. 2 GRADO
               04  GAR-IRT-AP2      PICTURE X(1).
      *                             EXT. RUT ACRED. PREF. 2 GRADO
               04  GAR-GRT-AP2      PICTURE X(3).
      *                             INDICADOR DE MAYOR DEUDOR
               04  GAR-IND-MAY-DDR  PICTURE X(1).
      *                             TIPO TASACION
               04  GAR-TIP-TSN      PICTURE 9(1).
               04  FILLER           PICTURE X(14).

      *------------------------------------------------------------*
      *                                                            *
      *                   ELEMENT LIST                             *
      *                                                            *
      *------------------------------------------------------------*

       01   ELEM-LIST-D03.
            03    ELEM-1                   PIC X(5) VALUE 'D0301'.
            03    ELEM-SEC                 PIC X(1).
            03    FILLER                   PIC X(5) VALUE SPACES.

      *------------------------------------------------------------*
      *                                                            *
      *                   PROGRAMA PRINCIPAL                       *
      *                                                            *
      *------------------------------------------------------------*

       PROCEDURE DIVISION.

           ENTRY 'DBMSCBL'.

           MOVE 'LOCKY' TO D03-COMMAND.
           MOVE SPACES  TO D03-VALUE.
           CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03.
           IF (D03-RET-COD NOT = SPACES)
              DISPLAY ' '
              DISPLAY 'GARP303 : '
              DISPLAY ' '
              DISPLAY '*****  TABLA D03 VACIA *****'
              DISPLAY ' '
              GOBACK.
           MOVE 'REDLE' TO D03-COMMAND.
           CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03.
           IF (D03-RET-COD = SPACES)
              ADD 1               TO CONT-D03-LEIDOS
              MOVE GAR-ID-SUP-DDR TO CLAVE-ANT CLAVE-SIG.
           OPEN INPUT D01.
           IF ST-D01 > 0
              DISPLAY '*** ERROR NO SE ENCUENTRA ARCHIVO "D01" '
              DISPLAY '*** STATUS : ' ST-D01
              GOBACK.
           MOVE '2'    TO D01-TIPO-REG.
           MOVE SPACES TO D01-RUT-D.
           START D01 KEY > D01-CLAVE INVALID KEY
                           DISPLAY '** NO SE PUDO POSESIONAR EN D01 **'
                           GOBACK.
           PERFORM LEE-SEC-D01.
           IF (D01-TIPO-REG NOT = '2') OR (EOF-D01 = 1)
              DISPLAY '*** ERROR NO EXISTE REGISTRO TIPO "2" '
              CLOSE D01
              GOBACK.
           PERFORM PROCESO-PRINC UNTIL (D03-RET-COD NOT = SPACES).
           DISPLAY ' '.
           DISPLAY 'GARP303 : '.
           DISPLAY ' '.
           DISPLAY 'FIN NORMAL .... '.
           DISPLAY ' '.
           DISPLAY ' '.
           DISPLAY 'DEUDORES  LEIDOS      : ' CONT-D03-LEIDOS.
           DISPLAY ' '.
           DISPLAY 'REGISTROS ACTUALIZADOS: ' CONT-D03-GRABADOS.
           DISPLAY ' '.
           CLOSE D01.
           GOBACK.

       PROCESO-PRINC SECTION.
      *----------------------

           IF CLAVE-ANT = GAR-ID-SUP-DDR
              PERFORM VEO-SI-LEE-D01
              PERFORM SUMA-POR-TIPO
              MOVE 'REDNX' TO D03-COMMAND
              CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03
              IF D03-RET-COD NOT = SPACES
                 MOVE '..........' TO CLAVE-SIG
                 PERFORM MOVER-SUMA-TOTAL
              ELSE
                 MOVE GAR-ID-SUP-DDR TO CLAVE-SIG
           ELSE
              PERFORM MOVER-SUMA-TOTAL.
       FIN-PROCESO-PRINC.
           EXIT.

       VEO-SI-LEE-D01 SECTION.
      *-----------------------

           IF PRIM-LECT = 0
              PERFORM LEE-D01.

       FIN-LEE-D01.
           EXIT.


       LEE-D01 SECTION.
      *----------------

           IF D01-RUT-D < GAR-ID-SUP-DDR
             PERFORM LEE-SEC-D01 UNTIL (D01-RUT-D NOT < GAR-ID-SUP-DDR)
                     OR (EOF-D01 = 1)
             MOVE 1 TO PRIM-LECT.

       FIN-LEE-D01.
           EXIT.


       LEE-SEC-D01 SECTION.
      *--------------------

           READ D01 NEXT AT END MOVE 1 TO EOF-D01.

       FIN-LEE-SEC-D01.
           EXIT.


       SUMA-POR-TIPO SECTION.
      *----------------------

           IF GAR-TIP-REA-NRE = 'R'
              COMPUTE SUMA-TOTAL-REA = SUMA-TOTAL-REA + GAR-VAL
           ELSE
              COMPUTE SUMA-TOTAL-NRE = SUMA-TOTAL-NRE + GAR-VAL.

       FIN-SUMAR-POR-TIPO.
           EXIT.


       MOVER-SUMA-TOTAL SECTION.
      *-------------------------

           MOVE CLAVE-ANT TO D03-VALUE.
           MOVE 'REDKY'   TO D03-COMMAND.
           CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03.
           IF D03-RET-COD NOT = SPACES
              DISPLAY 'C-ANT.: ' CLAVE-ANT
              DISPLAY 'C-SIG.: ' CLAVE-SIG
              DISPLAY 'ID-DDR: ' GAR-ID-SUP-DDR
              DISPLAY 'R. COD: ' D03-RET-COD
              DISPLAY '****   E R R O R   G R A V E ....*****'
              DISPLAY '****   A B O R T A           ....*****'
              STOP '****   E R R O R   P G M   A B O R T A'
              GOBACK.
           MOVE 'RDUKY'        TO D03-COMMAND.
           CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03.
           PERFORM MOVER-TOT-REG UNTIL (CLAVE-ANT NOT = GAR-ID-SUP-DDR)
                   OR (D03-RET-COD NOT = SPACES).
           MOVE 0 TO SUMA-TOTAL-REA SUMA-TOTAL-NRE.
           MOVE CLAVE-SIG TO CLAVE-ANT D03-VALUE.
           MOVE 0 TO PRIM-LECT.
           MOVE 'REDKY'   TO D03-COMMAND.
           CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03.
           IF (D03-RET-COD = SPACES)
              ADD 1       TO CONT-D03-LEIDOS.
       FIN-MOVER-SUMA-TOTAL.
           EXIT.

       MOVER-TOT-REG SECTION.
      *----------------------

           MOVE SUMA-TOTAL-REA TO GAR-VAL-GAR-REA.
           MOVE SUMA-TOTAL-NRE TO GAR-VAL-GAR-NRE.
           IF D01-RUT-D = GAR-ID-SUP-DDR
           THEN  MOVE D01-IND-MAYOR-DEUDOR TO GAR-IND-MAY-DDR.
      ******  IF GAR-IND-MAY-DDR = ' '
      ******     MOVE D01-EXCEDIDO TO GAR-IND-MAY-DDR.
           MOVE 'UPDAT'        TO D03-COMMAND.
           CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03.
           IF (D03-RET-COD NOT = SPACES)
              DISPLAY 'D03-RET-COD: ' D03-RET-COD
              DISPLAY 'D03-VALUE : ' D03-VALUE ' C-ANT: ' CLAVE-ANT
              STOP '*** NO ACTUALIZO DATOS , ABORTA ...'
              GOBACK.
           ADD 1               TO CONT-D03-GRABADOS.
           MOVE 'RDUNE'      TO D03-COMMAND.
           CALL 'DBNTRY' USING UIB REQ-AREA-D03 WA-D03 ELEM-LIST-D03.
       FIN-MOVER-TOT-REG.
           EXIT.

