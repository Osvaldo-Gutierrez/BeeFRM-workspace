       IDENTIFICATION DIVISION.
      *=======================
       PROGRAM-ID.      GARPGSE3.
       AUTHOR.          R. CHIANG.
       DATE-WRITTEN     23-MAY-2003.
      
       ENVIRONMENT DIVISION.
      *====================
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
      *        DECIMAL-POINT IS COMMA.
      
       DATA DIVISION.
      *=============
       WORKING-STORAGE SECTION.
      *-----------------------
      
       COPY GARBRGDS.
       COPY GARBRGSS.
       COPY GARBRTAG.
       COPY GARBRGYC.
       COPY GARBRSGV.
       COPY GARBRGCL.
       COPY GARBRDDS.
       COPY SGCBRDBS.
      
       COPY TABBRCAM.
       COPY GNSBRTAB.
       COPY GNSBRMSG.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-CAM-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-MSG-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAB-REQA==.
       COPY GNSWGRQA REPLACING ==ADR-REQA== BY ==ADR-TAG-REQA==.
       COPY GNSWGRQA.
       COPY GNSWGUIB.
       COPY GNSWGELS.
       COPY GNSWVIDD.
       COPY GNSWCFIO.
       COPY GNSWVFIO.
       COPY GNSWGSYS.
      
       01  VARIABLES.
           03 WSS-VAR-TAB       VALUE ZEROES      PIC 9(03).
           03 WSS-EOF-TAB       VALUE ZEROES      PIC 9(01).
           03 J                 VALUE ZEROES      PIC 9(03).
           03 WSS-EOF-GDG       VALUE ZEROES      PIC 9(01).
           03 WSS-EOF-GSE       VALUE ZEROES      PIC 9(01).
           03 WSS-SW1           VALUE ZEROES      PIC 9(01).
           03 WSS-SW2           VALUE ZEROES      PIC 9(01).
           03 WSS-DBS           VALUE ZEROES      PIC 9(01).
           03 WSS-CONSTITUIDAS  VALUE ZEROES      PIC 9(05).
           03 WSS-CONSEGURO     VALUE ZEROES      PIC 9(05).
           03 WSS-CONTADAS      VALUE ZEROES      PIC 9(05).
           03 WSS-SINSEGURO     VALUE ZEROES      PIC 9(05).
           03 WSS-CONTADOR      VALUE ZEROES      PIC 9(03).
           03 WSS-IND-EXI-SEG-TAG VALUE ZEROES    PIC 9(03).
      
           03  TAB-TIPSTI OCCURS 100 TIMES.
               05  TAB-TIP                        PIC 9(03).
               05  TAB-STI                        PIC 9(03).
      
           03  FECHA-PROCESO.
               05  WSS-PRO-AA   VALUE ZEROES      PIC 9(04).
               05  WSS-PRO-MM   VALUE ZEROES      PIC 9(02).
               05  WSS-PRO-DD   VALUE ZEROES      PIC 9(02).
      
      * VECTOR  DE RUT
            03  WDBC-RUT-CLI.
                04 WDBC-IDC-ICLI.
      *        RUT CLIENTE
                   05 WDBC-NUM-ICLI              PIC X(08).
      *        INDICADOR TIPO RUT
                   05 WDBC-IND-ICLI              PIC X(01).
      *        EXTENSION DEL RUT
                   05 WDBC-GLS-ICLI              PIC X(03).
      *        KEY DIGITO VERIFICADOR DEL RUT
                04 WDBC-VRF-ICLI                 PIC X(01).
      
       PROCEDURE DIVISION.
      *==================
      
       MAIN SECTION.
       INI-MAIN.
       COPY GNSBGEDB.
           ENTRY 'DBMSCBL'
           MOVE "GARPGSE3" TO FIO-PROG.
           PERFORM GNS-BUS-IDD.
           PERFORM OPEN-FILE.
           PERFORM INGRESA-PARAMETROS.
           PERFORM LEER-PRIMER-REGISTRO.
           MOVE 1 TO WSS-VAR-TAB.
           MOVE 0 TO WSS-DBS.
           PERFORM CARGA-TABLA.
       LEER-GARAN.
           PERFORM LEER-GARANTIAS.
           IF NOT FIO-STAT-OKS
              GO TO LEER-ARC-DDS.
           PERFORM PROCESO.
           GO TO LEER-GARAN.
       LEER-ARC-DDS.
           PERFORM LEER-DDS.
           IF NOT FIO-STAT-OKS
              GO TO ESTADISTICA-S.
           PERFORM CREA-ARCHIVO.
           GO TO LEER-ARC-DDS.
       ESTADISTICA-S.
           PERFORM ESTADISTICAS.
           PERFORM CLOSE-FILE.
           PERFORM GNS-PRO-END.
       FIN-MAIN.
           EXIT.
      *
      ******************************************************************
      *
       OPEN-FILE SECTION.
       INI-OPEN-FILE.
      *    DISPLAY 'INI-OPEN-FILE'.
           MOVE 'GAR'   TO FIO-SIST.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-GSS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM GAR-FIO-DDS.
           MOVE FIO-INP TO FIO-CMND.
           PERFORM SGC-FIO-DBS.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-GYC.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-SGV.
           MOVE FIO-OUT TO FIO-CMND.
           PERFORM GAR-FIO-GCL.
      *    DISPLAY 'FIN-OPEN-FILE'.
       FIN-OPEN-FILE.
           EXIT.
      *
      ******************************************************************
      *
       INGRESA-PARAMETROS SECTION.
       INI-INGRESA-PARAMETROS.
           DISPLAY 'FECHA DE PROCESO (SS/AA/MM/DD) : '.
           ACCEPT FECHA-PROCESO.
       FIN-INGRESA-PARAMETROS.
           EXIT.
      *
      ******************************************************************
      *
       LEER-PRIMER-REGISTRO SECTION.
       INI-LEER-PRIMER-REGISTRO.
           MOVE SPACES TO GAR-TIP-OPE IN TAG.
           MOVE SPACES TO GAR-STP-OPE IN TAG.
           MOVE FIO-GET-NLS TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
              DISPLAY ' '
              DISPLAY ' PROCESO CANCELADO '
              DISPLAY ' ERROR EN LECTURA DE TABLA '
              PERFORM PRG-ABT.
       FIN-LEER-PRIMER-REGISTRO.
           EXIT.
      *
      ******************************************************************
      *
       CARGA-TABLA SECTION.
       INI-CARGA-TABLA.
           IF GAR-IND-EXG-GSE IN TAG =  'S'
              MOVE GAR-TIP-OPE IN TAG TO TAB-TIP(WSS-VAR-TAB)
              MOVE GAR-STP-OPE IN TAG TO TAB-STI(WSS-VAR-TAB)
              ADD 1 TO WSS-VAR-TAB.
           IF WSS-VAR-TAB > 100
              GO TO FIN-CARGA-TABLA.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
              GO TO FIN-CARGA-TABLA
           ELSE
              GO TO INI-CARGA-TABLA.
       FIN-CARGA-TABLA.
           EXIT.
      *
      ******************************************************************
      *
       VALOR-CIC-EXI SECTION.
       INI-VALOR-CIC-EXI.
           MOVE 'GAR' TO TAB-COD-SIST.
           MOVE 'EXI' TO TAB-CIC-TTAB IN TAB.
           MOVE 'S'   TO TAB-CIC-CTAB IN TAB.
           MOVE 'TAB-CIC-TABL' TO FIO-AKEY.
           PERFORM VAL-TAB.
           IF MSG-COD-MENS > SPACES
               DISPLAY ' '
               DISPLAY ' PROCESO CANCELADO '
               DISPLAY ' ERROR VALIDACION OBTENCION EXIGENCIA '
               DISPLAY ' SEGURO EN GAR-EXI-S(CIC)'
               PERFORM PRG-ABT
           ELSE
               MOVE TAB-COD-CTAB IN TAB TO WSS-IND-EXI-SEG-TAG
               DISPLAY ' '.
               DISPLAY ' INDICADOR DE EXIGENCIA DE SEGURO EN TAG  : '
               DISPLAY ' WSS-IND-EXI-SEG-TAG.'.
       FIN-VALOR-CIC-EXI.
            EXIT.
      *
      ******************************************************************
      *
       PROCESO SECTION.
       INI-PROCESO.
           IF GAR-ES1 IN GDS  = '10' AND GAR-IND-CPL = 'S'
              ADD 1 TO WSS-CONSTITUIDAS
              MOVE 1 TO WSS-CONTADOR
              PERFORM BUSCA-TIPSTP
           ELSE
              GO TO FIN-PROCESO.
      
           IF WSS-CONTADOR > 100
              GO TO FIN-PROCESO.
       MED-PROCESO.
           IF WSS-SW1 = 0
              PERFORM LEER-SEGUROS
              IF NOT FIO-STAT-OKS
                 GO TO FIN-PROCESO.
      
           IF GAR-CLV-GDG-1 IN GDS > GAR-CLV-GSE-1 IN GSS
              MOVE 0 TO WSS-SW1
              GO TO MED-PROCESO
           ELSE
           IF GAR-CLV-GDG-1 IN GDS = GAR-CLV-GSE-1 IN GSS
              ADD 1 TO WSS-CONSEGURO
              ADD 1 TO WSS-CONTADAS
              PERFORM SEGURO-GARAN-VENCIDO
              MOVE 0 TO WSS-SW1
           ELSE
           IF GAR-CLV-GDG-1 IN GDS < GAR-CLV-GSE-1 IN GSS
              ADD 1 TO WSS-CONTADAS
              ADD 1 TO WSS-SINSEGURO
              PERFORM CREAR-REGISTROS
              MOVE 1 TO WSS-SW1.
       FIN-PROCESO.
           EXIT.
      *
      ******************************************************************
      *
       BUSCA-TIPSTP SECTION.
       INI-BUSCA-TIPSTP.
           IF WSS-CONTADOR > 100
              GO TO FIN-BUSCA-TIPSTP.
           IF TAB-TIP(WSS-CONTADOR) NOT > SPACES AND
              TAB-STI(WSS-CONTADOR) NOT > SPACES
              MOVE 101 TO WSS-CONTADOR
              GO TO FIN-BUSCA-TIPSTP.
           IF TAB-TIP( WSS-CONTADOR ) = GAR-TIP-OPE IN GDS AND
              TAB-STI( WSS-CONTADOR ) = GAR-STP-OPE IN GDS
              NEXT SENTENCE
           ELSE
              ADD 1 TO WSS-CONTADOR
              GO TO INI-BUSCA-TIPSTP.
       FIN-BUSCA-TIPSTP.
           EXIT.
      *
      *****************************************************************
      *
       LEER-TABLA-ATRIBUTOS SECTION.
       INI-LEER-TABLA-ATRIBUTOS.
           MOVE FIO-GET-NXT TO FIO-CMND
           PERFORM GAR-FIO-TAG.
           IF NOT FIO-STAT-OKS
              MOVE 101 TO WSS-VAR-TAB.
       FIN-LEER-TABLA-ATRIBUTOS.
           EXIT.
      *
      ******************************************************************
      *
       LEER-GARANTIAS SECTION.
       INI-LEER-GARANTIAS.
           MOVE FIO-GET-NXT TO FIO-CMND
           PERFORM GAR-FIO-GDS.
       FIN-LEER-GARANTIAS.
           EXIT.
      *
      ******************************************************************
      *
       LEER-SEGUROS SECTION.
       INI-LEER-SEGUROS.
           MOVE FIO-GET-NXT TO FIO-CMND
           PERFORM GAR-FIO-GSS.
       FIN-LEER-SEGUROS.
           EXIT.
      *
      ******************************************************************
      *
       SEGURO-GARAN-VENCIDO SECTION.
       INI-SEGURO-GARAN-VENCIDO.
           IF FECHA-PROCESO NOT < GAR-FEC-VTO-SEG
              MOVE GAR-NUM-SIS    IN GDS  TO SGV-NUM-SIS
              MOVE GAR-COD-CRR    IN GDS  TO SGV-COD-CRR
              MOVE GAR-TIP-OPE    IN GDS  TO SGV-TIP-OPE
              MOVE GAR-STP-OPE    IN GDS  TO SGV-STP-OPE
              MOVE GAR-COD-PLZ    IN GSS  TO SGV-COD-PLZ
              MOVE GAR-FA-VTO-SEG IN GSS  TO SGV-FA-VTO-SEG
              MOVE GAR-FM-VTO-SEG IN GSS  TO SGV-FM-VTO-SEG
              MOVE GAR-FD-VTO-SEG IN GSS  TO SGV-FD-VTO-SEG
              MOVE GAR-VCB-SEG    IN GSS  TO SGV-MON-SEG
              MOVE GAR-VAL-SEG    IN GSS  TO SGV-VAL-SEG
              MOVE FIO-PUT                TO FIO-CMND
              PERFORM GAR-FIO-SGV.
       FIN-CREAR-REGISTROS.
           EXIT.
      *
      ******************************************************************
      *
       CREAR-REGISTROS SECTION.
       INI-CREAR-REGISTROS.
           MOVE GDG-GLS-FLAG   IN GDS  TO GYC-GLS-FLAG.
           MOVE GAR-NUM-SIS    IN GDS  TO GYC-NUM-SIS.
           MOVE GAR-COD-CRR    IN GDS  TO GYC-COD-CRR.
           MOVE GAR-TIP-OPE    IN GDS  TO GYC-TIP-OPE.
           MOVE GAR-STP-OPE    IN GDS  TO GYC-STP-OPE.
           MOVE GAR-FA-ESC-CNS IN GDS  TO GYC-FA-ESC-CNS.
           MOVE GAR-FM-ESC-CNS IN GDS  TO GYC-FM-ESC-CNS.
           MOVE GAR-FD-ESC-CNS IN GDS  TO GYC-FD-ESC-CNS.
           MOVE FIO-PUT                TO FIO-CMND.
           PERFORM GAR-FIO-GYC.
       FIN-CREAR-REGISTROS.
           EXIT.
      *
      ******************************************************************
      *
       CREA-ARCHIVO SECTION.
       INI-CREA-ARCHIVO.
           IF WSS-DBS = 0
              PERFORM LEER-DBS
              MOVE 1 TO WSS-DBS.
           IF NOT FIO-STAT-OKS
              GO TO FIN-CREA-ARCHIVO.
           IF GDD-CIC-ICLI IN DDS = DBC-CIC-ICLI IN DBS
              PERFORM GRABAR-ARCHIVO
           ELSE
           IF GDD-CIC-ICLI IN DDS < DBC-CIC-ICLI IN DBS
              PERFORM GRABAR-ARCHIVO-ESPECIAL
              DISPLAY 'GAR DEUDOR SIN RUT (DBC):' GAR-ID IN DDS
           ELSE
              PERFORM LEER-DBS
              GO TO INI-CREA-ARCHIVO.
       FIN-CREA-ARCHIVO.
           EXIT.
      *
      *****************************************************************
      *
       LEER-DDS SECTION.
       INI-LEER-DDS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM GAR-FIO-DDS.
       FIN-LEER-DDS.
           EXIT.
      *
      *****************************************************************
      *
       LEER-DBS SECTION.
       INI-LEER-DBS.
           MOVE FIO-GET-NXT TO FIO-CMND.
           PERFORM SGC-FIO-DBS.
           PERFORM TRASPASA-RUT.
       FIN-LEER-DBS.
           EXIT.
      *
      *****************************************************************
      *
       TRASPASA-RUT SECTION.
       INI-TRASPASA-RUT.
           MOVE DBC-NUM-ICLI TO WDBC-NUM-ICLI.
           MOVE DBC-IND-ICLI TO WDBC-IND-ICLI.
           MOVE DBC-GLS-ICLI TO WDBC-GLS-ICLI.
           MOVE DBC-VRF-ICLI TO WDBC-VRF-ICLI.
       FIN-TRASPASA-RUT.
           EXIT.
      *
      ******************************************************************
      *
       GRABAR-ARCHIVO SECTION.
       INI-GRABAR-ARCHIVO.
           MOVE GAR-NUM-SIS    IN DDS  TO GCL-NUM-SIS.
           MOVE GAR-COD-CRR    IN DDS  TO GCL-COD-CRR.
           MOVE WDBC-NUM-ICLI          TO GCL-NUM-ICLI.
           MOVE WDBC-IND-ICLI          TO GCL-IND-ICLI.
           MOVE WDBC-GLS-ICLI          TO GCL-GLS-ICLI.
           MOVE WDBC-VRF-ICLI          TO GCL-VRF-ICLI.
           MOVE FIO-PUT       TO FIO-CMND.
           PERFORM GAR-FIO-GCL.
       FIN-GRABAR-ARCHIVO.
           EXIT.
      *
      ******************************************************************
      *
       GRABAR-ARCHIVO-ESPECIAL SECTION.
       INI-GRABA-ARCHIVO-ESPECIAL.
           MOVE GAR-NUM-SIS   IN DDS  TO GCL-NUM-SIS.
           MOVE GAR-COD-CRR   IN DDS  TO GCL-COD-CRR.
           MOVE SPACES                TO GCL-NUM-ICLI.
           MOVE SPACES                TO GCL-IND-ICLI.
           MOVE SPACES                TO GCL-GLS-ICLI.
           MOVE SPACES                TO GCL-VRF-ICLI.
           MOVE FIO-PUT               TO FIO-CMND.
           PERFORM GAR-FIO-GCL.
       FIN-GRABAR-ARCHIVO-ESPECIAL.
           EXIT.
      *
      ******************************************************************
      *
       ESTADISTICAS SECTION.
       INI-ESTADISTICAS.
           DISPLAY 'NUMERO DE GARANTIAS CONSTITUIDAS ANALIZADAS : '
                    WSS-CONSTITUIDAS.
           DISPLAY 'NUMERO DE GARANTIAS CONSTITUIDAS CON SEGURO OBLIGATO
      -            'RIO : ' WSS-CONTADAS.
           DISPLAY 'NUMERO DE GARANTIAS CONSTITUIDAS CON SEGURO OBLIGATO
      -            'RIO Y PRESENTA REGISTRO GSE : ' WSS-CONSEGURO.
           DISPLAY 'NUMERO DE GARANTIAS CONSTITUIDAS CON SEGURO OBLIGATO
      -            'RIO Y NO PRESENTA REGISTRO GSE : ' WSS-SINSEGURO.
       FIN-ESTADISTICAS.
           EXIT.
      *
      ******************************************************************
      *
       CLOSE-FILE SECTION.
       INI-CLOSE-FILE.
           MOVE FIO-CLO TO FIO-CMND.
           PERFORM GNS-FIO-TAB.
           MOVE FIO-CLO TO FIO-CMND
           PERFORM GAR-FIO-GDS.
           MOVE FIO-CLO TO FIO-CMND
           PERFORM GAR-FIO-GSS.
           MOVE FIO-CLO TO FIO-CMND
           PERFORM GAR-FIO-DDS.
           MOVE FIO-CLO TO FIO-CMND
           PERFORM SGC-FIO-DBS.
           MOVE FIO-CLO TO FIO-CMND
           PERFORM GAR-FIO-GYC.
           MOVE FIO-CLO TO FIO-CMND
           PERFORM GAR-FIO-SGV.
           MOVE FIO-CLO TO FIO-CMND
           PERFORM GAR-FIO-GCL.
      
       FIN-CLOSE-FILE.
           EXIT.
      *
      ******************************************************************
      *
       COPY GARBFGDS.
       COPY GARBFGSS.
       COPY GARBFTAG.
       COPY GARBFDDS.
       COPY SGCBFDBS.
       COPY GARBFGYC.
       COPY GARBFGCL.
       COPY GARBFSGV.
      
       COPY GNSBGDTC.
       COPY GNSBGIDD.
       COPY GNSBGEND.
       COPY GNSBGABT.
       COPY GNSBVTAB.
      
       COPY GNSBFTAB.
       COPY TABBFCAM.
       COPY GNSBFMSG.
       COPY GNSBBMSG.
       COPY GNSBBTAB.
       COPY GNSBBSYS.
      *
      ******************************************************************
      *
       PRG-ABT SECTION.
       INI-PRG-ABT.
            DISPLAY 'ABORTANDO EN PRG-ABT '.
            DISPLAY 'FIO-MENS : ' FIO-MENS.
            MOVE 100 TO RETURN-CODE.
            GOBACK.
       FIN-PRG-ABT.
            EXIT.
      
